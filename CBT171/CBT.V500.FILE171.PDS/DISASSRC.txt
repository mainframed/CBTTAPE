./ ADD NAME=DISASMB2
         TITLE 'DISASMB2 B2 OPCODE INTERPRETER'
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: DISASMB2 - B2 opcode interpreter                      *
*                                                                     *
*  Function:                                                          *
*   B2 opcodes are actually 2-byte opcodes.  All opcodes including    *
*   B2 are defined in DISASMOP.  The first 256 bytes of DISASMOP is   *
*   an index to the actual opcode definition.  To keep this index as  *
*   simple as possible, this module is called to modify the B2 entry  *
*   depending on the 2nd byte of the opcode.                          *
*                                                                     *
*   At entry                                                          *
*                                                                     *
*     R0  address of the instruction                                  *
*     R1  address of the B2 entry in DISASMOP                         *
*                                                                     *
*   The B2 entry of DISASMOP will be modified:                        *
*     .  The mnemonic will be set                                     *
*     .  The instruction format will be set                           *
*     .  "flags" (whether the instruction causes a data reference)    *
*        will be set.                                                 *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DISASMGB
DISASMB2 CSECT
DISASMB2 AMODE 31
         RMODE 24
         USING DISASMB2,R12
         USING DISASM00,R11
         USING OPDSECT,R1            DEFINE OPCODE DSECT BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     B20000                SKIP EYECATCHER
         DC    CL8'DISASMB2'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
B20000   DS    0H
         LR    R2,R0                 COPY INSTRUCTION ADDRESS
         LA    R3,INSTTBLE           B2 INSTRUCTION TABLE ADDRESS
         USING B2DSECT,R3            DEFINE BASE
B20010   DS    0H
         CLC   B2B2,1(R2)            ENTRY FOUND?
         BE    B20020                YES
         LA    R3,B2DSECTL(R3)       NEXT ENTRY
         CLI   0(R3),X'FF'           END OF TABLE?
         BNE   B20010                NO
         ABEND ABEND006,DUMP,,USER
B20020   DS    0H
         MVC   OPMNEM,B2MNEM         COPY MNEMONIC
         MVC   OPFORM,B2FORM         COPY FORMAT
         MVC   OPFLAGS,B2FLAGS       COPY FLAGS
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
* ------------------------------------------------------------------ *
*                                                                    *
* ------------------------------------------------------------------ *
         SPACE 1
INSTTBLE DS    0C
         DC    X'02',CL6'STIDP',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'04',CL6'SCK  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'05',CL6'STCK ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'06',CL6'SCKC ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'07',CL6'STCKC',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'08',CL6'SPT  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'09',CL6'STPT ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'0A',CL6'SPKA ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'0B',CL6'IPK  ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'0D',CL6'PTLB ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'10',CL6'SPX  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'11',CL6'STPX ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'12',CL6'STAP ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'18',CL6'PC   ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'19',CL6'SAC  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'1A',CL6'CFC  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'21',CL6'IPTE ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'22',CL6'IPM  ',AL1($OPB2R),AL1($OPNCMNT)
         DC    X'23',CL6'IVSK ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'24',CL6'IAC  ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'25',CL6'SSAR ',AL1($OPB2R),AL1($OPNCMNT)
         DC    X'26',CL6'EPAR ',AL1($OPB2R),AL1($OPNCMNT)
         DC    X'27',CL6'ESAR ',AL1($OPB2R),AL1($OPNCMNT)
         DC    X'28',CL6'PT   ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'29',CL6'ISKE ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'2A',CL6'RRBE ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'2B',CL6'SSKE ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'2C',CL6'TB   ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'2D',CL6'DXR  ',AL1($OPB2RR),AL1($OPNCMNT)
         DC    X'30',CL6'CSCH ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'31',CL6'HSCH ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'32',CL6'MSCH ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'33',CL6'SSCH ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'34',CL6'STSCH',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'35',CL6'TSCH ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'36',CL6'TPI  ',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'37',CL6'SAL  ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'38',CL6'RSCH ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'39',CL6'STCRW',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'3A',CL6'STCPS',AL1($OPB2S),AL1($OPREF+$OPNCMNT)
         DC    X'3B',CL6'RCHP ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'3C',CL6'SCHM ',AL1($OPB2),AL1($OPNCMNT)
         DC    X'FF'
         LTORG
* ------------------------------------------------------------------ *
*                                                                    *
*                                                                    *
*                                                                    *
* ------------------------------------------------------------------ *
B2DSECT  DSECT
B2B2     DS     X              2ND BYTE OF INSTRUCTION
B2MNEM   DS     CL6            MNEMONIC
B2FORM   DS     X              INSTRUCTION FORMAT
B2FLAGS  DS     X              FLAGS
B2DSECTL EQU    *-B2DSECT
* ------------------------------------------------------------------ *
*                                                                    *
*              DATA AREAS                                            *
*                                                                    *
* ------------------------------------------------------------------ *
         COPY  DISASMDA
* ------------------------------------------------------------------ *
*                                                                    *
*              COMMON DATA MAP                                       *
*                                                                    *
* ------------------------------------------------------------------ *
DISASM00 DISASM00 TYPE=DSECT
* ------------------------------------------------------------------ *
*                                                                    *
*              EQUATES                                               *
*                                                                    *
* ------------------------------------------------------------------ *
         COPY REGEQU
         END  DISASMB2
./ ADD NAME=DISASMDB
         TITLE 'DISASMDB - DEBUG MODULE'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASMDB                                             *
*                                                                    *
*  Function:                                                         *
*   If DISDEBUG DD is allocated, many internal fields and data       *
*   chains will be printed for trouble shooting.                     *
*                                                                    *
*   There are two "functions".  Normal function is to print the      *
*   internal fields and run the internal data chains.  The secondary *
*   function is to print the assembler output for module DISASM07.   *
*                                                                    *
*   NOTE:  Several fields are copied to a work field before          *
*          "unpacking" them for printing.  When "unpacking" the      *
*          field, 1 more byte than printed is unpacked.  When the    *
*          field is the last field in the data area, an S0C4 abend   *
*          can occur.  Copying the field before unpacking was to     *
*          prevent these S0C4 abends.  If any data area is modified  *
*          and some other field is added to the end of the area,     *
*          beware that if it is unpacked for printing it may need    *
*          to be copied before unpacking.                            *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASMDB CSECT
DISASMDB AMODE 31
DISASMDB RMODE 24
         USING DISASMDB,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     DBUG0000              SKIP EYECATCHER
         DC    CL8'DISASMDB'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
DBUG0000 DS    0H
         LR    R9,R1                 COPY PARAMETER BLOCK ADDRESS
         USING DBUGBLOK,R9           DEFINE BASE
         LA    R1,SAVEDB             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         ITRACE ID=ENTRY,                                              +
               DATA1=DBUGCMD
         TM    COMMDD,$DEBUGDD       DEBUG DD PRESENT?
         BNO   DBUG1000              NO, TRACE ENTRY BEFORE EXIT
         TM    DBUGFLAG,$DBUGOPN     DEBUG ALREADY OPEN?
         BO    DBUG0010              YES
         OI    DBUGFLAG,$DBUGOPN     INDICATE DCB IS OPEN
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (DISDEBUG,OUTPUT)     OPEN DISDEBUG
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
DBUG0010 DS    0H
         CLI   DBUGCMD,$DBUGHD       HEADING?
         BE    DBUG0300              YES
         CLI   DBUGCMD,$DBUGPRT      PRINT?
         BE    DBUG0310              YES
         BAL   R10,HEAD0000          PRINT DEBUG HEADING
DBUG0020 DS    0H
* ------------------------------------------------------------------- *
*             PRINT TRACE TABLE CONTROL                               *
* ------------------------------------------------------------------- *
         UNPK  PRTR1ST(9),TR1ST(5)   UNPACK TRACE 1ST
         MVZ   PRTR1ST,COMM0F0F      TURN OFF ZONES
         TR    PRTR1ST,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRTR1ST+8,C' '        RESTORE BLANK
         UNPK  PRTRLAST(9),TRLAST(5) UNPACK TRACE LAST
         MVZ   PRTRLAST,COMM0F0F     TURN OFF ZONES
         TR    PRTRLAST,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRTRLAST+8,C' '       RESTORE BLANK
         UNPK  PRTRCURR(9),TRCURR(5) UNPACK TRACE CURRENT
         MVZ   PRTRCURR,COMM0F0F     TURN OFF ZONES
         TR    PRTRCURR,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRTRCURR+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRTRL),PRTR   TRACE DATA
         BAL   R10,PRT0000           PRINT TRACE DATA
* ------------------------------------------------------------------- *
*             PRINT DISMOD I/O AREA ADDRESS                           *
* ------------------------------------------------------------------- *
         UNPK  PRIOA(9),COMMIO(5)    UNPACK I/O BUFFER ADDRESS
         MVZ   PRIOA,COMM0F0F        TURN OFF ZONES
         TR    PRIOA,COMMHXCH        TRANSLATE TO PRINTABLE
         MVI   PRIOA+8,C' '          RESTORE BLANK
         MVC   PRTDATA(PRIOL),PRIO   TRACE DATA
         BAL   R10,PRT0000           PRINT TRACE DATA
* ------------------------------------------------------------------- *
*             CHASE ESD CHAIN                                         *
* ------------------------------------------------------------------- *
DBUG0030 DS    0H
         ITRACE ID=ESDCHAIN
         ICM   R3,15,COMMESD         FIRST ESD ENTRY
         USING ESDDATA,R3            DEFINE BASE
         BZ    DBUG0050              NO ESD ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0040 DS    0H
         ITRACE ID=ESDENTRY
         ST    R3,COMMDWRD           USE DWRD FOR WORK AREA
         UNPK  PRESDBA(9),COMMDWRD(5)
         MVZ   PRESDBA,COMM0F0F      TURN OFF ZONES
         TR    PRESDBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRESDBA+8,C' '        RESTORE BLANK
         UNPK  PRESDNXT(9),ESDNEXT(5)  NEXT BLOCK'S ADDRESS
         MVZ   PRESDNXT,COMM0F0F     TURN OFF ZONES
         TR    PRESDNXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRESDNXT+8,C' '       RESTORE BLANK
         MVC   PRESDNM,ESDNAME       COPY ESD NAME
         UNPK  PRESDTYP(3),ESDTYPE(2)
         MVZ   PRESDTYP,COMM0F0F     TURN OFF ZONES
         TR    PRESDTYP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRESDTYP+2,C' '       RESTORE BLANK
         UNPK  PRESDADR(7),ESDADDR(4)
         MVZ   PRESDADR,COMM0F0F     TURN OFF ZONES
         TR    PRESDADR,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRESDADR+6,C' '       RESTORE BLANK
         UNPK  PRESDSEG(3),ESDSEG(2) UNPACK SEGMENT NUMBER
         MVZ   PRESDSEG,COMM0F0F     TURN OFF ZONES
         TR    PRESDSEG,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRESDSEG+2,C' '       RESTORE BLANK
         MVC   WORKX,ESDLEN          COPY TO WORK AREA
         UNPK  PRESDLEN(7),WORKX(4)  UNPACK LENGTH
         MVZ   PRESDLEN,COMM0F0F     TURN OFF ZONES
         TR    PRESDLEN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRESDLEN+6,C' '       RESTORE BLANK
         MVC   PRTDATA(PRESDL),PRESD COPY ESD DATA TO PRINT AREA
         BAL   R10,PRT0000           PRINT ESD DATA
         ICM   R3,15,ESDNEXT         NEXT ESD BLOCK
         BNZ   DBUG0040              LOOP
* ------------------------------------------------------------------- *
*             CHASE RLD CHAIN                                         *
* ------------------------------------------------------------------- *
DBUG0050 DS    0H
         ITRACE ID=RLDCHAIN
         ICM   R3,15,COMMRLD         FIRST RLD ENTRY
         USING RLDDATA,R3            DEFINE BASE
         BZ    DBUG0070              NO RLD ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0060 DS    0H
         ITRACE ID=RLDENTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRRLDBA(9),COMMDWRD(5)
         MVZ   PRRLDBA,COMM0F0F      TURN OFF ZONES
         TR    PRRLDBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRRLDBA+8,C' '        RESTORE BLANK
         UNPK  PRRLDNXT(9),RLDNEXT(5)
         MVZ   PRRLDNXT,COMM0F0F     TURN OFF ZONES
         TR    PRRLDNXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDNXT+8,C' '       RESTORE BLANK
         UNPK  PRRLDDSP(9),RLDDISP(5)
         MVZ   PRRLDDSP,COMM0F0F     TURN OFF ZONES
         TR    PRRLDDSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDDSP+8,C' '       RESTORE BLANK
         UNPK  PRRLDTYP(3),RLDTYPE(2)
         MVZ   PRRLDTYP,COMM0F0F     TURN OFF ZONES
         TR    PRRLDTYP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDTYP+2,C' '       RESTORE BLANK
         UNPK  PRRLDLEN(5),RLDLEN(3) UNPACK LENGTH
         MVZ   PRRLDLEN,COMM0F0F     TURN OFF ZONES
         TR    PRRLDLEN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDLEN+4,C' '       RESTORE BLANK
         UNPK  PRRLDPTR(5),RLDPTR(3) UNPACK POINTER
         MVZ   PRRLDPTR,COMM0F0F     TURN OFF ZONES
         TR    PRRLDPTR,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDPTR+4,C' '       RESTORE BLANK
         UNPK  PRRLDPP(5),RLDPP(3)   UNPACK POSITION POINTER
         MVZ   PRRLDPP,COMM0F0F      TURN OFF ZONES
         TR    PRRLDPP,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRRLDPP+4,C' '        RESTORE BLANK
         MVC   PRRLDDIR,RLDDIR       MOVE DIRECTION
         UNPK  PRRLDESD(9),RLDESD(5) UNPACK ESD BLOCK ADDRESS
         MVZ   PRRLDESD,COMM0F0F     TURN OFF ZONES
         TR    PRRLDESD,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRRLDESD+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRRLDL),PRRLD MOVE RLD DATA
         BAL   R10,PRT0000           PRINT RLD DATA
         ICM   R3,15,RLDNEXT         NEXT RLD BLOCK
         BNZ   DBUG0060              LOOP
* ------------------------------------------------------------------- *
*             CHASE USING CHAIN                                       *
* ------------------------------------------------------------------- *
DBUG0070 DS    0H
         ITRACE ID=USNGCHN
         ICM   R3,15,COMMUSNG        FIRST USING ENTRY
         USING USNGDSCT,R3           DEFINE BASE
         BZ    DBUG0090              NO USING ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0080 DS    0H
         ITRACE ID=USNGNTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRUSGBA(9),COMMDWRD(5)
         MVZ   PRUSGBA,COMM0F0F      TURN OFF ZONES
         TR    PRUSGBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRUSGBA+8,C' '        RESTORE BLANK
         UNPK  PRUSGNXT(9),USNGNEXT(5)
         MVZ   PRUSGNXT,COMM0F0F     TURN OFF ZONES
         TR    PRUSGNXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRUSGNXT+8,C' '       RESTORE BLANK
         MVC   PRUSGNME,USNGDSNM     DSECT'S NAME
         MVC   PRUSGLBL,USNGLBNM     LABEL WITHIN DSECT
         MVC   PRUSGBSE,USNGBASE     COPY BASE REGISTER
         TR    PRUSGBSE,COMMHXCH     TRANSLATE TO PRINTABLE
         UNPK  PRUSGFLG(3),USNGFLAG(2)
         MVZ   PRUSGFLG,COMM0F0F     TURN OFF ZONES
         TR    PRUSGFLG,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRUSGFLG+2,C' '       RESTORE BLANK
         UNPK  PRUSGBGN(9),USNGBEGN(5)
         MVZ   PRUSGBGN,COMM0F0F     TURN OFF ZONES
         TR    PRUSGBGN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRUSGBGN+8,C'-'       RESTORE HYPHEN
         UNPK  PRUSGDSP(9),USNGDISP(5)
         MVZ   PRUSGDSP,COMM0F0F     TURN OFF ZONES
         TR    PRUSGDSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRUSGDSP+8,C'-'       RESTORE HYPHEN
         MVC   WORKX,USNGEND         COPY TO WORK FIELD
         UNPK  PRUSGEND(9),WORKX(5)  UNPACK
         MVZ   PRUSGEND,COMM0F0F     TURN OFF ZONES
         TR    PRUSGEND,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRUSGEND+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRUSNGL),PRUSNG
         BAL   R10,PRT0000           PRINT USING DATA
         ICM   R3,15,USNGNEXT        NEXT USING BLOCK
         BNZ   DBUG0080              LOOP
* ------------------------------------------------------------------- *
*             CHASE DSECT CHAIN                                       *
* ------------------------------------------------------------------- *
DBUG0090 DS    0H
         ITRACE ID=DSCTCHN
         ICM   R3,15,COMMDSCT        FIRST DSECT ENTRY
         USING DSCTDSCT,R3           DEFINE BASE
         BZ    DBUG0120              NO DSECT ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0100 DS    0H
         ITRACE ID=DSCTNTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRDSBA(9),DSCTNEXT(5) UNPACK BLOCK ADDRESS
         MVZ   PRDSBA,COMM0F0F       TURN OFF ZONES
         TR    PRDSBA,COMMHXCH       TRANSLATE TO PRINTABLE
         MVI   PRDSBA+8,C' '         RESTORE BLANK
         UNPK  PRDSNXT(9),DSCTNEXT(5)
         MVZ   PRDSNXT,COMM0F0F      TURN OFF ZONES
         TR    PRDSNXT,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRDSNXT+8,C' '        RESTORE BLANK
         MVC   PRDSNAME,DSCTNAME     COPY DSECT'S NAME
         MVC   WORKX,DSCTLBA         COPY ADDRESS TO WORK AREA
         UNPK  PRDSLABL(9),WORKX(5)  UNPACK LABEL BLOCK ADDRESS
         MVZ   PRDSLABL,COMM0F0F     TURN OFF ZONES
         TR    PRDSLABL,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDSLABL+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRDSCTL),PRDSCT
         BAL   R10,PRT0000           PRINT DSECT DATA
         ICM   R4,15,DSCTLBA         FIRST LABEL FROM DSECT
         BZ    DBUG0110              NO LABELS
         BAL   R8,DBUG0190           FOLLOW LABEL CHAIN
DBUG0110 DS    0H
         ICM   R3,15,DSCTNEXT        NEXT DSECT
         BNZ   DBUG0100              LOOP
* ------------------------------------------------------------------- *
*             CHASE BASE CHAIN                                        *
* ------------------------------------------------------------------- *
DBUG0120 DS    0H
         ITRACE ID=BASECHN
         ICM   R3,15,COMMBASE        FIRST BASE ENTRY
         USING BASEDSCT,R3           DEFINE BASE
         BZ    DBUG0140              NO DSECT ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0130 DS    0H
         ITRACE ID=BASENTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRBSEBA(9),COMMDWRD(5)
         MVZ   PRBSEBA,COMM0F0F      TURN OFF ZONES
         TR    PRBSEBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRBSEBA+8,C' '        RESTORE BLANK
         UNPK  PRBSENXT(9),BASENEXT(5)
         MVZ   PRBSENXT,COMM0F0F     TURN OFF ZONES
         TR    PRBSENXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRBSENXT+8,C' '       RESTORE BLANK
         UNPK  PRBSEBGN(9),BASEBEGN(5)
         MVZ   PRBSEBGN,COMM0F0F     TURN OFF ZONES
         TR    PRBSEBGN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRBSEBGN+8,C'-'       RESTORE BLANK
         UNPK  PRBSEEND(9),BASEEND(5)
         MVZ   PRBSEEND,COMM0F0F     TURN OFF ZONES
         TR    PRBSEEND,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRBSEEND+8,C' '       RESTORE BLANK
         UNPK  PRBSEDSP(9),BASEDISP(5)
         MVZ   PRBSEDSP,COMM0F0F     TURN OFF ZONES
         TR    PRBSEDSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRBSEDSP+8,C' '       RESTORE BLANK
         MVC   PRBSEREG,BASEREG      COPY BASE REGISTER
         TR    PRBSEREG,COMMHXCH     TRANSLATE TO PRINTABLE
         MVC   PRTDATA(PRBASEL),PRBASE
         BAL   R10,PRT0000           PRINT BASE DATA
         ICM   R3,15,BASENEXT        NEXT BASE BLOCK
         BNZ   DBUG0130              LOOP
* ------------------------------------------------------------------- *
*             CHASE DATA BLOCK CHAIN                                  *
* ------------------------------------------------------------------- *
DBUG0140 DS    0H
         ITRACE ID=DATACHN
         ICM   R3,15,COMMDATA        FIRST DATA ENTRY
         USING DATADSCT,R3           DEFINE BASE
         BZ    DBUG0160              NO DSECT ENTRIES
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0150 DS    0H
         ITRACE ID=DATANTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRDTABA(9),COMMDWRD(5)
         MVZ   PRDTABA,COMM0F0F      TURN OFF ZONES
         TR    PRDTABA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRDTABA+8,C' '        RESTORE BLANK
         UNPK  PRDTANXT(9),DATANEXT(5)
         MVZ   PRDTANXT,COMM0F0F     TURN OFF ZONES
         TR    PRDTANXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTANXT+8,C' '       RESTORE BLANK
         UNPK  PRDTABGN(9),DATABEGN(5)
         MVZ   PRDTABGN,COMM0F0F     TURN OFF ZONES
         TR    PRDTABGN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTABGN+8,C'-'       RESTORE HYPHEN
         UNPK  PRDTAEND(9),DATAEND(5)
         MVZ   PRDTAEND,COMM0F0F     TURN OFF ZONES
         TR    PRDTAEND,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTAEND+8,C' '       RESTORE BLANK
         UNPK  PRDTALEN(9),DATALEN(5)
         MVZ   PRDTALEN,COMM0F0F     TURN OFF ZONES
         TR    PRDTALEN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTALEN+8,C' '       RESTORE BLANK
         UNPK  PRDTATYP(3),DATATYPE(2)
         MVZ   PRDTATYP,COMM0F0F     TURN OFF ZONES
         TR    PRDTATYP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTATYP+2,C' '       RESTORE BLANK
         MVC   PRDTANME,DATANAME     COPY DATA NAME
         UNPK  PRDTALBA(9),DATALBA(5)
         MVZ   PRDTALBA,COMM0F0F     TURN OFF ZONES
         TR    PRDTALBA,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTALBA+8,C'+'       RESTORE PLUS SIGN
         UNPK  PRDTALBD(9),DATALBD(5)
         MVZ   PRDTALBD,COMM0F0F     TURN OFF ZONES
         TR    PRDTALBD,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRDTALBD+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRDATAL),PRDATA
         BAL   R10,PRT0000           PRINT DATA DATA
         ICM   R3,15,DATANEXT        NEXT DATA BLOCK
         BNZ   DBUG0150              LOOP
* ------------------------------------------------------------------- *
*             CHASE REFERENCE BLOCK CHAIN                             *
* ------------------------------------------------------------------- *
DBUG0160 DS    0H
         ITRACE ID=REFCHAIN
         ICM   R3,15,COMMREF         FIRST REFERENCE BLOCK
         USING REFDSCT,R3            DEFINE BASE
         BZ    DBUG0180              NO REF BLOCK'S
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0170 DS    0H
         ITRACE ID=REFENTRY
         ST    R3,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRREFBA(9),REFNEXT(5)
         MVZ   PRREFBA,COMM0F0F      TURN OFF ZONES
         TR    PRREFBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRREFBA+8,C' '        RESTORE BLANK
         UNPK  PRREFNXT(9),REFNEXT(5)
         MVZ   PRREFNXT,COMM0F0F     TURN OFF ZONES
         TR    PRREFNXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFNXT+8,C' '       RESTORE BLANK
         UNPK  PRREFAD1(9),REFOPER1(5)
         MVZ   PRREFAD1,COMM0F0F     TURN OFF ZONES
         TR    PRREFAD1,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFAD1+8,C'-'       RESTORE HYPHEN
         UNPK  PRREFO1D(9),REFDISP1(5)
         MVZ   PRREFO1D,COMM0F0F     TURN OFF ZONES
         TR    PRREFO1D,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFO1D+8,C' '       RESTORE BLANK
         UNPK  PRREFAD2(9),REFOPER2(5)
         MVZ   PRREFAD2,COMM0F0F     TURN OFF ZONES
         TR    PRREFAD2,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFAD2+8,C'-'       RESTORE HYPHEN
         UNPK  PRREFO2D(9),REFDISP2(5)
         MVZ   PRREFO2D,COMM0F0F     TURN OFF ZONES
         TR    PRREFO2D,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFO2D+8,C' '       RESTORE BLANK
         MVC   WORKX,REFDISPI        COPY TO WORK AREA
         UNPK  PRREFDSP(9),WORKX(5)  UNPACK
         MVZ   PRREFDSP,COMM0F0F     TURN OFF ZONES
         TR    PRREFDSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRREFDSP+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRREFL),PRREF
         BAL   R10,PRT0000           PRINT REF DATA
         ICM   R3,15,REFNEXT         NEXT REF BLOCK
         BNZ   DBUG0170              LOOP
* ------------------------------------------------------------------- *
*             CHASE LABEL CHAIN FOR CSECT                             *
* ------------------------------------------------------------------- *
DBUG0180 DS    0H
         ITRACE ID=CSCTLABL
         ICM   R4,15,COMMLABL        FIRST REFERENCE BLOCK
         BZ    DBUG0210              NO CSECT LABELS
         BAL   R8,DBUG0190           CHASE THE CHAIN
         B     DBUG0210              PRINT DISPLACEMENT TABLE
* ------------------------------------------------------------------- *
*             CHASE LABEL CHAIN FOR DSECTS AND CSECTS                 *
* ------------------------------------------------------------------- *
DBUG0190 DS    0H
         USING LABLDSCT,R4           DEFINE BASE
         MVI   PRTCC,C'0'            DOUBLE SPACE
DBUG0200 DS    0H
         ITRACE ID=LABLNTRY
         ST    R4,COMMDWRD           USE DWRD AS WORK AREA
         UNPK  PRLBLBA(9),COMMDWRD(5)
         MVZ   PRLBLBA,COMM0F0F      TURN OFF ZONES
         TR    PRLBLBA,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   PRLBLBA+8,C' '        RESTORE BLANK
         UNPK  PRLBLNXT(9),LABLNEXT(5)
         MVZ   PRLBLNXT,COMM0F0F     TURN OFF ZONES
         TR    PRLBLNXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRLBLNXT+8,C' '       RESTORE BLANK
         MVC   PRLBLNM,LABLNAME      COPY NAME
         MVC   PRLBLTYP,LABLTYPE     COPY LABEL TYPE
         UNPK  PRLBLDSP(9),LABLDISP(5)
         MVZ   PRLBLDSP,COMM0F0F     TURN OFF ZONES
         TR    PRLBLDSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRLBLDSP+8,C' '       RESTORE BLANK
         UNPK  PRLBLEQU(9),LABLEQU(5)
         MVZ   PRLBLEQU,COMM0F0F     TURN OFF ZONES
         TR    PRLBLEQU,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   PRLBLEQU+8,C' '       RESTORE BLANK
         MVC   PRTDATA(PRLABLL),PRLABL
         BAL   R10,PRT0000           PRINT LABL DATA
         ICM   R4,15,LABLNEXT        NEXT LABL BLOCK
         BNZ   DBUG0200              LOOP
         BR    R8                    RETURN
* ------------------------------------------------------------------- *
*             PRINT INSTRUCTION DISPLACEMENT TABLE                    *
* ------------------------------------------------------------------- *
DBUG0210 DS    0H
         ICM   R3,15,COMMDISP        INSTRUCTION DISPLACEMENT TABLE
         BZ    EXIT0000              TABLE NOT ACQUIRED
         MVI   PRTCC,C'0'            DOUBLE SPACE
         MVC   PRTDATA(PRDISPL),PRDISP
         BAL   R10,PRT0000           PRINT HEADING
DBUG0220 DS    0H
         LA    R2,PRTDATA            PRINT DATA
         LA    R1,10                 10 DISPLACEMENTS PER LINE
DBUG0230 DS    0H
         CLC   XFFFF,0(R3)           END OF DISPLACEMENT TABLE?
         BE    DBUG0240              YES
         UNPK  0(9,R2),0(5,R3)       UNPACK DISPLACEMENT
         MVZ   0(8,R2),COMM0F0F      TURN OFF ZONES
         TR    0(8,R2),COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   8(R2),C' '            RESTORE BLANK
         LA    R2,9(R2)              NEXT IN PRINT AREA
         LA    R3,4(R3)              NEXT DISPLACEMENT
         BCT   R1,DBUG0230           LOOP
         BAL   R10,PRT0000           PRINT DISPLACEMENT DATA
         B     DBUG0220              BUILD A NEW LINE
DBUG0240 DS    0H
         CLC   PRTDATA,PRTDATA-1     LINE EMPTY?
         BE    EXIT0000              YES, EXIT
         BAL   R10,PRT0000           PRINT DISPLACEMENT DATA
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*             PRINT SUBHEADING                                        *
* ------------------------------------------------------------------- *
DBUG0300 DS    0H
         BAL   R10,HEAD0000          PRINT HEADING
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*             PRINT DATA                                              *
* ------------------------------------------------------------------- *
DBUG0310 DS    0H
         L     R1,DBUGDATA           DATA ADDRESS
         MVC   PRTDATA(120),0(R1)    COPY DATA
         BAL   R10,PRT0000           PRINT
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*             NO DISDEBUG DD PRESENT                                  *
* ------------------------------------------------------------------- *
DBUG1000 DS    0H
         ITRACE ID=NODEBUG           CAUSE TRACE ENTRY
         B     EXIT0000              EXIT
PRT0000  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISDEBUG,PRTCC        WRITE DEBUG OUTPUT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         AP    LINECT,P1             ADD 1 TO LINE COUNT
         CLI   PRTCC,C' '            SINGLE SPACED?
         BNE   PRT0010               NO
         AP    LINECT,P1             ADD 1 TO LINE COUNT
PRT0010  DS    0H
         MVC   PRTCC(PRTL),PRTCC-1   CLEAR PRINT I/O AREA
         CP    LINECT,COMMMAXL       PAGE OVERFLOW?
         BNHR  R10                   NO
HEAD0000 DS    0H
         MVC   PRTCC(DEBUGHDL),DEBUGHD
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISDEBUG,PRTCC        WRITE NEW HEADING
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   PRTCC(PRTL),PRTCC-1   CLEAR PRINT I/O AREA
         MVC   PRTDATA(L'COMMDBSH),COMMDBSH
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISDEBUG,PRTCC        WRITE SUB HEADING
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   PRTCC(PRTL),PRTCC-1   CLEAR PRINT I/O AREA
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISDEBUG,PRTCC        BLANK LINE
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         ZAP   LINECT,P1             RESET LINE COUNT
         BR    R10                   RETURN
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVEDB   DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
DBUGFLAG DC    X'00'
$DBUGOPN EQU   X'80'                 DCB IS OPEN
WORKX    DC    XL4'00'
P1       DC    P'1'
LINECT   DC    PL3'0'
XFFFF    DC    X'FFFFFFFF'
         SPACE 2
         DC    C' '
PRTCC    DC    C' '
PRTDATA  DC    CL132' '
PRTL     EQU   *-PRTCC
         SPACE 2
DEBUGHD  DS    0C
         DC    C'1               DISASSEMBLER DEBUG'
DEBUGHDL EQU   *-DEBUGHD
PRTR     DS    0C
         DC    C'TRACE TABLE FIRST '
PRTR1ST  DC    CL8' '
         DC    C'   LAST '
PRTRLAST DC    CL8' '
         DC    C'   CURRENT '
PRTRCURR DC    CL8' '
         DC    C' '
PRTRL    EQU   *-PRTR
PRIO     DS    0C
         DC    C' DISMOD I/O AREA ADDRESS '
PRIOA    DC    CL8' '
         DC    C' '
PRIOL    EQU   *-PRIO
PRESD    DS    0C
         DC    C'ESD BLOCK '
PRESDBA  DC    CL8' '
         DC    C' '
PRESDNXT DC    CL8' '
         DC    C'  NAME '
PRESDNM  DC    CL8' '
         DC    C'  TYPE '
PRESDTYP DC    CL2' '
         DC    C'  ADDRESS '
PRESDADR DC    CL7' '
         DC    C'  SEGMENT '
PRESDSEG DC    CL2' '
         DC    C'  LENGTH '
PRESDLEN DC    CL7' '
         DC    C' '
PRESDL   EQU   *-PRESD
PRRLD    DS    0C
         DC    C'RLD BLOCK '
PRRLDBA  DC    CL8' '
         DC    C' '
PRRLDNXT DC    CL8' '
         DC    C'  DISP '
PRRLDDSP DC    CL8' '
         DC    C'  TYPE '
PRRLDTYP DC    CL2' '
         DC    C'  LENGTH '
PRRLDLEN DC    CL4' '
         DC    C'  POINTER '
PRRLDPTR DC    CL4' '
         DC    C'  POSITION '
PRRLDPP  DC    CL4' '
         DC    C'  DIR '
PRRLDDIR DC    C' '
         DC    C'  ESD '
PRRLDESD DC    CL8' '
         DC    C' '
PRRLDL   EQU   *-PRRLD
PRUSNG   DS    0C
         DC    C'USING '
PRUSGBA  DC    CL8' '
         DC    C' '
PRUSGNXT DC    CL8' '
         DC    C'  DSECT '
PRUSGNME DC    CL8' '
         DC    C'  LABEL '
PRUSGLBL DC    CL8' '
         DC    C'  BASE '
PRUSGBSE DC    C' '
         DC    C'  FLAGS '
PRUSGFLG DC    CL2' '
         DC    C'  DISP '
PRUSGDSP DC    CL8' '
         DC    C'  BEGN/END'
PRUSGBGN DC    CL8' '
         DC    C'-'
PRUSGEND DC    CL8' '
         DC    C' '
PRUSNGL  EQU   *-PRUSNG
PRDSCT   DS    0C
         DC    C'DSECT '
PRDSBA   DC    CL8' '
         DC    C' '
PRDSNXT  DS    CL8' '
         DC    C'  DSECT NAME '
PRDSNAME DC    CL8' '
         DC    C'  1ST LABEL '
PRDSLABL DC    CL8' '
         DC    C' '
PRDSCTL  EQU   *-PRDSCT
PRBASE   DS    0C
         DC    C'BASE '
PRBSEBA  DS    CL8' '
         DC    C' '
PRBSENXT DC    CL8' '
         DC    C'  REGISTER '
PRBSEREG DC    C' '
         DC    C'  INSTRUCTION RANGE '
PRBSEBGN DC    CL9' '
PRBSEEND DC    CL9' '
         DC    C'  DISP REFERRED TO '
PRBSEDSP DC    CL9' '
PRBASEL  EQU   *-PRBASE
PRDATA   DS    0C
         DC    C'DATA '
PRDTABA  DC    CL8' '
         DC    C' '
PRDTANXT DC    CL8' '
         DC    C'  DISP '
PRDTABGN DC    CL8' '
         DC    C'-'
PRDTAEND DC    CL8' '
         DC    C'  LENGTH '
PRDTALEN DC    CL8' '
         DC    C'  TYPE '
PRDTATYP DC    CL2' '
         DC    C'  NAME '
PRDTANME DC    CL8' '
         DC    C'  LABEL '
PRDTALBA DC    CL8' '
         DC    C'+'
PRDTALBD DC    CL8' '
         DC    C' '
PRDATAL  EQU   *-PRDATA
PRREF    DS    0C
         DC    C'REF '
PRREFBA  DC    CL8' '
         DC    C' '
PRREFNXT DC    CL8' '
         DC    C'  LABEL 1 '
PRREFAD1 DC    CL8' '
         DC    C'-'
PRREFO1D DC    CL8' '
         DC    C'  LABEL 2 '
PRREFAD2 DC    CL8' '
         DC    C'-'
PRREFO2D DC    CL9' '
         DC    C'  INSTRUCTION DISP '
PRREFDSP DC    CL8' '
         DC    C' '
PRREFL   EQU   *-PRREF
PRLABL   DS    0C
         DC    C'LABEL '
PRLBLBA  DC    CL8' '
         DC    C' '
PRLBLNXT DC    CL8' '
         DC    C'  LABEL NAME '
PRLBLNM  DC    CL8' '
         DC    C'  TYPE '
PRLBLTYP DC    C' '
         DC    C'  DISP '
PRLBLDSP DC    CL8' '
         DC    C'  EQUATE '
PRLBLEQU DC    CL8' '
         DC    C' '
PRLABLL  EQU   *-PRLABL
PRDISP   DC    C'INSTRUCTION DISPLACEMENTS:'
PRDISPL  EQU   *-PRDISP
DISDEBUG DCB   DDNAME=DISDEBUG,      DEBUG DCB                         +
               DSORG=PS,             .. SEQUENTIAL                     +
               LRECL=133,            .. RECORD SIZE                    +
               RECFM=FBA,            .. RECORD FORMAT                  +
               MACRF=PM              .. PUT-MOVE MODE
         LTORG
         SPACE 2
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              INTERFACE BLOCK                                        *
*                                                                     *
*---------------------------------------------------------------------*
DBUGBLOK DBUGBLOK TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASMDB
./ ADD NAME=DISASMDC
1                      DISASSEMBLER
0 Change history:
    Dec  93    added "B2" instruction module
    Dec  93    converted code to run in 31-bit mode as much as possible
    Dec  93    corrected S0C4 abend when generating DC's with 31-bit flag
    Dec  93    added "no float" option
    Jan  94    added test for 8 byte literal strings
    Jan  94    added register tests for "even-odd", floating point
    Jan  94    added samples to JCL library
    Oct  04    allow user specified DATA areas to overlap RLD items
               change to DISASM07 to properly process SYSPRINT from
                 the the assembler with either SYSPRINT LRECL=121 or
                 SYSPRINT LRECL=133.
               added the ability to pre-process DSECTs

0 DISASM is a multi-pass disassembler.  Source assembler statements are
  produced from a CSECT of a linkedited module.  The module may contain
  multiple CSECT's, however DISASM disassembles one CSECT per execution.
0 Control statements:
- Comment statement (optional)
0 Columns  1 -  1     * (asterisk)
                      Comments may be specified at any time.  Comments
                      will be copied to the output listing.
- ASM START statement (optional)
0 Columns  1 -  9     Literal 'ASM START'.
                      Delimits the start of input for the assembler.
                      Any statements specified are also copied to the
                      end of the disassembled punched output.  This
                      allows DSECTs to be assembled so the labels
                      can be used for data references.  Any DSECT used
                      in a USING statement must be defined by this
                      method.  Use care when defining your DSECTs, the
                      disassembler requires the PRINT option to be on
                      to determine the DSECT and label names.  If a
                      macro or copy statement turns the PRINT off, the
                      DSECT and labels will not be available.
- ASM END statement (optional)
0 Columns  1 -  7     Literal 'ASM END'.
                      Delimits the end of the assembler input.
- BASE statement (optional)
0 Columns  1 -  4     Literal 'BASE'.
          10 - 12     Base register.  Register 1 may be specfied as 1 or
                      R1, register 10 may be specified as 10 or R10.
                      Registers 10 to 15 may be specified as A to F or
                      RA to RF.
          20 - 27     Starting displacement into the CSECT that the base
                      register is in effect.  Displacements are given as
                      hex values 1 to 8 digits long, leading zeros are
                      not required (0A0 is equivalent to A0).
          30 - 37     Optional.  Ending displacement into the CSECT that
                      the base register is in effect.  If omitted, the
                      ending displacement will be 4096 from the starting
                      value (in columns 20-27).  Coding is the same as
                      for the starting displacement.
          40 - 47     Displacement the base register refers to.  For
                      example if the base register is R12 and R12 is
                      used as the base in instructions starting at
                      +X'20', and R12 is set to point to +X'22C' into
                      the CSECT, the BASE statement would be:
0                     BASE     R12       20        22C
0                     If a base register is defined, a LABEL (or PREFIX)
                      statement is also required.
- CSECT statement (optional)
0 Columns  1 -  5     Literal 'CSECT'.
          10 - 17     CSECT's name.  If the CSECT is not specified, the
                      first CSECT in the module will be disassembled.
- DATA statement (optional)
0 Columns  1 -  4     Literal 'DATA'.
          10 - 17     Starting displacement into the CSECT where the data
                      begins.  DATA statements allow you to identify
                      areas that do not contain instructions.  See the
                      BASE statement for displacement coding syntax.
          20 - 27     Displacement where the data ends.
- LABEL statement (optional)
0 Columns  1 -  5     Literal 'LABEL'.
             or
           1 -  6     Literal 'PREFIX'.
          10 - 14     1 to 4 character label prefix.  Labels generated
                      due to references within the CSECT will be
                      PPPPNNNN where PPPP is the prefix specified here
                      and NNNN is the displacement into the CSECT or
                      a sequential number (0010, 0020, 0030, etc).
                      If the prefix is less than 4 characters long, the
                      numeric portion will be padded on the left with
                      zeros (labels will always be 8 characters long).
                      If the LABEL/PREFIX statement is used, base
                      registers must also be defined.
- LINE/LINES/MAXLINES statement (optional).
0 Columns  1 -  4     Literal 'LINE'
             or
           1 -  5     Literal 'LINES'
             or
           1 -  9     Literal 'MAXLINES'
          10 - 12     Maximum number of lines to print per page on the
                      DISPRINT and DISDEBUG data sets.  Line values are
                      2 to 3 digits, minimum is 10 lines.  Specify the
                      value left justified, leading zeros may be omitted.
                      Default line count is 60 lines per page.
- MODULE statement (required)
0 Columns  1 -  6     Literal 'MODULE'.
          10 - 17     Load module's name.  This module must be in the
                      library specified in the DISMOD DD.
- NO FLOAT statement (optional)
0 Columns  1 -  8     Literal 'NO FLOAT'.
             or
  Colums   1 - 7      Literal 'NOFLOAT'.
                      Suppresses floating point opcodes from being
                      considered as valid opcodes.  When you are certain
                      the module being processed does not use floating
                      point, suppressing the floating point opcodes
                      stops the disassembler from interpreting some of
                      the data areas as instructions.
1 RLD WARN statement  (optional)
                      When an RLD item overlaps a user specified DATA
                      area, DISASM will adjust the user defined DATA
                      areas.  By default no messages are issued.  If
                      you want to see how the disassembler is adjusting
                      the DATA areas, include a "RLD WARN" statement.
0 SEQ LABEL statement (optional)
0 Columns  1 -  9     Literal 'SEQ LABEL'.
                      Labels generated for labels within the CSECT will
                      be PPPPNNNN where PPPP is the prefix specified on
                      in the LABEL or PREFIX statement and NNNN is either
                      the displacement of the label into the CSECT or a
                      sequential number.  By default NNNN will be the
                      displacement in hex.  SEQ LABEL requests the labels
                      be generated with sequential numbers.  This may be
                      desirable if the source is to be modified... the
                      labels may no longer be at the same displacement.
- USING statement (optional)
0 Columns  1 -  5     Literal 'USING'.
          10 - 17     DSECT's name.  Any DSECTs referenced on USING
                      statements must be defined via assembler input.
          20 - 27     Label within the DSECT.  In some cases the base
                      register may be set to point to a label within the
                      DSECT rather than at displacement zero.  If the
                      base points to displacement zero, leave this
                      parameter blank.
          30 - 32     Base register.  See BASE statement for base reg
                      syntax.
          40 - 47     If the register specified as the base is used as
            and       the base in all cases where it appears in the
          50 - 57     code, leave these parameters blank.  If the base
                      is only used as a base for the specified DSECT at
                      in a range of instructions, specify the beginning
                      displacement in columns 40-47 and the ending
                      displacement in columns 50-57.  The same base may
                      be specified for the same DSECT multiple times with
                      different ranges if necessary.
1                 JCL REQUIREMENTS
0      //....     EXEC PGM=DISASM,REGION=nnnnK                 REQ
       //STEPLIB  DD DSN=xxxx,DISP=SHR                         OPT
       //SYSPRINT DD DSN=&&PRT,DISP=(NEW,PASS),                OPT
       //            UNIT=SYSDA,
       //            SPACE=(TRK,(15,15)),
       //            DCB=(RECFM=FBM,LRECL=121,BLKSIZE=12100)
       //SYSIN    DD DSN=&&IN,DISP=(NEW,PASS),                 OPT
       //            UNIT=SYSDA,
       //            SPACE=(TRK,(15,15)),
       //            DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120)
       //SYSLIB   DD DSN=xxxx,DISP=SHR                         OPT
       //SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(1,1))              OPT
       //SYSPUNCH DD DUMMY                                     OPT
       //DISDEBUG DD SYSOUT=*                                  OPT
       //DISPRINT DD SYSOUT=*                                  REQ
       //DISPUNCH DD SYSOUT=class                              OPT
       //DISDSECT DD DSN=dddd,DISP=SHR                         OPT
       //DISMOD   DD DSN=xxxx,DISP=SHR                         REQ
       //DISIN    DD *                                         REQ
             .
             control statements
             .
       /*
       //
0If DISASM is an a LINKLST library the STEPLIB is not required.
 If the assembler input (ASM START/ASM END) is used SYSPRINT, SYSIN,
 SYSLIB, SYSUT1, and SYSPUNCH are required, otherwise they may be
 omitted.
 If the DISPUNCH DD is present, the source will be generated as an
 80-byte file suitable for input to the assembler.
 Any macros or copy statements specified as assembler input must be
 available in a library in the SYSLIB concatenation.
1                 ABEND CODES and MESSAGES
0 ABEND 001     User requested an ABEND via the ABEND statement.
0 ABEND 002     Unknown return code from BLDL.
0 ABEND 003     Unknown RLD with unknown type encountered.
0 ABEND 004     Internal error, RLD data remaining went negative in
                DISASM05.
0 ABEND 005     Internal error, attempt to generate an instruction on
                on an odd address boundary.
0 DISASM0101I   No DISPUNCH DD present, no source will be generated
                Informational only.  The DISPUNCH DD was not present
                in the JCL, no source deck will be produced.
0 DISASM0102I   ABEND requested, program abnormally terminating
                For diagnositic purposes, an ABEND command was added
                to cause an abend just prior to freeing the trace table
                and terminating.  This message is issued immediately
                before the abend, and indicates the abend was due to
                the user's request and not due to a problem.
                DISASM will also have abended with a user 001.
0 DISASM0103I   ******* Disassembly complete *******
                Indicates that the disassembly was successful.
0 DISASM0104E   DISIN DD statement missing, processing will be aborted
                DISASM requires the DISIN DD statement.  DISASM must
                know at minimum which module it is to disassemble.  The
                MODULE and all other control statements are provided to
                DISASM via DISIN.
0 DISASM0105E   DISMOD DD statement missing, processing will be aborted
                DISASM loads the module to be disassembled from the
                library specified by the DISMOD DD, this DD is therefore
                required.
0 DISASM0106E   Error(s) in control statements, execution aborted
                Error(s) were detected in the control statements by
                module DISASM02.  Messages are issued to identify the
                statement(s) in error.  Correct the control statements
                and re-run.
0 DISASM0107E   Error(s) in loading object module, execution aborted
                Error(s) were encountered by DISASM03 in loading the
                specified module.  One of the following occurred:
                   1) the BLDL for the member failed
                   2) if the specified member was an alias, the
                      BLDL for the real member failed.
                   3) the POINT for the member (or real member) failed
                   4) the requested CSECT was not in the specified
                      module
                Further messages will have been issued to identify the
                the cause.
0 DISASM0108E   Error(s) in printing text, execution aborted
                Should never occur.  At present there are no errors
                detected by the text print module, DISASM06.
0 DISASM0109E   Error(s) in assembling DSECTs, execution aborted
                Either the return code from the assembler was greater
                than 4, or some other error in interpreting the DSECTs
                occurred.  Messages will have been issued to identify
                the cause.
0 DISASM0110E   Error(s) in generating label table, execution aborted
                An error condition was detected by DISASM08.  Messages
                will have been issued to identify the cause.
0 DISASM0111E   Error(s) in generating source, execution aborted
                An error condition was detected by DISASM09.  Messages
                will have been issued to identify the cause.
0 DISASM0202W   Sequentially numbered labels have already been requested
                The SEQ LABEL statement is specified more than once in
                the control statements.
0 DISASM0203E   Invalid control statement
                Columns 1-9 of a control statement contain an unknown
                statement type.  See the list of control statements
                for valid control statements and their syntax.
0 DISASM0204E   Extraneous data in register parameter
                DISASM checks for several blanks following a register
                specification on BASE and USING statements.  This check
                helps to make sure you specified parameters in the
                correct columns.  If the blanks are not present, this
                message is issued and the program abort flag set.
0 DISASM0205E   Invalid register reference
                The register specified on a BASE or USING statement is
                invalid.  Valid values are 0-15, A-F, R0-R15, and RA-RF.
0 DISASM0206E   End displacement is required when begin displacement is given
                On USING statements the beginning and ending displacement
                values are optional.  If the beginning displacement is
                given, the ending displacement is also required.
0 DISASM0207E   Begin displacement is larger than end displacement
                The beginning and ending displacement values specify
                a range of instruction displacements.  The end value
                must be larger than the begin value.
0 DISASM0208E   Bad hex digit in displacement
                An invalid character was found in a hex value.  Valid
                digits are 0-9 and A-F.
0 DISASM0209E   Too many digits in displacement
                Displacement values must be 8 characters or less.
0 DISASM0210E   End displacement not allowed unless start displacement
                is specified
                On USING statements the beginning and ending displacement
                values are optional.  If the ending displacement is
                given, the beginning displacement is also required.
0 DISASM0211E   Label prefix cannot be blank
                The prefix value on a PREFIX or LABEL statement has a
                blank in the first byte.
0 DISASM0212E   Label prefixes must be 4 characters or less
                The value on a PREFIX or LABEL statement is more than
                4 characters long.  Label prefixes must be 4 characters
                or less.
0 DISASM0213E   Base register is blank
                The base register on a BASE or USING statement is blank.
0 DISASM0214E   Base register name exceeds 3 characters in length
                The base register on a BASE or USING statement is more
                than 3 characters long.  Valid values are 0-15, A-F,
                R0-R15, and RA-RF.
0 DISASM0215E   'DATA' is reserved for data area prefixes, choose
                another prefix
0 DISASM0216E   Invalid digit in LINE/LINES/MAXLINES statement
                The line count limit specified contains a non-numeric
                digit.
0 DISASM0217E   Line count value on a LINE/LINES/MAXLINES statement is
                too long or contains extraneous data
                The line count value is 1 to 3 digits.  Either the value
                is more than 3 digits or there is other data in the
                next 6 spaces.
0 DISASM0218E   Line count value on a LINE/LINES/MAXLINES statement is
                below minimum allowed
                A minimum allowable line count value is specified in the
                global options (member DISASMGB) when DISASM is genned.
                The value you are requesting is below this minimum.
0 DISASM0219E   Label prefix has already been defined, choose one or the
                other
                DISASM only allows 1 label prefix value.  The PREFIX or
                LABEL statement has been found more than once.  Choose
                one of the prefixes and delete the second statement.
0 DISASM0220E   Label prefix must be defined when base register(s) are
                defined
                When a BASE is defined, labels will be generated for
                any points referenced within the CSECT.  Labels require
                a 1 to 4 character prefix specified by either the LABEL
                or PREFIX statement.
0 DISASM0221E   Label prefix not valid unless base register(s) are defined
                Labels for the CSECT cannot be generated unless a BASE
                is defined.
0 DISASM0222E   This area overlaps data at xxxx to xxxx
                An area being defined as a DATA area overlaps another
                area that has already been defined at xxxx to xxxx.
0 DISASM0223E   Beginning displacement is larger than ending displacement
                The beginning displacement is larger than the ending
                displacement on a BASE statement.
0 DISASM0301E   Specified CSECT not found
                The CSECT specified on the CSECT statement is not a
                part of the specified MODULE or the module contains no
                CSECTs.
0 DISASM0302E   Unknown return code from POINT macro
                DISASM received a return code from the POINT macro that
                it could not interpret.
0 DISASM0303E   DCB EODAD routine driven, end of control records not
                detected
                The EODAD routine of the DISMOD DCB should never be
                driven.  The last record of the requested CSECT should
                be detected.  This is an internal logic error.
0 DISASM0304E   Module does not exist in DISMOD library
                The member specified on the MODULE statement was not
                found in the DISMOD library.
0 DISASM0305E   Permanent I/O error
                BLDL received return code 08 with reason code 01.
0 DISASM0306E   Insufficient virtual storage
                BLDL received return code 08 with reason code 04.
0 DISASM0307E   DEB not in KEY 0-7
                BLDL received return code 08 with reason code 08.
0 DISASM0308E   Device does not support block identifier
                POINT received return code 04 with reason code 00.
0 DISASM0309E   Incorrect parameter
                POINT received return code 08 with reason code 01.
0 DISASM0310E   Incorrect DEB or DEBCHK error
                POINT received return code 08 with reason code 02.
0 DISASM0311E   Environmental error
                POINT received return code 08 with reason code 03.
0 DISASM0312E   Unsuccessful call to ESTAE
                POINT received return code 08 with reason code 0B.
0 DISASM0313E   Unsuccessful GETMAIN
                POINT received return code 08 with reason code 0C.
0 DISASM0314E   Input/output error
                POINT received return code 0C with reason code 00.
0 DISASM0501E   Unknown RLD data type
                RLD data contains an item that is of an unknown type.
                DISASM will have abended with code user 003.
0 DISASM0502E   RLD data remaining went negative
                Internal logic error in module DISASM05.
                DISASM will have abended with code user 004.
0 DISASM0503E   RLD pointer larger than number of ESD items
                Some RLD items have corresponding ESD items.  When
                the pointer value is non-zero, it is the relative number
                of the ESD item it is associated with.  In this case the
                pointer value is larger than the number of ESD items.
0 DISASM0701I   Assembler return code was xxxx
                If the return code is greater than 4, DISASM will abort
                processing.  If you need to view the assembler output,
                allocate the DISDEBUG DD.
0 DISASM0702I   No assembler input
0 DISASM0703E   Error assembling DSECTs, check assembler output in DISDEBUG
                The return code from the assembler was greater than 4.
0 DISASM0801E   DSECT xxxxxxxx is not present, but is referenced on a
                USING statement.
                Either 1) the source for the DSECT was not provided
                       2) the PRINT of the assembler was turned off
                       3) the name is misspelled
                If you are sure the name is correct and the source is
                present, allocate DISDEBUG and check the assembler output.
0 DISASM0802E   Instructions overlap data defined at xxxx to xxxx
                Instruction lengths are determined by the opcodes.  If
                a portion of an instruction overlaps into a DATA area,
                this is considered an error condition.  Either increase
                the size of the DATA area to include the entire instruc-
                tion, or decrease the size of the DATA area so it does
                not overlap the end of the instruction.
0 DISASM0803W   aaaaaaaa displacement in a bbbbbbbb block is changed
                from xxxx to yyyy to reference an instruction boundary
                   aaaaaaaa is either BEGINNING or ENDING
                   bbbbbbbb is either BASE or USING
                The beginning and ending displacements on BASE and USING
                statements must reference instruction boundaries.  This
                is because the USING or DROP statements generated cannot
                occur in the middle of an instruction.  If a displacement
                is found that is not on instruction boundary, it is forced
                to the beginning of the preceding instruction.
0 DISASM0804E   Invalid opcode during reference table generation
                Internal logic error in DISASM08.
0 DISASM0805E   Label llllllll is not in DSECT dddddddd as requested on
                a USING statement
                If you are sure the label is in the DSECT, allocate
                DISDEBUG to verify the assembler output.
0 DISASM0806E   Label not found in DSECT during reference table generation
                Internal logic error in DISASM08.
0 DISASM0808E   Overlapping data areas not detected by DISASM02
                Interal logic error.
0 DISASM0809E   Attempt to locate an instruction on an odd displacement
                boundary
                Internal error in DISASM08, DISASM will abend with code
                user 005.
0 DISASM0901E   Data area overlaps an instruction, should have been
                detected by DISASM08.
                Internal logic error.  See explanation of DISASM0802E.
0DISASM0902E    Invalid opcode detected
                Internal error.
0DISASM0903E    DC with length = zero detected
                Internal error.
0DISASM0904E    Attempt to generate an instruction on an odd address
                boundary
                Internal error.
1                 Program description
 This disassembler attempts to re-create assembler source from object
 code.  Label references are generated if the base registers for the
 CSECT or DSECTs are defined.
0CSECT labels will be generated for data referenced within the CSECT
 if BASE statements define the base register(s).  References to
 instructions will cause labels to be generated that occur at the
 displacement of the first byte of the instruction.  If the reference
 is not on the first byte of the instruction, the label will be referred
 to with a "+displacement".  For example if a program is self-modifying
 and sets the length in a MVC instruction with a STC that references the
 MVC instruction, the generated source might look like:
0            .
             STC   R1,MAIN002C+1
             .
             .
    MAIN002C MVC  DATAOUT(1),0(R2)
             .
0Labels will also be generated for any ENTRY points defined in the ESD
 data for the CSECT.
0DSECT labels require the DSECT's source to be assembled, and the base
 defined via a USING statement.  The assembler is dynamically
 invoked to assemble the DSECT(s).  The assembler output listing
 is scanned to determine the DSECT names, label names, and the displace-
 ment to the label in the DSECT.  In order for the disassembler to
 detect DSECTs and the labels, the assembler print must be on.  If any
 macro or copy code turns off the print, the DSECTs and labels will
 be unknown.  Any DSECT referred to by a USING statement must be defined
 by having the assembler assemble it.  DSECT source can be pre-processed
 by DISASMU1 or assembled at disassembly time.  DSECTs defined via
 ASM START/ASM END over-ride the same DSECT in the DISDSECT DD.
0Assembler source statements are delimited by ASM START/ASM END.  Any
 statements between the ASM START and ASM END statements will be copied
 to the assembler input data set and appended onto the end of the source
 written to DISPUNCH.  Any macros not in the assembler input stream must
 be available in a library in the SYSLIB concatenation.  If the return
 code from the assembler is greater than 4, the disassembler discontinues
 any further processing.  If there is no assembler input, the SYSPRINT,
 SYSIN, SYSUT1, SYSPUNCH, and SYSLIB DD's may be omitted.
0If the DISPUNCH DD is present, the generated code will also be
 written to this data set as 80-byte records suitable as input to the
 assembler.
0Object code is considered to be an instruction if:
    1) it is not in a defined data area
    2) it is on an even address boundary
    3) it is a valid opcode
    4) it does not overlap into a defined data area or RLD item.
    5) it is followed by another valid opcode or is an unconditional
       branch or SVC.
    6) a string of upper or lower case EBCDIC is not considered a valid
       instruction.
    7) if "NO FLOAT" was requested, the instruction cannot be a floating
       point instruction.
0THE VALID OPCODES INCLUDE XA, BUT NOT ESA (AR) INSTRUCTIONS.
0Extended mnemonics are used if possible for branch instructions.
0SVC's are interpreted if possible (SVC 0A is identified as GETMAIN).
0The DISDEBUG DD statement is optional.  If present, many internal
 fields and data chains will be printed to aid in debugging problems
 with the disassembler.  The assembler output is copied to DISDEBUG
 for diagnosing errors with the assembler input.



Pre-processing DSECTs
The disassembler can generate labels for data referenced in DSECTs.
requires the DSECT's source to be assembled.  The disassembler then
reads the assembler output (SYSPRINT) to learn the label names and
their displacements.  DSECTs can be pre-processed or assembled during
a disassembly.  Pre-processing can save time.

To pre-process DSECTs, allocate a PDS with RECFM VB with LRECL=.
Use a block size (BLKSIZE) that is efficient for your DASD.
Assemble any DSECTs you want to pre-process and input the SYSPRINT
to DISASMU1.  You can use member DISASMU1 as sample JCL to run
the assembler and DISASMU1 in one JOB.

DISASMU1 will create one member per DSECT in the DISDSECT library.
Some IBM provided MACROs actually generate multiple DSECTs.
Members in the DISDSECT library are cumulative.  Data for a given
member is not cumulative.  That is if you pre-process DSECTA in
one run and DSECTB in a second, the data for DSECTA will still
be in the the library.  If you pre-process DSECTA in a run, make
changes to the DSECT and re-process DSECTA, the info for DSECTA
in the second run competely replaces the data from the first run.

You can pre-process IBM DSECTs, your own DSECTs, or those of a
3rd party.. as long as you don't have duplicated DSECT names.

The DISDSECT DD can be a concatenation.  You may want to pre-process
IBM DSECTs into an "IBM" only library, 3rd party DSECTs into a
library per vendor, and your own into a company library and then
concatenate the libraries that you need for the program you are
disassembling at a given time.

The disassembler only reads the members that are referenced on
USING statements.  It does not load in the entire contents of
all pre-processed DSECTs.

If all the DSECTs referenced on USING statements are pre-processed
by DISASMU1 and in the DISDSECT library(s) at disassembly time
no ASM START/ASM END source is required for the dissembly and
the assembler will not need to be invoked.

Something that I have found is the DSECT names are not always
what I expected.  For example IEZWPL generates the mapping for
a "WTO   MF=L".  Label WPLTXT is in DSECT WPLRF not WPL.  You
may want to re-name members in the pre-processed DSECT library
or add ALIASes (like WPL as an alias to WPLRF).  Remember when
the disassembler looks for a pre-processed DSECT in the DISDSECT
library, it expects the member name to be the same as the DSECT
name.  For the WPL example, you would need a USING statment that
reads   "USING     WPLRF     WPLTXT    Rx".
./ ADD NAME=DISASMOP
          TITLE 'DISASMOP - OPCODE TABLE'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASMOP                                             *
*                                                                    *
*  Function:                                                         *
*     Define valid machine opcodes.                                  *
*                                                                    *
*--------------------------------------------------------------------*
         COPY   DISASMGB
DISASMOP CSECT
DISASMOP AMODE  31
DISASMOP RMODE  24
         ORG    DISASMOP+(256*4)
* ------------------------------------------------------------------- *
*        OPCODE TABLE                                                 *
* ------------------------------------------------------------------- *
         OPCODE 00,DC,0,2                 DUMMY ENTRY FOR DC'S
         OPCODE 04,SPM,$OPRR2,2
         OPCODE 05,BALR,$OPRR1,2,'PERFORM'
         OPCODE 06,BCTR,$OPRR1,2,'LOOP'
         OPCODE 07,BCR,$OPRR3,2,FLAGS=$OPEXT
         OPCODE 0A,SVC,$OPRR2,2,'SVC DESCRIPTION',FLAGS=$OPSVC
         OPCODE 0B,BSM,$OPRR1,2
         OPCODE 0C,BASSM,$OPRR1,2
         OPCODE 0D,BASR,$OPRR1,2
         OPCODE 0E,MVCL,$OPRR1,2,FLAGS=$OPCCA,REGS=$OP1EVEN+$OP2EVEN
         OPCODE 0F,CLCL,$OPRR1,2,FLAGS=$OPCCA,REGS=$OP1EVEN+$OP2EVEN
         OPCODE 10,LPR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 11,LNR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 12,LTR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 13,LCR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 14,NR,$OPRR1,2,FLAGS=$OPCCL
         OPCODE 15,CLR,$OPRR1,2,FLAGS=$OPCCC
         OPCODE 16,OR,$OPRR1,2,FLAGS=$OPCCL
         OPCODE 17,XR,$OPRR1,2,FLAGS=$OPCCL
         OPCODE 18,LR,$OPRR1,2
         OPCODE 19,CR,$OPRR1,2,FLAGS=$OPCCC
         OPCODE 1A,AR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 1B,SR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 1C,MR,$OPRR1,2,REGS=$OP1EVEN
         OPCODE 1D,DR,$OPRR1,2,REGS=$OP1EVEN
         OPCODE 1E,ALR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 1F,SLR,$OPRR1,2,FLAGS=$OPCCA
         OPCODE 20,LPDR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 21,LNDR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 22,LTDR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 23,LCDR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 24,HDR,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 25,LRDR,$OPRR1,2,FLAGS=$OPFLOAT,                       +
               REGS=$OP10246+$OP204
         OPCODE 26,MXR,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP104+$OP204
         OPCODE 27,MXDR,$OPRR1,2,FLAGS=$OPFLOAT,                       +
               REGS=$OP10246+$OP20246
         OPCODE 28,LDR,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 29,CDR,$OPRR1,2,FLAGS=$OPCCC+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 2A,ADR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 2B,SDR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 2C,MDR,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 2D,DDR,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 2E,AWR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 2F,SWR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 30,LPER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 31,LNER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 32,LTER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 33,LCER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                +
               REGS=$OP10246+$OP20246
         OPCODE 34,HER,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 35,LRER,$OPRR1,2,FLAGS=$OPFLOAT,                       +
               REGS=$OP10246+$OP20246
         OPCODE 36,AXR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP104+$OP204
         OPCODE 37,SXR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP104+$OP204
         OPCODE 38,LER,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 39,CER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 3A,AER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 3B,SER,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 3C,MER,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 3D,DER,$OPRR1,2,FLAGS=$OPFLOAT,                        +
               REGS=$OP10246+$OP20246
         OPCODE 3E,AUR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 3F,SUR,$OPRR1,2,FLAGS=$OPCCA+$OPFLOAT,                 +
               REGS=$OP10246+$OP20246
         OPCODE 40,STH,$OPRX,4,FLAGS=$OPREF
         OPCODE 41,LA,$OPRX,4,FLAGS=$OPREF
         OPCODE 42,STC,$OPRX,4,FLAGS=$OPREF
         OPCODE 43,IC,$OPRX,4,FLAGS=$OPREF
         OPCODE 44,EX,$OPRX,4,FLAGS=$OPREF
         OPCODE 45,BAL,$OPRX,4,'PERFORM',FLAGS=$OPREF
         OPCODE 46,BCT,$OPRX,4,'LOOP',FLAGS=$OPREF
         OPCODE 47,BC,$OPRX,4,FLAGS=$OPEXT+$OPREF
         OPCODE 48,LH,$OPRX,4,FLAGS=$OPREF
         OPCODE 49,CH,$OPRX,4,FLAGS=$OPREF+$OPCCC
         OPCODE 4A,AH,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 4B,SH,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 4C,MH,$OPRX,4,FLAGS=$OPREF
         OPCODE 4D,BAS,$OPRX,4,FLAGS=$OPREF
         OPCODE 4E,CVD,$OPRX,4,FLAGS=$OPREF
         OPCODE 4F,CVB,$OPRX,4,FLAGS=$OPREF
         OPCODE 50,ST,$OPRX,4,FLAGS=$OPREF
         OPCODE 54,N,$OPRX,4,FLAGS=$OPREF+$OPCCL
         OPCODE 55,CL,$OPRX,4,FLAGS=$OPREF+$OPCCC
         OPCODE 56,O,$OPRX,4,FLAGS=$OPREF+$OPCCL
         OPCODE 57,X,$OPRX,4,FLAGS=$OPREF+$OPCCL
         OPCODE 58,L,$OPRX,4,FLAGS=$OPREF
         OPCODE 59,C,$OPRX,4,FLAGS=$OPREF+$OPCCC
         OPCODE 5A,A,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 5B,S,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 5C,M,$OPRX,4,FLAGS=$OPREF
         OPCODE 5D,D,$OPRX,4,FLAGS=$OPREF,                             +
               REGS=$OP1EVEN
         OPCODE 5E,AL,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 5F,SL,$OPRX,4,FLAGS=$OPREF+$OPCCA
         OPCODE 60,STD,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                  +
               REGS=$OP10246
         OPCODE 67,MXD,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                  +
               REGS=$OP104
         OPCODE 68,LD,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 69,CD,$OPRX,4,FLAGS=$OPREF+$OPCCC+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 6A,AD,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 6B,SD,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 6C,MD,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 6D,DD,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 6E,AW,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 6F,SW,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 70,STE,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                  +
               REGS=$OP10246
         OPCODE 78,LE,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 79,CE,$OPRX,4,FLAGS=$OPREF+$OPCCC+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 7A,AE,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 7B,SE,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 7C,ME,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 7D,DE,$OPRX,4,FLAGS=$OPREF+$OPFLOAT,                   +
               REGS=$OP10246
         OPCODE 7E,AU,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 7F,SU,$OPRX,4,FLAGS=$OPREF+$OPCCA+$OPFLOAT,            +
               REGS=$OP10246
         OPCODE 80,SSM,$OPS,4,FLAGS=$OPREF
         OPCODE 82,LPSW,$OPS,4,FLAGS=$OPREF
         OPCODE 83,DIAG,$OPS,4
         OPCODE 86,BXH,$OPRS2,4,FLAGS=$OPREF
         OPCODE 87,BXLE,$OPRS2,4,FLAGS=$OPREF
         OPCODE 88,SRL,$OPRS1,4
         OPCODE 89,SLL,$OPRS1,4
         OPCODE 8A,SRA,$OPRS1,4,FLAGS=$OPCCA
         OPCODE 8B,SLA,$OPRS1,4,FLAGS=$OPCCA
         OPCODE 8C,SRDL,$OPRS1,4,                                      +
               REGS=$OP1EVEN
         OPCODE 8D,SLDL,$OPRS1,4,                                      +
               REGS=$OP1EVEN
         OPCODE 8E,SRDA,$OPRS1,4,FLAGS=$OPCCA,                         +
               REGS=$OP1EVEN
         OPCODE 8F,SLDA,$OPRS1,4,FLAGS=$OPCCA,                         +
               REGS=$OP1EVEN
         OPCODE 90,STM,$OPRS2,4,FLAGS=$OPREF
         OPCODE 91,TM,$OPSI,4,FLAGS=$OPREF+$OPCCL
         OPCODE 92,MVI,$OPSI,4,FLAGS=$OPREF
         OPCODE 93,TS,$OPS,4,FLAGS=$OPREF+$OPCCA
         OPCODE 94,NI,$OPSI,4,FLAGS=$OPREF+$OPCCL
         OPCODE 95,CLI,$OPSI,4,FLAGS=$OPREF+$OPCCC
         OPCODE 96,OI,$OPSI,4,FLAGS=$OPREF+$OPCCL
         OPCODE 97,XI,$OPSI,4,FLAGS=$OPREF+$OPCCL
         OPCODE 98,LM,$OPRS2,4,FLAGS=$OPREF
         OPCODE 99,TRACE,$OPRS2,4,FLAGS=$OPREF
         OPCODE AC,STNSM,$OPSI,4,FLAGS=$OPREF
         OPCODE AD,STOSM,$OPSI,4,FLAGS=$OPREF
         OPCODE AE,SIGP,$OPRS2,4,FLAGS=$OPCCA
         OPCODE AF,MC,$OPSI,4
         OPCODE B1,LRA,$OPRX,4,FLAGS=$OPREF+$OPCCA
*        OPCODE B2,DUMMY,$OPB2,4
         OPCODE B6,STCTL,$OPRS2,4,FLAGS=$OPREF
         OPCODE B7,LCTL,$OPRS2,4,FLAGS=$OPREF
         OPCODE BA,CS,$OPRS2,4,FLAGS=$OPREF+$OPCCC
         OPCODE BB,CDS,$OPRS2,4,FLAGS=$OPREF+$OPCCC,                   +
               REGS=$OP1EVEN
         OPCODE BD,CLM,$OPRS3,4,FLAGS=$OPREF+$OPCCC
         OPCODE BE,STCM,$OPRS3,4,FLAGS=$OPREF
         OPCODE BF,ICM,$OPRS3,4,FLAGS=$OPREF+$OPCCA
         OPCODE D1,MVN,$OPSS1,6,FLAGS=$OPREF
         OPCODE D2,MVC,$OPSS1,6,FLAGS=$OPREF
         OPCODE D3,MVZ,$OPSS1,6,FLAGS=$OPREF
         OPCODE D4,NC,$OPSS1,6,FLAGS=$OPREF+$OPCCL
         OPCODE D5,CLC,$OPSS1,6,FLAGS=$OPREF+$OPCCC
         OPCODE D6,OC,$OPSS1,6,FLAGS=$OPREF+$OPCCL
         OPCODE D7,XC,$OPSS1,6,FLAGS=$OPREF+$OPCCL
         OPCODE D9,MVCK,$OPSS3,6,FLAGS=$OPCCA
         OPCODE DA,MVCP,$OPSS3,6,FLAGS=$OPCCA
         OPCODE DB,MVCS,$OPSS3,6,FLAGS=$OPCCA
         OPCODE DC,TR,$OPSS1,6,FLAGS=$OPREF
         OPCODE DD,TRT,$OPSS1,6,FLAGS=$OPREF+$OPCCA
         OPCODE DE,ED,$OPSS1,6,FLAGS=$OPREF+$OPCCA
         OPCODE DF,EDMK,$OPSS1,6,FLAGS=$OPREF+$OPCCA
         OPCODE E8,MVCIN,$OPSS1,6,FLAGS=$OPREF
         OPCODE F0,SRP,$OPSS4,6,FLAGS=$OPREF+$OPCCA
         OPCODE F1,MVO,$OPSS2,6,FLAGS=$OPREF
         OPCODE F2,PACK,$OPSS2,6,FLAGS=$OPREF
         OPCODE F3,UNPK,$OPSS2,6,FLAGS=$OPREF
         OPCODE F8,ZAP,$OPSS2,6,FLAGS=$OPREF+$OPCCA
         OPCODE F9,CP,$OPSS2,6,FLAGS=$OPREF+$OPCCC
         OPCODE FA,AP,$OPSS2,6,FLAGS=$OPREF+$OPCCA
         OPCODE FB,SP,$OPSS2,6,FLAGS=$OPREF+$OPCCA
         OPCODE FC,MP,$OPSS2,6,FLAGS=$OPREF
         OPCODE FD,DP,$OPSS2,6,FLAGS=$OPREF
* ------------------------------------------------------------------- *
*                                                                     *
*        INDEX TO OPCODE TABLE                                        *
*                                                                     *
* ------------------------------------------------------------------- *
         ORG    DISASMOP+0
OPINDEX  DS     0A
         OPCODE TYPE=INDEX
         COPY   DISASMDA
         END    DISASMOP
./ ADD NAME=DISASMPR
         TITLE 'DISASMPR PRINTER MODULE'
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: DISASMPR - Printing module                            *
*                                                                     *
*  Function:                                                          *
*   DISASM was written in multiple CSECTs to keep any one module from *
*   being excessively large and avoid some base register concerns due *
*   to size, and to functionally divide up the over-all logic.  Since *
*   printing was to be necessary from many modules, it seemed best to *
*   have a separate print module.  All printing except DISDEBUG is    *
*   done here.                                                        *
*                                                                     *
*   The interface block is PRTBLOK.                                   *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DISASMGB
DISASMPR CSECT
DISASMPR AMODE 31
DISASMPR RMODE 24
         USING DISASMPR,R12
         USING DISASM00,R11
         USING PRTBLOK,R10           DEFINE PARAMETER BLOCK BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     PRT0000               SKIP EYECATCHER
         DC    CL8'DISASMPR'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
PRT0000  DS    0H
         LA    R15,PRTSAVE           OUR SAVE AREA ADDRESS
         ST    R13,4(R15)            CHAIN CALLER'S SAVE AREA TO OURS
         ST    R15,8(R13)            CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R15               SET SAVE AREA ADDRESS
         LR    R10,R1                COPY PARM BLOCK ADDRESS
         ITRACE ID=ENTRY,            TRACE PRINT MODULE ENTRY          +
               DATA1=PRTCMD          .. TRACE COMMAND
         CLI   PRTCMD,$PRTCLS        CLOSE FILES?
         BE    PRT0300               YES
         TM    PRTFLAG,$PRTOPEN      IS PRINT FILE OPEN?
         BO    PRT0010               YES
         ITRACE ID=PRTOPEN           TRACE PRINT DCB OPENING
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (DISPRINT,OUTPUT)     OPEN DISPRINT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         OI    PRTFLAG,$PRTOPEN      SET 'OPEN' FLAG
PRT0010  DS    0H
         CLI   PRTCMD,$PRTHEAD       PRINT HEADING?
         BE    PRT0100               YES
         CLI   PRTCMD,$PRTSUBH       PRINT SUB-HEADING?
         BE    PRT0110               YES
         CLI   PRTCMD,$PRTPRT        PRINT DATA?
         BE    PRT0020               YES
         ABEND 1,DUMP,,USER          ABEND
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0020  DS    0H
         ZAP   LINEWORK,P1           SET WORK TO 1
         CLI   PRTCC,C' '            SINGLE SPACE?
         BE    PRT0030               YES
         AP    LINEWORK,P1           ADD 1 TO WORK
         CLI   PRTCC,C'0'            DOUBLE SPACE
         BE    PRT0030               YES
         AP    LINEWORK,P1           ADD 1 TO WORK
PRT0030  DS    0H
         AP    LINEWORK,LINECT       NEW LINE COUNT
         CP    LINEWORK,COMMMAXL     WILL PAGE OVERFLOW?
         BNH   PRT0040               NO
         ITRACE ID=PAGEFULL          PAGE IS FULL
         BAL   R9,PRT0200            PRINT HEADING
PRT0040  DS    0H
         ITRACE ID=PRINT             PRINTING DATA
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISPRINT,PRTCC        PRINT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         AP    LINECT,P1             ADD 1 TO LINE COUNT
         CLI   PRTCC,C' '            SINGLE SPACE?
         BE    PRT0050               YES
         AP    LINECT,P1             ADD 1 TO LINE COUNT
         CLI   PRTCC,C'0'            DOUBLE SPACE
         BE    PRT0050               YES
         AP    LINECT,P1             ADD 1 TO LINE COUNT
PRT0050  DS    0H
         MVI   PRTDATA,C' '          INTIALIZE PRINT
         MVC   PRTDATA+1(L'PRTDATA-1),PRTDATA
         B     PRT9900               EXIT
* ------------------------------------------------------------------- *
*             FORCED HEADINGS                                         *
* ------------------------------------------------------------------- *
PRT0100  DS    0H
         ITRACE ID=FORCEDHD          FORCED HEADING
         BAL   R9,PRT0200            PRINT HEADING
         B     PRT9900               AND EXIT
* ------------------------------------------------------------------- *
*             FORCED SUB-HEADINGS                                     *
* ------------------------------------------------------------------- *
PRT0110  DS    0H
         ITRACE ID=FORCESHD          FORCED SUB-HEADING
         BAL   R9,PRT0210            PRINT HEADING
         B     PRT9900               AND EXIT
* ------------------------------------------------------------------- *
*             PRINT HEADING                                           *
* ------------------------------------------------------------------- *
PRT0200  DS    0H
         ITRACE ID=PRTHEAD           PRINTING HEADING
         AP    PAGECT,P1             ADD 1 TO PAGE COUNT
         MVC   HEADPAGE,PAGEEDWD     SET EDIT WORD
         ED    HEADPAGE,PAGECT       EDIT PAGE NUMBER
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISPRINT,HEADING      WRITE HEADING
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         ZAP   LINECT,P1             SET LINE COUNT TO 1
PRT0210  DS    0H
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,1,COMMSUBL+1       SUBHEADING LENGTH
         BZR   R9                    NO SUB-HEADING
         CLI   COMMSUBL,X'FF'        NON-CENTERED HEADING?
         BE    PRT0220               YES
         LA    R2,L'SUBHWORK         WORK AREA SIZE
         SR    R2,R1                 MINUS SUBHEADING LENGTH
         SRL   R2,1                  DIVIDED BY 2
         MVI   SUBHWORK,C'-'         INITIALIZE WITH HYPHEN
         MVC   SUBHWORK+1(L'SUBHWORK-1),SUBHWORK
         LA    R2,SUBHWORK(R2)       ADDRESS FOR CENTERED SUB-HEADING
         BCTR  R1,0                  FOR EXECUTE
         EX    R1,SUBHMVC1           MOVE SUB-HEADING
         B     PRT0230
PRT0220  DS    0H
         BCTR  R1,0                  MINUS 1
         EX    R1,SUBHMVC2           MOVE NON-CENTERED
PRT0230  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISPRINT,SUBHCC       PRINT SUBHEADING
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         AP    LINECT,P1             ADD 1 TO LINE COUNT
         MVI   SUBHWORK,C' '         CLEAR WORK AREA
         MVC   SUBHWORK+1(L'SUBHWORK-1),SUBHWORK
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISPRINT,SUBHCC       PRINT BLANK LINE
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         AP    LINECT,P1             ADD 1 TO LINE COUNT
         BR    R9
* ------------------------------------------------------------------- *
*             CLOSE PRINTER                                           *
* ------------------------------------------------------------------- *
PRT0300  DS    0H
         ITRACE ID=PRTCLOSE          CLOSING PRINT DCB
         TM    PRTFLAG,$PRTOPEN      PRINTER OPEN?
         BNO   PRT9900               NO.. EXIT
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         CLOSE DISPRINT              CLOSE PRINTER
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         NI    PRTFLAG,255-$PRTOPEN  TURN OPEN FLAG OFF
* ------------------------------------------------------------------- *
*             EXIT                                                    *
* ------------------------------------------------------------------- *
PRT9900  DS    0H
         ITRACE ID=EXIT              EXITING PRINT MODULE
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
SUBHMVC1 MVC   0(0,R2),COMMSUBH      COPY SUBHEADING (CENTERED)
SUBHMVC2 MVC   SUBHWORK(0),COMMSUBH  COPY SUBHEADING (NON-CENTERED)
* ------------------------------------------------------------------ *
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
* ------------------------------------------------------------------ *
PRTSAVE  DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
PRTFLAG  DC    X'00'
$PRTOPEN EQU   X'80'
P1       DC    P'1'                  CONSTANT
LINECT   DC    PL3'0'                LINE COUNT
LINEWORK DC    PL3'0'                LINES ADDED BY THIS I/O
PAGECT   DC    PL3'0'                PAGE COUNT
PAGEEDWD DC    X'402020202120'       CONSTANT
HEADING  DS    0C
         DC    CL01'1'
         DC    CL53' '
         DC    CL15'DISASSEMBLER'
         DC    CL54' '
         DC    CL04'PAGE'
HEADPAGE DC    CL06' '
SUBHCC   DC    C'0'
SUBHWORK DC    CL133' '
DISPRINT DCB   DSORG=PS,                     PRINTER DCB               +
               RECFM=FBA,                    .. RECORD FORMAT          +
               LRECL=L'PRTCC+L'PRTDATA,      .. RECORD LENGTH          +
               DDNAME=DISPRINT,              .. DD NAME                +
               MACRF=PM                      .. MACRO FORMAT
         LTORG
* ------------------------------------------------------------------ *
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
* ------------------------------------------------------------------ *
PRTBLOK  PRTBLOK  TYPE=DSECT
         SPACE 2
* ------------------------------------------------------------------ *
*                                                                    *
*              COMMON DATA MAP                                       *
*                                                                    *
* ------------------------------------------------------------------ *
DISASM00 DISASM00 TYPE=DSECT
* ------------------------------------------------------------------ *
*                                                                    *
*              EQUATES                                               *
*                                                                    *
* ------------------------------------------------------------------ *
         COPY REGEQU
         END  DISASMPR
./ ADD NAME=DISASMRR
*---------------------------------------------------------------------*
*                                                                     *
*  Module name: DISASMRR - Verify operands for RR format instructions *
*                                                                     *
*  Function:                                                          *
*   Some instructions require that the operand(s) be even-odd pairs   *
*   of registers.  This code verifies the operands for most 'RR'      *
*   format instructions.                                              *
*                                                                     *
*   At entry R8 will be address of the current OPCODE table entry.    *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DISASMGB
DISASMRR CSECT
DISASMRR AMODE 31
DISASMRR RMODE 24
         USING DISASMRR,R12
         USING OPDSECT,R8            DEFINE OPCODE DSECT BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     VER0000               SKIP EYECATCHER
         DC    CL8'DISASMRR'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
VER0000  DS    0H
         TM    OPREGS,X'F0'          RESTRICTIONS ON OPERAND 1?
         BZ    VER0100               NO
         TM    1(R5),X'10'           OPERAND 1 AN ODD NUMBER?
         BO    BAD0000               YES.. BAD
         TM    OPREGS,$OP1EVEN       JUST INTERESTED IN AN EVEN NUMBER?
         BO    VER0100               OPERAND 1 IS OK, CHECK OPERAND 2
         TM    1(R5),X'80'           REGISTER NUMBER 8 OR GREATER?
         BO    BAD0000               YES.. CANNOT BE VALID
         TM    OPREGS,$OP10246       NEED 0, 2, 4, OR 6?
         BO    VER0100               OPERAND 1 IS OK, CHECK OPERAND 2
         TM    1(R5),X'20'           REGISTER 2 OR 6?
         BO    BAD0000               YES.. NOT VALID
VER0100  DS    0H
         TM    OPREGS,X'0F'          RESTRICTIONS ON OPERAND 2?
         BZ    GOOD0000              NO.. ALL OPERANDS ARE OK
         TM    1(R5),X'01'           OPERAND 2 AN ODD NUMBER?
         BO    BAD0000               YES.. BAD
         TM    OPREGS,$OP2EVEN       JUST INTERESTED IN AN EVEN NUMBER?
         BO    GOOD0000              ALL OPERANDS ARE OK
         TM    1(R5),X'08'           REGISTER NUMBER 8 OR GREATER?
         BO    BAD0000               YES.. CANNOT BE VALID
         TM    OPREGS,$OP20246       NEED 0, 2, 4, OR 6?
         BO    GOOD0000              ALL OPERANDS ARE OK
         TM    1(R5),X'02'           REGISTER 2 OR 6?
         BO    BAD0000               YES.. NOT VALID
GOOD0000 DS    0H
         SR    R15,R15               OPERAND(S) ARE OK
         B     EXIT0000              EXIT
BAD0000  DS    0H
         LA    R15,8                 OPERAND(S) ARE NOT OK
* ------------------------------------------------------------------- *
*             EXIT                                                    *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         L     R14,12(R13)           RESTORE R14                        ASE01680
         LM    R0,R12,20(R13)        RESTORE OTHER REGISTERS            ASE01680
         BR    R14                   RETURN TO CALLER                   ASE01700
* ------------------------------------------------------------------ *
*                                                                    *
*              EQUATES                                               *
*                                                                    *
* ------------------------------------------------------------------ *
         COPY DISASMDA
         COPY REGEQU
         END  DISASMRR
./ ADD NAME=DISASMTS
1   I read the article on the disassembler by Alan Field in the
 February issue of Technical Support.  I have used the disassembler
 mentioned in the article (from file 217 of the CBT).  There are
 several things about this disassembler that I thought could be improved.
 For example, I wanted the disassembler listing to look as much like the
 original assembler list as possible.  The disassembler did allow DSECTs
 to be defined, but you had to code statements to do this yourself.
 Some parameters were entered via the "PARM" on the execute statement,
 others by SYSIN type statements.  There were some fixed size tables
 that could fill up.
0   I wrote a new disassembler, borrowing from the one on the CBT.  The
 new program produces a listing very similar to assembler output.  DSECTs
 are defined by using the actual dsect source or macro.  There are no
 hardcoded fixed size tables that may fill up.  All control parameters
 are entered via "SYSIN" type statements.  I admitted up front that there
 would be problems that would be difficult to trouble shoot, so the
 program has a built in internal trace table.  There turned out to be
 so many internal control blocks that following the chains in a dump
 was very time consuming, so I wrote a "debug" program to run the chains
 and format the data at specific points.
0   Whereas the "old" disassembler consisted of 3 modules, mine has 15.
 The individual CSECTs are:
             DISASM00 - common data and trace table code
             DISASM01 - mainline
             DISASM02 - parameter reader/converter
             DISASM03 - object code reader
             DISASM04 - ESD data processor
             DISASM05 - RLD data processor
             DISASM06 - text printer
             DISASM07 - assembler interface/dsect interpreter
             DISASM08 - reference table builder
             DISASM09 - source code generator
             DISASMB2 - interprets "B2xx" opcodes
             DISASMDB - debug
             DISASMOP - opcode table
             DISASMPR - printer
             DISASMRR - verifies register operands
 Altogether the code comes to about 38K.
0   As in the "old" disassembler, extended mnemonics are used if possible
 for branch instructions.  SVC's are interpreted (the macro is not re-
 coded).  Control statements allow the definition of areas that do not
 contain instructions.  If the DISPUNCH DD is allocated, an 80-byte
 card type source deck will be produced that may be used as input to the
 assembler.
0   Labels generated for the disassembled CSECT have a form PPPPNNNN.
 If the label precedes an instruction, the PPPP is a 1 to 4 character
 user supplied prefix.  If the label preceeds a data area, PPPP is the
 literal "DATA".  NNNN is either the displacement into the CSECT where
 the label appears or is a sequentially assigned number (0010, 0020,
 etc).  Sequentially numbered labels may be more desirable if the source
 is to be modified because the label may no longer be at the same
 displacement.
0   If the disassembler can determine a label, it will use it in the
 generated source.  Instead of a MVC instruction always looking like:
 "MVC   324(18,R2),21(R3)", it might look like "MVC   FIELDB(18),FIELDA".
0   ENTRY points will be reproduced from the ESD data.  RLD items will
 cause an ADCON, VCON, Q, or CXD at the point the RLD item exists.
 ADCONs will cause a label to be generated at the point referenced by
 the ADCON.  VCONs and Q type items will have the name inserted:
 "DC  V(nnnnn)" or "Q(nnnnn)".
0   I tried to make the program as self documenting and as uncluttered
 as possible.  By breaking it up into multiple CSECTs functions could
 be isolated.  This kept base register problems to a minimum and allowed
 some isolation of testing individual components.
-                       PROGRAM DESCRIPTION
   STEP   MODULE
     1   DISASM01  Acquire trace table storage and initialize the trace
                   control data in DISASM00.
     2   DISASM01  Scan the TIOT to determine which DD's are present.
     3   DISASM01  Verify that all required DD's were present.
     4   DISASM01  Call the parameter reader, DISASM02.
     5   DISASM02  Read all control statements.  Each statement is
                   printed on the DISPRINT listing.  All parameters
                   set fields or flags in the common module, DISASM00.
     6   DISASM02  When end-of-file is reached on DISIN, verify that all
                   required parameters were entered.
     7   DISASM01  If there were errors detected by DISASM02, an error
                   message is issued and the program terminates.
     8   DISASM01  Acquire storage for object module I/O area.
     9   DISASM01  Call the object module reader, DISASM03.
    10   DISASM03  Issue a BLDL for the specified module.
    11   DISASM03  If the BLDL was unsuccessful, interpret the return
                     A) interpret the BLDL return code
                     B) print an error message
                     C) set the abort flag
                     D) return to mainline
    12   DISASM03  Interpret data from the directory data (text's TTR,
                       entry point, module's length, etc).
    13   DISASM03  Issue a POINT to prepare for reading the module.
    14   DISASM03  If the POINT was unsuccessful
                     A) interpret the POINT return code
                     B) print an error message
                     C) set the abort flag
                     D) return to mainline
    15   DISASM03  Read records from the object module:
    16   DISASM03  If the record contains
                     A) CESD records, process them internally.  If this
                        is the requested CSECT's data, copy the data
                        into the storage acquired by DISASM04.
                     B) ESD data, call DISASM04 to process.
         DISASM04       DISASM04 interprets and prints the ESD data.
                        ESD info is saved in "ESDDATA" blocks chained
                        from the common data module, DISASM00.  If the
                        requested CSECT is found, acquire storage for
                        text, and set the "CSECT found" flag.
                     C) RLD data, call DISASM05 to process.
         DISASM05       DISASM05 interprets and prints RLD data.  RLD
                        info is saved in "RLDDATA" blocks chained from
                        the common data module, DISASM00.  If an RLD
                        item overlaps an area defined by a DATA
                        statement, the DATA item's beginning and/or
                        ending displacement will be adjusted.  A new
                        DATA block will be created if necessary.
                        RLD items are checked to see if they reference
                        ESD items (like VCONs).  If an RLD item does
                        reference an ESD entry, the ESD item is linked
                        to the RLD item so the name can be generated.
    17   DISASM01  If there were errors detected by DISASM03, DISASM04,
                   or DISASM05, an error message is issued and the
                   program terminates.
    18   DISASM01  Call the object text printer DISASM06.
    19   DISASM06  Prints the object module in dump type format.
    20   DISASM01  Call the assembler interface and dsect interpreter,
                   DISASM07.
    21   DISASM07  If no assembler input was specified in the DISIN
                   parameters, issue a message and return to mainline.
    22   DISASM07  LOAD the assembler, IEV90.
    23   DISASM07  Link to the assembler.
    24   DISASM07  Print the assembler return code.
    25   DISASM07  Read the assembler listing -
                      A) when a DSECT is detected, add a new DSCTBLOK
                         to the chain (CSECT's are treated the same
                         as DSECT's).
                      B) when a label is detected add it to the label
                         chain from the current DSECT.
    26   DISASM08  Chain the USING blocks to the DSECT blocks they
                   refer to.
    27   DISASM08  Scan the object code and determine the displacements
                   where valid instructions occur. Generate DATA blocks
                   for any areas that do not contain instructions.
    28   DISASM08  Verify that all BASE and USINGs reference instruction
                   boundaries.
    29   DISASM08  Generate LABELs for ENTRY points in the requested
                   CSECT.
    30   DISASM08  Generate LABELs for ADCON references.
    31   DISASM08  Generate the reference table.  If the disassembler
                   can determine a label in the CSECT or a DSECT,
                   the "reference" will be the address of the LABEL
                   block and the displacement from that label.
    32   DISASM09  Source code generation














./ ADD NAME=DISASMU1
         TITLE 'DISASMU1 - PREPROCESS DSECTS'
         COPY  DISASMGB
&DAPRT   SETC  'ON'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASMU1                                             *
*                                                                    *
*  Function:                                                         *
*                                                                    *
*   The disassembler can use actual assembler source for label       *
*   mapping.  The source can be supplied to the disassembler         *
*   during a disassembly run or pre-processed by this utility.       *
*                                                                    *
*   The assembler output is scanned to obtain DSECT names, label     *
*   names, and the displacements to the labels.  In order for the    *
*   utility to find names and displacements, the PRINT option        *
*   must be ON.                                                      *
*                                                                    *
*   SYSUT1   SYSPRINT file from the assembler                        *
*   SYSPRINT Messages generated by this utility                      *
*   SYSUT2   Output.  This is the DSECT information that can be      *
*            supplied to the disassembler via the DISDSECT DD.       *
*                                                                    *
*                                                                    *
*   This utility dynamically allocates the DD's for SYSUT2.          *
*   The data set name for the DSECT information output must          *
*   be specified in the PARM on the JCL EXEC statement.              *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
DISASMU1 CSECT
DISASMU1 AMODE 24
DISASMU1 RMODE 24
         USING DISASMU1,R12
         USING DSCTDSCT,R10
         USING LABLDSCT,R9
         USING EQUDATA,R8
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     UTIL0000              SKIP EYECATCHER
         DC    CL8'DISASMU1'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
UTIL0000 DS    0H
         LA    R15,WK_SAVEAREA
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         L     R2,0(R1)              PARM ADDRESS
         SR    R3,R3                 CLEAR REGISTER
         ICM   R3,3,0(R2)            LENGTH
         BCTR  R3,0                  MINUS 1
         EX    R3,DSNMVC             MOVE DATA SET NAME
         OPEN  (SYSUT1,INPUT)        OPEN SYSUT1 FOR INPUT
         OPEN  (SYSPRINT,OUTPUT)     OPEN SYSPRINT FOR OUTPUT
         PUT   SYSPRINT,MSG00
         SR    R10,R10               NO DSECT FOUND SO FAR
         SR    R9,R9                 NO LABEL FOUND SO FAR
UTIL0010 DS    0H
         GET   SYSUT1,ASM_DATA       READ A SYSUT1 RECORD
         CLI   ASM_LABEL,C'*'        COMMENT STATEMENT?
         BE    UTIL0010              YES
         CLC   ASM_DATA+43,SRCSTMT   ASSEMBLER HEADING LINE?
         BE    UTIL0010              YES, IGNORE IT
         CLC   XREF,ASM_DATA+45      START OF CROSS REFERENCE?
         BNE   UTIL0020              NO
         OI    WK_FLAGS,$XREF        SET CROSS REFERENCE FLAG
UTIL0020 DS    0H
         TM    WK_FLAGS,$XREF        CROSS REFERENCE FOUND?
         BO    UTIL0010              YES
         CLI   ASM_LABEL,C' '        LABEL PRESENT?
         BE    UTIL0010              NO
         LA    R6,ASM_LABEL          FIRST BYTE OF LABEL
         LA    R2,WK_LABEL           FIRST BYTE OF WORK LABEL
         MVC   WK_LABEL,BLANKS       CLEAR LABEL NAME
         LA    R1,8                  MAX LOOPS
UTIL0030 DS    0H
         CLI   0(R6),C' '            BLANK?
         BE    UTIL0040              YES
         MVC   0(1,R2),0(R6)         COPY TO WORK LABEL
         LA    R2,1(R2)              NEXT
         LA    R6,1(R6)              NEXT
         BCT   R1,UTIL0030           LOOP
         CLI   0(R6),C' '            BLANK?
         BE    UTIL0040              NO... TOO LONG FOR A LABEL
         B     UTIL0010              READ NEXT SYSUT1 RECORD
UTIL0040 DS    0H
         LA    R1,8                  MAX LOOPS
UTIL0050 DS    0H
         CLI   0(R6),C' '            BLANK?
         BNE   UTIL0060              NO
         LA    R6,1(R6)              NEXT
         BCT   R1,UTIL0050           LOOP
         B     UTIL0110              ASSUME IT IS A LABEL
UTIL0060 DS    0H
         CLC   OP_DSECT(6),0(R6)     DSECT?
         BE    UTIL0070              YES
         CLC   OP_CSECT,0(R6)        CSECT?  (TREATED LIKE DSECTS)
         BE    UTIL0070              YES
         CLC   OP_EQU,0(R6)          EQUATE STATEMENT?
         BNE   UTIL0110              NO
         LTR   R9,R9                 LABEL FOUND YET?
         BZ    UTIL0010              NO
         CLC   =C'000',ASM_ADDR      FIRST 3 DIGITS ZEROS?
         BNE   UTIL0010              NO
         GETMAIN RU,                 LENGTH                            +
               LV=EQUL,                                                +
               LOC=BELOW
         LR    R8,R1                 COPY EQU BLOCK ADDRESS
         XC    EQUDATA(EQUL),EQUDATA INITIALIZE THE EQUATE BLOCK
         MVC   EQUEYE,EQUID          IDENTIFY THIS BLOCK
         MVC   EQUNEXT,LABLEQU       SET CHAIN ADDRESS
         ST    R8,LABLEQU            SET NEW BLOCK'S ADDRESS IN LABEL
         MVC   EQULABEL,WK_LABEL     SET LABEL
         NC    ASM_ADDR+3(2),X1F1F   PREPARE FOR PACKING
         TR    ASM_ADDR+3(2),CHXH    TRANSLATE FOR PACKING
         PACK  WK_DISP_OUT(2),ASM_ADDR+3(3)
         MVC   EQUVALUE,WK_DISP_OUT  COPY EQUATE VALUE
         B     UTIL0010
UTIL0070 DS    0H
         LA    R2,WK_DSECT_CHAIN     DSECT ANCHOR
         ICM   R10,15,WK_DSECT_CHAIN FIRST DSECT BLOCK
         BZ    UTIL0090              NO DSECTS YET
UTIL0080 DS    0H
         CLC   DSCTNAME,WK_LABEL     ALREADY ON DSECT CHAIN?
         BE    UTIL0100              YES.. EXIT WITH BASE SET
         LR    R2,R10                COPY ADDRESS
         ICM   R10,15,DSCTNEXT       NEXT DSECT BLOCK
         BNZ   UTIL0080              LOOP
UTIL0090 DS    0H
         GETMAIN RU,                 ACQUIRE NEW DSECT BLOCK           +
               LV=DSCTL,                                               +
               LOC=BELOW
         ST    R1,DSCTNEXT-DSCTDSCT(R2)   CHAIN NEW TO PREVIOUS BLOCK
         LR    R10,R1                SET BASE
         MVC   DSCTEYE,OP_DSECT      SET BLOCK ID
         XC    DSCTNEXT,DSCTNEXT     ZERO 'NEXT' BLOCK ADDRESS
         MVC   DSCTNAME,WK_LABEL     SET DSECT'S NAME
         XC    DSCTLBA,DSCTLBA       CLEAR LABEL POINTER
         SR    R9,R9                 CLEAR LABEL BLOCK ADDRESS
         B     UTIL0010
UTIL0100 DS    0H
         B     UTIL0010
UTIL0110 DS    0H
         LTR   R10,R10               DSECT DETERMINED YET?
         BZ    UTIL0010              NO
         CLI   ASM_DISP,C'A'         VALID DISPLACEMENT?
         BL    UTIL0010              NO
         CLI   ASM_DISP,C'F'         VALID DISPLACEMENT?
         BNH   UTIL0120              YES
         CLI   ASM_DISP,C'0'         VALID DISPLACEMENT?
         BL    UTIL0010              NO
         CLI   ASM_DISP,C'9'         VALID DISPLACEMENT?
         BH    UTIL0010              NO
UTIL0120 DS    0H
         MVC   WK_DISP_IN,ASM_DISP   COPY DISPLACEMENT
         NC    WK_DISP_IN,X1F1F      PREPARE FOR TRANSLATE
         TR    WK_DISP_IN,CHXH       TRANSLATE FOR PACKING
         PACK  WK_DISP_OUT(4),WK_DISP_IN(7) PACK DISPLACEMENT
         LA    R2,DSCTLBA            LABEL CHAIN ANCHOR
         ICM   R9,15,DSCTLBA         FIRST LABEL
         BZ    UTIL0140              NO LABELS
UTIL0130 DS    0H
         CLC   WK_DISP_OUT(3),LABLDISP+1 INSERT HERE?
         BH    UTIL0140              YES
         LR    R2,R9                 COPY ADDRESS
         ICM   R9,15,LABLNEXT        NEXT LABEL
         BNZ   UTIL0130              LOOP
UTIL0140 DS    0H
         GETMAIN RU,                 ACQUIRE NEW LABEL BLOCK           +
               LV=LABLL,                                               +
               LOC=BELOW
         ST    R1,LABLNEXT-LABLDSCT(R2)  CHAIN PREVIOUS BLOCK TO NEW
         ST    R9,LABLNEXT-LABLDSCT(R1)  CHAIN NEXT BLOCK TO NEW
         LR    R9,R1                 SET BASE
         MVC   LABLEYE,LABEL         SET BLOCK IDENTIFIER
         MVC   LABLNAME,WK_LABEL     SET LABEL NAME
         MVI   LABLDISP,X'00'        FORCE FIRST BYTE TO ZERO
         MVC   LABLDISP+1(3),WK_DISP_OUT SET DISPLACEMENT TO LABEL
         XC    LABLEQU,LABLEQU       INITIALIZE EQUATE BLOCK ADDRESS
         MVI   LABLTYPE,$LABLD       DATA TYPE LABEL
         B     UTIL0010
*---------------------------------------------------------------------*
*                                                                     *
*    EOF on the assembler output has been reached.                    *
*                                                                     *
*    Print the info we have gathered and write it to the              *
*    pre-processed DSECT info file.  One member per DSECT.            *
*                                                                     *
*---------------------------------------------------------------------*
EOF0000  DS    0H
         ICM   R10,15,WK_DSECT_CHAIN ANY DSECTS?
         BZ    EOF0070               NO
         B     EOF0020
EOF0010  DS    0H
         BAL   R11,PRT0000           PRINT A BLANK LINE
EOF0020  DS    0H
         MVC   MSG01_NAME,DSCTNAME   COPY DSECT NAME
         MVC   PR_DATA(MSG01L),MSG01
         BAL   R11,PRT0000
         MVC   WK_MEMBER_NAME,DSCTNAME   COPY TO MEMBER NAME
         BAL   R11,DAIR0000          ALLOCATE THE MEMBER
         OPEN  (SYSUT2,OUTPUT)       OPEN MEMBER IN SYSUT2
         OI    WK_FLAGS,$OPEN        SYSUT2 DCB IS OPEN
         MVC   WK_IO_DATA(DSCTL),DSCTDSCT
         LA    R1,DSCTL+4
         STCM  R1,3,WK_IO_LENGTH
         PUT   SYSUT2,WK_IO
         ICM   R9,15,DSCTLBA         FIRST LABEL BLOCK
         BZ    EOF0050
EOF0030  DS    0H
         MVC   MSG02_NAME,LABLNAME   COPY LABEL NAME
         UNPK  MSG02_DISP(9),LABLDISP(5)
         TR    MSG02_DISP,HEXCHAR
         MVI   MSG02_DISP+8,C' '
         MVC   MSG02_TYPE,LABLTYPE   COPY LABEL TYPE
         MVC   PR_DATA(MSG02L),MSG02
         BAL   R11,PRT0000           PRINT
         MVC   WK_IO_DATA(LABLL),LABLDSCT
         LA    R1,LABLL+4
         STCM  R1,3,WK_IO_LENGTH
         PUT   SYSUT2,WK_IO
         ICM   R8,15,LABLEQU         FIRST EQU BLOCK
         BZ    EOF0050
EOF0040  DS    0H
         MVC   MSG03_NAME,EQULABEL   COPY LABEL
         MVC   WK_UNPACK(1),EQUVALUE
         UNPK  MSG03_VALUE(3),WK_UNPACK(2)
         TR    MSG03_VALUE,HEXCHAR
         MVI   MSG03_VALUE+2,C' '
         MVC   PR_DATA(MSG03L),MSG03
         BAL   R11,PRT0000
         MVC   WK_IO_DATA(EQUL),EQUDATA
         LA    R1,EQUL+4
         STCM  R1,3,WK_IO_LENGTH
         PUT   SYSUT2,WK_IO
         ICM   R8,15,EQUNEXT
         BNZ   EOF0040
EOF0050  DS    0H
         ICM   R9,15,LABLNEXT
         BNZ   EOF0030
EOF0060  DS    0H
         CLOSE (SYSUT2)
         ICM   R10,15,DSCTNEXT
         BNZ   EOF0010
         B     EXIT0000              AND EXIT
EOF0070  DS    0H
         MVC   PR_DATA(MSG09L),MSG09
         BAL   R11,PRT0000
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DAIR0000 DS    0H
         L     R1,WK_DD_NUMBER
         LA    R1,1(R1)
         ST    R1,WK_DD_NUMBER
         UNPK  WK_UNPACK(9),WK_DD_NUMBER(5)
         TR    WK_UNPACK,HEXCHAR
         MVC   WK_DD_NAME,WK_UNPACK
         MVI   WK_DD_NAME,C'D'
         LA    R2,SYSUT2
         USING IHADCB,R2
         MVC   DCBDDNAM,WK_DD_NAME
         DROP  R2
         LA    R2,WK_RB
         USING S99RB,R2
         MVI   S99VERB,S99VRBAL      SET VERB
         LA    R15,WK_TEXT_ADDR      TEXT UNIT LIST ADDRESS
         ST    R15,S99TXTPP          SET TEXT ADDRESS
         MVI   S99RBLN,S99RBEND-S99RB
         LA    R1,WK_RB_ADDRESS      REQUEST BLOCK POINTER'S ADDRESS
         SVC   99                    ISSUE SVC
         LTR   R15,R15               SUCCESSFUL?
         BZR   R11                   YES
         DC    H'0'
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
PRT0000  DS    0H
         CP    WK_LINE,P_65          TIME FOR HEADING?
         BL    PRT0010               NO
         PUT   SYSPRINT,MSG00
         ZAP   WK_LINE,P_1
PRT0010  DS    0H
         PUT   SYSPRINT,PR_CC
         MVC   PR_DATA,PR_CC
         AP    WK_LINE,P_1
         BR    R11
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         CLOSE SYSUT1
         CLOSE SYSPRINT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DSNMVC   MVC   WK_DS_NAME(0),2(R2)
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
WK_SAVEAREA    DC    18F'0'         REGISTER SAVE AREA
WK_DSECT_CHAIN DC    A(0)
WK_LABEL       DC    CL8' '
WK_FLAGS       DC    X'00'
$XREF          EQU   X'80'          .. CROSS REFERENCE HAS BEEN FOUND
$OPEN          EQU   X'80'          .. SYSUT2 DCB IS OPEN
WK_DD_NUMBER   DC    F'0'
WK_UNPACK      DC    CL16' '
WK_RB_ADDRESS  DC    A(WK_RB+X'80000000')
WK_RB          DS    (S99RBEND-S99RB)X
WK_TEXT_ADDR   DC    A(WK_DD_TEXT)
               DC    A(WK_DS_TEXT)
               DC    A(WK_MEMBER_TEXT)
               DC    A(WK_DISP_TEXT)
               DC    A(WK_FREE_TEXT+X'80000000')

WK_DD_TEXT     DC    AL2(DALDDNAM)         DDNAME PARMS
               DC    AL2(1)
               DC    AL2(8)
WK_DD_NAME     DC    CL8' '                DDNAME

WK_DS_TEXT     DC    AL2(DALDSNAM)         DATASET NAME PARMS
               DC    AL2(1)
               DC    AL2(44)
WK_DS_NAME     DC    CL44' '               DATASET NAME

WK_MEMBER_TEXT DC    AL2(DALMEMBR)         MEMBER NAME PARMS
               DC    AL2(1)
               DC    AL2(8)
WK_MEMBER_NAME DC    CL8' '                DATASET NAME

WK_DISP_TEXT   DC    AL2(DALSTATS)         INITIAL DISPOSITION PARMS
               DC    AL2(1)
               DC    AL2(1)
               DC    X'08'                 INITIAL DISPOSITION (SHR)

WK_FREE_TEXT   DC    AL2(DALCLOSE)         FREE=CLOSE
               DC    AL2(0)

WK_IO          DS    0X
WK_IO_LENGTH   DC    XL2'00'
               DC    XL2'00'
WK_IO_DATA     DC    100X'00'

XREF           DC    C' CROSS REFERENCE '

HEXCHAR  EQU   *-C'0'
         DC    C'0123456789ABCDEF'

P_1      DC    P'1'
P_65     DC    P'65'
WK_LINE  DC    PL3'0'

X1F1F    DC    8X'1F'
         SPACE 1 0 1 2 3 4 5 6 7 8 9 A B C D E F
CHXH     DC    X'000A0B0C0D0E0F000000000000000000'  00-0F
         DC    X'00010203040506070809000000000000'  10-1F
         SPACE 1
ASMHEAD  DC    CL35'ASSEMBLER OUTPUT'
BLANKS   DC    CL8' '
OP_DSECT DC    CL8'DSECT'
OP_CSECT DC    CL8'CSECT'
OP_EQU   DC    C'EQU '
SRCSTMT  DC    C'SOURCE STATEMENT'
WK_DISP_IN DC  CL7' '
WK_DISP_OUT DC XL4'000000'
LABEL    DC    CL8'LABEL'
EQUID    DC    CL8'EQU'

PR_CC    DC    C' '
PR_DATA  DC    CL120' '

MSG00    DC    CL121'1    DISASMUT1   Preprocessed DSECTs'
MSG00L   EQU   *-MSG00
MSG01          DS    0C
               DC    C'DSECT '
MSG01_NAME     DC    CL8' '
MSG01L         EQU   *-MSG01
MSG02          DS    0C
               DC    C'  LABEL '
MSG02_NAME     DC    CL8' '
               DC    C'  DISP '
MSG02_DISP     DC    CL8' '
               DC    C' '
MSG02_TYPE     DC    C' '
MSG02L         EQU   *-MSG02
MSG03          DS    0C
               DC    C'    EQUATE '
MSG03_NAME     DC    CL8' '
               DC    C'  VALUE '
MSG03_VALUE    DC    CL2' '
               DC    C' '
MSG03L         EQU   *-MSG03
MSG09          DS    0C
               DC    C'**  No DSECTS found'
MSG09L         EQU   *-MSG09
*---------------------------------------------------------------------*
*                                                                     *
*              ASSEMBLER OUTPUT DCB                                   *
*                                                                     *
*---------------------------------------------------------------------*
SYSUT1    DCB  DDNAME=SYSUT1,                                          +
               DSORG=PS,                                               +
               EODAD=EOF0000,                                          +
               MACRF=GM
SYSUT2    DCB  DDNAME=SYSUT2,                                          +
               DSORG=PS,                                               +
               LRECL=LABLL+4,                                          +
               MACRF=PM
SYSPRINT  DCB  DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               RECFM=FBA,                                              +
               LRECL=121,                                              +
               MACRF=PM
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
          AIF   (&ASMG EQ 133).GENASMA
ASM_DATA  DC    CL121' '
ASM_DATAL EQU   *-ASM_DATA
          ORG   ASM_DATA+1
ASM_DISP  DS    CL6
          ORG   ASM_DATA+29
ASM_ADDR  DS    CL5
          ORG   ASM_DATA+41
ASM_LABEL DS    CL8
          ORG   ASM_DATA+121
          AGO   .GENASMB
.GENASMA  ANOP
ASM_DATA  DC    CL133' '
ASM_DATAL EQU   *-ASM_DATA
          ORG   ASM_DATA+1
ASM_DISP  DS    CL6
          ORG   ASM_DATA+23
ASM_ADDR  DS    CL5
          ORG   ASM_DATA+41
ASM_LABEL DS    CL8
          ORG   ASM_DATA+133
.GENASMB  ANOP
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         COPY   DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         DCBD   DSORG=PS
         IEFZB4D0
         IEFZB4D2
         COPY REGEQU
         END  DISASMU1
./ ADD NAME=DISASM00
         TITLE 'DISASM00 - COMMON DATA MODULE'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM00                                             *
*                                                                    *
*  Function:                                                         *
*              Common data module and trace table.                   *
*              At initialization time, the mainline module, DISASM01,*
*              sets R11 to the address of DISASM00.  All other       *
*              modules depend on this address remaining unchanged.   *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM00 DISASM00 TYPE=CSECT
         COPY  REGEQU
         END
./ ADD NAME=DISASM01
         TITLE 'DISASM01 - MAINLINE'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM01                                             *
*                                                                    *
*  Function:                                                         *
*   Mainline module.                                                 *
*                                                                    *
*     STEP                                                           *
*       1. Set R11 to address of DISASM00 (the common module).       *
*       2. Acquire storage for the trace table and initialize the    *
*          control data in DISASM00.                                 *
*       3. Scan the TIOT to determine which DD's are present.        *
*       4. Verify required DD's are present.                         *
*       5. Call the parameter reader module, DISASM02.               *
*       6. Call the debug module, DISASMDB to print internal data.   *
*       7. If errors from DISASM02, print message, go to step 21.    *
*       8. Call the module reader, DISASM03.                         *
*       9. If errors from DISASM03, print message, go to step 21.    *
*      10. Call the text printer module DISASM06.                    *
*      11. Call the debug module, DISASMDB to print internal data.   *
*      12. If errors from DISASM06, print message, go to step 21.    *
*      13. Call assembler interface module, DISASM07 to assemble     *
*          DSECTs and build dsect/dsect label chains.                *
*      14. Call the debug module, DISASMDB to print internal data.   *
*      15. If errors from DISASM07, print message, go to step 21.    *
*      16. Call module DISASM08 to build the internal labels and     *
*          the reference table.                                      *
*      17. Call the debug module, DISASMDB to print internal data.   *
*      18. If errors from DISASM08, print message, go to step 21.    *
*      19. Call the source code generator, DISASM09.                 *
*      20. If errors from DISASM09, print message.                   *
*      21. Free the storage for all internal chains and the trace    *
*          table.                                                    *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB              COPY GLOBAL OPTIONS
DISASM01 CSECT
DISASM01 AMODE 31
DISASM01 RMODE 24
         USING DISASM01,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     MAIN0000              SKIP EYECATCHER
         DC    CL8'DISASM01'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MAIN0000 DS    0H
         L     R11,V00               DISASM00'S ADDRESS
         LA    R1,SAVE01             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
* ------------------------------------------------------------------- *
*         Initialize trace table                                      *
* ------------------------------------------------------------------- *
         AIF   ('&TROPT' EQ 'OFF').NOTR1
         L     R2,TRSIZE             TRACE TABLE'S SIZE
         LA    R2,32(R2)             FOR BOUNDARY ROUNDING
         GETMAIN RU,                 ACQUIRE STORAGE FOR TRACE TABLE   +
               LV=(R2),              .. SIZE                           +
               LOC=ANY               .. ANY AREA
         ST    R1,TRADDR             SAVE TRACE TABLE ADDRESS
         LA    R1,32(R1)             PLUS 32
         SRL   R1,5                  ROUND THE ADDRESS TO...
         SLL   R1,5                  ...NEAREST MULTIPLE OF 32
         ST    R1,TR1ST              SET FIRST TRACE ENTRY ADDRESS
         ST    R1,TRCURR             SET CURRENT TRACE ENTRY ADDRESS
         XC    0(TRENTRYL,R1),0(R1)  INITIALIZE FIRST ENTRY
         A     R1,TRSIZE             PLUS USED PORTION'S SIZE
         SH    R1,COMMH32            MINUS 1 ENTRY
         ST    R1,TRLAST             INITIALIZE LAST ENTRY ADDRESS
         ITRACE ID=INIT,             INITIAL TRACE ENTRY               +
               DATA1=TR1ST,          .. FIRST TRACE TABLE ENTRY ADDR   +
               DATA2=TRLAST          .. LAST TRACE TABLE ENTRY ADDR
.NOTR1   ANOP
* ------------------------------------------------------------------- *
*         Scan TIOT                                                   *
* ------------------------------------------------------------------- *
         USING PSA,R0                DEFINE BASE
         L     R1,PSATNEW            MY TCB'S ADDRESS
         USING TCB,R1                DEFINE TCB BASE
         L     R2,TCBTIO             TIOT ADDRESS
         USING TIOT1,R2              DEFINE BASE
         LA    R3,TIOENTRY           FIRST TIOT ENTRY
         USING TIOENTRY,R3           DEFINE BASE
         SR    R4,R4                 CLEAR FOR LENGTHS
MAIN0010 DS    0H
         ICM   R4,1,TIOELNGH         LENGTH OF THIS ENTRY
         BZ    MAIN0080              END OF TABLE
         CLC   TIOEDDNM,INDD         DISIN DD?
         BE    MAIN0030              YES
         CLC   TIOEDDNM,PRTDD        DISPRINT DD?
         BE    MAIN0040              YES
         CLC   TIOEDDNM,LIBDD        DISMOD DD?
         BE    MAIN0050              YES
         CLC   TIOEDDNM,PUNCHDD      DISPUNCH DD?
         BE    MAIN0060              YES
         CLC   TIOEDDNM,DEBUGDD      DISDEBUG DD?
         BE    MAIN0070              YES
         CLC   TIOEDDNM,DSECTDD      DISDSECT DD?
         BE    MAIN0075              YES
MAIN0020 DS    0H
         AR    R3,R4                 NEXT TIOT ENTRY
         B     MAIN0010              LOOP
MAIN0030 DS    0H
         ITRACE ID=INDD              DISIN DD FOUND
         OI    COMMDD,$INDD          INDICATE DISIN IS PRESENT
         B     MAIN0020
MAIN0040 DS    0H
         ITRACE ID=PRTDD             DISPRINT DD FOUND
         OI    COMMDD,$PRTDD         INDICATE DISPRINT IS PRESENT
         B     MAIN0020
MAIN0050 DS    0H
         ITRACE ID=MODDD             DISMOD DD FOUND
         OI    COMMDD,$MODDD         INDICATE DISMOD IS PRESENT
         B     MAIN0020
MAIN0060 DS    0H
         ITRACE ID=PUNCHDD           DISPUNCH DD FOUND
         OI    COMMDD,$PUNCHDD       INDICATE DISPUNCH IS PRESENT
         B     MAIN0020
MAIN0070 DS    0H
         ITRACE ID=DEBUGDD           DISDEBUG DD FOUND
         OI    COMMDD,$DEBUGDD       INDICATE DISDEBUG IS PRESENT
         B     MAIN0020
MAIN0075 DS    0H
         ITRACE ID=DSECTDD           DISDSECT DD FOUND
         OI    COMMDD,$DSECTDD       INDICATE DISDSECT IS PRESENT
         B     MAIN0020
* ------------------------------------------------------------------- *
*         Determine DD'S present                                      *
* ------------------------------------------------------------------- *
MAIN0080 DS    0H
         TM    COMMDD,$PRTDD         WAS PRINT DD FOUND?
         BO    MAIN0090              YES
         WTO   'DISPRINT DD STATEMENT MISSING, EXECUTION ABORTED'
         OI    COMMFLAG,$ABORT+$ERROR SET FLAGS
         B     EXIT0000              AND EXIT
MAIN0090 DS    0H
         MVI   PRTCMD,$PRTHEAD       SET COMMAND
         LA    R1,PRTBLOK            PRINT INTERFACE BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         TM    COMMDD,$INDD          IS DISIN DD PRESENT?
         BO    MAIN0100              YES
         MVC   PRTDATA(MSG04L),MSG04
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ABORT+$ERROR SET FLAGS
MAIN0100 DS    0H
         TM    COMMDD,$MODDD         DISMOD DD PRESENT?
         BO    MAIN0110              YES
         MVC   PRTDATA(MSG05L),MSG05
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ABORT+$ERROR SET FLAGS
MAIN0110 DS    0H
         TM    COMMDD,$PUNCHDD       DISPUNCH DD PRESENT?
         BO    MAIN0120              YES
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
MAIN0120 DS    0H
         TM    COMMFLAG,$ABORT       ABORT FLAG SET?
         BO    EXIT0000              YES, EXIT
* ------------------------------------------------------------------- *
*         Call parameter reader                                       *
* ------------------------------------------------------------------- *
         L     R15,A02               PARAMETER READER ENTRY POINT
         BALR  R14,R15               LINK TO PARAMETER CONVERTER
         MVC   COMMDBSH,A02SUB       SET SUBHEADING
         BAL   R10,DEBUG000          CALL DEBUG
         TM    COMMFLAG,$ABORT       SERIOUS ERROR?
         BNO   MAIN0130              NO
         MVC   PRTDATA(MSG06L),MSG06 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*         Call object module reader                                   *
* ------------------------------------------------------------------- *
MAIN0130 DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR I/O BUFFER    +
               LV=$IOSIZE,           .. SIZE                           +
               LOC=BELOW             .. 24-BIT AREA
         ST    R1,COMMIO             SET ADDRESS IN COMM AREA
         ITRACE ID=CALLA03           TRACE LINK TO MODULE 03
         L     R15,A03               MODULE READER ENTRY POINT
         BALR  R14,R15               LINK TO MODULE READER
         TM    COMMFLAG,$ABORT       SERIOUS ERROR?
         BNO   MAIN0140              NO
         MVC   PRTDATA(MSG07L),MSG07 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*         Call object text printer                                    *
* ------------------------------------------------------------------- *
MAIN0140 DS    0H
         L     R15,A06               MODULE TEXT PRINTER ENTRY POINT
         BALR  R14,R15               LINK TO TEXT PRINTER
         MVC   COMMDBSH,A06SUB       SET SUBHEADING
         BAL   R10,DEBUG000          CALL DEBUG
         TM    COMMFLAG,$ABORT       SERIOUS ERROR?
         BNO   MAIN0150              NO
         MVC   PRTDATA(MSG08L),MSG08 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*         Call the assembler interface to assemble dsects and build   *
*         the dsect/dsect label chains.                               *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         L     R15,A07               DSECT INTERPRETER ENTRY POINT
         BALR  R14,R15               LINK TO DSECT INTERPRETER
         MVC   COMMDBSH,A07SUB       SET SUBHEADING
         BAL   R10,DEBUG000          CALL DEBUG
         TM    COMMFLAG,$ABORT       SERIOUS ERROR?
         BNO   MAIN0160              NO
         MVC   PRTDATA(MSG09L),MSG09 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*         Call internal label and reference table generator.          *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=CALL08            CALLING LABEL TABLE GENERATOR
         L     R15,A08               LABEL GENERATOR ENTRY POINT
         BALR  R14,R15               LINK TO LABEL GENERATOR
         MVC   COMMDBSH,A08SUB       SET SUBHEADING
         BAL   R10,DEBUG000          CALL DEBUG
         TM    COMMFLAG,$ERROR       ANY ERRORS?
         BNO   MAIN0170              NO
         MVC   PRTDATA(MSG10L),MSG10
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         ITRACE ID=CALL09            CALLING SOURCE GENERATOR
         L     R15,A09               SOURCE GENERATOR ENTRY POINT
         BALR  R14,R15               LINK TO SOURCE GENERATOR
         TM    COMMFLAG,$ERROR       ANY ERRORS?
         BO    MAIN0180              YES
         ITRACE ID=SUCCESS
         MVC   PRTDATA(MSG03L),MSG03 SET FINAL MESSAGE
         BAL   R10,PRT0000           PRINT FINAL MESSAGE
         B     EXIT0000              AND EXIT
MAIN0180 DS    0H
         MVC   PRTDATA(MSG11L),MSG11
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*        Link to DEBUG module                                         *
* ------------------------------------------------------------------- *
DEBUG000 DS    0H
         MVI   DBUGCMD,$DBUG         NORMAL DEBUG
         LA    R1,DBUGBLOK           PARAMETER BLOCK ADDRESS
         L     R15,ADB               DEBUG MODULE ENTRY POINT
         BALR  R14,R15               LINK TO DEBUG MODULE
         BR    R10                   RETURN
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R10                   RETURN
EXIT0000 DS    0H
         TM    COMMFLAG,$ABEND       ABEND REQUESTED?
         BNO   EXIT0010              NO
         ITRACE ID=ABEND
         MVC   PRTDATA(MSG02L),MSG02 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         ABEND ABEND001,DUMP,,USER   GIVE 'EM WHAT THEY ASKED FOR
EXIT0010 DS    0H
         ICM   R1,15,COMMIO          I/O BUFFER ADDRESS
         BZ    EXIT0020              NO BUFFER
         ITRACE ID=FREEIO,           FREEING I/O STORAGE               +
               RDATA1=R1             .. I/O AREA'S ADDRESS
         FREEMAIN RU,A=(1),LV=$IOSIZE FREE I/O BUFFER
EXIT0020 DS    0H
         ICM   R1,15,COMMESD         FIRST ESD ENTRY
         USING ESDDATA,R1            DEFINE BASE
         BZ    EXIT0040              NO ESD ENTRIES
EXIT0030 DS    0H
         L     R2,ESDNEXT            NEXT ENTRY
         ITRACE ID=FREEESD,          FREEING ESD BLOCK                 +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(R1),LV=ESDDATAL
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0030              LOOP
EXIT0040 DS    0H
         ICM   R1,15,COMMTXT         TEXT'S STORAGE ADRESS
         BZ    EXIT0050              NOT ACQUIRED
         L     R2,COMMCSLN           STORAGE SIZE
         LA    R2,32(R2)             PLUS FUDGE FACTOR (SEE DISASM04)
         ITRACE ID=FREETEXT,         FREEING TEXT'S STORAGE            +
               RDATA1=R1,            .. TEXT'S ADDRESS                 +
               RDATA2=R2             .. TEXT'S LENGTH
         FREEMAIN RU,A=(R1),LV=(R2)  FREE TEXT'S STORAGE
EXIT0050 DS    0H
         ICM   R1,15,COMMUSNG        FIRST USING BLOCK
         USING USNGDSCT,R1           DEFINE BASE
         BZ    EXIT0070              NO BLOCK'S TO FREE
EXIT0060 DS    0H
         L     R2,USNGNEXT           NEXT BLOCK ON CHAIN
         ITRACE ID=FREEUSNG,         FREEING USING BLOCK'S STORAGE     +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(1),LV=USNGL  FREE THE BLOCK
         LTR   R1,R2                 COPY ADDRESS
         BNZ   EXIT0060              FREE ALL BLOCKS
EXIT0070 DS    0H
         ICM   R1,15,COMMRLD         FIRST RLD BLOCK
         USING RLDDATA,R1            DEFINE BASE
         BZ    EXIT0100              NO RLD BLOCK'S TO FREE
EXIT0090 DS    0H
         L     R2,RLDNEXT            NEXT BLOCK'S ADDRESS
         ITRACE ID=FREERLD,          FREEING RLD DATA                  +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(1),LV=RLDDATAL
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0090              LOOP
EXIT0100 DS    0H
         ICM   R1,15,COMMBASE        FIRST BASE BLOCK
         USING BASEDSCT,R1           DEFINE BASE
         BZ    EXIT0120              NO BASE BLOCK'S TO FREE
EXIT0110 DS    0H
         L     R2,BASENEXT           NEXT BLOCK'S ADDRESS
         ITRACE ID=FREEBASE,         FREEING BASE BLOCK                +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(1),LV=BASEL  FREE BASE BLOCK STORAGE
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0110              LOOP
EXIT0120 DS    0H
         ICM   R1,15,COMMDATA        FIRST DATA BLOCK
         USING DATADSCT,R1           DEFINE BASE
         BZ    EXIT0140              NO DATA BLOCK'S TO FREE
EXIT0130 DS    0H
         L     R2,DATANEXT           NEXT BLOCK'S ADDRESS
         ITRACE ID=FREEDATA,         FREEING DATA BLOCK                +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(1),LV=DATAL  FREE DATA BLOCK STORAGE
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0130              LOOP
EXIT0140 DS    0H
         ICM   R3,15,COMMDSCT        FIRST DSECT BLOCK
         USING DSCTDSCT,R3           DEFINE BASE
         BZ    EXIT0200              NO DSECT BLOCK'S TO FREE
EXIT0150 DS    0H
         ICM   R4,15,DSCTLBA         FIRST LABEL IN THIS DSECT
         USING LABLDSCT,R4           DEFINE BASE
         BZ    EXIT0190              NO LABEL BLOCKS TO FREE
EXIT0160 DS    0H
         ICM   R1,15,LABLEQU         FIRST EQUATE BLOCK
         BZ    EXIT0180              NO EQUATE BLOCKS
         USING EQUDATA,R1            DEFINE BASE
EXIT0170 DS    0H
         L     R2,EQUNEXT            NEXT EQUATE BLOCK'S ADDRESS
         ITRACE ID=FREEEQU,          FREEING AN EQU BLOCK              +
               DATA1=EQULABEL,       .. EQUATE NAME                    +
               RDATA2=R1             .. EQU BLOCK ADDRESS
         FREEMAIN RU,A=(1),LV=EQUL   FREE EQUATE BLOCK
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0170              LOOP
EXIT0180 DS    0H
         L     R2,LABLNEXT           NEXT LABEL BLOCK'S ADDRESS
         ITRACE ID=FREELABL,         FREEING A LABEL BLOCK             +
               DATA1=LABLNAME,       .. LABEL NAME                     +
               RDATA2=R4             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(4),LV=LABLL  FREE LABEL BLOCK STORAGE
         LTR   R4,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0160              LOOP
EXIT0190 DS    0H
         L     R2,DSCTNEXT           NEXT BLOCK'S ADDRESS
         ITRACE ID=FREEDSCT,         FREEING DSECT BLOCK               +
               DATA1=DSCTNAME,       .. DSECT NAME                     +
               RDATA2=R3             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(3),LV=DSCTL  FREE DSECT BLOCK STORAGE
         LTR   R3,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0150              LOOP
EXIT0200 DS    0H
         ICM   R4,15,COMMLABL        FIRST CSECT LABEL
         BZ    EXIT0220              NO CSECT LABELS
EXIT0210 DS    0H
         L     R2,LABLNEXT           NEXT LABEL BLOCK'S ADDRESS
         ITRACE ID=FREELABL,         FREEING A LABEL BLOCK             +
               DATA1=LABLNAME,       .. LABEL NAME                     +
               DATA2=R4              .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(4),LV=LABLL  FREE LABEL BLOCK STORAGE
         LTR   R4,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0210              LOOP
EXIT0220 DS    0H
         ICM   R1,15,COMMREF         FIRST REFERENCE BLOCK
         USING REFDSCT,R1            DEFINE BASE
         BZ    EXIT0240              NO MORE BLOCK'S TO FREE
EXIT0230 DS    0H
         L     R2,REFNEXT            NEXT BLOCK'S ADDRESS
         ITRACE ID=FREEREF,          FREEING REFERENCE BLOCK           +
               RDATA1=R1             .. BLOCK'S ADDRESS
         FREEMAIN RU,A=(1),LV=REFL   FREE REFERENCE BLOCK STORAGE
         LTR   R1,R2                 COPY NEXT BLOCK'S ADDRESS
         BNZ   EXIT0230              LOOP
EXIT0240 DS    0H
         ICM   R1,15,COMMDISP        DISPLACEMENT TABLE ACQUIRED?
         BZ    EXIT0250              NO
         L     R2,COMMCSLN           CSECT SIZE
         LA    R2,4(R2)              PLUS 4
         ITRACE ID=FREEDISP,         FREEMAINING DISPLACEMENT TABLE    +
               RDATA1=R1,            .. STORAGE ADDRESS                +
               RDATA2=R2             .. STORAGE SIZE
         FREEMAIN RU,A=(1),LV=(2)
EXIT0250 DS    0H
         ITRACE ID=FREETRCE
         ICM   R1,15,TRADDR          TRACE TABLE'S ADDRESS
         BZ    EXIT0260              NOT ACQUIRED
         L     R2,TRSIZE             TRACE TABLE'S SIZE
         LA    R2,32(R2)             FOR BOUNDARY ROUNDING
         FREEMAIN RU,A=(R1),LV=(R2)  FREEMAIN TRACE TABLE
EXIT0260 DS    0H
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE01   DC    18F'0'                REGISTER SAVE AREA
V00      DC    V(DISASM00)           COMMON MODULE'S ADDRESS
INDD     DC    CL8'DISIN'
PRTDD    DC    CL8'DISPRINT'
LIBDD    DC    CL8'DISMOD'
PUNCHDD  DC    CL8'DISPUNCH'
DEBUGDD  DC    CL8'DISDEBUG'
DSECTDD  DC    CL8'DISDSECT'
A02SUB   DC    CL35'Internal data after module DISASM02'
A06SUB   DC    CL35'Internal data after module DISASM06'
A07SUB   DC    CL35'Internal data after module DISASM07'
A08SUB   DC    CL35'Internal data after module DISASM08'
MSG01    DC    C'DISASM0101I No DISPUNCH DD present, no source will be +
               generated'
MSG01L   EQU   *-MSG01
MSG02    DC    C'DISASM0102I ABEND requested, program abnormally termin+
               ating'
MSG02L   EQU   *-MSG02
MSG03    DC    C'DISASM0103I ******* Disassembly complete ******'
MSG03L   EQU   *-MSG03
MSG04    DC    C'DISASM0104E DISIN DD statement missing, processing wil+
               l be aborted'
MSG04L   EQU   *-MSG04
MSG05    DC    C'DISASM0105E DISMOD DD statement missing, processing wi+
               ll be aborted'
MSG05L   EQU   *-MSG05
MSG06    DC    C'DISASM0106E Error(s) in control parameters, execution +
               aborted'
MSG06L   EQU   *-MSG06
MSG07    DC    C'DISASM0107E Error(S) in loading object module, executi+
               on aborted'
MSG07L   EQU   *-MSG07
MSG08    DC    C'DISASM0108E Error(s) in printing text, execution abort+
               ed'
MSG08L   EQU   *-MSG08
MSG09    DC    C'DISASM0109E Error(s) in assembling DSECTS, execution a+
               borted'
MSG09L   EQU   *-MSG09
MSG10    DC    C'DISASM0110E Error(s) in generating label table, execut+
               ion aborted'
MSG10L   EQU   *-MSG10
MSG11    DC    C'DISASM0111E Error(s) in generating source, execution a+
               borted'
MSG11L   EQU   *-MSG11
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              DEBUG MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
DBUGBLOK DBUGBLOK TYPE=CSECT
         SPACE 2
         LTORG
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         IHAPSA LIST=NO
         IKJTCB LIST=NO
         IEFTIOT1
         END  DISASM01
./ ADD NAME=DISASM02
         TITLE 'DISASM02 - PARAMETER READER/INTERPRETER'
         MACRO
         REG &REG,&VALUE
         DC     CL3'&REG'            REGISTER NAME
         DC     AL1(&VALUE)          VALUE IN INSTRUCTIONS
         MEND
         COPY  DISASMGB              COPY GLOBAL DEFINITIONS
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM02                                             *
*                                                                    *
*  Function:                                                         *
*   Read the parameter statements.  All parameter statements         *
*   including comment statements are copied to DISPRINT.  See the    *
*   DISASM documentation for a list of the parameter statements      *
*   and their syntax.                                                *
*                                                                    *
*--------------------------------------------------------------------*
DISASM02 CSECT
DISASM02 AMODE 31
DISASM02 RMODE 24
         USING DISASM02,R12,R10
         USING DISASM00,R11
         USING REGDSCT,R4            DEFINE BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         LA    R10,2048(R12)         SET BASE 2..
         LA    R10,2048(R10)         .. 4K FROM 1ST
         B     PARM0000              SKIP EYECATCHER
         DC    CL8'DISASM02'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
PARM0000 DS    0H
         LA    R1,SAVE02             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (DISIN,INPUT)         OPEN DISIN
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   COMMSUBH(SUBHEADL),SUBHEAD
         LA    R1,SUBHEADL           SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
PARM0010 DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         GET   DISIN,CTLSTMT         READ A CONTROL STATEMENT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   PRTDATA(CTLSTMTL),CTLSTMT
         BAL   R9,PRT0000            PRINT CONTROL STATEMENT
         CLI   CTLSTMT,C'*'          IS IT A COMMENT?
         BE    PARM0010              YES
         LA    R1,CNTLTBLE           CONTROL TABLE ADDRESS
PARM0020 DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    PARM0040              YES
         CLC   CTLTYPE,0(R1)         DEFINED CONTROL STATEMENT?
         BE    PARM0030              YES
         LA    R1,13(R1)             NEXT KEYWORD/ADDRESS
         B     PARM0020              LOOP
PARM0030 DS    0H
         ICM   R15,15,9(R1)          INSERT ADDRESS
         BR    R15                   BRANCH TO PROPER ROUTINE
PARM0040 DS    0H
         MVC   PRTDATA(EMSG03L),EMSG03
         BAL   R9,PRT0000            PRINT MESSAGE
         OI    COMMFLAG,$ABORT       SET ABORT FLAG
         B     PARM0010              READ NEXT STATEMENT
* ------------------------------------------------------------------- *
*                                                                     *
*          Set ABEND flag                                             *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND             ABEND AT EXIT
         OI    COMMFLAG,$ABEND       SET ABEND FLAG
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process assembler input                                    *
*                                                                     *
* ------------------------------------------------------------------- *
ASM0000  DS    0H
         ITRACE ID=ASMSTART
ASM0010  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         GET   DISIN,CTLSTMT         READ A CONTROL STATEMENT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   PRTDATA(CTLSTMTL),CTLSTMT
         BAL   R9,PRT0000            PRINT CONTROL STATEMENT
         CLC   CTLTYPE,CNTLASME      ASSEMBLER INPUT (END)?
         BE    ASM0030               YES
         TM    PGMFLAG,$ASMOPEN      ASSEMBLER DCB OPEN?
         BO    ASM0020               YES
         OI    PGMFLAG,$ASMOPEN      INDICATE DCB IS OPEN
         OI    COMMFLAG,$ASMIN       INDICATE ASSEMBLER INPUT PRESENT
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (SYSIN,OUTPUT)        OPEN SYSIN DCB
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
ASM0020  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   SYSIN,CTLSTMT         COPY CONTROL STATEMENT TO SYSIN
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         B     ASM0010               LOOP UNTIL EOF OR 'ASM END'
ASM0030  DS    0H
         ITRACE ID=ASMEND
         B     PARM0010              READ NEXT CONTROL STATEMENT
* ------------------------------------------------------------------- *
*                                                                     *
*          Process BASE statements                                    *
*                                                                     *
* ------------------------------------------------------------------- *
BASE0000 DS    0H
         ITRACE ID=BASE
         LA    R1,CTL10              REGISTER NAME'S ADDRESS
         BAL   R9,REG0000            FIND REGISTER TABLE ENTRY
* ------------------------------------------------------------------- *
*       R4 POINTS TO REGISTER TABLE ENTRY                             *
* ------------------------------------------------------------------- *
         CLC   CTL20(20),COMMBLKS    START AND END ALL BLANK?
         BE    BASE0020              YES
         LA    R1,8                  MAX DIGITS
         LA    R2,CTL20              FIRST CHARACTER OF DISPLACEMENT
         BAL   R9,HEX0000            CONVERT DISPLACEMENT TO HEX
         MVC   SAVEBEGN,DISPOUT      SAVE BEGINNING DISPLACEMENT
         CLC   CTL30,COMMBLKS        ENDING DISPLACEMENT BLANK?
         BE    BASE0010              YES
         LA    R1,8                  MAX DIGITS
         LA    R2,CTL30              FIRST CHARACTER OF DISPLACEMENT
         BAL   R9,HEX0000            CONVERT DISPLACEMENT TO HEX
         MVC   SAVEEND,DISPOUT       SAVE ENDING DISPLACEMENT
         CLC   SAVEBEGN,SAVEEND      BEGIN LARGER THAN END?
         BH    BASE0060              YES.. INVALID
         B     BASE0030
BASE0010 DS    0H
         ICM   R1,15,SAVEBEGN        BEGINNING POINT
         AH    R1,H4096              PLUS 4K
         STCM  R1,15,SAVEEND         SAVE ENDING POINT
         B     BASE0030
BASE0020 DS    0H
         XC    SAVEBEGN,SAVEBEGN     SET BEGIN TO ZERO
         MVC   SAVEEND,XFFFF         SET END TO HEX FF'S
BASE0030 DS    0H
         LA    R1,8                  MAX DIGITS
         LA    R2,CTL40              FIRST CHARACTER OF DISPLACEMENT
         BAL   R9,HEX0000            CONVERT DISPLACEMENT TO HEX
         LA    R5,COMMBASE           SET PREVIOUS FORWARD POINTER
         ICM   R3,15,COMMBASE        FIRST BASE ENTRY
         USING BASEDSCT,R3           DEFINE BASE
         BZ    BASE0050              NO BLOCK'S ON CHAIN
BASE0040 DS    0H
         CLC   BASEBEGN,DISPOUT      INSERT IT HERE?
         BL    BASE0050              YES
         LA    R5,BASENEXT           FORWARD POINTER'S ADDRESS
         ICM   R3,15,BASENEXT        NEXT BASE BLOCK
         BNZ   BASE0040              LOOP
BASE0050 DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR NEW BASE BLOCK+
               LV=BASEL,             .. SIZE                           +
               LOC=ANY               .. ANY AREA
         ITRACE ID=NEWBASE,          NEW BLOCK ACQUIRED                +
               RDATA1=R1             .. CAPTURE STORAGE ADDRESS
         ST    R1,0(R5)              CHAIN PREVIOUS BLOCK TO NEW BLOCK
         ST    R3,BASENEXT-BASEDSCT(R1)   CHAIN NEXT BLOCK TO NEW BLOCK
         LR    R3,R1                 SET BASE
         MVC   BASEEYE,CNTLBASE      SET BLOCK IDENTIFIER
         MVC   BASEBEGN,SAVEBEGN     SET STARTING DISPLACEMENT
         MVC   BASEEND,SAVEEND       SET ENDING DISPLACEMENT
         MVC   BASEREG,REGVALUE      SET BASE REGISTER
         MVC   BASEDISP,DISPOUT      SET DISPLACEMENT BASE REFERS TO
         B     PARM0010              READ NEXT CONTROL STATEMENT
BASE0060 DS    0H
         MVC   PRTDATA(EMSG23L),EMSG23
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
* ------------------------------------------------------------------- *
*                                                                     *
*          Process CSECT statements                                   *
*                                                                     *
* ------------------------------------------------------------------- *
CSCT0000 DS    0H
         ITRACE ID=CSCTNAME,         CSECT NAME                        +
               DATA1=CTLDATA
         MVC   COMMCSNM,CTLDATA      SET CSECT NAME
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process DATA statements                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DATA0000 DS    0H
         ITRACE ID=DATA
         LA    R1,8                  MAX DIGITS
         LA    R2,CTL10              FIRST CHARACTER OF DISPLACEMENT
         BAL   R9,HEX0000            CONVERT DISPLACEMENT TO HEX
         MVC   SAVEBEGN,DISPOUT      SAVE BEGINNING DISPLACEMENT
         LA    R1,8                  MAX DIGITS
         LA    R2,CTL20              FIRST CHARACTER OF END DISP
         BAL   R9,HEX0000            CONVERT END DISP TO HEX
         LA    R2,COMMDATA           DATA BLOCK ANCHOR
         ICM   R3,15,COMMDATA        FIRST DATA BLOCK
         USING DATADSCT,R3           DEFINE BASE
         BZ    DATA0030              NO DATA BLOCKS
DATA0010 DS    0H
         CLC   DATAEND,SAVEBEGN      BELOW THIS AREA?
         BL    DATA0020              YES
         CLC   DATABEGN,DISPOUT      ABOVE THIS AREA?
         BH    DATA0030              YES
         B     DATA0040              OVERLAPS PREVIOUSLY DEFINED AREA
DATA0020 DS    0H
         LA    R2,DATANEXT           FORWARD POINTER ADDRESS
         ICM   R3,15,DATANEXT        NEXT DATA BLOCK
         BNZ   DATA0010              LOOP
DATA0030 DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR NEW DATA BLOCK+
               LV=DATAL,             .. SIZE                           +
               LOC=ANY               .. IN ANY AREA
         ITRACE ID=NEWDATA,          NEW DATA BLOCK ADQUIRED           +
               RDATA1=R1             .. NEW BLOCK'S ADDRESS
         ST    R1,0(R2)              PREVIOUS BLOCK TO NEW BLOCK
         ST    R3,DATANEXT-DATADSCT(R1)   CHAIN NEXT BLOCK TO NEW BLOCK
         LR    R3,R1                 SET BASE REG
         MVC   DATAEYE,CNTLDATA      SET BLOCK IDENTIFIER
         MVC   DATABEGN,SAVEBEGN     SET BEGINNING DISPLACEMENT
         MVC   DATAEND,DISPOUT       SET ENDING DISPLACEMENT
         ICM   R0,15,DATABEGN        BEGINNING DISPLACEMENT
         ICM   R1,15,DATAEND         ENDING DISPLACEMENT
         SR    R1,R0                 LENGTH - 1
         LA    R1,1(R1)              TOTAL LENGTH
         STCM  R1,15,DATALEN         SET LENGTH
         MVI   DATATYPE,$DATAUSR     USER DEFINED DATA AREA
         MVC   DATANAME,COMMBLKS     INITIALIZE NAME
         XC    DATALBA,DATALBA       INITIALIZE DATA BLOCK'S ADDRESS
         B     PARM0010              READ NEXT CONTROL STATEMENT
DATA0040 DS    0H
         ITRACE ID=DATAOVLP          DATA AREA OVERLAP
         UNPK  EMSG22A(5),DATABEGN(3)
         MVZ   EMSG22A,COMM0F0F      TURN OFF ZONES
         TR    EMSG22A,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   EMSG22A+4,C' '        RESTORE BLANK
         UNPK  EMSG22B(5),DATAEND(3) UNPACK ENDING DISPLACEMENT
         MVZ   EMSG22B,COMM0F0F      TURN OFF ZONES
         TR    EMSG22B,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   EMSG22B+4,C' '        RESTORE BLANK
         MVC   PRTDATA(EMSG22L),EMSG22
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
* ------------------------------------------------------------------- *
*                                                                     *
*          LABEL or PREFIX statements                                 *
*                                                                     *
* ------------------------------------------------------------------- *
LABL0000 DS    0H
         ITRACE ID=LABEL
         CLC   COMMPFX,COMMBLKS      PREFIX STILL BLANK?
         BNE   LABL0030              NO.. DUPLICATED
         CLI   CTL10,C' '            PREFIX BLANK?
         BE    LABL0040              YES.. INVALID
         CLC   CTL10(4),CNTLDATA     PREFIX 'DATA'?
         BE    LABL0050              YES.. NOT VALID
         LA    R1,CTL10              FIRST CHARACTER OF PREFIX
         LA    R2,4                  MAXIMUM LENGTH
         SR    R3,R3                 INITIALIZE FOR LENGTH
LABL0010 DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    LABL0020              YES
         LA    R1,1(R1)              NEXT
         LA    R3,1(R3)              ADD 1 TO LENGTH
         BCT   R2,LABL0010           LOOP
LABL0020 DS    0H
         CLC   COMMBLKS(4),0(R1)     A FEW BLANKS?
         BNE   LABL0060              NO
         MVC   COMMPFX,CTL10         SET PREFIX
         STH   R3,COMMPFXL           SET PREFIX LENGTH
         B     PARM0010              READ NEXT CONTROL STATEMENT
LABL0030 DS    0H
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG19L),EMSG19
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
LABL0040 DS    0H
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG11L),EMSG11
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
LABL0050 DS    0H
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG15L),EMSG15
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
LABL0060 DS    0H
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG12L),EMSG12
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010              READ NEXT CONTROL STATEMENT
* ------------------------------------------------------------------- *
*                                                                     *
*          Line count statements                                      *
*                                                                     *
* ------------------------------------------------------------------- *
LINE0000 DS    0H
         ITRACE ID=LINES
         MVC   LINEIN,LINEIN-1       INITIALIZE WITH ZEROS
         LA    R1,CTL10              FIRST DIGIT
         LA    R2,3                  MAX DIGITS
LINE0010 DS    0H
         CLI   0(R1),C' '            BLANK
         BE    LINE0020              YES
         CLI   0(R1),C'0'            INVALID DIGIT?
         BL    LINE0030              YES
         CLI   0(R1),C'9'            INVALID DIGIT?
         BH    LINE0030              YES
         MVC   LINEIN(L'LINEIN-1),LINEIN+1 SHIFT DIGITS LEFT 1
         MVC   LINEIN+2(1),0(R1)     INSERT IN LOW ORDER POSITION
         LA    R1,1(R1)              NEXT DIGIT
         BCT   R2,LINE0010           LOOP
LINE0020 DS    0H
         CLC   0(6,R1),COMMBLKS      SEVERAL TRAILING BLANKS?
         BNE   LINE0040              NO
         PACK  LINEOUT,LINEIN        PACK LINE COUNT
         CP    LINEOUT,PMIN          LESS THAN MINIMUM?
         BL    LINE0050              YES
         ZAP   COMMMAXL,LINEOUT      SET MAX LINE COUNT
         B     PARM0010              READ NEXT CONTROL STATEMENT
LINE0030 DS    0H
         MVC   PRTDATA(EMSG16L),EMSG16
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
LINE0040 DS    0H
         MVC   PRTDATA(EMSG17L),EMSG17
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
LINE0050 DS    0H
         MVC   PRTDATA(EMSG18L),EMSG18
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process MODULE Statements                                  *
*                                                                     *
* ------------------------------------------------------------------- *
MOD0000  DS    0H
         ITRACE ID=MODNAME,          MODULE NAME                       +
               DATA1=CTLDATA
         MVC   COMMMOD,CTLDATA       SET MODULE NAME
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process NO FLOAT statements                                *
*                                                                     *
* ------------------------------------------------------------------- *
NOB20000 DS    0H
         ITRACE ID=NOB2              NO 'B2' INSTRUCTIONS
         OI    COMMFLAG,$NOB2        SET 'NO B2 INSTRUCTIONS' FLAG
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process NO FLOAT statements                                *
*                                                                     *
* ------------------------------------------------------------------- *
NOFLOAT0 DS    0H
         OI    COMMFLAG,$NOFLOAT     SET NO FLOATING POINT INSTR FLAG
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process RLD WARN statements                                *
*                                                                     *
*   When a user define DATA area overlaps an RLD item, DISASM adjusts *
*   the DATA block(s).  Normally DISASM does this without printing    *
*   any messages.  If you want to know what "adjustments" DISASM      *
*   may be making to your DATA areas, specify "RLD WARN" as a control *
*   statement.                                                        *
*                                                                     *
* ------------------------------------------------------------------- *
RLD0000  DS    0H
         OI    COMMFLG2,$RLDWARN     SET THE RLD WARN INDICATOR
         B     PARM0010              CONTINUE
* ------------------------------------------------------------------- *
*                                                                     *
*          Set sequentially numbered labels flag                      *
*                                                                     *
* ------------------------------------------------------------------- *
SEQ0000  DS    0H
         ITRACE ID=SEQLABEL
         TM    COMMFLAG,$SEQLABL     ALREADY SET?
         BO    SEQ0010               YES
         OI    COMMFLAG,$SEQLABL     SET SEQUENTIALLY NUMBER LABEL FLAG
         B     PARM0010
SEQ0010  DS    0H
         MVC   PRTDATA(WMSG02L),WMSG02
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*          Process USING statements                                   *
*                                                                     *
* ------------------------------------------------------------------- *
USNG0000 DS    0H
         ITRACE ID=USING             USING STATEMENT FOUND
         LA    R3,COMMUSNG           CURRENT BLOCK IS ANCHOR
         USING USNGDSCT,R3           DEFINE BASE
USNG0010 DS    0H
         ICM   R1,15,USNGNEXT        NEXT BLOCK ON CHAIN
         BZ    USNG0020              END OF CHAIN FOUND
         LR    R3,R1                 COPY ADDRESS
         B     USNG0010              KEEP FOLLOWING CHAIN
USNG0020 DS    0H
         LA    R1,CTL30              REGISTER NAME'S ADDRESS
         BAL   R9,REG0000            FIND REGISTER TABLE ENTRY
* ------------------------------------------------------------------- *
*       R3 POINTS TO LAST USING BLOCK OR ANCHOR                       *
*       R4 POINTS TO REGISTER TABLE ENTRY                             *
* ------------------------------------------------------------------- *
         CLC   CTL40,COMMBLKS        STARTING DISPLACEMENT ALL BLANK?
         BE    USNG0070              YES
         CLC   CTL50,COMMBLKS        ENDING DISPLACEMENT ALL BLANK?
         BE    USNG0050              YES.. NOT VALID
         LA    R1,8                  MAXIMUM NUMBER OF DIGITS
         LA    R2,CTL40              FIRST CHARACTER OF BEGIN DISP
         BAL   R9,HEX0000            VERIFY/CONVERT BEGIN DISP
         MVC   SAVEBEGN,DISPOUT      SAVE BEGINNING DISPLACEMENT
         LA    R1,8                  MAXIMUM NUMBER OF DIGITS
         LA    R2,CTL50              FIRST CHARACTER OF END DISP
         BAL   R9,HEX0000            VERIFY/CONVERT END DISP
         CLC   SAVEBEGN,DISPOUT      BEGIN LARGER THAN END?
         BH    USNG0060              YES.. ERROR
USNG0030 DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR USING ENTRY   +
               LV=USNGL,             .. SIZE                           +
               LOC=ANY               .. IN ANY AREA
         ITRACE ID=NEWUSNG,                                            +
               RDATA1=R1             .. TRACE NEW BLOCK'S ADDRESS
         ST    R1,USNGNEXT           CHANGE TO PREVIOUS BLOCK
         LR    R3,R1                 COPY BASE
         MVC   USNGEYE,CNTLUSNG      SET BLOCK IDENTIFIER
         XC    USNGNEXT,USNGNEXT     CLEAR FORWARD POINTER
         MVI   USNGFLAG,0            SET ALL FLAGS OFF
         MVC   USNGDSNM,CTL10        SET DSECT'S NAME
         MVC   USNGLBNM,CTL20        SET LABEL WITHIN DSECT
         XC    USNGDSA,USNGDSA       CLEAR DSECT BLOCK'S ADDRESS
         XC    USNGLBA,USNGLBA       CLEAR LABEL BLOCK'S ADDRESS
         XC    USNGDISP,USNGDISP     CLEAR LABEL DISP INTO DSECT
         MVC   USNGBASE,REGVALUE     SET BASE VALUE
         CLC   CTL40,COMMBLKS        STARTING DISPLACEMENT OMITTED?
         BE    USNG0040              YES
         MVC   USNGBEGN,SAVEBEGN     SET BEGINNING DISPLACEMENT
         MVC   USNGEND,DISPOUT       SET ENDING DISPLACEMENT
         B     PARM0010              READ NEXT CONTROL STATEMENT
USNG0040 DS    0H
         OI    USNGFLAG,$USNGND      NO DISPLACEMENTS
         XC    USNGBEGN,USNGBEGN     CLEAR STARTING DISP
         XC    USNGEND,USNGEND       CLEAR ENDING DISP
         B     PARM0010              READ NEXT CONTROL STATEMENT
USNG0050 DS    0H
         MVC   PRTDATA(EMSG06L),EMSG06 SET MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
USNG0060 DS    0H
         MVC   PRTDATA(EMSG07L),EMSG07 SET MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
USNG0070 DS    0H
         CLC   CTL50,COMMBLKS        ENDING DISPLACEMENT BLANK?
         BE    USNG0030              YES
         MVC   PRTDATA(EMSG10L),EMSG10
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
* ------------------------------------------------------------------- *
*                                                                     *
*        Locate register table entry                                  *
*                                                                     *
*          R1  is register name's address                             *
*          R9  is return address                                      *
*                                                                     *
*          At exit R4 will point to the register table entry.         *
*                                                                     *
*          If any error is found, control is passed to 'PARM0010'.    *
*                                                                     *
* ------------------------------------------------------------------- *
REG0000  DS    0H
         ITRACE ID=CONVREG
         CLI   0(R1),C' '            BLANK?
         BE    REG0050               YES.. INVALID
         CLC   3(7,R1),COMMBLKS      REFERENCE TOO LONG?
         BNE   REG0060               YES.. INVALID
         CLI   2(R1),C' '            3-CHARACTER NAME?
         BNE   REG0010               YES
         CLI   1(R1),C' '            2-CHARACTER NAME?
         BNE   REG0020               YES
         ITRACE ID=REG1
         LA    R4,REGTBL1            1-CHARACTER NAME TABLE
         SR    R2,R2                 SET LENGTH (1 BYTE)
         B     REG0030               FIND TABLE ENTRY
REG0010  DS    0H
         ITRACE ID=REG3
         LA    R4,REGTBL3            3-CHARACTER NAME TABLE
         LA    R2,2                  SET LENGTH (3 BYTES)
         B     REG0030               FIND TABLE ENTRY
REG0020  DS    0H
         ITRACE ID=REG2
         LA    R4,REGTBL2            2-CHARACTER NAME TABLE
         LA    R2,1                  SET LENGTH (2 BYTES)
REG0030  DS    0H
         CLI   0(R4),X'FF'           END OF TABLE?
         BE    REG0040               YES.. INVALID
         EX    R2,REGCLC             REGISTER NAME MATCH?
         BER   R9                    YES
         LA    R4,REGL(R4)           NEXT REGISTER ENTRY
         B     REG0030               LOOP
REG0040  DS    0H
         ITRACE ID=BADREG
         MVC   PRTDATA(EMSG05L),EMSG05 SET MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
REG0050  DS    0H
         ITRACE ID=REGBLANK
         MVC   PRTDATA(EMSG13L),EMSG13 SET MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
REG0060  DS    0H
         ITRACE ID=REGLONG
         MVC   PRTDATA(EMSG14L),EMSG14 SET MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
REGCLC   CLC   REGNAME(0),0(R1)      TEST REGISTER NAME
* ------------------------------------------------------------------- *
*                                                                     *
*          Convert character to hex                                   *
*                                                                     *
*          R1 should be the number of characters (up to 8)            *
*          R2 should be the address of the first character            *
*          R9 should be the return address                            *
*                                                                     *
*             At exit 'DISPOUT' will be the value in hex              *
*                                                                     *
*          If any error is found, control is passed to 'PARM0010'.    *
*                                                                     *
* ------------------------------------------------------------------- *
HEX0000  DS    0H
         MVC   DISPIN,=C'00000000'   INITIALIZE DISP TO ZERO
HEX0010  DS    0H
         CLI   0(R2),C' '            END OF DISPLACEMENT?
         BE    HEX0030               YES
         CLI   0(R2),C'A'            TOO LOW FOR VALID HEX?
         BL    HEX0040               YES
         CLI   0(R2),C'F'            WITHIN A-F?
         BNH   HEX0020               YES, VALID
         CLI   0(R2),C'0'            TOO LOW FOR NUMERIC?
         BL    HEX0040               YES
         CLI   0(R2),C'9'            TOO HIGH FOR VALID NUMERIC?
         BH    HEX0040               YES
HEX0020  DS    0H
         MVC   DISPIN(L'DISPIN-1),DISPIN+1    SHIFT LEFT 1 DIGIT
         MVC   DISPIN+L'DISPIN-1(1),0(R2)     SET LOW ORDER DIGIT
         LA    R2,1(R2)              NEXT DISP CHARACTER
         BCT   R1,HEX0010            LOOP
         CLI   0(R2),C' '            BLANK?
         BNE   HEX0050               NO.. TOO MANY DIGITS
HEX0030  DS    0H
         NC    DISPIN,COMM1F1F       PREPARE FOR TRANSLATE
         TR    DISPIN,COMMCHHX       TRANSLATE FOR PACKING
         PACK  DISPOUT(5),DISPIN(9)  PACK
         BR    R9                    GET OUTTA HERE
HEX0040  DS    0H
         MVC   PRTDATA(EMSG08L),EMSG08
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
HEX0050  DS    0H
         MVC   PRTDATA(EMSG09L),EMSG09
         OI    COMMFLAG,$ERROR+$ABORT
         BAL   R9,PRT0000            PRINT MESSAGE
         B     PARM0010
PRT0000  DS    0H
         TM    PGMFLAG,$SUBH         HAS SUB-HEADING BEEN PRINTED?
         BO    PRT0010               YES
         OI    PGMFLAG,$SUBH         SET FLAG
         MVI   PRTCMD,$PRTSUBH       SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
PRT0010  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R9                    RETURN
EXIT0000 DS    0H
         BAL   R14,AM31              FORCE 31-BIT MODE
         ITRACE ID=PARMEOF           END OF FILE
         TM    PGMFLAG,$ASMOPEN      ASSEMBLER INPUT OPEN?
         BNO   EXIT0010              NO
         ITRACE ID=CLOSEASM          CLOSING SYSIN DCB
         NI    PGMFLAG,255-$ASMOPEN  INDICATE DCB IS CLOSED
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         CLOSE SYSIN
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
EXIT0010 DS    0H
         OC    COMMBASE,COMMBASE     BASE REGISTER(S) DEFINED?
         BZ    EXIT0020              NO
         CLC   COMMPFX,COMMBLKS      PREFIX DEFINED?
         BNE   EXIT0030              YES
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG20L),EMSG20
         BAL   R9,PRT0000            PRINT MESSAGE
         B     EXIT0030              EXIT
EXIT0020 DS    0H
         CLC   COMMPFX,COMMBLKS      PREFIX DEFINED?
         BE    EXIT0030              NO
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG21L),EMSG21
         BAL   R9,PRT0000            PRINT MESSAGE
EXIT0030 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE02   DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
XFFFF    DC    X'FFFFFFFF'
H4096    DC    H'4096'               4K IN DECIMAL
PMIN     DC    PL3'&MINL'            MINIMUM LINE COUNT ALLOWED
PGMFLAG  DC    X'00'
$SUBH    EQU   X'80'                 SUBHEADING PRINTED
$ASMOPEN EQU   X'40'                 ASSEMBLER INPUT DCB OPEN
$BLANK   EQU   X'20'
         SPACE 1
DISPIN   DC    CL8' '
         DC    X'00'                 PAD FOR PACKING
DISPOUT  DC    XL4'00000000'
         DC    X'00'                 PAD FOR PACKING
         SPACE 1
         DC    C'0'
LINEIN   DC    CL3'000'
LINEOUT  DC    PL3'0'
         SPACE 1
SAVEBEGN DS    XL4                   SAVED BEGINNING DISPLACEMENT
SAVEEND  DS    XL4                   SAVED ENDING DISPLACEMENT
SUBHEAD  DC    C' CONTROL STATEMENTS '
SUBHEADL EQU   *-SUBHEAD
*---------------------------------------------------------------------*
*              WARNING MESSAGES                                       *
*---------------------------------------------------------------------*
WMSG02   DC    C'DISASM0202W Sequentially numbered labels have already +
               been requested'
WMSG02L  EQU   *-WMSG02
*---------------------------------------------------------------------*
*              ERROR MESSAGES                                         *
*---------------------------------------------------------------------*
EMSG03   DC    C'DISASM0203E Invalid control statement'
EMSG03L  EQU   *-EMSG03
EMSG04   DC    C'DISASM0204E Extraneous data in register parameter'
EMSG04L  EQU   *-EMSG04
EMSG05   DC    C'DISASM0205E Invalid register reference'
EMSG05L  EQU   *-EMSG05
EMSG06   DC    C'DISASM0206E End displacement is required when begin di+
               splacement is given'
EMSG06L  EQU   *-EMSG06
EMSG07   DC    C'DISASM0207E Begin displacement is larger than end disp+
               lacement'
EMSG07L  EQU   *-EMSG07
EMSG08   DC    C'DISASM0208E Bad hex digit in displacement'
EMSG08L  EQU   *-EMSG08
EMSG09   DC    C'DISASM0209E Too many digits in displacement'
EMSG09L  EQU   *-EMSG09
EMSG10   DC    C'DISASM0210E End displacement not allowed unless start +
               displacement is specified'
EMSG10L  EQU   *-EMSG10
EMSG11   DC    C'DISASM0211E Label prefix cannot be blank'
EMSG11L  EQU   *-EMSG11
EMSG12   DC    C'DISASM0212E Label prefixes must be 4 characters or les+
               s'
EMSG12L  EQU   *-EMSG12
EMSG13   DC    C'DISASM0213E Base register is blank'
EMSG13L  EQU   *-EMSG13
EMSG14   DC    C'DISASM0214E Base register name exceeds 3 characters in+
                length'
EMSG14L  EQU   *-EMSG14
EMSG15   DC    C'DISASM0215E ''DATA'' is reserved for data area prefixe+
               s, choose another prefix'
EMSG15L  EQU   *-EMSG15
EMSG16   DC    C'DISASM0216E Invalid digit in LINE/LINES/MAXLINES state+
               ment'
EMSG16L  EQU   *-EMSG16
EMSG17   DC    C'DISASM0217E Line count value in LINE/LINES/MAXLINES st+
               atement is too long or contains extraneous data'
EMSG17L  EQU   *-EMSG17
EMSG18   DC    C'DISASM0218E Line count value in LINE/LINES/MAXLINES st+
               atement is below minimum allowed'
EMSG18L  EQU   *-EMSG18
EMSG19   DC    C'DISASM0219E Label prefix has already been defined, cho+
               ose one or the other'
EMSG19L  EQU   *-EMSG19
EMSG20   DC    C'DISASM0220E Label prefix must be defined when base reg+
               isters are defined'
EMSG20L  EQU   *-EMSG20
EMSG21   DC    C'DISASM0221E Label prefix not valid unless base registe+
               r(s) are defined'
EMSG21L  EQU   *-EMSG21
EMSG22   DC    C'DISASM0222E This area overlaps area at '
EMSG22A  DC    CL4' '
         DC    C' to '
EMSG22B  DC    CL4' '
         DC    C' '
EMSG22L  EQU   *-EMSG22
EMSG23   DC    C'DISASM0223E Starting displacement is larger than endin+
               g displacement'
EMSG23L  EQU   *-EMSG23
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
CTLSTMT  DS    0CL80
CTLTYPE  DC    CL09' '               STATEMENT TYPE
CTLDATA  DC    CL71' '               RELATED DATA
CTLSTMTL EQU   *-CTLSTMT
         ORG   CTLDATA
CTL10    DS    CL10                  DATA STARTING AT COLUMN 10
CTL20    DS    CL10                  DATA STARTING AT COLUMN 20
CTL30    DS    CL10                  DATA STARTING AT COLUMN 30
CTL40    DS    CL10                  DATA STARTING AT COLUMN 40
CTL50    DS    CL10                  DATA STARTING AT COLUMN 50
CTL60    DS    CL10                  DATA STARTING AT COLUMN 60
CTL70    DS    CL10                  DATA STARTING AT COLUMN 70
         ORG   CTLSTMT+80
         SPACE 1
CNTLTBLE DS    0X
         DC    CL09'ABEND    ',AL4(ABEND000)
         DC    CL09'ASM START',AL4(ASM0000)
CNTLBASE DC    CL09'BASE     ',AL4(BASE0000)
         DC    CL09'CSECT    ',AL4(CSCT0000)
CNTLDATA DC    CL09'DATA     ',AL4(DATA0000)
         DC    CL09'LABEL    ',AL4(LABL0000)
         DC    CL09'LINE     ',AL4(LINE0000)
         DC    CL09'LINES    ',AL4(LINE0000)
         DC    CL09'MAX LINES',AL4(LINE0000)
         DC    CL09'MODULE   ',AL4(MOD0000)
         DC    CL09'NO B2    ',AL4(NOB20000)
         DC    CL09'NOB2     ',AL4(NOB20000)
         DC    CL09'NO FLOAT ',AL4(NOFLOAT0)
         DC    CL09'NOFLOAT  ',AL4(NOFLOAT0)
         DC    CL09'PREFIX   ',AL4(LABL0000)
         DC    CL09'RLD WARN ',AL4(RLD0000)
         DC    CL09'SEQ LABEL',AL4(SEQ0000)
CNTLUSNG DC    CL09'USING    ',AL4(USNG0000)
         DC    X'FF'
CNTLASME DC    CL09'ASM END'
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              DCB'S                                                  *
*                                                                     *
*---------------------------------------------------------------------*
DISIN    DCB   DDNAME=DISIN,         CONTROL STATEMENT DCB             +
               DSORG=PS,             .. SEQUENTIAL                     +
               EODAD=EXIT0000,       .. EOF ADDRESS                    +
               LRECL=80,             .. MUST BE LRECL=80               +
               MACRF=GM              .. GET-MOVE MODE
SYSIN    DCB   DDNAME=SYSIN,         ASSEMBLER'S INPUT DCB             +
               DSORG=PS,             .. SEQUENTIAL                     +
               LRECL=80,             .. MUST BE LRECL=80               +
               BLKSIZE=3120,         .. BLOCK SIZE                     +
               MACRF=PM              .. PUT-MOVE MODE
REGTBL1  DS    0C                    1-CHARACTER REGISTER NAMES
         REG   0,0
         REG   1,1
         REG   2,2
         REG   3,3
         REG   4,4
         REG   5,5
         REG   6,6
         REG   7,7
         REG   8,8
         REG   9,9
         REG   A,10
         REG   B,11
         REG   C,12
         REG   D,13
         REG   E,14
         REG   F,15
         DC    X'FF'
REGTBL2  DS    0C                    2-CHARACTER REGISTER NAMES
         REG   R0,0
         REG   R1,1
         REG   R2,2
         REG   R3,3
         REG   R4,4
         REG   R5,5
         REG   R6,6
         REG   R7,7
         REG   R8,8
         REG   R9,9
         REG   10,10
         REG   11,11
         REG   12,12
         REG   13,13
         REG   14,14
         REG   15,15
         REG   RA,10
         REG   RB,11
         REG   RC,12
         REG   RD,13
         REG   RE,14
         REG   RF,15
         DC    X'FF'
REGTBL3  DS    0C                    3-CHARACTER REGISTER NAMES
         REG   R10,10
         REG   R11,11
         REG   R12,12
         REG   R13,13
         REG   R14,14
         REG   R15,15
         DC    X'FF'
         LTORG
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              REGISTER TABLE                                         *
*                                                                     *
*---------------------------------------------------------------------*
REGDSCT  DSECT
REGNAME  DS    CL3                   REGISTER NAME
REGVALUE DS    AL1                   VALUE USED IN INSTRUCTIONS
REGL     EQU   *-REGDSCT
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM02
./ ADD NAME=DISASM03
         TITLE 'DISASM03 - LOAD MODULE READER'
         MACRO
         PROC   &TYPE,&PROC1,&PROC2,&FLAGS
         LCLC   &A,&OPTS
&OPTS    SETC   '00'
         AIF    (T'&FLAGS EQ 'O').PROC20
&OPTS    SETC   '&FLAGS'
.PROC20  ANOP
         DC     X'&TYPE'             RECORD TYPE
         DC     AL1(&OPTS)           FLAGS
         AIF    (T'&PROC1 EQ 'O').PROC30
         DC     AL2(&PROC1-DISASM03) DISPLACEMENT TO PROCESSING ROUTINE
         AGO    .PROC40
.PROC30  ANOP
         DC     AL2(0)               NO PROCESSING ROUTINE
.PROC40  ANOP
         AIF    (T'&PROC2 EQ 'O').PROC50
         DC     AL2(&PROC2-DISASM00) DISPLACEMENT TO MODULE'S ADDRESS
         MEXIT
.PROC50  ANOP
         DC     AL2(0)               NO EXTERNAL PROCESSING MODULE
         MEND
         COPY   DISASMGB
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM03                                             *
*                                                                    *
*  Function:                                                         *
*   Object module reader.  Many of the fields in the directory       *
*   entry are interpreted and printed (module size, whether or not   *
*   the module has the RENT, REUS, REFR flags, etc).  If the module  *
*   name is an alias, the real module's directory info will be       *
*   printed also.  The ESD and RLD info from the module is printed   *
*   and control blocks built that will be used later for generating  *
*   labels and ENTRY statements.  Module DISASM04 is called as a     *
*   sub-function to interpret ESD data.  Module DISASM05 is called   *
*   as a sub-function to interpret RLD data.                         *
*                                                                    *
*   If the module is successfully read and the requested CSECT       *
*   located, COMMTXT will be set to the CSECT's storage address,     *
*   COMMCSAD will be the CSECT's address within the load module,     *
*   COMMCSEP will be the load module's entry point, COMMCSEA will    *
*   be the CSECT's ending address within the load module, and        *
*   COMMCSLN will the the CSECT's length.                            *
*                                                                    *
*--------------------------------------------------------------------*
DISASM03 CSECT
DISASM03 AMODE 31
DISASM03 RMODE 24
         USING DISASM03,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     MOD0000               SKIP EYECATCHER
         DC    CL8'DISASM03'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
MOD0000  DS    0H
         LA    R1,SAVE03             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (DISMOD,INPUT)        OPEN DISMOD
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   COMMSUBH(SUBHD1L),SUBHD1
         LA    R1,SUBHD1L            SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
         MVC   DIRMEM,COMMMOD        SET MEMBER NAME = MODULE NAME
MOD0010  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         BLDL  DISMOD,BLDLIST        ISSUE BLDL
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         LTR   R15,R15               BLDL SUCCESSFUL?
         BNZ   ERR0010               NO
         MVC   MSG01MEM,DIRMEM       SET MEMBER NAME
         TM    DIRINDS,$ALIAS        IS THIS AN ALIAS?
         BO    MOD0020               YES
         MVC   MSG01ALS,NO           NOT AN ALIAS
         B     MOD0030
MOD0020  DS    0H
         MVC   MSG01ALS,YES          MEMBER IS AN ALIAS
MOD0030  DS    0H
         UNPK  MSG01TXT(7),DIRTTTR(4)
         MVZ   MSG01TXT,COMM0F0F     TURN OFF ZONES
         TR    MSG01TXT,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG01TXT+6,C' '       RESTORE BLANK
         UNPK  MSG01NTE(7),DIRNTTR(4)
         MVZ   MSG01NTE,COMM0F0F     TURN OFF ZONES
         TR    MSG01NTE,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG01NTE+6,C' '       RESTORE BLANK
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,DIR#NOTE           NUMBER OF NOTE LIST ENTRIES
         CVD   R1,COMMDWRD           CONVERT TO DECIMAL
         MVC   MSG02NTE,=X'40202120' INITIALIZE WITH EDIT WORD
         ED    MSG02NTE,COMMDWRD+6   EDIT NOTE LIST
         UNPK  MSG02SZ(7),DIRMSIZE(4) UNPACK MODULE SIZE
         MVZ   MSG02SZ,COMM0F0F      TURN OFF ZONES
         TR    MSG02SZ,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   MSG02SZ+6,C' '        RESTORE BLANK
         MVC   COMMCSEP+1,DIREPA     SAVE ENTRY POINT
         UNPK  MSG02EPA(7),DIREPA(4) UNPACK ENTRY POINT
         MVZ   MSG02EPA,COMM0F0F     TURN OFF ZONES
         TR    MSG02EPA,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02EPA+6,C' '       RESTORE BLANK
         MVC   MSG02MEM,COMMBLKS     CLEAR MEMBER NAME
         TM    DIRINDS,$ALIAS        AN ALIAS?
         BNO   MOD0040               NO
         MVC   MSG02MEM,DIRRMEM      SET REAL MEMBER NAME
MOD0040  DS    0H
         MVC   PRTDATA(MSG02L),MSG02 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         LA    R1,DIRSCTR            START OF VARIABLE PORTION
         TM    DIRATTR3,$SSI         SSI INFO PRESENT?
         BNO   MOD0050               NO
         LA    R1,L'DIRSSI(R1)       SKIP SSI INFO
MOD0050  DS    0H
         TM    DIRINDS,$ALIAS        IS THIS AN ALIAS?
         BNO   MOD0060               NO
         LA    R1,L'DIRMEP+L'DIRRMEM(R1)
MOD0060  DS    0H
         TM    DIRATTR1,$SCTR        SCATTER LOAD?
         BNO   MOD0070               NO
         LA    R1,8(R1)              SKIP SCATTER STUFF
MOD0070  DS    0H
         UNPK  MSG03ATH(3),1(2,R1)   UNPACK AUTH CODE
         MVZ   MSG03ATH,COMM0F0F     TURN OFF ZONES
         TR    MSG03ATH,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG03ATH+2,C' '       RESTORE BLANK
         MVC   MSG03SSI,COMMBLKS     CLEAR SSI INFO
         TM    DIRATTR3,$SSI         SSI INFO PRESENT?
         BNO   MOD0100               NO
         LA    R1,DIRSCTR            START OF VARIABLE PORTION
         TM    DIRATTR1,$SCTR        SCATTER LOAD?
         BNO   MOD0080               NO
         TM    DIRINDS,$ALIAS        ALIAS?
         BO    MOD0100               YES.. NO SSI
         LA    R1,8(R1)              SKIP SCATTER STUFF
         B     MOD0090
MOD0080  DS    0H
         TM    DIRINDS,$ALIAS        ALIAS?
         BNO   MOD0090               NO
         LA    R1,L'DIRMEP+L'DIRRMEM(R1)
MOD0090  DS    0H
         UNPK  MSG03SSI(9),0(5,R1)   UNPACK SSI INFO
         MVZ   MSG03SSI,COMM0F0F     TURN OFF ZONES
         TR    MSG03SSI,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG03SSI+8,C' '       RESTORE BLANK
MOD0100  DS    0H
         TM    DIRATTR1,$RENT        RE-ENTRANT?
         BO    MOD0110               YES
         MVC   MSG03RNT,NO           NOT RE-ENTRANT
         B     MOD0120
MOD0110  DS    0H
         MVC   MSG03RNT,YES          RE-ENTRANT
MOD0120  DS    0H
         TM    DIRATTR1,$REUS        REUSABLE?
         BO    MOD0130               YES
         MVC   MSG03RUS,NO           NOT REUSABLE
         B     MOD0140
MOD0130  DS    0H
         MVC   MSG03RUS,YES          REUSABLE
MOD0140  DS    0H
         MVC   PRTDATA(MSG03L),MSG03 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         TM    DIRATTR1,$OVRLY       OVERLAY?
         BO    MOD0150               YES
         MVC   MSG04OVR,NO           NOT OVERLAY
         B     MOD0160
MOD0150  DS    0H
         MVC   MSG04OVR,YES          OVERLAY
MOD0160  DS    0H
         TM    DIRATTR1,$SCTR        SCATTER LOAD?
         BO    MOD0170               YES
         MVC   MSG04SCT,NO           NOT SCATTER LOAD
         B     MOD0180
MOD0170  DS    0H
         MVC   MSG04SCT,YES          SCATTER LOAD
MOD0180  DS    0H
         TM    DIRATTR1,$EXEC        EXECUTABLE?
         BO    MOD0190               YES
         MVC   MSG04EXC,NO           NOT EXECUTABLE
         B     MOD0200
MOD0190  DS    0H
         MVC   MSG04EXC,YES          EXECUTABLE
MOD0200  DS    0H
         TM    DIRATTR2,$REFR        REFRESHABLE?
         BO    MOD0210               YES
         MVC   MSG04RFR,NO           NO REFRESHABLE
         B     MOD0220
MOD0210  DS    0H
         MVC   MSG04RFR,YES          REFRESHABLE
MOD0220  DS    0H
         MVC   PRTDATA(MSG04L),MSG04 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         TM    DIRINDS,$ALIAS        AN ALIAS?
         BNO   MOD0230               NO
         MVC   PRTDATA(MSG05L),MSG05 SET MESSAGE
         MVI   PRTCC,C'0'            SET DOUBLE SPACE
         BAL   R10,PRT0000           PRINT MESSAGE
         MVC   DIRMEM,DIRRMEM        CHANGE NAMES
         B     MOD0010               BLDL FOR REAL MEMBER
MOD0230  DS    0H
         MVI   DIRMTTRZ+3,X'00'      FORCE ZERO
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
*        POINT DISMOD,DIRMTTRZ,TYPE=REL
         FIND  DISMOD,COMMMOD,D
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         LTR   R15,R15               POINT SUCCESSFUL?
         BZ    MOD0260               YES
         STC   R15,POINTR15          SAVE RETURN CODE
         STC   R0,POINTR0            SAVE REASON CODE
         LA    R1,PNTMSGS            MESSAGE TABLE ADDRESS
MOD0240  DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    ERR0070               YES
         CLC   PNTCODE,0(R1)         MESSAGE FOUND?
         BE    MOD0250               YES
         LA    R1,PNTMSGL(R1)        NEXT MESSAGE
         B     MOD0240               LOOP
MOD0250  DS    0H
         MVC   PRTDATA(PNTMSGL-2),2(R1)
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
MOD0260  DS    0H
         TM    MODFLAG,$MODEOF       EOF FLAG ON?
         BO    EXIT0000              YES, EXIT
         BAL   R10,READ0000          READ A RECORD
         ITRACE ID=FINDPROC,                                           +
               DATA1=(R3)            .. CAPTURE 8 BYTES OF DATA
         LA    R9,PROCTBLE           FIRST PROCESSOR TABLE ENTRY
         USING PROCDSCT,R9           DEFINE BASE
MOD0270  DS    0H
         CLI   0(R9),X'FF'           END OF TABLE
         BE    MOD0310               NOT LOCATED.. FORGET IT
         CLC   PROCTYPE,0(R3)        RECORD TYPE FOUND?
         BE    MOD0280               YES
         LA    R9,PROCL(R9)          NEXT ENTRY
         B     MOD0270               LOOP
MOD0280  DS    0H
         ITRACE ID=PROCFND,                                            +
               DATA1=PROCTYPE
         TM    PROCFLAG,$CSECT       CSECT REQUIRED?
         BNO   MOD0290               NO
         TM    COMMFLAG,$CSECT       CSECT LOCATED?
         BNO   ERR0060               NO
         TM    0(R3),X'08'           END OF MODULE?
         BNO   MOD0290               NO
         ITRACE ID=EOF
         OI    MODFLAG,$MODEOF       SET EOF FLAG
MOD0290  DS    0H
         SR    R15,R15               CLEAR REGISTER
         ICM   R15,3,PROCXTNL        EXTERNAL PROCESSING MODULE DISP
         BZ    MOD0300               NO EXTERNAL MODULE
         AR    R15,R11               PLUS DISASM00 BASE ADDRESS
         L     R15,0(R15)            RECORD PROCESSOR ENTRY POINT
         ITRACE ID=CALLXTNL,         CALLING EXTERNAL RECORD PROCESSOR +
               RDATA1=R15            .. PROCESSOR'S ENTRY POINT ADDR
         BALR  R14,R15               LINK TO PROCESSOR
MOD0300  DS    0H
         SR    R15,R15               CLEAR REGISTER
         ICM   R15,3,PROCINTL        INTERNAL PROCESSING RTN DISP
         BZ    MOD0260               NO INTERNAL PROCESSING MODULE
         AR    R15,R12               PLUS BASE REG
         ITRACE ID=CALLINTL,         CALLING INTERNAL RECORD PROCESSOR +
               RDATA1=R15            .. PROCESSOR'S ENTRY POINT ADDR
         BR    R15                   CALL INTERNAL RECORD PROCESSOR
MOD0310  DS    0H
         ITRACE ID=NOPROC            NO PROCESSOR FOR THIS RECORD TYPE
         B     MOD0260               READ NEXT RECORD
CSCT0000 DS    0H
         ITRACE ID=CSECT
         L     R3,COMMIO             I/O AREA ADDRESS
         TM    0(R3),X'02'           RLD AND CSECT?
         BO    CSCT0010              YES
         LA    R4,16(R3)             CESD ENTRY NUMBER ADDRESS
         B     CSCT0020
CSCT0010 DS    0H
         LH    R4,6(R3)              RLD SECTION LENGTH
         LA    R4,16(R3,R4)          CESD ENTRY NUMBER ADDRESS
CSCT0020 DS    0H
         LH    R5,4(R3)              CSECT INFO LENGTH
         SRL   R5,2                  CONVERT TO NUMBER OF ENTRIES
         ITRACE ID=CSECTNBR,                                           +
               RDATA1=R4,            .. CSECT DATA'S ADDRESS           +
               RDATA2=R5             .. NUMBER OF ENTRIES
         SR    R6,R6                 INITIALIZE OFFSET
         SR    R7,R7                 ASSUME LENGTH IS ZERO
CSCT0030 DS    0H
         CLC   COMMESID,0(R4)        CORRECT ESD ID?
         BE    CSCT0040              YES
         AH    R6,2(R4)              PLUS LENGTH OF THIS ESD
         LA    R4,4(R4)              NEXT ESD ID ENTRY
         BCT   R5,CSCT0030           LOOP
         B     CSCT0050
CSCT0040 DS    0H
         ITRACE ID=ESDFND,                                             +
               RDATA1=R4             .. ESD ADDRESS
         LH    R7,2(R4)              TEXT LENGTH
CSCT0050 DS    0H
         SR    R2,R2                 CLEAR REGISTER
         ICM   R2,7,9(R3)            ASSIGNED ADDRESS
         ITRACE ID=READTEXT
         BAL   R10,READ0000          READ NEXT TEXT BLOCK
         LTR   R7,R7                 TEXT LENGTH ZERO?
         BZ    MOD0260               YES
         AR    R2,R6                 ADDRESS + DISPLACEMENT
         S     R2,COMMCSAD           MINUS STARTING ADDRESS
         A     R2,COMMTXT            PLUS TEXT'S BASE ADDRESS
         A     R6,COMMIO             DISP + I/O BASE
         ITRACE ID=MOVETEXT
         LR    R3,R7                 COPY LENGTH
         MVCL  R2,R6                 COPY TEXT (WHEW!)
         B     MOD0260               DONE
READ0000 DS    0H
         L     R3,COMMIO             I/O BUFFER'S ADDRESS
         ITRACE ID=READ,                                               +
               RDATA1=R3
         XC    MODDECB,MODDECB       CLEAR ECB
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         READ  MODDECB,              READ LOAD MODULE                  +
               SF,                   .. SEQUENTIALLY FORWARD           +
               DISMOD,               .. FROM LODLIB DATA SET           +
               (R3),                 .. I/O AREA'S ADDRESS             +
               32760                 .. LENGTH FROM DCB
         CHECK MODDECB               WAIT FOR READ
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         BR    R10                   RETURN
EOD00000 DS    0H
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         ITRACE ID=EOD
         OI    COMMFLAG,$ERROR+$ABORT SET FLAGS
         MVC   PRTDATA(EMSG3L),EMSG3 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              EXIT
ERR0010  DS    0H
         MVI   BLDLR0,0              INITIALIZE REASON CODE
         STC   R15,BLDLR15           SAVE R15
         CLI   BLDLR15,8             R15 = 8?
         BNE   ERR0020               NO
         STC   R0,BLDLR0             SAVE R0
ERR0020  DS    0H
         LA    R1,BLDLMSGS           FIRST BLDL MESSAGE
ERR0030  DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    ERR0050               YES
         CLC   BLDLCODE,0(R1)        PROPER MESSAGE FOUND?
         BE    ERR0040               YES
         LA    R1,BLDLMSGL(R1)       NEXT MESSAGE
         B     ERR0040               LOOP
ERR0040  DS    0H
         MVC   PRTDATA(BLDLMSGL-2),2(R1)
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ABORT       SET ABORT FLAG
         B     EXIT0000              AND EXIT
ERR0050  DS    0H
         ITRACE ID=INVBLDLC,         INVALID BLDLCODE                  +
               DATA1=BLDLCODE        ..
         ABEND ABEND002,DUMP,,USER
ERR0060  DS    0H
         MVC   PRTDATA(EMSG01L),EMSG01
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0070  DS    0H
         MVC   PRTDATA(EMSG02L),EMSG02
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
PRT0000  DS    0H
         TM    MODFLAG,$SUBH         HAS SUB-HEADING BEEN PRINTED?
         BO    PRT0010               YES
         OI    MODFLAG,$SUBH         SET FLAG
         MVI   PRTCMD,$PRTSUBH       SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
PRT0010  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R10                   RETURN
EXIT0000 DS    0H
         ITRACE ID=EXIT
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         CLOSE DISMOD                CLOSE DISMOD
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE03   DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
* ------------------------------------------------------------------- *
BLDLIST  DS    0F                                                     
         DC    H'1'                  ONE MEMBER                       
         DC    H'80'                 LENGTH PER MEMBER                
DIRDATA  DS    CL80                                                   
         ORG   DIRDATA                                                
DIRMEM   DC    CL8' '                MEMBER NAME                      
DIRMTTRZ DC    XL4'00000000'         MEMBER'S RELATIVE ADDRESS        
         DC    XL1'00'                                                
DIRINDS  DC    X'00'                 INDICATORS                       
$ALIAS   EQU   X'80'                 .. MEMBER IS AN ALIAS            
DIRTTTR  DC    XL3'000000'           TEXT'S RELATIVE ADDRESS          
         DC    XL1'00'                                                
DIRNTTR  DC    XL3'000000'           NOTE LIST (OR SCATTER LIST) TTR
DIR#NOTE DC    X'00'                 NUMBER OF NOTE LIST ENTRIES      D
DIRATTR1 DC    X'00'                 ATTRIBUTE FLAGS                  A
$RENT    EQU   X'80'                 .. RE-ENTRANT                    T
$REUS    EQU   X'40'                 .. REUSABLE                      A
$OVRLY   EQU   X'20'                 .. OVERLAY
$TEST    EQU   X'10'                 .. UNDER TEST                    R
$LDONLY  EQU   X'08'                 .. LOAD ONLY                     E
$SCTR    EQU   X'04'                 .. SCATTER FORMAT                T
$EXEC    EQU   X'02'                 .. EXECUTABLE                    U
$1TEXT   EQU   X'01'                 .. 1 TEXT, NO RLD RECORDS        R
DIRATTR2 DC    X'00'                 ATTRIBUTE FLAGS                  N
$NOLINK1 EQU   X'80'                 .. NOT PROCESSABLE BY LINK EDIT  E
$ORGZERO EQU   X'40'                 .. TEXT ORIGIN IS ZERO           D
$EPZERO  EQU   X'20'                 .. ENTRY POINT IS ZERO
$NORLD   EQU   X'10'                 .. NO RLD RECORDS                B
$NOLINK2 EQU   X'08'                 .. NOT PROCESSABLE BY LINK EDIT  Y
$TESTRAN EQU   X'04'                 .. CONTAINS TESTRAN SYMBOLS
$LINK    EQU   X'02'                 .. CREATED BY LINKAGE EDITOR     B
$REFR    EQU   X'01'                 .. REFRESHABLE                   L
DIRMSIZE DC    XL3'000000'           MODULE'S SIZE                    D
DIRTXTL  DC    XL2'0000'             TEXT RECORD SIZE                 L
DIREPA   DC    XL3'000000'           ENTRY POINT
DIRATTR3 DC    X'00'                                                  
$OSLINK  EQU   X'80'                 .. PROCESSED BY O/S LINK EDITOR  
$PALIGN  EQU   X'20'                 .. PAGE ALIGNMENT REQUIRED       
$SSI     EQU   X'10'                 .. SSI PRESENT                   
DIRATTR4 DC    X'00'                 ATTRIBUTES                       
$RANY    EQU   X'10'                 .. RMODE=ANY                     
$AA31    EQU   X'08'                 .. AMODE=31 (ALIAS)              
$AA24    EQU   X'04'                 .. AMODE=24 (ALIAS)              
$AM31    EQU   X'02'                 .. AMODE=31 (MAIN)               
$AM24    EQU   X'01'                 .. AMODE=24 (MAIN)               
DIR#RLD  DC    X'00'                 NUMBER OF RLD'S AFTER 1ST TEXT   
DIRSCTR  DC    XL2'0000'             SCATTER LIST LENGTH              
DIRTRAN  DC    XL2'0000'             TRANSLATION TABLE LENGTH         
DIRTCEST DC    XL2'0000'             CESD NUMBER FOR 1ST TXT RECORD   
DIRECESD DC    XL2'0000'             CESD NUMBER FOR ENTRY POINT      
         ORG   DIRSCTR               RESET TO VARIABLE PORTION        
DIRMEP   DC    XL3'000000'           ENTRY POINT OF MEMBER NAME       
DIRRMEM  DC    CL8' '                REAL MEMBER NAME IF ALIAS        
DIRSSI   DC    XL4'00000000'         SSI INFO                         
DIRAUTHL DC    XL1'00'               AUTH CODE LENGTH                 
DIRAUTHC DC    XL1'00'               AUTH CODE                        
         ORG   DIRDATA+80                                             
* ------------------------------------------------------------------- *
         SPACE 1
MODFLAG  DC    X'00'                 PROGRAM FLAGS/SWITCHES
$SUBH    EQU   X'80'                 .. SUBHEADING PRINTED
$MODEOF  EQU   X'40'                 .. END OF CONTROL DATA
BLDLCODE DS    0XL2                  BLDL RETURN CODE/REASON CODE
BLDLR15  DC    X'00'                 .. R15
BLDLR0   DC    X'00'                 .. R0
PNTCODE  DS    0XL2                  POINT RETURN CODE/REASON CODE
POINTR15 DC    X'00'                 .. R15
POINTR0  DC    X'00'                 .. R0
NO       DC    CL3'NO'
YES      DC    CL3'YES'
SUBHD1   DC    C' LOAD MODULE ATTRIBUTES '
SUBHD1L  EQU   *-SUBHD1
MSG01    DS    0C
         DC    CL12'MEMBER NAME:'
MSG01MEM DC    CL08' '
         DC    CL10' '
         DC    CL06'ALIAS:'
MSG01ALS DC    CL03' '
         DC    CL21' '
         DC    CL09'TEXT TTR:'
MSG01TXT DC    CL06' '
         DC    CL15' '
         DC    CL13'NOTELIST TTR:'
MSG01NTE DC    CL06' '
         DC    C' '
MSG01L   EQU   *-MSG01
MSG02    DS    0C
         DC    CL17'NOTELIST ENTRIES:'
MSG02NTE DC    CL04' '
         DC    CL09' '
         DC    CL17'LOAD MODULE SIZE:'
MSG02SZ  DC    CL06' '
         DC    CL07' '
         DC    CL12'ENTRY POINT:'
MSG02EPA DC    CL06' '
         DC    CL12' '
         DC    CL17'REAL MEMBER NAME:'
MSG02MEM DC    CL8' '
MSG02L   EQU   *-MSG02
MSG03    DS    0C
         DC    CL10'AUTH CODE:'
MSG03ATH DS    CL02' '
         DC    CL18' '
         DC    CL09'SSI INFO:'
MSG03SSI DC    CL08' '
         DC    CL13' '
         DC    CL11'RE-ENTRANT:'
MSG03RNT DC    CL03' '
         DC    CL16' '
         DC    CL09'REUSABLE:'
MSG03RUS DC    CL03' '
MSG03L   EQU   *-MSG03
MSG04    DS    0C
         DC    CL08'OVERLAY:'
MSG04OVR DS    CL03' '
         DC    CL19' '
         DC    CL13'SCATTER LOAD:'
MSG04SCT DC    CL03' '
         DC    CL14' '
         DC    CL11'EXECUTABLE:'
MSG04EXC DC    CL03' '
         DC    CL16' '
         DC    CL12'REFRESHABLE:'
MSG04RFR DC    CL03' '
MSG04L   EQU   *-MSG04
MSG05    DC    48C'-'
         DC    CL24' REAL MODULE ATTRIBUTES '
         DC    48C'-'
MSG05L   EQU   *-MSG05
EMSG01   DC    C'DISASM0301E Specified CSECT not found'
EMSG01L  EQU   *-EMSG01
EMSG02   DC    C'DISASM0302E Unknown return code from POINT macro'
EMSG02L  EQU   *-EMSG02
EMSG3    DC    C'DISASM0303E DCB EODAD routine driven, end of control r+
               ecords not detected'
EMSG3L   EQU   *-EMSG3
BLDLMSGS DS    0C
         DC    X'0400',CL55'DISASM0304E Module does not exist in DISMOD+
                library'
BLDLMSGL EQU   *-BLDLMSGS
         DC    X'0800',CL55'DISASM0305E Permanent I/O error'
         DC    X'0804',CL55'DISASM0306E Insufficient virtual storage'
         DC    X'0808',CL55'DISASM0307E DEB not in KEY 0-7'
         DC    X'FF'
PNTMSGS  DS    0C
         DC    X'0400',CL55'DISASM0308E Device does not support block i+
               dentifier'
PNTMSGL  EQU   *-PNTMSGS
         DC    X'0801',CL55'DISASM0309E Incorrect parameter'
         DC    X'0802',CL55'DISASM0310E Incorrect DEB or DEBCHK error'
         DC    X'0803',CL55'DISASM0311E Environmental error'
         DC    X'080B',CL55'DISASM0312E Unsuccessful call to ESTAE'
         DC    X'080C',CL55'DISASM0313E Unsuccessful GETMAIN'
         DC    X'0C00',CL55'DISASM0314E Input/output error'
         DC    X'FF'
PROCTBLE DS    0X
         PROC  01,CSCT0000,,$CSECT            CSECT RECORDS
         PROC  02,,A05,$CSECT                 RLD RECORDS
         PROC  03,CSCT0000,A05,$CSECT         CSECT AND RLD RECORDS
         PROC  05,CSCT0000,,$CSECT            CSECT RECORDS
         PROC  06,,A05,$CSECT                 RLD RECORDS
         PROC  07,CSCT0000,A05,$CSECT         CSECT AND RLD RECORDS
         PROC  0D,CSCT0000,,$CSECT            CSECT RECORDS
         PROC  0E,,A05,$CSECT                 RLD RECORDS
         PROC  0F,CSCT0000,A05,$CSECT         CSECT AND RLD RECORDS
         PROC  20,,A04                        CESD RECORDS
         DC    X'FF'
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              OBJECT MODULE LIBRARY DCB                              *
*                                                                     *
*---------------------------------------------------------------------*
DISMOD   DCB   DDNAME=DISMOD,        OBJECT MODULE LIBRARY DCB         +
               DSORG=PO,             .. PARTITIONED DATA SET           +
               RECFM=U,              .. UNDEFINED RECORD FORMAT        +
               EODAD=EOD00000,       .. END OF DATA                    +
               MACRF=R               .. READ ONLY
         SPACE 2
         LTORG
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              PROCESSOR TABLE                                        *
*                                                                     *
*---------------------------------------------------------------------*
PROCDSCT DSECT
PROCTYPE DS   X                     RECORD CODE
PROCFLAG DS   X                     FLAGS
PROCINTL DS   AL2                   INTERNAL PROCESSING RTN
PROCXTNL DS   AL2                   EXTERNAL PROCESSING MODULE
PROCL    EQU  *-PROCDSCT
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM03
./ ADD NAME=DISASM04
         TITLE 'DISASM04 - ESD DATA PROCESSOR'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM04                                             *
*                                                                    *
*  Function:                                                         *
*   Process ESD data as a sub-function of DISASM03.  ESD data is     *
*   printed and saved in ESDDATA blocks for use in generating labels *
*   and ENTRY statements.                                            *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM04 CSECT
DISASM04 AMODE 31
DISASM04 RMODE 24
         USING DISASM04,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     ESD0000               SKIP EYECATCHER
         DC    CL8'DISASM04'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
ESD0000  DS    0H
         LA    R1,SAVE04             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         ITRACE ID=ENTRY
         MVC   COMMSUBH(SUBHEADL),SUBHEAD
         LA    R1,SUBHEADL           SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
         MVI   COMMSUBL,X'FF'        INDICATE NON-CENTERED
         TM    ESDFLAG,$SUBH         INITIAL SUB HEADING PRINTED?
         BO    ESD0010               YES
         OI    ESDFLAG,$SUBH         SET FLAG
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         MVC   PRTDATA(SUBHEADL-1),SUBHEAD+1
         BAL   R10,PRT0000           PRINT MESSAGE
ESD0010  DS    0H
         LA    R4,COMMESD            FIRST ESD RECORD'S ADDRESS
         USING ESDDATA,R4            DEFINE BASE
ESD0020  DS    0H
         OC    ESDNEXT,ESDNEXT       MORE ON CHAIN?
         BZ    ESD0030               NO
         L     R4,ESDNEXT            FOLLOW THE CHAIN
         B     ESD0020               LOOP
ESD0030  DS    0H
         L     R3,COMMIO             I/O AREA ADDRESS
         MVC   SAVEESD,4(R3)         SAVE ESD ID OF FIRST ITEM
         LH    R5,6(R3)              SIZE OF ESD DATA
         SRL   R5,4                  COMPUTE NUMBER OF ESD ENTRIES
         LA    R6,8(R3)              FIRST ESD ITEM
         ITRACE ID=PROCESD,                                            +
               RDATA1=R5,            .. NUMBER OF ESD ENTRIES          +
               RDATA2=R6             .. FIRST ESD DATA ADDRESS
ESD0040  DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR ESD DATA      +
               LV=ESDDATAL,          .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWESD,                                             +
               RDATA1=R1             .. NEW ESD BLOCK'S ADDRESS
         ST    R1,ESDNEXT            CHAIN FORWARD
         LR    R4,R1                 SET NEW BLOCK ADDRESS
         MVC   ESDEYE,ESD            SET EYECATCHER
         XC    ESDNEXT,ESDNEXT       CLEAR NEXT BLOCK'S ADDRESS
         MVC   ESDNAME(ESDL),0(R6)   COPY ESD DATA
         LH    R1,SAVEESD            CURRENT ESD ID NUMBER
         STCM  R1,3,ESDID            SET ESD ID
         LA    R1,1(R1)              ADD 1 TO ESD ID NUMBER
         STH   R1,SAVEESD            SAVE UPDATED ID
         MVC   MSG02SYM,ESDNAME      SET NAME
         OC    MSG02SYM,COMMBLKS     FORCE AT LEAST X'40'S
         LA    R1,ESDTBLE            FIRST ESD TYPE/DESCRIPTOR
ESD0050  DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    ESD0060               YES
         CLC   ESDTYPE,0(R1)         DESCRIPTOR FOUND?
         BE    ESD0060               YES
         LA    R1,ESDTBLEL(R1)       NEXT ESD DESCRIPTOR
         B     ESD0050               LOOP
ESD0060  DS    0H
         MVC   MSG02TYP,1(R1)        SET ESD TYPE
         UNPK  MSG02ADR(7),ESDADDR(4)
         MVZ   MSG02ADR,COMM0F0F     TURN OFF ZONES
         TR    MSG02ADR,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02ADR+6,C' '       RESTORE BLANK
         UNPK  MSG02SEG(3),ESDSEG(2) UNPACK SEGMENT NUMBER
         MVZ   MSG02SEG,COMM0F0F     TURN OFF ZONES
         TR    MSG02SEG,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02SEG+2,C' '       RESTORE BLANK
         UNPK  MSG02LEN(7),ESDLEN(4) UNPACK LENGTH
         MVZ   MSG02LEN,COMM0F0F     TURN OFF ZONES
         TR    MSG02LEN,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02LEN+6,C' '       RESTORE BLANK
         UNPK  MSG02ESD(5),ESDID(3)  UNPACK ESD ID
         MVZ   MSG02ESD,COMM0F0F     TURN OFF ZONES
         TR    MSG02ESD,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02ESD+4,C' '       RESTORE BLANK
         MVC   MSG02MSG,COMMBLKS     CLEAR MESSAGE
         CLI   ESDTYPE,$ESDSD        EXTERNAL SYMBOL?
         BE    ESD0070               YES
         CLI   ESDTYPE,$ESDPC        PRIVATE CODE?
         BNE   ESD0090               NO
ESD0070  DS    0H
         CLI   COMMCSNM,C' '         CSECT GIVEN?
         BNE   ESD0080               YES
         MVC   COMMCSNM,ESDNAME      SET NAME (WILL BE FIRST CSECT)
ESD0080  DS    0H
         CLC   ESDNAME,COMMCSNM      CORRECT CSECT FOUND?
         BNE   ESD0090               NO
         ITRACE ID=CSECTFND
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,7,ESDADDR          CSECT'S ADDRESS
         ST    R1,COMMCSAD           SET CSECT ADDRESS
         SR    R2,R2                 CLEAR REGISTER
         ICM   R2,7,ESDLEN           CSECT LENGTH
         ST    R2,COMMCSLN           SET CSECT LENGTH
         AR    R1,R2                 ADDRESS + LENGTH
         BCTR  R1,0                  MINUS 1
         ST    R1,COMMCSEA           SAVE ENDING ADDRESS
         ITRACE ID=CSECTAD,DATA1=COMMCSAD,DATA2=COMMCSEA
         LA    R2,32(R2)             PREVENT'S 0C4'S IN TEXT PRINTER
         GETMAIN RU,                 ACQUIRE STORAGE FOR CSECT         +
               LV=(R2),              .. SIZE                           +
               LOC=BELOW
         ST    R1,COMMTXT            SAVE TEXT'S STORAGE ADDRESS
         MVC   COMMESID,ESDID        SAVE ESD ID OF CSECT
         OI    COMMFLAG,$CSECT       CSECT HAS BEEN FOUND
         MVC   MSG02MSG,MSGMSG       SET MESSAGE
ESD0090  DS    0H
         MVC   PRTDATA(MSG02L),MSG02 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         LA    R6,ESDL(R6)           NEXT ESD DATA
         BCT   R5,ESD0040            LOOP
         B     EXIT0000
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET INTERFACE BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               PRINT SUBHEADING
         BR    R10                   EXIT
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE04   DC    18F'0'                REGISTER SAVE AREA
SAVEESD  DC    H'0'
ESDFLAG  DC    X'00'
$SUBH    EQU   X'80'                 INITIAL SUB HEADING PRINTED
ESD      DC    CL8'ESD'
SUBHEAD  DC    C'0'
         DC    CL08' '
         DC    CL08' SYMBOL '
         DC    CL02' '
         DC    CL04'TYPE'
         DC    CL02' '
         DC    CL06' ADDR '
         DC    CL02' '
         DC    CL03'SEG'
         DC    CL02' '
         DC    CL06'LENGTH'
         DC    CL02' '
         DC    CL06'ESDID'
SUBHEADL EQU   *-SUBHEAD
MSG01    DC    48C'-'
         DC    CL23' EXTERNAL SYMBOL TABLE '
         DC    49C'-'
MSG01L   EQU   *-MSG01
MSG02    DS    0C
         DC    CL08' '
MSG02SYM DC    CL08' '                 EXTERNAL SYMBOL
         DC    CL02' '
MSG02TYP DC    CL04' '                 SYMBOL TYPE
         DC    CL02' '
MSG02ADR DC    CL06' '                 ADDRESS
         DC    CL02' '
MSG02SEG DC    CL02' '                 SEGMENT NUMBER
         DC    CL03' '
MSG02LEN DC    CL06' '                 LENGTH
         DC    CL02' '
MSG02ESD DC    CL04' '                 ESD ID
         DC    CL02' '
MSG02MSG DC    CL15' '                 MESSAGE
MSG02L   EQU   *-MSG02
MSGMSG   DC    CL15'REQUESTED CSECT'
ESDTBLE  DS    0C
         DC    AL1($ESDSD),CL4' SD'
ESDTBLEL EQU   *-ESDTBLE
         DC    AL1($ESDER),CL4' ER'
         DC    AL1($ESDLR),CL4' LR'
         DC    AL1($ESDPC),CL4' PC'
         DC    AL1($ESDCM),CL4' CM'
         DC    AL1($ESDPR),CL4' PS'
         DC    AL1($ESDNULL),CL4'NULL'
         DC    AL1($ESDWX),CL4' WX '
         DC    AL1($ESDOV),CL4' OV '
         DC    X'FF',CL4'UNKN'
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              LITERALS                                               *
*                                                                     *
*---------------------------------------------------------------------*
         LTORG
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM04
./ ADD NAME=DISASM05
         TITLE 'DISASM05 - RLD DATA PROCESSOR'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM05                                             *
*                                                                    *
*  Function:                                                         *
*   Process RLD records.  This module runs as a sub-function of      *
*   DISASM03.  RLD data is interpreted and individual fields are     *
*   represented in RLDDATA blocks chained from field COMMRLD of      *
*   the common module, DISASM00.                                     *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM05 CSECT
DISASM05 AMODE 31
DISASM05 RMODE 24
         USING DISASM05,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     RLD0000               SKIP EYECATCHER
         DC    CL8'DISASM05'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
RLD0000  DS    0H
         LA    R1,SAVE05             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         ITRACE ID=ENTRY
         MVC   COMMSUBH(SUBHEADL),SUBHEAD
         LA    R1,SUBHEADL           SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
         MVI   COMMSUBL,X'FF'        INDICATE NON-CENTERED
         TM    RLDFLAG,$SUBH         SUBHEADING PRINTED?
         BO    RLD0010               YES
         OI    RLDFLAG,$SUBH         SET FLAG
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         MVC   PRTDATA(SUBHEADL-1),SUBHEAD+1
         BAL   R10,PRT0000           PRINT MESSAGE
RLD0010  DS    0H
         LA    R5,COMMRLD            FIRST RLD RECORD'S ADDRESS
         USING RLDDATA,R5            DEFINE BASE
RLD0020  DS    0H
         OC    RLDNEXT,RLDNEXT       MORE ON CHAIN?
         BZ    RLD0030               NO
         L     R5,RLDNEXT            FOLLOW THE CHAIN
         B     RLD0020               LOOP
RLD0030  DS    0H
         L     R4,COMMIO             I/O AREA ADDRESS
         LH    R6,6(R4)              SIZE OF RLD DATA
         LA    R7,16(R4)             FIRST RLD ITEM
         ITRACE ID=PROCRLD,                                            +
               RDATA1=R6,            .. RLD DATA LENGTH                +
               RDATA2=R7             .. FIRST RLD DATA ADDRESS
RLD0040  DS    0H
         MVC   SAVEPTR,0(R7)         SAVE RLD POINTER
         MVC   SAVEPP,2(R7)          SAVE POSITION POINTER
         LA    R7,4(R7)              SKIP RLD AND POSITION POINTERS
         SH    R6,COMMH4             MINUS LENGTH USED
         BZ    EXIT0000              NO DATA.. EXIT
RLD0050  DS    0H
         ITRACE ID=NEWITEM,                                            +
               RDATA1=R7,                                              +
               DATA2=0(R7)
         CLC   1(3,R7),COMMCSAD+1    ADDRESS TOO LOW?
         BL    RLD0250               YES..
         CLC   1(3,R7),COMMCSEA+1    ADDRESS TOO HIGH?
         BH    RLD0250               YES..
         GETMAIN RU,                 ACQUIRE STORAGE FOR RLD DATA      +
               LV=RLDDATAL,          .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWRLD,                                             +
               RDATA1=R1             .. NEW RLD BLOCK'S ADDRESS
         ST    R1,RLDNEXT            CHAIN FORWARD
         LR    R5,R1                 SET NEW BLOCK ADDRESS
         MVC   RLDEYE,RLD            SET EYECATCHER
         XC    RLDNEXT,RLDNEXT       CLEAR NEXT BLOCK'S ADDRESS
         MVC   RLDLEN,0(R7)          SET LENGTH (SORT OF)
         NI    RLDLEN,X'0F'          TURN OFF 'TYPE' BITS
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,RLDLEN             LENGTH, DIRECTION, AND INDICATOR
         SRL   R1,2                  SHIFT OUT DIRECTION AND INDICATOR
         LA    R1,1(R1)              +1 = REAL LENGTH
         STH   R1,RLDLEN             SAVE LENGTH
         ICM   R1,7,1(R7)            CONSTANT'S ADDRESS
         S     R1,COMMCSAD           DISPLACEMENT INTO THIS CSECT
         STCM  R1,15,RLDDISP         SAVE DATA DISPLACEMENT
         MVC   RLDPTR,SAVEPTR        SET RLD POINTER
         MVC   RLDPP,SAVEPP          SET POSITION POINTER
         MVC   RLDTYPE,0(R7)         SET RLD TYPE
         NI    RLDTYPE,X'F0'         CLEAR LEN, DIRECTION, INDICATOR
         TM    0(R7),X'02'           DIRECTION BACKWARD (-) ?
         BO    RLD0060               YES
         MVI   RLDDIR,C'+'           SET DIRECTION (PLUS)
         B     RLD0070
RLD0060  DS    0H
         MVI   RLDDIR,C'-'           SET DIRECTION (MINUS)
RLD0070  DS    0H
         UNPK  MSG02PTR(5),RLDPTR(3) UNPACK RLD POINTER
         MVZ   MSG02PTR,COMM0F0F     TURN OFF ZONES
         TR    MSG02PTR,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02PTR+4,C' '       RESTORE BLANK
         UNPK  MSG02PP(5),RLDPP(3)   UNPACK POSITION POINTER
         MVZ   MSG02PP,COMM0F0F      TURN OFF ZONES
         TR    MSG02PP,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   MSG02PP+4,C' '        RESTORE BLANK
         LA    R1,RLDTBLE            FIRST RLD TYPE/DESCRIPTOR
RLD0080  DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    RLD0090               YES
         CLC   RLDTYPE,0(R1)         DESCRIPTOR FOUND?
         BE    RLD0090               YES
         LA    R1,RLDTBLEL(R1)       NEXT RLD DESCRIPTOR
         B     RLD0080               LOOP
RLD0090  DS    0H
         MVC   MSG02TYP,1(R1)        SET RLD TYPE
         MVC   MSG02LEN,RLDLEN+1     MOVE LENGTH
         OI    MSG02LEN,X'F0'        CONVERT TO EBCDIC
         MVC   MSG02DIR,RLDDIR       COPY DIRECTION
         UNPK  MSG02DSP(9),RLDDISP(5)
         MVZ   MSG02DSP,COMM0F0F     TURN OFF ZONES
         TR    MSG02DSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG02DSP+8,C' '       RESTORE BLANK
         MVC   MSG02ENM,COMMBLKS     CLEAR NAME
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,3,RLDPTR           RLD POINTER
         BZ    RLD0150               IGNORE IF ZERO
         L     R3,COMMESD            FIRST ESD ENTRY
         USING ESDDATA,R3            DEFINE BASE
         B     RLD0110               ENTER LOOP
RLD0100  DS    0H
         ICM   R3,15,ESDNEXT         NEXT ESD ENTRY
         BZ    ERR0020               IF ZERO.. BAD NEWS
RLD0110  DS    0H
         BCT   R1,RLD0100            LOOP
RLD0120  DS    0H
         ST    R3,RLDESD             CHAIN RLD TO ESD
         MVC   MSG02ENM,ESDNAME      COPY NAME
         LA    R1,ESDTBLE            FIRST ESD TYPE/DESCRIPTOR
RLD0130  DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    RLD0140               YES
         CLC   ESDTYPE,0(R1)         DESCRIPTOR FOUND?
         BE    RLD0140               YES
         LA    R1,ESDTBLEL(R1)       NEXT ESD DESCRIPTOR
         B     RLD0130               LOOP
RLD0140  DS    0H
         MVC   MSG02ETY,1(R1)        SET ESD TYPE
RLD0150  DS    0H
         MVC   PRTDATA(MSG02L),MSG02 SET MESSAGE
         BAL   R10,PRT0000           PRINT RLD MESSAGE
*--------------------------------------------------------------------*
*      DETERMINE IF THE RLD DATA IS WITHIN A DEFINED DATA AREA       *
*--------------------------------------------------------------------*
         ICM   R0,15,RLDDISP         DISPLACEMENT TO RLD DATA
         LH    R1,RLDLEN             RLD DATA LENGTH
         AR    R1,R0                 PLUS BEGINNING DISPLACEMENT
         BCTR  R1,0                  MINUS 1
         STCM  R1,15,TEMPEND         ENDING DISPLACEMENT
RLD0155  DS    0H
         LA    R2,COMMDATA           'LAST' FORWARD POINTER
         ICM   R8,15,COMMDATA        FIRST 'DATA' BLOCK
         USING DATADSCT,R8           DEFINE BASE
         BZ    RLD0170               NO DATA BLOCKS
RLD0160  DS    0H
         CLC   RLDDISP,DATABEGN      AT EXACT SAME LOCATION?
         BE    RLD0230               YES
         CLC   TEMPEND,DATABEGN      TOO LOW?
         BL    RLD0170               NOT IN A DEFINED AREA
         CLC   RLDDISP,DATAEND       TOO HIGH?
         BNH   RLD0240               IN A DEFINED AREA
         LA    R2,DATANEXT           LAST FORWARD POINTER
         ICM   R8,15,DATANEXT        NEXT DATA BLOCK'S ADDRESS
         BNZ   RLD0160               LOOP
*--------------------------------------------------------------------*
*      RLD DATA IS NOT IN A DEFINED AREA                             *
*--------------------------------------------------------------------*
RLD0170  DS    0H
         GETMAIN RU,                 ACQUIRE DATA BLOCK STORAGE        +
               LV=DATAL,             .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWDATA,          TRACE NEW BLOCKS                  +
               RDATA1=R1
         ST    R1,0(R2)              CHAIN TO PREVIOUS BLOCK
         ST    R8,DATANEXT-DATADSCT(R1)
         LR    R8,R1                 SET BASE
         MVC   DATAEYE,DATA          SET EYECATCHER
         MVC   DATABEGN,RLDDISP      DATA BEGINNING DISPLACEMENT
         MVC   DATAEND,TEMPEND       DATA ENDING DISPLACEMENT
         XC    DATALEN(2),DATALEN
         MVC   DATALEN+2(2),RLDLEN   SET RLD DATA LENGTH
         MVC   DATANAME,COMMBLKS     INITIALIZE NAME
         XC    DATALBA,DATALBA       INITIALIZE LABEL BLOCK'S ADDRESS
         CLI   RLDTYPE,$RLDACON      ADCON?
         BE    RLD0180               YES
         CLI   RLDTYPE,$RLDVCON      VCON?
         BE    RLD0200               YES
         CLI   RLDTYPE,$RLDER1       UNRESOLVED EXTERNAL REFERENCE?
         BE    RLD0200               YES
         CLI   RLDTYPE,$RLDER2       UNRESOLVED EXTERNAL REFERENCE?
         BE    RLD0200               YES
         CLI   RLDTYPE,$RLDPSSZ      PSEUDO AREA SIZE?
         BE    RLD0210               YES
         CLI   RLDTYPE,$RLDPSDP      PSEUDO AREA DISPLACEMENT?
         BE    RLD0220               YES
         MVC   PRTDATA(EMSG01L),EMSG01
         BAL   R10,PRT0000           PRINT THE MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         ABEND ABEND003,DUMP,,USER   ABEND
RLD0180  DS    0H
         CLC   ESDNAME,COMMCSNM      SAME AS REQUESTED CSECT NAME?
         BNE   RLD0190               NO
         MVI   DATATYPE,$DATAACN     ADCON
         B     RLD0250               AND EXIT
RLD0190  DS    0H
         UNPK  MSG03DSP(9),RLDDISP(5)
         MVZ   MSG03DSP,COMM0F0F     CLEAR ZONES
         TR    MSG03DSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG03DSP+8,C' '       RESTORE BLANK
         MVC   PRTDATA(MSG03L),MSG03 COPY MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
RLD0200  DS    0H
         MVI   DATATYPE,$DATAVCN     VCON
         MVC   DATANAME,ESDNAME      COPY NAME FOR VCON
         B     RLD0250               AND EXIT
RLD0210  DS    0H
         MVI   DATATYPE,$DATACXD     PSEUDO AREA SIZE
         B     RLD0250               AND EXIT
RLD0220  DS    0H
         MVI   DATATYPE,$DATAQ       PSEUDO AREA DISPLACEMENT
         MVC   DATANAME,ESDNAME      COPY NAME FOR Q AREA
         B     RLD0250
RLD0230  DS    0H
         CLC   RLDLEN,DATALEN+2      SAME LENGTH?
         BNE   RLD0240               NO
         CLI   DATATYPE,$DATAACN     IS DATA AN ADCON?
         BNE   RLD0240               NO
         CLI   RLDTYPE,$RLDACON      IS RLD ITEM AN ADCON?
         BNE   RLD0240               NO
         ITRACE ID=DUPADCON,                                           +
               RDATA1=R7,                                              +
               DATA2=0(R7)
         MVC   PRTDATA(MSG04L),MSG04
         BAL   R10,PRT0000           PRINT MESSAGE
         B     RLD0250
RLD0240  DS    0H
         ITRACE ID=OVERLAP,                                            +
               RDATA1=R5,            .. RLD ITEM ADDRESS               +
               RDATA2=R8             .. DATA ITEM ADDRESS
         L     R15,A10               DISASM10 ENTRY POINT
         BALR  R14,R15               LINK TO DISASM10
         B     RLD0155               CHECK OVERLAP(S) AGAIN
RLD0250  DS    0H
         TM    0(R7),X'01'           RLD/POS PTRS VALID FOR NEXT ITEM?
         BO    RLD0260               YES
         MVC   SAVEPTR,4(R7)         SAVE NEW RLD POINTER
         MVC   SAVEPP,6(R7)          SAVE NEW POSITION POINTER
         LA    R7,4(R7)              UPDATE DATA ADDRESS
         SH    R6,COMMH4             MINUS LENGTH USED
         BZ    EXIT0000              ALL DONE
RLD0260  DS    0H
         LA    R7,4(R7)              NEXT RLD ITEM
         SH    R6,COMMH4             MINUS LENGTH USED
         BH    RLD0050               PROCESS NEXT RLD ITEM
ERR0010  DS    0H
         MVC   PRTDATA(EMSG02L),EMSG02
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         ABEND ABEND004,DUMP,,USER
         B     EXIT0000              AND EXIT
ERR0020  DS    0H
         MVC   PRTDATA(EMSG03L),EMSG03
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET INTERFACE BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               PRINT SUBHEADING
         BR    R10                   EXIT
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
DWORD    DC    D'0'
SAVE05   DC    18F'0'                REGISTER SAVE AREA
RLDFLAG  DC    X'00'
$SUBH    EQU   X'80'                 SUBHEADING PRINTED
SAVEPTR  DC    XL2'0000'
SAVEPP   DC    XL2'0000'
TEMPEND  DC    XL4'0000'
RLD      DC    CL8'RLD'
DATA     DC    CL8'DATA'
SUBHEAD  DS    0C
         DC    CL08' '
         DC    CL06'RELPTR'
         DC    CL02' '
         DC    CL16'POSITION POINTER'
         DC    CL02' '
         DC    CL10'   TYPE   '
         DC    CL02' '
         DC    CL06'LENGTH'
         DC    CL02' '
         DC    CL09'DIRECTION'
         DC    CL05' '
         DC    CL06' DISP '
         DC    CL04' '
         DC    CL10'ESD SYMBOL'
         DC    CL02' '
         DC    CL08'ESD TYPE'
SUBHEADL EQU   *-SUBHEAD
MSG01    DC    55C'-'
         DC    CL10' RLD DATA '
         DC    55C'-'
MSG01L   EQU   *-MSG01
MSG02    DS    0C
         DC    CL09' '
MSG02PTR DC    CL04' '                 RLD POINTER
         DC    CL09' '
MSG02PP  DC    CL04' '                 POSITION POINTER
         DC    CL08' '
MSG02TYP DC    CL10' '                 RLD TYPE
         DC    CL05' '
MSG02LEN DC    CL01' '                 LENGTH
         DC    CL08' '
MSG02DIR DC    CL01' '                 DIRECTION (+ OR -)
         DC    CL07' '
MSG02DSP DC    CL08' '                 DISPLACEMENT
         DC    CL04' '
MSG02ENM DC    CL08' '                 CORRESPONDING ESD ENTRY NAME
         DC    CL04' '
MSG02ETY DC    CL04' '                 CORRESPONDING ESD ENTRY TYPE
MSG02L   EQU   *-MSG02
MSG03    DC    C'DISASM0503W RLD item at '
MSG03DSP DC    CL8' '
         DC    C' changed from ADCON to VCON, reference is in a differe+
               nt CSECT'
MSG03L   EQU   *-MSG03
MSG04    DC    C'DISASM0504W This RLD item references an ADCON previous+
               ly encountered'
MSG04L   EQU   *-MSG04
EMSG01   DC    C'DISASM0501E Unknown RLD data type'
EMSG01L  EQU   *-EMSG01
EMSG02   DC    C'DISASM0502E RLD data remaining went negative'
EMSG02L  EQU   *-EMSG02
EMSG03   DC    C'DISASM0503E RLD pointer larger than number of ESD item+
               s'
EMSG03L  EQU   *-EMSG03
RLDTBLE  DS    0C
         DC    AL1($RLDACON),CL10'ADCON'
RLDTBLEL EQU   *-RLDTBLE
         DC    AL1($RLDVCON),CL10'VCON'
         DC    AL1($RLDPSSZ),CL10'CXD'
         DC    AL1($RLDPSDP),CL10'Q'
         DC    AL1($RLDER1),CL10'UNRESOLVED'
         DC    AL1($RLDER2),CL10'UNRESOLVED'
         DC    X'FF',CL10'UNKNOWN'
ESDTBLE  DS    0C
         DC    AL1($ESDSD),CL4' SD'
ESDTBLEL EQU   *-ESDTBLE
         DC    AL1($ESDER),CL4' ER'
         DC    AL1($ESDLR),CL4' LR'
         DC    AL1($ESDPC),CL4' PC'
         DC    AL1($ESDCM),CL4' CM'
         DC    AL1($ESDPR),CL4' PS'
         DC    AL1($ESDNULL),CL4'NULL'
         DC    AL1($ESDWX),CL4' WX '
         DC    AL1($ESDOV),CL4' OV '
         DC    X'FF',CL4'UNKN'
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
         SPACE 1
         LTORG
         SPACE 1
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM05
./ ADD NAME=DISASM06
         TITLE 'DISASM06 - TEXT PRINTER'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM06                                             *
*                                                                    *
*  Function:                                                         *
*   TEXT printer.  The object module will have already been read     *
*   into storage by the module reader DISASM03.  Fields COMMTXT and  *
*   COMMCSLN in DISASM00 will have been set to the address and       *
*   length of the module in storage.                                 *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM06 CSECT
DISASM06 AMODE 31
DISASM06 RMODE 24
         USING DISASM06,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     TEXT0000              SKIP EYECATCHER
         DC    CL8'DISASM06'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
TEXT0000 DS    0H
         LA    R1,SAVE06             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         MVC   COMMSUBH(SUBHEADL),SUBHEAD
         LA    R1,SUBHEADL           SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
         MVI   PRTCMD,$PRTHEAD       FORCED HEADING
         BAL   R10,PRT0010           PRINT FORCED HEADING
         L     R3,COMMCSLN           CSECT'S LENGTH
         L     R4,COMMTXT            TEXT'S ADDRESS
         XC    TEXTDISP,TEXTDISP     INITIALIZE DISPLACEMENT
TEXT0020 DS    0H
         LR    R5,R3                 COPY LENGTH
         CH    R5,COMMH32            MORE THAN 1 LINE'S WORTH?
         BNH   TEXT0030              NO
         LH    R5,COMMH32            LIMIT TO 1 LINE
TEXT0030 DS    0H
         MVC   MSG01HX,COMMBLKS      INITIALIZE HEX DATA
         MVC   MSG01CH,COMMBLKS      INITIALIZE CHARACTER DATA
         UNPK  MSG01DSP(9),TEXTDISP(5)
         MVZ   MSG01DSP,COMM0F0F     TURN OFF ZONES
         TR    MSG01DSP,COMMHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG01DSP+8,C' '       RESTORE BLANK
         UNPK  WORKHX1(9),00(5,R4)   UNPACK DATA
         UNPK  WORKHX2(9),04(5,R4)   UNPACK DATA
         UNPK  WORKHX3(9),08(5,R4)   UNPACK DATA
         UNPK  WORKHX4(9),12(5,R4)   UNPACK DATA
         UNPK  WORKHX5(9),16(5,R4)   UNPACK DATA
         UNPK  WORKHX6(9),20(5,R4)   UNPACK DATA
         UNPK  WORKHX7(9),24(5,R4)   UNPACK DATA
         UNPK  WORKHX8(9),28(5,R4)   UNPACK DATA
         MVZ   WORKHX1,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX2,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX3,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX4,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX5,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX6,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX7,COMM0F0F      TURN OFF ZONES
         MVZ   WORKHX8,COMM0F0F      TURN OFF ZONES
         TR    WORKHX1,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX2,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX3,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX4,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX5,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX6,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX7,COMMHXCH      TRANSLATE TO PRINTABLE
         TR    WORKHX8,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   WORKHX1+8,C' '        RESTORE BLANK
         MVI   WORKHX2+8,C' '        RESTORE BLANK
         MVI   WORKHX3+8,C' '        RESTORE BLANK
         MVI   WORKHX4+8,C' '        RESTORE BLANK
         MVI   WORKHX5+8,C' '        RESTORE BLANK
         MVI   WORKHX6+8,C' '        RESTORE BLANK
         MVI   WORKHX7+8,C' '        RESTORE BLANK
         LR    R1,R5                 COPY LENGTH
         LA    R1,LENTBLE(R1)        PRINT LENGTH'S ADDRESS
         SR    R2,R2                 CLEAR REGISTER
         IC    R2,0(R1)              PRINT LENGTH
         BCTR  R2,0                  MINUS 1 FOR EXECUTE
         EX    R2,HEXMVC             MOVE HEX DATA
         LR    R1,R5                 COPY LENGTH
         BCTR  R1,0                  MINUS 1 FOR EXECUTES
         EX    R1,CHARMVC            MOVE CHARACTER
         EX    R1,CHARTR             TRANSLATE UNPRINTABLES TO PERIODS
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT TEXT
         ICM   R1,15,TEXTDISP        CURRENT DISPLACEMENT
         LA    R1,32(R1)             UPDATE DISPLACEMENT
         STCM  R1,15,TEXTDISP        SAVE UPDATED DISPLACEMENT
         LA    R4,32(R4)             NEXT TEXT
         SR    R3,R5                 MINUS LENGTH PRINTED
         BNZ   TEXT0020              CONTINUE
         B     EXIT0000              EXIT
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
PRT0010  DS    0H
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R10                   RETURN
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
HEXMVC   MVC   MSG01HX(0),HEXWORK    MOVE HEX TO PRINT MESSAGE
CHARMVC  MVC   MSG01CH(0),0(R4)      MOVE CHARACTER TO PRINT
CHARTR   TR    MSG01CH(0),PRTTABLE   TRANSLATE ALL TO PRINTABLE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE06   DC    18F'0'                REGISTER SAVE AREA
TEXTDISP DC    XL4'00'
SUBHEAD  DC    C' TEXT '
SUBHEADL EQU   *-SUBHEAD
MSG01    DS    0C
MSG01DSP DC    CL08' '
         DC    CL03' '
MSG01HX  DC    CL73' '
         DC    CL03' '
MSG01CH  DC    CL32' '
MSG01L   EQU   *-MSG01
LENTBLE  DC    AL1(00)                  NOT USED
         DC    AL1(02)                  WHEN LENGTH = 01
         DC    AL1(04)                  WHEN LENGTH = 02
         DC    AL1(06)                  WHEN LENGTH = 03
         DC    AL1(08)                  WHEN LENGTH = 04
         DC    AL1(11)                  WHEN LENGTH = 05
         DC    AL1(13)                  WHEN LENGTH = 06
         DC    AL1(15)                  WHEN LENGTH = 07
         DC    AL1(17)                  WHEN LENGTH = 08
         DC    AL1(20)                  WHEN LENGTH = 09
         DC    AL1(22)                  WHEN LENGTH = 10
         DC    AL1(24)                  WHEN LENGTH = 11
         DC    AL1(26)                  WHEN LENGTH = 12
         DC    AL1(29)                  WHEN LENGTH = 13
         DC    AL1(31)                  WHEN LENGTH = 14
         DC    AL1(33)                  WHEN LENGTH = 15
         DC    AL1(35)                  WHEN LENGTH = 16
         DC    AL1(40)                  WHEN LENGTH = 17
         DC    AL1(42)                  WHEN LENGTH = 18
         DC    AL1(44)                  WHEN LENGTH = 19
         DC    AL1(46)                  WHEN LENGTH = 20
         DC    AL1(49)                  WHEN LENGTH = 21
         DC    AL1(51)                  WHEN LENGTH = 22
         DC    AL1(53)                  WHEN LENGTH = 23
         DC    AL1(55)                  WHEN LENGTH = 24
         DC    AL1(58)                  WHEN LENGTH = 25
         DC    AL1(60)                  WHEN LENGTH = 26
         DC    AL1(62)                  WHEN LENGTH = 27
         DC    AL1(64)                  WHEN LENGTH = 28
         DC    AL1(67)                  WHEN LENGTH = 29
         DC    AL1(69)                  WHEN LENGTH = 30
         DC    AL1(71)                  WHEN LENGTH = 31
         DC    AL1(73)                  WHEN LENGTH = 32
HEXWORK  DS    0C
WORKHX1  DC    CL8' '
         DC    CL1' '
WORKHX2  DC    CL8' '
         DC    CL1' '
WORKHX3  DC    CL8' '
         DC    CL1' '
WORKHX4  DC    CL8' '
         DC    CL3' '
WORKHX5  DC    CL8' '
         DC    CL1' '
WORKHX6  DC    CL8' '
         DC    CL1' '
WORKHX7  DC    CL8' '
         DC    CL1' '
WORKHX8  DC    CL8' '
         DC    CL1' '
PRTTABLE DC    256C'.'
         ORG   PRTTABLE+X'40'
         DC    C' '
         ORG   PRTTABLE+X'4A'
         DC    C'.<(+'
         DC    X'50'
         ORG   PRTTABLE+X'5A'
         DC    C'!$*);-/'
         ORG   PRTTABLE+X'6B'
         DC    C',%_>?'
         ORG   PRTTABLE+X'7A'
         DC    C':#@'
         DC    X'7D'
         DC    C'="'
         ORG   PRTTABLE+X'C0'
         DC    C'{ABCDEFGHI'
         ORG   PRTTABLE+X'D0'
         DC    C'}JKLMNOPQR'
         ORG   PRTTABLE+X'E2'
         DC    C'STUVWXYZ'
         ORG   PRTTABLE+X'F0'
         DC    C'0123456789'
         ORG   PRTTABLE+256
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
         SPACE 2
         LTORG
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM06
./ ADD NAME=DISASM07
         TITLE 'DISASM07 - DSECT ASSEMBLER AND INTERPRETER'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM07                                             *
*                                                                    *
*  Function:                                                         *
*   Dynamically invoke the assembler (IEV90) to assemble DSECTs.     *
*   Any method of defining DSECTs that are valid to the assembler    *
*   may be used.  They may be defined inline, by macros, or copy     *
*   statements.  The disassembler links to the assembler to assemble *
*   the source, then scans the assembler output to learn the DSECT   *
*   names, label names, and displacements to the labels.  In order   *
*   for DISASM to find the names and displacements, the PRINT option *
*   of the assembler must be on.                                     *
*                                                                    *
*   Labels and DSECT names are limited to 8 characters in length.    *
*                                                                    *
*   Input to the assembler is delimited by ASM START and ASM END     *
*   statements.  Any statements between the ASM START and ASM END    *
*   are copied to SYSIN.  Assembler input statements are listed on   *
*   the DISPRINT output, but are otherwise ignored.  Any macros not  *
*   defined inline and any copy elements must be available to the    *
*   assembler in a library in the SYSLIB concatenation.              *
*                                                                    *
*   If the return code from the assembler is greater than 4, the     *
*   disassembly is aborted.  The assembler output is copied to the   *
*   DISDEBUG data set if allocated.                                  *
*                                                                    *
*   The DSECTs are chained from field COMMDSCT of module DISASM00.   *
*   The labels within the DSECT are chained from field DSCTLBA.      *
*   DSCTDSCT maps the DSECT control blocks, LABLDSCT maps the label  *
*   control blocks.                                                  *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM07 CSECT
DISASM07 AMODE 31
DISASM07 RMODE 24
         USING DISASM07,R12
         USING DISASM00,R11
         USING DSCTDSCT,R3           DEFINE BASE
         USING LABLDSCT,R4           DEFINE BASE
         USING EQUDATA,R5            DEFINE BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     DSCT0000              SKIP EYECATCHER
         DC    CL8'DISASM07'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
DSCT0000 DS    0H
         XC    COMMSUBL,COMMSUBL     NO SUBHEADING
         LA    R1,SAVE07             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         TM    COMMFLAG,$ASMIN       ANY ASSEMBLER INPUT?
         BNO   DSCT0300              NO
         LOAD  EP=IEV90              LOAD THE ASSEMBLER
         ST    R0,ASMEP              SAVE ASSEMBLER'S ENTRY POINT
         LR    R15,R0                COPY TO R15
         LA    R1,AASMPARM           ASSEMBLER PARM LIST ADDRESS
         ITRACE ID=CALLASM,          CALLING THE ASSEMBLER             +
               RDATA1=R15,           .. ASSEMBLER'S ENTRY POINT        +
               RDATA2=R1             .. ASSEMBLER'S PARM LIST ADDRESS
         BASR  R14,R15               LINK TO ASSEMBLER
         BAL   R14,AM31              FORCE 31-BIT MODE
         ITRACE ID=ASMRC,            TRACE ASSEMBLER'S RETURN CODE     +
               RDATA1=R15            .. RETURN CODE
         STH   R15,ASMRC             SAVE ASSEMBLER RETURN CODE
         CVD   R15,COMMDWRD          CONVERT TO DECIMAL
         MVI   PRTCC,C'0'            DOUBLE SPACE
         MVC   MSG01RC,=X'40202120'  SET EDIT WORD
         ED    MSG01RC,COMMDWRD+6    EDIT RETURN CODE
         MVC   PRTDATA(MSG01L),MSG01 SET MESSAGE
         BAL   R10,PRT0000           PRINT RETURN CODE MESSAGE
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (SYSPRINT,INPUT)      OPEN SYSPRINT AS INPUT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   COMMDBSH,ASMHEAD      SET HEADING
         MVI   DBUGCMD,$DBUGHD       SET COMMAND
         LA    R1,DBUGBLOK           DEBUG PARAMETER BLOCK ADDRESS
         L     R15,ADB               DEBUG ENTRY POINT
         BALR  R14,R15               PRINT DEBUG HEADING
         MVI   DBUGCMD,$DBUGPRT      SET COMMAND
         LA    R1,ASMSTMT+1          DATA ADDRESS FOR DEBUG
         ST    R1,DBUGDATA           SET DATA ADDRESS
         SR    R3,R3                 NO DSECT IS ACTIVE
         SR    R4,R4                 NO LABEL IS ACTIVE
DSCT0010 DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         GET   SYSPRINT,ASMSTMT      READ A SYSPRINT RECORD
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         LA    R1,DBUGBLOK           DBUG PARAMETER BLOCK ADDRESS
         L     R15,ADB               DEBUG ENTRY POINT
         BALR  R14,R15               LINK TO DEBUG
         CLC   ASMRC,COMMH4          ASSEMBLER ERROR?
         BH    DSCT0010              YES
         ITRACE ID=TEMP1,                                              +
               DATA1=ASMLABL,                                          +
               DATA2=ASMDISP
         CLI   ASMLABL,C'*'          COMMENT STATEMENT?
         BE    DSCT0010              YES
         CLC   ASMSTMT+43,SRCSTMT    ASSEMBLER HEADING LINE?
         BE    DSCT0010              YES, IGNORE IT
         CLC   XREF,ASMSTMT+45       START OF CROSS REFERENCE?
         BNE   DSCT0020              NO
         OI    XREFFLAG,$XREF        SET CROSS REFERENCE FLAG
         ITRACE ID=XREF              CROSS REFERENCE STARTED
DSCT0020 DS    0H
         TM    XREFFLAG,$XREF        CROSS REFERENCE FOUND?
         BO    DSCT0010              YES
         CLI   ASMLABL,C' '          LABEL PRESENT?
         BE    DSCT0010              NO
         LA    R6,ASMLABL            FIRST BYTE OF LABEL
         LA    R2,WORKLABL           FIRST BYTE OF WORK LABEL
         MVC   WORKLABL,COMMBLKS     CLEAR LABEL NAME
         LA    R1,8                  MAX LOOPS
DSCT0030 DS    0H
         CLI   0(R6),C' '            BLANK?
         BE    DSCT0040              YES
         MVC   0(1,R2),0(R6)         COPY TO WORK LABEL
         LA    R2,1(R2)              NEXT
         LA    R6,1(R6)              NEXT
         BCT   R1,DSCT0030           LOOP
         CLI   0(R6),C' '            BLANK?
         BE    DSCT0040              NO... TOO LONG FOR A LABEL
         ITRACE ID=LONGLABL
         B     DSCT0010              READ NEXT SYSPRINT RECORD
DSCT0040 DS    0H
         LA    R1,8                  MAX LOOPS
DSCT0050 DS    0H
         CLI   0(R6),C' '            BLANK?
         BNE   DSCT0060              NO
         LA    R6,1(R6)              NEXT
         BCT   R1,DSCT0050           LOOP
         B     DSCT0110              ASSUME IT IS A LABEL
DSCT0060 DS    0H
         CLC   DSECTOP(6),0(R6)      DSECT?
         BE    DSCT0070              YES
         CLC   CSECTOP,0(R6)         CSECT?  (TREATED LIKE DSECTS)
         BE    DSCT0070              YES
         CLC   EQUOP,0(R6)           EQUATE STATEMENT?
         BNE   DSCT0110              NO
         LTR   R4,R4                 LABEL BASE SET?
         BZ    DSCT0010              NO
         CLC   =C'000',ASMADDR2      FIRST 3 DIGITS ZEROS?
         BNE   DSCT0010              NO
         GETMAIN RU,                 LENGTH                            +
               LV=EQUL,                                                +
               LOC=ANY
         LR    R5,R1                 COPY EQU BLOCK ADDRESS
         ITRACE ID=NEWEQU,           NEW EQUATE                        +
               RDATA1=R5
         XC    EQUDATA(EQUL),EQUDATA INITIALIZE THE EQUATE BLOCK
         MVC   EQUEYE,EQUID          IDENTIFY THIS BLOCK
         MVC   EQUNEXT,LABLEQU       SET CHAIN ADDRESS
         ST    R5,LABLEQU            SET NEW BLOCK'S ADDRESS IN LABEL
         MVC   EQULABEL,WORKLABL     SET LABEL
         NC    ASMADDR2+3(2),X1F1F   PREPARE FOR PACKING
         TR    ASMADDR2+3(2),CHXH    TRANSLATE FOR PACKING
         PACK  DISPOUT(2),ASMADDR2+3(3)
         MVC   EQUVALUE,DISPOUT      COPY EQUATE VALUE
         B     DSCT0010
DSCT0070 DS    0H
         ITRACE ID=SCANDSCT
         LA    R2,COMMDSCT           DSECT ANCHOR
         ICM   R3,15,COMMDSCT        FIRST DSECT BLOCK
         BZ    DSCT0090              NO DSECTS YET
DSCT0080 DS    0H
         CLC   DSCTNAME,WORKLABL     ALREADY ON DSECT CHAIN?
         BE    DSCT0100              YES.. EXIT WITH BASE SET
         LR    R2,R3                 COPY ADDRESS
         ICM   R3,15,DSCTNEXT        NEXT DSECT BLOCK
         BNZ   DSCT0080              LOOP
DSCT0090 DS    0H
         GETMAIN RU,                 ACQUIRE NEW DSECT BLOCK           +
               LV=DSCTL,                                               +
               LOC=ANY
         ST    R1,DSCTNEXT-DSCTDSCT(R2)   CHAIN NEW TO PREVIOUS BLOCK
         ITRACE ID=NEWDSECT,         NEW DSECT BLOCK                   +
               RDATA1=R1,            .. BLOCK'S ADDRESS                +
               DATA2=WORKLABL        .. DSECT'S NAME
         LR    R3,R1                 SET BASE
         MVC   DSCTEYE,DSECTOP       SET BLOCK ID
         XC    DSCTNEXT,DSCTNEXT     ZERO 'NEXT' BLOCK ADDRESS
         MVC   DSCTNAME,WORKLABL     SET DSECT'S NAME
         XC    DSCTLBA,DSCTLBA       CLEAR LABEL POINTER
         B     DSCT0010
DSCT0100 DS    0H
         ITRACE ID=DUPDSECT
         B     DSCT0010
DSCT0110 DS    0H
         LTR   R3,R3                 DSECT DETERMINED YET?
         BZ    DSCT0010              NO
         CLI   ASMDISP,C'A'          VALID DISPLACEMENT?
         BL    DSCT0010              NO
         CLI   ASMDISP,C'F'          VALID DISPLACEMENT?
         BNH   DSCT0120              YES
         CLI   ASMDISP,C'0'          VALID DISPLACEMENT?
         BL    DSCT0010              NO
         CLI   ASMDISP,C'9'          VALID DISPLACEMENT?
         BH    DSCT0010              NO
DSCT0120 DS    0H
         MVC   DISPIN,ASMDISP        COPY DISPLACEMENT
         NC    DISPIN,COMM1F1F       PREPARE FOR TRANSLATE
         TR    DISPIN,COMMCHHX       TRANSLATE FOR PACKING
         PACK  DISPOUT(4),DISPIN(7)  PACK DISPLACEMENT
         LA    R2,DSCTLBA            LABEL CHAIN ANCHOR
         ICM   R4,15,DSCTLBA         FIRST LABEL
         BZ    DSCT0140              NO LABELS
DSCT0130 DS    0H
         CLC   DISPOUT(3),LABLDISP+1 INSERT HERE?
         BH    DSCT0140              YES
         LR    R2,R4                 COPY ADDRESS
         ICM   R4,15,LABLNEXT        NEXT LABEL
         BNZ   DSCT0130              LOOP
DSCT0140 DS    0H
         GETMAIN RU,                 ACQUIRE NEW LABEL BLOCK           +
               LV=LABLL,                                               +
               LOC=ANY
         ITRACE ID=NEWLABL,          NEW LABEL BLOCK                   +
               RDATA1=R1,            .. BLOCK'S ADDRESS                +
               DATA2=WORKLABL        .. LABEL'S NAME
         ST    R1,LABLNEXT-LABLDSCT(R2)  CHAIN PREVIOUS BLOCK TO NEW
         ST    R4,LABLNEXT-LABLDSCT(R1)  CHAIN NEXT BLOCK TO NEW
         LR    R4,R1                 SET BASE
         MVC   LABLEYE,LABEL         SET BLOCK IDENTIFIER
         MVC   LABLNAME,WORKLABL     SET LABEL NAME
         MVI   LABLDISP,X'00'        FORCE FIRST BYTE TO ZERO
         MVC   LABLDISP+1(3),DISPOUT SET DISPLACEMENT TO LABEL
         MVI   LABLTYPE,$LABLD       DATA TYPE LABEL
         B     DSCT0010
DSCT0200 DS    0H
         CLOSE SYSPRINT
         BAL   R14,AM31              FORCE AMODE 31
         CLC   ASMRC,COMMH4          ERROR DURING ASSEMBLY?
         BNH   EXIT0000              NO
         ITRACE ID=ASMERROR
         OI    COMMFLAG,$ERROR+$ABORT
         MVC   PRTDATA(EMSG01L),EMSG01
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
DSCT0300 DS    0H
         ITRACE ID=NOASMIN
         MVI   PRTCC,C'0'            DOUBLE SPACE
         MVC   PRTDATA(MSG99L),MSG99 SET MESSAGE
         BAL   R10,PRT0000           PRINT MESSAGE
         B     EXIT0000              AND EXIT
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
PRT0010  DS    0H
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R10                   RETURN
EXIT0000 DS    0H
         OC    ASMEP,ASMEP           ASSEMBLER LOADED?
         BZ    EXIT0010              NO
         ITRACE ID=DELASM            DELETE ASSEMBLER
         DELETE EP=IEV90
EXIT0010 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE07   DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
ASMEP    DC    A(0)                  ASSEMBLER'S ENTRY POINT
AASMPARM DC    A(ASMPARM+X'80000000') ASSEMBLER PARM'S
ASMRC    DC    H'0'                  ASSEMBLER'S RETURN CODE
ASMPARM  DC    Y(ASMPARML-2)
         DC    C'DECK,NOOBJECT'
ASMPARML EQU   *-ASMPARM
XREF     DC    C' CROSS REFERENCE '
XREFFLAG DC    X'00'                 CROSS REFERENCE FLAG
$XREF    EQU   X'80'                 .. CROSS REFERENCE HAS BEEN FOUND
X1F1F    DC    8X'1F'
         SPACE 1 0 1 2 3 4 5 6 7 8 9 A B C D E F
CHXH     DC    X'000A0B0C0D0E0F000000000000000000'  00-0F
         DC    X'00010203040506070809000000000000'  10-1F
         SPACE 1
DSECTOP  DC    CL8'DSECT'
CSECTOP  DC    CL8'CSECT'
EQUOP    DC    C'EQU '
SRCSTMT  DC    C'SOURCE STATEMENT'
WORKLABL DC    CL8' '
DISPIN   DC    CL7' '
DISPOUT  DC    XL4'000000'
LABEL    DC    CL8'LABEL'
EQUID    DC    CL8'EQU'
         AIF   (&ASMG EQ 133).GENASMA
ASMSTMT  DC    CL121' '
ASMSTMTL EQU   *-ASMSTMT
         ORG   ASMSTMT+1
ASMDISP  DS    CL6
         ORG   ASMSTMT+29
ASMADDR2 DS    CL5
         ORG   ASMSTMT+41
ASMLABL  DS    CL8
         ORG   ASMSTMT+121
         AGO   .GENASMB
.GENASMA ANOP
ASMSTMT  DC    CL133' '
ASMSTMTL EQU   *-ASMSTMT
         ORG   ASMSTMT+1
ASMDISP  DS    CL6
         ORG   ASMSTMT+23
ASMADDR2 DS    CL5
         ORG   ASMSTMT+41
ASMLABL  DS    CL8
         ORG   ASMSTMT+133
.GENASMB ANOP
ASMHEAD  DC    CL35'ASSEMBLER OUTPUT'
MSG01    DC    C'DISASM0701I Assembler return code was '
MSG01RC  DC    CL04' '
MSG01L   EQU   *-MSG01
MSG99    DC    C'DISASM0702I No assembler input'
MSG99L   EQU   *-MSG99
EMSG01   DC    C'DISASM0703E Error assembling DSECTS, check assembler o+
               utput in DISDEBUG'
EMSG01L  EQU   *-EMSG01
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              DEBUG MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
DBUGBLOK DBUGBLOK TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*              ASSEMBLER OUTPUT DCB                                   *
*                                                                     *
*---------------------------------------------------------------------*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               EODAD=DSCT0200,                                         +
               MACRF=GM
         SPACE 2
         LTORG
         SPACE 2
         COPY     DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM07
./ ADD NAME=DISASM08
         TITLE 'DISASM08 - CSECT LABEL ASSIGNER AND REFERENCE TABLE GEN+
               ERATOR'
* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DISASM08                                              *
*                                                                     *
*  FUNCTION:                                                          *
*   1) Chain the USING blocks to the DSECT blocks they refer to.      *
*   2) Scan the object code and                                       *
*         A. Determine the displacements where valid instructions     *
*            occur.                                                   *
*         B. Add DATA blocks to represent any areas that do not       *
*            contain valid instructions and are not already defined   *
*            by data blocks.                                          *
*   3) Verify that all BASE and USINGs reference displacements where  *
*      a valid instruction occurs.  This is because a USING or DROP   *
*      statement cannot be generated in the middle of an instruction. *
*   4) Generate label blocks for entry points that occur within the   *
*      csect.  This info comes from the ESD blocks built by module    *
*      DISASM04.                                                      *
*   5) Generate label blocks for the points referenced by ADCONs.     *
*      This info comes from the RLD blocks built by module DISASM05.  *
*   6) Scan the object code and generate the REF blocks for data      *
*      references.                                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY  DISASMGB
DISASM08 CSECT
DISASM08 AMODE 31
DISASM08 RMODE 24
         USING DISASM08,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     LABL0000              SKIP EYECATCHER
         DC    CL8'DISASM08'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
LABL0000 DS    0H
         LA    R1,SAVE08             OUR SAVE AREA ADDRESS
         USING SAVE08,R13
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         ITRACE ID=ENTRY
         TM    COMMDD,$DSECTDD       DISDSECT DD PRESENT?
         BNO   LABL0020              NO
         BAL   R14,AM24
         OPEN  (PREDCB,INPUT)        OPEN THE DSECTS DCB
         BAL   R14,AM31
         LA    R1,PREDCB
         USING IHADCB,R1
         SR    R0,R0
         ICM   R0,3,DCBBLKSI         GET BLOCK SIZE
         ST    R0,PREIOSZ
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,PREIOA             SAVE I/O AREA
* ------------------------------------------------------------------- *
*        Chain USING blocks to their related DSECT blocks             *
* ------------------------------------------------------------------- *
LABL0020 DS    0H
         ICM   R3,15,COMMUSNG        FIRST USING BLOCK'S ADDRESS
         USING USNGDSCT,R3           DEFINE BASE
         BZ    LABL0220              END OF CHAIN
LABL0030 DS    0H
         ITRACE ID=FINDDSCT,         STARTING SEARCH FOR A DSECT ENTRY +
               DATA1=USNGDSNM        .. DSECT'S NAME
         ICM   R2,15,COMMDSCT        FIRST DSECT ENTRY
         USING DSCTDSCT,R2           DEFINE BASE
LABL0040 DS    0H
         BZ    LABL0050              NOT DEFINED IN ASM START/ASM END
         ITRACE ID=TESTDSCT,         CHECKING A DSECT ENTRY            +
               DATA1=DSCTNAME        .. DSECT'S NAME
         CLC   USNGDSNM,DSCTNAME     DSECT FOUND?
         BE    LABL0160              YES
         ICM   R2,15,DSCTNEXT        NEXT DSECT BLOCK
         B     LABL0040              LOOP
LABL0050 DS    0H
         ITRACE ID=CHKPRE            CHECKING PRE-PROCESSED DSECTS
         FIND  PREDCB,USNGDSNM,D     'FIND' MEMBER
         LTR   R15,R15               MEMBER IN DATASET?
         BNZ   ERR0010               NO..
         SR    R6,R6                 NO LABEL SO FAR
         SR    R7,R7                 NO EQUATE SO FAR
         USING LABLDSCT,R6
         USING EQUDATA,R7
LABL0060 DS    0H
         BAL   R14,AM24
         L     R4,PREIOA
         READ  READECB,              READ ANOTHER BLOCK                +
               SF,                                                     +
               PREDCB,                                                 +
               (R4),                                                   +
               ,                                                       +
               S
         CHECK READECB
         BAL   R14,AM31
         LH    R5,0(R4)              TOTAL BLOCK SIZE
         SH    R5,H4                 MINUS BDW LENGTH
         LA    R4,4(R4)              FIRST RECORD IN BLOCK
LABL0070 DS    0H
         CLC   8(8,R4),DSCT_ID       DSECT?
         BE    LABL0080
         CLC   8(8,R4),LABL_ID       LABEL?
         BE    LABL0090
         CLC   8(8,R4),EQU_ID        EQUATE?
         BE    LABL0120
         ABEND 1,DUMP,,USER
LABL0080 DS    0H
         SR    R6,R6                 NO LABEL SO FAR
         SR    R7,R7                 NO EQUATE SO FAR
         GETMAIN RU,                                                   +
               LV=DSCTL,                                               +
               LOC=ANY
         LR    R2,R1                 COPY NEW STORAGE ADDRESS
         MVC   DSCTDSCT(DSCTL),4(R4) COPY FROM I/O AREA
         MVC   DSCTNEXT,COMMDSCT     COPY FIRST BLOCK'S ADDRESS
         ST    R2,COMMDSCT           ADD NEW BLOCK TO CHAIN
         XC    DSCTLBA,DSCTLBA       CLEAR 1ST LABEL BLOCK ADDRESS
         B     LABL0150
LABL0090 DS    0H
         SR    R7,R7                 NO EQUATE SO FAR
         GETMAIN RU,                                                   +
               LV=LABLL,                                               +
               LOC=ANY
         OC    DSCTLBA,DSCTLBA       LABEL ALREADY ON CHAIN?
         BNZ   LABL0100              YES
         ST    R1,DSCTLBA            CHAIN TO DSECT BLOCK
LABL0100 DS    0H
         LTR   R6,R6                 PREVIOUS LABEL BLOCK?
         BZ    LABL0110              NO
         ST    R1,LABLNEXT           CHHAIN NEW BLOCK TO PREVIOUS
LABL0110 DS    0H
         LR    R6,R1                 COPY STORAGE ADDRESS
         MVC   LABLDSCT(LABLL),4(R4) COPY FROM I/O AREA
         XC    LABLEQU,LABLEQU       CLEAR EQUATE BLOCK ADDRESS
         B     LABL0150
LABL0120 DS    0H
         GETMAIN RU,                                                   +
               LV=EQUL,                                                +
               LOC=ANY
         OC    LABLEQU,LABLEQU       EQAUTE ALREADY ON CHAIN?
         BNZ   LABL0130              YES
         ST    R1,LABLEQU            CHAIN TO EQUATE BLOCK
LABL0130 DS    0H
         LTR   R7,R7                 PREVIOUS EQUITE BLOCK
         BZ    LABL0140              NO
         ST    R1,EQUNEXT            CHHAIN NEW BLOCK TO PREVIOUS
LABL0140 DS    0H
         LR    R7,R1                 COPY STORAGE ADDRESS
         MVC   EQUDATA(EQUL),4(R4)   COPY FROM I/O AREA
LABL0150 DS    0H
         SH    R5,0(R4)              MINUS THIS RECORD'S SIZE
         BZ    LABL0060
         AH    R4,0(R4)
         B     LABL0070
         DROP  R6,R7
LABL0160 DS    0H
         BAL   R14,AM31
         ITRACE ID=DSCTFND,          DSECT ENTRY HAS BEEN FOUND        +
               RDATA1=R2             .. DSECT BLOCK'S ADDRESS
         ST    R2,USNGDSA            CHAIN DSECT BLOCK TO USING BLOCK
         CLC   USNGLBNM,COMMBLKS     LABEL BLANK?
         BE    LABL0200              YES
         ICM   R9,15,DSCTLBA         FIRST LABEL IN THE DSECT
         USING LABLDSCT,R9           DEFINE BASE
         BZ    LABL0180              NO LABELS
LABL0170 DS    0H
         CLC   LABLNAME,USNGLBNM     LABEL LOCATED?
         BE    LABL0190              YES
         ICM   R9,15,LABLNEXT        NEXT LABEL
         BNZ   LABL0170              LOOP
LABL0180 DS    0H
         MVC   EMSG05N,USNGLBNM      COPY LABEL NAME
         MVC   EMSG05D,USNGDSNM      COPY DSECT NAME
         MVC   PRTDATA(EMSG05L),EMSG05
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     LABL0210
LABL0190 DS    0H
         ST    R9,USNGLBA            SET ASSOCIATED LABEL BLOCK ADDRESS
         MVC   USNGDISP,LABLDISP     SET DISP TO LABEL
         B     LABL0210              PROCESS NEXT USING
LABL0200 DS    0H
         ICM   R9,15,DSCTLBA         ANY LABELS?
         BZ    LABL0210              NO
         ST    R9,USNGLBA            SET ASSOCIATED LABEL BLOCK ADDRESS
         XC    USNGDISP,USNGDISP     SET DISP TO ZERO
LABL0210 DS    0H
         ICM   R3,15,USNGNEXT        NEXT USING BLOCK
         BNZ   LABL0030              LOOP
         TM    COMMFLAG,$ABORT       SERIOUS ERROR?
         BO    EXIT0000              YES, STOP NOW
* ------------------------------------------------------------------- *
*        Scan the object code and determine the displacements         *
*        to all valid instructions.  This is necessary because        *
*        some instructions may reference other instructions to        *
*        modify them (like zapping in a length).  If an instruction   *
*        references another at other than the opcode address, the     *
*        generated label will be 'PRFXNNNN+D'.                        *
*                                                                     *
*        'DATA' blocks will be generated to indicate areas that do    *
*        not have valid opcodes and are not already defined as being  *
*        data.                                                        *
*                                                                     *
*        The hex value for character text is also valid opcodes.      *
*        To prevent the disassembler from interpreting a string of    *
*        character data as instructions, 6 consecutive characters     *
*        will be considered data.                                     *
*                                                                     *
*        Each displacement will be 4-bytes.  Worst case would be a    *
*        module that consists of only 2-byte opcodes.  Each entry     *
*        will be a 4-byte displacement, so the table's length would   *
*        be twice the CSECT's size at most.  Four additional bytes    *
*        are added for end of table flag (X'FFFFFFFF').               *
* ------------------------------------------------------------------- *
LABL0220 DS    0H
         L     R1,COMMCSLN           CSECT'S TOTAL LENGTH
         SLL   R1,1                  DOUBLE IT
         LA    R1,4(R1)              PLUS ROOM FOR END OF TABLE FLAG
         GETMAIN RU,                 ACQUIRE STORAGE FOR INSTR DISP'S  +
               LV=(1),               .. SIZE                           +
               LOC=ANY
         LR    R9,R1                 INITIALIZE DISP TABLE ADDRESS
         ST    R1,COMMDISP           SAVE DISPLACEMENT TABLE'S ADDRESS
         L     R5,COMMTXT            TEXT'S STORAGE ADDRESS
         MVC   0(4,R9),XFFFF         INITIALIZE END OF TABLE FLAG
         SR    R6,R6                 INITIALIZE DISPLACEMENT
LABL0230 DS    0H
         C     R6,COMMCSLN           BEYOND END OF CSECT?
         BNL   LABL0440              TEST FOR FINAL DATA BLOCK
         ICM   R7,15,COMMDATA        FIRST DATA BLOCK
         BZ    LABL0260              NO DATA BLOCKS
         USING DATADSCT,R7           DEFINE BASE
         ITRACE ID=DATACHK1,         TESTING FOR DATA AREA             +
               RDATA1=R6             .. CURRENT DISPLACEMENT
LABL0240 DS    0H
         CLM   R6,15,DATABEGN        DATA AREA BEGINNING DISPLACEMENT?
         BL    LABL0250              THIS AREA IS BELOW CURRENT DISP
         BE    LABL0430              DATA AREA FOUND
         CLM   R6,15,DATAEND         TOO HIGH FOR THIS DATA AREA?
         BH    LABL0250              YES
         OC    DATASIZE,DATASIZE     DATA OVER-LAPPING DATA?
         BNZ   ERR0060               YES.. DATA OVERLAPS DATA
         B     ERR0020               INSTRUCTION OVERLAPS DATA
LABL0250 DS    0H
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         BNZ   LABL0240              LOOP
LABL0260 DS    0H
         STC   R6,COMMDWRD           SAVE LAST BYTE
         TM    COMMDWRD,1            ON AN ODD BOUNDARY?
         BO    LABL0400              YES
         L     R0,COMMCSLN           CSECT TOTAL LENGTH
         SR    R0,R6                 MINUS CURRENT DISPLACEMENT
         CH    R0,COMMH8             8 OR MORE BYTES LEFT?
         BL    LABL0280              NO
         TRT   0(8,R5),ALPHATRT      8 ALPHA CHARACTERS IN A ROW?
         BNZ   LABL0270              NO.. DON'T GIVE UP COMPLETELY
         LA    R1,8                  SET FOR 8 BYTES
         B     LABL0410              SKIP THESE 8
LABL0270 DS    0H
         TRT   0(6,R5),ALPHATRT      6 ALPHA CHARACTERS IN A ROW?
         BNZ   LABL0280              NO
         LA    R1,6                  SKIP 6 BYTES
         B     LABL0410              SKIP ALL 6
LABL0280 DS    0H
         SR    R8,R8                 CLEAR REGISTER
         ICM   R8,1,0(R5)            INSERT POSSIBLE OPCODE
         BZ    LABL0400              NOT A VALID OPCODE
         SLL   R8,2                  MULTIPLY BY 4
         A     R8,AOP                PLUS BASE ADDRESS
         ICM   R8,15,0(R8)           OPCODE ENTRY ADDRESS
         USING OPDSECT,R8            DEFINE BASE
         BZ    LABL0400              NOT A VALID OPCODE
         TM    COMMFLAG,$NOFLOAT     FLOATING POINT ALLOWED?
         BNO   LABL0290              YES
         TM    OPFLAGS,$OPFLOAT      FLOATING POINT INSTRUCTION?
         BO    LABL0400              YES.. NOT VALID
LABL0290 DS    0H
         CLI   OPREGS,0              ANY REGISTER RESTRICTIONS?
         BE    LABL0300              NO
         L     R15,ARR               REGISTER OPERAND CHECKER
         BALR  R14,R15               CALL REGISTER OPERAND CHECKER
         LTR   R15,R15               OPERAND(S) VALID?
         BNZ   LABL0400              NO
LABL0300 DS    0H
         LH    R1,OPLENGTH           OPCODE LENGTH
         AR    R1,R6                 DISPLACEMENT OF END OF INSTR + 1
         BCTR  R1,0                  DISPLACEMENT OF END OF INSTR
         ICM   R7,15,COMMDATA        FIRST DATA BLOCK
         BZ    LABL0330              NO DATA BLOCKS
LABL0310 DS    0H
         CLM   R1,15,DATABEGN        TOO LOW?
         BL    LABL0320              YES
         CLM   R6,15,DATAEND         TOO HIGH?
         BNH   LABL0400              NO, OVERLAPS INTO DATA
LABL0320 DS    0H
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         BNZ   LABL0310              LOOP
LABL0330 DS    0H
         ICM   R4,15,COMMESD         FIRST ESD BLOCK
         USING ESDDATA,R4            DEFINE BASE
         BZ    LABL0360              NO ESD BLOCKS
LABL0340 DS    0H
         CLM   R6,7,ESDADDR          ESD LABEL AT THIS POINT?
         BE    LABL0390              YES.. FORCE DATA BLOCK NOW
         BH    LABL0350              NO.. BEYOND THIS ESD ENTRY
         CLM   R1,7,ESDADDR          SPAN THE ESD ITEM?
         BH    LABL0400              YES.. INSTRUCTION WOULD SPAN LABEL
LABL0350 DS    0H
         ICM   R4,15,ESDNEXT         NEXT ESD ITEM
         BNZ   LABL0340              LOOP
LABL0360 DS    0H
         LH    R1,OPLENGTH           OPCODE LENGTH
         AR    R1,R6                 NEXT OPCODE'S DISPLACEMENT
         L     R0,COMMCSLN           CSECT LENGTH
         SR    R0,R1                 MINUS NEXT OPCODE'S DISPLACEMENT
         CH    R0,COMMH8             8 OR MORE BYTES LEFT?
         BL    LABL0390              ACCEPT THE OPCODE
         LH    R1,OPLENGTH           OPCODE LENGTH
         AR    R1,R5                 PLUS CURRENT INSTRUCTION ADDRESS
         TRT   0(6,R1),ALPHATRT      6 ALPHA CHARACTERS IN A ROW?
         BZ    LABL0370              YES.. CHECK FOR EXCEPTIONS
         LH    R1,OPLENGTH           OPCODE LENGTH
         AR    R1,R5                 PLUS CURRENT INSTRUCTION ADDRESS
         SR    R14,R14               CLEAR REGISTER
         ICM   R14,1,0(R1)           INSERT POSSIBLE OPCODE
         BZ    LABL0370              NOT A VALID OPCODE
         SLL   R14,2                 MULTIPLY BY 4
         A     R14,AOP               PLUS BASE ADDRESS
         ICM   R14,15,0(R14)         ANOTHER VALID OPCODE?
         BZ    LABL0370              NO
         TM    COMMFLAG,$NOFLOAT     FLOATING POINT ALLOWED?
         BNO   LABL0390              YES
         TM    OPFLAGS-OPDSECT(R14),$OPFLOAT  FLOATING POINT?
         BNO   LABL0390              NO
LABL0370 DS    0H
         CLI   0(R5),X'05'           BRANCH AND LINK REGISTER?
         BE    LABL0390              YES, ALLOW IT
         CLI   0(R5),X'0A'           SVC?
         BE    LABL0390              YES, ALLOW IT
         CLI   0(R5),X'07'           BRANCH REGISTER?
         BE    LABL0380              YES
         CLI   0(R5),X'45'           SVC?
         BE    LABL0390              YES, ALLOW IT
         CLI   0(R5),X'47'           BRANCH?
         BNE   LABL0400              NO.. NOT VALID OPCODE
LABL0380 DS    0H
         TM    1(R5),X'F0'           UNCONDITIONAL BRANCH?
         BNO   LABL0400              NO.. NOT VALID OPCODE
LABL0390 DS    0H
         BAL   R10,LABL2000          ADD A NEW DATA BLOCK IF NECESSARY
         STCM  R6,15,0(R9)           SAVE VALID DISPLACEMENT
         LA    R9,4(R9)              NEXT DISPLACEMENT SLOT
         MVC   0(4,R9),XFFFF         SET END OF TABLE
         LH    R1,OPLENGTH           INSTRUCTION'S LENGTH
         B     LABL0420
LABL0400 DS    0H
         LA    R1,1                  NEXT BYTE
LABL0410 DS    0H
         L     R0,DATASIZE           DATA AREA SIZE SO FAR
         AR    R0,R1                 PLUS SKIP AMOUNT
         ST    R0,DATASIZE           SAVE TOTAL
         TM    PGMFLAG,$BGNDISP      DISPLACEMENT ALREADY SET?
         BO    LABL0420              YES
         OI    PGMFLAG,$BGNDISP      STARTING DISPLACEMENT IS KNOWN
         ST    R6,DATADISP           SAVE STARTING DISPLACEMENT
LABL0420 DS    0H
         AR    R5,R1                 NEXT OBJECT CODE BYTE
         AR    R6,R1                 NEXT NEXT DISPLACEMENT
         B     LABL0230              LOOP
LABL0430 DS    0H
         BAL   R10,LABL2000          ADD DATA AREA IF NECESSARY
         ICM   R6,15,DATAEND         ENDING DISPLACEMENT
         LA    R6,1(R6)              NEXT BYTE
         LR    R5,R6                 COPY DISPLACEMENT
         A     R5,COMMTXT            PLUS BASE
         ITRACE ID=DATASKIP,         DISPLACEMENT SKIPPED DUE TO DATA  +
               RDATA1=R5,            .. NEW TEXT'S ADDRESS             +
               RDATA2=R6             .. NEW DISPLACEMENT
         B     LABL0230              LOOP
* ------------------------------------------------------------------- *
*        Add last data block if necessary                             *
* ------------------------------------------------------------------- *
LABL0440 DS    0H
         BAL   R10,LABL2000          ADD DATA BLOCK
* ------------------------------------------------------------------- *
*        Verify that all BASE and USINGs reference data areas or      *
*        instruction boundaries.  This is because DROP and USING      *
*        statements cannot be generated in the middle of an           *
*        instruction.  Also I will not generate DROPs or USINGs in    *
*        data areas (I guess this may cause problems with "S" type    *
*        DC instructions).                                            *
*                                                                     *
* ------------------------------------------------------------------- *
         ICM   R3,15,COMMBASE        FIRST BASE ENTRY
         USING BASEDSCT,R3
         BZ    LABL0470              NO BASES DEFINED
LABL0450 DS    0H
         MVC   WORKDISP,BASEBEGN     SET BEGINNING DISPLACEMENT
         MVC   EMSG03A,BEGNDISP      SET 'BEGINNING' IN MESSAGE
         BAL   R2,LABL0500           CHECK BEGINNING DISPLACEMENT
         MVC   BASEBEGN,WORKDISP     SET VERIFIED DISPLACEMENT
         CLC   BASEEND,COMMCSLN      BEYOND END OF CSECT?
         BNH   LABL0460              NO
         MVC   BASEEND,COMMCSLN      LIMIT TO CSECT LENGTH
LABL0460 DS    0H
         MVC   WORKDISP,BASEEND      SET ENDING DISPLACEMENT
         MVC   EMSG03A,ENDDISP       SET 'ENDING' IN MESSAGE
         BAL   R2,LABL0500           CHECK ENDING DISPLACEMENT
         MVC   BASEEND,WORKDISP      SET VERIFIED DISPLACMENT
         ICM   R3,15,BASENEXT        NEXT BASE BLOCK
         BNZ   LABL0450              LOOP
LABL0470 DS    0H
         ICM   R3,15,COMMUSNG        FIRST USING ENTRY
         USING USNGDSCT,R3           DEFINE BASE
         BZ    LABL0490              NO USINGS
LABL0480 DS    0H
         TM    USNGFLAG,$USNGND      DISPLACEMENTS?
         BO    LABL0485              NO
         MVC   WORKDISP,USNGBEGN     SET BEGINNING DISPLACEMENT
         MVC   EMSG03A,BEGNDISP      SET 'BEGINNING' IN MESSAGE
         BAL   R2,LABL0500           CHECK BEGINNING DISPLACEMENT
         MVC   USNGBEGN,WORKDISP     SET VERIFIED DISPLACEMENT
         MVC   WORKDISP,USNGEND      SET ENDING DISPLACEMENT
         MVC   EMSG03A,ENDDISP       SET 'ENDING' IN MESSAGE
         BAL   R2,LABL0500           CHECK ENDING DISPLACEMENT
         MVC   USNGEND,WORKDISP      SET VERIFIED DISPLACEMENT
LABL0485 DS    0H
         ICM   R3,15,USNGNEXT        NEXT USING BLOCK
         BNZ   LABL0480              LOOP
LABL0490 DS    0H
         TM    COMMFLAG,$ERROR       ERROR DETECTED YET?
         BO    EXIT0000              YES.. STOP
         B     LABL0570
LABL0500 DS    0H
         ICM   R7,15,COMMDATA        FIRST DATA
         BZ    LABL0530              NO DATA AREAS
LABL0510 DS    0H
         CLC   WORKDISP,DATABEGN     POSSIBLY IN DATA?
         BL    LABL0520              NO
         CLC   WORKDISP,DATAEND      WITHIN DATA?
         BNHR  R2                    YES, DISP IS OK
LABL0520 DS    0H
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         BNZ   LABL0510              LOOP
LABL0530 DS    0H
         L     R1,COMMDISP           DISPLACEMENT TABLE ADDRESS
LABL0540 DS    0H
         CLC   XFFFF,0(R1)           END OF TABLE REACHED?
         BE    LABL0560              YES.. INVALID BOUNDARY
         CLC   WORKDISP,0(R1)        DISPLACEMENT FOUND?
         BER   R2                    YES.. GOOD
         BL    LABL0560              INVALID BOUNDARY
LABL0550 DS    0H
         LA    R1,4(R1)              NEXT DISPLACEMENT
         B     LABL0540              LOOP
LABL0560 DS    0H
         MVC   EMSG03B,4(R3)         SET BLOCK ID
         UNPK  EMSG03O(9),WORKDISP(5)
         MVZ   EMSG03O,COMM0F0F      PREPARE FOR TRANSLATE
         TR    EMSG03O,COMMHXCH      TRANSLATE FOR PRINTING
         MVI   EMSG03O+8,C' '        RESTORE BLANK
         SH    R1,COMMH4             BACK-UP 1 INSTRUCTION
         MVC   WORKDISP,0(R1)        FORCE DISP TO LAST INSTRUCTION
         UNPK  EMSG03N(9),WORKDISP(5)
         MVZ   EMSG03N,COMM0F0F      PREPARE FOR TRANSLATE
         TR    EMSG03N,COMMHXCH      TRANSLATE FOR PRINTING
         MVI   EMSG03N+8,C' '        RESTORE BLANK
         MVC   PRTDATA(EMSG03L),EMSG03
         BAL   R10,PRT0000           PRINT MESSAGE
         BR    R2                    RETURN
* ------------------------------------------------------------------- *
*        Generate any labels for ENTRY points within the module       *
* ------------------------------------------------------------------- *
LABL0570 DS    0H
         ICM   R4,15,COMMESD         FIRST ESD ENTRY
         USING ESDDATA,R4            DEFINE BASE
         BZ    LABL0610              NO ESD ENTRIES
LABL0580 DS    0H
         CLI   ESDTYPE,$ESDLR        LABEL?
         BE    LABL0590              YES
         CLI   ESDTYPE,$ESDPC        PRIVATE CODE?
         BNE   LABL0600              NO
LABL0590 DS    0H
         CLC   ESDADDR,COMMCSAD+1    BELOW REQUESTED CSECT?
         BL    LABL0600              YES
         CLC   ESDADDR,COMMCSEA+1    ABOVE REQUESTED CSECT?
         BH    LABL0600              YES
         CLC   ESDNAME,COMMCSNM      CSECT'S NAME?
         BE    LABL0600              YES
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,7,ESDADDR          SYMBOL'S ADDRESS IN THE CSECT
         S     R1,COMMCSAD           CONVERT TO DISPLACEMENT
         STCM  R1,15,WORKDISP        SET DISPLACEMENT
         MVC   WORKLABL,ESDNAME      SET LABEL NAME
         MVI   WORKTYPE,$LABLE       LABEL WILL BE FROM AN ESD ENTRY
         BAL   R10,LABL1150          ADD LABEL
LABL0600 DS    0H
         ICM   R4,15,ESDNEXT         NEXT ESD ENTRY
         BNZ   LABL0580              LOOP
* ------------------------------------------------------------------- *
*        Generate labels for ADCON references                         *
* ------------------------------------------------------------------- *
LABL0610 DS    0H
         ICM   R7,15,COMMDATA        FIRST DATA BLOCK
         BZ    LABL0690              NO DATA AREAS
LABL0620 DS    0H
         CLI   DATATYPE,$DATAACN     ADCON?
         BNE   LABL0680              NO
         ICM   R1,15,DATABEGN        DISPLACEMENT TO ADCON
         A     R1,COMMTXT            PLUS BASE ADDRESS
         CLI   DATALEN+3,4           4-BYTE ADCON?
         BE    LABL0630              YES
         CLI   DATALEN+3,3           3-BYTE ADCON?
         BE    LABL0640              YES
         CLI   DATALEN+3,2           2-BYTE ADCON?
         BE    LABL0650              YES
         XC    WORKDISP(3),WORKDISP  SET BYTES 1-3 TO ZERO
         MVC   WORKDISP+3(1),0(R1)   COPY DISPLACEMENT
         B     LABL0660
LABL0630 DS    0H
         MVC   WORKDISP,0(R1)        COPY DISPLACEMENT
         B     LABL0660
LABL0640 DS    0H
         MVI   WORKDISP,X'00'        ZERO BYTE 1
         MVC   WORKDISP+1(3),0(R1)   COPY DISPLACEMENT
         B     LABL0660
LABL0650 DS    0H
         XC    WORKDISP(2),WORKDISP  CLEAR BYTES 1-2
         MVC   WORKDISP+2(2),0(R1)   COPY DISPLACEMENT
LABL0660 DS    0H
         TM    WORKDISP,X'80'        31-BIT MODE BIT ON?
         BNO   LABL0670              NO
         OI    DATAFLAG,$DATA31      SET 31-BIT INDICATOR
         NI    WORKDISP,X'7F'        TURN BIT OFF (NOT PART OF ADDRESS)
LABL0670 DS    0H
         MVI   WORKTYPE,$LABLR       LABEL WILL BE FOR RLD DATA
         ST    R7,SAVERLD            SAVE DATA RLD ITEM'S ADDRESS
         BAL   R10,LABL1040          ADD LABEL
         L     R7,SAVERLD            RESTORE DATA RLD ITEM'S ADDRESS
         MVC   DATALBA,WORKREF       SET LABEL BLOCK ADDRESS
         MVC   DATALBD,WORKOPD       SET DISPLACEMENT FROM LABEL
LABL0680 DS    0H
         ICM   R7,15,DATANEXT        NEXT BLOCK
         BNZ   LABL0620              LOOP
LABL0690 DS    0H
         MVI   WORKTYPE,C' '         CLEAR LABEL TYPE
* ------------------------------------------------------------------- *
*        Scan the object code and generate the reference table        *
* ------------------------------------------------------------------- *
         LA    R4,COMMREF            REFERENCE TABLE ANCHOR
         USING REFDSCT,R4            DEFINE BASE
         L     R5,COMMTXT            TEXT'S STORAGE ADDRESS
         SR    R6,R6                 INITIALIZE DISPLACEMENT
LABL0700 DS    0H
         C     R6,COMMCSLN           BEYOND END OF CSECT?
         BNL   LABL3000              YES, QUIT
         ICM   R7,15,COMMDATA        FIRST DATA BLOCK
         BZ    LABL0730              NOT WITHIN ANY DATA AREA
         ITRACE ID=DATACHK2,         TESTING FOR DATA AREA             +
               RDATA1=R6             .. CURRENT DISPLACEMENT
LABL0710 DS    0H
         CLM   R6,15,DATABEGN        TOO LOW FOR THIS DATA AREA?
         BL    LABL0720              YES
         CLM   R6,15,DATAEND         TOO HIGH FOR THIS DATA AREA?
         BH    LABL0720              YES
         ITRACE ID=DATA1             CURRENTLY IN A DATA AREA
         ICM   R6,15,DATAEND         ENDING DISPLACEMENT
         LA    R6,1(R6)              NEXT POSSIBLE INSTRUCTION DISP
         LR    R5,R6                 COPY DISPLACEMENT
         A     R5,COMMTXT            PLUS BASE ADDRESS
         ITRACE ID=NEWADDR1,         NEW ADDRESS AND DISP SET          +
               RDATA1=R5,            .. CURRENT TEXT ADDRESS           +
               RDATA2=R6             .. CURRENT DISPLACEMENT
         B     LABL0700              LOOP
LABL0720 DS    0H
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         BNZ   LABL0710              LOOP
LABL0730 DS    0H
         STC   R6,COMMDWRD           SAVE LAST BYTE
         TM    COMMDWRD,1            ODD ADDRESS?
         BO    ERR0070               YES
         ITRACE ID=NEWOPCDE,         CHECKING AN OPCODE                +
               DATA1=(R5),           .. CURRENT OPCODE                 +
               RDATA2=R6             .. CURRENT DISPLACEMENT
         SR    R8,R8                 CLEAR REGISTER
         IC    R8,0(R5)              INSERT POSSIBLE OPCODE
         SLL   R8,2                  MULTIPLY BY 4
         A     R8,AOP                PLUS BASE ADDRESS
         ICM   R8,15,0(R8)           OPCODE ENTRY ADDRESS
         BZ    ERR0030               NOT A VALID OPCODE
         USING OPDSECT,R8            DEFINE BASE
         ITRACE ID=OPCODE,           VALID OPCODE                      +
               RDATA1=R8,            .. OPCODE TABLE ENTRY'S ADDRESS   +
               DATA2=(R8)            .. PART OF THE OPCODE TABLE ENTRY
         TM    COMMFLAG,$NOB2        BYPASS B2 INSTRUCTIONS
         BO    LABL0740              YES
         CLI   0(R5),X'B2'           2-BYTE OPCODE?
         BNE   LABL0740              NO
         LR    R0,R5                 COPY INSTRUCTION ADDRESS
         LR    R1,R8                 COPY OPCODE TABLE ENTRY ADDRESS
         L     R15,AB2               'B2' OPCODES ENTRY POINT
         BALR  R14,R15               LINK TO DISASMB2
LABL0740 DS    0H
         TM    OPFLAGS,$OPREF        REFERENCE GENERATED?
         BNO   LABL0770              NO
* ------------------------------------------------------------------- *
*        Determine if operand 1 references a known BASE, DATA, or     *
*        USING (DSECT).                                               *
* ------------------------------------------------------------------- *
         XC    WORKOP1,WORKOP1       CLEAR REFERENCE 1
         XC    WORKOP2,WORKOP2       CLEAR REFERENCE 2
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,2(R5)              INSERT BASE AND PART OF DISP
         SRL   R1,4                  SHIFT BASE TO LOW ORDER BITS
         STC   R1,WORKBASE           SAVE BASE REG
         XC    WORKDISP,WORKDISP     CLEAR BYTES 1 AND 2
         MVC   WORKDISP+2(2),2(R5)   COPY BASE AND DISP
         NI    WORKDISP+2,X'0F'      LEAVE ONLY DISPLACEMENT
         BAL   R10,LABL1000          DETERMINE REFERENCE
         ITRACE ID=OP1REF,           OPERAND 1'S LABEL REFERENCE       +
               DATA1=WORKREF,        .. LABEL BLOCK'S ADDRESS          +
               DATA2=WORKOPD         .. DISPLACEMENT FROM LABEL
         MVC   WORKOP1,WORKREF       SAVE OPERAND 1 REFERENCE
         MVC   WORKOPD1,WORKOPD      SAVE DISPLACEMENT FROM LABEL
         CLI   OPLENGTH,6            TWO OPERANDS?
         BNE   LABL0750              NO
* ------------------------------------------------------------------- *
*        Determine if operand 2 references a known BASE, DATA, or     *
*        USING (DSECT).                                               *
* ------------------------------------------------------------------- *
LABL0750 DS    0H
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,4(R5)              INSERT BASE AND PART OF DISP
         SRL   R1,4                  SHIFT BASE TO LOW ORDER BITS
         STC   R1,WORKBASE           SAVE BASE REG
         XC    WORKDISP,WORKDISP     CLEAR WORK AREA
         MVC   WORKDISP+2(2),4(R5)   COPY BASE AND DISP
         NI    WORKDISP+2,X'0F'      LEAVE ONLY DISPLACEMENT
         BAL   R10,LABL1000          DETERMINE REFERENCE
         ITRACE ID=OP2REF,           OPERAND 2'S LABEL REFERENCE       +
               DATA1=WORKREF,        .. LABEL BLOCK'S ADDRESS          +
               DATA2=WORKOPD         .. DISPLACEMENT FROM LABEL
         MVC   WORKOP2,WORKREF       SAVE OPERAND 2 REFERENCE
         MVC   WORKOPD2,WORKOPD      SAVE DISPLACEMENT FROM LABEL
LABL0760 DS    0H
         OC    WORKOP1(8),WORKOP1    BOTH REFERENCES ZERO?
         BZ    LABL0770              YES
         GETMAIN RU,                 ACQUIRE STORAGE FOR REF BLOCK     +
               LV=REFL,              .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWREF,           NEW REFERENCE BLOCK               +
               RDATA1=R1             .. BLOCK'S ADDRESS
         ST    R1,REFNEXT            CHAIN TO PREVIOUS BLOCK
         LR    R4,R1                 SET BASE
         XC    REFNEXT(REFL),REFNEXT CLEAR BLOCK
         MVC   REFEYE,REF            SET BLOCK ID TO 'REF'
         MVC   REFOPER1,WORKOP1      SET OPERAND 1 REFERENCE ENTRY
         MVC   REFOPER2,WORKOP2      SET OPERAND 2 REFERENCE ENTRY
         MVC   REFDISP1,WORKOPD1     SET DISPLACEMENT FROM LABEL
         MVC   REFDISP2,WORKOPD2     SET DISPLACEMENT FROM LABEL
         STCM  R6,15,REFDISPI        REFERENCING INSTRUCTION'S DISP
LABL0770 DS    0H
         AH    R5,OPLENGTH           NEXT INSTRUCTION'S ADDRESS
         AH    R6,OPLENGTH           NEXT INSTRUCTION'S DISPLACEMENT
         B     LABL0700
* ------------------------------------------------------------------- *
*                                                                     *
*        Determine if base register is referencing a known BASE,      *
*        DATA area, or DSECT.  The base register has been isolated    *
*        in field 'WORKBASE'.                                         *
*                                                                     *
*        If a reference is found, 'WORKREF'  will be set to the       *
*        'LABEL' block assigned to that location.  If no LABEL can    *
*        determined, 'WORKREF' will be set to zero.                   *
*                                                                     *
*        R10 is the return address.                                   *
*                                                                     *
* ------------------------------------------------------------------- *
LABL1000 DS    0H
         ITRACE ID=FINDLABL,         ATTEMPTING TO FIND A LABEL        +
               RDATA1=(R6),          .. INSTRUCTION'S DISPLACEMENT     +
               RDATA2=(R5)           .. INSTRUCTION
         XC    WORKREF,WORKREF       ASSUME NO VALID REFERENCE
         ITRACE ID=SRCHBASE          SEARCHING BASE ENTRIES
         ICM   R3,15,COMMBASE        FIRST BASE ENTRY
         USING BASEDSCT,R3           DEFINE BASE
LABL1010 DS    0H
         BZ    LABL1200              NOT REFERENCING A KNOWN BASE
         CLC   WORKBASE,BASEREG      CORRECT REGISTER?
         BNE   LABL1020              NO
         CLM   R6,15,BASEBEGN        TOO LOW?
         BL    LABL1020              YES
         CLM   R6,15,BASEEND         TOO HIGH?
         BNH   LABL1030              THIS IS A DEFINED BASE
LABL1020 DS    0H
         ICM   R3,15,BASENEXT        NEXT BASE
         B     LABL1010              LOOP
LABL1030 DS    0H
         ITRACE ID=BASEFND,          BASE REFERENCE FOUND              +
               RDATA1=R3,            .. BASE FOR REFERENCE             +
               DATA2=BASEBEGN        .. A PORTION OF THE ENTRY
         ICM   R1,15,BASEDISP        DISPLACEMENT TO BASE
         A     R1,WORKDISP           PLUS DISPLACEMENT FROM BASE
         STCM  R1,15,WORKDISP        SAVE TOTAL DISPLACEMENT
LABL1040 DS    0H
         XC    WORKOPD,WORKOPD       CLEAR DISPLACEMENT FROM LABEL
         ICM   R7,15,COMMDATA        FIRST DATA ENTRY
LABL1050 DS    0H
         BZ    LABL1090              NOT IN A DATA AREA
         CLC   WORKDISP,DATABEGN     TOO LOW?
         BL    LABL1060              YES
         CLC   WORKDISP,DATAEND      TOO HIGH?
         BNH   LABL1070              NO.. DATA REFERENCED
LABL1060 DS    0H
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         B     LABL1050              LOOP
* ------------------------------------------------------------------- *
*         DATA area referenced                                        *
* ------------------------------------------------------------------- *
LABL1070 DS    0H
         ITRACE ID=DATAREF,          DATA REFERENCE FOUND              +
               RDATA1=R7,            .. BASE FOR REFERENCE             +
               DATA2=DATABEGN        .. A PORTION OF THE ENTRY
         MVC   WORKLABL,DATA         SET NAME TO 'DATA    '
         XC    WORKOPD,WORKOPD       ASSUME NO DISPLACEMENT
         CLI   DATATYPE,$DATAUSR     USER DEFINED DATA?
         BE    LABL1080              YES
         CLI   DATATYPE,$DATAINT     INTERNALLY DETECTED DATA AREA?
         BE    LABL1080              YES
* ------------------------------------------------------------------- *
*         Data referenced is an RLD item (ADCON, VCON, Q, or CXD).    *
*         RLD items cannot be sub-divided (we cannot generate         *
*         a label in the middle of a four byte ADCON for example).    *
*         The reference will be changed so the label will be defined  *
*         at the beginning of the RLD and a displacement from the     *
*         label will be returned in WORKOPD.                          *
* ------------------------------------------------------------------- *
         ICM   R0,15,WORKDISP        DISPLACEMENT
         ICM   R1,15,DATABEGN        DISPLACEMENT
         STCM  R1,15,WORKDISP        CHANGE TO DATA ORIGIN
         SR    R0,R1                 MINUS ORIGIN
         STCM  R0,15,WORKOPD         DISPLACEMENT FROM LABEL
LABL1080 DS    0H
         CLI   WORKTYPE,$LABLR       LABEL FOR AN RLD REFERENCE?
         BE    LABL1150              YES
         MVI   WORKTYPE,$LABLD       DATA LABEL
         B     LABL1150
* ------------------------------------------------------------------- *
*         CSECT reference from a defined BASE                         *
* ------------------------------------------------------------------- *
LABL1090 DS    0H
         ITRACE ID=CSECTREF,         DATA REFERENCE FOUND              +
               RDATA1=R3,            .. BASE FOR REFERENCE             +
               DATA2=BASEBEGN        .. A PORTION OF THE ENTRY
         CLI   WORKTYPE,$LABLR       WORKING ON A RLD ITEM?
         BE    LABL1100              YES
         MVI   WORKTYPE,$LABLI       INSTRUCTION LABEL
LABL1100 DS    0H
         MVC   WORKLABL,CHARZERO     SET NAME TO '0000'
         SR    R15,R15               CLEAR REGISTER
         ICM   R15,3,COMMPFXL        PREFIX'S LENGTH
         BZ    LABL1110              PREFIX NOT DEFINED
         BCTR  R15,0                 FOR EXECUTE
         EX    R15,LABLBMVC          SET LABEL PREFIX
         B     LABL1120
LABL1110 DS    0H
         MVC   WORKLABL,DATA         SET LABEL PREFIX
LABL1120 DS    0H
         L     R1,COMMDISP           DISPLACEMENT TABLE ADDRESS
LABL1130 DS    0H
         CLC   XFFFF,0(R1)           END OF TABLE?
         BE    LABL1140              YES
         CLC   WORKDISP,0(R1)        DISPLACEMENT FOUND?
         BE    LABL1150              YES
         BL    LABL1140              ONE TOO FAR
         LA    R1,4(R1)              NEXT DISPLACEMENT
         B     LABL1130              LOOP
LABL1140 DS    0H
         C     R1,COMMDISP           FIRST ONE?
         BER   R10                   YES, NO VALID LABEL
         SH    R1,COMMH4             BACK-UP 1 DISPLACEMENT
         ICM   R0,15,WORKDISP        DISPLACEMENT TO DATA REFERENCED
         S     R0,0(R1)              MINUS DISPLACEMENT TO LABEL
         ST    R0,WORKOPD            SET DISPLACEMENT FROM LABEL
         MVC   WORKDISP,0(R1)        SET DISPLACEMENT TO PREV INSTR
* ------------------------------------------------------------------- *
*         Determine where this LABEL goes in the CSECT LABEL chain    *
* ------------------------------------------------------------------- *
LABL1150 DS    0H
         ITRACE ID=ADDLABEL,         CSECT REFERENCE                   +
               DATA1=WORKDISP        .. DISPLACEMENT REFERENCED
         LA    R3,COMMLABL           FORWARD POINTER'S ADDRESS
         ICM   R9,15,COMMLABL        FIRST CSECT LABEL
         USING LABLDSCT,R9           DEFINE BASE
         BZ    LABL1170              INSERT ON END OF CHAIN
LABL1160 DS    0H
         CLC   LABLDISP,WORKDISP     TEST DISPLACEMENT
         BE    LABL1190              DUPLICATE
         BH    LABL1170              INSERT NEW NAME HERE
         LA    R3,LABLNEXT           CURRENT BLOCK'S FWD POINTER ADDR
         ICM   R9,15,LABLNEXT        NEXT CSECT LABEL
         BNZ   LABL1160              LOOP
* ------------------------------------------------------------------- *
*         A new CSECT LABEL needs to be generated                     *
* ------------------------------------------------------------------- *
LABL1170 DS    0H
         GETMAIN RU,                 ACQUIRE A NEW LABEL BLOCK         +
               LV=LABLL,             .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWLABL,          NEW CSECT LABEL                   +
               RDATA1=R1,            .. BLOCK'S ADRESS                 +
               DATA2=WORKDISP        .. LABEL'S DISPLACEMENT
         ST    R1,LABLNEXT-LABLDSCT(R3)  PREVIOUS BLOCK'S FWD POINTER
         ST    R9,LABLNEXT-LABLDSCT(R1)  NEXT BLOCK'S ADDRESS
         LR    R9,R1                 SET BASE
         MVC   LABLEYE,LABL          SET BLOCK ID
         CLI   WORKTYPE,$LABLE       ESD LABEL?
         BE    LABL1180              YES
         TM    COMMFLAG,$SEQLABL     SEQUENTIAL LABELS?
         BO    LABL1180              YES.. SUFFIX WILL BE CREATED LATER
         UNPK  WORKLABL+4(5),WORKDISP+2(3)
         MVZ   WORKLABL+4(8),COMM0F0F  TURN OFF ZONES
         TR    WORKLABL+4(8),COMMHXCH  TRANSLATE TO PRINTABLE
LABL1180 DS    0H
         MVC   LABLNAME,WORKLABL     SET LABEL'S NAME
         MVC   LABLDISP,WORKDISP     SET DISPLACEMENT
         MVC   LABLTYPE,WORKTYPE     SET LABEL TYPE
LABL1190 DS    0H
         ST    R9,WORKREF            SET REFERENCE TO THIS LABEL
         CLI   LABLTYPE,$LABLD       LABEL TYPE CURRENTLY 'DATA'?
         BER   R10                   YES
         CLI   WORKTYPE,$LABLI       WAS REQUEST FOR INSTRUCTION LABEL?
         BNER  R10                   NO
         MVC   LABLNAME(4),WORKLABL  CHANGE PREFIX
         BR    R10
LABLBMVC MVC   WORKLABL(0),COMMPFX   SET PREFIX
* ------------------------------------------------------------------- *
*         No defined CSECT reference was found, try a DSECT           *
* ------------------------------------------------------------------- *
LABL1200 DS    0H
         ITRACE ID=SRCHDSCT          SEARCHING DSECT ENTRIES
         ICM   R3,15,COMMUSNG        FIRST USING BLOCK
         USING USNGDSCT,R3           DEFINE BASE
LABL1210 DS    0H
         BZR   R10                   NOT REFERENCING A DSECT
         CLC   WORKBASE,USNGBASE     CORRECT REGISTER?
         BNE   LABL1220              NO
         TM    USNGFLAG,$USNGND      DISPLACEMENTS ON USING STATEMENT?
         BO    LABL1230              NO
         CLM   R6,15,USNGBEGN        TOO LOW?
         BL    LABL1220              YES
         CLM   R6,15,USNGEND         TOO HIGH?
         BNH   LABL1230              NO, DSECT REFERENCE LOCATED
LABL1220 DS    0H
         ICM   R3,15,USNGNEXT        NEXT USING BLOCK
         B     LABL1210              LOOP
LABL1230 DS    0H
         L     R2,USNGDSA            ASSOCIATED DSECT BLOCK'S ADDRESS
         ITRACE ID=DSCTFND,          DSECT REFERENCE                   +
               RDATA1=R3,            .. USING BLOCK'S ADDRESS          +
               RDATA2=R2             .. DSECT BLOCK'S ADDRESS
         ICM   R9,15,DSCTLBA         FIRST LABEL BLOCK'S ADDRESS
         BZR   R10                   NO VALID LABEL KNOWN
         ICM   R0,15,USNGDISP        SET OFFSET INTO DSECT
LABL1240 DS    0H
         ICM   R1,15,LABLDISP        DISPLACEMENT TO LABEL
         SR    R1,R0                 MINUS DISPLACEMENT TO LABEL
         CLM   R1,15,WORKDISP        DISPLACEMENTS MATCH?
         BNH   LABL1250              EQUAL OR LOW.. TAKE IT
         ICM   R9,15,LABLNEXT        NEXT LABEL
         BNZ   LABL1240              LOOP
         BR    R10                   RETURN
LABL1250 DS    0H
         ITRACE ID=LABLFND,          LABEL WITHIN THE DSECT FOUND      +
               RDATA1=R9,            .. LABEL BLOCK'S ADDRESS          +
               DATA2=LABLNAME        .. LABEL
         ICM   R0,15,WORKDISP        DISPLACEMENT FROM INSTRUCTION
         SR    R0,R1                 DISPLACEMENT TO LABEL
         STCM  R0,15,WORKOPD         SAVE DISPLACEMENT FROM THE LABEL
         ST    R9,WORKREF            LABEL BLOCK'S ADDRESS
         BR    R10                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
*        Add DATA blocks                                              *
*                                                                     *
*        R10 is the return address                                    *
*                                                                     *
* ------------------------------------------------------------------- *
LABL2000 DS    0H
         OC    DATASIZE,DATASIZE     ANY DATA
         BZR   R10                   NO DATA
         LA    R2,COMMDATA           ANCHOR'S ADDRESS
         ICM   R7,15,COMMDATA        FIRST DATA AREA
         BZ    LABL2020              NO DATA AREAS
LABL2010 DS    0H
         CLC   DATABEGN,DATADISP     DOES IT GO HERE?
         BH    LABL2020              YES
         LA    R2,DATANEXT           NEXT BLOCK'S POINTER
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK'S ADDRESS
         BNZ   LABL2010              LOOP
LABL2020 DS    0H
         GETMAIN RU,                 ACQUIRE STORAGE FOR DATA BLOCK    +
               LV=DATAL,             .. SIZE                           +
               LOC=ANY
         ITRACE ID=NEWDATA,          NEW BLOCK DATA BLOCK              +
               RDATA1=R1             .. BLOCK'S ADDRESS
         ST    R1,0(R2)              CHAIN PREVIOUS BLOCK TO NEW
         ST    R7,DATANEXT-DATADSCT(R1)   CHAIN NEXT BLOCK TO NEW BLOCK
         LR    R7,R1                 SET BASE
         MVC   DATAEYE,DATA          SET BLOCK IDENTIFIER
         MVC   DATABEGN,DATADISP     STARTING DISPLACEMENT
         LR    R1,R6                 CURRENT DISPLACEMENT
         C     R1,COMMCSLN           BEYOND END OF CSECT?
         BL    LABL2030              NO
         L     R1,COMMCSLN           LIMIT TO CSECT LENGTH
         B     LABL2040
LABL2030 DS    0H
         BCTR  R1,0
LABL2040 DS    0H
         STCM  R1,15,DATAEND         ENDING DISPLACEMENT
         S     R1,DATABEGN           STARTING DISPLACEMENT
         LA    R1,1(R1)              PLUS 1
         STCM  R1,15,DATALEN         DATA AREA SIZE
         XC    DATASIZE,DATASIZE     CLEAR DATA SKIPPED
         XC    DATADISP,DATADISP     CLEAR STARTING DISPLACEMENT
         MVI   DATATYPE,$DATAINT     INTERNALLY DETECTED DATA
         MVC   DATANAME,COMMBLKS     INITIALIZE NAME
         XC    DATALBA,DATALBA       INITIALIZE LABEL BLOCK ADDRESS
         XC    DATALBD,DATALBD       INITIALIZE DISPLACEMENT FROM LABEL
         NI    PGMFLAG,255-$BGNDISP  RESET FLAG
         BR    R10
* ------------------------------------------------------------------- *
*                                                                     *
*        If sequential LABELs are desired, run the LABEL chain and    *
*        set the suffix in the non-ESD LABELs.                        *
*                                                                     *
* ------------------------------------------------------------------- *
LABL3000 DS    0H
         TM    COMMFLAG,$SEQLABL     SEQUENTIALLY NUMBERED LABELS?
         BNO   EXIT0000              NO
         ICM   R9,15,COMMLABL        FIRST LABEL
         BZ    EXIT0000              NO LABELS TO NUMBER
         ITRACE ID=SEQNBR
LABL3010 DS    0H
         CLI   LABLTYPE,$LABLE       ESD TYPE LABEL?
         BE    LABL3020              YES, DON'T MODIFY IT
         AP    LABLNBR,P10           ADD TO LABEL COUNTER
         MVC   EDITWORK,EDITWORD     INITIALIZE WITH EDIT WORD
         ED    EDITWORK,LABLNBR      EDIT LABEL NUMBER
         MVC   LABLNAME+4(4),EDITWORK+2
LABL3020 DS    0H
         ICM   R9,15,LABLNEXT        NEXT LABEL BLOCK
         BNZ   LABL3010              LOOP
         B     EXIT0000              EXIT
ERR0010  DS    0H
         MVC   EMSG01NM,USNGDSNM     SET DSECT'S NAME
         MVC   PRTDATA(EMSG01L),EMSG01
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0020  DS    0H
         UNPK  EMSG02B(9),DATABEGN(5)
         MVZ   EMSG02B,COMM0F0F      PREPARE FOR TRANSLATE
         TR    EMSG02B,COMMHXCH      TRANSLATE FOR PRINTING
         MVI   EMSG02B+8,C' '        RESTORE BLANK
         UNPK  EMSG02E(9),DATAEND(5) UNPACK ENDING DISPLACEMENT
         MVZ   EMSG02E,COMM0F0F      PREPARE FOR TRANSLATE
         TR    EMSG02E,COMMHXCH      TRANSLATE FOR PRINTING
         MVI   EMSG02E+8,C' '        RESTORE BLANK
         MVC   PRTDATA(EMSG02L),EMSG02
         BAL   R10,PRT0000           PRINT MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0030  DS    0H
         MVC   PRTDATA(EMSG04L),EMSG04
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0040  DS    0H
         MVC   PRTDATA(EMSG06L),EMSG06
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0060  DS    0H
         MVC   PRTDATA(EMSG08L),EMSG08
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
ERR0070  DS    0H
         MVC   PRTDATA(EMSG09L),EMSG09
         BAL   R10,PRT0000           PRINT ERROR MESSAGE
         OI    COMMFLAG,$ERROR+$ABORT
         B     EXIT0000              AND EXIT
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R10                   RETURN
EXIT0000 DS    0H
         TM    COMMDD,$DSECTDD       DISDSECT DD PRESENT?
         BNO   EXIT0010              NO
         ITRACE ID=CLOSE
         CLOSE PREDCB
         L     R0,PREIOSZ            I/O AREA SIZE
         L     R1,PREIOA             I/O AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
EXIT0010 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE08   DC    18F'0'                REGISTER SAVE AREA
WORKOP1  DC    A(0)                  OPERAND 1 REFERENCE
WORKOP2  DC    A(0)                  OPERAND 2 REFERENCE
WORKREF  DC    A(0)                  REFERENCED LABEL BLOCK
SAVERLD  DC    A(0)                  SAVED RLD DATA ITEM ADDRESS
PREIOA   DC    A(0)                  PRE-PROCESSED DSECT I/O AREA
PREIOSZ  DC    A(0)                  SIZE OF PREIOA
X80      DC    A(X'80000000')
WORKOPD  DC    XL4'00'               DISPLACEMENT FROM LABEL
WORKOPD1 DC    XL4'00'               DISPLACEMENT FROM LABEL (OPER 1)
WORKOPD2 DC    XL4'00'               DISPLACEMENT FROM LABEL (OPER 2)
WORKDISP DC    XL4'00'               DISPLACEMENT FROM WORK BASE
DATADISP DC    XL4'00'               STARTING DISPLACEMENT
DATASIZE DC    XL4'00'               SIZE OF CURRENT DATA AREA
H4       DC    H'4'
PGMFLAG  DC    X'00'
$BGNDISP EQU   X'01'                 .. DATA AREA BEGIN DISP KNOWN
LABLNBR  DC    PL3'0'                COUNTER FOR SEQUENTIAL LABELS
P10      DC    P'10'                 CONSTANT
EDITWORK DC    X'F02020202020'       EDIT WORK AREA
EDITWORD DC    X'F02020202020'       CONSTANT
DSECTOFF DC    XL2'00'               DISPLACEMENT TO LABEL IN DSECT
WORKLABL DC    CL9' '                TEMP LABEL NAME
WORKBASE DC    X'00'                 BASE FOR CURRENT OPERAND
WORKTYPE DC    C' '                  LABEL TYPE BEING CREATED
DSCT_ID  DC    CL8'DSECT'
LABL_ID  DC    CL8'LABEL'
EQU_ID   DC    CL8'EQU'
REF      DC    CL8'REF'
ESD      DC    CL8'ESD '
DATA     DC    CL8'DATA'
LABL     DC    CL8'LABL'
CHARZERO DC    CL4'0000'
XFFFF    DC    X'FFFFFFFF'
BEGNDISP DC    CL09'BEGINNING'
ENDDISP  DC    CL09'ENDING   '
EMSG01   DC    C'DISASM0801E DSECT '
EMSG01NM DC    CL08' '
         DC    C' is not present, but is referenced on a USING statemen+
               t'
EMSG01L  EQU   *-EMSG01
EMSG02   DC    C'DISASM0802E instructions overlap data defined at '
EMSG02B  DC    CL8' '
         DC    C' to '
EMSG02E  DC    CL8' '
         DC    C' '
EMSG02L  EQU   *-EMSG02
EMSG03   DC    C'DISASM0803W '
EMSG03A  DC    CL9' '
         DC    C' Displacement in a '
EMSG03B  DC    CL8' '
         DC    C' block is changed from '
EMSG03O  DC    CL8' '
         DC    C' to '
EMSG03N  DC    CL8' '
         DC    C' to reference an instruction boundary'
EMSG03L  EQU   *-EMSG03
EMSG04   DC    C'DISASM0804E Invalid opcode during reference table gene+
               ration'
EMSG04L  EQU   *-EMSG04
EMSG05   DC    C'DISASM0805E Label '
EMSG05N  DC    CL8' '
         DC    C' is not in DSECT '
EMSG05D  DC    CL8' '
         DC    C' as requested on a USING statement'
EMSG05L  EQU   *-EMSG05
EMSG06   DC    C'DISASM0806E Label not found in DSECT during reference +
               table generation'
EMSG06L  EQU   *-EMSG06
EMSG08   DC    C'DISASM0808E overlapping data areas not detected by DIS+
               ASM02'
EMSG08L  EQU   *-EMSG08
EMSG09   DC    C'DISASM0809E Attempt to locate an instruction on an odd+
                displacement boundary'
EMSG09L  EQU   *-EMSG09
PREDCB   DCB   DSORG=PO,                                               +
               DDNAME=DISDSECT,                                        +
               EODAD=LABL0160,                                         +
               RECFM=VB,                                               +
               MACRF=R
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
         SPACE 2
         LTORG
ALPHATRT DS    0C 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    00-0F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    10-1F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    20-2F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    30-3F
         DC    X'00FFFFFFFFFFFFFFFFFFFF00FFFFFFFF'    40-4F BLANK AND .
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFF00FFFFFF'    50-5F *
         DC    X'0000FFFFFFFFFFFFFFFFFF00FFFFFFFF'    60-6F - / AND ,
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    70-7F
         DC    X'FF000000000000000000FFFFFFFFFFFF'    80-8F LOWER A-I
         DC    X'FF000000000000000000FFFFFFFFFFFF'    90-9F LOWER J-R
         DC    X'FFFF0000000000000000FFFFFFFFFFFF'    A0-AF LOWER S-Z
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'    B0-BF
         DC    X'FF000000000000000000FFFFFFFFFFFF'    C0-CF UPPER A-I
         DC    X'FF000000000000000000FFFFFFFFFFFF'    D0-DF UPPER J-R
         DC    X'FFFF0000000000000000FFFFFFFFFFFF'    E0-EF UPPER S-Z
         DC    X'00000000000000000000FFFFFFFFFFFF'    F0-FF 0-9
         SPACE 2
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         DCBD  DSORG=PO
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM08
./ ADD NAME=DISASM09
         TITLE 'DISASM09 - SOURCE CODE GENERATOR'
         MACRO
         SVCDEF &SVCNBR,&DESC
         LCLA   &LEN
&LEN     SETA   K'&DESC-2
         DC     AL2(&LEN+5)          TOTAL ENTRY LENGTH
         DC     AL2(&LEN)            DESCRIPTION'S LENGTH
         DC     X'&SVCNBR'           SVC NUMBER
         DC     C&DESC               DESCRIPTION
         MEND
         COPY   DISASMGB
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM09                                             *
*                                                                    *
*  Function:                                                         *
*   Source code generator (This is it folks).                        *
*                                                                    *
*--------------------------------------------------------------------*
DISASM09 CSECT
DISASM09 AMODE 31
DISASM09 RMODE 24
         USING DISASM09,R12,R10
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     GEN0000               SKIP EYECATCHER
         DC    CL8'DISASM09'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
GEN0000  DS    0H
         LA    R10,2048(R12)         LOAD 2ND BASE..
         LA    R10,2048(R10)         .. 4K FROM 1ST BASE
         LA    R1,SAVE09             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         ITRACE ID=ENTRY
         TM    COMMDD,$PUNCHDD       IS DISPUNCH DD PRESENT?
         BNO   GEN0010               NO
         ITRACE ID=OPENPNCH
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (DISPUNCH,OUTPUT)     OPEN DISPUNCH
         BAL   R14,AM31              SWITCH TO 24-BIT MODE
GEN0010  DS    0H
         MVC   COMMSUBH(SUBHEADL),SUBHEAD
         LA    R1,SUBHEADL           SUBHEADING LENGTH
         STH   R1,COMMSUBL           SET LENGTH
         MVI   COMMSUBL,X'FF'        SET NON-CENTERED INDICATOR
         MVC   SRCLABL,COMMCSNM      SET CSECT NAME
         MVC   SRCMNEM,CSCTOPCD      SET MNEMONIC TO 'CSECT'
         MVC   SRCDISP,CHARZERO      DISPLACEMENT IS ZERO
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT CSECT STATEMENT
         BAL   R9,PUNCH000           PUNCH CSECT STATEMENT
* ------------------------------------------------------------------- *
*      Generate ENTRY statements                                      *
* ------------------------------------------------------------------- *
         ICM   R3,15,COMMESD         FIRST ESD ENTRY
         USING ESDDATA,R3            DEFINE BASE
         BZ    GEN0050               NO ESD ENTRIES
GEN0020  DS    0H
         CLI   ESDTYPE,$ESDLR        LABEL?
         BE    GEN0030               YES
         CLI   ESDTYPE,$ESDPC        PRIVATE CODE?
         BNE   GEN0040               NO
GEN0030  DS    0H
         CLC   ESDADDR,COMMCSAD+1    TOO LOW FOR OUR CSECT?
         BL    GEN0040               YES
         CLC   ESDADDR,COMMCSEA+1    TOO HIGH FOR OUR CSECT?
         BH    GEN0040               YES
         CLC   ESDNAME,COMMCSNM      SAME AS THE CSECT NAME?
         BE    GEN0040               YES
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE AREA
         MVC   SRCMNEM,ENTROPCD      SET OPCODE (ENTRY)
         MVC   SRCOPER(L'ESDNAME),ESDNAME
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT ENTRY STATEMENT
         BAL   R9,PUNCH000           PUNCH ENTRY STATEMENT
GEN0040  DS    0H
         ICM   R3,15,ESDNEXT         NEXT ESD ENTRY
         BNZ   GEN0020               LOOP
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
GEN0050  DS    0H
         SR    R3,R3                 INITIALIZE INSTRUCTION DISP
         STCM  R3,15,DISPI           SET INSTRUCTION DISPLACEMENT
         L     R4,COMMTXT            INITIALIZE INSTRUCTION ADDRESS
         ICM   R5,15,COMMLABL        FIRST CSECT LABEL
         USING LABLDSCT,R5           DEFINE BASE
         BZ    GEN0060               NO LABELS
         MVC   DISPL,LABLDISP        SET LABEL DISPLACEMENT
         B     GEN0070
GEN0060  DS    0H
         MVI   DISPL,X'FF'           SET EOF FLAG
GEN0070  DS    0H
         ICM   R6,15,COMMREF         FIRST REFERENCE ENTRY
         USING REFDSCT,R6            DEFINE BASE
         BZ    GEN0080               NO REFERENCES
         MVC   DISPR,REFDISPI        SET REFERENCE DISP
         B     GEN0090
GEN0080  DS    0H
         MVI   DISPR,X'FF'           SET EOF FLAG
GEN0090  DS    0H
         ICM   R7,15,COMMDATA        FIRST DATA AREA
         USING DATADSCT,R7           DEFINE BASE
         BZ    GEN0100               NO DATA AREAS
         MVC   DISPD,DATABEGN        SET DATA AREA DISPLACEMENT
         B     GEN0110
GEN0100  DS    0H
         MVI   DISPD,X'FF'           SET EOF FLAG
* ------------------------------------------------------------------- *
*                                                                     *
*        Beginning of source generation loop                          *
*                                                                     *
* ------------------------------------------------------------------- *
GEN0110  DS    0H
         ITRACE ID=GENLOOP,          STARTING GEN LOOP                 +
               DATA1=DISPI,          .. INSTRUCTION DISPLACEMENT       +
               DATA2=DISPD           .. NEXT DATA AREA DISPLACEMENT
         C     R3,COMMCSLN           REACHED END OF MODULE?
         BNL   GEN0700               YES.. COPY ASSEMBLER INPUT
         CLC   DISPI,DISPL           GEN A LABEL AT THIS TIME?
         BNE   GEN0130               NO
* ------------------------------------------------------------------- *
*        Generate a LABEL                                             *
* ------------------------------------------------------------------- *
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE STATEMENT AREA
         UNPK  SRCDISP(9),DISPI(5)   UNPACK DISPLACEMENT
         MVZ   SRCDISP,COMM0F0F      TURN OFF ZONES
         TR    SRCDISP,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   SRCDISP+8,C' '        RESTORE THE BLANK
         MVC   SRCLABL,LABLNAME      SET LABEL
         MVC   SRCMNEM,DSOPCD        SET OPCODE TO 'DS'
         MVC   SRCOPER(L'OPER0H),OPER0H
         TM    LABLTYPE,$LABLI       LABEL AT AN INSTRUCTION?
         BO    GEN0115               YES
         MVC   SRCOPER(L'OPER0C),OPER0C
GEN0115  DS    0H
         MVC   PRTDATA(SRCL),SRC     COPY STATEMENT TO PRINT
         BAL   R9,PRT0000            PRINT SOURCE STATEMENT
         BAL   R9,PUNCH000           PUNCH SOURCE STATEMENT
         ICM   R5,15,LABLNEXT        NEXT LABEL
         BNZ   GEN0120
         MVI   DISPL,X'FF'           SET END OF FILE
         B     GEN0130
GEN0120  DS    0H
         MVC   DISPL,LABLDISP        SET NEW LABEL DISPLACEMENT
* ------------------------------------------------------------------- *
*        Test for DATA area                                           *
* ------------------------------------------------------------------- *
GEN0130  DS    0H
         CLC   DISPI,DISPD           IS THIS DATA?
         BE    GEN0400               YES
         BH    ERR0010               INTERNAL ERROR
         TM    DISPI+3,X'01'         DISPLACEMENT ODD?
         BO    ERR0040               YES
* ------------------------------------------------------------------- *
*        Generate DROP statements for USING blocks                    *
* ------------------------------------------------------------------- *
         ICM   R2,15,COMMUSNG        FIRST USING BLOCK
         USING USNGDSCT,R2           DEFINE BASE
         BZ    GEN0160               NO USING BLOCKS
GEN0140  DS    0H
         TM    USNGFLAG,$USNGND      DISPLACEMENTS?
         BO    GEN0150               NO.. NEVER NEED A DROP
         CLC   USNGEND,DISPI         TIME FOR 'DROP' STATEMENT?
         BNE   GEN0150               NO
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE AREA
         MVC   SRCMNEM,DROPOPCD      SET OPCODE
         MVI   SRCOPER,C'R'          SET REGISTER PREFIX
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,USNGBASE           BASE REGISTER
         SLL   R1,1                  MULTIPLY BY 2
         LA    R1,COMMNBR(R1)        NUMBER'S ADDRESS
         MVC   SRCOPER+1(2),0(R1)    MOVE NUMBER
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT DROP STATEMENT
         BAL   R9,PUNCH000           PUNCH DROP STATEMENT
GEN0150  DS    0H
         ICM   R2,15,USNGNEXT        NEXT USING BLOCK
         BNZ   GEN0140               LOOP
* ------------------------------------------------------------------- *
*        Generate DROP statements for BASE blocks                     *
* ------------------------------------------------------------------- *
GEN0160  DS    0H
         ICM   R2,15,COMMBASE        FIRST USING BLOCK
         USING BASEDSCT,R2           DEFINE BASE
         BZ    GEN0190               NO USING BLOCKS
GEN0170  DS    0H
         CLC   BASEEND,DISPI         TIME FOR 'DROP' STATEMENT?
         BNE   GEN0180               NO
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE AREA
         MVC   SRCMNEM,DROPOPCD      SET OPCODE
         MVI   SRCOPER,C'R'          SET REGISTER PREFIX
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,BASEREG            BASE REGISTER
         SLL   R1,1                  MULTIPLY BY 2
         LA    R1,COMMNBR(R1)        NUMBER'S ADDRESS
         MVC   SRCOPER+1(2),0(R1)    MOVE NUMBER
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT DROP STATEMENT
         BAL   R9,PUNCH000           PUNCH DROP STATEMENT
GEN0180  DS    0H
         ICM   R2,15,BASENEXT        NEXT BASE BLOCK
         BNZ   GEN0170               LOOP
* ------------------------------------------------------------------- *
*        Generate USING statements for USING blocks                   *
* ------------------------------------------------------------------- *
GEN0190  DS    0H
         ICM   R2,15,COMMUSNG        FIRST USING BLOCK
         USING USNGDSCT,R2           DEFINE BASE
         BZ    GEN0270               NO USING BLOCKS
GEN0200  DS    0H
         OC    DISPI,DISPI           INITIAL DISPLACEMENT?
         BNZ   GEN0210               NO
         TM    USNGFLAG,$USNGND      DISPLACEMENTS?
         BO    GEN0230               NO..  GENERATE AT DISP ZERO
         B     GEN0220               YES.. DISPLACEMENTS MUST MATCH
GEN0210  DS    0H
         TM    USNGFLAG,$USNGND      DISPLACEMENTS?
         BO    GEN0260               NO
GEN0220  DS    0H
         CLC   USNGBEGN,DISPI        TIME FOR 'USING' STATEMENT?
         BNE   GEN0260               NO
GEN0230  DS    0H
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE AREA
         MVC   SRCMNEM,USNGOPCD      SET OPCODE
         MVC   SRCOPER(8),USNGDSNM   SET DSECT'S NAME
         CLC   USNGLBNM,COMMBLKS     LABEL NAME PRESENT?
         BE    GEN0235               NO
         MVC   SRCOPER(8),USNGLBNM   SET LABEL NAME
GEN0235  DS    0H
         LA    R1,SRCOPER            FIRST CHARACTER
GEN0240  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    GEN0250               YES
         LA    R1,1(R1)              NEXT
         B     GEN0240               LOOP
GEN0250  DS    0H
         ST    R1,GENADDR            SET OUTPUT ADDRESS
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKREG,USNGBASE      COPY REGISTER
         BAL   R15,GENREG00          GENERATE REGISTER
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT USING STATEMENT
         BAL   R9,PUNCH000           PUNCH USING STATEMENT
GEN0260  DS    0H
         ICM   R2,15,USNGNEXT        NEXT USING BLOCK
         BNZ   GEN0200               LOOP
* ------------------------------------------------------------------- *
*        Generate using statements for BASE blocks                    *
* ------------------------------------------------------------------- *
GEN0270  DS    0H
         ICM   R2,15,COMMBASE        FIRST USING BLOCK
         USING BASEDSCT,R2           DEFINE BASE
         BZ    GEN0330               NO BASE BLOCKS
GEN0280  DS    0H
         CLC   BASEBEGN,DISPI        TIME FOR 'USING' STATEMENT?
         BNE   GEN0320               NO
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE AREA
         MVC   SRCMNEM,USNGOPCD      SET OPCODE
         MVC   SRCOPER(8),COMMCSNM   SET OPERAND (CSECT'S NAME)
         LA    R1,SRCOPER            CURRENT OUTPUT ADDRESS
GEN0290  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    GEN0300               YES
         LA    R1,1(R1)              NEXT
         B     GEN0290               LOOP
GEN0300  DS    0H
         ST    R1,GENADDR            SET ADDRESS
         ICM   R0,15,BASEDISP        DISPLACEMENT REFERRED TO
         BZ    GEN0310               DIRECTLY AT CSECT
         MVI   0(R1),C'+'            INSERT PLUS
         LA    R1,1(R1)              NEXT
         ST    R1,GENADDR            SET ADDRESS
         STCM  R0,15,WORKNBR         SET VALUE
         BAL   R15,GENNBR00          GENERATE NUMERIC VALUE
GEN0310  DS    0H
         BAL   R15,GENCOMMA          INSERT COMMA
         MVC   WORKREG,BASEREG       COPY REGISTER
         BAL   R15,GENREG00          GENERATE REGISTER
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT USING STATEMENT
         BAL   R9,PUNCH000           PUNCH USING STATEMENT
GEN0320  DS    0H
         ICM   R2,15,BASENEXT        NEXT BASE BLOCK
         BNZ   GEN0280               LOOP
* ------------------------------------------------------------------- *
*        Generate an instruction                                      *
* ------------------------------------------------------------------- *
GEN0330  DS    0H
         SR    R8,R8                 CLEAR REGISTER
         ICM   R8,1,0(R4)            POSSIBLE OPCODE
         BZ    ERR0020               NOT A VALID OPCODE
         SLL   R8,2                  MULTIPLY BY 4
         A     R8,AOP                PLUS OPCODE TABLE BASE ADDRESS
         ICM   R8,15,0(R8)           OPCODE TABLE ENTRY ADDRESS
         USING OPDSECT,R8            DEFINE BASE
         BZ    ERR0020               NOT A VALID OPCODE
         TM    COMMFLAG,$NOB2        BYPASS B2 INSTRUCTIONS?
         BO    GEN0340               YES
         CLI   0(R4),X'B2'           B2 INSTRUCTION?
         BNE   GEN0340               NO
         LR    R0,R4                 COPY OPCODE'S ADDRESS
         LR    R1,R8                 COPY B2'S ADDRESS IN DISASMOP
         L     R15,AB2               B2 INTERPRETER ENTRY POINT
         BALR  R14,R15               INTERPRET B2 OPCODE
GEN0340  DS    0H
         ITRACE ID=GENINSTR,         GENERATING A VALID INSTRUCTION    +
               DATA1=(R4),           .. INSTRUCTION                    +
               DATA2=OPMNEM          .. OPCODE TABLE DATA
         BAL   R9,GEN0620            GENERATE DISP AND HEX
         SR    R15,R15               CLEAR REGISTER
         IC    R15,OPFORM            OPCODE FORM
         SLL   R15,2                 MULTIPLY BY 4
         LA    R15,FMTTABLE(R15)     GENERATING ROUTINE'S ADDRESS
         L     R15,0(R15)            LOAD THE ADDRESS
         BR    R15                   GENERATE THE INSTRUCTION
GEN0350  DS    0H
         TM    OPFLAGS,$OPCCA+$OPCCL+$OPCCC   CONDITION CODE CHANGED?
         BZ    GEN0360               NO
         MVC   SAVEFLAG,OPFLAGS      SAVE CONDITION CODE FLAGS
GEN0360  DS    0H
         TM    OPFLAGS,$OPNCMNT      COMMENT PRESENT?
         BO    GEN0370               NO
         MVC   SRCCMNT,OPCMNT        SET COMMENT
GEN0370  DS    0H
         ITRACE ID=PRTSRC
         MVC   PRTDATA(SRCL),SRC     SET PRINT DATA
         BAL   R9,PRT0000            PRINT THE GENERATED INSTRUCTION
         ITRACE ID=PUNCHSRC
         BAL   R9,PUNCH000           PUNCH SOURCE STATEMENT
         CLC   DISPI,DISPR           LABEL REFERENCE USED?
         BNE   GEN0390               NO
         ICM   R6,15,REFNEXT         NEXT REFERENCE BLOCK
         BZ    GEN0380               NO MORE REFERENCE BLOCKS
         MVC   DISPR,REFDISPI        SET DISPLACEMENT
         ITRACE ID=NEXTREF,          WE HAVE A NEW REFERENCE BLOCK     +
               DATA1=DISPR           .. INSTRUCTION'S DISPLACEMENT
         B     GEN0390
GEN0380  DS    0H
         ITRACE ID=REFEOF            NO MORE REFERENCE BLOCKS
         MVI   DISPR,X'FF'           SET EOF FLAG
GEN0390  DS    0H
         AH    R3,OPLENGTH           UPDATE DISPLACEMENT
         AH    R4,OPLENGTH           UPDATE INSTRUCTION ADDRESS
         STCM  R3,15,DISPI           UPDATE CONTROL DATA
         ITRACE ID=NEWDISPI,         NEW DISPLACEMENT                  +
               RDATA1=R3,            .. DISPLACEMENT                   +
               RDATA2=R4             .. ASSOCIATED STORAGE ADDRESS
         B     GEN0110               LOOP
* ------------------------------------------------------------------- *
*        Generate constants                                           *
* ------------------------------------------------------------------- *
GEN0400  DS    0H
         ITRACE ID=GENDATA,          IN A DATA AREA                    +
               DATA1=DATABEGN,       .. BEGINNING POINT                +
               DATA2=DATAEND         .. ENDING POINT
         L     R8,AOP                OPCODE TABLE ADDRESS
         L     R8,0(R8)              DC'S DUMMY ENTRY ADDRESS
         CLI   DATATYPE,$DATAACN     AD-CON?
         BE    GEN0470               YES
         CLI   DATATYPE,$DATAVCN     V-CON?
         BE    GEN0520               YES
         CLI   DATATYPE,$DATACXD     CXD (PSEUDO AREA SIZE)?
         BE    GEN0550               YES
         CLI   DATATYPE,$DATAQ       Q (PSEUDO AREA DISPLACEMENT)?
         BE    GEN0560               YES
         MVC   WORKNBR,DATAEND       ASSUME FULL DATA SIZE
         SR    R9,R9                 CLEAR REGISTER
         CLI   DISPL,X'FF'           END OF LABELS REACHED?
         BE    GEN0410               YES
         ICM   R9,15,DISPL           NEXT LABEL'S DISPLACEMENT
         BCTR  R9,0                  MINUS 1
         CLM   R9,15,WORKNBR         LABEL WITHIN DATA?
         BH    GEN0410               NO
         STCM  R9,15,WORKNBR         LIMIT TO 1 BYTE BEFORE LABEL
GEN0410  DS    0H
         ICM   R9,15,WORKNBR         END OF DATA DISPLACEMENT
         ICM   R0,15,DISPD           STARTING DISPLACEMENT
         SR    R9,R0                 NUMBER OF BYTES OF DATA MINUS 1
         LA    R9,1(R9)              FULL NUMBER OF BYTES
         CH    R9,COMMH8             MORE THAN 8 BYTES?
         BNH   GEN0420               NO
         LH    R9,COMMH8             LIMIT TO 8 BYTES
GEN0420  DS    0H
         ITRACE ID=DCLEN1,           LENGTH TO LABEL OR END OF AREA    +
               RDATA1=R9
         LTR   R9,R9                 LENGTH ZERO?
         BZ    ERR0030               YES..
         TRT   0(1,R4),COMMPRT       PRINTABLE CHARACTER?
         BZ    GEN0450               YES
         ITRACE ID=HEXDC
         LR    R15,R9                COPY LENGTH
         BCTR  R15,0                 MINUS 1
         EX    R15,NPRTTRT           SCAN FOR ALL NON-PRINTABLE
         BZ    GEN0430               ALL NON-PRINTABLE
         LR    R9,R1                 STOPPING POINT
         SR    R9,R4                 NUMBER OF BYTES SCANNED
GEN0430  DS    0H
         ITRACE ID=DCLEN2,           LENGTH OF HEX DATA                +
               RDATA1=R9
         STH   R9,OPLENGTH           SET LENGTH IN DC ENTRY
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCOPER(2),HEXDC      SET OPERAND TO X'
         LA    R1,SRCOPER+2          STARTING DATA POINT
         LH    R15,OPLENGTH          DATA LENGTH
GEN0440  DS    0H
         UNPK  0(3,R1),0(2,R4)       UNPACK DATA
         MVZ   0(2,R1),COMM0F0F      TURN OFF ZONES
         TR    0(2,R1),COMMHXCH      TRANSLATE TO PRINTABLE
         LA    R1,2(R1)              NEXT IN OUTPUT
         LA    R3,1(R3)              UPDATE DISPLACEMENT
         LA    R4,1(R4)              NEXT OBJECT MODULE BYTE
         BCT   R15,GEN0440           LOOP
         MVI   0(R1),C''''           INSERT ENDING APOSTROPHE
         B     GEN0600               PRINT/PUNCH
GEN0450  DS    0H
         ITRACE ID=CHARDC
         LR    R15,R9                COPY LENGTH
         BCTR  R15,0                 MINUS 1
         EX    R15,PRTTRT            SCAN FOR ALL PRINTABLE
         BZ    GEN0460               ALL PRINTABLE
         LR    R9,R1                 COPY STOPPING POINT
         SR    R9,R4                 NUMBER OF BYTES SCANNED
         LR    R15,R9                COPY LENGTH
         BCTR  R15,0                 MINUS 1
GEN0460  DS    0H
         ITRACE ID=DCLEN3,           TO END OF DATA OR HEX DATA        +
               RDATA1=R9,                                              +
               RDATA2=R15
         STH   R9,OPLENGTH           SET LENGTH
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCOPER(2),CHARDC     SET OPERAND TO C'
         EX    R15,CHDCMVC           MOVE CHARACTER DATA
         LA    R1,SRCOPER+3(R15)     ENDING POINT
         MVI   0(R1),C''''           INSERT ENDING APOSTROPHE
         B     GEN0590
GEN0470  DS    0H
         ITRACE ID=GENADCON          GENERATING AN ADCON
         MVC   OPLENGTH,DATALEN+2    SET LENGTH
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCOPER(2),=C'AL'     SET ADCON-LENGTH
         MVC   SRCOPER+2(1),DATALEN+3
         OI    SRCOPER+2,X'F0'       MAKE IT PRINTABLE
         MVI   SRCOPER+3,C'('        OPEN PARENTHESIS
         ICM   R1,15,DATALBA         LABEL BLOCK'S ADDRESS
         MVC   SRCOPER+4(8),LABLNAME-LABLDSCT(R1)
         LA    R1,SRCOPER+4
GEN0480  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    GEN0490               YES
         LA    R1,1(R1)              NEXT
         B     GEN0480               LOOP
GEN0490  DS    0H
         ST    R1,GENADDR            SET CURRENT ADDRESS
         SR    R0,R0                 CLEAR REGISTER
         ICM   R0,15,DATALBD         DISPLACEMENT FROM LABEL
         BZ    GEN0500               NO DISPLACEMENT
         MVI   0(R1),C'+'            INSERT PLUS SIGN
         LA    R1,1(R1)              NEXT
         ST    R1,GENADDR            SAVE ADDRESS
         STCM  R0,15,WORKNBR         SET DISPLACEMENT
         BAL   R15,GENNBR00          GENERATE DISPLACEMENT
GEN0500  DS    0H
         L     R1,GENADDR            CURRENT ADDRESS
         TM    DATAFLAG,$DATA31      31-BIT MODE BIT ON?
         BNO   GEN0510               NO
         MVC   0(AM31INDL,R1),AM31IND   GEN 31-BIT MODE
         B     GEN0590               DONE
GEN0510  DS    0H
         BAL   R15,GENPRN2           GENERATE CLOSING PARENTHESIS
         B     GEN0590               DONE
GEN0520  DS    0H
         ITRACE ID=GENVCON
         MVC   OPLENGTH,DATALEN+2    SET LENGTH
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCOPER(2),=C'VL'     SET VCON-LENGTH
         MVC   SRCOPER+2(1),DATALEN+3
         OI    SRCOPER+2,X'F0'       MAKE IT PRINTABLE
         MVI   SRCOPER+3,C'('        OPEN PARENTHESIS
         MVC   SRCOPER+4(8),DATANAME COPY EXTERNAL SYMBOL NAME
         LA    R1,SRCOPER+4
GEN0530  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    GEN0540               YES
         LA    R1,1(R1)              NEXT
         B     GEN0530               LOOP
GEN0540  DS    0H
         MVI   0(R1),C')'            CLOSING PARENTHESIS
         B     GEN0590
GEN0550  DS    0H
         MVC   OPLENGTH,DATALEN+2    SET LENGTH
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCMNEM,CXDOPCD       CHANGE OPCODE TO CXD
         B     GEN0590
GEN0560  DS    0H
         ITRACE ID=GENQ
         MVC   OPLENGTH,DATALEN+2    SET LENGTH
         BAL   R9,GEN0620            GENERATE OBJECT AND MNEMONIC
         MVC   SRCOPER(2),=C'QL'     SET Q-LENGTH
         MVC   SRCOPER+2(1),DATALEN+3
         OI    SRCOPER+2,X'F0'       MAKE IT PRINTABLE
         MVI   SRCOPER+3,C'('        OPEN PARENTHESIS
         MVC   SRCOPER+4(8),DATANAME COPY EXTERNAL SYMBOL NAME
         LA    R1,SRCOPER+4
GEN0570  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    GEN0580               YES
         LA    R1,1(R1)              NEXT
         B     GEN0570               LOOP
GEN0580  DS    0H
         MVI   0(R1),C')'            CLOSING PARENTHESIS
GEN0590  DS    0H
         AH    R3,OPLENGTH           UPDATE DISPLACEMENT
         AH    R4,OPLENGTH           NEXT OBJECT MODULE BYTE
GEN0600  DS    0H
         MVC   PRTDATA(SRCL),SRC     SET DATA FOR PRINTING
         BAL   R9,PRT0000            PRINT
         BAL   R9,PUNCH000           PUNCH
         STCM  R3,15,DISPI           SET NEW DISPLACEMENT
         STCM  R3,15,DISPD           SET DATA DISPLACEMENT
         ITRACE ID=DCDONE,           DC PROCESSING COMPLETE            +
               DATA1=DISPD,          .. DATA DISPLACEMENT NOW          +
               DATA2=DATAEND         .. END OF DATA AREA
         CLC   DISPD,DATAEND         BEYOND END OF DATA AREA?
         BNH   GEN0110               NO
         ITRACE ID=NEXTDATA,                                           +
               RDATA1=R7,            .. CURRENT DATA BLOCK ADDRESS     +
               DATA2=DATANEXT        .. NEXT DATA BLOCK'S ADDRESS
         ICM   R7,15,DATANEXT        NEXT DATA BLOCK
         BNZ   GEN0610               SET NEXT DATA DISP
         MVI   DISPD,X'FF'           SET END OF FILE
         B     GEN0110
GEN0610  DS    0H
         MVC   DISPD,DATABEGN        SET BEGINNING OF NEXT DATA AREA
         B     GEN0110
* ------------------------------------------------------------------- *
*        Generate displacement, mnemonic, and instruction in hex      *
* ------------------------------------------------------------------- *
GEN0620  DS    0H
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE STATEMENT
         UNPK  SRCDISP(9),DISPI(5)   UNPACK DISPLACEMENT
         MVZ   SRCDISP,COMM0F0F      TURN OFF ZONES
         TR    SRCDISP,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   SRCDISP+8,C' '        RESTORE BLANK
         MVC   SRCMNEM,OPMNEM        SET MNEMONIC
         LH    R1,OPLENGTH           INSERT INSTRUCTION LENGTH
         BCTR  R1,0                  ADJUST FOR EXECUTE
         EX    R1,OBJMVC1            COPY FOR UNPACKING
         UNPK  OBJOUT1(5),OBJIN(3)   UNPACK OBJECT CODE
         MVZ   OBJOUT1,COMM0F0F      TURN OFF ZONES
         TR    OBJOUT1,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   OBJOUT1+4,C' '        RESTORE THE BLANK
         UNPK  OBJOUT2(5),OBJIN+2(3) UNPACK OBJECT CODE
         MVZ   OBJOUT2,COMM0F0F      TURN OFF ZONES
         TR    OBJOUT2,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   OBJOUT2+4,C' '        RESTORE THE BLANK
         UNPK  OBJOUT3(5),OBJIN+4(3) UNPACK OBJECT CODE
         MVZ   OBJOUT3,COMM0F0F      TURN OFF ZONES
         TR    OBJOUT3,COMMHXCH      TRANSLATE TO PRINTABLE
         MVI   OBJOUT3+4,C' '        RESTORE THE BLANK
         SLL   R1,1                  MULTIPLY LENGTH BY 2
         LA    R1,OBJLEN(R1)         TRANSLATE LENGTH TO OUTPUT LENGTH
         LH    R1,0(R1)              OUTPUT LENGTH
         EX    R1,OBJMVC2            COPY OUTPUT TO SOURCE AREA
         LA    R1,SRCOPER            1ST OPERAND BYTE
         ST    R1,GENADDR            SAVE CURRENT ADDRESS
         BR    R9
OBJMVC1  MVC   OBJIN(0),0(R4)        COPY DATA TO BE DISPLAYED
OBJMVC2  MVC   SRCOBJ1(0),OBJOUT1    COPY DATA TO BE DISPLAYED
* ------------------------------------------------------------------- *
*        Copy assembler input statements                              *
* ------------------------------------------------------------------- *
GEN0700  DS    0H
         TM    COMMFLAG,$ASMIN       ANY ASSEMBLER INPUT?
         BNO   GEN0730               NO
         ITRACE ID=ASMIN             COPYING ASSEMBLER INPUT
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         OPEN  (SYSIN,INPUT)         OPEN SYSIN
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
GEN0710  DS    0H
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         GET   SYSIN                 READ A SYSIN STATEMENT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         MVC   SRCLABL(80),0(R1)     COPY TO SOURCE STATEMENT AREA
         BAL   R9,PUNCH000           COPY TO PUNCH FILE
         B     GEN0710               LOOP
GEN0720  DS    0H
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         ITRACE ID=ASMINEND          END OF SYSIN REACHED
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         CLOSE SYSIN                 CLOSE SYSIN
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         B     GEN0800
GEN0730  DS    0H
         ITRACE ID=NOASMIN           NO ASSEMBLER INPUT TO COPY
* ------------------------------------------------------------------- *
*        Generate REGEQU statement                                    *
* ------------------------------------------------------------------- *
GEN0800  DS    0H
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE STATEMENT
         MVC   SRCMNEM(L'REGEQU),REGEQU
         BAL   R9,PUNCH000           PUNCH STATEMENT
* ------------------------------------------------------------------- *
*        Generate END statement                                       *
* ------------------------------------------------------------------- *
         DS    0H
         MVC   SRC(SRCL),SRC-1       CLEAR SOURCE STATEMENT
         MVC   SRCMNEM,ENDOPCD       SET OPCODE 'END'
         MVC   SRCOPER(L'COMMCSNM),COMMCSNM
         BAL   R9,PUNCH000           PUNCH END STATEMENT
         B     EXIT0000              ALL DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RR format 1 instructions                     *
*                                                                     *
*---------------------------------------------------------------------*
GENRR100 DS    0H
         ITRACE ID=GENRR1            GENERATE SVC FORMAT INSTRUCTION
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT REGISTERS
         SRL   R14,4                 SHIFT R1 TO LOW BITS
         STC   R14,WORKREG           SET REGISTER
         BAL   R15,GENREG00          GENERATE R1
         BAL   R15,GENCOMMA          INSERT COMMA
         MVC   WORKREG,1(R4)         COPY R1 AND R2
         NI    WORKREG,X'0F'         LEAVE ONLY R2
         BAL   R15,GENREG00          GENERATE R2
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RR format 2 instructions                      *
*                                                                     *
*---------------------------------------------------------------------*
GENRR200 DS    0H
         ITRACE ID=GENRR2            GENERATE SVC FORMAT INSTRUCTION
         XC    WORKNBR,WORKNBR       CLEAR NUMERIC
         MVC   WORKNBR+3(1),1(R4)    COPY SVC NUMBER
         BAL   R15,GENNBR00          GENERATE SVC NUMBER
         TM    OPFLAGS,$OPSVC        IS THIS AN SVC?
         BNO   GEN0350               NOPE
         L     R1,ASVCDESC           SVC DESCRIPTIONS
         USING SVCDSECT,R1           DEFINE BASE
GENRR210 DS    0H
         CLI   SVCLEN,X'FF'          END OF TABLE?
         BE    GEN0350               YES.. NO SVC COMMENT
         CLC   SVCNBR,1(R4)          SVC NUMBER LOCATED?
         BE    GENRR220              YES
         AH    R1,SVCLEN             NEXT SVC
         B     GENRR210              LOOP
GENRR220 DS    0H
         SR    R15,R15               CLEAR REGISTER
         ICM   R15,3,SVCCMNTL        COMMENT'S LENGTH
         BCTR  R15,0                 MINUS 1
         MVC   OPCMNT,COMMBLKS       INITIALIZE COMMENT
         EX    R15,SVCCMVC           MOVE SVC COMMENT
         B     GEN0350               COMPLETE
SVCCMVC  MVC   OPCMNT(0),SVCCMNT     SET COMMENT
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RR branch instructions                        *
*                                                                     *
*---------------------------------------------------------------------*
GENRR300 DS    0H
         ITRACE ID=GENRRBR           GENERATE RR MASK TYPE INSTRUCTION
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT MASK
         SRL   R14,4                 SHIFT TO LOW BITS
         XC    WORKNBR,WORKNBR       ZERO BYTE 1-3
         STC   R14,WORKNBR+3         SET BYTE 4
         TM    SAVEFLAG,$OPCCA       ARITHMETIC MNEMONICS?
         BO    GENRR310              YES
         TM    SAVEFLAG,$OPCCC       COMPARE MNEMONICS?
         BO    GENRR320              YES
         LA    R1,GENRRCCL           LOGICAL EXTENDED MNEMONICS
         B     GENRR330
GENRR310 DS    0H
         LA    R1,GENRRCCA           ARITHMETIC EXTENDED MNEMONICS
         B     GENRR330
GENRR320 DS    0H
         LA    R1,GENRRCCC           COMPARE EXTENDED MNEMONICS
GENRR330 DS    0H
         CLI   0(R1),X'FF'           EXTENDED MNEMONIC NOT FOUND?
         BE    GENRR340              NO
         CLM   R14,1,0(R1)           MASK FOUND?
         BE    GENRR350              YES
         LA    R1,7(R1)              NEXT MASK/EXTENDED MNEMONIC
         B     GENRR330              LOOP
GENRR340 DS    0H
         BAL   R15,GENNBR00          GENERATE MASK VALUE
         BAL   R15,GENCOMMA          INSERT COMMA
         B     GENRR360              GENERATE OPERAND
GENRR350 DS    0H
         MVC   SRCMNEM,1(R1)         SET EXTENDED MNEMONIC
GENRR360 DS    0H
         MVC   WORKREG,1(R4)         COPY REGISTER
         NI    WORKREG,X'0F'         LEAVE ONLY REGISTER VALUE
         BAL   R15,GENREG00          GENERATE REGISTER
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RX format instructions                        *
*                                                                     *
*---------------------------------------------------------------------*
GENRX00  DS    0H
         ITRACE ID=GENRX             GENERATE RX FORMAT INSTRUCTION
         TM    OPFLAGS,$OPEXT        EXTENDED FORMATS?
         BO    GENB000               YES
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1 AND INDEX REGISTER
         SRL   R14,4                 SHIFT R1 INTO LOW BITS
         STC   R14,WORKREG           SET REGISTER
         BAL   R15,GENREG00          GENERATE REGISTER
         BAL   R15,GENCOMMA          INSERT COMMA
         MVC   WORKX,1(R4)           COPY INDEX REGISTER
         NI    WORKX,X'0F'           LEAVE ONLY INDEX REGISTER VALUE
         MVI   WORKOPER,$OPER1+$OPERNDX
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate S format instructions                         *
*                                                                     *
*---------------------------------------------------------------------*
GENS00   DS    0H
         ITRACE ID=GENS              GENERATE S FORMAT INSTRUCTION
         MVI   WORKOPER,$OPER1       SET OPERAND 1
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate BRANCH instructions                           *
*                                                                     *
*---------------------------------------------------------------------*
GENB000  DS    0H
         ITRACE ID=GENBRNCH          GENERATE BRANCH INSTRUCTIONS
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT CONDITION CODE AND REGISTER
         SRL   R14,4                 SHIFT MASK INTO LOW BITS
         XC    WORKNBR,WORKNBR       ZERO BYTE 1
         STC   R14,WORKNBR+3         SET BYTE 4
         TM    SAVEFLAG,$OPCCA       ARITHMETIC MNEMONICS?
         BO    GENB010               YES
         TM    SAVEFLAG,$OPCCC       COMPARE MNEMONICS?
         BO    GENB020               YES
         LA    R1,GENBCCL            RR FORM3 EXTENDED MNEMONIC TABLE
         B     GENB030
GENB010  DS    0H
         LA    R1,GENBCCA            ARITHMETIC MNEMONICS
         B     GENB030
GENB020  DS    0H
         LA    R1,GENBCCC            COMPARE MNEMONICS
GENB030  DS    0H
         CLI   0(R1),X'FF'           EXTENDED MNEMONIC NOT FOUND?
         BE    GENB040               NO
         CLM   R14,1,0(R1)           MASK FOUND?
         BE    GENB050               YES
         LA    R1,7(R1)              NEXT MASK/EXTENDED MNEMONIC
         B     GENB030               LOOP
GENB040  DS    0H
         MVC   SRCMNEM,BCOPCD        SET OPCODE TO 'BC'
         BAL   R15,GENNBR00          GENERATE MASK
         BAL   R15,GENCOMMA          GENRATE COMMA
         B     GENB060
GENB050  DS    0H
         MVC   SRCMNEM,1(R1)         SET EXTENDED MNEMONIC
GENB060  DS    0H
         MVI   WORKOPER,$OPER1+$OPERNDX
         MVC   WORKX,1(R4)           COPY INDEX REGISTER
         NI    WORKX,X'0F'           LEAVE ONLY INDEX REGISTER VALUE
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate SI format instructions                        *
*                                                                     *
*---------------------------------------------------------------------*
GENSI00  DS    0H
         ITRACE ID=GENSI             GENERATE SI FORMAT INSTRUCTION
         MVI   WORKOPER,$OPER1       OPERAND 1
         BAL   R9,GENOP000           GENERATE OPERAND
         L     R15,GENADDR           CURRENT OUTPUT ADDRESS
         CLC   DISPI,DISPR           LABEL REFERENCE?
         BNE   GENSI30               NO
         OC    REFDISP1,REFDISP1     DISPLACEMENT ZERO?
         BNZ   GENSI30               NO
         ICM   R2,15,REFOPER1        LABEL REFERENCE?
         BZ    GENSI30               LABEL NOT REFERENCED
         DROP  R5
         USING LABLDSCT,R2           DEFINE BASE
         ICM   R1,15,LABLEQU         FIRST EQUATE
         USING EQUDATA,R1            DEFINE BASE
         BZ    GENSI30
GENSI10  DS    0H
         CLC   EQUVALUE,1(R4)        EQUATE VALUE MATCH?
         BE    GENSI20               YES
         ICM   R1,15,EQUNEXT         NEXT EQUATE BLOCK
         BNZ   GENSI10               LOOP
         B     GENSI30
GENSI20  DS    0H
         MVI   0(R15),C','           INSERT COMMA
         MVC   1(8,R15),EQULABEL     INSERT LABEL
         B     GEN0350               COMPLETE
GENSI30  DS    0H
         MVC   0(3,R15),GENSIDLM     INSERT COMMA AND X'
         MVC   3(2,R15),SRCOBJ1+2    COPY THE VALUE
         MVI   5(R15),C''''          INSERT CLOSING QUOTE
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RS format instructions (SHIFTS)               *
*                                                                     *
*---------------------------------------------------------------------*
GENRS100 DS    0H
         ITRACE ID=GENRS1            GENERATE SHIFT TYPE INSTRUCTIONS
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1
         SRL   R14,4                 SHIFT R1 INTO LOW BITS
         STC   R14,WORKREG           SET REGISTER NUMBER
         BAL   R15,GENREG00          GENERATE REGISTER
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER1       OPERAND 1
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RS format instructions (BXH, BXLE, ..)        *
*                                                                     *
*---------------------------------------------------------------------*
GENRS200 DS    0H
         ITRACE ID=GENRS2            GENERATE BXH, BXLE, ..
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1
         SRL   R14,4                 SHIFT R1 INTO LOW BITS
         STC   R14,WORKREG           SET REGISTER NUMBER
         BAL   R15,GENREG00          GENERATE REGISTER
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKREG,1(R4)         SET R3
         NI    WORKREG,X'0F'         LEAVE ONLY R3
         BAL   R15,GENREG00          GENERATE REGISTER
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER1       OPERAND 1
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               DONE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate RS format instructions (CLM, ICM, ..)         *
*                                                                     *
*---------------------------------------------------------------------*
GENRS300 DS    0H
         ITRACE ID=GENRS3            GENERATE CLM, ICM..
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1
         SRL   R14,4                 SHIFT R1 INTO LOW BITS
         STC   R14,WORKREG           SET REGISTER NUMBER
         BAL   R15,GENREG00          GENERATE REGISTER
         BAL   R15,GENCOMMA          GENERATE COMMA
         XC    WORKNBR,WORKNBR       SET WORK NUMERIC
         MVC   WORKNBR+3(1),1(R4)    COPY MASK
         NI    WORKNBR+3,X'0F'       LEAVE ONLY MASK
         BAL   R15,GENNBR00          GENERATE MASK
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER1       SET FOR OPERAND 1
         BAL   R9,GENOP000           GENERATE LABEL
         B     GEN0350               DONE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate SS character instructions                     *
*                                                                     *
*---------------------------------------------------------------------*
GENSS100 DS    0H
         ITRACE ID=GENSS1            GENERATE SS CHARACTER INSTRUCTIONS
         MVC   WORKX,1(R4)           SET LENGTH
         MVI   WORKOPER,$OPER1+$OPERL
         BAL   R9,GENOP000           GENERATE LABEL 1
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER2       OPERAND 2, NO LENGTH
         BAL   R9,GENOP000           GENERATE LABEL 2
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate SS packed decimal instructions                *
*                                                                     *
*---------------------------------------------------------------------*
GENSS200 DS    0H
         ITRACE ID=GENSS2            GENERATE SS CHARACTER INSTRUCTIONS
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,1(R4)              INSERT LENGTHS
         SRL   R1,4                  SHIFT L1 TO LOW BITS
         STC   R1,WORKX              SET LENGTH 1
         MVI   WORKOPER,$OPER1+$OPERL
         BAL   R9,GENOP000           GENERATE OPERAND 1
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKX,1(R4)           COPY LENGTHS
         NI    WORKX,X'0F'           LEAVE ONLY L2
         MVI   WORKOPER,$OPER2+$OPERL
         BAL   R9,GENOP000           GENERATE OPERAND 2
         B     GEN0350               COMPLETE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              Generate SS (MVCP, MVCS, MVCK)                         *
*                                                                     *
*---------------------------------------------------------------------*
GENSS300 DS    0H
         ITRACE ID=GENSS3            GENERATE SS CHARACTER INSTRUCTIONS
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1 AND R3
         SRL   R14,4                 SHIFT R1 TO LOW BITS
         STC   R14,WORKX             SET R1 AS IF AN INDEX REGISTER
         MVI   WORKOPER,$OPER1+$OPERNDX
         BAL   R9,GENOP000           GENERATE OPERAND
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER2       OPERAND 2
         BAL   R9,GENOP000           GENERATE OPERAND
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKREG,1(R4)         COPY R1 AND R3
         NI    WORKREG,X'0F'         LEAVE ONLY R3
         BAL   R15,GENREG00          GENERATE R3
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate SS (SRP)                                      *
*                                                                     *
*---------------------------------------------------------------------*
GENSS400 DS    0H
         ITRACE ID=GENSS4            GENERATE SS CHARACTER INSTRUCTIONS
         SR    R14,R14               CLEAR REGISTER
         IC    R14,1(R4)             INSERT R1 AND R3
         SRL   R14,4                 SHIFT R1 TO LOW BITS
         STC   R14,WORKX             SET R1 AS IF AN INDEX REGISTER
         MVI   WORKOPER,$OPER1+$OPERL
         BAL   R9,GENOP000           GENERATE OPERAND
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVI   WORKOPER,$OPER2       OPERAND 2
         BAL   R9,GENOP000           GENERATE OPERAND
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKREG,1(R4)         COPY R1 AND R3
         NI    WORKREG,X'0F'         LEAVE ONLY R3
         BAL   R15,GENREG00          GENERATE R3
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate B2 with no operand format                     *
*                                                                     *
*---------------------------------------------------------------------*
GENB2000 DS    0H
         ITRACE ID=GENB2             GENERATE B2
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate B2 "S" format                                 *
*                                                                     *
*---------------------------------------------------------------------*
GENB2S00 DS    0H
         ITRACE ID=GENB2S            GENERATE B2 "S" FORMAT
         MVI   WORKOPER,$OPER1       SET OPERAND 1
         BAL   R9,GENOP000           GENERATE OPERAND
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate B2 "R" format                                 *
*                                                                     *
*---------------------------------------------------------------------*
GENB2R00 DS    0H
         ITRACE ID=GENB2R            GENERATE B2 "R" FORMAT
         SR    R14,R14               CLEAR REGISTER
         IC    R14,3(R4)             INSERT REGISTER
         SRL   R14,4                 SHIFT TO LOW BITS
         STC   R14,WORKREG           SET REGISTER
         BAL   R15,GENREG00          GENERATE REGISTER
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate B2 "RR" format                                *
*                                                                     *
*---------------------------------------------------------------------*
GENB2RR0 DS    0H
         ITRACE ID=GENB2RR           GENERATE B2 "RR" FORMAT
         SR    R14,R14               CLEAR REGISTER
         IC    R14,3(R4)             INSERT REGISTER
         SRL   R14,4                 SHIFT TO LOW BITS
         STC   R14,WORKREG           SET REGISTER
         BAL   R15,GENREG00          GENERATE REGISTER R1
         BAL   R15,GENCOMMA          GENERATE COMMA
         MVC   WORKREG,3(R4)         COPY R1 AND R2
         NI    WORKREG,X'0F'         CLEAR REGISTER R1
         BAL   R15,GENREG00          GENERATE REGISTER R2
         B     GEN0350               COMPLETE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate COMMAs                                        *
*                                                                     *
*   R15 is the return address                                         *
*                                                                     *
*---------------------------------------------------------------------*
GENCOMMA DS    0H
         L     R14,GENADDR           CURRENT ADDRESS
         MVI   0(R14),C','           INSERT COMMA
         LA    R14,1(R14)            NEXT
         ST    R14,GENADDR           SAVE ADDRESS
         BR    R15                   DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate OPEN parenthesis                              *
*                                                                     *
*   R15 is the return address                                         *
*                                                                     *
*---------------------------------------------------------------------*
GENPRN1  DS    0H
         L     R14,GENADDR           CURRENT ADDRESS
         MVI   0(R14),C'('           INSERT OPEN PARENTHESIS
         LA    R14,1(R14)            NEXT
         ST    R14,GENADDR           SAVE ADDRESS
         BR    R15                   DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate CLOSE parenthesis                             *
*                                                                     *
*   R15 is the return address                                         *
*                                                                     *
*---------------------------------------------------------------------*
GENPRN2  DS    0H
         L     R14,GENADDR           CURRENT ADDRESS
         MVI   0(R14),C')'           INSERT CLOSE PARENTHESIS
         LA    R14,1(R14)            NEXT
         ST    R14,GENADDR           SAVE ADDRESS
         BR    R15                   DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate REGISTERS                                     *
*                                                                     *
*   WORKREG  is the value of the register to Generate                 *
*                                                                     *
*   R15 is the return address                                         *
*                                                                     *
*---------------------------------------------------------------------*
GENREG00 DS    0H
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,WORKREG            INSERT REGISTER VALUE
         SLL   R1,1                  MULTIPLY BY 2
         LA    R1,COMMNBR(R1)
         L     R14,GENADDR           CURRENT ADDRESS IN OPER AREA
         MVI   0(R14),C'R'           SET REGISTER PREFIX
         MVC   1(2,R14),0(R1)        COPY REGISTER VALUE
         LA    R14,2(R14)            MINIMUM LENGTH
         CLI   0(R14),C' '           BLANK?
         BE    GENREG10              YES
         LA    R14,1(R14)            2 DIGIT REGISTER NUMBER
GENREG10 DS    0H
         ST    R14,GENADDR           SAVE CURRENT ADDRESS
         BR    R15                   DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate data operands with or without index           *
*                                                                     *
*   WORKOPER flags control Generated source                           *
*                                                                     *
*   R9 is the return address                                          *
*                                                                     *
*---------------------------------------------------------------------*
GENOP000 DS    0H
         CLC   DISPI,DISPR           LABEL REFERENCE?
         BNE   GENOP070              NO
         TM    WORKOPER,$OPER2       OPERAND 2?
         BO    GENOP010              YES
         MVC   WORKNBR,REFDISP1      COPY DISPLACEMENT
         ICM   R2,15,REFOPER1        LABEL REFERENCE?
         BZ    GENOP080              LABEL NOT REFERENCED
         B     GENOP020
GENOP010 DS    0H
         MVC   WORKNBR,REFDISP2      COPY DISPLACEMENT
         ICM   R2,15,REFOPER2        LABEL REFERENCE?
         BZ    GENOP090              LABEL NOT REFERENCED
GENOP020 DS    0H
         L     R14,GENADDR           CURRENT ADDRESS IN SRCOPER
         MVC   0(L'LABLNAME,R14),LABLNAME
GENOP030 DS    0H
         CLI   0(R14),C' '           BLANK?
         BE    GENOP040              YES
         LA    R14,1(R14)            NEXT
         B     GENOP030              LOOP
GENOP040 DS    0H
         ST    R14,GENADDR           SAVE ADDRESS
         OC    WORKNBR,WORKNBR       DISPLACEMENT ZERO?
         BZ    GENOP050              YES, DIRECT REFERENCE
         MVI   0(R14),C'+'           INSERT PLUS
         LA    R14,1(R14)            NEXT
         ST    R14,GENADDR           SAVE ADDRESS
         BAL   R15,GENNBR00          GENERATE DISPLACEMENT
GENOP050 DS    0H
         TM    WORKOPER,$OPERL       LENGTH WITH OPERAND?
         BO    GENOP060              YES
         TM    WORKOPER,$OPERNDX     INDEX WITH OPERAND?
         BNOR  R9                    NO, DONE
         CLI   WORKX,0               INDEX ZERO?
         BER   R9                    YES
         BAL   R15,GENPRN1           OPEN PARENTHESIS
         MVC   WORKREG,WORKX         SET REGISTER
         BAL   R15,GENREG00          GEN REGISTER
         BAL   R15,GENPRN2           CLOSE PARENTHESIS
         BR    R9                    DONE
GENOP060 DS    0H
         BAL   R15,GENPRN1           OPEN PARENTHESIS
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,WORKX              INSERT LENGTH
         LA    R1,1(R1)              PLUS 1
         STCM  R1,15,WORKNBR         SET LENGTH
         BAL   R15,GENNBR00          GEN LENGTH
         BAL   R15,GENPRN2           CLOSE PARENTHESIS
         BR    R9                    DONE
GENOP070 DS    0H
         TM    WORKOPER,$OPER2       OPERAND 2?
         BO    GENOP090              YES
GENOP080 DS    0H
         XC    WORKNBR,WORKNBR       CLEAR WORK NUMERIC
         MVC   WORKNBR+2(2),2(R4)    COPY DISPLACEMENT
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,2(R4)              INSERT BASE 1
         B     GENOP100
GENOP090 DS    0H
         XC    WORKNBR,WORKNBR       CLEAR WORK NUMERIC
         MVC   WORKNBR+2(2),4(R4)    COPY DISPLACEMENT
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,4(R4)              INSERT BASE 2
GENOP100 DS    0H
         NI    WORKNBR+2,X'0F'       LEAVE ONLY DISPLACEMENT
         SRL   R1,4                  SHIFT TO LOW BITS
         STC   R1,WORKBASE           SAVE BASE
         BAL   R15,GENNBR00          GENERATE DISPLACEMENT
         TM    WORKOPER,$OPERL       LENGTH WITH OPERAND?
         BO    GENOP120              YES
         TM    WORKOPER,$OPERNDX     INDEX WITH OPERAND?
         BNO   GENOP110              NO
         OC    WORKX(2),WORKX        BASE AND INDEX ZERO?
         BZR   R9                    YES, DONE
         B     GENOP120
GENOP110 DS    0H
         CLI   WORKBASE,0            BASE ZERO?
         BER   R9                    YES, DONE
GENOP120 DS    0H
         BAL   R15,GENPRN1           OPEN PARENTHESIS
         TM    WORKOPER,$OPERNDX     INDEX?
         BNO   GENOP140              NO
         CLI   WORKX,0               INDEX ZERO?
         BE    GENOP130              YES
         MVC   WORKREG,WORKX         SET REGISTER
         BAL   R15,GENREG00          GEN INDEX
         CLI   WORKBASE,0            BASE ZERO?
         BE    GENOP160              YES
GENOP130 DS    0H
         BAL   R15,GENCOMMA          GEN COMMA
GENOP140 DS    0H
         TM    WORKOPER,$OPERL       LENGTH PRESENT?
         BNO   GENOP150              NO
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,WORKX              INSERT LENGTH
         LA    R1,1(R1)              PLUS 1
         ST    R1,WORKNBR            SET LENGTH
         BAL   R15,GENNBR00          GENERATE LENGTH
         BAL   R15,GENCOMMA          GEN COMMA
GENOP150 DS    0H
         CLI   WORKBASE,0            BASE ZERO?
         BE    GENOP160              YES
         MVC   WORKREG,WORKBASE      SET REGISTER
         BAL   R15,GENREG00          GENERATE BASE REGISTER
GENOP160 DS    0H
         BAL   R15,GENPRN2           CLOSING PARENTHESIS
         BR    R9                    DONE
*---------------------------------------------------------------------*
*                                                                     *
*              Generate lengths/displacements                         *
*                                                                     *
*   WORKNBR will be set to the length or displacement value           *
*                                                                     *
*   R15 is the return address                                         *
*                                                                     *
*---------------------------------------------------------------------*
GENNBR00 DS    0H
         STCM  R15,15,GENNBRSV       SAVE R15
         L     R1,WORKNBR            DISPLACEMENT VALUE
         CVD   R1,COMMDWRD           CONVERT TO DECIMAL
         MVC   DISPWORK,DISPEDWD     INITIALIZE WITH EDIT WORD
         ED    DISPWORK,COMMDWRD+2   EDIT DISPLACEMENT
         L     R14,GENADDR           CURRENT OUTPUT ADDRESS
         LA    R15,DISPWORK+2        FIRST POSSIBLE DIGIT
         LA    R1,10                 MAXIMUM DIGITS
GENNBR10 DS    0H
         CLI   0(R15),C' '           BLANK?
         BE    GENNBR20              YES
         MVC   0(1,R14),0(R15)       COPY THE DIGIT
         LA    R14,1(R14)            NEXT
GENNBR20 DS    0H
         LA    R15,1(R15)            NEXT
         BCT   R1,GENNBR10           LOOP
         ST    R14,GENADDR           SAVE ADDRESS
         ICM   R15,15,GENNBRSV       RESTORE R15
         BR    R15                   DONE
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
ERR0010  DS     0H
         MVC   PRTDATA(EMSG01L),EMSG01
         OI    COMMFLAG,$ERROR
         BAL   R9,PRT0000            PRINT MESSAGE
         B     EXIT0000              AND EXIT
ERR0020  DS    0H
         MVC   PRTDATA(EMSG02L),EMSG02
         OI    COMMFLAG,$ERROR
         BAL   R9,PRT0000            PRINT MESSAGE
         DC    H'0'
         B     EXIT0000              AND EXIT
ERR0030  DS    0H
         MVC   PRTDATA(EMSG03L),EMSG03
         OI    COMMFLAG,$ERROR
         BAL   R9,PRT0000            PRINT MESSAGE
         B     EXIT0000              AND EXIT
ERR0040  DS    0H
         MVC   PRTDATA(EMSG04L),EMSG04
         OI    COMMFLAG,$ERROR
         BAL   R9,PRT0000            PRINT MESSAGE
         ABEND ABEND005,DUMP,,USER   ABEND
PUNCH000 DS    0H
         TM    COMMDD,$PUNCHDD       IS DISPUNCH DD PRESENT?
         BNOR  R9                    NO
         BAL   R14,AM24              SWITCH TO 24-BIT MODE
         PUT   DISPUNCH,SRCLABL      PUNCH SOURCE STATEMENT
         BAL   R14,AM31              SWITCH TO 31-BIT MODE
         BR    R9                    RETURN
PRT0000  DS    0H
         TM    PRTFLAG,$SUBH         HAS SUB-HEADING BEEN PRINTED?
         BO    PRT0010               YES
         OI    PRTFLAG,$SUBH         SET FLAG
         MVI   PRTCMD,$PRTHEAD       SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
PRT0010  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET PARAMETER BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               LINK TO PRINT MODULE
         BR    R9                    RETURN
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
AM24     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         BSM   R0,R14             RETURN IN 24-BIT MODE
AM31     DS    0H
         LA    R14,0(R14)         CLEAR HIGH BIT(S)
         O     R14,X80            SET 31-BIT MODE
         BSM   R0,R14             RETURN IN 31-BIT MODE
*---------------------------------------------------------------------*
*                                                                     *
*              EXECUTED INSTRUCTIONS                                  *
*                                                                     *
*---------------------------------------------------------------------*
NPRTTRT  TRT   0(0,R4),COMMNPRT      SCAN NON-PRINTABLE
PRTTRT   TRT   0(0,R4),COMMPRT       SCAN PRINTABLE
CHDCMVC  MVC   SRCOPER+2(0),0(R4)    COPY CHARACTER DATA
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
SAVE09   DC    18F'0'                REGISTER SAVE AREA
X80      DC    A(X'80000000')
ASVCDESC DC    A(SVCDESC)            SVC DESCRIPTIONS
FMTTABLE DS    0A
         DC    A(GENRR100)           RR FORMAT 1
         DC    A(GENRR200)           RR FORMAT 2 (SVC)
         DC    A(GENRR300)           RR FORMAT 3 (MASK TYPE)
         DC    A(GENRX00)            RX FORMAT
         DC    A(GENS00)             S  FORMAT
         DC    A(GENSI00)            SI FORMAT
         DC    A(GENRS100)           RS FORMAT 1
         DC    A(GENRS200)           RS FORMAT 2 (BXLE, BXH, ..)
         DC    A(GENRS300)           RS FORMAT 3 (MASK TYPE-TM, CLM..)
         DC    A(GENSS100)           SS FORMAT 1 (CHARACTER-CHARACTER)
         DC    A(GENSS200)           SS FORMAT 2 (PACKED DECIMAL)
         DC    A(GENSS300)           SS FORMAT 3 (MVCS, MVCP)
         DC    A(GENSS400)           SS FORMAT 4 (SRP)
         DC    A(GENB2000)           B2 WITH NO OPERAND
         DC    A(GENB2S00)           B2 S FORMAT
         DC    A(GENB2R00)           B2 R FORMAT
         DC    A(GENB2RR0)           B2 RR FORMAT
         SPACE 1
GENADDR  DC    A(0)                  CURRENT ADDRESS IN SRCOPER AREA
         SPACE 1
GENNBRSV DC    A(0)                  R15 SAVE AREA FOR 'GENNBR'
         SPACE 1
DISPCNTL DS    0C
         DC    CL8'INSTR'            EYECATCHER
DISPI    DC    XL4'000000'           INSTRUCTION DISPLACEMENT
         DC    CL8'LABEL'            EYECATCHER
DISPL    DC    XL4'000000'           LABEL DISPLACEMENT
         DC    CL8'DATA '            EYECATCHER
DISPD    DC    XL4'000000'           DATA DISPLACEMENT
         DC    CL8'REF  '            EYECATCHER
DISPR    DC    XL4'000000'           REFERENCE DISPLACEMENT
         SPACE 2
WORKNBR  DC    XL4'00'               LENGTHS/DISPLACEMENTS WORK AREA
WORKOPER DC    X'00'                 OPERAND TO GENERATE
$OPER1   EQU   X'80'                 .. OPERAND 1
$OPER2   EQU   X'40'                 .. OPERAND 2
$OPERNDX EQU   X'20'                 .. INDEXED OPERAND
$OPERL   EQU   X'10'                 .. OPERAND WITH LENGTH
WORKX    DC    X'00'                 INDEX REGISTER OR LENGTH
WORKBASE DC    X'00'                 BASE REGISTER
WORKREG  DC    X'00'
         SPACE 2
* ------------------------------------------------------------------- *
*           FLAG/SWITCH BYTES                                         *
* ------------------------------------------------------------------- *
PRTFLAG  DC    X'00'                 HEADING FLAGS
$SUBH    EQU   X'80'                 .. SUBHEADING PRINTED
SAVEFLAG DC    X'00'                 'FLAG' BYTE FOR EXTENDED MNEMONICS
         SPACE 1
AM31IND  DC    C'+X''80000000'')'
AM31INDL EQU   *-AM31IND
         SPACE 1
DISPWORK DC    CL12' '
DISPEDWD DC    X'402020202020202020202120'
CSCTOPCD DC    CL6'CSECT'
ENTROPCD DC    CL6'ENTRY'
DCOPCD   DC    CL6'DC'
CXDOPCD  DC    CL6'CXD'
DSOPCD   DC    CL6'DS'
DROPOPCD DC    CL6'DROP'
USNGOPCD DC    CL6'USING'
REGEQU   DC    C'COPY   REGEQU'
ENDOPCD  DC    CL6'END'
BCOPCD   DC    CL6'BC'
OPER0C   DC    CL02'0C'
OPER0H   DC    CL02'0H'
HEXDC    DC    C'X'''
CHARDC   DC    C'C'''
GENSIDLM DC    C',X'''               DELIMITER AND X'
CHARZERO DC    CL8'00000000'         CONSTANT
SUBHEAD  DS    0C
         DC    CL08'  DISP  '
         DC    CL02' '
         DC    CL14'OBJECT CODE'
         DC    CL05' '
         DC    CL08' LABEL '
         DC    CL01' '
         DC    CL06'OPCODE'
         DC    CL01' '
         DC    CL25'OPERANDS'
         DC    CL05' '
         DC    CL07'COMMENT'
SUBHEADL EQU   *-SUBHEAD
         DC    C' '
SRC      DS    0C
SRCDISP  DC    CL08' '               DISPLACEMENT
         DC    CL02' '
SRCOBJ1  DC    CL04' '               OBJECT CODE BYTES 1 AND 2
         DC    CL01' '
SRCOBJ2  DC    CL04' '               OBJECT CODE BYTES 3 AND 4
         DC    CL01' '
SRCOBJ3  DC    CL04' '               OBJECT CODE BYTES 5 AND 6
         DC    CL05' '
SRCLABL  DC    CL08' '  01 - 08      LABEL
         DC    CL01' '  09 - 09
SRCMNEM  DC    CL06' '  10 - 15      MNEMONIC
         DC    CL01' '  16 - 16
SRCOPER  DC    CL35' '  17 - 51      OPERANDS
         ORG   SRCLABL+46
SRCCMNT  DC    CL25' '  46 - 71      COMMENTS
         DC    CL01' '  72 - 72      CONTINUATION COLUMN
SRCSEQ   DC    CL08' '  73 - 80      STATEMENT SEQUENCE NUMBER
SRCL     EQU   *-SRC
OBJIN    DC    CL8' '                INPUT STAGING AREA
OBJOUT1  DC    CL4' '                OBJECT CODE BYTES 1 AND 2
         DC    CL1' '
OBJOUT2  DC    CL4' '                OBJECT CODE BYTES 3 AND 4
         DC    CL1' '
OBJOUT3  DC    CL4' '                OBJECT CODE BYTES 5 AND 6
         DC    CL1' '
OBJLEN   DS    0H
         DC    H'01'                 02 BYTES OUT FOR 01 BYTE
         DC    H'03'                 04 BYTES OUT FOR 02 BYTES
         DC    H'06'                 07 BYTES OUT FOR 03 BYTES
         DC    H'08'                 09 BYTES OUT FOR 04 BYTES
         DC    H'11'                 12 BYTES OUT FOR 05 BYTES
         DC    H'13'                 14 BYTES OUT FOR 06 BYTES
         DC    H'13'                 14 BYTES OUT FOR 07 BYTES
         DC    H'13'                 14 BYTES OUT FOR 08 BYTES
EMSG01   DC    C'DISASM0901E Data area overlaps in instruction, should +
               have been detected by DISASM08'
EMSG01L  EQU   *-EMSG01
EMSG02   DC    C'DISASM0902E Invalid opcode detected'
EMSG02L  EQU   *-EMSG02
EMSG03   DC    C'DISASM0903E DC with length = zero detected'
EMSG03L  EQU   *-EMSG03
EMSG04   DC    C'DISASM0904E Attempt to generate instruction on an odd +
               address boundary'
EMSG04L  EQU   *-EMSG04
         SPACE 2
GENRRCCA DS    0C                    COMPARE MNEMONICS
         DC    X'00',CL6'NOPR  '     NO-OP
         DC    X'01',CL6'BOR   '     BRANCH OVERFLOW
         DC    X'02',CL6'BPR   '     BRANCH PLUS
         DC    X'04',CL6'BMR   '     BRANCH MINUS
         DC    X'07',CL6'BNZR  '     BRANCH NOT ZERO
         DC    X'08',CL6'BZR   '     BRANCH IF ZERO
         DC    X'0B',CL6'BNMR  '     BRANCH NOT MINUS
         DC    X'0D',CL6'BNPR  '     BRANCH NOT PLUS
         DC    X'0E',CL6'BNOR  '     BRANCH NOT OVERFLOW
         DC    X'0F',CL6'BR    '     UNCONDITIONAL BRANCH
         DC    X'FF'
GENRRCCC DS    0C                    ARITHMETIC MNEMONICS
         DC    X'00',CL6'NOPR  '     NO-OP
         DC    X'02',CL6'BHR   '     BRANCH HIGH
         DC    X'04',CL6'BLR   '     BRANCH LOW
         DC    X'07',CL6'BNER  '     BRANCH NOT EQUAL
         DC    X'08',CL6'BER   '     BRANCH EQUAL
         DC    X'0B',CL6'BNLR  '     BRANCH NOT LOW
         DC    X'0D',CL6'BNHR  '     BRANCH NOT HIGH
         DC    X'0F',CL6'BR    '     UNCONDITIONAL BRANCH
         DC    X'FF'
GENRRCCL DS    0C                    LOGICAL MNEMONICS
         DC    X'00',CL6'NOPR  '     NO-OP
         DC    X'01',CL6'BOR   '     BRANCH ONES
         DC    X'04',CL6'BMR   '     BRANCH MIXED
         DC    X'07',CL6'BNZR  '     BRANCH NOT ZEROS
         DC    X'08',CL6'BZR   '     BRANCH IF ZEROS
         DC    X'0B',CL6'BNMR  '     BRANCH NOT MIXED
         DC    X'0E',CL6'BNOR  '     BRANCH NOT ONES
         DC    X'0F',CL6'BR    '     UNCONDITIONAL BRANCH
         DC    X'FF'
GENBCCA  DS    0C                    COMPARE MNEMONICS
         DC    X'00',CL6'NOP   '     NO-OP
         DC    X'01',CL6'BO    '     BRANCH OVERFLOW
         DC    X'02',CL6'BP    '     BRANCH PLUS
         DC    X'04',CL6'BM    '     BRANCH MINUS
         DC    X'07',CL6'BNZ   '     BRANCH NOT ZERO
         DC    X'08',CL6'BZ    '     BRANCH IF ZERO
         DC    X'0B',CL6'BNM   '     BRANCH NOT MINUS
         DC    X'0D',CL6'BNP   '     BRANCH NOT PLUS
         DC    X'0E',CL6'BNO   '     BRANCH NOT OVERFLOW
         DC    X'0F',CL6'B     '     UNCONDITIONAL BRANCH
         DC    X'FF'
GENBCCC  DS    0C                    ARITHMETIC MNEMONICS
         DC    X'00',CL6'NOP   '     NO-OP
         DC    X'02',CL6'BH    '     BRANCH HIGH
         DC    X'04',CL6'BL    '     BRANCH LOW
         DC    X'07',CL6'BNE   '     BRANCH NOT EQUAL
         DC    X'08',CL6'BE    '     BRANCH EQUAL
         DC    X'0B',CL6'BNL   '     BRANCH NOT LOW
         DC    X'0D',CL6'BNH   '     BRANCH NOT HIGH
         DC    X'0F',CL6'B     '     UNCONDITIONAL BRANCH
         DC    X'FF'
GENBCCL  DS    0C                    LOGICAL MNEMONICS
         DC    X'00',CL6'NOP   '     NO-OP
         DC    X'01',CL6'BO    '     BRANCH ONES
         DC    X'04',CL6'BM    '     BRANCH MIXED
         DC    X'07',CL6'BNZ   '     BRANCH NOT ZEROS
         DC    X'08',CL6'BZ    '     BRANCH IF ZEROS
         DC    X'0B',CL6'BNM   '     BRANCH NOT MIXED
         DC    X'0E',CL6'BNO   '     BRANCH NOT ONES
         DC    X'0F',CL6'B     '     UNCONDITIONAL BRANCH
         DC    X'FF'
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
DISPUNCH DCB   DDNAME=DISPUNCH,      SOURCE CODE PUNCHED OUTPUT        +
               DSORG=PS,             .. SEQUENTIAL                     +
               LRECL=80,             .. LRECL IS 80                    +
               MACRF=PM              .. PUT-MOVE MODE
SYSIN    DCB   DDNAME=SYSIN,         ASSEMBLER INPUT FILE              +
               DSORG=PS,             .. SEQUENTIAL                     +
               EODAD=GEN0720,        .. END OF DATA                    +
               LRECL=80,             .. LRECL IS 80                    +
               MACRF=GL              .. GET LOCATE MODE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              SVC DESCRIPTIONS                                       *
*                                                                     *
*---------------------------------------------------------------------*
SVCDESC  DS    0C
         SVCDEF 00,'EXCP/XDAP'
         SVCDEF 01,'WAIT/WAITR/PRTOV'
         SVCDEF 02,'POST'
         SVCDEF 03,'EXIT'
         SVCDEF 04,'GETMAIN'
         SVCDEF 05,'FREEMAIN'
         SVCDEF 06,'LINK/LINKX'
         SVCDEF 07,'XCTL/XCTLX'
         SVCDEF 08,'LOAD'
         SVCDEF 09,'DELETE'
         SVCDEF 0A,'GETMAIN/FREEMAIN'
         SVCDEF 0B,'TIME'
         SVCDEF 0C,'SYNCH/SYNCHX'
         SVCDEF 0D,'ABEND'
         SVCDEF 0E,'SPIE'
         SVCDEF 0F,'ERREXCP'
         SVCDEF 10,'PURGE'
         SVCDEF 11,'RESTORE'
         SVCDEF 12,'BLDL/FIND (TYPE D)'
         SVCDEF 13,'OPEN'
         SVCDEF 14,'CLOSE'
         SVCDEF 15,'STOW'
         SVCDEF 16,'OPEN (TYPE=J)'
         SVCDEF 17,'CLOSE (TYPE=T)'
         SVCDEF 18,'DEVTYPE'
         SVCDEF 19,'TRKBAL'
         SVCDEF 1A,'CATALOG/INDEX/LOCATE'
         SVCDEF 1B,'OBTAIN'
         SVCDEF 1D,'SCRATCH'
         SVCDEF 1E,'RENAME'
         SVCDEF 1F,'FEOV'
         SVCDEF 20,'ALLOC'
         SVCDEF 21,'IOHALT'
         SVCDEF 22,'MGCR/QEDIT'
         SVCDEF 23,'WTO/WTOR'
         SVCDEF 24,'WTL'
         SVCDEF 25,'SEGLD/SEGWT'
         SVCDEF 27,'LABEL'
         SVCDEF 28,'EXTRACT'
         SVCDEF 29,'IDENTIFY'
         SVCDEF 2A,'ATTACH/ATTACHX'
         SVCDEF 2B,'CIRB'
         SVCDEF 2C,'CHAP'
         SVCDEF 2D,'OVLYBRCH'
         SVCDEF 2E,'TIMER'
         SVCDEF 2F,'STIMER'
         SVCDEF 30,'DEQ'
         SVCDEF 33,'SNAP/SNAPX/SDUMP/SDUMPX'
         SVCDEF 34,'RESTART'
         SVCDEF 35,'RELEX'
         SVCDEF 36,'DISABLE'
         SVCDEF 37,'EOV'
         SVCDEF 38,'ENQ/RESERVE'
         SVCDEF 39,'FREEDBUF'
         SVCDEF 3A,'RELBUF/REQBUF'
         SVCDEF 3B,'OLTEP'
         SVCDEF 3C,'STAE/STAI-ESTAE/ESTAI'
         SVCDEF 3D,'IKJEGS6A'
         SVCDEF 3E,'DETACH'
         SVCDEF 3F,'CHKPT'
         SVCDEF 40,'RDJFCB'
         SVCDEF 42,'BTAMTEST'
         SVCDEF 44,'SYNADF/SYNADRLS'
         SVCDEF 45,'BSP'
         SVCDEF 46,'GSERV'
         SVCDEF 47,'ASGNBFR/BUFINQ/RLSEBFR'
         SVCDEF 49,'SPAR'
         SVCDEF 4A,'DAR'
         SVCDEF 4B,'DQUEUE'
         SVCDEF 4C,'IFBSTAT'
         SVCDEF 4E,'LSPACE'
         SVCDEF 4F,'STATUS'
         SVCDEF 51,'SETPRT'
         SVCDEF 53,'SMFWTM'
         SVCDEF 54,'GRAPHICS'
         SVCDEF 55,'DDRSWAP'
         SVCDEF 56,'ATLAS'
         SVCDEF 57,'DOM'
         SVCDEF 5B,'VOLSTAT'
         SVCDEF 5C,'TCPEXCP'
         SVCDEF 5D,'TGET/TPUT'
         SVCDEF 5E,'TGET/TPUT'
         SVCDEF 5F,'SYSEVENT'
         SVCDEF 60,'STAX'
         SVCDEF 61,'IKJEGS9G'
         SVCDEF 62,'PROTECT'
         SVCDEF 63,'DYNALLOC'
         SVCDEF 64,'IKJEFFIB'
         SVCDEF 65,'QTIP'
         SVCDEF 66,'AQCTL'
         SVCDEF 67,'XLATE'
         SVCDEF 68,'TOPCTL'
         SVCDEF 69,'IMGLIB'
         SVCDEF 6B,'MODESET'
         SVCDEF 70,'PGRLSE'
         SVCDEF 71,'PGFIX/PGFREE/PGLOAD/PGOUT'
         SVCDEF 72,'EXCPVR'
         SVCDEF 75,'DEBCHK'
         SVCDEF 77,'TESTAUTH'
         SVCDEF 78,'GETMAIN/FREEMAIN'
         SVCDEF 79,'VSAM'
         SVCDEF 7B,'PURGEDQ'
         SVCDEF 7C,'TPIO'
         SVCDEF 7D,'EVENTS'
         SVCDEF 7E,'MSS(ICB2SVC)'
         SVCDEF 82,'RACHECK'
         SVCDEF 83,'RACINIT'
         SVCDEF 84,'RACLIST'
         SVCDEF 85,'RACDEF'
         SVCDEF 89,'ESR'
         SVCDEF 8A,'PGSER'
         SVCDEF 8B,'CVAFDIR'
         DC    X'FF'
         SPACE 2
         LTORG
         SPACE 2
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              SVC DSECT                                              *
*                                                                     *
*---------------------------------------------------------------------*
SVCDSECT DSECT
SVCLEN   DS   XL2                LENGTH OF THIS SVC DEFINITION
SVCCMNTL DS   XL2                LENGTH OF DESCRIPTION
SVCNBR   DS   X                  SVC NUMBER
SVCCMNT  DS   0C                 DESCRIPTION
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM09
./ ADD NAME=DISASM10
         TITLE 'DISASM10 - RLD DATA PROCESSOR'
*--------------------------------------------------------------------*
*                                                                    *
*  Module name: DISASM10                                             *
*                                                                    *
*  Function:                                                         *
*   When RLD items overlap user defined DATA areas, this module      *
*   will 'adjust' the DATA items.                                    *
*                                                                    *
*   There are five possibilities:                                    *
*      1) the RLD item is exactly the same as the DATA item.         *
*         In this case the 'type' is changed to reflect the RLD type *
*      2) The DATA item spans the RLD item.                          *
*         This requires "breaking the DATA item in two".             *
*         The end result is a DATA item that starts at the user      *
*         specified start point and ends immediately before the RLD  *
*         item and a second that starts immediately after the RLD    *
*         item and ends at the user specified point.                 *
*      3) The RLD item overlaps the first part of the DATA item.     *
*         In this case the beginning displacement in the DATA item   *
*         is changed to reference the byte immediately following the *
*         RLD item.                                                  *
*      4) The RLD item overlaps the end of a DATA item.              *
*         In this case the ending displacement in the DATA item is   *
*         changed to reference the byte immediately preceding the    *
*         RLD item.                                                  *
*      5) The RLD item overlaps the entire DATA item.                *
*         In this case the DATA block is removed from the DATA       *
*         block chain.                                               *
*                                                                    *
*                                                                    *
*  This module is 'called' from DISASM05.  The call does NOT follow  *
*  standard subroutine linkage.                                      *
*                                                                    *
*    At the time of the call, R5 will be set to the address of the   *
*    new RLDDATA block, R8 will be set to the address of the         *
*    DATADSCT block.                                                 *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DISASMGB
DISASM10 CSECT
DISASM10 AMODE 31
DISASM10 RMODE 24
         USING DISASM10,R12
         USING DISASM00,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     PROC0000              SKIP EYECATCHER
         DC    CL8'DISASM10'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
PROC0000 DS    0H
         LA    R1,SAVE10             OUR SAVE AREA ADDRESS
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         USING RLDDATA,R5            DEFINE BASE
         USING DATADSCT,R8           DEFINE BASE
         ITRACE ID=ENTRY,                                              +
               DATA1=RLDDISP,                                          +
               DATA2=RLDLEN
         ICM   R1,15,RLDDISP         DISPLACEMENT (BEGIN)
         ST    R1,TEMPBEGN           SAVE BEGIN DISP
         AH    R1,RLDLEN             PLUS LENGTH
         BCTR  R1,0                  MINUS 1
         ST    R1,TEMPEND            SAVE END DISP
* ------------------------------------------------------------------- *
*                                                                     *
*   Test for case 1.                                                  *
*        RLD and DATA items reference the same area.                  *
*                                                                     *
*   Action taken:                                                     *
*        Remove the DATA item.                                        *
*                                                                     *
* ------------------------------------------------------------------- *
         CLC   TEMPBEGN,DATABEGN     SAME START?
         BE    PROC1000              YES
         B     PROC2000              OTHERWISE NO
PROC1000 DS    0H
         CLC   TEMPEND,DATAEND       SAME END
         BE    PROC1010              YES
         B     PROC2000              OTHERWISE NO
PROC1010 DS    0H
         ITRACE ID=CASE1
         TM    COMMFLG2,$RLDWARN     PRINT WARNING MESSAGES?
         BNO   PROC1020              NO
         UNPK  MSG01BGN(9),DATABEGN(5)      BEGINNING DISPLACEMENT
         MVZ   MSG01BGN,HEX0000      TURN OFF ZONES
         TR    MSG01BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG01BGN+8,C' '
         UNPK  MSG01END(9),DATAEND(5)       ENDING DISPLACEMENT
         MVZ   MSG01END,HEX0000      TURN OFF ZONES
         TR    MSG01END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG01END+8,C'.'
         MVC   PRTDATA(MSG01L),MSG01
         BAL   R10,PRT0000           PRINT
PROC1020 DS    0H
         LA    R1,COMMDATA           'POINTER' TO FIRST DATA BLOCK
PROC1030 DS    0H
         C     R8,0(R1)              POINTER TO OUR DATA BLOCK?
         BE    PROC1040              YES
         L     R1,0(R1)              NEXT BLOCK
         B     PROC1030
PROC1040 DS    0H
         MVC   0(4,R1),DATANEXT      UNCHAIN THIS DATA BLOCK
         ITRACE ID=FREEDATA,                                           +
               RDATA1=R8
         FREEMAIN RU,A=(R8),LV=DATAL
         B     EXIT0000              AND EXIT
* ------------------------------------------------------------------- *
*   Test for case 2.                                                  *
*         A DATA item spans the RLD item.                             *
*         (The RLD item is in the middle of a user specified area)    *
*                                                                     *
*   Action taken:                                                     *
*        The data item is "split" into two parts.                     *
* ------------------------------------------------------------------- *
PROC2000 DS    0H
         CLC   DATABEGN,TEMPBEGN     DATA BEGIN < RLD BEGIN
         BL    PROC2010              YES
         B     PROC3000              OTHERWISE NO
PROC2010 DS    0H
         CLC   DATAEND,TEMPEND       DATA END > RLD END?
         BH    PROC2020              YES
         B     PROC3000              OTHERWISE NO
PROC2020 DS    0H
         ITRACE ID=CASE2
         MVC   WORKB1,DATABEGN       COPY BEGIN FOR DATA ITEM 1
         L     R1,TEMPBEGN           RLD BEGIN DISP
         BCTR  R1,0                  MINUS 1
         ST    R1,WORKE1             SAVE DATA ITEM END DISP
         S     R1,WORKB1             MINUS STARTING DISPLACEMENT
         LA    R1,1(R1)              PLUS 1
         STH   R1,WORKL1             SAVE LENGTH
         L     R1,TEMPEND            RLD END DISP
         LA    R1,1(R1)              PLUS 1
         ST    R1,WORKB2             SAVE BEGIN DISP FOR DATA ITEM 2
         MVC   WORKE2,DATAEND        SAVE END DISP FOR DATA ITEM 2
         L     R1,WORKE2             ENDING DISPLACEMENT
         S     R1,WORKB2             MINUS STARTING DISPLACEMENT
         LA    R1,1(R1)              PLUS 1
         STH   R1,WORKL2             SAVE LENGTH
         TM    COMMFLG2,$RLDWARN     PRINT WARNING MESSAGES?
         BNO   PROC2030              NO
         UNPK  MSG02BGN(9),DATABEGN(5)      BEGINNING DISPLACEMENT
         MVZ   MSG02BGN,HEX0000      TURN OFF ZONES
         TR    MSG02BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG02BGN+8,C' '
         UNPK  MSG02END(9),DATAEND(5)       ENDING DISPLACEMENT
         MVZ   MSG02END,HEX0000      TURN OFF ZONES
         TR    MSG02END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG02END+8,C'.'
         MVC   PRTDATA(MSG02L),MSG02
         BAL   R10,PRT0000           PRINT
         UNPK  MSG03B1(9),WORKB1(5)  BEGINNING DISPLACEMENT
         MVZ   MSG03B1,HEX0000       TURN OFF ZONES
         TR    MSG03B1,HEXCHAR       TRANSLATE TO PRINTABLE
         MVI   MSG03B1+8,C' '
         UNPK  MSG03E1(9),WORKE1(5)  ENDING DISPLACEMENT
         MVZ   MSG03E1,HEX0000       TURN OFF ZONES
         TR    MSG03E1,HEXCHAR       TRANSLATE TO PRINTABLE
         MVI   MSG03E1+8,C' '
         UNPK  MSG03B2(9),WORKB2(5)  BEGINNING DISPLACEMENT
         MVZ   MSG03B2,HEX0000       TURN OFF ZONES
         TR    MSG03B2,HEXCHAR       TRANSLATE TO PRINTABLE
         MVI   MSG03B2+8,C' '
         UNPK  MSG03E2(9),WORKE2(5)  ENDING DISPLACEMENT
         MVZ   MSG03E2,HEX0000       TURN OFF ZONES
         TR    MSG03E2,HEXCHAR       TRANSLATE TO PRINTABLE
         MVI   MSG03E2+8,C' '
         MVC   PRTDATA(MSG03L),MSG03
         BAL   R10,PRT0000           PRINT
PROC2030 DS    0H
         MVC   DATAEND,WORKE1        CHANGE ENDING DISPLACEMENT
         MVC   DATALEN,WORKL1        CHANGE LENGTH
         GETMAIN RU,                                                   +
               LV=DATAL,             .. SIZE                           +
               LOC=ANY
         MVC   DATANEXT-DATADSCT(L'DATANEXT,R1),DATANEXT
         ST    R1,DATANEXT
         MVC   DATAEYE-DATADSCT(L'DATAEYE,R1),DATAID
         MVC   DATABEGN-DATADSCT(L'DATABEGN,R1),WORKB2
         MVC   DATAEND-DATADSCT(L'DATAEND,R1),WORKE2
         MVC   DATALEN-DATADSCT(L'DATALEN,R1),WORKL2
         MVC   DATATYPE-DATADSCT(L'DATATYPE,R1),DATATYPE
         B     EXIT0000
* ------------------------------------------------------------------- *
*   TEST FOR CASE 3.                                                  *
*         A RLD ITEM OVERLAPS THE FIRST PORTION OF A DATA AREA.       *
*                                                                     *
*   Action taken:                                                     *
*        CHANGE THE BEGINNING DISPLACEMENT IN THE DATA AREA           *
* ------------------------------------------------------------------- *
PROC3000 DS    0H
         CLC   TEMPBEGN,DATABEGN     RLD BEGIN = OR < DATA BEGIN?
         BE    PROC3010              YES
         BL    PROC3010              YES
         B     PROC4000              OTHERWISE NO
PROC3010 DS    0H
         CLC   TEMPEND,DATABEGN      RLD END = OR > DATA BEGIN?
         BE    PROC3020              YES
         BH    PROC3020              YES
         B     PROC4000              OTHERWISE NO
PROC3020 DS    0H
         CLC   TEMPEND,DATAEND       RLD END < DATA END?
         BL    PROC3030              YES
         B     PROC4000              OTHERWISE NO
PROC3030 DS    0H
         ITRACE ID=CASE3
         TM    COMMFLG2,$RLDWARN     PRINT RLD WARNING MESSAGES?
         BNO   PROC3040              NO
         UNPK  MSG04BGN(9),DATABEGN(5)      BEGINNING DISPLACEMENT
         MVZ   MSG04BGN,HEX0000      TURN OFF ZONES
         TR    MSG04BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG04BGN+8,C' '
         UNPK  MSG04END(9),DATAEND(5)       ENDING DISPLACEMENT
         MVZ   MSG04END,HEX0000      TURN OFF ZONES
         TR    MSG04END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG04END+8,C'.'
         MVC   PRTDATA(MSG04L),MSG04
         BAL   R10,PRT0000           PRINT
PROC3040 DS    0H
         L     R1,TEMPEND            RLD END DISPLACEMENT
         LA    R1,1(R1)              PLUS 1
         ST    R1,DATABEGN           SET NEW BEGIN DISPLACEMENT
         TM    COMMFLG2,$RLDWARN     PRINT RLD WARNING MESSAGES?
         BNO   PROC3050              NO
         UNPK  MSG05BGN(9),DATABEGN(5)      BEGIN DISPLACEMENT
         MVZ   MSG05BGN,HEX0000      TURN OFF ZONES
         TR    MSG05BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG05BGN+8,C' '
         BAL   R10,PRT0000           PRINT
PROC3050 DS    0H
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*   TEST FOR CASE 4.                                                  *
*         A RLD ITEM OVERLAPS THE LAST PORTION OF A DATA AREA.        *
*                                                                     *
*   Action taken:                                                     *
*        CHANGE THE ENDING DISPLACEMENT IN THE DATA AREA              *
* ------------------------------------------------------------------- *
PROC4000 DS    0H
         CLC   TEMPBEGN,DATABEGN     RLD BEGIN > DATA BEGIN
         BH    PROC4010              YES
         B     PROC5000              OTHERWISE NO
PROC4010 DS    0H
         CLC   TEMPBEGN,DATAEND      RLD BEGIN = OR < DATA END?
         BE    PROC4020              YES
         BL    PROC4020              YES
         B     PROC5000              OTHERWISE NO
PROC4020 DS    0H
         CLC   TEMPEND,DATAEND       RLD END > DATA END?
         BH    PROC4030              YES
         B     PROC5000              OTHERWISE NO
PROC4030 DS    0H
         ITRACE ID=CASE4
         TM    COMMFLG2,$RLDWARN     PRINT RLD WARNING MESSAGES?
         BNO   PROC4040              NO
         UNPK  MSG06BGN(9),DATABEGN(5)      BEGINNING DISPLACEMENT
         MVZ   MSG06BGN,HEX0000      TURN OFF ZONES
         TR    MSG06BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG06BGN+8,C' '
         UNPK  MSG06END(9),DATAEND(5)       ENDING DISPLACEMENT
         MVZ   MSG06END,HEX0000      TURN OFF ZONES
         TR    MSG06END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG06END+8,C'.'
         MVC   PRTDATA(MSG06L),MSG06
         BAL   R10,PRT0000           PRINT
PROC4040 DS    0H
         L     R1,TEMPBEGN           RLD BEGIN DISPLACEMENT
         BCTR  R1,0                  MINUS 1
         ST    R1,DATAEND            SET NEW ENDING DISPLACEMENT
         TM    COMMFLG2,$RLDWARN     PRINT RLD WARNING MESSAGES?
         BNO   PROC4050              NO
         UNPK  MSG07END(9),DATAEND(5)      ENDING DISPLACEMENT
         MVZ   MSG07END,HEX0000      TURN OFF ZONES
         TR    MSG07END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG07END+8,C' '
         BAL   R10,PRT0000           PRINT
PROC4050 DS    0H
         B     EXIT0000              EXIT
* ------------------------------------------------------------------- *
*   TEST FOR CASE 5.                                                  *
*         A RLD ITEM OVERLAPS AN ENTIRE DATA AREA                     *
*                                                                     *
*   Action taken:                                                     *
*        DELETE THE DATA AREA.                                        *
* ------------------------------------------------------------------- *
PROC5000 DS    0H
         CLC   TEMPBEGN,DATABEGN     RLD BEGIN = OR < DATA BEGIN
         BE    PROC5010              YES
         BL    PROC5010              YES
         B     EXIT0000              OTHERWISE NO
PROC5010 DS    0H
         CLC   TEMPEND,DATAEND       RLD END = OR > DATA END
         BE    PROC5020              YES
         BH    PROC5020              YES
         B     EXIT0000              OTHERWIZE NO
PROC5020 DS    0H
         ITRACE ID=CASE5
         TM    COMMFLG2,$RLDWARN     PRINT RLD WARNING MESSAGES?
         BNO   PROC5030              NO
         UNPK  MSG08BGN(9),DATABEGN(5)      BEGINNING DISPLACEMENT
         MVZ   MSG08BGN,HEX0000      TURN OFF ZONES
         TR    MSG08BGN,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG08BGN+8,C' '
         UNPK  MSG08END(9),DATAEND(5)       ENDING DISPLACEMENT
         MVZ   MSG08END,HEX0000      TURN OFF ZONES
         TR    MSG08END,HEXCHAR      TRANSLATE TO PRINTABLE
         MVI   MSG08END+8,C','
         MVC   PRTDATA(MSG08L),MSG08
         BAL   R10,PRT0000           PRINT
PROC5030 DS    0H
         LA    R1,COMMDATA           'POINTER' TO FIRST DATA AREA
PROC5040 DS    0H
         C     R8,0(R1)              IS THIS OUR DATA AREA?
         BE    PROC5050              YES
         L     R1,0(R1)              NEXT BLOCK
         B     PROC5040
PROC5050 DS    0H
         MVC   0(4,R1),DATANEXT      UNCHAIN THIS BLOCK
         FREEMAIN RU,A=(R8),LV=DATAL
         B     EXIT0000              EXIT
*-------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0000  DS    0H
         MVI   PRTCMD,$PRTPRT        SET COMMAND
         LA    R1,PRTBLOK            SET INTERFACE BLOCK ADDRESS
         L     R15,APR               PRINT MODULE ENTRY POINT
         BALR  R14,R15               PRINT SUBHEADING
         BR    R10                   EXIT
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
DWORD    DC    D'0'
SAVE10   DC    18F'0'                REGISTER SAVE AREA
TEMPBEGN DS    A
TEMPEND  DS    A
WORKB1   DS    A
WORKE1   DS    A
WORKL1   DS    H
WORKB2   DS    A
WORKE2   DS    A
WORKL2   DS    H
DATAID   DC    CL8'DATA'
HEX0000  DC    16X'00'
HEXCHAR  DC    C'0123456789ABCDEF'
MSG01    DS    0C
         DC    C'DISASM1001W RLD is the same as data item at '
MSG01BGN DC    CL8' '
         DC    C' to '
MSG01END DC    CL8' '
         DC    C'.  DATA item removed'
MSG01L   EQU   *-MSG01
MSG02    DS    0C
         DC    C'DISASM1002W DATA area from '
MSG02BGN DC    CL8' '
         DC    C' to '
MSG02END DC    CL8' '
         DC    C'.  spans this RLD item.'
MSG02L   equ   *-MSG02
MSG03    DS    0C
         DC    C'DISASM1003W DATA area(s) now from '
MSG03B1  DC    CL8' '
         DC    C' to '
MSG03E1  DC    CL8' '
         DC    C' and '
MSG03B2  DC    CL8' '
         DC    C' to '
MSG03E2  DC    CL8' '
         DC    C' '
MSG03L   EQU   *-MSG03
MSG04    DS    0C
         DC    C'DISASM1004W This RLD item overlaps first part of DATA +
               area at '
MSG04BGN DC    CL8' '
         DC    C' to '
MSG04END DC    CL8' '
MSG04L   equ   *-MSG04
MSG05    DS    0C
         DC    C'DISASM1005W DATA area begin displacement changed to '
MSG05BGN DC    CL8' '
         DC    C' '
MSG05L   equ   *-MSG05
MSG06    DS    0C
         DC    C'DISASM1006W This RLD item overlaps first part of DATA +
               area at '
MSG06BGN DC    CL8' '
         DC    C' to '
MSG06END DC    CL8' '
MSG06L   EQU   *-MSG06
MSG07    DS    0C
         DC    C'DISASM1007W DATA area end displacement changed to '
MSG07END DC    CL8' '
         DC    C' '
MSG07L   EQU   *-MSG07
MSG08    DS    0C
         DC    C'DISASM1008W This RLD item overlaps DATA area at '
MSG08BGN DC    CL8' '
         DC    C' to '
MSG08END DC    CL8', DATA area deleted'
MSG08L   EQU   *-MSG08
*---------------------------------------------------------------------*
*                                                                     *
*              PRINT MODULE INTERFACE BLOCK                           *
*                                                                     *
*---------------------------------------------------------------------*
PRTBLOK  PRTBLOK  TYPE=CSECT
         SPACE 1
         LTORG
         SPACE 1
         COPY  DISASMDA
*---------------------------------------------------------------------*
*                                                                     *
*              COMMON DATA MAP                                        *
*                                                                     *
*---------------------------------------------------------------------*
DISASM00 DISASM00 TYPE=DSECT
*---------------------------------------------------------------------*
*                                                                     *
*              EQUATES                                                *
*                                                                     *
*---------------------------------------------------------------------*
         COPY REGEQU
         END  DISASM10
