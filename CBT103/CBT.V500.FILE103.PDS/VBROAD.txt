         PROC 0 DEBUG TEST
         CONTROL NOFLUSH ASIS NOMSG
         IF &DEBUG = DEBUG THEN CONTROL LIST SYMLIST CONLIST MSG
         ISPEXEC CONTROL ERRORS RETURN

         ALLOC FI(VBROAD) DSN('MVS.BRODCAST') OLD
         SET CC_ALLOC = &LASTCC
         IF &CC_ALLOC > 0 THEN DO
           SET ZEDSMSG = TRY LATER
           SET ZEDLMSG = DIALOG IN USE BY ANOTHER USER.  ALLOC RC WAS
           SET ZEDLMSG = &ZEDLMSG &CC_ALLOC..
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT CODE(4)
           END

         ISPEXEC SELECT PGM(A75905)
         ISPEXEC VGET (XNAME,ZDATE,ZUSER) SHARED

         ISPEXEC TBOPEN VBROAD LIBRARY(VBROAD)
         SET CC_TBOPEN = &LASTCC
         IF &CC_TBOPEN > 0 THEN DO
           SET MSG1 = TBOPEN FAILED.  RETCODE WAS &CC_TBOPEN..
           SET MSG2 =
           SET MSG3 =
           SET MSG4 =
           ISPEXEC DISPLAY PANEL(VBROAD2)
           EXIT CODE(16)
           END

         ISPEXEC LMINIT DATAID(LMBROAD) DDNAME(VBROAD)
         SET CC_LMINIT = &LASTCC
         IF &CC_LMINIT > 0 THEN DO
           SET MSG1 = LMINIT FAILED.  RETCODE WAS &CC_LMINIT..
           SET MSG2 =
           SET MSG3 =
           SET MSG4 =
           ISPEXEC DISPLAY PANEL(VBROAD2)
           EXIT CODE(16)
           END

         SET TABLE_UPDATED = NO
         SET QUIT = NO
         ISPEXEC TBDISPL VBROAD PANEL(VBROAD0)
         SET CC_TBDISPL = &LASTCC
         SET COMMAND = &ZCMD
         SET ZCMD =

         DO WHILE (&CC_TBDISPL = 0)

           /*---------------------------*/
           /* ADD A NEW BROADCAST ITEM. */
           /*---------------------------*/

           IF &COMMAND = A THEN DO
             ISPEXEC TBBOTTOM VBROAD
             SET VBKEY = &VBKEY + 1
             SET VBOUSER = &ZUSER
             ISPEXEC VGET (ZTIME) SHARED
             SET VBODATE = &STR(&ZDATE)
             SET VBOTIME = &ZTIME
             SET ZEDSMSG =
             SET ZEDLMSG = EDIT BROADCAST MESSAGE.  HIT THE END KEY
             SET ZEDLMSG = &ZEDLMSG WHEN FINISHED.
             ISPEXEC SETMSG MSG(ISRZ000)
             ISPEXEC EDIT DATASET('MVS.BRODCAST(M&VBKEY)') +
             MACRO(VBROADE)
             SET CC_EDIT = &LASTCC
             IF &CC_EDIT > 0 THEN DO
               IF &CC_EDIT = 4 THEN DO
                 SET ZEDSMSG = NO MESSAGES ADDED
                 SET ZEDLMSG = NO NEW BROADCAST MESSAGES WERE ADDED
                 ISPEXEC SETMSG MSG(ISRZ000)
                 END
               ELSE DO
                 SET ZEDSMSG = EDIT FAILED
                 SET ZEDLMSG = RETURN CODE FROM EDIT WAS &CC_EDIT..
                 ISPEXEC SETMSG MSG(ISRZ001)
                 END
               END
             ELSE DO
               SET VBMUSER =
               SET VBMDATE =
               SET VBMTIME =
               SET VBNAME = &XNAME
               ISPEXEC LMOPEN DATAID(&LMBROAD)
               ISPEXEC LMMFIND DATAID(&LMBROAD) MEMBER(M&VBKEY)
               ISPEXEC LMGET DATAID(&LMBROAD) DATALOC(VBLINE1) +
               MODE(INVAR) DATALEN(LLINE1) MAXLEN(78)
               ISPEXEC LMCLOSE DATAID(&LMBROAD)
               SET MO =
               SET DA =
               SET YEAR =
               DO WHILE (&YEAR = &STR())
                 ISPEXEC DISPLAY PANEL(VBROAD1)
                 END
               SET VBEXPDT = &STR(&MO/&DA/&SUBSTR(3:4,&YEAR))
               ISPEXEC TBADD VBROAD
               SET ZEDSMSG = MESSAGE ADDED
               SET ZEDLMSG = BROADCAST MESSAGE BEGINNING
               SET ZEDLMSG = &STR(&ZEDLMSG "&SUBSTR(1:20,&VBLINE1)")
               SET ZEDLMSG = &STR(&ZEDLMSG WAS ADDED)
               ISPEXEC SETMSG MSG(ISRZ000)
               SET TABLE_UPDATED = YES
               END
             END

           /*-----------------------------------------------------*/
           /* UPDATE BROADCAST WHETHER ANYTHING IS CHANGE OR NOT. */
           /*-----------------------------------------------------*/

           ELSE IF &COMMAND = U THEN DO
             SET TABLE_UPDATED = YES
             SET ZEDSMSG = SET FOR UPDATE
             SET ZEDLMSG = SYSTEM BROADCAST MESSAGES WILL BE UPDATED
             SET ZEDLMSG = &ZEDLMSG WHEN YOU EXIT THIS DIALOG.
             ISPEXEC SETMSG MSG(ISRZ000)
             END

           /*--------------------------*/
           /* MODIFY A BROADCAST ITEM. */
           /*--------------------------*/

           ELSE IF &ZSEL = S THEN DO
             SET ZEDLMSG = EDIT BROADCAST MESSAGE.  HIT THE END KEY
             SET ZEDLMSG = &ZEDLMSG WHEN FINISHED.
             ISPEXEC SETMSG MSG(ISRZ000)
             ISPEXEC EDIT DATASET('MVS.BRODCAST(M&VBKEY)') +
             MACRO(VBROADE)
             SET CC_EDIT = &LASTCC
             IF &CC_EDIT > 0 THEN DO
               IF &CC_EDIT = 4 THEN DO
                 SET ZEDSMSG = NOTHING CHANGED
                 SET ZEDLMSG = SELECTED BROADCAST MESSAGE WAS NOT
                 SET ZEDLMSG = &ZEDLMSG CHANGED BY EDIT.
                 ISPEXEC SETMSG MSG(ISRZ000)
                 END
               ELSE DO
                 SET ZEDSMSG = EDIT FAILED
                 SET ZEDLMSG = RETURN CODE FROM EDIT WAS &CC_EDIT..
                 ISPEXEC SETMSG MSG(ISRZ001)
                 END
               END
             ELSE DO
               SET VBMUSER = &ZUSER
               ISPEXEC VGET (ZTIME) SHARED
               SET VBMDATE = &STR(&ZDATE)
               SET VBMTIME = &ZTIME
               ISPEXEC LMOPEN DATAID(&LMBROAD)
               ISPEXEC LMMFIND DATAID(&LMBROAD) MEMBER(M&VBKEY)
               ISPEXEC LMGET DATAID(&LMBROAD) DATALOC(VBLINE1) +
               MODE(INVAR) DATALEN(LLINE1) MAXLEN(78)
               ISPEXEC LMCLOSE DATAID(&LMBROAD)
               SET MO = &SUBSTR(1:2,&VBEXPDT)
               SET DA = &SUBSTR(4:5,&VBEXPDT)
               SET YEAR = &STR(19&SUBSTR(7:8,&VBEXPDT))
               ISPEXEC DISPLAY PANEL(VBROAD1)
               SET VBEXPDT = &STR(&MO/&DA/&SUBSTR(3:4,&YEAR))
               ISPEXEC TBMOD VBROAD
               SET ZEDSMSG = MESSAGE UPDATED
               SET ZEDLMSG = BROADCAST MESSAGE BEGINNING
               SET ZEDLMSG = &STR(&ZEDLMSG "&SUBSTR(1:20,&VBLINE1)")
               SET ZEDLMSG = &STR(&ZEDLMSG WAS UPDATED)
               ISPEXEC SETMSG MSG(ISRZ000)
               SET TABLE_UPDATED = YES
               END
             END

           /*--------------------------*/
           /* DELETE A BROADCAST ITEM. */
           /*--------------------------*/

           ELSE IF &ZSEL = D THEN DO
             ISPEXEC LMOPEN DATAID(&LMBROAD) OPTION(OUTPUT)
             SET CC_LMOPEN = &LASTCC
             IF &CC_LMOPEN > 0 THEN DO
               SET MSG1 = LMOPEN FAILED.  RETCODE WAS &CC_LMOPEN..
               SET MSG2 =
               SET MSG3 =
               SET MSG4 =
               ISPEXEC DISPLAY PANEL(VBROAD2)
               END
             ISPEXEC LMMDEL DATAID(&LMBROAD) MEMBER(M&VBKEY)
             SET CC_LMMDEL = &LASTCC
             IF &CC_LMMDEL > 0 THEN DO
               SET MSG1 = LMMDEL FAILED.  RETCODE WAS &CC_LMMDEL..
               SET MSG2 =
               SET MSG3 =
               SET MSG4 =
               ISPEXEC DISPLAY PANEL(VBROAD2)
               END
             ISPEXEC LMCLOSE DATAID(&LMBROAD)
             ISPEXEC TBDELETE VBROAD
             SET CC_DELETE = &LASTCC
             IF &CC_DELETE = 0 THEN DO
               SET ZEDSMSG = MESSAGE DELETED
               SET ZEDLMSG = SPECIFIED BROADCAST MESSAGE HAS BEEN
               SET ZEDLMSG = &ZEDLMSG DELETED.
               ISPEXEC SETMSG MSG(ISRZ000)
               SET TABLE_UPDATED = YES
               END
             ELSE DO
               SET MSG1 = DELETE OF BROADCAST MESSAGE FAILED.
               SET MSG2 = RETURN CODE WAS &CC_DELETE..
               SET MSG3 =
               SET MSG4 =
               ISPEXEC DISPLAY PANEL(VBROAD2)
               EXIT CODE(16)
               END

             END

           /*---------------------------*/
           /* BROWSE BROADCAST MESSAGE. */
           /*---------------------------*/

           ELSE IF &ZSEL = B THEN DO
             ISPEXEC BROWSE DATASET('MVS.BRODCAST(M&VBKEY)')
             END

           /*----------------------*/
           /* PROCESS DEBUG OPTION */
           /*----------------------*/

           ELSE IF &COMMAND = DEBUG +
           THEN CONTROL LIST CONLIST SYMLIST MSG
           ELSE IF &COMMAND = NODEBUG +
           THEN CONTROL NOLIST NOCONLIST NOSYMLIST NOMSG

           /*-----------------------*/
           /* PROCESS CANCEL OPTION */
           /*-----------------------*/

           ELSE IF &COMMAND = CANCEL THEN DO

             /* NOTE - THIS CODE WILL CANCEL UPDATE TO THE BROADCAST  */
             /*        DATASET, BUT ANY DATA ALREADY EDITED AND SAVED */
             /*        FOR A NEW MESSAGE WILL APPEAR THE NEXT TIME    */
             /*        A NEW MESSAGE IS ENTERED.                      */

             ISPEXEC TBEND VBROAD
             ISPEXEC TBOPEN AXSXHXUX LIBRARY(SKQTXPG) /* APAR OZ93950 */
             ISPEXEC LMFREE DATAID(&LMBROAD)
             FREE FI(VBROAD)
             EXIT CODE(8)
             END

           ISPEXEC TBTOP VBROAD
           ISPEXEC TBDISPL VBROAD PANEL(VBROAD0)
           SET CC_TBDISPL = &LASTCC
           SET COMMAND = &ZCMD
           SET ZCMD =
           END

         /*********************************************************/
         /* REINITIALIZE BROADCAST MESSAGES IF TABLE WAS CHANGED. */
         /*********************************************************/

         IF &TABLE_UPDATED = YES THEN DO

           /*-------------------------------------*/
           /* DELETE EXISTING BROADCAST MESSAGES. */
           /*-------------------------------------*/

           SET SYSOUTTRAP = 500
           LISTBC
           SET J = &SYSOUTLINE
           IF &TEST = TEST +
           THEN WRITE &J MESSAGES WOULD BE DELETED.
           ELSE DO
             OPER
             SET I = 1
             DO WHILE (&I <= &J)
               SEND &I,DELETE
               SET I = &I + 1
               END
             CONTROL END(XXX)
             END
             CONTROL END(END)
             END

           ISPEXEC TBTOP VBROAD
           ISPEXEC TBSKIP VBROAD NUMBER(1)
           SET CC_TBSKIP = &LASTCC
           DO WHILE (&CC_TBSKIP = 0)
             SET DN = &STR(&ZDATE)
             SET D1 = &STR(&SUBSTR(1:2,&DN)&SUBSTR(4:5,&DN))
             SET D1 = &STR(&D1&SUBSTR(7:8,&DN))
             SET D2 = &STR(&SUBSTR(7:8,&VBEXPDT)&SUBSTR(1:2,&VBEXPDT))
             SET D2 = &STR(&D2&SUBSTR(4:5,&VBEXPDT))
             IF &D1 > &D2 THEN DO
            /* ISPEXEC CONTROL DISPLAY LOCK                    */
            /* SET ZEDSMSG =                                   */
            /* SET ZEDLMSG = DELETING OBSOLETE MESSAGE ENTERED */
            /* SET ZEDLMSG = &STR(&ZEDLMSG &VBODATE)           */
            /* ISPEXEC DISPLAY MSG(ISRZ000)                    */
               ISPEXEC LMOPEN DATAID(&LMBROAD) OPTION(OUTPUT)
               ISPEXEC LMMDEL DATAID(&LMBROAD) MEMBER(M&VBKEY)
               ISPEXEC LMCLOSE DATAID(&LMBROAD)
               ISPEXEC TBDELETE VBROAD
               END
             ELSE DO
            /* ISPEXEC CONTROL DISPLAY LOCK                          */
            /* SET ZEDSMSG =                                         */
            /* SET ZEDLMSG = GENERATING BROADCAST MESSAGE ENTERED ON */
            /* SET ZEDLMSG = &STR(&ZEDLMSG &VBODATE AT &VBOTIME)     */
            /* ISPEXEC DISPLAY MSG(ISRZ000)                          */
               ISPEXEC LMOPEN DATAID(&LMBROAD)
               ISPEXEC LMMFIND DATAID(&LMBROAD) MEMBER(M&VBKEY)
               ISPEXEC LMGET DATAID(&LMBROAD) DATALOC(M) +
               MODE(INVAR) DATALEN(LBROAD) MAXLEN(78)
               SET &CC_LMGET = &LASTCC

               CONTROL NOLIST NOCONLIST NOSYMLIST
               SET L = &LENGTH(&STR(&M))
               SET I = 1
               SET J = 0
               DO WHILE (&I < &L)
                 IF &SUBSTR(&I:&I,&M) = &STR(') AND +
                 &SUBSTR(&I+1:&I+1,&M) ¬= &STR(') THEN DO
                   SET M = +
                   &STR(&SUBSTR(1:&I,&M)&STR(')&SUBSTR(&I+1:&L,&M))
                   END
                 IF &SUBSTR(&I:&I+1,&M) = &STR('') THEN DO
                   SET I = &I + 1
                   SET J = &J + 1
                   END
                 SET I = &I + 1
                 END
               SET L = &L - &J
               IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST

               SET X = &STR(MESSAGE ENTERED ON &VBODATE AT &VBOTIME BY)
               SET X = &STR(&X &VBNAME)
               IF &TEST = TEST +
               THEN DO
                 WRITE OPER
                 WRITE SEND '=========> &X',ALL,SAVE
                 WRITE END
                 END
               ELSE DO
                 OPER
                 SEND '=========> &X',ALL,SAVE
                 CONTROL END(XXX)
                 END
                 CONTROL END(END)
                 END
               DO WHILE (&CC_LMGET = 0)
                 IF &TEST = TEST +
                 THEN DO
                   WRITE OPER
                   WRITE SEND '&M',ALL,SAVE
                   WRITE END
                   END
                 ELSE DO
                   OPER
                   SEND '&M',ALL,SAVE
                   CONTROL END(XXX)
                   END
                   CONTROL END(END)
                   END
                 ISPEXEC LMGET DATAID(&LMBROAD) DATALOC(M) +
                 MODE(INVAR) DATALEN(LBROAD) MAXLEN(78)
                 SET &CC_LMGET = &LASTCC

                 CONTROL NOLIST NOCONLIST NOSYMLIST
                 SET L = &LENGTH(&STR(&M))
                 SET I = 1
                 SET J = 0
                 DO WHILE (&I < &L)
                   IF &SUBSTR(&I:&I,&M) = &STR(') AND +
                   &SUBSTR(&I+1:&I+1,&M) ¬= &STR(') THEN DO
                     SET M = +
                     &STR(&SUBSTR(1:&I,&M)&STR(')&SUBSTR(&I+1:&L,&M))
                     END
                   IF &SUBSTR(&I:&I+1,&M) = &STR('') THEN DO
                     SET I = &I + 1
                     SET J = &J + 1
                     END
                   SET I = &I + 1
                   END
                 SET L = &L - &J
                 IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST

                 END
               ISPEXEC LMCLOSE DATAID(&LMBROAD)
               END
             ISPEXEC TBSKIP VBROAD NUMBER(1)
             SET CC_TBSKIP = &LASTCC
             END
           SET ZEDSMSG = BROADCAST UPDATED
           SET ZEDLMSG = ONE OR MORE BROADCAST MESSAGES WERE UPDATED
           SET ZEDLMSG = &ZEDLMSG BY THE PREVIOUS DIALOG.
           ISPEXEC SETMSG MSG(ISRZ000)
           END

         ELSE DO /* NO UPDATES */
           SET ZEDSMSG = NO MESSAGES UPDATED
           SET ZEDLMSG = NO BROADCAST MESSAGES WERE UPDATED
           SET ZEDLMSG = &ZEDLMSG BY THE PREVIOUS DIALOG.
           ISPEXEC SETMSG MSG(ISRZ000)
           END

         ISPEXEC TBCLOSE VBROAD LIBRARY(VBROAD)
         SET CC_TBCLOSE = &LASTCC
         IF &CC_TBCLOSE > 0 THEN DO
           SET MSG1 = TBCLOSE FAILED. RETCODE WAS &CC_TBCLOSE..
           SET MSG2 =
           SET MSG3 =
           SET MSG4 =
           ISPEXEC DISPLAY PANEL(VBROAD2)
           EXIT CODE(16)
           END

         ISPEXEC TBOPEN AXSXHXUX LIBRARY(SKQTXPGQ) /* APAR OZ93950 */

         ISPEXEC LMFREE DATAID(&LMBROAD)
         FREE FI(VBROAD)
