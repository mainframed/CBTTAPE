/*-------------------------------------------------------------------*/
/*       MACRO: ELIST                                                */
/*       PURPOSE: LIST CURRENT DATA BEING EDITED (INCLUDING CHANGES) */
/*-------------------------------------------------------------------*/

         ISREDIT MACRO (PARM)
         CONTROL NOFLUSH NOMSG /* LIST CONLIST SYMLIST */
         ISREDIT (N1,N2)   = NUMBER
         ISREDIT (CHANGED) = DATA_CHANGED
         ISREDIT (DATASET) = DATASET
         ISREDIT (MEMBER)  = MEMBER
         ISREDIT (LEVEL)   = LEVEL
         ISREDIT (LAST)    = LINENUM .ZLAST
         ISREDIT (VERSION) = VERSION
         ISREDIT (STATS)   = STATS

         /* IF THIS IS A NUMBERED DATASET, TURN NUMBER OFF  */
         /* TEMPORARILY, SO THAT THE LINE NUMBERS WILL SHOW */

         IF &N1 = ON THEN DO
           SET CHANGED_NUMBER = YES
           ISREDIT NUMBER OFF
           END
         ELSE SET CHANGED_NUMBER = NO

         /* ALLOCATE SYSOUT DATASET */

         ALLOC FI(ELIST) SYSOUT(A) REUSE +
         LRECL(255) RECFM(V B) BLKSIZE(6000) &PARM
         SET &ACC = &LASTCC
         IF &ACC > 0 THEN DO
           SET ZEDSMSG = INVALID ATTRIBUTE(S)
           SET ZEDLMSG = INVALID ATTRIBUTE(S) IN "&PARM".
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT CODE(8)
           END

         /* OPEN DATASET AND WRITE HEADINGS */

         OPENFILE ELIST OUTPUT
         SET ELIST = &STR(ISPF/PDF ELIST - &SYSTIME &SYSDATE USERID:)
         SET ELIST = &STR(&ELIST &SYSUID)
         PUTFILE ELIST
         SET ELIST =
         PUTFILE ELIST
         SET ELIST = &STR(EDIT ---- &DATASET)
         IF &MEMBER ¬= &STR() THEN SET ELIST = &STR(&ELIST(&MEMBER) -)
         IF &STATS = ON THEN SET ELIST = &STR(&ELIST &VERSION..&LEVEL)
         IF &CHANGED = NO +
         THEN SET ELIST = &STR(&ELIST - DATA HAS NOT BEEN CHANGED)
         ELSE SET ELIST = &STR(&ELIST - CHANGES HAVE BEEN MADE TO DATA)
         PUTFILE ELIST
         SET ELIST =
         PUTFILE ELIST

         /* WRITE EACH LINE OF THE DATA-BEING-EDITED */

         SET I = 1
         DO WHILE (&I <= &LAST)
           ISREDIT (ELIST) = LINE &I
           IF &CHANGED_NUMBER = NO +
           THEN SET ELIST = &STR(&ELIST  &I)
           PUTFILE ELIST
           SET I = &I + 1
           END
         CLOSFILE ELIST
         FREE FI(ELIST)

         IF &CHANGED_NUMBER = YES THEN ISREDIT NUMBER &N1 &N2

         SET ZEDSMSG = DATA LISTED
         SET ZEDLMSG = &LAST LINES WILL BE LISTED
         IF &PARM ¬= &STR() +
         THEN SET ZEDLMSG = &ZEDLMSG WITH ATTRIBUTES &PARM..
         ELSE SET ZEDLMSG = &ZEDLMSG WITH DEFAULT ATTRIBUTES.
         ISPEXEC SETMSG MSG(ISRZ000)
