         ISREDIT MACRO

/**********************************************************************/
/* THIS MACRO CAN BE USED TO RECEIVE "SCREEN COPY" DATA INTO ISPF     */
/* EDIT ON AN IBM PC3270 OR 3290.  IT WILL DISPLAY A PANEL ONTO       */
/* WHICH THE DESIRED INFORMATION SHOULD BE COPIED.  IT WILL THEN ADD  */
/* THE LINES COPIED ONTO THE PANEL TO THE END OF THE CURRENT EDIT     */
/* DATASET.  AS MANY PANELS OF DATA MAY BE COPIED AS DESIRED; THE     */
/* ENTER KEY MUST BE HIT TO RECEIVE EACH COPIED SCREEN OF DATA.  IF   */
/* THE DATASET BEING EDITED HAS "NUMBER OFF", MEANING, PRESUMABLY     */
/* THAT THE DATA COULD BE 80 BYTES LONG, THE PANEL DISPLAYED WILL     */
/* ALLOW COPYING ALL 80 BYTES OF EACH LINE OF THE COPIED SCREEN.      */
/**********************************************************************/

         CONTROL ASIS
         SET ECMSG =
         SET COUNT1 = 0
         SET COUNT2 = 0
         ISREDIT (EDSN) = DATASET
         ISREDIT (EMEM) = MEMBER
         SET VECDSN = &EDSN
         IF &EMEM ¬= &STR() THEN SET VECDSN = &EDSN(&EMEM)
         ISPEXEC DISPLAY PANEL(COPYSC01) MSG(&ECMSG)
         SET PCC = &LASTCC
         DO WHILE(&PCC = 0)

           /*------------------------------------*/
           /* DIVIDE INPUT INTO 80 BYTE RECORDS. */
           /*------------------------------------*/

           SET I = 2
           DO WHILE (&I <=20)
             SET CLINE&I =
             SET I = &I + 1
             END
           SET I = 87
           SET J = 2
           SET L = &LENGTH(&STR(&CLINE1))
           DO WHILE (&I <= &L)
             SET K = &I + 79
             IF &K > &L THEN SET K = &L
             SET X = &SUBSTR(&I:&K,&CLINE1)
             SET CLINE&J = &STR(&X)
             SET I = &I + 80
             SET J = &J + 1
             END
           SET K = 86
           IF &K > &L THEN SET K = &L
           SET &CLINE1 = &SUBSTR(7:&K,&CLINE1)

           /*------------------------------------------------------*/
           /* ADD EACH LINE OF THE COPIED SCREEN TO THE END OF THE */
           /* CURRENT DATASET.                                     */
           /*------------------------------------------------------*/

           SET I = 1
           DO WHILE (&I <=20)
             SET X = &STR(&&CLINE&I)
             IF &STR(&X) ¬= &STR() THEN DO
               IF &SYSINDEX(&STR(&X),') > 0 +
               THEN ISREDIT LINE_AFTER .ZLAST = "&X"
               ELSE ISREDIT LINE_AFTER .ZLAST = '&X'
               SET COUNT1 = &COUNT1 + 1
               END
             SET I = &I + 1
             END
           SET ZEDSMSG = &STR(&COUNT1 LINES COPIED)
           SET ZEDLMSG = +
           &STR(&COUNT1 LINES COPIED FROM COPY PANEL TO END OF DATA)
           SET ECMSG = ISRZ000
           SET COUNT2 = &COUNT2 + &COUNT1
           SET COUNT1 = 0

           /*---------------------------------------*/
           /* REDISPLAY PANEL TO RECEIVE MORE DATA. */
           /*---------------------------------------*/

           ISPEXEC DISPLAY PANEL(COPYSC01) MSG(&ECMSG)
           SET PCC = &LASTCC
           END

         /*----------------------------------------------------------*/
         /* EXIT THE MACRO; TELL THE USER HOW MANY LINES WERE ADDED. */
         /*----------------------------------------------------------*/

         SET ZEDSMSG = &STR(&COUNT2 LINES ADDED)
         SET ZEDLMSG = +
         &STR(&COUNT2 TOTAL LINES WERE ADDED TO THE END OF THE DATA)
         ISPEXEC SETMSG MSG(ISRZ000)
         EXIT
