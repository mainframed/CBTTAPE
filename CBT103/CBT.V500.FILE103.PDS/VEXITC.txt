         PROC 0 LOGOFF LOGON DEBUG TEST NEWID(NONE)

         /*----------------------------------------------------*/
         /*                                                    */
         /* This CLIST will exit ISPF and (optionally)         */
         /*                                                    */
         /*   o Log the user off TSO                           */
         /*   o Log the user off TSO and log another (io the   */
         /*     same) user on TSO                              */
         /*                                                    */
         /*----------------------------------------------------*/

         CONTROL NOFLUSH ASIS
         IF &DEBUG = DEBUG THEN CONTROL LIST SYMLIST CONLIST MSG
         SET ACTION = Exit
         SET COMMAND =

         IF &LOGOFF = LOGOFF THEN DO
           SET ACTION = Logoff
           SET COMMAND = LOGOFF
           END
         ELSE IF &LOGON = LOGON THEN DO
           SET ACTION = Logon
           SET COMMAND = LOGON
           IF &STR(&NEWID) ¬= &STR(NONE) +
           THEN SET COMMAND = LOGON &NEWID
           END

         ISPEXEC VGET (ZSPLIT,ZAPPLID,ZDEL)
         SET VEMSG1 =
         SET VEMSG2 =

         /* Disallow anything but Exit if in split screen */

         SET TECLMSG =
         IF &ZSPLIT = YES AND &ACTION ¬= Exit THEN DO
           SET TECSMSG =
           SET TECLMSG = +
           &ACTION is not supported in a split screen environment; +
           changed to Exit.
           SET COMMAND =
           SET ACTION = Exit
           END

         IF &COMMAND ¬= &STR() THEN DO
           IF &TEST = TEST +
           THEN WRITE &COMMAND
           ELSE DO
             CONTROL NOMSG
             ISPEXEC SELECT CMD(TSOEXEC &COMMAND) MODE(FSCR)

             /* If command fails, retry once */

             IF &LASTCC = 24 +
             THEN ISPEXEC SELECT CMD(TSOEXEC &COMMAND) MODE(FSCR)
             CONTROL MSG
             END
           END

         /* Handle list / log if pending (and not if split screen */

         IF &ZSPLIT = NO THEN DO
           ISPEXEC VGET (ZLSTNAME,ZLOGNAME,ZLOGFDSP,ZLSTFDSP)

           IF &ZLSTNAME ¬= &STR() AND &ZLSTFDSP = &STR() THEN DO
             SET TECSMSG =
             SET TECLMSG = &ACTION cannot proceed until LIST dataset +
                           disposition is specified.
             ISPEXEC SETMSG MSG(TECZ000)
             ISPEXEC SELECT PGM(ISPLLP) PARM(LIST)
             IF &LASTCC > 0 THEN DO
               SET TECSMSG = &ACTION cancelled
               SET TECLMSG = &ACTION not done because LIST dataset +
                             was not disposed of on the previous +
                             panel.
               ISPEXEC SETMSG MSG(TECZ000)
               EXIT CODE(4)
               END
             END

           IF &ZLOGNAME ¬= &STR() AND &ZLOGFDSP = &STR() THEN DO
             SET TECSMSG =
             SET TECLMSG = &ACTION cannot proceed until LOG dataset +
                           disposition is specified.
             ISPEXEC SETMSG MSG(TECZ000)
             ISPEXEC SELECT PGM(ISPLLP) PARM(LOG)
             IF &LASTCC > 0 THEN DO
               SET TECSMSG = &ACTION cancelled
               SET TECLMSG = &ACTION not done because LOG dataset +
                             was not disposed of on the previous +
                             panel.
               ISPEXEC SETMSG MSG(TECZ000)
               EXIT CODE(4)
               END
             END
           END

         SET ZCMD = &STR(=X&ZDEL.=X&ZDEL.=X&ZDEL.=X&ZDEL.=X&ZDEL.=X)
         ISPEXEC CONTROL DISPLAY LOCK
         IF &STR(&TECLMSG) = &STR() +
         THEN ISPEXEC DISPLAY PANEL(VEXIT)
         ELSE ISPEXEC DISPLAY PANEL(VEXIT) MSG(TECZ000)
