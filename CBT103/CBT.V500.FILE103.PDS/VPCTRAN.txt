         PROC 2 TYPE IND$DSN APPEND ADMGDF ASCII CRLF CYLINDERS TRACKS +
                DEBUG LRECL(0) BLKSIZE(0) RECFM(X) SPACE(0) AVBLOCK(0)

         /*-----------------------------------------------------------*/
         /* THIS CLIST IS INVOKED FROM THE COMMAND TABLE WHENEVER     */
         /* "IND$SAVE" IS ENTERED ON THE COMMAND LINE.  THIS CLIST    */
         /* INVOKES THE IND$SAVE COMMAND TO SEND/RECEIVE DATA FROM    */
         /* A PC TO FROM MVS/TSO.  IF THE ADMGDF KEYWORK IS           */
         /* SPECIFIED, THE DATA IS CONVERTED FROM/TO PIF FORMAT,      */
         /* RESPECTIVELY.  THIS CLIST IS BASED ON CLIST ADMUPCFT,     */
         /* WHICH IS DISTRIBUTED WITH GDDM.  THIS CLIST MUST RUN      */
         /* UNDER ISPF.                                               */
         /*-----------------------------------------------------------*/

         CONTROL NOPROMPT
         IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST
         IF &TYPE = GET +
         THEN SET ACTION = RECEIVE
         ELSE IF &TYPE = PUT THEN SET ACTION = TRANSMIT
         ELSE DO
           SET ZEDSMSG = INVALID "TYPE"
           SET ZEDLMSG = CONTACT SYSTEM PROGRAMMER
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT CODE(20)
           END

         /****************************************/
         /* SET UP OPTIONS FOR IND$FILE COMMAND. */
         /****************************************/

         IF &APPEND = APPEND THEN SET OPTIONS = &STR(&OPTIONS APPEND)
         IF &ASCII  = ASCII  THEN SET OPTIONS = &STR(&OPTIONS ASCII)
         IF &CRLF   = CRLF   THEN SET OPTIONS = &STR(&OPTIONS CRLF)
         IF &TRACKS = TRACKS THEN SET OPTIONS = &STR(&OPTIONS TRACKS)
         IF &CYLINDERS = CYLINDERS +
         THEN SET OPTIONS = &STR(&OPTIONS CYLINDERS)
         IF &LRECL ¬= 0 THEN SET OPTIONS = &STR(&OPTIONS LRECL(&LRECL))
         IF &BLKSIZE ¬= 0 +
         THEN SET OPTIONS = &STR(&OPTIONS BLKSIZE(&BLKSIZE))
         IF &RECFM ¬= X THEN SET OPTIONS = &STR(&OPTIONS RECFM(&RECFM))
         IF &SPACE ¬= 0 THEN SET OPTIONS = &STR(&OPTIONS SPACE(&SPACE))
         IF &AVBLOCK ¬= 0 +
         THEN SET OPTIONS = &STR(&OPTIONS AVBLOCK(&AVBLOCK))

         /************************************************************/
         /* IF GDDM IS NOT INVOLVED, JUST ISSUE THE IND$FILE COMMAND */
         /* AND EXIT.                                                */
         /************************************************************/

         IF &ADMGDF = &Z THEN DO
           ISPEXEC SELECT CMD(IND$FILE &TYPE &IND$DSN &OPTIONS)
           SET IND$CC = &LASTCC
           IF &IND$CC = 0 THEN DO
             SET ZEDSMSG = &STR(&ACTION SUCCESSFUL)
             SET ZEDLMSG = +
             &STR(&ACTION OF DATASET &IND$DSN WAS SUCCESSFUL)
             ISPEXEC SETMSG MSG(ISRZ000)
          /* SEND 'PC &TYPE SUCCESSFUL FOR ',USER(S35607),LOGON */
             END
           ELSE DO
             SET ZEDSMSG = &STR(&ACTION FAILED)
             SET ZEDLMSG = +
             &STR(&ACTION OF DATASET &IND$DSN FAILED.  RC = &IND$CC..)
             ISPEXEC SETMSG MSG(ISRZ001)
             END
           EXIT CODE(&IND$CC)
           END

         /**********************************************************/
         /* A FILE IS TO BE CONVERTED BETWEEN GDDM AND PIF FORMAT. */
         /**********************************************************/

         /*--------------------------------------------------*/
         /* ALLOCATE AN INTERMEDIATE DATASET FOR CONVERSION. */
         /*--------------------------------------------------*/

         SET CHECK = &SYSDSN(GDDM.PIF)
         IF &STR(&CHECK) ¬= OK THEN DO
           ALLOC FI(ADMPIF) DSN(GDDM.PIF) NEW CAT SPACE(8 2) TRACKS +
           LRECL(400) RECFM(F B) BLKSIZE(4000) REUSE
           END
         ELSE ALLOC FI(ADMPIF) DSN(GDDM.PIF) SHR REUSE

         /*------------------------------------------*/
         /* GET MEMBER NAME OF DATA TO BE CONVERTED. */
         /*------------------------------------------*/

         SET RP = &STR())
         SET &ILP = &SYSINDEX((,&IND$DSN)
         SET &IRP = &SYSINDEX(&RP,&IND$DSN)
         IF &ILP = 0 OR &IRP = 0 OR &IRP - &ILP > 9 THEN DO
           SET ZEDSMSG = INVALID MEMBER
           SET ZEDLMSG = MEMBER NAME OF &IND$DSN IS MISSING OR INVALID.
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT CODE(16)
           END
         SET PIFMEM = &SUBSTR(&ILP+1:&IRP-1,&IND$DSN)
         SET INDDSN = &SUBSTR(1:&ILP-1,&IND$DSN)
         IF &SUBSTR(1:1,&INDDSN) = &STR(') +
         THEN SET INDDSN = &STR(&INDDSN')

         /*------------------------------------------*/
         /* MAKE SURE SPECIFIED GDDM DATASET EXISTS. */
         /*------------------------------------------*/

         SET CHECK = &SYSDSN(&IND$DSN)
         IF &STR(&CHECK) ¬= OK THEN DO
           IF &TYPE = PUT THEN DO
             IF &STR(&CHECK) = &STR(MEMBER NOT FOUND) +
             THEN ALLOC FI(ADMGDF) DSN(&INDDSN) SHR REUSE
             ELSE ALLOC FI(ADMGDF) DSN(&INDDSN) NEW CAT SPACE(8 2) +
             TRACKS LRECL(400) RECFM(F B) BLKSIZE(4000) DIR(10) REUSE
             END
           ELSE DO
             SET ZEDSMSG = ALLOCATION ERROR
             SET ZEDLMSG = +
             &STR(DATASET &IND$DSN WAS NOT ALLOCATED BECAUSE &CHECK..)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(16)
             END
           END
         ELSE ALLOC FI(ADMGDF) DSN(&INDDSN) SHR REUSE

         /*-------------------------------------------------------*/
         /* A GDDM FILE IS BEING RECEIVED ON THE PC.  CONVERT THE */
         /* FILE TO PIF FORMAT AND RECEIVE IT.                    */
         /*-------------------------------------------------------*/

         IF &TYPE = GET THEN DO
           ISPEXEC SELECT PGM(ADMUPCT) +
           PARM( (&TYPE &PIFMEM )
           SET CONVCC = &LASTCC
           FREE FI(ADMPIF,ADMGDF)
           IF &CONVCC > 0 THEN DO
             SET ZEDSMSG = CONVERSION FAILED
             SET ZEDLMSG = +
             &STR(DATASET &IND$DSN WAS NOT CONVERTED TO PIF FORMAT.)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END
           ELSE DO
             ISPEXEC SELECT CMD(IND$FILE &TYPE GDDM.PIF &OPTIONS)
             SET IND$CC = &LASTCC
             IF &IND$CC = 0 THEN DO
               SET ZEDSMSG = &STR(&ACTION SUCCESSFUL)
               SET ZEDLMSG = +
               &STR(&ACTION AND CONVERSION OF &IND$DSN WAS SUCCESSFUL)
               ISPEXEC SETMSG MSG(ISRZ000)
               END
             ELSE DO
               SET ZEDSMSG = &STR(&ACTION FAILED)
               SET ZEDLMSG = +
               &STR(&ACTION OF DATASET &IND$DSN FAILED.  RC = &IND$CC..)
               ISPEXEC SETMSG MSG(ISRZ001)
               END
             EXIT CODE(&IND$CC)
             END
           END

         /*-----------------------------------------------------*/
         /* A GDDM FILE IS BEING XMITTED FROM THE PC.  TRANSMIT */
         /* THE FILE TO TSO AND CONVERT IT TO GDDM FORMAT.      */
         /*-----------------------------------------------------*/

         ELSE DO
           ISPEXEC SELECT +
           CMD(IND$FILE &TYPE GDDM.PIF &OPTIONS)
           SET IND$CC = &LASTCC
           IF &IND$CC > 0 THEN DO
             SET ZEDSMSG = &STR(&ACTION FAILED)
             SET ZEDLMSG = +
             &STR(&ACTION OF DATASET &IND$DSN FAILED.  RC = &IND$CC..)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(&IND$CC)
             END
           ISPEXEC SELECT PGM(ADMUPCT) +
           PARM( (&TYPE &PIFMEM)
           SET CONVCC = &LASTCC
           FREE FI(ADMPIF,ADMGDF)
           IF &CONVCC > 0 THEN DO
             SET ZEDSMSG = CONVERSION FAILED
             SET ZEDLMSG = +
             &STR(DATASET &IND$DSN WAS NOT CONVERTED TO PIF FORMAT.)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END
           ELSE DO
             SET ZEDSMSG = &STR(&ACTION SUCCESSFUL)
             SET ZEDLMSG = +
             &STR(&ACTION AND CONVERSION OF &IND$DSN WAS SUCCESSFUL)
             ISPEXEC SETMSG MSG(ISRZ000)
             END
           END

         EXIT CODE(0)
