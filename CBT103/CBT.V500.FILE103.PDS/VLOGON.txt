//VLOGON   JOB (NNNNN,8),'BILL HORTON',CLASS=A,NOTIFY=XXXXXx
//*-----------------------------------------------------------------***
//*                                                                 ***
//* LIBRARY: xxxxxxxxxxxxxxxx                                       ***
//* MEMBER:  VLOGON                                                 ***
//*                                                                 ***
//* PURPOSE:                                                        ***
//* INSTALL VLOGON PROGRAM (UPDATE TSO LOGON INFO DIALOG)           ***
//*                                                                 ***
//* STRINGS TO BE CHANGED BEFORE SUBMISSION ARE:                    ***
//*                                                                 ***
//* USERID   <=== YOUR USER ID                                      ***
//* PNAME    <=== YOUR NAME                                         ***
//*                                                                 ***
//*-----------------------------------------------------------------***
//       EXEC ASMHCL,PARM.ASM='TERM,RENT',
//            PARM.LKED='LIST,MAP,RENT,REUS'
         TITLE 'VLOGON - ISPF DIALOG TO UPDATE TEC LOGON INFO'
VLOGON   CSECT
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9            POINTER TO DYNAMIC AREA
R10      EQU   10           BASE REGISTER
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*---------------------------------------------------------------------*
*                                                                     *
* THIS PROGRAM IS AN ISPF/PDF DIALOG FOR DISPLAYING/UPDATING TEC      *
* LOGON INFORMATION.  THIS INFO IS KEPT IN THE RACF DATASET; THE      *
* PROGRAMMER NAME IN THE PGMRNAME FIELD AND THE OTHER FIELDS IN THE   *
* USERDATA FIELD.                                                     *
*                                                                     *
* USER ABEND CODES:                                                   *
*                   0001 - BAD RETURN FROM RACF LOCATE FUNCTION       *
*                   0002 - BAD RETURN FROM RACF ALTER FUNCTION        *
*                          (REGISTERS 5 AND 6 HOLD REGISTER 15 AND    *
*                           0 CONTENTS AFTER USER ABENDS)             *
*                                                                     *
*---------------------------------------------------------------------*
         SAVE  (14,12),,VLOGON._&SYSDATE._&SYSTIME
         LR    R10,R15
         USING VLOGON,R10
         L     R0,LDYNAREA              LOAD LENGTH OF DYNAMIC AREA.
         GETMAIN R,LV=(R0)              GET DYNAMIC AREA.
         LR    R9,R1                    SAVE ADDRESS.
         LR    R2,R9                    POINT TO DYNAMIC AREA.
         LR    R3,R0                    GET LENGTH.
         LA    R4,0                     ZERO.
         LA    R5,0                     PADDING BYTE AND LENGTH.
         MVCL  R2,R4                    CLEAR DYNAMIC AREA.
         USING DYNAREA,R9               ADDRESSIBILITY.
         LA    R2,SAVEAREA
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
*
* INITIALIZE WORK AREA.
*
         MVC   ACCOUNT,BLANKS
         MVC   CURSOR,BLANKS
         LA    R3,=A(WEND-WORKAREA)     GET LENGTH OF WORKAREA.
         ST    R3,WLENGTH
         PUSH  PRINT
         PRINT NOGEN
         ICHEACTN FIELD=PGMRNAME,FLDATA=(20,RNAME),MF=(E,LUPD1)
         MVI   LUPD1+1,X'01'            SET NUMBER OF FIELDS BYTE.
         ICHEACTN FIELD=TACCNT,FLDATA=(40,LLACCT),MF=(E,LUPD2)
         MVI   LUPD2+1,X'01'            SET NUMBER OF FIELDS BYTE.
         ICHEACTN FIELD=TLPROC,FLDATA=(8,LLPROC),MF=(E,LUPD3)
         MVI   LUPD3+1,X'01'            SET NUMBER OF FIELDS BYTE.
         ICHEACTN FIELD=INSTDATA,FLDATA=(4,LLSTA),MF=(E,LUPD4)
         MVI   LUPD4+1,X'01'            SET NUMBER OF FIELDS BYTE.
         MVC   RRP1(ERRP0-RRP0),RRP0    COPY RACROUTE MACRO FOR TSOPROC
         MVC   RRA1(ERRA0-RRA0),RRA0    COPY RACROUTE MACRO FOR TACCNT
         L     R3,16                    POINT TO CVT.
         L     R3,0(R3)                 POINT TO TCB CHAIN.
         L     R3,4(R3)                 GET CURRENT TCB.
         L     R3,12(R3)                POINT TO TIOT (JOBNAME).
         TRT   0(8,R3),BLANKTAB         FIND FIRST BLANK.
         SR    R1,R3                    GET LENGTH OF USERID.
         STC   R1,LUSERID               SAVE LENGTH OF USERID.
         MVC   USERID,0(R3)             COPY USERID.
*
* SET UP IPSF ENVIRONMENT
*
         LOAD  EP=ISPLINK               LOAD ISPLINK ROUTINE.
         ST    R0,@ISPLINK              SAVE.
         DELETE EP=ISPLINK              GET RID OF ISPF MODULE.
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#XNAME,XNAME,#C,F20),VL,MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#XACCT,XACCT,#C,F5),VL,MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#XPROC,XPROC,#C,F8),VL,MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#XCMD,XCMD,#C,F80),VL,MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#XSTA,XSTA,#C,F4),VL,MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#TECSMSG,TECSMSG,#C,F24),VL,             X
               MF=(E,VDEFINE)
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDEFINE,#TECLMSG,TECLMSG,#C,F78),VL,             X
               MF=(E,VDEFINE)
         POP   PRINT
         LA    R1,PASSWORD              POINT TO PASSWORD.
         LA    R15,3                    REQUEST AUTHORIZATION.
         SVC   255
         LA    R2,LUSERID               POINT TO LENGTH OF USERID.
         LA    R3,WORKAREA              POINT TO WORK AREA.
         MVC   LACCT(3),=X'002928'      SPECIFY LENGTH.
         MVC   LPROC(3),=X'000908'      SPECIFY LENGTH.
         MVC   LSTA(3),=X'000504'       SPECIFY LENGTH.
         MVI   RACCT,C' '               BLANK OUT.
         MVC   RACCT+1(39),RACCT        ACCOUNT FIELD.
         ICHEINTY LOCATE,TYPE='USR',ENTRY=(R2),WKAREA=(R3),            X
               ACTIONS=(ACT1,ACT2,ACT3,ACT4),                          X
               OPTIONS=(FLDEF,ACTION),MF=(E,LOCATE)
         LR    R5,R15                   SAVE RETURN CODE.
         LR    R6,R0                    ADDITIONAL CODES.
         LA    R15,4                    REQUEST UNAUTHORIZATION.
         SVC   255
         LTR   R5,R5                    GOOD RETURN FROM RACF ?
         BZ    GETOK                    YES, CONTINUE.
         MVC   TECSMSG,=CL24'Read from RACF failed'
         MVC   TECLMSG,=CL78'Unable to read current LOGON data from RACX
               F dataset'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     RETURN
GETOK    MVC   XNAME,RNAME              COPY FIELDS.
         MVC   XACCT,RACCT
         MVC   XPROC,RPROC
         SR    R1,R1
         USING PSA,R1
         L     R2,PSAANEW
         USING ASCB,R2
         L     R3,ASCBASXB
         USING ASXB,R3
         L     R4,ASXBLWA
         DROP  R1,R2,R3
         USING LWA,R4
         MVC   XCMD,LWALGCMD
         MVC   XSTA,RSTA
*
*        DISPLAY FIRST PANEL
*
DVPANEL  DS    0H
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#DISPLAY,#VPANL,BLANKS,CURSOR),VL,MF=(E,DISPLAY)
         LTR   R15,R15                  TEST RETURN CODE.
         BNZ   RETURN                   RC > 0, GO HOME.
         MVC   CURSOR,BLANKS            RESET NAME OF CURSOR FIELD.
         MVI   CHSWITCH,C'N'            SET "NO CHANGE".
*
*        SEE IF THE NAME HAS CHANGED
*
         CLC   XNAME,RNAME              HAS XNAME CHANGED ?
         BE    CHKACCT                  NO, CONTINUE
         MVI   CHSWITCH,C'Y'            SET "CHANGE".
*
*        SEE IF THE ACCOUNT CODE HAS CHANGED
*
CHKACCT  CLC   XACCT,RACCT              HAS XACCT CHANGED ?
         BE    CHKPROC                  NO, CONTINUE
         MVI   CHSWITCH,C'Y'            SET "CHANGE".
         MVC   ACCOUNT(5),XACCT
         RACROUTE REQUEST=AUTH,ENTITY=ACCOUNT,WORKA=RACWORK,MF=(E,RRA1)
         CH    R15,=H'0'                IS ACCOUNT CODE OK ?
         BE    CHKPROC                  YES, CONTINUE
         MVC   CURSOR,=CL9'XACCT'
         CH    R15,=H'4'                "RESOURCE NOT PROTECTED" ?
         BNE   NOACCT                   NO, CONTINUE
         MVC   TECSMSG,=CL24'Account code not defined'
         MVC   TECLMSG,=CL78'Call extension 3089 or 3090 (3601 after 5 X
               p.m.) for further information'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
NOACCT   CH    R15,=H'8'                "USER NOT AUTHORIZED" ?
         BNE   BADRACF1                 NO, CONTINUE
         MVC   TECSMSG,=CL24'User unauthorized'
         MVC   TECLMSG,=CL78'You are not authorized to use this accountX
                code'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
BADRACF1 MVC   TECSMSG,=CL24'Logic error'
         MVC   TECLMSG,=CL78'RACROUTE for account validation failed'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
CHKPROC  CLC   XPROC,RPROC              HAS XPROC CHANGED ?
         BE    CHKSTA                   NO, CONTINUE
         MVI   CHSWITCH,C'Y'            SET "CHANGE".
         RACROUTE REQUEST=AUTH,ENTITY=XPROC,WORKA=RACWORK,MF=(E,RRP1)
         CH    R15,=H'0'                IS PROC NAME OK ?
         BE    CHKSTA                   YES, CONTINUE
         MVC   CURSOR,=CL9'XPROC'
         CH    R15,=H'4'                "RESOURCE NOT PROTECTED" ?
         BNE   NOPROC                   NO, CONTINUE
         MVC   TECSMSG,=CL24'Proc name not defined'
         MVC   TECLMSG,=CL78'Contact the TSO system programmer for a liX
               st of valid logon procedures'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
NOPROC   CH    R15,=H'8'                "USER NOT AUTHORIZED" ?
         BNE   BADRACF2                 NO, CONTINUE
         MVC   TECSMSG,=CL24'User unauthorized'
         MVC   TECLMSG,=CL78'You are not authorized to use this TSO logX
               on procedure'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
BADRACF2 MVC   TECSMSG,=CL24'Logic error'
         MVC   TECLMSG,=CL78'RACROUTE for procedure validation failed'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     DVPANEL
CHKSTA   CLC   XSTA,RSTA                HAS XSTA CHANGED ?
         BE    CHKMOD                   NO, CONTINUE
         MVI   CHSWITCH,C'Y'            SET "CHANGE".
CHKMOD   CLI   CHSWITCH,C'N'            "NO CHANGE" ?
         BE    CHKCMD                   YES, CONTINUE.
*
*        ONE OF THE RACF FIELDS HAS CHANGED.  UPDATE RACF DATASET.
*
         MVC   RNAME,XNAME              UPDATE FIELDS.
         MVC   RACCT,XACCT
         MVC   RPROC,XPROC
         MVC   RSTA,XSTA
         TR    RNAME,TTABLE             TRANSLATE OUT BAD CHARS
         TR    RACCT,TTABLE             TRANSLATE OUT BAD CHARS
         TR    RPROC,TTABLE             TRANSLATE OUT BAD CHARS
         TR    RSTA(4),TTABLE           BAD CHARACTERS.
         LA    R1,PASSWORD              POINT TO PASSWORD.
         LA    R15,3                    REQUEST AUTHORIZATION.
         SVC   255
         LA    R2,LUSERID               POINT TO LENGTH OF USERID.
         ICHEINTY ALTER,TYPE='USR',ENTRY=(R2),                         X
               ACTIONS=(LUPD1,LUPD2,LUPD3,LUPD4),                      X
               OPTIONS=(FLDEF,ACTION),MF=(E,ALTER)
         LR    R5,R15                   SAVE RETURN CODE.
         LR    R6,R0                    ADDITIONAL CODES.
         LA    R15,4                    REQUEST UNAUTHORIZATION.
         SVC   255
         LTR   R5,R5                    GOOD RETURN FROM RACF ?
         BZ    CHKCMD                   YES, CONTINUE.
         MVC   TECSMSG,=CL24'Write to RACF failed'
         MVC   TECLMSG,=CL78'Unable to write new logon data to RACF datX
               aset'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ001),VL,MF=(E,VDELETE)
         B     RETURN
*
*        SEE IF LOGON COMMAND HAS CHANGED.
*
CHKCMD   CLC   XCMD,LWALGCMD            IS XCMD UNCHANGED ?
         BE    DVPANEL                  YES, CONTINUE
         LA    R1,PASSWORD              POINT TO PASSWORD.
         LA    R15,3                    REQUEST AUTHORIZATION.
         SVC   255
         MODESET KEY=ZERO
         MVC   LWALGCMD,XCMD            COPY IN NEW COMMAND.
         MODESET KEY=NZERO
         LA    R15,4                    REQUEST UNAUTHORIZATION.
         SVC   255
UPDOK    MVC   TECSMSG,=CL24'Logon data updated'
         MVC   TECLMSG,=CL78'New values will take effect upon your nextX
                logon to TSO'
         L     R15,@ISPLINK
         CALL  (15),(#SETMSG,#TECZ000),VL,MF=(E,VDELETE)
         B     DVPANEL
PASSWORD DC    C'AUTH'
         EJECT
*
*        PROCESSING COMPLETED.
*
RETURN   DS    0H
         L     R15,@ISPLINK             GET ISPLINK ADDRESS.
         CALL  (15),(#VDELETE,#ALL),VL,MF=(E,VDELETE)
RET      DS    0H
         L     R13,SAVEAREA+4
         L     R0,LDYNAREA              FREE
         FREEMAIN R,LV=(R0),A=(R9)      DYNAMIC AREA.
         LA     R15,0                   SET RETURN CODE.
         RETURN (14,12),,RC=15          RETURN TO CALLER.
LDYNAREA DC    A(EDYNAREA-DYNAREA)
ACT1     ICHEACTN FIELD=PGMRNAME
PGMRNAME EQU   *-8
ACT2     ICHEACTN FIELD=TACCNT
TACCNT   EQU   *-8
ACT3     ICHEACTN FIELD=TLPROC
TLPROC   EQU   *-8
ACT4     ICHEACTN FIELD=INSTDATA
INSTDATA EQU   *-8
RRA0     RACROUTE REQUEST=AUTH,CLASS='ACCTNUM',MF=L
ERRA0    EQU   *
RRP0     RACROUTE REQUEST=AUTH,CLASS='TSOPROC',MF=L
ERRP0    EQU   *
F4       DC    F'4'
F5       DC    F'5'
F8       DC    F'8'
F20      DC    F'20'
F24      DC    F'24'
F78      DC    F'78'
F80      DC    F'80'
BLANKS   DC    CL80' '
*
* DEFINE ISPF FUNCTION NAMES
*
#VDEFINE DC    C'VDEFINE '
#VDELETE DC    C'VDELETE '
#DISPLAY DC    C'DISPLAY '
#C       DC    C'CHAR '
#VPANL   DC    C'VLOGON '
#SETMSG  DC    C'SETMSG'
#ALL     DC    C'* '
*
* DEFINE ISPF DIALOG VARIABLES
*
#XNAME   DC    C'XNAME '
#XACCT   DC    C'XACCT '
#XPROC   DC    C'XPROC '
#XCMD    DC    C'XCMD '
#XSTA    DC    C'XSTA  '
#TECSMSG DC    C'TECSMSG '
#TECLMSG DC    C'TECLMSG '
#TECZ000 DC    C'TECZ000 '
#TECZ001 DC    C'TECZ001 '
*                0123456789ABCDEF
TTABLE   DC    C'                ' 0
         DC    C'                ' 1
         DC    C'                ' 2
         DC    C'                ' 3
         DC    C'           .<(+|' 4
         DC    C'          !$*);Â¬' 5
         DC    C'-/         ,%_>?' 6
         DC    C'          :#@''="' 7
         DC    C' ABCDEFGHI      ' 8
         DC    C' JKLMNOPQR      ' 9
         DC    C'  STUVWXYZ      ' A
         DC    C'                ' B
         DC    C' ABCDEFGHI      ' C
         DC    C' JKLMNOPQR      ' D
         DC    C'  STUVWXYZ      ' E
         DC    C'0123456789      ' F
BLANKTAB DC    XL256'00'
         ORG   BLANKTAB+X'40'
         DC    X'40'
         ORG
         LTORG ,
         EJECT
DYNAREA  DSECT
SAVEAREA DS    18F
DISPLAY  CALL  ,(,,),VL,MF=L
VDEFINE  CALL  ,(,,,,),VL,MF=L
VDELETE  CALL  ,(,),VL,MF=L
LUPD1    ICHEACTN FIELD=PGMRNAME,FLDATA=(20,LUPD1),MF=L
LUPD2    ICHEACTN FIELD=TACCT,FLDATA=(20,LUPD2),MF=L
LUPD3    ICHEACTN FIELD=TLPROC,FLDATA=(20,LUPD3),MF=L
LUPD4    ICHEACTN FIELD=INSTDATA,FLDATA=(4,LUPD4),MF=L
LOCATE   ICHEINTY LOCATE,ACTIONS=(LUPD1,LUPD2,LUPD3,LUPD4),MF=L
ALTER    ICHEINTY ALTER,ACTIONS=(LUPD1,LUPD2,LUPD3,LUPD4),MF=L
RRA1     RACROUTE REQUEST=AUTH,CLASS='ACCTNUM',MF=L
RRP1     RACROUTE REQUEST=AUTH,CLASS='TSOPROC',MF=L
HALF1    DS    D
@ISPLINK DS    F                        ADDRESS OF ISPLINK ROUTINE.
*
* ISPF/PDF DISPLAY VARIABLE STORAGE.
*
XNAME    DS    CL20                    /* PROGRAMMER NAME            */
XACCT    DS    CL5                     /* ACCOUNTING CODE            */
XPROC    DS    CL8                     /* LOGON PROCEDURE NAME       */
XCMD     DS    CL80                    /* INITIAL TSO COMMAND        */
XSTA     DS    CL4                     /* MAIL STATION               */
TECSMSG  DS    CL24                    /* SHORT MESSAGE              */
TECLMSG  DS    CL78                    /* LONG  MESSAGE              */
LUSERID  DS    C
USERID   DS    CL7
CURSOR   DS    CL9                     /* NAME OF FIELD FOR CURSOR   */
CHSWITCH DS    C                       /* "N": NO CHANGE "Y": CHANGE */
ACCOUNT  DS    CL40                    /* ACCOUNT CODE               */
WORKAREA DS    0F                      /* WORK AREA FOR ICHEACTN     */
WLENGTH  DS    A                       /* LENGTH OF WORK AREA        */
WRBA     DS    CL6                     /* RBA RETURN AREA            */
WFLAGS   DS    C                       /* FLAGS                      */
WRES1    DS    C                       /* RESERVED                   */
WDDSNC   DS    CL4                     /* DUPL. DATASET NAME COUNT   */
WRES2    DS    CL8                     /* RESERVED                   */
WDATALEN DS    CL4                     /* LENGTH OF RETURNED DATA    */
         DS    CL2                     /* LENGTH OF PGMRNAME FIELD   */
RNAME    DS    CL20                    /* PROGRAMMER NAME            */
LACCT    DS    CL2                     /* LENGTH OF ACCOUNT FIELD    */
LLACCT   DS    C                       /* LENGTH OF DATA IN FIELD    */
RACCT    DS    CL5                     /* ACCOUNT CODE               */
         DS    CL35                    /* REST OF ACCOUNT CODE FIELD */
LPROC    DS    CL2                     /* LENGTH OF PROC NAME FIELD  */
LLPROC   DS    C                       /* LENGTH OF DATA IN FIELD    */
RPROC    DS    CL8                     /* NAME OF LOGON PROCEDURE    */
LSTA     DS    CL2                     /* LENGTH OF INSTDATA FIELD   */
LLSTA    DS    C                       /* LENGTH OF DATA IN FIELD    */
RSTA     DS    CL4                     /* MAIL STATION               */
         DS    CL247                   /* RESERVED                   */
         DS    F                       /*                            */
WEND     EQU   *                       /* END OF WORK AREA           */
RACWORK  DS    CL512                   /* RACROUTE WORK AREA         */
EDYNAREA EQU   *
         IHAPSA
         IHAASCB
         IHAASXB
         IKJEFLWA
         END
/*
//LKED.SYSLMOD DD DSN=SYS1.LPALIB2(VLOGON),DISP=SHR
