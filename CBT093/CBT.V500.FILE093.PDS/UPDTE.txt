         PRINT   GEN                                                    00001
*                                                                       00002
*  PROGRAM NAME - UPDTE                                                 00003
*                                                                       00004
*  FUNCTION     - RECREATE A PDS FROM A SEQUENTIAL DATASET              00005
*                                                                       00006
*  FILES:                                                               00007
*    SYSPRINT   - MESSAGES, INFORMATIONAL AND DIAGNOSTIC                00008
*    SYSUT2     - OUTPUT PDS. MEMBERS ARE REPLACED IF THEY ALREADY      00009
*                 EXIST, OTHERWISE THEY ARE ADDED. OLD MEMBERS ARE      00010
*                 NOT DELETED IF NOT REPLACED.                          00011
*    SYSIN      - AN INPUT STREAM THAT LOOKS LIKE THAT FOR AN IEBUPDTE  00012
*                 RECREATION OF A LIBRARY. HOWEVER, VARIABLE LENGTH     00013
*                 RECORDS ARE SUPPORTED AS WELL AS NON-80 BYTE FIXED    00014
*                 LENGTH RECORDS.                                       00015
*                                                                       00016
*  RESTRICTIONS - THE RECORD FORMATS OF SYSIN AND SYSUT2 MUST BE THE    00017
*                 SAME, AND THE BLOCKSIZE OF SYSUT2 MUST BE GREATER     00018
*                 THAN THE LRECL OF SYSIN. RENUMBERING IS NOT SUPPORTED 00019
*                                                                       00020
*  UPDATE BY R. E. STYMA (GTE NETWORK SYSTEMS - AUT) 08/04/83           00021
*                 CHANGE TO ./ ADD CARD SCANNING CODE TO PROPERLY       00022
*                 HANDLE VB RECORDS WHICH HAVE BEEN EDITED AND HAD      00023
*                 THE TRAILING BLANKS REMOVED                           00024
*                                                                       00025
*                                                                       00026
*                                                                       00027
*                                                                       00028
*                                                                       00029
UPDTE    CSECT                                                          00030
         USING UPDTE,R15                                                00031
         SAVE  (14,12),,*                                               00032
         LA    R2,SAVE001              *SET UP SAVEAREA LINKAGE         00033
         ST    R13,SAVE001+4           *                                00034
         ST    R2,8(,R13)              *                                00035
         LR    R13,R2                  *                                00036
         DROP  R15                                                      00037
         USING SAVE001,R13                                              00038
         B     SAVE001A                                                 00039
SAVE001  DS    18F                                                      00040
SAVE001A EQU   *                                                        00041
         OPEN  (SYSPRINT,(OUTPUT),SYSUT2,(OUTPUT),SYSIN,(INPUT))        00042
         LA    R12,SYSUT2              THE OUTPUT DCB ADDRESS           00043
         USING IHADCB,R12              TELL THE ASSEMBLER WHERE IT IS   00044
         LA    R7,SYSIN                WE NEED THIS FOR F/FB TYPES      00045
         MVC   ALLDONE,DCBRECFM        CHECK OUT ERROR CONDITIONS       00046
         NI    ALLDONE,X'C0'           ALL BUT RECFM BITS               00047
         TM    ALLDONE,X'C0'           IS IT A VALID RECFM?             00048
         BNM   ALLFAIL4                IF RECFM U, HE BLEW IT           00049
         NC    ALLDONE,DCBRECFM-IHADCB(R7)                              00050
         BZ    ALLFAIL5                IF NOT THE SAME, HE BLEW IT      00051
         TM    ALLDONE,X'80'           IS IT FB?                        00052
         BZ    CM0                     BRANCH IF NOT                    00053
         MVC   HLDBLKSI,DCBBLKSI  SAVE THE MAXIMUM BLOCK FOR LATER      00054
         CLC   DCBLRECL,DCBLRECL-IHADCB(R7)                             00055
         BE    CM0                     IF THEY ARE THE SAME, NO MSG     00056
         PUT   SYSPRINT,WMSG1          WARN HIM                         00057
CM0      DS    0H                                                       00058
         MVI   ALLDONE,X'00'           CLEAR THE FLAG FOR EOF TEST      00059
         LM    R10,R11,OUTPARMS        INITIALIZE THE OUTPUT LOOP       00060
         GET   SYSIN                                                    00061
HEADER   DS    0H                                                       00062
         TM    DCBRECFM-IHADCB(R7),X'80'                                00063
         BZ    VB1                                                      00064
         LA    R2,7(,R1)               1ST POSSIBLE ADDRESS FOR 'NAME=' 00065
         LH    R3,DCBLRECL-IHADCB(,R7) MAXIMUM NUM OF STARTS FOR NAME=  00066
         S     R3,=F'12'               12 = 7(./ADD) + 5(NAME=)         00067
         B     CM1                                                      00068
VB1      DS    0H                                                       00069
         LA    R2,11(,R1)              1ST POSSIBLE ADDRESS FOR 'NAME=' 00070
         LH    R3,0(,R1)               MAXIMUM NUM OF STARTS FOR NAME=  00071
         S     R3,=F'16'               16 = 7(./ADD) + 4(RDW) +5(NAME=) 00072
CM1      DS    0H                                                       00073
FINDNAME DS    0H                                                       00074
         CLC   0(5,R2),=C'NAME='       IS THIS THE PLACE?               00075
         BE    FOUNDNM                 BRANCH IF SO                     00076
         LA    R2,1(,R2)               ELSE TRY NEXT PLACE              00077
         BCT   R3,FINDNAME             *                                00078
ALLFAIL3 DS    0H                                                       00079
         PUT   SYSPRINT,MSG3           WE DIDN'T FIND NAME=             00080
         ABEND 3333,DUMP                                                00081
FOUNDNM  DS    0H                                                       00082
         C     R3,=F'8'                IS THE REMAINDER GREATER THAN 8  00083
         BNH   SKIP1                   IF NOT, USE THIS VALUE           00084
         SPACE                                                          00085
         LA    R3,8                    IF SO, USE 8 AS THE MAX LEN      00086
         SPACE                                                          00087
SKIP1    DS    0H                                                       00088
         LA    R2,5(,R2)               THE ADDRESS OF THE MEMBER NAME   00089
         LA    R4,0(,R2)               FIRST PLACE TO LOOK FOR DELIM    00090
SCANNAME DS    0H                                                       00091
         CLI   0(R4),C' '              IS IT A DELIMITER?               00092
         BE    SCANEND                 *                                00093
         CLI   0(R4),C','              *                                00094
         BE    SCANEND                 *                                00095
         LA    R4,1(,R4)               TRY THE NEXT CHARACTER           00096
         BCT   R3,SCANNAME             *                                00097
SCANEND  DS    0H                                                       00098
         SR    R4,R2                   COMPUTE LENGTH AND MOVE NAME     00099
         BCTR  R4,0                    *                                00100
         MVC   MEMBNAME,=CL8' '        *                                00101
         EX    R4,MOVEMNM              *                                00102
         NOTE  SYSUT2                  *                                00103
         ST    R1,MEMBADDR             *                                00104
GETNEXT  DS    0H                                                       00105
         GET   SYSIN                   MAIN LOOP START                  00106
         TM    DCBRECFM-IHADCB(R7),X'80'                                00107
         BZ    VB2                                                      00108
         CLC   0(7,R1),=C'./ ADD '     IS IT ANOTHER HEADER???          00109
         B     CM2                                                      00110
VB2      DS    0H                                                       00111
         CLC   4(7,R1),=C'./ ADD '     IS IT ANOTHER HEADER???          00112
CM2      DS    0H                                                       00113
         BE    STOWIT                  BRANCH IF IT IS                  00114
         BAL   R9,PUTIT                PUT THE RECORD IN OUTPUT BLOCK   00115
         B     GETNEXT                 AND GO AROUND FOR NEXT INPUT     00116
STOWIT   DS    0H                                                       00117
         LR    R2,R1              SAVE THE RECORD ADDRESS               00118
         BAL   R8,WRITEBLK        WRITE THE LAST BLOCK                  00119
         STOW  SYSUT2,MEMBNAME,R                                        00120
         B     *+4(R15)                TEST RETURN CODE                 00121
         B     STOWEDR                 STOWED AS A REPLACE              00122
         B     ALLFAIL1                ERROR CONDITION                  00123
         B     STOWEDA                 STOWED AS AN ADD                 00124
         B     ALLFAIL1                ERROR CONDITION                  00125
         B     ALLFAIL1                ERROR CONDITION                  00126
         B     ALLFAIL1                ERROR CONDITION                  00127
         B     ALLFAIL1                ERROR CONDITION                  00128
STOWEDR  DS    0H                                                       00129
         MVC   STOWMODE,=CL8'REPLACED'                                  00130
         B     ENDTEST                                                  00131
STOWEDA  DS    0H                                                       00132
         MVC   STOWMODE,=CL8'ADDED'                                     00133
ENDTEST  DS    0H                                                       00134
         MVC   OUTMEM,MEMBNAME         BUILD AND PRINT A MESSAGE        00135
         UNPK  OUTTTR,MEMBADDR         *                                00136
         NC    OUTTTR,=7X'0F'          *                                00137
         TR    OUTTTR,=C'0123456789ABCDEF'                              00138
         MVI   OUTTTR+6,C' '           *                                00139
         PUT   SYSPRINT,OUTLINE        *                                00140
         CLI   ALLDONE,1               ARE WE FINISHED?                 00141
         LR    R1,R2              RESTORE THE RECORD ADDRESS            00142
         BNE   HEADER                                                   00143
         PUT   SYSPRINT,ALLDNMSG                                        00144
         CLOSE (SYSIN,,SYSUT2,,SYSPRINT)                                00145
         L     R13,4(,R13)             *GO BACK TO THE CALLER           00146
         LM    R14,R12,12(R13)         *                                00147
         SR    R15,R15                 *                                00148
         BR    R14                     *                                00149
ALLFAIL1 DS    0H                                                       00150
         PUT   SYSPRINT,MSG1           PRINT AN INDICATION              00151
         ABEND 1111,DUMP                                                00152
EOF      DS    0H                                                       00153
         MVI   ALLDONE,1               REMEMBER WE'RE ALL DONE          00154
         B     STOWIT                                                   00155
PUTIT    DS    0H                                                       00156
         LTR   R10,R10                 IS IT THE FIRST TIME?            00157
         BNM   NOT1ST                  *                                00158
         LH    R10,DCBBLKSI            INITIALIZE THE LOOP ELEMENTS     00159
         L     R11,=A(OUTBLOCK)        *                                00160
         TM    DCBRECFM,X'80'          *                                00161
         BO    CM3                     *                                00162
         S     R10,=F'4'               *                                00163
         LA    R11,4(,R11)             *                                00164
CM3      DS    0H                                                       00165
NOT1ST   DS    0H                                                       00166
         TM    DCBRECFM,X'80'                                           00167
         BZ    VB4                                                      00168
         CH    R10,DCBLRECL            IS THERE ENOUGH ROOM?            00169
         BNL   MOVEIN                  BRANCH IF THERE IS               00170
         B     CM4                                                      00171
VB4      DS    0H                                                       00172
         CH    R10,0(,R1)              IS THERE ENOUGH ROOM             00173
         BNL   MOVEIN                  BRANCH IF THERE IS               00174
         CLC   DCBBLKSI,0(R1)          COULD THERE BE ENOUGH ROOM?      00175
         BL    ALLFAIL2                BRANCH IF NOT                    00176
CM4      DS    0H                                                       00177
         LA    R8,MOVEIN               NORMAL USAGE                     00178
WRITEBLK DS    0H                                                       00179
         L     R11,=A(OUTBLOCK)        BEGINNING OF THE BLOCK           00180
         LR    R2,R1                   SAVE THE RECORD ADDRESS          00181
         LCR   R10,R10                 THE AMOUNT LEFT UNUSED           00182
         AH    R10,DCBBLKSI            COMPUTE THE AMOUNT USED          00183
         TM    DCBRECFM,X'80'                                           00184
         BZ    VB5                                                      00185
         STH   R10,DCBBLKSI            CALL IT BLOCK SIZE               00186
         WRITE DECBFB,SF,SYSUT2,(R11),'S'                               00187
         CHECK DECBFB                                                   00188
         MVC   DCBBLKSI,HLDBLKSI       RESTORE THE ORIGINAL BLKSIZE     00189
         B     CM5                                                      00190
VB5      DS    0H                                                       00191
         STH   R10,0(,R11)             AND CALL IT BLOCK SIZE           00192
         WRITE DECB,SF,SYSUT2,(R11)                                     00193
         CHECK DECB                    MAKE SURE THE RECORD IS OUT OK   00194
CM5      DS    0H                                                       00195
         LH    R10,DCBBLKSI            RE-INITIALIZE THE LOOP           00196
         L     R11,=A(OUTBLOCK)        *                                00197
         TM    DCBRECFM,X'80'          *                                00198
         BO    CM6                     *                                00199
         S     R10,=F'4'               *                                00200
         LA    R11,4(,R11)             *                                00201
CM6      DS    0H                                                       00202
         LR    R1,R2                   RESTORE THE RECORD ADDRESS       00203
         BR    R8                      GO BACK TO USER                  00204
MOVEIN   DS    0H                                                       00205
         LR    R2,R1                   SET UP THE FOUR REGS FOR MVCL    00206
         LR    R4,R11                  *                                00207
         TM    DCBRECFM,X'80'          *                                00208
         BO    FB7                     *                                00209
         LH    R3,0(,R2)               *                                00210
         LR    R5,R3                   *                                00211
         LR    R15,R5                  SAVE THE LENGTH                  00212
         B     CM7                     *                                00213
FB7      DS    0H                      *                                00214
         LH    R3,DCBLRECL-IHADCB(R7)  *                                00215
         LH    R5,DCBLRECL             *                                00216
         LR    R15,R5                  SAVE THE LENGTH                  00217
         O     R5,=X'40000000'         *                                00218
CM7      DS    0H                      *                                00219
         MVCL  R4,R2                   PUT THE RECORD IN THE OUTPUT     00220
         AR    R11,R15                 ADJUST THE LOOP REGISTERS        00221
         SR    R10,R15                 *                                00222
         BR    R9                      GO BACK TO THE MAIN ROUTINE      00223
ALLFAIL2 DS    0H                                                       00224
         PUT   SYSPRINT,MSG2           SOME INDICATION                  00225
         ABEND 2222,DUMP                                                00226
ALLFAIL4 DS    0H                                                       00227
         PUT   SYSPRINT,MSG4                                            00228
         ABEND 444,DUMP                                                 00229
ALLFAIL5 DS    0H                                                       00230
         PUT   SYSPRINT,MSG5                                            00231
         ABEND 555,DUMP                                                 00232
SYSPRINT DCB   BLKSIZE=121,LRECL=121,RECFM=FBA,DDNAME=SYSPRINT,        *00233
               DSORG=PS,MACRF=PM                                        00234
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,EODAD=EOF,MACRF=GL                 00235
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=W                           00236
OUTPARMS DC    F'-1,-1'                                                 00237
MEMBNAME DC    CL8' '                                                   00238
MEMBADDR DC    F'0'                                                     00239
MOVEMNM  MVC   MEMBNAME(0),0(R2)                                        00240
OUTLINE  DC    CL121' '                                                 00241
         ORG   OUTLINE+1                                                00242
         DC    C'MEMBER NAME '                                          00243
OUTMEM   DC    CL8' '                                                   00244
         DC    C'   TTR '                                               00245
OUTTTR   DC    CL7' '                                                   00246
         DC    C'    '                                                  00247
STOWMODE DC    CL8' '                                                   00248
         ORG                                                            00249
WMSG1    DC    CL121' WARNING: TRUNCATION OR PADDING OF RECORDS WILL OC*00250
               CUR BECAUSE LRECL''S ARE NOT THE SAME'                   00251
HLDBLKSI DC    H'0'                                                     00252
MSG1     DC    CL121'-BAD RETURN CODE FROM STOW. SEE R15 IN DUMP'       00253
MSG2     DC    CL121'-BAD LRECL/BLKSIZE MATCH. SEE RECORD AT 2R%'       00254
MSG3     DC    CL121'-INVALID ./ ADD. NO "NAME=" FOUND.'                00255
MSG4     DC    CL121'-INVALID RECFM. NOT F OR V'                        00256
MSG5     DC    CL121'-RECFM''S NOT THE SAME IN SYSIN AND SYSUT2'        00257
ALLDNMSG DC    CL60' ',CL61'               ALL PROCESSING COMPLETED'    00258
ALLDONE  DC    X'00'                                                    00259
         LTORG                                                          00260
OUTBLOCK DS    CL32767                                                  00261
         DCBD  DSORG=PO                                                 00262
R0       EQU   0                                                        00263
R1       EQU   1                                                        00264
R2       EQU   2                                                        00265
R3       EQU   3                                                        00266
R4       EQU   4                                                        00267
R5       EQU   5                                                        00268
R6       EQU   6                                                        00269
R7       EQU   7                                                        00270
R8       EQU   8                                                        00271
R9       EQU   9                                                        00272
R10      EQU   10                                                       00273
R11      EQU   11                                                       00274
R12      EQU   12                                                       00275
R13      EQU   13                                                       00276
R14      EQU   14                                                       00277
R15      EQU   15                                                       00278
         END                                                            00279
