PDSLOAD  TITLE '   P D S L O A D    '
*$DOC@*****************************************************************
*                                                                     *
*          PDSLOAD - LOAD SEQUENTIAL DATA SET INTO PDS MEMBERS        *
*                                                                     *
***********************************************************************
*
*         WRITTEN BY: BILL GODFREY, PLANNING RESEARCH CORPORATION.
*         INSTALLATION: PRC COMPUTER CENTER INC, MCLEAN VA.
*         DATE WRITTEN: NOVEMBER 25 1980.
*         DATE UPDATED: FEBRUARY 13, 2000
*         ATTRIBUTES: RE-ENTRANT. (AMODE AND RMODE MUST BE 24.)
*
*         THIS PROGRAM CONVERTS A SEQUENTIAL DATA SET OF PDS
*         MEMBERS IN 'IEBUPDTE' FORMAT TO A PARTITIONED DATA SET.
*
*         THE IEBUPDTE UTILITY PROGRAM CAN DO THE SAME THING,
*         BUT THIS PROGRAM HAS THE FOLLOWING ADDED CAPABILITIES:
*         .  STORES SPF STATISTICS IN A MEMBER'S DIRECTORY ENTRY
*            IF THEY ARE PRESENT ON THE './ ADD' STATEMENT,
*         .  CAN GENERATE SPF STATISTICS IF NONE ARE PRESENT,
*         .  CAN SELECT ONE MEMBER FROM INPUT FILE,
*         .  RESTORES MODIFIED IEBUPDTE STATEMENTS WITHIN A MEMBER
*            (CHANGES '/.' BACK TO './').
*         .  DOES NOT LIST THE DATA IN THE MEMBER,
*         .  PRINTS NUMBER OF RECORDS FOR EACH MEMBER WRITTEN.
*
*         IF PARM='UPDTE(XX)' IS SPECIFIED, THEN ALL OCCURRENCES
*         OF THE 2 CHARACTERS IN PARENTHESES WILL BE CHANGED TO
*         './' BEFORE BEING WRITTEN TO THE PDS, IF THEY OCCUR
*         IN COLUMNS 1-2 OF THE DATA.  THIS IS USED IN CONJUNCTION
*         WITH ANOTHER PROGRAM WHICH CREATES IEBUPDTE-FORMAT
*         DATA SETS FROM A PDS, AND IN SO DOING CHANGES ALL
*         OCCURRENCES OF './' IN COLUMNS 1-2 OF THE DATA WITHIN
*         THE MEMBERS TO ANOTHER CONSTANT, SO IEBUPDTE AND THIS
*         PROGRAM WILL NOT TREAT THE DATA RECORDS AS CONTROL CARDS.
*         IEBUPDTE DOESNT CHANGE THE DATA BACK. THIS PROGRAM DOES.
*
*         LOG OF CHANGES:
*          28MAY81 - SUPPORT NAME= AS SECOND OPERAND (FOLLOWING SSI).
*          28MAY81 - SINGLE MEMBER MAY BE SPECIFIED IN PARM.
*                    PARM='S(ABC)' WILL SELECT MEMBER 'ABC' ONLY.
*          28MAY81 - PARM=SPF WILL FORCE SPF STATS TO BE GENERATED.
*                    IF SPF IS SPECIFIED IN PARM, IT MUST BE FIRST.
*                    IF UPDTE(XX) IS SPECIFIED, IT MUST BE LAST.
*----------------------------------------------------------------------
*          ??JUL87 - THIS VERSION OF THE PDSLOAD PROGRAM HAS BEEN
*                    MODIFIED WITH A GLOBAL VARIABLE SO THE LRECL OF
*                    THE INPUT AND OUTPUT DATASETS CAN BE CHANGED
*                    MERELY BY MODIFYING THE GLOBAL AND REASSEMBLING
*                    THE PROGRAM.  AN EYECATCHER HAS BEEN ADDED SO
*                    THAT YOU CAN SEE THE ASSEMBLED LRECL BY BROWSING
*                    THE LOAD MODULE.  SEE LABEL "&LRECL".
*                    YOU NEED TO REASSEMBLE THE PROGRAM EVERY TIME YOU
*                    WANT TO CHANGE THE LRECL THAT THE PROGRAM TAKES.
*                    (ADMITTEDLY IT'S BETTER TO DO THIS WITH A PARM AT
*                    EXECUTION TIME.)  BUT THIS WORKS.  (IT HAS NOT
*                    BEEN TESTED WITH LRECL MUCH LESS THAN 80.)
*
* S. GOLOB - NEWSWEEK - MOUNTAIN LAKES, N.J.                        NWK
*----------------------------------------------------------------------
*          01SEP92 - AVOID ABEND S0C4 AFTER ERROR RETURN CODE FROM
*                    STOW.  (EG. DIRECTORY FULL, OR, I/O ERROR.)
*                  - CEASE PROCESSING INPUT RECORDS AFTER A './ ENDUP '
*                    RECORD IS ENCOUNTERED.
*                  - DELETE RUN-TIME CODE TO LOAD EXIT ENTRY POINTS
*                    INTO DCB AREAS BY SPECIFYING APPROPRIATE OPERANDS
*                    ON THE DCB MACROS.
*                  - DELETE THE &LRECL SYMBOLIC VARIABLE AND ALTER
*                    CODE TO HANDLE ANY RECORD SIZE UP TO 256.
*                    RECORD FORMAT MUST STILL BE FIXED LENGTH RECORDS.
*                    INPUT AND OUTPUT RECORD LENGTHS NEED NOT BE THE
*                    SAME.  HENCE, OFFLOAD/PDSLOAD CAN BE USED TO
*                    TRUNCATE OR EXTEND THE RECORD LENGTH OF A PDS.
*                  - ENSURE BLANKS ARE USED FOR RECORD EXTENSION.
*                  - IF THE OPEN OF SYSUT1 FAILS THEN THE OPEN IS
*                    RETRIED WITH DDNAME=SYSIN IN CASE THE JOB IS
*                    A HASTILY RE-EDITED IEBUPDTE JOB STREAM.
*                  - REDO LOGIC OF PDS OPEN EXIT TO COPY INPUT LRECL
*                    TO OUTPUT LRECL (IF NOT SUPPLIED), AND TO USE
*                    SYSTEM-DETERMINED BLOCKSIZE (IF NOT SUPPLIED)
*                    IF DFP IS 2.4.0 OR HIGHER.  OTHERWISE, 39 TIMES
*                    THE LRECL IS USED IF IT IS LESS THAN 32K (WHICH
*                    IT SHOULD BE SINCE MAX LRECL IS 256), OR IF IT
*                    IS NOT UNDER 32K THEN THE PDS IS NOT BLOCKED.
*
* GREG PRICE - FERNTREE COMPUTER SERVICES - MELBOURNE, AUSTRALIA  GP@FT
*----------------------------------------------------------------------
*          26JUN98 - ADD Y2K DATE WINDOWING CODE FOR SPFCREDT AND
*                    SPFCHGDT.
*
* JOHN KALINICH - USA LSSC, ST. LOUIS, MISSOURI               DRK JUN98
*----------------------------------------------------------------------
*          20APR99 - SUPPORT ANY LRECL FOR FIXED-LENGTH AND
*                    VARIABLE LENGTH RECORDS.
*                  - FIXED-LENGTH OUTPUT RECORDS ARE WRITTEN FROM
*                    EITHER FIXED-LENGTH OR VARIABLE-LENGTH INPUT.
*                    (OFTEN PC TEXT FILE TRANSFERS GO TO A RECFM=VB
*                    DATA SET ON MVS.)
*                  - VARIABLE-LENGTH OUTPUT DATA REQUIRES VARIABLE-
*                    LENGTH INPUT RECORDS.
*                  - MINIMUM INPUT LRECL (F AND V) REMAINS AT 80, BUT
*                    MINIMUM OUTPUT LRECL IS 1 (PLUS 4 FOR RDW IF V).
*                  - RECORDS ARE TRUNCATED TO FIT INTO THE OUTPUT
*                    LRECL AS NECESSARY.
*                  - BLANKS ARE USED TO EXTEND FIXED-LENGTH RECORDS
*                    WHERE OUTPUT LRECL IS BIGGER THAN INPUT LRECL.
*                  - SUPPORT DDNAME OVERRIDES AS PER IEBUPDTE AND
*                    OTHER OS UTILITIES FOR INTERFACING WITH REVIEW.
*                  - PARM=NEW IS USED TO REQUEST IEBUPDTE BEHAVIOUR.
*                    SPECIFICALLY, OPENING SYSUT1 IS NOT ATTEMPTED,
*                    AND SYSIN IS ASSUMED TO SPECIFY THE INPUT FILE.
*                    'NEW' MUST BE THE FIRST OR ONLY OPTION IN THE
*                    PARAMETER STRING, BEFORE 'SPF' EVEN.
*                  - THE RECORD COUNT STORED IN SPF STATISTICS WILL
*                    BE THE NUMBER OF RECORDS LOADED BY PDSLOAD, AND
*                    NOT NECESSARILY THE VALUE ON THE ./ ADD CARD.
*                  - SUPPORT MEMBER NAME SELECTION BY MASK.
*                    EG. S(ABC)      - MEMBER ABC ONLY.
*                        S(ABC*****) - ALL MEMBERS STARTING WITH ABC.
*                        S(*BC*)     - ABC, ABCD, EBC3 BUT NOT ABCDE.
*                    ASTERISK (*), QUESTION MARK (?), AND PERCENT
*                    SIGN (%) CAN BE USED INTERCHANGABLY AS SINGLE-
*                    CHARACTER PLACEHOLDERS.
*                  - ENSURE SPF STATS DATA FROM ./ ADD ARE NUMERIC.
* GREG PRICE                                                      GP@P6
*----------------------------------------------------------------------
*          13FEB00 - STARTOOL'S "COMBINE" SUBCOMMAND PUTS     SBG 02/00
*                    ISPF STATISTICS RECORDS INTO THE ./ ADD  SBG 02/00
*                    CARDS DIFFERENTLY THAN LISTPDS AND       SBG 02/00
*                    REVIEW.  PDSLOAD IS BEING MODIFIED TO    SBG 02/00
*                    RECOGNIZE EITHER FORMAT, AND TO BE ABLE  SBG 02/00
*                    TO RE-CONSTITUTE THE ISPF STATS NO       SBG 02/00
*                    MATTER WHICH UTILITY WAS USED TO CREATE  SBG 02/00
*                    THEM, INSIDE THE ./ ADD NAME=XXXX CARDS. SBG 02/00
*                                                             SBG 02/00
*      WHAT "STARTOOL COMBINE" WAS DOING, THE DIFFERENCE      SBG 02/00
*      IS AS FOLLOWS:   "CREATE DATE" AND "MODIFY DATE"       SBG 02/00
*      ARE GIVEN SEVEN CHARACTERS INSTEAD OF FIVE, THAT IS:   SBG 02/00
*                                                             SBG 02/00
*        CCYYDDD , INSTEAD OF YYDDD .                         SBG 02/00
*                                                             SBG 02/00
*      THIS INTRODUCES FOUR EXTRA CHARACTERS.  IF YOU HAVE    SBG 02/00
*      A 7-CHARACTER USERID, A NON-BLANK WILL BE SHOVED INTO  SBG 02/00
*      COLUMN 72, RENDERING THE MEMBER UN-STOWABLE BY IBM'S   SBG 02/00
*      IEBUPDTE.  IEBUPDTE TREATS THE NON-BLANK IN COLUMN 72  SBG 02/00
*      AS A CONTINUATION CHARACTER, AND EXPECTS ANOTHER       SBG 02/00
*      CONTROL CARD ON THE NEXT LINE.  AT LEAST PDSLOAD WILL  SBG 02/00
*      NOW BE ABLE TO DEAL WITH THIS SITUATION PROPERLY.      SBG 02/00
*                                                             SBG 02/00
*      I'VE ENTERED AN APAR WITH BRUCE LELAND OF STARTOOL     SBG 02/00
*      SUPPORT.  BUT THE OLD SEQUENTIAL DATASETS THAT HAVE    SBG 02/00
*      BEEN CREATED WITH EXISTING RELEASES OF STARTOOL, IN    SBG 02/00
*      ITS FORMAT, STILL HAVE TO BE DEALT WITH.               SBG 02/00
*                                                             SBG 02/00
* SAM GOLOB                                                   SBG 02/00
*----------------------------------------------------------------------
***********************************************************************
         EJECT
***********************************************************************
*
*     PDSLOAD NOW ACCEPTS DDNAME OVERRIDES IN THE SECOND PROGRAM
*     PARAMETER.  THE IBM OS/360-TO-OS/390 UTILITY PROGRAM DDNAME
*     PARAMETER FORMAT IS USED.  THE THIRD PARAMETER (INITIAL PAGE
*     NUMBER FOR REPORT) IS NOT USED.  THE SYSIN SLOT IS USED FOR
*     THE INPUT SEQUENTIAL FILE DDNAME, THE SYSPRINT SLOT IS USED
*     FOR THE OUTPUT REPORT DDNAME, AND THE SYSUT2 SLOT IS USED
*     FOR THE OUTPUT PARTITIONED FILE DDNAME.  THE PARAMETER
*     CONSISTS OF A HALFWORD LENGTH COUNT FOLLOWED BY A NUMBER OF
*     CONTIGUOUS 8-BYTE SLOTS.  PDSLOAD REQUIRES THAT THE LENGTH
*     COUNT IS A MULTIPLE OF 8.  A NULL SLOT MEANS NO OVERRIDE FOR
*     THE CORRESPONDING DDNAME.  THERE IS NO MINIMUM SLOT COUNT
*     REQUIREMENT.  DDNAMES MUST BE PADDED WITH BLANKS TO 8-BYTES
*     IF NECESSARY.
*
*     THE ABILITY TO OVERRIDE DDNAMES WAS IMPLEMENTED TO FACILITATE
*     THE DYNAMIC INVOCATION OF PDSLOAD FROM THE REVIEW TSO COMMAND.
*
*     THE IBM UTILITY DDNAME SLOT CONVENTION GOES SOMETHING LIKE THIS:
*         01  SYSLIN
*         02  OUTPUT MEMBER NAME
*         03  SYSLMOD
*         04  SYSLIB
*         05  SYSIN
*         06  SYSPRINT      (SYSLOUT)
*         07  SYSPUNCH
*         08  SYSUT1
*         09  SYSUT2
*         10  SYSUT3
*         11  SYSUT4
*         12  SYSTERM
*         13  SYSUT5
*         14  SYSUT6        (SYSCIN FOR PL/I COMPILER)
*         15  SYSUT7
*         16  SYSADATA
*         17  SYSIDL
*
***********************************************************************
         EJECT
***********************************************************************
*
*         THE ONLY IEBUPDTE CONTROL STATEMENTS THAT ARE ACCEPTABLE
*         ARE THE ./ ADD STATEMENT AND THE ./ ALIAS STATEMENT.
*         THE 'NAME=' OPERAND MUST BE SPECIFIED AS THE FIRST OR
*         SECOND OPERAND (SOMETIMES SSI= IS SPECIFIED FIRST).
*         ANY OTHER IEBUPDTE OPERAND (EXCEPT SSI=) IS INVALID AND
*         WILL PREVENT SUBSEQUENT OPERANDS FROM BEING PROCESSED.
*         ./ ENDUP STATEMENTS WILL NOW TERMINATE PROCESSING.
*
*         THESE ADD STATEMENTS WILL BE PROCESSED CORRECTLY -
*         ./ ADD NAME=XYZ
*         ./ ADD NAME=XYZ,SSI=0012C06A
*         ./ ADD SSI=1234ABCD,NAME=XYZ
*
*         IN ORDER FOR SPF STATISTICS TO BE STORED, THE './ ADD'
*         STATEMENT MUST LOOK LIKE THIS:
*
*            COL     DESCRIPTION
*            1-20    ./ ADD NAME=XXXXXXXX
*            21      BLANK
*            22-71   VVMM-YYDDD-YYDDD-HHMM-NNNNN-NNNNN-NNNNN-UUUUUUUUUU
*                    VER CREATE LASTMODIFY  SIZE  INIT   MOD   ID
*
*         THE 'LISTPDS' UTILITY PROGRAM (FROM NASA GODDARD) HAS
*         BEEN LOCALLY MODIFIED TO PUNCH AN IEBUPDTE DECK WITH
*         SPF STATISTICS IN THE ABOVE FORMAT. IT DOES NOT PUNCH
*         ./ ALIAS STATEMENTS, HOWEVER.
*
*         THE 'REVIEW' UTILITY TSO COMMAND IN CBT FILE 134 HAS
*         BEEN LOCALLY MODIFIED TO PUNCH AN IEBUPDTE DECK WITH
*         SPF STATISTICS IN THE ABOVE FORMAT. IT CAN ALSO PUNCH
*         ./ ALIAS STATEMENTS WHEN THE MEMBER LIST HAS BEEN SORTED
*         INTO TTR ORDER.
*
*         THE SPF DATA IS IN THE 'COMMENTS' AREA OF THE STATEMENT
*         SO THE INPUT COULD BE RUN THRU 'IEBUPDTE' SUCCESSFULLY.
*         IT WOULD JUST IGNORE THE SPF DATA.
*
*         THE FORMAT OF THE 50-BYTE SPF FIELD IS FIXED. EACH VALUE
*         MUST HAVE THE CORRECT NUMBER OF DIGITS (USE LEADING ZEROES
*         IF NECESSARY). ONLY THE 10-BYTE 'ID' FIELD AT THE END
*         MAY HAVE A VARIABLE LENGTH. NO IMBEDDED BLANKS ALLOWED.
*         VALUES MUST BE SEPARATED BY A HYPEN AS ABOVE. IF THE
*         DATA DOES NOT CONFORM TO THESE RULES, THE MEMBER WILL
*         STILL BE WRITTEN BUT WITHOUT SPF STATISTICS.  NOTE THAT
*         ISPF INSISTS THAT THE NINTH AND TENTH BYTES OF THE USERID
*         ARE BLANKS OR ISPF WILL NOT SHOW THE STATISTICS.
*
*         INPUT (SYSUT1) DCB ATTRIBUTES NEED NOT BE SPECIFIED IF
*         THE FILE HAS STANDARD LABELS. IF IT IS AN UNLABELED TAPE,
*         ONLY THE BLKSIZE NEED BE SPECIFIED (IF IT IS NOT &LRECL).
*
*         IF THE OUTPUT FILE DOES NOT HAVE ATTRIBUTES IN ITS LABEL
*         AND NONE ARE SPECIFIED IN THE SYSUT2 DD STATEMENT, THE
*         PROGRAM WILL SET THEM TO LRECL=&LRECL, BLKSIZE=39*&LRECL. NWK
*
*         RECOMMENDATION: SUPPLY COMPLETE INPUT DCB EITHER VIA DATA
*         SET LABELS (TAPE OR DISK) OR VIA JCL.
*
*         NOTES ON OUTPUT DCB:  IF SUPPLIED COMPLETELY FROM DATA SET
*         LABELS, JCL, OR BOTH COMBINED, THEN THAT IS FINE.  SOME
*         PEOPLE CODE 'RECFM=F,BLKSIZE=80' SO DCBLRECL IS ZERO.
*         HENCE, IF OUTPUT BLKSIZE IS SUPPLIED AND OUTPUT LRECL IS
*         NOT, THEN IT IS COPIED FROM THE OUTPUT BLKSIZE AND AN
*         UNBLOCKED PDS IS ASSUMED.  IF BOTH LRECL AND BLKSIZE ARE
*         SUPPLIED THEN THE SUPPLIED VALUES ARE USED.  IF NEITHER ARE
*         SUPPLIED THEN THE OUTPUT LRECL IS COPIED FROM THE INPUT
*         SEQUENTIAL FILE LRECL.  IF YOU HAVE DFP 2.4.0 OR LATER
*         THEN CODE OR LEAVE OUTPUT BLKSIZE=0 FOR SYSTEM-DETERMINED
*         BLOCKSIZE.  OLD DFP OR NON-DFP DEFAULT IS 39 TIMES LRECL.
*
*$DOC$*****************************************************************
         SPACE
PDSLOAD  START
         USING PDSLOAD,R15
         B     @PROLOG
         DROP  R15
         DC    AL1(17),CL17'PDSLOAD &SYSDATC '
         DC    CL10' ANY LRECL'
         DC    CL48' OUT:  1:F,V->F (TRUNC,EXTEND)  2:V->V (TRUNC)  '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
         USING PDSLOAD,R10,R11
@PROLOG  STM   R14,R12,12(R13)
         LR    R10,R15             BASE REGISTER
         LA    R15,4095
         LA    R11,1(R15,R10)      BASE REGISTER
         LR    R2,R1
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         L     R15,@SIZE           TARGET SIZE
         LR    R14,R1              TARGET ADDRESS
         SLR   R5,R5               SOURCE SIZE AND PAD
         MVCL  R14,R4              ZERO DYNAMIC AREA
         ST    R13,4(,R1)          CHAIN SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING @DATA,R13
         SPACE
         ZAP   REPORTPG,=P'0'      INITIAL PAGE COUNTER
         ZAP   REPORTLN,=P'0'      INITIAL LINE COUNTER
         ZAP   REPORTMX,=P'50'     INITIAL LINES PER PAGE
         MVI   REPORTO-1,C' '      BLANK PROPOGATOR
         MVI   LINE-1,C' '         BLANK PROPOGATOR
         MVC   MEMSEL,=CL8' '
         MVC   UPDTE,=C'./'
         SPACE
         L     R1,0(,R2)           POINT TO PARM
         LH    R0,0(,R1)           GET LENGTH OF PARM
         LTR   R0,R0
         BZ    PARMX
         LA    R1,2(,R1)           POINT TO PARM DATA
         CH    R0,=H'3'            IS PARM LONG ENOUGH FOR SPF?
         BL    PARMSX              NO, BYPASS COMPARE
         CLC   0(3,R1),=C'NEW'     IS IT NEW, AS PER IEBUPDTE?
         BNE   TRYSPF              NO
         OI    FLAG1,NEWPRM        YES, TOLERATE AS PDSLOAD DOES THIS
         LA    R1,3(,R1)
         SH    R0,=H'3'
         BZ    PARMX
         CLI   0(R1),C','
         BNE   PARMERR
         LA    R1,1(,R1)           POINT PAST COMMA
         SH    R0,=H'1'
         BZ    PARMX
TRYSPF   CLC   0(3,R1),=C'SPF'     IS IT SPF?
         BNE   PARMSX              NO
         OI    FLAG1,GENSPF        YES, GENERATE SPF STATISTICS
         LA    R1,3(,R1)
         SH    R0,=H'3'
         BZ    PARMX
         CLI   0(R1),C','
         BNE   PARMERR
         LA    R1,1(,R1)           POINT PAST COMMA
         SH    R0,=H'1'
         BZ    PARMX
PARMSX   DS    0H
PARMM    CH    R0,=H'3'            LONG ENOUGH FOR S(X ?
         BL    PARMERR             NO
         CLC   0(2,R1),=C'S('      IS IT S( ?
         BNE   PARMMX              NO, BRANCH
         LA    R1,2(,R1)           YES, POINT PAST PAREN
         SH    R0,=H'2'
         LA    R15,MEMSEL
         LA    R14,MEMSEL+8
PARMML   CLI   0(R1),C')'
         BE    PARMME
         CR    R15,R14             IS MEMBER NAME TOO LONG?
         BNL   PARMMIG             YES, IGNORE EXTRA CHARACTERS
         MVC   0(1,R15),0(R1)      COPY ONE BYTE OF MEMBER NAME
         LA    R15,1(,R15)
         LA    R1,1(,R1)
PARMMIG  BCT   R0,PARMML
         B     PARMX
PARMME   LA    R1,1(,R1)           POINT PAST PAREN
         SH    R0,=H'1'
         BZ    PARMX
         CLI   0(R1),C','
         BNE   PARMERR
         LA    R1,1(,R1)           POINT PAST COMMA
         SH    R0,=H'1'
         BZ    PARMERR
PARMMX   DS    0H
         CH    R0,=H'9'            IS PARM LONG ENOUGH FOR UPDTE(XX)?
         BNE   PARMERR
         CLC   0(6,R1),=C'UPDTE('
         BNE   PARMERR
         CLI   08(R1),C')'
         BNE   PARMERR
         MVC   UPDTE,6(R1)         LOOK FOR XX INSTEAD OF ./
         B     PARMX
PARMERR  WTO   'PDSLOAD WILL ABEND - INVALID PARAMETER',ROUTCDE=(11)
         ABEND 100
PARMX    DS    0H
         EJECT
************************************************************
*                                                          *
*        INITIALIZE THE DCBS                               *
*                                                          *
************************************************************
         SPACE
         MVC   PRTDCBW(PRTDCBL),PRTDCB
         MVC   UT1DCBW(UT1DCBL),UT1DCB
         MVC   PDSDCBW(PDSDCBL),PDSDCB
         LA    R3,PRTDCBW
         LA    R4,UT1DCBW
         LA    R5,PDSDCBW
         TM    FLAG1,NEWPRM        WAS PARM=NEW SPECIFIED?
         BZ    NEWPRMOK            NO
         MVC   DDNAM(8,R4),=CL8'SYSIN'   YES, BEHAVE LIKE IEBUPDTE
NEWPRMOK TM    0(R2),X'80'         ANY DDNAME OVERRIDES?
         BO    PARM2X              NO, ONLY ONE PARAMETER
         L     R1,4(,R2)           POINT TO SECOND PARAMETER
         LA    R1,0(,R1)           ENSURE ADDRESS FORMAT
         LTR   R1,R1               ZERO POINTER?
         BZ    PARM2BAD            YES, NO OVERRIDES SUPPLIED
         CLI   0(R1),0             LENGTH LESS THAN 256?
         BNE   PARM2BAD            NO
         TM    1(R1),7             MULTIPLE OF EIGHT?
         BNZ   PARM2BAD            NO
         CLI   1(R1),8*5           SYSIN SLOT PRESENT?
         BL    PARM2X              NO
         CLI   34(R1),0   (8*4+2)  SYSIN OVERRIDE SPECIFIED?
         BE    PARM2I              NO
         MVC   DDNAM(8,R4),34(R1)  YES, COPY IT INTO DCB
PARM2I   CLI   1(R1),8*6           SYSPRINT SLOT PRESENT?
         BL    PARM2X              NO
         CLI   42(R1),0   (8*5+2)  SYSPRINT OVERRIDE SPECIFIED?
         BE    PARM2P              NO
         MVC   DDNAM(8,R3),42(R1)  YES, COPY IT INTO DCB
PARM2P   CLI   1(R1),8*9           SYSUT2 SLOT PRESENT?
         BL    PARM2X              NO
         CLI   66(R1),0   (8*8+2)  SYSUT2 OVERRIDE SPECIFIED?
         BE    PARM2X              NO
         MVC   DDNAM(8,R5),66(R1)  YES, COPY IT INTO DCB
         B     PARM2X              DDNAME OVERRIDE PROCESSING COMPLETE
PARM2BAD WTO   'PDSLOAD - INVALID DDNAME OVERRIDE PARAMETER IGNORED',  +
               ROUTCDE=(11)
PARM2X   DS    0H
         EJECT
************************************************************
*                                                          *
*        OPEN THE DCBS                                     *
*                                                          *
************************************************************
         SPACE
         SPACE
         LA    R6,OPEN
         MVI   0(R6),X'80'
         SPACE
         OPEN  ((R3),OUTPUT),MF=(E,(R6))
         SPACE
         TM    OFLGS(R3),X'10'
         BO    OKPRT
         LA    R15,16
         B     EXIT
PRTEXO   CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),LRECL(R1)
         NI    RECFM(R1),255-X'10' CHANGE RECFM FROM FBA TO FA
         BR    R14
OKPRT    OI    STATUS,PRT          SYSPRINT IS OPEN
         SPACE
         OPEN  ((R4),INPUT),MF=(E,(R6))
         SPACE
         TM    OFLGS(R4),X'10'
         BO    OKUT1
         LA    R15,16
         CLC   DDNAM(8,R4),=CL8'SYSUT1'  AFTER TRYING SYSUT1?
         BNE   EXIT                      NO, OPEN FAILED
         MVC   DDNAM(8,R4),=CL8'SYSIN'   YES, NO OVERRIDE SO RETRY
         WTO   'PDSLOAD WILL RETRY WITH DDNAME=SYSIN',                 +
               ROUTCDE=(11)        USE SAME ROUTING AS IEC130I
         B     OKPRT               RETRY OPEN WITH SYSIN
UT1EXO   TM    RECFM(R1),X'C0'     UNDEFINED RECORD FORMAT?
         BOR   R14                 YES, TAKE NO ACTION HERE
         MVI   DWLIN+1,4           PREPARE FOR INPUT RDWS
         TM    RECFM(R1),X'40'     FIXED LENGTH RECORD INPUT?
         BOR   R14                 NO, TAKE NO ACTION HERE
         MVI   DWLIN+1,0           NO INPUT RDWS
         OI    RECFM(R1),X'80'     FORCE SOME RECORD FORMAT
         CLC   BLKSI(2,R1),=H'0'   IS BLKSIZE SPECIFIED?
         BNE   UT1EX1              YES, CHECK LRECL
         MVC   BLKSI(2,R1),LRECL(R1)  NO, DEFAULT IS UNBLOCKED
         NI    RECFM(R1),255-X'10' CHANGE RECFM FROM FB TO F
         BR    R14                 RETURN
UT1EX1   CLC   LRECL(2,R1),=H'0'   IS LRECL SPECIFIED?
         BNER  R14                 YES, RETURN
         MVC   LRECL(2,R1),BLKSI(R1)  NO, DEFAULT IS UNBLOCKED
         NI    RECFM(R1),255-X'10' CHANGE RECFM FROM FB TO F
         BR    R14                 RETURN
OKUT1    OI    STATUS,UT1          SYSUT1 IS OPEN
         TM    RECFM(R4),X'C0'     INPUT RECFM=U?
         BO    UNDEFBAD            YES, INVALID
         MVC   LINE,LINE-1
         MVC   LINE+1(SHOMSGLN),SHORTMSG
         CLC   =H'80',LRECL(R4)    INPUT LRECL<80?
         BH    FAILEXIT            YES, INVALID
         SPACE
         OPEN  ((R5),OUTPUT),MF=(E,(R6))
         SPACE
         TM    OFLGS(R5),X'10'
         BO    OKUT2
         LA    R15,16
         B     EXIT
UT2EXO   TM    RECFM(R1),X'C0'     NULL RECORD FORMAT?
         BM    UT2EX3              NO, FIXED OR VARIABLE LENGTH
         BZ    UT2EX2              YES, COPY INPUT RECORD FORMAT
         XC    LRECL(2,R1),LRECL(R1)   RECFM=U SO FORCE LRECL=0
         BR    R14                 RETURN
UT2EX2   MVC   RECFM(1,R1),RECFM(R4)  YES, COPY INPUT RECFM
UT2EX3   CLC   LRECL(2,R1),=H'0'   IS LRECL SPECIFIED?
         BNE   UT2EX4              YES, TAKE NO ACTION HERE
         MVC   LRECL(2,R1),LRECL(R4)    COPY INPUT LRECL
UT2EX4   TM    RECFM(R1),X'80'     FIXED LENGTH RECORDS?
         BOR   R14                 YES, USE SDB OR GOOD USER SPEC
         MVI   DWLOT+1,4           USE OUTPUT BDWS AND RDWS
         BR    R14                 USE SDB OR GOOD USER SPEC
*
*  TEST FOR BACK LEVEL MVS AND DFP DROPPED.  COMPLETE DCB DETAILS
*  OF TARGET PDS SHOULD BE IN VTOC BEFORE THIS PROGRAM GETS CONTROL.
*  EVEN SO, THE ABOVE CODE SHOULD ALLOW A PDSLOAD TO A NEW PDS THAT
*  HAS HAD NO DCB ATTRIBUTES PREVIOUSLY SUPPLIED ON A "MODERN" MVS.
*
*  IN ANY CASE, DCB CONFLICTS ARE HEREBY DECREED TO BE USER ERROR(S).
*
OKUT2    OI    STATUS,UT2          SYSUT2 IS OPEN
         TM    RECFM(R5),X'C0'     OUTPUT RECFM=U?
         BO    UNDEFBAD            YES, INVALID
         TM    RECFM(R5),X'40'     FIXED-LENGTH OUTPUT RECORDS?
         BZ    GETBUF              YES
         TM    RECFM(R4),X'80'     VARIABLE-LENGTH INPUT RECORDS?
         BZ    GETBUF              YES
         MVC   LINE,LINE-1         NO, F(B)-IN NO GOOD FOR V(B)-OUT
         MVC   LINE+1(FIXMSGLN),FIXINMSG
         B     FAILEXIT
         SPACE
GETBUF   LA    R0,7
         AH    R0,BLKSI(,R5)
         SRL   R0,3
         SLL   R0,4                DOUBLE ROUNDED-UP BLKSIZE
         ST    R0,FREEM
         GETMAIN R,LV=(0)
         ST    R1,FREEM+4
         ST    R1,PUTPDSX          BUFFER 1 ADDRESS
         XC    0(4,R1),0(R1)       RESET BDW 1
         LR    R0,R1
         AH    R0,DWLOT            ALLOW FOR OUTPUT BDW
         ST    R0,PUTPDSX+4        BUFFER 1 ADDRESS TO USE NEXT
         AH    R1,BLKSI(,R5)
         ST    R1,PUTPDSX+8        PAST END OF BUFFER 1
         LA    R1,7(,R1)
         SRL   R1,3
         SLL   R1,3                ALIGN BUFFER 2
         ST    R1,PUTPDSY          BUFFER 2 ADDRESS
         XC    0(4,R1),0(R1)       RESET BDW 2
         LR    R0,R1
         AH    R0,DWLOT            ALLOW FOR OUTPUT BDW
         ST    R0,PUTPDSY+4        BUFFER 2 ADDRESS TO USE NEXT
         AH    R1,BLKSI(,R5)
         ST    R1,PUTPDSY+8        PAST END OF BUFFER 2
         LA    R1,PDSDECB1
         MVC   0(PDSDECBL,R1),PDSDECB
         ST    R1,PUTPDSX+12
         LA    R1,PDSDECB2
         MVC   0(PDSDECBL,R1),PDSDECB
         ST    R1,PUTPDSY+12
         SPACE
         MVC   LINE,LINE-1
         MVC   MEMBER,=CL8' '      BLANK OUT MEMBER NAME
         SR    R1,R1
         ST    R1,SEQ
         ST    R1,UPR
         BAL   R14,REPORT          FORCE A HEADER
         EJECT
************************************************************
*                                                          *
*        READ AN INPUT RECORD                              *
*                                                          *
************************************************************
         SPACE
READ     DS    0H
         GET   (R4)
         SPACE
         ST    R1,RECAD            SAVE INPUT RECORD ADDRESS
         LA    R0,1
         A     R0,COUNTIN
         ST    R0,COUNTIN
         MVC   LINE,LINE-1
         LR    R15,R1              POINT TO INPUT RECORD
         AH    R15,DWLIN           POINT TO INPUT DATA
         CLC   =C'./',0(R15)       CONTROL STATEMENT?
         BNE   COPY                NO
         SPACE
************************************************************
*                                                          *
*         PARSE THE CONTROL STATEMENT                      *
*                                                          *
************************************************************
         SPACE
         MVI   INREC,C' '          CLEAR CONTROL RECORD HOLD AREA
         MVC   INREC+1(79),INREC
         LA    R14,72-1            GET LENGTH CODE IF FIXED LENGTH
         CR    R1,R15              FIXED LENGTH RECORDS?
         BE    COPYCNTL            YES, AND MINIMUM LRECL IS 80
         SLR   R3,R3               NO
         ICM   R3,3,0(R1)          GET RECORD LENGTH
         SH    R3,=H'5'            GET DATA LENGTH CODE
         CR    R3,R14              IS RECORD TOO LONG FOR INREC?
         BH    COPYCNTL            YES
         LR    R14,R3              NO, USE ACTUAL DATA LENGTH
COPYCNTL EX    R14,LOADECHO        COPY CONTROL STATEMENT IMAGE
         LA    R15,INREC           POINT TO COLUMN 1
         LA    R3,72-1             LENGTH CODE OF CONTROL STATEMENT
         LA    R6,ODL              POINT TO OPERAND DESCRIPTOR LIST
         XC    0(ODLL,R6),0(R6)    ZERO THE ODL
         SR    R1,R1               INSURE HI ORDER BYTE ZERO
         LA    R0,ODLL/8-1         NUMBER OF ENTRIES IN O.D.L.
*                                  MINUS 1 (LAST ODE WILL REMAIN ZERO)
LOOP     XC    0(8,R6),0(R6)       ZERO THE OPERAND DESCRIPTOR ENTRY
         EX    R3,TRTNONBL         FIND A NONBLANK
         BZ    DONE                BRANCH IF ALL BLANKS
         LR    R14,R1              GET ADDRESS OF STRING
         SR    R14,R15             GET LENGTH OF PRECEDING BLANKS
         SR    R3,R14              GET LENGTH OF REMAINING TEXT
         LR    R15,R1              GET ADDRESS OF NONBLANK
         EX    R3,TRTBLANK         FIND A BLANK
         BZ    LAST                BRANCH IF NOT FOUND
         LR    R14,R1              GET ADDRESS OF BLANK
         SR    R14,R15             GET LENGTH OF FIELD
         OI    6(R6),X'80'         OPERAND PRESENT
         ST    R15,0(,R6)          ADDRESS OF OPERAND
         STH   R14,4(,R6)          LENGTH OF OPERAND
         SR    R3,R14              GET LENGTH CODE OF REMAINING TEXT
         BZ    DONE                BRANCH IF ONE TRAILING BLANK
         LA    R6,8(,R6)           POINT TO NEXT O.D.E.
         LR    R15,R1              POINT TO BLANK
         BCT   R0,LOOP
         B     DONE
LOADECHO MVC   INREC(0),0(R15)     <<< EXECUTED >>>
TRTNONBL TRT   0(0,R15),TABNONBL   <<< EXECUTED >>>
TRTBLANK TRT   0(0,R15),TABBLANK   <<< EXECUTED >>>
LAST     LA    R14,1(,R3)          GET LENGTH
         OI    6(R6),X'80'         OPERAND PRESENT
         ST    R15,0(,R6)          ADDRESS OF OPERAND
         STH   R14,4(,R6)          LENGTH OF OPERAND
DONE     DS    0H
         SPACE
************************************************************
*                                                          *
*         DETERMINE TYPE OF CONTROL STATEMENT              *
*                                                          *
************************************************************
         SPACE
         LA    R6,ODE1             POINT TO FIRST O.D.E.
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    COPY                BRANCH IF WHOLE STATEMENT BLANK
         L     R1,0(,R6)           POINT TO FIRST STRING
         LH    R15,4(,R6)          GET LENGTH OF STRING
         LA    R0,3                MAX VALID LENGTH
         CR    R15,R0              IS STRING TOO LONG ?
         BH    COPY                BRANCH IF TOO LONG
         SLL   R15,2               MULTIPLY LENGTH BY 4
         B     *(R15)              BRANCH TO ONE OF NEXT 7
         B     COPY                1 CHAR
         B     CONTROL             2 CHAR
         B     COPY                3 CHAR
         SPACE
COPY     DS    0H
         CLI   MEMBER,C' '         HAS A ./ADD RECORD BEEN READ?
         BE    COPYERR             NO, PRINT A MESSAGE
         LA    R1,1
         A     R1,SEQ              COUNT THE RECORDS IN THIS MEMBER
         ST    R1,SEQ
         SPACE
         TM    FLAG1,SELECT        ARE WE SELECTING THIS MEMBER?
         BZ    READ                NO, BRANCH
         SPACE
*        MVC   INREC+72(8),=C'00000000'
*        CVD   R1,DOUBLE
*        OI    DOUBLE+7,X'0F'
*        UNPK  INREC+72(5),DOUBLE+5(3)
         SPACE
         LH    R1,LRECL(,R4)       GET INPUT LRECL
         L     R14,RECAD           POINT TO THE INPUT RECORD
         CLI   DWLIN+1,0           FIXED-LENGTH INPUT?
         BE    *+8                 YES
         ICM   R1,3,0(R14)         NO, GET RECORD LENGTH
         SH    R1,DWLIN            GET DATA LENGTH
         CH    R1,=H'1'            LONG ENOUGH?
         BNH   NOTUPDTE            NO
         AH    R14,DWLIN           POINT TO DATA OF INPUT RECORD
         CLC   0(2,R14),UPDTE      IS THIS XX OF UPDTE(XX)?
         BNE   NOTUPDTE            NO, SKIP MVC
         MVC   0(2,R14),=C'./'     YES, CHANGE TO ./
         LA    R1,1
         A     R1,UPR              COUNT THE MODIFIED RECORDS
         ST    R1,UPR
NOTUPDTE DS    0H
         SPACE
         BAL   R14,PUTPDS
         B     READ
         SPACE
COPYERR  TM    STATUS,COPYM        HAS MESSAGE BEEN ISSUED?
         BO    READ                YES, BRANCH
         OI    STATUS,COPYM
         MVC   LINE,LINE-1
         MVC   LINE+1(24),=C'SKIPPING FOR ./ ADD CARD'
         BAL   R14,REPORT
         B     READ
         EJECT
************************************************************
*                                                          *
*         WRITE A LOGICAL RECORD TO THE PDS                *
*                                                          *
************************************************************
         SPACE
PUTPDS   ST    R14,PUTPDSR         SAVE RETURN ADDRESS
PUTREDO  LM    R6,R9,PUTPDSX       FIRST, CURRENT, END, DECB
         L     R14,RECAD           POINT TO THE INPUT RECORD
         LH    R1,LRECL(,R5)       GET LRECL OF OUTPUT PDS
         CLI   DWLOT+1,0           FIXED-LENGTH OUTPUT RECORDS?
         BE    PUTLEN              YES
         CLM   R1,3,0(R14)         NO, TRUNCATE INPUT RECORD?
         BL    PUTLEN              YES
         ICM   R1,3,0(R14)         NO, USE INPUT RECORD LENGTH
PUTLEN   LR    R0,R7               COPY TARGET FOR THIS RECORD
         AR    R7,R1               POINT TO NEXT "CURRENT" RECORD
         CR    R7,R8               WILL THIS RECORD FIT IN THIS BLOCK?
         BH    PUTPDS1             NO, ISSUE WRITE AND PROCURE BUFFER
         ST    R7,PUTPDSX+4        YES, SAVE TARGET FOR NEXT RECORD
         LH    R15,LRECL(,R4)      GET LRECL OF INPUT
         CLI   DWLIN+1,0           FIXED-LENGTH INPUT RECORDS?
         BE    PUTPAD              YES, PROCEED WITH F(B) TO F(B)
         ICM   R15,3,0(R14)        NO, GET INPUT RECORD LENGTH FROM RDW
         CLI   DWLOT+1,0           FIXED-LENGTH OUTPUT RECORDS?
         BNE   PUTVTOV             NO, VARIABLE-LENGTH IN AND OUT
         AH    R14,DWLIN           YES, POINT PAST INPUT RDW
         SH    R15,DWLIN           REDUCE LENGTH OF DATA TO COPY
         B     PUTPAD              PROCEED WITH V(B) TO F(B)
PUTVTOV  SR    R7,R6               GET NEW SIZE OF BLOCK
         STH   R7,0(,R6)           UPDATE LENGTH IN BDW
         LR    R7,R0               COPY TARGET FOR THIS RECORD
         STCM  R1,3,0(R7)          SET OUTPUT LENGTH IN RDW
         AH    R14,=H'2'           DO NOT COPY RDW LENGTH, BUT DO
         SH    R15,=H'2'                  COPY RDW SECOND HALF
         AH    R0,=H'2'
         SH    R1,=H'2'
PUTPAD   ICM   R15,8,LINE-1        LOAD BLANK PAD CHARACTER
         MVCL  R0,R14              MOVE RECORD INTO BLOCK
         L     R14,PUTPDSR         RESTORE RETURN ADDRESS
         BR    R14                 RETURN
PUTPDS1  TM    STATUS,DECB         ANY WRITES OUTSTANDING?
         BZ    PUTPDS2             NO, BRANCH
         L     R1,PUTPDSY+12       GET LAST DECB
         CHECK (1)
PUTPDS2  DS    0H
         WRITE (R9),SF,(R5),(R6),MF=E
         MVC   PUTPDSX(16),PUTPDSY SWAP
         STM   R6,R9,PUTPDSY       SWAP
         AH    R6,DWLOT            ALLOW FOR OUTPUT BDW
         ST    R6,PUTPDSY+4
         OI    STATUS,DECB
         B     PUTREDO             REDRIVE WITH BUFFER NOW AVAILABLE
         EJECT
************************************************************
*                                                          *
*         PROCESS EACH ./ STATEMENT                        *
*                                                          *
************************************************************
         SPACE
CONTROL  BAL   R14,STOW            FINISH PREVIOUS MEMBER
         MVC   MEMBER,=CL8' '      RESET
         NI    STATUS,255-COPYM     RESET
         NI    STATUS,255-SPF        RESET
         NI    STATUS,255-SSI         RESET
         SR    R1,R1                   RESET
         ST    R1,SEQ                   RESET
         ST    R1,UPR                    RESET
         NI    FLAG1,255-SELECT           RESET
         SPACE
         MVC   LINE,LINE-1         PRINT
         MVC   LINE+1(72),INREC     THE
         BAL   R14,REPORT            ./ADD STATEMENT
         CLC   0(12,R1),=C'./ ADD NAME='
         BE    SIMPLE
         LA    R6,ODE2             POINT TO SECOND O.D.E.
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    COPY                BRANCH IF NO OPERAND
         CLC   4(2,R6),=H'3'       LENGTH OF 'ADD'?
         BNE   NOTADD
         L     R1,0(,R6)           POINT TO OPERAND
         CLC   0(3,R1),=C'ADD'
         BNE   COPY
         SPACE
************************************************************
*                                                          *
*         PROCESS THE ./ ADD STATEMENT                     *
*                                                          *
************************************************************
         SPACE
SIMPLE   MVC   MEMBER,=CL8' '
         LA    R6,ODE3             POINT TO FIRST OPERAND DESCRIPTOR
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    COPY                BRANCH IF NO OPERAND
         LH    R15,4(,R6)          GET LENGTH OF STRING
         SH    R15,=H'5'           LENGTH OF 'NAME='
         BNP   OP2NAME             BRANCH IF TOO SHORT FOR NAME=
         L     R1,0(,R6)           POINT TO OPERAND
         CLC   0(5,R1),=C'NAME='   IS IT NAME= ?
         BNE   OP2NAME             NO, ITS PROBABLY SSI=
         LA    R1,5(,R1)
         LA    R0,8                MAX VALID LENGTH
         CR    R15,R0              IS STRING TOO LONG ?
         BH    COPY                BRANCH IF TOO LONG
         MVC   MEMBER,=CL8' '
         BCTR  R15,0               GET LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   MEMBER(0),0(R1)     <<< EXECUTED >>>
         EX    R15,*-6             MOVE NAME TO MEMBER NAME
         TRT   MEMBER,ALPHANUM     IS MEMBER NAME VALID
         BNZ   ILLMEM              NO, BRANCH
         CLI   MEMBER,C'0'         IS FIRST CHAR NUMERIC?
         BNL   ILLMEM              YES, BRANCH
         CLI   MEMBER,C'-'         IS FIRST CHAR HYPHEN?
         BE    ILLMEM
         CLI   MEMSEL,C' '         ARE WE SELECTING ALL MEMBERS?
         BE    SELMEM1             YES, BRANCH
         BAL   R14,MEMMATCH        NO, IS THIS A SELECTED MEMBER?
         BNE   *+8                 NO
SELMEM1  OI    FLAG1,SELECT        INDICATE THIS MEMBER SELECTED
         B     OP2SSI
*
*              SSI= WAS PROBABLY SPECIFIED FIRST,
*              SEE IF NAME= IS SECOND
*
OP2NAME  LA    R6,ODE4             POINT TO SECOND OPERAND
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    COPY                BRANCH IF NO OPERAND
         LH    R15,4(,R6)          GET LENGTH OF STRING
         SH    R15,=H'5'           LENGTH OF 'NAME='
         BNP   COPY                BRANCH IF TOO SHORT FOR NAME=
         L     R1,0(,R6)           POINT TO OPERAND
         CLC   0(5,R1),=C'NAME='
         BNE   COPY
         LA    R1,5(,R1)
         LA    R0,8                MAX VALID LENGTH
         CR    R15,R0              IS STRING TOO LONG ?
         BH    COPY                BRANCH IF TOO LONG
         MVC   MEMBER,=CL8' '
         BCTR  R15,0               GET LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   MEMBER(0),0(R1)     <<< EXECUTED >>>
         EX    R15,*-6             MOVE NAME TO MEMBER NAME
         TRT   MEMBER,ALPHANUM     IS MEMBER NAME VALID
         BNZ   ILLMEM              NO, BRANCH
         CLI   MEMBER,C'0'         IS FIRST CHAR NUMERIC?
         BNL   ILLMEM              YES, BRANCH
         CLI   MEMBER,C'-'         IS FIRST CHAR HYPHEN?
         BE    ILLMEM
         CLI   MEMSEL,C' '         ARE WE SELECTING ALL MEMBERS?
         BE    SELMEM2             YES, BRANCH
         BAL   R14,MEMMATCH        NO, IS THIS A SELECTED MEMBER?
         BNE   *+8                 NO
SELMEM2  OI    FLAG1,SELECT        INDICATE THIS MEMBER SELECTED
         SPACE
         LA    R6,ODE3             POINT TO FIRST OPERAND
         LH    R15,4(,R6)          LENGTH
         L     R6,0(,R6)           POINT TO OPERAND
         CH    R15,=H'12'          IS LENGTH RIGHT FOR SSI=XXXXXXXX?
         BNE   NOUSER              NO, IGNORE FIRST OPND, NO USER DATA
         CLC   0(4,R6),=C'SSI='    IS IT SSI= ?
         BE    SSICVT              YES, GO PROCESS SSI
         B     NOUSER              NO, IGNORE FIRST OPND, NO USER DATA
*
*              NAME= WAS SPECIFIED FIRST
*              CHECK FOR SSI= OR SPF STATS
*
OP2SSI   LA    R6,ODE4             POINT TO 2ND OPERAND DESCRIPTOR
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    NOUSER              NO USER DATA TO BE STOWED
         LH    R15,4(,R6)          LENGTH
         L     R6,0(,R6)           POINT TO OPERAND
         CH    R15,=H'12'          IS LENGTH RIGHT FOR SSI=XXXXXXXX?
         BNE   NOSSI               NO, BRANCH
         CLC   0(4,R6),=C'SSI='    IS IT SSI= ?
         BNE   NOSSI               NO, BRANCH
SSICVT   MVC   MEMUSER(8),4(R6)    MOVE 8 HEX CHARACTERS
         LA    R1,MEMUSER          POINT TO DATA TO BE CONVERTED
         LA    R0,4                LENGTH/2 OF DATA TO BE CONVERTED
         BAL   R14,PACK            CONVERT HEX TO BINARY
         OI    STATUS,SSI          SET FLAG FOR STOW
         B     READ
NOSSI    DS    0H
         MVI   SKIPFLG,X'00'       RESET SKIP FLAG            SBG 02/00
         CLI   12(R6),C'-'         EXTRA 2 NUMBERS IN CRDATE? SBG 02/00
         BNE   TESTOLD             NOT STARTOOL COMBINE       SBG 02/00
         MVI   SKIPFLG,X'01'       SET SKIP FLAG              SBG 02/00
         B     TESTNEW             TEST NEW FORMAT            SBG 02/00
TESTOLD  DS    0H                  SAME CODE AS BEFORE        SBG 02/00
         SH    R15,=H'41'          MINIMUM LENGTH 41
         BM    NOSPF
         CH    R15,=H'9'           ID LENGTH CODE MAY BE 0 TO 9
         BH    NOSPF               TOTAL LENGTH EXCEEDS 50
         CLI   4(R6),C'-'
         BNE   NOSPF
         CLI   10(R6),C'-'
         BNE   NOSPF
         CLI   16(R6),C'-'
         BNE   NOSPF
         CLI   21(R6),C'-'
         BNE   NOSPF
         CLI   27(R6),C'-'
         BNE   NOSPF
         CLI   33(R6),C'-'
         BNE   NOSPF
         CLI   39(R6),C'-'
         BNE   NOSPF
         B     TESTNUM             TEST NUMERICS OLD WAY      SBG 02/00
TESTNEW  DS    0H                  TEST FOR NEW FORMAT        SBG 02/00
         SH    R15,=H'45'          MINIMUM LENGTH 45          SBG 02/00
         BM    NOSPF                                          SBG 02/00
         CH    R15,=H'9'        ID LENGTH CODE MAY BE 0 TO 9  SBG 02/00
         BH    NOSPF               TOTAL LENGTH EXCEEDS 54    SBG 02/00
         CLI   4(R6),C'-'                                     SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   12(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   20(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   25(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   31(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   37(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         CLI   43(R6),C'-'                                    SBG 02/00
         BNE   NOSPF                                          SBG 02/00
         B     TESTNUMN            TEST NUMERICS NEW WAY      SBG 02/00
TESTNUM  DS    0H                                             SBG 02/00
         USING XPF,R6                                         SBG 02/00
         TRT   XPFVM,NUMERIC                                  SBG 02/00
         BNZ   NOSPF
         TRT   XPFCREDT,NUMERIC
         BNZ   NOSPF
         TRT   XPFCHGDT,NUMERIC
         BNZ   NOSPF
         TRT   XPFHHMM,NUMERIC
         BNZ   NOSPF
         TRT   XPFCCNT,NUMERIC
         BNZ   NOSPF
         TRT   XPFICNT,NUMERIC
         BNZ   NOSPF
         TRT   XPFMOD,NUMERIC
         BNZ   NOSPF
         PACK  DOUBLE,XPFVM+0(2)   GET V OF V.M
         CVB   R0,DOUBLE
         STC   R0,SPFVM
         PACK  DOUBLE,XPFVM+2(2)   GET M OF V.M
         CVB   R0,DOUBLE
         STC   R0,SPFVM+1
         PACK  DOUBLE,XPFCREDT     GET YYDDD
         OI    DOUBLE+7,X'0F'
         CLC   XPFCREDT(2),=C'66'  BELOW 19XX WINDOW?         DRK JUN98
         BNL   *+8                 NO,  THEN 19XX             DRK JUN98
         OI    DOUBLE+4,X'01'      YES, THEN 20XX             DRK JUN98
         MVC   SPFCREDT,DOUBLE+4
         PACK  DOUBLE,XPFCHGDT     GET YYDDD
         OI    DOUBLE+7,X'0F'
         CLC   XPFCHGDT(2),=C'66'  BELOW 19XX WINDOW?         DRK JUN98
         BNL   *+8                 NO,  THEN 19XX             DRK JUN98
         OI    DOUBLE+4,X'01'      YES, THEN 20XX             DRK JUN98
         MVC   SPFCHGDT,DOUBLE+4   DATE LAST MODIFIED
         PACK  DOUBLE,XPFHHMM
         L     R0,DOUBLE+4         GET 000HHMMC
         SRL   R0,4                GET 0000HHMM
         STH   R0,SPFHHMM          TIME LAST MODIFIED
         PACK  DOUBLE,XPFCCNT      SIZE
         CVB   R0,DOUBLE
         STH   R0,SPFCCNT          CURRENT SIZE
         PACK  DOUBLE,XPFICNT      SIZE
         CVB   R0,DOUBLE
         STH   R0,SPFICNT          INITIAL SIZE
         PACK  DOUBLE,XPFMOD       MOD
         CVB   R0,DOUBLE
         STH   R0,SPFMOD           LINES MODIFIED
         MVC   SPFUSER,=CL10' '    PAD WITH BLANKS
         B     *+10
         MVC   SPFUSER(0),XPFUSER
         EX    R15,*-6
         OI    STATUS,SPF          SET FLAG FOR STOW
         DROP  R6                                             SBG 02/00
         B     NOSPF               GO PROCESS FURTHER         SBG 02/00
TESTNUMN DS    0H                                             SBG 02/00
         USING XP2,R6                                         SBG 02/00
         TRT   XP2VM,NUMERIC                                  SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2CREDT,NUMERIC                               SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2CHGDT,NUMERIC                               SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2HHMM,NUMERIC                                SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2CCNT,NUMERIC                                SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2ICNT,NUMERIC                                SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         TRT   XP2MOD,NUMERIC                                 SBG 02/00
         BNZ   NOSPF                                          SBG 02/00
         PACK  DOUBLE,XP2VM+0(2)   GET V OF V.M               SBG 02/00
         CVB   R0,DOUBLE                                      SBG 02/00
         STC   R0,SPFVM                                       SBG 02/00
         PACK  DOUBLE,XP2VM+2(2)   GET M OF V.M               SBG 02/00
         CVB   R0,DOUBLE                                      SBG 02/00
         STC   R0,SPFVM+1                                     SBG 02/00
         PACK  DOUBLE,XP2CREDT     GET YYDDD                  SBG 02/00
         OI    DOUBLE+7,X'0F'                                 SBG 02/00
         CLC   XP2CREDT(2),=C'66'  BELOW 19XX WINDOW?         DRK JUN98
         BNL   *+8                 NO,  THEN 19XX             DRK JUN98
         OI    DOUBLE+4,X'01'      YES, THEN 20XX             DRK JUN98
         MVC   SPFCREDT,DOUBLE+4                              SBG 02/00
         PACK  DOUBLE,XP2CHGDT     GET YYDDD                  SBG 02/00
         OI    DOUBLE+7,X'0F'                                 SBG 02/00
         CLC   XP2CHGDT(2),=C'66'  BELOW 19XX WINDOW?         DRK JUN98
         BNL   *+8                 NO,  THEN 19XX             DRK JUN98
         OI    DOUBLE+4,X'01'      YES, THEN 20XX             DRK JUN98
         MVC   SPFCHGDT,DOUBLE+4   DATE LAST MODIFIED         SBG 02/00
         PACK  DOUBLE,XP2HHMM                                 SBG 02/00
         L     R0,DOUBLE+4         GET 000HHMMC               SBG 02/00
         SRL   R0,4                GET 0000HHMM               SBG 02/00
         STH   R0,SPFHHMM          TIME LAST MODIFIED         SBG 02/00
         PACK  DOUBLE,XP2CCNT      SIZE                       SBG 02/00
         CVB   R0,DOUBLE                                      SBG 02/00
         STH   R0,SPFCCNT          CURRENT SIZE               SBG 02/00
         PACK  DOUBLE,XP2ICNT      SIZE                       SBG 02/00
         CVB   R0,DOUBLE                                      SBG 02/00
         STH   R0,SPFICNT          INITIAL SIZE               SBG 02/00
         PACK  DOUBLE,XP2MOD       MOD                        SBG 02/00
         CVB   R0,DOUBLE                                      SBG 02/00
         STH   R0,SPFMOD           LINES MODIFIED             SBG 02/00
         MVC   SPFUSER,=CL10' '    PAD WITH BLANKS            SBG 02/00
         B     *+10                                           SBG 02/00
         MVC   SPFUSER(0),XP2USER                             SBG 02/00
         EX    R15,*-6                                        SBG 02/00
         OI    STATUS,SPF          SET FLAG FOR STOW          SBG 02/00
         DROP  R6                                             SBG 02/00
NOSPF    DS    0H
NOUSER   DS    0H
         B     READ
         SPACE
ILLMEM   MVC   MEMBER,=CL8' '      SUPPRESS COPY
         B     COPY
         SPACE
PACK     ST    R14,PACKR
         LR    R15,R1              REG 15 --> SENDING/RECEIVING FIELD
         SR    R14,R14
         IC    R14,0(,R1)          REG 14  =  1ST CHAR
         CLI   0(R1),C'0'          NUMBER OR LETTER?
         BNL   *+8                 NUMBER - BRANCH
         LA    R14,57(,R14)        LETTER - CONVERT TO FA-FF
         SLL   R14,4               SHIFT LEFT 4 BITS
         STC   R14,0(,R15)         STORE THE LEFT HALF
         IC    R14,1(,R1)          REG 14  =  2ND CHAR
         CLI   1(R1),C'0'          NUMBER OR LETTER?
         BNL   *+8                 NUMBER - BRANCH
         LA    R14,57(,R14)        LETTER - CONVERT
         SLL   R14,28              SHIFT LEFT HALF TO OBLIVION
         SRL   R14,28              SHIFT BACK AGAIN
         STC   R14,1(,R15)         STORE RIGHT HALF
         OC    0(1,R15),1(R15)     'OR' RIGHT HALF OVER LEFT HALF
         LA    R1,2(,R1)           INCREMENT SENDING FIELD
         LA    R15,1(,R15)         INCREMENT RECEIVING FLD
         BCT   R0,PACK+6           LOOP USING LENGTH IN REG 0
         L     R14,PACKR
         BR    R14                 EXIT
         SPACE
MEMMATCH STM   R14,R12,12(R13)     SAVE REGISTERS
         LA    R3,MEMBER           POINT TO MEMBER NAME
         LA    R4,MEMSEL           POINT TO SELECTION MASK
         LA    R0,8                GET MEMBER NAME LENGTH
MASKLOOP CLI   0(R4),C'*'          GENERIC CHARACTER MATCH?
         BE    MASKNEXT            YES, THIS CHARACTER MATCHES
         CLI   0(R4),C'?'          GENERIC CHARACTER MATCH?
         BE    MASKNEXT            YES, THIS CHARACTER MATCHES
         CLI   0(R4),C'%'          GENERIC CHARACTER MATCH?
         BE    MASKNEXT            YES, THIS CHARACTER MATCHES
         CLC   0(1,R4),0(R3)       SPECIFIC CHARACTER MATCH?
         BNE   MASKEXIT            NO, UNSUCCESSFUL MATCH
MASKNEXT LA    R3,1(,R3)           POINT TO NEXT BYTE OF ITEM
         LA    R4,1(,R4)           POINT TO NEXT BYTE OF MASK
         BCT   R0,MASKLOOP         TEST NEXT BYTE
MASKEXIT LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         EJECT
************************************************************
*                                                          *
*         PROCESS THE ./ ALIAS STATEMENT                   *
*                                                          *
************************************************************
         SPACE
NOTADD   CLC   4(2,R6),=H'5'       LENGTH OF 'ALIAS'?
         BNE   COPY
         L     R1,0(,R6)           POINT TO OPERAND
         CLC   0(5,R1),=C'ENDUP'   END OF IEBUPDTE-LIKE INPUT?
         BE    EODUT1              YES, SIMULATE END-OF-FILE
         CLC   0(5,R1),=C'ALIAS'
         BNE   COPY
         LA    R6,ODE3             POINT TO THIRD O.D.E.
         TM    6(R6),X'80'         ANYTHING PRESENT?
         BZ    COPY                BRANCH IF NO OPERAND
         LH    R15,4(,R6)          GET LENGTH OF STRING
         SH    R15,=H'5'           LENGTH OF 'NAME='
         BNP   COPY
         L     R1,0(,R6)           POINT TO OPERAND
         CLC   0(5,R1),=C'NAME='
         BNE   COPY
         LA    R1,5(,R1)
         LA    R0,8                MAX VALID LENGTH
         CR    R15,R0              IS STRING TOO LONG ?
         BH    COPY                BRANCH IF TOO LONG
         MVC   MEMBER,=CL8' '
         BCTR  R15,0               GET LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   MEMBER(0),0(R1)     <<< EXECUTED >>>
         EX    R15,*-6             MOVE NAME TO MEMBER NAME
         TRT   MEMBER,ALPHANUM     IS MEMBER NAME VALID
         BNZ   ILLMEM              NO, BRANCH
         CLI   MEMBER,C'0'         IS FIRST CHAR NUMERIC?
         BNL   ILLMEM              YES, BRANCH
         CLI   MEMBER,C'-'         IS FIRST CHAR HYPHEN?
         BE    ILLMEM
         MVC   LINE,LINE-1
         MVC   LINE+32(05),=C'ALIAS'
         MVC   LINE+39(08),MEMBER
         LA    R6,MEMBER
         OI    11(R6),X'80'        SET ALIAS BIT ON
         STOW  (R5),(R6),R
         LA    R14,ALIASR          RETURN ADDRESS FROM STOWAR
         ST    R14,STOWR           RETURN ADDRESS FROM STOWAR
         B     STOWAR(R15)
ALIASR   MVC   MEMBER,=CL8' '
         B     READ
         EJECT
************************************************************
*                                                          *
*        ADD OR REPLACE A MEMBER                           *
*                                                          *
************************************************************
         SPACE
STOW     DS    0H
         ST    R14,STOWR
         TM    STATUS,DECB         ANY WRITES OUTSTANDING?
         BZ    STOWW               NO, BRANCH
         L     R1,PUTPDSY+12       GET LAST DECB
         CHECK (1)
         NI    STATUS,255-DECB     NO OUTSTANDING WRITES
STOWW    LM    R6,R9,PUTPDSX       GET WRITE REGISTERS
         SR    R7,R6               GET LENGTH OF FINAL BLOCK
         SH    R7,DWLOT            ACCOUNT FOR ANY BDW
         BNP   STOWSET             BRANCH IF NOTHING IN BUFFER
         LH    R8,BLKSI(,R5)       SAVE ORIGINAL BLKSIZE
         CLI   DWLOT+1,4           VARIABLE-LENGTH OUTPUT RECORDS?
         BE    *+8                 YES, LENGTH IN DATA, NOT DCB
         STH   R7,BLKSI(,R5)       NO, STORE FINAL BLOCK LENGTH IN DCB
         WRITE (R9),SF,(R5),(R6),MF=E
         CHECK (R9)
         STH   R8,BLKSI(,R5)       RESTORE DCB BLKSIZE FOR NEXT MEMBER
STOWSET  L     R1,PUTPDSX
         AH    R1,DWLOT            ALLOW FOR OUTPUT BDW
         ST    R1,PUTPDSX+4
         L     R1,PUTPDSY
         AH    R1,DWLOT            ALLOW FOR OUTPUT BDW
         ST    R1,PUTPDSY+4
         MVC   LINE,LINE-1
         CLI   MEMBER,C' '         IS THERE A MEMBER NAME?
         BE    STOWX               NO, BRANCH
         TM    FLAG1,SELECT        WAS THIS MEMBER SELECTED?
         BZ    STOWX               NO, BRANCH
         MVI   MEMC,0
         TM    STATUS,SPF          IS THERE SPF USERDATA?
         BZ    STOWNSPF            NO, BRANCH
         MVI   MEMC,15             LENGTH OF USER DATA IN HALFWORDS
         CLC   SPFCCNT,SEQ+2       IS THE RECORD COUNT CORRECT?
         BE    STOWNSSI            YES
         MVC   SPFCCNT,SEQ+2       NO, CORRECT IT
         OI    FLAG1,FIXSZ         FLAG CORRECTION
         B     STOWNSSI
STOWNSPF DS    0H
         TM    FLAG1,GENSPF        SPF STATS TO BE GENERATED?
         BZ    STOWNGEN            NO, BRANCH
         MVI   MEMC,15             LENGTH OF USER DATA IN HALFWORDS
         XC    MEMUSER(30),MEMUSER
         MVI   SPFVM,1             1.0
         XC    SPFRESV,SPFRESV     ZERO RESERVED BYTES
         TIME  DEC
         ST    R1,SPFCREDT         DATE CREATED
         ST    R1,SPFCHGDT         DATE OF LAST CHANGE
         SRL   R0,16
         STH   R0,SPFHHMM          TIME OF LAST CHANGE
         L     R1,SEQ              SIZE
         STH   R1,SPFICNT
         STH   R1,SPFCCNT
         MVC   SPFUSER,=CL10'PDSLOAD'
         B     STOWNSSI
STOWNGEN DS    0H
         TM    STATUS,SSI          IS THERE SSI USERDATA?
         BZ    STOWNSSI            NO, BRANCH
         MVI   MEMC,2              LENGTH OF USERDATA SSI IN HALFWORDS
STOWNSSI DS    0H
         MVC   LINE+32(06),=C'MEMBER'
         MVC   LINE+39(08),MEMBER
         LA    R6,MEMBER
         XC    8(03,R6),8(R6)      TTR
         SPACE
         STOW  (R5),(R6),R
         SPACE
         CLM   R15,3,=X'0018'      UNEXPECTED RETURN CODE?
         BH    STOWPDSE            YES, PROBABLY SOME PDSE WORRY
         B     STOWAR(R15)          R             A
STOWAR   B     STOWAR00             REPLACED      ADDED
         B     STOWAR04             N/A           NOT ADDED, EXISTS
         B     STOWAR08             ADDED         N/A
         B     STOWAR0C
         B     STOWAR10
         B     STOWAR14
         B     STOWAR18
         SPACE
RESULT   EQU   LINE+48
         SPACE
STOWAR00 TRT   LINE+39(9),TABBLANK
         MVC   1(8,R1),=CL8'REPLACED'
         LA    R1,9(,R1)
         B     STOWCOMW
STOWAR04 MVC   RESULT(23),=CL32'NOT ADDED *** ALREADY EXISTS ***'
         B     STOWCOM
STOWAR08 TRT   LINE+39(9),TABBLANK
         MVC   1(5,R1),=CL5'ADDED'
         LA    R1,6(,R1)
         B     STOWCOMW
STOWAR0C MVC   RESULT(33),=CL33'NOT STOWED *** DIRECTORY FULL ***'
         B     STOWCOM
STOWAR10 MVC   RESULT(28),=CL28'NOT STOWED *** I/O ERROR ***'
         B     STOWCOM
STOWAR14 MVC   RESULT(28),=CL28'NOT STOWED *** DCB ERROR ***'
         B     STOWCOM
STOWAR18 MVC   RESULT(32),=CL32'NOT STOWED *** STORAGE ERROR ***'
         B     STOWCOM
STOWPDSE MVC   RESULT(29),=CL29'NOT STOWED *** PDSE ERROR ***'
         SPACE
STOWCOM  DS    0H
         MVI   RC+1,4
         BAL   R14,REPORT          OUTPUT PRINT LINE
         B     STOWX               THAT IS ALL FOR THIS MEMBER
STOWCOMW DS    0H
         TM    MEMC,X'80'          WAS THAT AN ALIAS?
         BO    STOWXA              YES, BRANCH
         ST    R1,STOWSVR1
         L     R15,SEQ
         CVD   R15,DOUBLE
         MVC   1(7,R1),=X'4020206B202120'
         ED    1(7,R1),DOUBLE+5
         MVI   1(R1),C'('
         LA    R1,2(,R1)
STOWCOMJ CLI   0(R1),C' '
         BNE   STOWCOMK
         MVC   0(7,R1),1(R1)
         B     STOWCOMJ
STOWCOMK LA    R1,1(,R1)
         CLI   0(R1),C' '
         BNE   STOWCOMK
         MVC   1(08,R1),=C'RECORDS)'
         CH    R15,=H'1'           ONE RECORD?
         BNE   STOWCHEK            NO, TEXT IS GOOD
         MVC   7(2,R1),8(R1)       CHANGE RECORDS TO RECORD
         BCTR  R1,0                ADJUST FOR TEXT LENGTH CHANGE
STOWCHEK TM    FLAG1,FIXSZ         WAS THE SPF STATISTICS SIZE FIXED?
         BZ    STOWREPT            NO
         NI    FLAG1,255-FIXSZ     YES, RESET FLAG
         MVC   8(15,R1),=C' - STATS FIXED)'
STOWREPT BAL   R14,REPORT
         L     R15,UPR
         LTR   R15,R15
         BZ    STOWX
         MVC   LINE,LINE-1
         L     R1,STOWSVR1
         CVD   R15,DOUBLE
         MVC   1(7,R1),=X'4020206B202120'
         ED    1(7,R1),DOUBLE+5
         MVI   1(R1),C'('
         LA    R1,2(,R1)
STOWUPRJ CLI   0(R1),C' '
         BNE   STOWUPRK
         MVC   0(7,R1),1(R1)
         B     STOWUPRJ
STOWUPRK LA    R1,1(,R1)
         CLI   0(R1),C' '
         BNE   STOWUPRK
         MVC   1(09,R1),=C'MODIFIED)'
STOWXA   BAL   R14,REPORT
STOWX    L     R14,STOWR
         BR    R14
         SPACE
UNDEFBAD MVC   LINE,LINE-1
         MVC   LINE+1(UNDMSGLN),UNDEFMSG
FAILEXIT BAL   R14,REPORT          REPORT REASON FOR TERMINATION
         LA    R15,16
         B     EXIT
         EJECT
************************************************************
*                                                          *
*         REPORT WRITER                                    *
*                                                          *
************************************************************
         SPACE
REPORT   LA    R1,LINE
         LA    R0,121
REPORTW  STM   R14,R2,REPORTS
         LA    R3,PRTDCBW          POINT R3 TO DCB
         CP    REPORTLN,REPORTMX   IS LINECOUNT LIMIT REACHED?
         BNH   *+10                NO
         ZAP   REPORTLN,=P'0'      YES, FORCE NEW PAGE
         CP    REPORTLN,=P'0'      IS NEW PAGE REQUESTED?
         BE    REPORTH             YES, GO PRINT HEADING
REPORTD  CH    R0,=H'121'          IS OUTPUT LINE LENGTH OK?
         BNL   REPORTP             YES, BRANCH
         MVC   REPORTO,REPORTO-1   BLANK THE WORK AREA
         BCTR  R14,0               LENGTH MINUS 1
         B     *+10
         MVC   REPORTO(0),0(R1)    COPY OUTPUT LINE
         EX    R14,*-6             EXECUTE MVC
REPORTB  LA    R1,REPORTO          POINT TO NEW OUTPUT LINE
REPORTP  LR    R2,R1               POINT R2 TO OUTPUT LINE
         PUT   (R3),(R2)           WRITE OUTPUT LINE
         AP    REPORTLN,=P'1'      INCREMENT LINE COUNTER
REPORTX  LM    R14,R2,REPORTS      RESTORE REGS
         BR    R14                 RETURN
REPORTH  AP    REPORTPG,=P'1'      INCREMENT PAGE COUNTER
         MVC   REPORTO,REPORTO-1   BLANK HEADING
         MVI   REPORTO,C'1'        CC = NEW PAGE
         MVC   REPORTO+1(L'HEAD1),HEAD1
         LA    R1,REPORTO+075-9    RIGHT EDGE PAGE NO
         MVC   3(6,R1),=X'402020202020' EDIT MASK
         ED    3(6,R1),REPORTPG    UNPACK PAGE NO
         MVC   0(4,R1),=C'PAGE'    INSERT 'PAGE'
         PUT   (R3),REPORTO        PUT HEADING LINE 1
         MVC   REPORTO,REPORTO-1   BLANK LINE
         PUT   (R3),REPORTO        PUT HEADING LINE 2
         LM    R0,R1,REPORTS+8     RESTORE R0 AND R1
         B     REPORTD             GO PRINT DETAIL LINE
         EJECT
************************************************************
*                                                          *
*         END OF FILE                                      *
*                                                          *
************************************************************
         SPACE
EODUT1   DS    0H
         BAL   R14,STOW
EXITRC   LH    R15,RC
EXIT     LR    R2,R15              SAVE RETURN CODE
         LA    R6,CLOSE
         MVI   0(R6),X'80'
         TM    STATUS,UT2          IS SYSUT2 OPEN?
         BZ    EXITC5X             NO, SKIP CLOSE
         CLOSE ((R5)),MF=(E,(R6))
         FREEPOOL (R5)
         TM    STATUS,PDS
         BZ    EXITC5X
         LM    R0,R1,FREEM         GET LENGTH AND LOCATION OF BUFFERS
         FREEMAIN R,LV=(0),A=(1)
EXITC5X  DS    0H
         TM    STATUS,UT1          IS SYSUT1 OPEN?
         BZ    EXITC4X             NO, SKIP CLOSE
         CLOSE ((R4)),MF=(E,(R6))
         FREEPOOL (R4)
EXITC4X  DS    0H
         TM    STATUS,PRT          IS SYSPRINT OPEN?
         BZ    EXITC3X             NO, SKIP CLOSE
         LA    R3,PRTDCBW          POINT R3 TO DCB
         CLOSE ((R3)),MF=(E,(R6))
         FREEPOOL (R3)
EXITC3X  DS    0H
         SPACE 1
         LR    R1,R13
         L     R0,@SIZE
         L     R13,4(,R13)
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2              RESTORE RETURN CODE
         LM    R0,R12,20(R13)
         L     R14,R12(,R13)
         BR    R14
         EJECT
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         DS    0F
UT1EXLST DC    AL1(X'85'),AL3(UT1EXO)  DCB EXIT LIST FOR SYSUT1
PDSEXLST DC    AL1(X'85'),AL3(UT2EXO)  DCB EXIT LIST FOR SYSUT2
PRTEXLST DC    AL1(X'85'),AL3(PRTEXO)  DCB EXIT LIST FOR SYSPRINT
         LTORG
         SPACE
HEAD1    DC    C'--- PDSLOAD --- PDS MEMBER RELOAD UTILITY ---'
         SPACE
FIXINMSG DC    C'    PDSLOAD TERMINATED - '
         DC    C'VARIABLE-LENGTH OUTPUT REQUIRES VARIABLE-LENGTH INPUT'
FIXMSGLN EQU   *-FIXINMSG
         SPACE
UNDEFMSG DC    C'    PDSLOAD TERMINATED - '
         DC    C'UNDEFINED RECORD FORMAT NOT SUPPORTED'
UNDMSGLN EQU   *-UNDEFMSG
         SPACE
SHORTMSG DC    C'    PDSLOAD TERMINATED - '
         DC    C'INPUT FILE LOGICAL RECORD LENGTH LESS THAN 80'
SHOMSGLN EQU   *-SHORTMSG
         SPACE
         PRINT NOGEN
         SPACE
PRTDCB   DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,EXLST=PRTEXLST,     +
               RECFM=FBA,LRECL=121
PRTDCBL  EQU   *-PRTDCB
         SPACE
UT1DCB   DCB   DDNAME=SYSUT1,MACRF=(GL),DSORG=PS,                      +
               EODAD=EODUT1,EXLST=UT1EXLST
UT1DCBL  EQU   *-UT1DCB
         SPACE
PDSDCB   DCB   DDNAME=SYSUT2,MACRF=(W),DSORG=PO,                       +
               EXLST=PDSEXLST,BUFNO=2
PDSDCBL  EQU   *-PDSDCB
         SPACE
         PRINT GEN
         SPACE
         WRITE PDSDECB,SF,MF=L
PDSDECBL EQU   *-PDSDECB
         SPACE
TABNONBL DC    64X'FF'
         DC    X'00'               BLANK
         DC    42X'FF'
         DC    X'00'               COMMA
         DC    148X'FF'
TABBLANK DC    64X'00'
         DC    X'40'               BLANK
         DC    42X'00'
         DC    X'6B'               COMMA
         DC    148X'00'
         SPACE
ALPHANUM DC    0D'0',64X'FF',X'00',26X'FF'
*                            SPACE
         DC    X'00',4X'FF',X'00',26X'FF',2X'00',68X'FF',09X'00'
*                $            -              #@           A-I
         DC    7X'FF',9X'00',8X'FF',8X'00',6X'FF',10X'00',6X'FF'
*                       J-R            S-Z            0-9
         SPACE
*              THE ABOVE TABLE CAN BE USED TO TEST FOR ALPHANUMERIC
*              (PLUS NATIONALS, HYPHEN, AND BLANK)
*              WITH A 'TRT' INSTRUCTION.
*              IF THE FIELD IS VALID, CONDITION CODE IS 0.
*              WARNING: 'TRT' CAN CHANGE THE LOW ORDER 8 BITS
*              OF REGISTER 2 AND LOW ORDER 24 BITS OF REG 1.
         SPACE
NUMERIC  DC    240X'FF',10X'00',6X'FF'
         SPACE
EODAD    EQU   32                  OFFSET INTO DCB
RECFM    EQU   36                  OFFSET INTO DCB
EXLST    EQU   36                  OFFSET INTO DCB
OFLGS    EQU   48                  OFFSET INTO DCB
DDNAM    EQU   40                  OFFSET INTO DCB
BLKSI    EQU   62                  OFFSET INTO DCB
LRECL    EQU   82                  OFFSET INTO DCB
         EJECT
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
OPEN     DS    F
CLOSE    EQU   OPEN
COUNTIN  DS    F
SEQ      DS    F
UPR      DS    F
STOWSVR1 DS    F
OPTIONS  DS    H
RC       DS    H
UPDTE    DS    CL2
PRTDCBW  DS    0F,(PRTDCBL)X
UT1DCBW  DS    0F,(UT1DCBL)X
PDSDCBW  DS    0F,(PDSDCBL)X
FREEM    DS    2F
PUTPDSR  DS    F
PUTPDSX  DS    4F
PUTPDSY  DS    4F
RECAD    DS    F
DWLIN    DS    H
DWLOT    DS    H
SKIPFLG  DS    X                                              SBG 02/00
PDSDECB1 DS    0F,(PDSDECBL)X
PDSDECB2 DS    0F,(PDSDECBL)X
STOWR    DS    F
PACKR    DS    F
INREC    DS    0D,CL80
ADD      DS    CL256
ODL      DS    0F                  OPERAND DESCRIPTOR LIST
ODE1     DS    2F                  OPERAND DESCRIPTOR ENTRY 1
ODE2     DS    2F                  OPERAND DESCRIPTOR ENTRY 2
ODE3     DS    2F                  OPERAND DESCRIPTOR ENTRY 3
ODE4     DS    2F                  OPERAND DESCRIPTOR ENTRY 4
ODE5     DS    2F                  OPERAND DESCRIPTOR ENTRY 5
ODLL     EQU   *-ODL
STATUS   DS    C
PRT      EQU   X'80'
UT1      EQU   X'40'
UT2      EQU   X'20'
PDS      EQU   X'10'
DECB     EQU   X'08'
COPYM    EQU   X'04'
SPF      EQU   X'02'
SSI      EQU   X'01'
FLAG1    DS    C
SELECT   EQU   X'80'
GENSPF   EQU   X'40'               GENERATE SPF STATS
NEWPRM   EQU   X'20'               PARM=NEW WAS SPECIFIED
FIXSZ    EQU   X'10'               SPF STATS SIZE WAS CORRECTED
         DS    C                   LINE-1
LINE     DS    CL133
LINEH1   DS    CL133
*
MEMSEL   DS    D
MEMBER   DS    D
MEMTTR   DS    XL3
MEMC     DS    XL1                 LENGTH/2 OF MEMUSER
MEMUSER  DS    CL30                SPF STATISTICS OR SSI
         ORG   *-30
SPFVM    DS    XL2                 VERSION, LEVEL
SPFRESV  DS    XL2                 RESERVED
SPFCREDT DS    PL4                 DATE CREATED
SPFCHGDT DS    PL4                 DATE LAST UPDATED
SPFHHMM  DS    XL2                 TIME LAST UPDATED
SPFCCNT  DS    H                   CURRENT SIZE
SPFICNT  DS    H                   INITIAL SIZE
SPFMOD   DS    H                   MODS
SPFUSER  DS    CL10                USERID
*
REPORTS  DS    6F                  REGISTER SAVE AREA
REPORTPG DS    PL3                 PAGE COUNT, INIT P'0'
REPORTLN DS    PL2                 LINE COUNT, INIT P'0'
REPORTMX DS    PL2                 LINES/PAGE, INIT P'50'
REPORTOB DS    CL1                 REPORTO-1 (INIT BLANK)
REPORTO  DS    CL133               OUTPUT AREA
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
XPF      DSECT
XPFVM    DS    CL4,C               VERSION, LEVEL
XPFCREDT DS    CL5,C               DATE CREATED
XPFCHGDT DS    CL5,C               DATE LAST UPDATED
XPFHHMM  DS    CL4,C               TIME LAST UPDATED
XPFCCNT  DS    CL5,C               CURRENT SIZE
XPFICNT  DS    CL5,C               INITIAL SIZE
XPFMOD   DS    CL5,C               MODS
XPFUSER  DS    CL10                USERID
XP2      DSECT
XP2VM    DS    CL4,C               VERSION, LEVEL
         DS    CL2                 EXTRA LETTERS FOR YEAR
XP2CREDT DS    CL5,C               DATE CREATED
         DS    CL2                 EXTRA LETTERS FOR YEAR
XP2CHGDT DS    CL5,C               DATE LAST UPDATED
XP2HHMM  DS    CL4,C               TIME LAST UPDATED
XP2CCNT  DS    CL5,C               CURRENT SIZE
XP2ICNT  DS    CL5,C               INITIAL SIZE
XP2MOD   DS    CL5,C               MODS
XP2USER  DS    CL10                USERID
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
