SP213MPL TITLE 'ERBUSMPL -- RMF USER MPL ADJUSTMENT CONSTANTS'
***********************************************************************
**                                                                   **
**     THIS MODULE MUST BE LINK EDITED INTO MODULE "ERBRUS99" AS     **
**     "RENT".  THIS ASSEMBLY REQUIRES THE "AAPVT" COMPONENT FROM    **
**     THE SYSTEM OPTIONAL MACHINE READABLE MATERIAL AS A MACLIB.    **
**                                                                   **
**     MVS/SP2.1.3                                                   **
**                                                                   **
***********************************************************************
ERBUSMPL CSECT
ERBUSMPL AMODE 31
ERBUSMPL RMODE ANY
         USING *,15
         B     BYMODULE
         DC    AL1(31)
         DC    CL8'ERBUSMPL'
         DC    CL7' &SYSTIME'
         DC    CL8'&SYSDATE'
         DC    C' MVS/SP213'
         PRINT NOGEN
BYMODULE SAVE  (14,12)
         USING GETMAINA,R13
         LA    R1,SAVEARE1
         ST    R1,8(,R13)     SAVE FORWARD POINTER
         ST    R13,4(,R1)     SAVE BACKWARD POINTER
         LR    R13,R1         SET UP REGISTER FOR SAVE AREA
         DROP  R13
         USING SAVEARE1,R13
         DROP  R15
         BALR  R12,0
         USING *,R12
         L     R11,RMCTA      POINTER TO THE RMCT
         USING RMCT,R11
         CLC   =CL4'RMCT',RMCTNAME IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         SPACE 2
*     THE FIRST SECTION OF THIS REPORT IS FROM THE "IRARMRMR" MODULE.
         SPACE 1
         L     R10,RMCTRCT    POINTER TO THE RCT
         USING RCT,R10
*        MVC   DUMPER(RCTLEN),RCT ****** FOR DEBUGGING ONLY ***********
*        LA    R10,DUMPER         ****** FOR DEBUGGING ONLY ***********
         CLC   =CL4'RCT',RCTRCT IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         MVC   PUTLHDRL,HDRLHDA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         L     R5,RMCTEPBG
         USING EPAT,R5
*        MVC   DUMPER(255),EPAT   ****** FOR DEBUGGING ONLY ***********
*        MVC   DUMPER+255(255),EPAT+255 ***** FOR DEBUGGING ONLY ******
*        MVC   DUMPER+510(255),EPAT+510 ***** FOR DEBUGGING ONLY ******
*        MVC   DUMPER+765(255),EPAT+765 ***** FOR DEBUGGING ONLY ******
         LA    R5,RMEPBRM2
         DROP  R5
         MVI   PRTLINE,C' '  HEADING SECTION -- MPL INVOCATION INTERVAL
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG(24),=CL24'MPL ADJUSTMENT'
         MVC   M2IND,=CL4' SEC'
         MVC   M2AVG,MASKL6
         L     R4,36(R5)
         CVD   R4,AVGVALUE
         SRP   AVGVALUE,64-3,5
         ED    M2AVG,AVGVALUE+5
         MVC   M2HT+2(5),=CL5'EVERY'
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         SPACE 3
         BAL   R4,PUTLINE
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,MPLHDR2
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- PAGE FAULT RATE
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVPTR
         CVD   R5,AVGVALUE
         ED    M1AVG,AVGVALUE+5
         MVC   M1HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCPTRTH
         CVD   R5,HIGTHRES
         ED    M1HT,HIGTHRES+5
         MVC   M1LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCPTRTL
         CVD   R5,LOWTHRES
         ED    M1LT,LOWTHRES+5
         MVC   M1VAR,=CL19'PAGE FAULT RATE'
         BAL   R4,SETINDIC
         MVC   M1IND,PINDHOLD
         MVC   M2AVG,MASKL6        PRINT SECTION -- UNREF. INTVL. COUNT
         LH    R5,RCVUICA     CURRENT AVERAGE
         CVD   R5,AVGVALUE
         ED    M2AVG,AVGVALUE+5
         MVC   M2HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCUICTH
         CVD   R5,HIGTHRES
         ED    M2HT,HIGTHRES+5
         MVC   M2LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCUICTL
         CVD   R5,LOWTHRES
         ED    M2LT,LOWTHRES+5
         MVC   M2VAR,=CL19'UNREF. INTVL. COUNT'
         BAL   R4,SETINDID
         MVC   M2IND,PINDHOLD
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- AUXSTM QUEUE LENGTH
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVASMQA
         CVD   R5,AVGVALUE
         ED    M1AVG,AVGVALUE+5
         MVC   M1HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCASMTH
         CVD   R5,HIGTHRES
         ED    M1HT,HIGTHRES+5
         MVC   M1LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCASMTL
         CVD   R5,LOWTHRES
         ED    M1LT,LOWTHRES+5
         MVC   M1VAR,=CL19'AUXSTM QUEUE LENGTH'
         BAL   R4,SETINDIC
         MVC   M1IND,PINDHOLD
*                                  PRINT SECTION -- DPR & (CPU | PD)
         LH    R5,RCVMSPP     PAGE DELAY
         CH    R5,RCCMSPTH
         BNH   *+10
         MVC   M2AVG,=CL6' &&PD'
         LH    R5,RCVCPUA     CPU UTILIZATION
         CH    R5,RCCCPUPH
         BNH   *+10
         MVC   M2HT,=CL6' &&CPU'
         LH    R5,RCVDPR      DEMAND PAGING RATE
         CH    R5,RCCDPRTH
         BNH   *+10
         MVC   M2LT,=CL6'  DPR'
         CLC   M2LT,=CL6' '     SET INDICATOR?
         BE    DPRPDEND
         CLC   M2HT,=CL6' '
         BNE   DPRPDMVC
         CLC   M2AVG,=CL6' '
         BE    DPRPDEND
DPRPDMVC MVC   M2IND,=C'<***'
DPRPDEND MVC   M2VAR,=CL19'DPR && (CPU | PD)'
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- DEFERRED I/O UTIL.
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
*        MVC   M1AVG,MASKL6   CURRENT AVERAGE
*        LH    R5,RCVTOTDF
*        CVD   R5,AVGVALUE
*        ED    M1AVG,AVGVALUE+5
*        MVC   M1HT,MASKL6    HIGH THRESHOLD
*        LH    R5,RCCTOTUT
*        CVD   R5,HIGTHRES
*        ED    M1HT,HIGTHRES+5
*        ZAP   LOWTHRES,HIGTHRES
*        MVC   M1VAR,=CL19'DEFERRED I/O UTIL.'
*        BAL   R4,SETINDIC
*        MVC   M1IND,PINDHOLD
         MVC   M2AVG,MASKL6        PRINT SECTION -- DEMAND PAGING RATE
         LH    R5,RCVDPR      CURRENT AVERAGE
         CVD   R5,AVGVALUE
         ED    M2AVG,AVGVALUE+5
         MVC   M2HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCDPRTH
         CVD   R5,HIGTHRES
         ED    M2HT,HIGTHRES+5
         MVC   M2LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCDPRTL
         CVD   R5,LOWTHRES
         ED    M2LT,LOWTHRES+5
         MVC   M2VAR,=CL19'DEMAND PAGING RATE'
         BAL   R4,SETINDIC
         MVC   M2IND,PINDHOLD
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- CPU UTILIZATION
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVCPUA
         SRA   R5,4 /16
         CVD   R5,AVGVALUE
         ED    M1AVG,AVGVALUE+5
         MVC   M1HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCCPUTH
         SRA   R5,4 /16
         CVD   R5,HIGTHRES
         ED    M1HT,HIGTHRES+5
         MVC   M1LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCCPUTL
         SRA   R5,4 /16
         CVD   R5,LOWTHRES
         ED    M1LT,LOWTHRES+5
         MVC   M1VAR,=CL19'CPU UTILIZATION'
         BAL   R4,SETINDIC
         MVC   M1IND,PINDHOLD
         MVC   M2AVG,MASKL6        PRINT SECTION -- CPU & DEMAND PAGING
         ED    M2AVG,AVGVALUE+5 CURRENT AVERAGE
         MVC   M2HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCCPUPH
         SRA   R5,4 /16
         CVD   R5,HIGTHRES
         ED    M2HT,HIGTHRES+5
         MVC   M2LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCCPUPL
         SRA   R5,4 /16
         CVD   R5,LOWTHRES
         ED    M2LT,LOWTHRES+5
         MVC   M2VAR,=CL19'CPU && DEMAND PAGING'
         BAL   R4,SETINDIC
         MVC   M2IND,PINDHOLD
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- PAGE DELAY(MS)
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVMSPP
         CVD   R5,AVGVALUE
         ED    M1AVG,AVGVALUE+5
         MVC   M1HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCPDLTH
         CVD   R5,HIGTHRES
         ED    M1HT,HIGTHRES+5
         MVC   M1LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCPDLTL
         CVD   R5,LOWTHRES
         ED    M1LT,LOWTHRES+5
         MVC   M1VAR,=CL19'PAGE DELAY(MS)'
         BAL   R4,SETINDIC
         MVC   M1IND,PINDHOLD
         MVC   M2AVG,MASKL6        PRINT SECTION -- PG DLAY & DMAND PG
         ED    M2AVG,AVGVALUE+5 CURRENT AVERAGE
         MVC   M2HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCMSPTH
         CVD   R5,HIGTHRES
         ED    M2HT,HIGTHRES+5
         MVC   M2LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCMSPTL
         CVD   R5,LOWTHRES
         ED    M2LT,LOWTHRES+5
         MVC   M2VAR,=CL19'PG DLAY && DMAND PG'
         BAL   R4,SETINDIC
         MVC   M2IND,PINDHOLD
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- ONLINE STORAGE %
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVFXIOP
         CVD   R5,AVGVALUE
         ED    M1AVG,AVGVALUE+5
         MVC   M1HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCFXTTH
         CVD   R5,HIGTHRES
         ED    M1HT,HIGTHRES+5
         MVC   M1LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCFXTTL
         CVD   R5,LOWTHRES
         ED    M1LT,LOWTHRES+5
         MVC   M1VAR,=CL19'ONLINE STORAGE %'
         BAL   R4,SETINDIC
         MVC   M1IND,PINDHOLD
*                                  PRINT SECTION -- STORAGE BELOW 16M
         MVC   M2AVG,MASKL6   CURRENT AVERAGE
         LH    R5,RCVMFXA
         CVD   R5,AVGVALUE
         ED    M2AVG,AVGVALUE+5
         MVC   M2HT,MASKL6    HIGH THRESHOLD
         LH    R5,RCCFXETH
         CVD   R5,HIGTHRES
         ED    M2HT,HIGTHRES+5
         MVC   M2LT,MASKL6    LOW THRESHOLD
         LH    R5,RCCFXETL
         CVD   R5,LOWTHRES
         ED    M2LT,LOWTHRES+5
         MVC   M2VAR,=CL19'STORAGE BELOW 16M %'
         BAL   R4,SETINDIC
         MVC   M2IND,PINDHOLD
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- CONT. INDX EXCH. TH
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1HT+3(9),=XL9'402020202020212060'
         LH    R5,RCCDCITL
         CVD   R5,HIGTHRES
         ED    M1HT+3(9),HIGTHRES+4
         MVC   M1VAR,=CL19'CONT. INDX EXCH. TH'
*                                  PRINT SECTION -- NUMBER OF AVQ LOWS
         MVC   M2HT+3(9),=XL9'402020202020212060'
         LH    R5,RCVAVQC
         CVD   R5,HIGTHRES
         ED    M2HT+3(9),HIGTHRES+4
         MVC   M2VAR,=CL19'NUMBER OF AVQ LOWS'
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '        PRINT SECTION -- AVAILABLE FRAME AVG
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M1HT+3(9),=XL9'402020202020212060'
         LH    R5,RCVAFQA
         CVD   R5,HIGTHRES
         ED    M1HT+3(9),HIGTHRES+4
         MVC   M1VAR,=CL19'AVAILABLE FRAME AVG'
*                                  PRINT SECTION -- TOTAL PAGING RATE
         MVC   M2HT+3(9),=XL9'402020202020212060'
         LH    R5,RCVPAGRT
         CVD   R5,HIGTHRES
         ED    M2HT+3(9),HIGTHRES+4
         MVC   M2VAR,=CL19'TOTAL PAGING RATE'
         LA    R5,PRTDTL
         ST    R5,PUTLRECD
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,HEADER0
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         SPACE 3
         LA    R1,MPLHDR3
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVC   PUTLHDRL,HDRLHDA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         BAL   R4,PUTLINE
         L     R8,RMCTDMDT    LOAD ADDRESS OF THE FIRST DMDT
         USING DMDT,R8
*        MVC   DUMPER(255),DMDT   ****** FOR DEBUGGING ONLY ***********
DMDTLOOP C     R8,RMCTDMDE    IS THIS THE END OF THE TABLE?
         BH    DMDTLEND
         CLC   DMDTHI,=H'1'   *** DROP SPECIAL PURPOSE DOMAINS FROM ***
         BNE   DMDTLOP1       *** THE DISPLAY (MUCH SHORTER)        ***
         CLC   DMDTLO,=H'1'
         BNE   DMDTLOP1
         CLC   DMDTCMPL,=H'1'
         BE    DMDTLOP6
DMDTLOP1 MVI   PRTLINE,C' '        PRINT SECTION -- DEFERED I/O UTIL.
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         TM    DMDTFLGS,DMDTRTO RTO DOMAIN?
         BNO   DMDTLOP3
         L     R3,DMDTTRNC    SET UP TRANSACTION COUNT
         CVD   R3,AVGVALUE
         MVC   M3TRNC,MASKL4
         ED    M3TRNC,AVGVALUE+6
         CP    AVGVALUE,=P'0' PREVENT ZERO DIVIDE
         BE    DMDTLOP2
         L     R5,DMDTTRNT    SET UP CURRENT ELAPSED TIME AVERAGE
         LTR   R5,R5
         BZ    DMDTLOP2
         SR    R4,R4
         D     R4,DMDTTRNC
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-2,5
         MVC   M3TRNTC,MASKL5D
         ED    M3TRNTC,AVGVALUE+6
DMDTLOP2 L     R3,DMDTTWET    SET UP ELAPSED TIME AVERAGE FOR RTO
         LTR   R3,R3
         BZ    DMDTLOP3
         CVD   R3,AVGVALUE
         SRP   AVGVALUE,64-2,5
         MVC   M3TWET,MASKL5D
         ED    M3TWET,AVGVALUE+6
DMDTLOP3 L     R3,DMDTTWSR    SET UP WEIGHTED INTERVAL DOMAIN SERVICE
         CVD   R3,AVGVALUE
         MVC   M3TWSR,MASKL4
         ED    M3TWSR,AVGVALUE+6
         CP    AVGVALUE,=P'1000'
         BNH   DMDTLOP4
         SRP   AVGVALUE,64-3,5
         MP    AVGVALUE,=P'10'
         MVC   M3TWSR,MASKL4
         ED    M3TWSR,AVGVALUE+6
         MVI   M3TWSR+L'M3TWSR-1,C'K'
         CP    AVGVALUE,=P'10000'
         BNH   DMDTLOP4
         SRP   AVGVALUE,64-4,5
         MP    AVGVALUE,=P'10'
         MVC   M3TWSR,MASKL4
         ED    M3TWSR,AVGVALUE+6
         MVI   M3TWSR+L'M3TWSR-1,C'M'
DMDTLOP4 L     R3,DMDTWMS     SET UP INTERVAL DOMAIN SERVICE ACCUMUL
         CVD   R3,AVGVALUE
         MVC   M3TWMS,MASKL6
         ED    M3TWMS,AVGVALUE+5
         CP    AVGVALUE,=P'1000'
         BNH   DMDTLOP5
         SRP   AVGVALUE,64-3,5
         MP    AVGVALUE,=P'10'
         MVC   M3TWMS,MASKL6
         ED    M3TWMS,AVGVALUE+5
         MVI   M3TWMS+L'M3TWMS-1,C'K'
         CP    AVGVALUE,=P'10000'
         BNH   DMDTLOP5
         SRP   AVGVALUE,64-4,5
         MP    AVGVALUE,=P'10'
         MVC   M3TWMS,MASKL6
         ED    M3TWMS,AVGVALUE+5
         MVI   M3TWMS+L'M3TWMS-1,C'M'
DMDTLOP5 LH    R3,DMDTRUMX    SET UP MAX # READY USERS IN INTERVAL
         CVD   R3,AVGVALUE
         MVC   M3RUMX,MASKL4
         ED    M3RUMX,AVGVALUE+6
         LH    R3,DMDTNSW     SET UP CURRENT # NONSWAPPABLE IN USERS
         CVD   R3,AVGVALUE
         MVC   M3NSW,MASKL4
         ED    M3NSW,AVGVALUE+6
         LH    R3,DMDTOUTU    SET UP CURRENT # USERS ON OUT Q
         CVD   R3,AVGVALUE
         MVC   M3OUTU,MASKL4
         ED    M3OUTU,AVGVALUE+6
         LH    R3,DMDTGOOU    SET UP CURRENT # USERS GOING OUT
         CVD   R3,AVGVALUE
         MVC   M3GOOU,MASKL4
         ED    M3GOOU,AVGVALUE+6
         LH    R3,DMDTINCU    SET UP CURRENT # SWAPPABLE INCORE USERS
         CVD   R3,AVGVALUE
         MVC   M3INCU,MASKL4
         ED    M3INCU,AVGVALUE+6
         LH    R3,DMDTCMPL    SET UP CURRENT MPL
         CVD   R3,AVGVALUE
         MVC   M3DMPL,MASKL6
         ED    M3DMPL,AVGVALUE+5
         LH    R3,DMDTCIDX    SET UP CONTENTION INDEX
         CVD   R3,AVGVALUE
         MVC   M3CIDX,MASKL4
         ED    M3CIDX,AVGVALUE+6
         LH    R3,DMDTMPLT    SET UP CURRENT MPL TARGET
         CVD   R3,AVGVALUE
         MVC   M3MPLT,MASKL4
         ED    M3MPLT,AVGVALUE+6
         LH    R4,DMDTRUA     SET UP AVERAGE NUMBER OF READY USERS
         SRDA  R4,4           SEPARATE FRACTION
         SRL   R5,28
         CVD   R4,AVGVALUE
         M     R4,=F'100'
         D     R4,=F'16'
         CVD   R5,LOWTHRES
         SRP   LOWTHRES,64-1,5
         MVC   M3RUA+1(5),MASKL5D
         ED    M3RUA+1(5),LOWTHRES+6
         MVC   M3RUA(4),MASKL4
         ED    M3RUA(4),AVGVALUE+6
         SR    R3,R3          SET UP WEIGHTING FACTOR
         IC    R3,DMDTWT
         MVI   M3RUA,C'W'
         CVD   R3,AVGVALUE
         MVC   M3WT,MASKL4
         ED    M3WT,AVGVALUE+6
         SR    R3,R3          SET UP MAXIMUM MPL NUMBER
         LH    R3,DMDTHI
         CVD   R3,AVGVALUE
         MVC   M3HI,MASKL4
         ED    M3HI,AVGVALUE+6
         SR    R3,R3          SET UP MINIMUM MPL NUMBER
         LH    R3,DMDTLO
         MVI   M3HI,C'/'
         CVD   R3,AVGVALUE
         MVC   M3LO,MASKL4
         ED    M3LO,AVGVALUE+6
         SR    R3,R3          SET UP DOMAIN NUMBER
         IC    R3,DMDTNO
         CVD   R3,AVGVALUE
         UNPK  M3NO,AVGVALUE+6(2)
         OI    M3NO+2,X'F0'
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
DMDTLOP6 LA    R8,DMDTEND
*        MVC   DUMPER(DMDTLEN),DMDT **** FOR DEBUGGING ONLY ***********
         B     DMDTLOOP
         DROP  R8,R10
DMDTLEND B     EXIT
MPLHDR2  DC    H'79',CL80'    VARIABLE          LT   HT  AVG           X
               VARIABLE         LT   HT  AVG     '
MPLHDR3  DC    H'79',CL80'DMN  CONSTRAINT   RUA TAR  CI  CMPL SIC  GO  X
               OT  NS MAX  SERV WSR  WET ET/C  C '
         SPACE 3
EXIT     SR    R15,R15
EXITF    L     R13,4(0,R13)
         L     R14,12(0,R13)
         LM    R0,R12,20(R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
SETINDIC MVC   PINDHOLD,=CL6' ' SET THE INDICATOR AS APPROPRIATE
         CP    AVGVALUE,LOWTHRES
         BLR   R4
         MVC   PINDHOLD,=CL4'  -'
         CP    AVGVALUE,HIGTHRES
         BNHR  R4
         MVC   PINDHOLD,=CL4'<---'
         BR    R4
         SPACE 1
SETINDID MVC   PINDHOLD,=CL6' ' SET THE INDICATOR AS APPROPRIATE
         CP    AVGVALUE,HIGTHRES
         BHR   R4
         MVC   PINDHOLD,=CL4'  -'
         CP    AVGVALUE,LOWTHRES
         BNLR  R4
         MVC   PINDHOLD,=CL4'<---'
         BR    R4
         SPACE 1
PUTLINE  LA    R1,PUTLRECD    LOAD ADDRESS OF PARAMETERS
         L     R15,PUTLINEA   ADDRESS OF PUTLINE ROUTINE
         BALR  R14,R15        GO DISPLAY THE REQUEST
         LTR   R15,R15        WAS THE PUTLINE SUCCESSFUL?
         BZR   R4              YES, CONTINUE THE PROCESSING
         LA    R15,12          NO, RETURN TO RMF CONTROL
         B     EXITF
         SPACE 3
HEADER0  DC    H'2',CL2' '
MASKL6   DC    XL6'402020202120'
MASKL4   DC    XL4'40202120'
MASKL5D  DC    XL5'4021204B20'
         SPACE 1
         LTORG
         EJECT
         PRINT GEN
         IRARMCT
         EJECT
         IRARCT
         SPACE 3
         IRADMDT
         SPACE 3
         PRINT NOGEN
* IGNORE ANY ASSEMBLY ERRORS IN THE FOLLOWING MACRO
         IRAEPAT
         SPACE 3
         PRINT GEN
         ERBUSDTA
         ORG   PRTLINE    DISPLAY AREA
M1VAR    DS    CL19           DESCRIPTION 1
         ORG   *-1
M1LT     DS    CL6            LOW THRESHOLD VALUE
         ORG   *-1
M1HT     DS    CL6            HIGH THRESHOLD VALUE
         ORG   *-1
M1AVG    DS    CL6            CURRENT AVERAGE VALUE
M1IND    DS    CL4            INDICATOR FOR OVER/UNDER
         DS    CL2
M2VAR    DS    CL19           DESCRIPTION 2
         ORG   *-1
M2LT     DS    CL6            LOW THRESHOLD VALUE
         ORG   *-1
M2HT     DS    CL6            HIGH THRESHOLD VALUE
         ORG   *-1
M2AVG    DS    CL6            CURRENT AVERAGE VALUE
M2IND    DS    CL4            INDICATOR FOR OVER/UNDER
         ORG   PRTLINE
M3NO     DS    CL3            DOMAIN NUMBER
M3LO     DS    CL4            MINIMUM MPL NUMBER
M3HI     DS    CL4            MAXIMUM MPL NUMBER
M3WT     DS    CL4            WEIGHTING FACTOR
M3RUA    DS    CL6            AVERAGE NUMBER OF READY USERS
M3MPLT   DS    CL4            CURRENT MPL TARGET
M3CIDX   DS    CL4            CONTENTION INDEX
M3DMPL   DS    CL6            CURRENT MPL
M3INCU   DS    CL4            CURRENT # SWAPPABLE INCORE USERS
M3GOOU   DS    CL4            CURRENT # OF USERS GOING OUT
M3OUTU   DS    CL4            CURRENT # USERS ON OUT Q
M3NSW    DS    CL4            CURRENT # NONSWAPPABLE IN USERS
M3RUMX   DS    CL4            MAX # READY USERS IN INTERVAL
M3TWMS   DS    CL6            INTVL DMN SVCE ACCUM
M3TWSR   DS    CL4            WEIGHTED INTVL DMN SVCE
M3TWET   DS    CL5            ELAPSED TIME AVG FOR RTO
M3TRNTC  DS    CL5            CURRENT ELAPSED TIME AVG FOR RTO
M3TRNC   DS    CL4            XACTION COUNT FOR RTO
         SPACE 3
         PRINT NOGEN
         REGS
         END
