ERBUSASM TITLE 'ERBUSASM -- RMF USER ASM DATA SET REPORT'
***********************************************************************
**                                                                   **
**     THIS MODULE MUST BE LINK EDITED INTO MODULE "ERBRUS99" AS     **
**     "RENT".  THIS ASSEMBLY REQUIRES THE "AAPVT" COMPONENT FROM    **
**     THE SYSTEM OPTIONAL MACHINE READABLE MATERIAL AS A MACLIB.    **
**                                                                   **
**     MVS/SE2 MVS/SP1.1.0 MVS/SP1.1.1 MVS/SP1.3.1 MVS/SP1.3.2       **
**     MVS/SP1.3.3 MVS/SP1.3.4                                       **
**                                                                   **
***********************************************************************
ERBUSASM CSECT
         USING *,15
         B     BYMODULE
         DC    AL1(23)
         DC    CL8'ERBUSASM'
         DC    CL7' &SYSTIME'
         DC    CL8'&SYSDATE'
         DC    C' MVS/SE2'
         PRINT NOGEN
BYMODULE SAVE  (14,12)
         USING GETMAINA,R13
         LA    R1,SAVEARE1
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         DROP  R13,R15
         USING SAVEARE1,R13
         BALR  R12,0
         USING *,R12
         SPACE 3
         L     R5,CVTA        ADDRESS OF THE CVT
         USING CVT,R5
         L     R10,CVTASMVT   POINTER TO THE ASMVT
         DROP  R5
         USING ASMVT,R10
         L     R9,ASMPART     POINTER TO THE PART
         USING PART,R9
         CLC   =CL4'PART',PARTIDEN IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         MVC   PUTLHDRL,HDRLHDA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,ASMDHDR1
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         TM    ASMFLAG1,ASMNODPX TEST FOR DUPLEXING SUSPENDED
         BNO   ASMVTER1
         LA    R1,ASMDERR2
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTER1 TM    ASMFLAG1,ASMPLPAF TEST FOR PLPA DATA SET FULL
         BNO   ASMVTER2
         LA    R1,ASMDERR3
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTER2 TM    ASMFLAG1,ASMCOMMF TEST FOR COMMON PAGE DATA SET FULL
         BNO   ASMVTER3
         LA    R1,ASMDERR4
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTER3 TM    ASMFLAG1,ASMPLPAS TEST FOR PLPA SPILL AT IPL
         BNO   ASMVTER4
         LA    R1,ASMDERR5
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTER4 TM    ASMFLAG2,ASMNOSAV TEST FOR SYS1.STGINDEX UNAVAILABLE
         BNO   ASMVTNM1
         LA    R1,ASMDERR6
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTNM1 MVC   PUTLDSPL,DSPLLOWA
         TM    ASMFLAG2,ASMQUICK TEST FOR CLPA AT LAST IPL
         BO    ASMVTNM9
         LA    R1,ASMDNML1
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMVTNM9 MVI   PRTLINE,C' '                       PAGE DATA SETS HEADER
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   PRTLINE(32),=CL32'PAGE DATA SETS:  SERVICE BURST ='
         SR    R4,R4
         L     R5,ASMBURST
         D     R4,=F'1000'    CONVERT TO MILLISECONDS
         CVD   R5,AVGVALUE
         MVC   PRTLINE+32(L'MASKL6),MASKL6
         ED    PRTLINE+32(L'MASKL6),AVGVALUE+5
         MVC   PRTLINE+39(2),=CL2'MS'
         L     R4,ASMIOCNT    SIO COUNT NOT COMPLETE ***DELETED SP1.3.X
         LTR   R4,R4            CATCH ANYTHING?
         BZ    ASMDRPT1         NO, SKIP IT
         CVD   R4,AVGVALUE
         MVC   PRTLINE+44(16),=CL16'SIO NOT COMPLETE'
         MVC   PRTLINE+62(L'MASKL4),MASKL4
         ED    PRTLINE+62(L'MASKL4),AVGVALUE+6
ASMDRPT1 L     R4,ASMSRBCT    SRB COUNT NOT COMPLETE ***DELETED SP1.3.X
         LTR   R4,R4
         BZ    ASMDRPT2
         CVD   R4,AVGVALUE
         MVC   PRTLINE+69(8),=CL8'SRB ¬CMP'
         MVC   PRTLINE+77(2),=XL2'4021'
         ED    PRTLINE+77(2),AVGVALUE+7
ASMDRPT2 MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         LA    R1,ASMDHDR2
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         SR    R4,R4          COUNT OF NUMBER OF ENTRIES
         ST    R4,LOWTHRES
         LA    R8,PARTENTS
         USING PARTENT,R8
ASMDDLP1 TM    PAREFLG1,PARENUSE IS THIS PARTE IN USE?
         BO    ASMDDLP9         NO, SKIP IT
         MVI   PRTLINE,C' '                       PAGE DATA SETS DETAIL
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         L     R3,PARESZSL    NUMBER OF SLOTS IN DATASET
         CVD   R3,AVGVALUE
         MVC   M4SLOTS,MASKL8
         ED    M4SLOTS,AVGVALUE+4
         TM    PARETYPE,PAREPLPA WHAT TYPE PAGE DATA SET IS THIS?
         BNO   *+10
         MVC   M4TYPE,=CL6'PLPA'
         TM    PARETYPE,PARECOMM
         BNO   *+10
         MVC   M4TYPE,=CL6'COMMON'
         TM    PARETYPE,PAREDPLX
         BNO   *+10
         MVC   M4TYPE,=CL6'DUPLEX'
         TM    PARETYPE,PARELOCL
         BNO   *+10
         MVC   M4TYPE,=CL6'LOCAL'
         TM    PAREFLG1,PAREDSBD IS THIS DATASET OK?
         BO    ASMDDLP5            NO, SAY SO
         LR    R5,R3          % OF PAGE DATA SET USED
         S     R5,PARESLTA
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         MVC   M4USED(L'MASKL4PC),MASKL4PC
         ED    M4USED,AVGVALUE+6
         B     ASMDDLP6
ASMDDLP5 MVC   M4SLOTS+2(10),=CL10'BAD DATAST'
ASMDDLP6 TM    PAREFLG1,PAREINCP IS DUPLEX INTERCEPT ACTIVE?
         BNO   ASMDDLP7            NO, FORGET IT
         TM    ASMFLAG1,ASMDUPLX   MAYBE, TEST MORE
         BNO   ASMDDLP7              NO, FORGET IT
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,ASMDERR1
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMDDLP7 L     R5,PAREUCBP    FIND VOLUME SERIAL NUMBER
         USING UCBCMSEG,R5
         MVC   M4VOLSER,UCBVOLI
         DROP  R5
         L     R6,PAREPCTP    FIND THE DEVICE TYPE
         USING PCT,R6
         CLC   =CL4'PCT ',PCTID IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         MVC   M4DEVTYP,PCTDTYPE
         DROP  R6
         L     R5,PARERRCT    FIND THE NUMBER OF PERM I/O ERRORS
         CVD   R5,AVGVALUE
         MVC   M4IOERR,MASKL8
         ED    M4IOERR,AVGVALUE+4
         LH    R5,PARENN      MOVE IN DATA SET NAME
         M     R4,=F'44'
         L     R4,PARTDSNL
         LA    R5,0(R4,R5)
         MVC   M4DSN,0(R5)
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMDDLP9 LA    R8,L'PARTENT(,R8) SET UP FOR NEXT ENTRY
         L     R3,LOWTHRES    CHECK FOR END OF TABLE
         LA    R3,1(,R3)        INCREMENT BY ONE
         LH    R4,PARENN      CHECK FOR END OF TABLE
         ST    R4,LOWTHRES
         C     R3,PARTEUSE
         BL    ASMDDLP1
         DROP  R8
         MVI   PRTLINE,C' '                LOCAL PAGE DATA SETS SUMMARY
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         L     R3,ASMSLOTS    TOTAL LOCAL SLOTS IN ALL LOCALS
         CVD   R3,AVGVALUE
         MVC   M4SLOTS,MASKL8
         ED    M4SLOTS,AVGVALUE+4
         LR    R5,R3          % OF LOCAL PAGE SPACE USED
         S     R5,ASMBKSLT
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         MVC   M4USED(L'MASKL4PC),MASKL4PC
         ED    M4USED,AVGVALUE+6
         L     R4,ASMERRS     BAD SLOTS FOUND ON LOCAL DATA SETS
         CVD   R4,AVGVALUE
         MVC   M4IOERR,MASKL8
         ED    M4IOERR,AVGVALUE+4
         MVC   M4DSN(28),=CL28' LOCAL PAGE DATA SET SUMMARY'
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '            LOCAL PAGE DATA SETS VIO SUMMARY
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         L     R5,ASMVSC      TOTAL LOCAL SLOTS ALLOCATED TO VIO
         CVD   R5,AVGVALUE
         MVC   M4SLOTS,MASKL8
         ED    M4SLOTS,AVGVALUE+4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         MVC   M4USED(L'MASKL4PC),MASKL4PC
         ED    M4USED,AVGVALUE+6
         MVC   M4VOLSER(31),=CL31' LOCAL PAGE DATA SET VIO IMPACT'
         BAL   R4,PUTLINE
         L     R9,ASMSART     POINTER TO THE SART
         USING SART,R9
         CLC   =CL4'SART',SARID IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,HEADER0
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '                       PAGE DATA SETS HEADER
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   PRTLINE(33),=CL33'SWAP DATA SETS:  NO. PAGES/SLOT ='
         L     R5,SARSETSZ
         CVD   R5,AVGVALUE
         MVC   PRTLINE+33(L'MASKL4),MASKL4
         ED    PRTLINE+33(L'MASKL4),AVGVALUE+6
         L     R4,SARSRBCT    SRB COUNT NOT COMPLETE
         LTR   R4,R4
         BZ    ASMSRPT1
         CVD   R4,AVGVALUE
         MVC   PRTLINE+69(8),=CL8'SRB ¬CMP'
         MVC   PRTLINE+77(2),=XL2'4021'
         ED    PRTLINE+77(2),AVGVALUE+7
ASMSRPT1 MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         L     R4,SARUSE      ARE THERE ANY SWAP DATASETS?
         LTR   R4,R4
         BNP   SWPCOD0
         LA    R1,ASMSHDR2
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         LA    R8,SARENTS
         SR    R4,R4          COUNT OF NUMBER OF ENTRIES
         ST    R4,LOWTHRES
         ST    R4,HIGTHRES
         USING SARTE,R8
ASMSDLP1 TM    SREFLG,SRENUSE IS THIS SARTE IN USE?
         BO    ASMSDLP9         NO, SKIP IT
         MVI   PRTLINE,C' '                       PAGE DATA SETS DETAIL
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         L     R3,SRETOTSL    NUMBER OF SLOTS IN DATASET
         CVD   R3,AVGVALUE
         MVC   M4SLOTS,MASKL8
         ED    M4SLOTS,AVGVALUE+4
         L     R4,HIGTHRES      TOTAL NUMBER OF SWAP SETS ON SYSTEM
         AR    R4,R3
         ST    R4,HIGTHRES
         TM    SREFLG,SREDSBD IS THIS DATASET OK?
         BO    ASMSDLP5            NO, SAY SO
         LR    R5,R3          % OF PAGE DATA SET USED
         S     R5,SREAVLSL
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         MVC   M4USED(L'MASKL4PC),MASKL4PC
         ED    M4USED,AVGVALUE+6
         B     ASMSDLP6
ASMSDLP5 MVC   M4SLOTS+2(10),=CL10'BAD DATAST'
ASMSDLP6 L     R5,SREUCB      FIND VOLUME SERIAL NUMBER
         USING UCBCMSEG,R5
         MVC   M4VOLSER,UCBVOLI
         DROP  R5
         L     R6,SRESDCTE    FIND THE DEVICE TYPE
         MVC   M4DEVTYP,0(R6)
         L     R5,SRERRCNT    FIND THE NUMBER OF PERM I/O ERRORS
         CVD   R5,AVGVALUE
         MVC   M4IOERR,MASKL8
         ED    M4IOERR,AVGVALUE+4
         LH    R5,SRENN      MOVE IN DATA SET NAME
         M     R4,=F'44'
         L     R4,SARDSNL
         LA    R5,0(R4,R5)
         MVC   M4DSN,0(R5)
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
ASMSDLP9 LA    R8,L'SARTE(,R8) SET UP FOR NEXT ENTRY
         L     R3,LOWTHRES    CHECK FOR END OF TABLE
         LA    R3,1(,R3)        INCREMENT BY ONE
         LH    R4,SRENN      CHECK FOR END OF TABLE
         ST    R4,LOWTHRES
         C     R3,SARUSE
         BL    ASMSDLP1
         DROP  R8
         MVI   PRTLINE,C' '                       SWAP DATA SET SUMMARY
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         L     R3,HIGTHRES    TOTAL SLOTS IN ALL SWAP DATA SETS
         CVD   R3,AVGVALUE
         MVC   M4SLOTS,MASKL8
         ED    M4SLOTS,AVGVALUE+4
         LR    R5,R3          % OF SWAP SPACE USED
         S     R5,SARSETCT
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         MVC   M4USED(L'MASKL4PC),MASKL4PC
         ED    M4USED,AVGVALUE+6
         MVC   M4VOLSER(28),=CL28' SWAP DATA SET SUMMARY'
         DROP  R9,R10
         BAL   R4,PUTLINE
SWPCOD0  L     R11,RMCTA      POINTER TO THE RMCT
         USING RMCT,R11
         L     R10,RMCTRMCA   POINTER TO THE RMCA
         USING RMCA,R10
         CLC   =CL4'RMCA',RMCANAME IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         MVC   PUTLHDRL,HDRLDTA MOVE IN ADDRESS OF THE PARAMETERS
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,HEADER0
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,SWPCHDR1
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         LA    R1,SWPCHDR2
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '                       SWAP COUNTS BY REASON
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         SR    R3,R3          FIND THE TOTAL OF ALL OF THEM
         L     R4,RMCATOSC
         A     R4,RMCATISC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5TOSC,MASKL8
         ED    M5TOSC,AVGVALUE+4
         L     R4,RMCALWSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5LWSC,MASKL8
         ED    M5LWSC,AVGVALUE+4
         L     R4,RMCAXSSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5XSSC,MASKL6
         ED    M5XSSC,AVGVALUE+5
         L     R4,RMCARSSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5RSSC,MASKL6
         ED    M5RSSC,AVGVALUE+5
         L     R4,RMCARQSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5RQSC,MASKL6
         ED    M5RQSC,AVGVALUE+5
         L     R4,RMCADWSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5DWSC,MASKL8
         ED    M5DWSC,AVGVALUE+4
         L     R4,RMCANQSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5NQSC,MASKL6
         ED    M5NQSC,AVGVALUE+5
         L     R4,RMCAEXSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5EXSC,MASKL6
         ED    M5EXSC,AVGVALUE+5
         L     R4,RMCAUSSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5USSC,MASKL6
         ED    M5USSC,AVGVALUE+5
         L     R4,RMCATSSC
         CVD   R4,AVGVALUE
         AR    R3,R4
         MVC   M5TSSC,MASKL6
         ED    M5TSSC,AVGVALUE+5
         L     R4,RMCALSSC
         CVD   R4,AVGVALUE
         MVC   M5LSSC,MASKL8
         ED    M5LSSC,AVGVALUE+4
         L     R4,RMCALFSC
         CVD   R4,AVGVALUE
         MVC   M5LFSC,MASKL6
         ED    M5LFSC,AVGVALUE+5
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,PRTDTL
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         MVI   PRTLINE,C' '                            SWAP % BY REASON
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         LTR   R3,R3
         BZ    SWPCOD12
         L     R5,RMCATOSC
         A     R5,RMCATISC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD1
         MVC   M5TOSC+3(L'MASKL4PC),MASKL4PC
         ED    M5TOSC+3(L'MASKL4PC-1),AVGVALUE+6
SWPCOD1  L     R5,RMCALWSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD2
         MVC   M5LWSC+3(L'MASKL4PC),MASKL4PC
         ED    M5LWSC+3(L'MASKL4PC-1),AVGVALUE+6
SWPCOD2  L     R5,RMCAXSSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD3
         MVC   M5XSSC+1(L'MASKL4PC),MASKL4PC
         ED    M5XSSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD3  L     R5,RMCARSSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD4
         MVC   M5RSSC+1(L'MASKL4PC),MASKL4PC
         ED    M5RSSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD4  L     R5,RMCARQSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD5
         MVC   M5RQSC+1(L'MASKL4PC),MASKL4PC
         ED    M5RQSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD5  L     R5,RMCADWSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD6
         MVC   M5DWSC+3(L'MASKL4PC),MASKL4PC
         ED    M5DWSC+3(L'MASKL4PC-1),AVGVALUE+6
SWPCOD6  L     R5,RMCANQSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD7
         MVC   M5NQSC+1(L'MASKL4PC),MASKL4PC
         ED    M5NQSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD7  L     R5,RMCAEXSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD8
         MVC   M5EXSC+1(L'MASKL4PC),MASKL4PC
         ED    M5EXSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD8  L     R5,RMCAUSSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD9
         MVC   M5USSC+1(L'MASKL4PC),MASKL4PC
         ED    M5USSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD9  L     R5,RMCATSSC
         SR    R4,R4
         M     R4,=F'1000'
         DR    R4,R3
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD10
         MVC   M5TSSC+1(L'MASKL4PC),MASKL4PC
         ED    M5TSSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD10 L     R5,RMCALFSC
         SR    R4,R4
         M     R4,=F'1000'
         CLC   RMCALSSC,=F'0'
         BE    SWPCOD11
         D     R4,RMCALSSC
         CVD   R5,AVGVALUE
         SRP   AVGVALUE,64-1,5
         BZ    SWPCOD1
         MVC   M5LFSC+1(L'MASKL4PC),MASKL4PC
         ED    M5LFSC+1(L'MASKL4PC-1),AVGVALUE+6
SWPCOD11 BAL   R4,PUTLINE
SWPCOD12 CLC   RMCADFCT,=F'0'                       SWAP IN FAIL COUNTS
         BNE   SWPCOD13
         CLC   RMCASFCT,=F'0'
         BE    SWPCOD14
SWPCOD13 MVI   PRTLINE,C' '
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M5TOSC(25),=CL25'SWAP IN FAIL--THRESHOLD ='
         LH    R4,RMCASFET
         CVD   R4,AVGVALUE
         MVC   M5TOSC+25(L'MASKL6),MASKL6
         ED    M5TOSC+25(L'MASKL6),AVGVALUE+5
         MVC   M5TOSC+35(16),=CL16'DEFERRED USERS ='
         LH    R4,RMCADFCT
         CVD   R4,AVGVALUE
         MVC   M5TOSC+51(L'MASKL6),MASKL6
         ED    M5TOSC+51(L'MASKL6),AVGVALUE+5
         MVC   M5TOSC+58(13),=CL13'SYSTEM FAIL ='
         LH    R4,RMCASFCT
         CVD   R4,AVGVALUE
         MVC   M5TOSC+72(L'MASKL6),MASKL6
         ED    M5TOSC+72(L'MASKL6),AVGVALUE+5
         BAL   R4,PUTLINE
SWPCOD14 CLC   RMCAFHLD,=F'0'                       SWAP IN FAIL COUNTS
         BE    SWPCOD15
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MVC   M5TOSC(16),=CL16'HOLD SYSEVENTS ='
         L     R4,RMCANHLD
         CVD   R4,AVGVALUE
         MVC   M5TOSC+16(L'MASKL8),MASKL8
         ED    M5TOSC+16(L'MASKL8),AVGVALUE+4
         MVC   M5TOSC+26(28),=CL28'RESULTING SWAP TURN AROUND ='
         L     R4,RMCAFHLD
         CVD   R4,AVGVALUE
         MVC   M5TOSC+54(L'MASKL8),MASKL8
         ED    M5TOSC+54(L'MASKL8),AVGVALUE+4
         BAL   R4,PUTLINE
SWPCOD15 B     EXIT
ASMDHDR1 DC    H'45',CL45'                                ASM DATA SETSX
               '
ASMDHDR2 DC    H'79',CL80' TYPE   SLOTS USED VOLSER DEVTYP BAD SLOT    X
                     DATA SET NAME               '
ASMDERR1 DC    H'56',CL56'PLPA OR COMMON DATA SET NOT AVAILABLE--DUPLEXX
               ING IN USE'
ASMDERR2 DC    H'38',CL38'ASMNODPX--DUPLEXING HAS BEEN SUSPENDED'
ASMDERR3 DC    H'32',CL32'ASMPLPAF--PLPA DATA SET IS FULL'
ASMDERR4 DC    H'38',CL38'ASMCOMMF--COMMON PAGE DATA SET IS FULL'
ASMDERR5 DC    H'44',CL44'ASMPLPAS--PLPA DATA SPILLED TO COMMON AT IPL'
ASMDERR6 DC    H'44',CL44'ASMNOSAV--SYS1.STGINDEX UNAVAILABLE OR FULL'
ASMDNML1 DC    H'42',CL42'ASMQUICK--A CLPA WAS DONE AT THE LAST IPL'
         SPACE 1
ASMSHDR2 DC    H'79',CL80'   SWAP SETS  USED VOLSER DEVTYP BAD SETS    X
                     DATA SET NAME               '
         SPACE 1
SWPCHDR1 DC    H'22',CL22'SWAP COUNTS BY REASON:'
SWPCHDR2 DC    H'79',CL80' TERM-WT LONG-WT AUXST REALS  DET-WT  REQ ENQX
               -X RECOM UNLAT TRANS LOGIC-SW FAIL'
         SPACE 2
EXIT     SR    R15,R15
EXITF    L     R13,4(,R13)    RESTORE REGISTER 13
         L     R14,12(0,R13)
         LM    R0,R12,20(R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
PUTLINE  LA    R1,PUTLRECD    LOAD ADDRESS OF PARAMETERS
         L     R15,PUTLINEA   ADDRESS OF PUTLINE ROUTINE
         BALR  R14,R15        GO DISPLAY THE REQUEST
         LTR   R15,R15        WAS THE PUTLINE SUCCESSFUL?
         BZR   R4              YES, CONTINUE THE PROCESSING
         LA    R15,12          NO, RETURN TO RMF CONTROL
         B     EXITF
         SPACE 3
MASKL6   DC    XL6'402020202120'
MASKL4   DC    XL4'40202120'
MASKL4PC DC    XL5'402021206C'
MASKL8   DC    XL8'4020202020202120'
HEADER0  DC    H'2',C'  '
         SPACE 1
         LTORG
         EJECT
         PRINT NOGEN
*  IGNORE ASSEMBLY ERRORS IN THIS MACRO
         IRARMCT
         EJECT
         PRINT GEN
         ILRASMVT
         SPACE 3
         IRARMCA
         SPACE 3
PART     DSECT
PARTIDEN DS    CL4            PART IDENTIFIER
PARTSIZE DS    F              TOTAL NUMBER OF PART ENTRIES
PARTEUSE DS    F              NUMBER OF PARTE'S IN USE
PARTAIAE DS    F              AIA ERROR QUEUE
         DS    2F             NEXT PARTE QUEUES
PARTDSNL DS    F              ADDRESS OF DATASET NAME LIST IN CSA
PARTPCTQ DS    F              ADDRESS OF CHAIN OF PCT'S
PARTLCNT DS    H              COUNT OF ACTIVE LOCAL PAGE DATASETS
         DS    H
         DS    11F
PARTENTS DS    0F
         SPACE 1
PARTENTX DSECT
PARTENT  DS    0CL64        PART ENTRY REPRESENTING A PAGE DATA SET
PAREPARE DS    F              POINTER TO NEXT PARTE IN USE
PARELKUP DS    F              LOCK FOR THIS PARTE
PARETYPE DS    X              PAGE DATA SET TYPE FLAGS
PAREPLPA EQU   B'10000000'      PARTE FOR PLPA DATA SET
PARECOMM EQU   B'01000000'      PARTE FOR COMMON DATA SET
PAREDPLX EQU   B'00100000'      PARTE FOR DUPLEX COMMON DATA SET
PARELOCL EQU   B'00010000'      PARTE FOR LOCAL DATA SET
PAREFLG1 DS    X              PARTE FLAGS
PARENUSE EQU   B'10000000'     PARTE NOT IN USE
PAREDSBD EQU   B'01000000'     DATA SET BAD FLAG
PAREINCP EQU   B'00100000'     INTERCEPTED FLAG
PARENN   DS    H              PART NUMBER FOR THIS PARTE
PAREIOEQ DS    F              ADDRESS OF FIRST IOE ON UNSORTED QUEUE
PARESZSL DS    F              SIZE OF PAGE DATASET IN SLOTS
PARESLTA DS    F              NUMBER OF SLOTS AVAILABLE
PARERRCT DS    F              NUMBER OF PERM I/O ERRORS ON DATASET
PAREIORB DS    F              POINTER TO FIRST IORB
PAREPATP DS    F              POINTER TO PAT FOR THIS PAGE DATA SET
PAREPCTP DS    F              POINTER TO PCT FOR THIS PAGE DATASET TYPE
PAREEDBP DS    F              POINTER TO EDB FOR PAGE DATA SET
PAREUCBP DS    F              POINTER TO UCB FOR PAGE DATA SET
         SPACE 3
SART     DSECT
SARID    DS    CL4            SART IDENTIFIER
SARSIZE  DS    F              TOTAL NUMBER OF SART ENTRIES
SARUSE   DS    F              NUMBER OF SARTE'S IN USE
         DS    3F
SARDSNL  DS    F              ADDRESS OF DATASET NAME LIST IN CSA
SARSDCT  DS    F              ADDRESS OF SWAP DEVICE TABLE
         DS    F
SARSETCT DS    F              NUMBER OF SWAP SETS AVAILABLE
         DS    3F
SARSRBCT DS    F              SRBS SCHEDULED BUT NOT DISPATCHED
SARSETSZ DS    F              NO OF PAGES/SLOT IN SWAP SET
         DS    5F
SARENTS  DS    0F
         SPACE 1
SARTEX   DSECT
SARTE    DS    0CL48        SART ENTRY REPRESENTING A SWAP DATA SET
SRENEXT  DS    F              POINTER TO NEXT SARTE IN CIRCULAR CHAIN
SRELOCK  DS    F              LOCK TO SERIALIZE SWAP
         DS    X
SREFLG   DS    X              SARTE FLAGS
SRENUSE  EQU   B'10000000'     SARTE NOT IN USE
SREDSBD  EQU   B'01000000'     DATA SET BAD FLAG
SREFIXED EQU   B'00010000'     FIXED HEAD FILE FLAG
SRENN    DS    H              SART NUMBER FOR THIS SARTE
         DS    F
SRETOTSL DS    F              NUMBER OF SWAP SETS IN THIS DATA SET
SREAVLSL DS    F              AVAILABLE SWAP SETS ON THIS DATA SET
SRERRCNT DS    F              ERROR SWAP SETS ON THIS DATA SET
         DS    F
SRESAT   DS    F              POINTER TO SAT FOR THIS SWAP DATA SET
SRESDCTE DS    F              POINTER TO SDCTE FOR THIS SWAP DATA SET
SREEDB   DS    F              POINTER TO EDB FOR SWAP DATA SET
SREUCB   DS    F              POINTER TO UCB FOR SWAP DATA SET
         SPACE 3
PCT      DSECT
PCTID    DS    CL4            PCT IDENTIFIER
PCTDTYPE DS    CL6            DEVICE TYPE IN EBCDIC
         SPACE 3
         ERBUSDTA
         ORG   PRTLINE
M4TYPE   DS    CL6            TYPE OF PAGE DATASET
         ORG   *-1
M4SLOTS  DS    CL8            SLOTS ON THE DEVICE
M4USED   DS    CL4            % OF DATA SET THAT IS USED
         DS    CL2
M4VOLSER DS    CL6            VOLUME SERIAL OF THE DATA SET
         DS    C
M4DEVTYP DS    CL6            DEVICE TYPE OF THE DATA SET
M4IOERR  DS    CL8            NUMBER OF PERM I/O ERRORS ON DATA SET
         DS    C
M4DSN    DS    CL38           DATA SET NAME
         ORG   PRTLINEE
         ORG   PRTLINE
M5TOSC   DS    CL8            SWAP COUNTS
M5LWSC   DS    CL8
M5XSSC   DS    CL6
M5RSSC   DS    CL6
M5DWSC   DS    CL8
         ORG   *-1
M5RQSC   DS    CL6
M5NQSC   DS    CL6
M5EXSC   DS    CL6
M5USSC   DS    CL6
M5TSSC   DS    CL6
M5LSSC   DS    CL8
M5LFSC   DS    CL6
         ORG   PRTLINEE
         SPACE 3
         PRINT NOGEN
         REGS
         IEFUCBOB
         CVT   DSECT=YES
         END
