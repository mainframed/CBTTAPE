/*----------------------------------------------------------------rexx+
 | NAME:    RACFGRPS                                                  |
 |                                                                    |
 | PURPOSE: Indicates whether the user is a member of a group or      |
 |          returns all groups to which a user is connected.          |
 |                                                                    |
 | FORMAT:  RACFGRPS(checkgroup)                                      |
 |    checkgroup - optional group to check if user is a member        |
 |                 returns "YES" if user is connected to group        |
 |                 returns "NO" if user is not connected              |
 |               - if checkgroup is not specified then the function   |
 |                 returns a list of all of the user's connect group  |
 |                 each separated by spaces.                          |
 +--------------------------------------------------------------------*/
arg checkgroup
ASXBPTR = storage(224,4)
ASXB    = storage(d2x(c2d(ASXBPTR)+108),4)
ACEE    = storage(d2x(c2d(ASXB)+200),4)
ACEECGRP= storage(d2x(c2d(ACEE)+116),4)
ACEEGRPL_LEN = c2d(Storage(d2x(c2d(ACEECGRP)+5),3))
ACEEGRPL= storage(d2x(c2d(ACEECGRP)+32),Aceegrpl_len)
group_count = c2d(storage(d2x(c2d(ACEECGRP)+8),2))
allgroups=''
do i=1 to group_count
  parse var aceegrpl 1 group 9 . 25 aceegrpl
  allgroups=group allgroups
end
parse source . calltype .
if calltype = 'COMMAND' then
  do
    say "You are connected to the groups="allgroups
    return
  end
else
  if checkgroup="" then
    return allgroups
  else
    if wordpos(checkgroup,allgroups)>0
    then return "YES"
    else return "NO"
