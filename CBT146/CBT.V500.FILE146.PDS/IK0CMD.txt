*COPY                                                 IK0CMD
         TITLE 'USNTRF Routine - execute main loop'
* Execute Kermit commands (beginning with default TAKE files)
* Entry: environment already set up
* Exit: R15=0
*       ERRNUM set appropriately
USNTRF   ENTER
         LA    0,KRMPROT                                       @SC86295
         LA    1,USNCMD      Full list of commands             @SC87117
         BAL   14,LOOPS      Set up loop return                @SC86295
         LA    2,USRTAKE
         LA    1,LUSRT       Length of name                    @SC86295
         BAL   9,LUPTIN      Test user KERMINI                 @SC86295
           NOP 0             Not found, skip it                @SC86295
         LA    2,SYSTAKE                                       @SC86135
         LA    1,LSYST       Length of name                    @SC86295
         BAL   9,LUPTIN      Test system KERMINI               @SC86295
           NOP 0             Not found, skip it                @SC86295
         MVI   ERRNUM,ERRNFT No transfers yet                  @SC86295
         KCALL SUPFNC,6,E=LOOP                                 @SC86295
         OI    KFLG,CMDL+SIGN Got command line, suppress banner@SC86295
         B     LOOP                                            @SC86295
*
KRMININC WTEXT 'Kermit-&KSYS Version &KVRSN (&KDATE.)'         @SC86268
         WTEXT 'Enter ? for a list of valid commands'
         STRTMSGS ,          Any system-specific messages...   @SC87338
         OI    KFLG,SIGN     Banner done                       @SC86295
KRMPROB  PTEXT BLANK,1               And leave a blank line
         B     LUPWRT        Not an error                      @SC86295
*
KRMPROT  TM    KFLG,CMDL                                       @SC86295
         BZ    KRMPROCL              Go if Not cmd line
         NI    KFLG,255-CMDL Turn off command line             @SC86295
         OI    KFLG,CMDC     Command from cmd line             @SC86295
         L     1,CBUF                address of cmd
         L     0,CLEN        Length                            @SC86171
         B     LUPPRS        Go process it                     @SC86295
*
KRMPROCL TM    KFLG,CMDC                                       @SC86295
         BZ    KRMPROR               Go if not cmd line
         KCALL SUPFNC,7,E=(KRMXITQ,Z) Go if nothing stacked    @SC86295
KRMPROR  TM    KFLG,SIGN     Already printed banner?           @SC86295
         BO    KRMPROX       Yes, or suppressed                @SC86295
         KCALL SUPFNC,7,E=(KRMININC,Z) Go if nothing stacked   @SC86295
KRMPROX  LA    3,CMD                                           @SC86295
         LA    4,KPRPT       Current prompt                    @SC87268
         SR    0,0                                             @SC87268
         IC    0,KPRPL       Prompt length                     @SC87268
         RTEXT (3),PROMPT=((4),(0))                            @SC87268
         LA    1,CMD         Ptr to command                    @SC86171
         B     LUPPRS        Go process it                     @SC86295
*
USNCMD   KW    'EXIT',KRMXIT,MIN=2
         KW    'QUIT',KRMXIT
USNCMDX  KW    'BYE',KRMBYE,MIN=3                              @SC86155
         KW    'DIRECTORY',KRMDIR,MIN=3                        @SC86295
         KW    'ECHO',KRMECO,MIN=2
         KW    'FINISH',KRMFIN,MIN=3                           @SC86155
         KW    'GET',KRMGET                                    @SC86155
         KW    'HELP',KRMHLP
         KW    'LOCAL',LUPTOK,MIN=3                            @SC86295
         KW    'RECEIVE',KRMREC,MIN=3
         KW    'REMOTE',KRMREM,MIN=3                           @SC86155
         KW    'SEND',KRMSND,MIN=3
         KW    'SERVER',KRMSRV,MIN=3
         KW    'XECHO',KRMXPE,MIN=2                            @SC86204
         KW    'XTYPE',KRMNPS,MIN=2                            @SC86204
SRVKCMD  KW    '&KSYS.',LUPHST,MIN=2  Valid in Server mode ... @SC86295
         AIF   ('&KSYS' NE 'CMS').CM0Z                         @SC86355
         KW    'CP',LUPCP,MIN=2                                @SC86295
.CM0Z    KW    'CWD',LUPCWD,MIN=2                              @SC86295
         KW    'GIVE',LUPGIV,MIN=2                             @SC87117
         KW    'HOST',LUPHST,MIN=2                             @SC87253
         KW    'KERMIT',LUPTOK                                 @SC87343
         KW    'SET',LUPSET,MIN=3                              @SC86295
         KW    'SHOW',LUPSHO,MIN=2                             @SC86295
         KW    'SPACE',LUPSPA,MIN=2                            @SC86295
         KW    'STATUS',LUPSTA,MIN=2                           @SC86295
         KW    'TAKE',LUPTAK,MIN=2                             @SC86295
         KW    'TDUMP',LUPDMP,MIN=2                            @SC86295
         KW    'TYPE',LUPHSTI,MIN=2                            @SC86295
         KW
*
KRMECO   L     3,ADR                 Pick rest of line
         ICM   4,B'1111',LEN         Remaining data length
         BNP   KRMPROB               Go if nothing left in cmd
         B     LUPWRT        Else, print the rest              @SC86295
*                                                              @SC86155
KRMREM   KCALL GENCMD,0,E=LUPERK Send remote command           @SC86295
         B     KRMXFZ                                          @SC87300
*                                                              @SC86155
KRMBYE   BAL   14,LUPCNF     Check for illegal extras          @SC86295
         KCALL GENCMD,AL     Send Logout command               @SC86155
         B     KRMXFZ                                          @SC87300
*
KRMFIN   BAL   14,LUPCNF     Check for illegal extras          @SC86295
         KCALL GENCMD,AF     Send Finish command               @SC86155
         B     KRMXFZ                                          @SC87300
*
KRMGET   LA    0,FFGET+FFRCF                                   @SC86295
         KCALL FSPEC,JFSPEC  Get foreign filespec              @SC86295
         BAL   14,LUPCKFN                                      @SC86295
         LA    0,FFGET                                         @SC86295
         KCALL FSPEC,FILNAM  Get native filespec, if any       @SC86295
         BAL   14,LUPCKFN                                      @SC86295
         BAL   14,LUPCNF     Check for illegal extras          @SC86295
         LA    0,FFNEW+FFGET                                   @SC87012
         KCALL FSPEC,FILNAM,E=LUPWRTE Avoid collisions         @SC87012
         BAL   8,IPKSET      Set state table, exchange parms   @SC86295
* Init packet  Rpack interpret input tables                    @SC86155
         DC    AL1(AY),AL3(0)        ACK'ed                    @SC86155
         DC    AL1(00),AL3(KRMGETAB) Error                     @SC86155
         BAL   9,PAKFIL      Copy file specification to buffer @HF86223
         BAL   9,ENCODEN     Encode file-spec                  @SC86295
         MVI   STYPE,AR      Packet type = receive initiate    @SC86155
         KCALL SPACK,E=KRMGETAB Send name                      @SC86155
         KCALL RECEIV                                          @SC86155
         B     KRMXFZ                                          @SC86239
*
KRMGETAB KCALL INTINI,0                                        @SC86155
         B     KRMXFZ                                          @SC87300
*
KRMREC   LA    0,FFRCF                                         @SC86295
         KCALL FSPEC,FILNAM  Get filespec                      @SC86295
         BAL   14,LUPCKFN                                      @SC86295
         BAL   14,LUPCNF     Check for illegal extras          @SC86295
         LA    0,FFNEW+FFGET                                   @SC87012
         KCALL FSPEC,FILNAM,E=LUPWRTE Avoid collisions         @SC87012
         KCALL INTINI,3,E=KRMXFZ  Initialize for receive       @SC87300
         KCALL RECEIV
         B     KRMXFZ                                          @SC86239
*
KRMNPS   OI    FL4,NPS                                         @SC86165
         MVI   TCTLQ,0       Turn off control quoting          @SC86165
*
KRMSND   LA    0,FFSND                                         @SC86295
         KCALL FSPEC,IFILE   Get filespec                      @SC86295
         BAL   14,LUPCKFN                                      @SC86295
         LA    0,FFSND+FFRCF                                   @SC86295
         KCALL FSPEC,JFSPEC  Get filespec                      @SC86295
         BAL   14,LUPCKFN                                      @SC86295
         BAL   14,LUPCNF     Check for illegal extras          @SC86295
KRMSNDBG KCALL SEND
KRMXFZ   SR    3,3                                             @SC86355
         ICM   3,1,ERRNUM    Ok?                               @SC86355
         BZ    LOOP          Yes, get next command             @SC86355
         SLL   3,2           No, convert error number          @SC86355
         A     3,AERRTAB     Ptr into message table            @SC86355
         SR    4,4                                             @SC86355
         IC    4,0(3)        Length                            @SC86355
         ICM   3,7,1(3)      Message ptr                       @SC86355
         B     LUPWRTE       Display it and go on              @SC86355
*
KRMXPE   L     5,ADR         Pointer to rest of line           @HF86150
         ICM   4,15,LEN      Remaining data length             @HF86150
         BNP   KRMXPEH       Go if nothing specified           @HF86150
         L     3,RBUF                                          @HF86150
         MVC   0(256,3),0(5) Copy to disk read buffer          @HF86150
         AR    4,3           Get end                           @HF86150
         STM   3,4,TXTPTR    Point to text to copy             @HF86150
         OI    FL4,SFM+NPS   Data source: text string          @SC86165
         MVI   TCTLQ,AUP     Turn on control quoting           @SC86165
         B     KRMSNDBG                                        @SC86165
*
KRMXPEH  PTEXT 'Text string with Â¬X for cntl-X'                @SC86165
         B     LUPWRT                                          @SC86295
*
KRMSRV   BAL   14,LUPCNF     Check for illegal extras          @SC86295
         KCALL SERVER        Call SERVER routine               @SC86295
         B     KRMXFZ        Return to normal mode             @SC86355
*
KRMDIR   LA    0,FFUTL+FFWLD                                   @SC86295
         KCALL FSPEC,FILNAM  Get pattern filespec              @SC86295
         BAL   14,LUPCKFN    Make sure ok                      @SC86295
         LA    0,13                                            @SC86295
         KCALL DISKIO,FILNAM Do a DIR on it                    @SC86295
         B     LOOP                                            @SC86295
*
KRMHLP   KCALL KHELP         Issue help request                @SC86355
         B     LOOP                                            @SC86355
*
KRMXIT   FTOKN N=KRMXITQ,H=LUPCRH Check for illegal extras     @SC86295
         B     LUPBAD        Not just QUIT, maybe system Q     @SC86295
*
KRMXITQ  NXTFSET ,END        Flush pending file list           @SC86355
         L     2,TAKLEVK                                       @SC86295
KRMXITL  BCTR  2,0                                             @SC86295
         LTR   3,2           Any pending TAKE files?           @SC86295
         BM    RTRN0         No                                @SC86295
         SLA   3,2                                             @SC86295
         CLOSF TAKTABK(3)    Close the open file               @SC86295
         B     KRMXITL       Keep checking                     @SC86295
         LOCALS ,                                              @SC86295
* See SERVER for mapping                                       @SC86295
         DS    A             Return adr if no more TAKE stuff  @SC86295
         DS    A             Adr of command table              @SC86295
TAKLEVK  DS    F             Take file level                   @SC86295
TAKTABK  DS    (TAKMAX)F     Tickets for I/O                   @SC86295
KFLG     DS    X             Local flags in main program       @SC86295
SIGN     EQU   X'04'         Already printed Kermit banner     @SC86295
CMDC     EQU   X'02'         Command gotten from cmd
CMDL     EQU   X'01'         Data on cmd line
USNTRF   EXIT
         TITLE 'SET Routine - perform SET command options'
* Set/change values in STORAG.
* Entry: SCANPTR string has option
* Exit: R15=0 if ok, 1 if help needed, 2 if bad parameter name
*       ERRNUM unchanged
SET      ENTER
         MVI   SETXI,X'97'   XI instruction                    @SC86273
         NTOKN N=RTRN2                                         @SC86171
         NI    FL3,255-PXCH  Make sure server renegotiates     @SC86295
         SCAN  SETCMDKW,RTRN1                                  @SC86295
         B     RTRN2                                           @SC86295
*
SETOOKW  KW    'OFF',SETOFF,MIN=2                              @SC87166
         KW    'ON',SETON,MIN=2                                @SC87166
         KW    ,                                               @SC87166
*
SETROFF  KW    'OFF',0,MIN=2                                   @SC87166
SETRAW   KW    'RAW',SETDEBRW                                  @SC86316
SETCMDOO KW    'OFF',SETOFFS,MIN=2                             @SC87166
SETONKW  KW    'ON',SETONS,MIN=2                               @SC87166
         KW    ,                                               @SC86171
*
SETOFF   EX    0,0(9)        Yes, first turn flag on...        @SC87166
         EX    0,SETXI       Then off                          @SC86273
         B     RTRN0                                           @SC87166
*
SETON    EX    0,0(9)        Turn flag on                      @SC87166
         B     RTRN0                                           @SC87166
*
SETOFFS  B     4(9)                                            @SC87166
*
SETONS   BR    9             Go to ON handler                  @SC87166
*
SETTRKW  KW    'TTY',SETT                                      @SC87166
         KW    'SERIES1',SETT                                  @SC87166
         KW    'GRAPHICS',SETT                                 @SC87166
         KW    'FULLSCREEN',SETT                               @SC87300
         KW    ,                                               @SC87166
*
SETT     MVC   TRMTP,0(6)                                      @SC87166
         B     RTRN0                                           @SC87166
*
SETSWT   KW    'CONTINUE',SETOFF                               @SC86171
         KW    'HALT',SETON                                    @SC86171
         KW    ,                                               @SC86171
*
SETDSC   KW    'DISCARD',SETOFF                                @SC86225
         KW    'KEEP',SETON                                    @SC86225
         KW    ,                                               @SC86225
*
SETPAR   KW    'MARK',SETOFF                                   @SC86316
         KW    'NONE',SETON                                    @SC86316
         KW    ,                                               @SC86316
*
SETTABS  LA    4,SETCMDOO                                      @SC87166
         BAL   14,SETSCN                                       @SC87166
          B    SETTBON       Turn on                           @SC86355
         NI    FL2,255-TABS  Turn off                          @SC86355
         MVC   TABCNT,F0     Clear count                       @SC86355
         B     RTRN0                                           @SC86295
SETTBON  OI    FL2,TABS      Turn on                           @SC86355
         MVC   TABCNT,F0     Clear count                       @SC86355
         SR    0,0           Init previous tab                 @SC86355
         LA    3,TABTBL      Point to start of tab table       @TS86100
         LA    8,255         Limit on tab stops                @SC86355
         LA    5,TABCNT      End of table                      @SC86355
SETTBLP  ICM   2,15,LEN      Any more tokens?                  @SC86355
         BNP   SETTBN        No, done                          @SC86355
         STC   0,0(3)        Save previous tab                 @SC86355
         BAL   2,SETNUM      Read number                       @SC86355
         CLM   0,1,0(3)      Is this tab higher than previous? @SC86355
         BNH   SETTBSEQ      No, tab out of sequence           @TS86100
         CR    3,5           Exceeded capacity?                @SC86355
         BNL   SETTBHI       Yes                               @TS86100
         STC   0,0(3)        Save tab setting                  @TS86100
         LA    3,1(3)        Bump counter                      @SC86355
         B     SETTBLP                                         @SC86355
SETTBN   LA    0,TABTBL      Point to start of tab table       @SC86355
         SR    3,0           Get length of table               @SC86355
         STH   3,TABCNT      Save the tab count                @TS86100
         B     RTRN0                                           @SC86355
SETTBHI  PTEXT 'Too many tabs'                                 @SC86355
         B     SETTBER       Return error                      @SC86355
SETTBSEQ PTEXT 'Tabs out of sequence'                          @TS86100
SETTBER  NI    FL2,255-TABS  Turn off                          @SC86355
         B     SUBERR        Return error                      @TS86100
*
SETLIN   BAL   2,SETFSTR     Get fixed-format string           @SC86166
         PTEXT 'Bad line'                                      @SC87351
         KCALL SETMSG,5,E=SUBERR Make sure it's ok             @SC87351
         B     RTRN0                                           @SC86166
*
SETPRP   LA    0,KPRPT       Ptr to new prompt string          @SC87351
         KCALL SUPFNC,11     Ok it with system                 @SC87351
         B     RTRN0                                           @SC87351
*
         KSETPRC ,           System-specific options           @SC86355
*
SETFKW   KW    'LRECL',SHOLR               **COMPAT**          @SC87166
         KW    'T',SETFT                   **COMPAT**          @SC87166
         KW    'TYPE',SHOFILT              **COMPAT**          @SC87166
        KFILKW ,                           **COMPAT**          @SC87166
SETFIL   KW    'TEXT',SETFILET                                 @SC86133
         KW    'BINARY',SETFILEB                               @SC86262
SETDBIN  KW    'D-BINARY',SETFILEB                             @SC86262
         KW    'V-BINARY',SETFILEB                             @SC86151
         KW
*
SETFILEB OI    FL1,BINF              Set binary on
SETFLR   SR    0,0                                             @SC87012
         ICM   0,3,LRECL     Record length                     @SC86295
         ST    0,MAXOUT      Max output buffer size            @SC86295
         MVC   TYPFIL,0(6)   Save type                         @SC86151
         B     RTRN0                                           @SC86295
*
SETFILET NI    FL1,255-BINF          Set it OFF
         B     SETFLR                                          @SC87012
*
         KFILSET ,                                             @SC87012
*
SETDEB   LA    0,ATOE        EBCDIC log                        @SC86316
         LA    4,SETRAW                                        @SC86316
         BAL   14,SETSCN                                       @SC86316
          B    SETDEBON                                        @SC86273
SETDEBOF NI    FL1,255-DEBUG         Set it OFF
         ST    0,DBGTYP      Save indicator                    @SC86316
         CLOSF LOGPTR        Done logging                      @SC86135
         B     RTRN0                                           @SC86295
*
SETDEBRW SR    0,0           No translation for log            @SC86316
SETDEBON ST    0,DBGTYP      Save indicator                    @SC86316
         TM    FL1,DEBUG     Already on?                       @SC87012
         BO    RTRN0         Yes                               @SC87012
         NI    LOGFLGS,255-APPN                                @SC86295
         LA    0,L'LOGNAM    Name string length                @SC86295
         LA    1,LOGNAM      and address                       @SC86295
         STM   0,1,SCANPTR                                     @SC86295
         LA    0,FFRCF                                         @SC86295
         KCALL FSPEC,IFILE   Convert to filespec               @SC86295
         PTEXT 'DEBUG error'                                   @SC87012
         OPENF O,IFILE,LOGFDB,LOGPTR,E=SUBERR                  @SC87012
         OI    FL1,DEBUG     Enable logging                    @SC87012
         B     RTRN0                                           @SC86295
*
SET8B    NTOKN N=SET8BH,H=SET8BH                               @SC87008
         LA    4,AAMP        Default value                     @SC87008
         LA    9,SET8BS                                        @SC87008
         SCAN  SETONKW,RTRN2
         SR    4,4           Zero value means OFF              @SC87008
         LTR   7,7           Length=1?                         @SC87008
         BNZ   SET8BS        No, can't be ON                   @SC87008
         BAL   2,SETQCH2     Make sure it's valid              @SC87008
SET8BS   STC   4,EBQC        New value                         @SC87008
         B     RTRN0                                           @SC87008
SET8BH   PTEXT 'Must be ON, OFF, or a character'               @SC87008
         B     SUBERR                                          @SC87008
*
SETSTR   LR    2,14                                            @SC87268
         MVI   0(8),0        Default to blank                  @SC87166
         BAL   9,WSP         Remaining data length             @SC86224
          B    RTRN0         Null string                       @SC86295
         LR    1,4           Max length allowed                @SC87268
         CR    6,1                                             @SC86345
         BH    SETSTRH       Too long                          @SC86345
         STC   6,0(8)        Save length                       @SC87166
         LA    8,1(8)        Skip over length byte             @SC87268
         XR    6,7           Exchange ptr and length           @SC87268
         XR    7,6                                             @SC87268
         XR    6,7                                             @SC87268
         B     SETFST1       Go copy string                    @SC87268
*
SETRCTLQ BAL   2,SETQCHR             Get a char for Receive-Ctl-quote
         STC   4,RCTLQ(5)    Set receive ctl quote             @SC86164
         LTR   5,5           Done if SEND                      @SC86223
         BNZ   RTRN0                                           @SC86295
         STC   4,DEFPARM+5   Set default for SPAR              @SC86120
         B     RTRN0                                           @SC86295
*
SETQCHR  NTOKN H=SETQCHRH,N=SETQCHRH
         LTR   7,7                   Token length - 1
         BP    SETQCHRH              Pos: token is too long
SETQCH2  SR    4,4                                             @SC87008
         IC    4,0(6)        Get the quote char                @SC86120
         IC    4,ETOA(4)     Get ASCII form                    @SC86120
         NOTQR SETQCHRH      Go if not 33-62 or 96-126         @SC86120
         BR    2
*
SETQCHRH PTEXT 'One char with ASCII value 33-62 or 96-126'     @SC86224
         B     SUBERR                                          @SC86295
*
SETLR    ST    0,MAXOUT      Max output buffer size            @SC87166
         B     RTRN0                                           @SC86295
*
SETTIMO  BCT   5,RTRN0       Done if rec                       @SC87166
         TOCHR 0,,DEFPARM+1  Set default for SPAR              @SC86164
         B     RTRN0                                           @SC86295
*
SETPADN  BCT   5,RTRN0       Done if rec                       @SC87166
         TOCHR 0,,DEFPARM+2  Set default for SPAR              @SC86164
         B     RTRN0                                           @SC86295
*
SETPADC  BCT   5,RTRN0       Done if rec                       @SC87166
         CTL   0,,DEFPARM+3  Set default for SPAR              @SC86164
         B     RTRN0                                           @SC86295
*
SETEOL   BCT   5,RTRN0       Done if rec                       @SC87166
         STC   0,S1EOL       Extra copy for prompting          @SC87274
         TOCHR 0,,DEFPARM+4          Set default for SPAR
         B     RTRN0                                           @SC86295
*
SETSIZ   C     0,AKMIN       Less than min Kermit size?        @SC87166
         BL    SETKSIZH      Yes, error                        @SC86164
         C     0,AKMAX       More than max Kermit size?        @SC86164
         BNH   SETRPS1       No, skip message call             @TB86196
         LTR   5,5           SEND?                             @SC86224
         BNZ   SETKSIZH      Yes, can't set it long            @SC86224
         LR    6,0           Save value across WTEXT           @SC86316
         WTEXT 'Type 0 long packets specified'                 @SC86202
         LR    0,6                                             @SC86316
SETRPS1  DS    0H                                              @TB86196
         BCT   5,RTRN0       Done if recv                      @SC86295
         TOCHR 0,,DEFPARM+0          Set default for SPAR
         B     RTRN0                                           @SC86295
*
SETKSIZH PTEXT 'Operand must be 20-94 for SEND' KMIN-KMAX      @SC86295
         B     SUBERR                                          @SC86295
*
SETETOA  LA    3,ETOA        Address of table to change        @SC86265
         B     SETTET2                                         @SC87117
SETTET   LA    3,TETOA       Address of table to change        @SC87117
SETTET2  LA    2,ETOAD       Address of original               @SC87117
SETTR0   ICM   0,15,LEN      Any more tokens?                  @SC87117
         BP    SETTR1        Yes, must be numeric              @SC87117
         MVC   0(256,3),0(2) No, just reset table              @SC87117
         B     RTRN0                                           @SC87117
SETTR1   LA    8,255         Limit for each                    @SC87117
         BAL   2,SETNUM      Get a number for table offset     @SC86295
         AR    3,0           Save table offset here            @SC86295
         BAL   2,SETNUM      Get a number for value            @SC86295
         STC   0,0(3)        Change value                      @SC86295
         B     RTRN0         All done                          @SC86295
*
SETATOE  LA    3,ATOE        Adr of table to edit              @SC86265
         B     SETTAT2                                         @SC87117
SETTAT   LA    3,TATOE       Address of table to change        @SC87117
SETTAT2  LA    2,ATOED       Address of original               @SC87117
         B     SETTR0                Use common routine
*
*
* R6 points to token, R7 has length-1.  Convert to binary in R0.
* Return via R2
SETNUM2  LR    2,14          Save return                       @SC87166
SETNUM   NTOKN H=SETNUMH,N=SETNUMH                             @SC86295
         LA    7,1(7)        Length                            @SC86316
         BAL   14,GETNUM                                       @SC86316
          B    SETNUMH                                         @SC86316
         CLR   0,8           Within limit?                     @SC86295
         BH    SETNUMH       Too big                           @SC87166
         CLI   0(2),X'47'    Entered at SETNUM2?               @SC87166
         BNER  2             No, return immediately            @SC87166
         LR    14,2          Ptr to caller                     @SC87166
         S     14,F8         Back up to the LOAD instr         @SC87166
         MVC   SETXI,0(14)   Copy and modify op instr          @SC87166
         NC    SETXI(2),=X'F60F'                               @SC87166
         CLI   SETXI,X'B6'   Was is ICM?                       @SC87166
         BNE   *+8           No, ok                            @SC87166
         MVI   SETXI,X'BE'   Yes, make into STCM               @SC87166
         EX    0,SETXI       Store value                       @SC87166
         BR    2             Return                            @SC87166
*
SETNUMH  LA    15,CMD+16                                       @SC86295
SETMAXH  MVC   CMD(26),=C'Operand must be of length '          @SC86295
         MVI   0(15),C'<'                                      @SC86295
         LA    15,1(15)                                        @SC86295
         LR    4,8                                             @SC86345
         A     4,F1                                            @SC86345
         BAL   2,EDDEC       Put limit into message            @SC86295
         LR    4,15          End                               @SC86295
         LA    3,CMD                                           @SC86295
         SR    4,3                                             @SC86295
         B     SUBERR                                          @SC86295
*
SETFSTR  LR    1,9           Save length                       @SC87166
         NTOKN N=SETFST0,H=SETSTRH                             @SC87166
         LA    7,1(7)                                          @SC86295
         CR    7,1           Name too long?                    @SC86295
         BNH   SETFST1       No, do it                         @SC86295
SETSTRH  LR    8,1           Copy max length                   @SC86295
         LA    15,CMD+26     Base message size                 @SC86295
         B     SETMAXH                                         @SC86295
SETFST0  SR    7,7           Empty string                      @SC86295
SETFST1  ICM   7,8,BLANK     Set for blank fill                @SC86295
         LR    9,1                                             @SC87166
         MVCL  8,6           Copy name                         @SC87166
         BR    2                                               @SC86295
         TITLE 'SHOW Routine - performs SHOW command options'
* Display current values in STORAG.
* Entry: SCANPTR string has option
* Exit: R15=0 if ok, 1 if help needed, 2 if bad parameter name
*       ERRNUM unchanged
SHOW     ENTER ALT                                             @SC86133
         LA    0,CMD                                           @SC86227
         ST    0,SHOPTR      Initialize output ptr             @SC86227
         MVI   SETXI,X'91'   TM instruction                    @SC87166
         NTOKN N=SHOALL                                        @SC86133
         SCAN  SHOCMDS,RTRN1                                   @SC86295
SHOBAD   B     RTRN2         Invalid operand                   @SC86295
*
SETCMDKW DS    0H                                              @SC87166
         KW    'ATOE',SETATOE,MIN=4                            @SC87166
         KW    'ETOA',SETETOA,MIN=4                            @SC87166
         KW    'FILE-TYPE',SHOFILT,MIN=5                       @SC87166
         KW    'TATOE',SETTAT,MIN=5                            @SC87166
         KW    'TETOA',SETTET,MIN=5                            @SC87166
*
SHOCMDS  KW    'RECFM',SHORFM,MIN=4                            @SC87012
         KW    'LRECL',SHOLR                                   @SC86133
SHOCMDKW EQU   *             Must match order of code
         KW    'TABS-EXPAND',SHOTABS                           @SC86133
         KW    'EOF',SHOEOF,MIN=3                              @SC86133
         KW    'DEBUG',SHODEB                                  @SC86133
         KW    'BLOCK-CHECK',SHOBLK                            @SC86133
         KW    '8-BIT-QUOTE',SHO8B                             @SC87008
         KW    'PROMPT',SHOPRP,MIN=2                           @SC87268
         KW    'LINE',SHOLIN,MIN=3                             @SC87166
         KW    'CONTROLLER',SHOTRM,MIN=3                       @SC87268
         KW    'HANDSHAKE',SHOHND                              @SC87274
         KW    'PARITY',SHOPRTY                                @SC86316
         KW    'WARNING',SHOWARN                               @SC86133
         KW    'SYSCMD',SHOSYS,MIN=2                           @SC86295
         KW    'TTABLE',SHOTTB,MIN=2                           @SC87117
         KW    'DELAY',SHODLY,MIN=3                            @SC86164
         KW    'APPEND',SHOAPP,MIN=3                           @SC86203
         KW    'INCOMPLETE',SHOINC,MIN=3                       @SC86225
         KW    'TEST',SHOTST,MIN=4                             @SC87166
        KSETKW ,             Specific parameters               @SC87166
         KW    'FILE',SHOFIL                                   @SC86295
         KW    'MARGIN',SHOMRG                                 @SC87253
         KW    'FOREIGN',SHOFOR,MIN=3                          @HF86223
         KW    'RETRY',SHORETR,MIN=3                           @SC86345
         KW    'TAKE',SHOTAK,MIN=3                             @SC86171
         KW    'RECEIVE',SHORECV,MIN=3                         @SC86133
         KW    'SEND',SHOSEND,MIN=3                            @SC86224
         KW    ,                                               @SC86133
*
SHOALL   OI    SFLG,ALLF     Do all                            @SC86295
         LA    1,SHOCMDKW    Start at beginning                @SC86133
*
*          Each routine begins with R1-> keyword item          @SC86133
SHOTABS  CLI   SETXI,X'97'   SET or SHOW?                      @SC87166
         BE    SETTABS                                         @SC87166
         BAL   14,SHOOO      On or off                         @SC86133
          OI   FL2,TABS                                        @SC87166
SHOTABSZ LH    5,TABCNT      Count of tabs                     @SC86355
         LA    3,TABTBL      Ptr to table of tabs              @SC86355
         BAL   14,SHOLIST    Display list of tab stops, if any @SC86355
          NOP  0                                               @SC87166
SHOEOF   BAL   14,SHOOO      On or off                         @SC86133
          OI   FL2,EOFZ                                        @SC87166
SHODEB   CLI   SETXI,X'97'   SET or SHOW?                      @SC87166
         BE    SETDEB                                          @SC87166
         LA    4,SETCMDOO                                      @SC86345
         CLC   DBGTYP,F0     EBCDIC log?                       @SC86345
         BNE   *+8           Yes, it's ON or OFF               @SC87166
         LA    4,SETROFF     No, it's RAW                      @SC87166
         BAL   14,SHOXY                                        @SC86345
          OI   FL1,DEBUG                                       @SC87166
SHOBLK   SR    4,4                                             @SC86133
         LA    8,3           Limit                             @SC87166
         IC    4,BCTC        Get block check type              @SC86133
         BAL   14,SHONUM     Print it                          @SC86133
          B    RTRN0         OK                                @SC87166
SHO8B    LA    8,EBQC                                          @SC87008
         BAL   14,SHOCHRA    Display ASCII char                @SC87008
          B    SET8B                                           @SC87166
SHOPRP   LA    8,KPRPL       Ptr to prompt                     @SC87268
         LA    4,20          Max length                        @SC87268
         BAL   14,SHOSTR                                       @SC87268
          B    SETPRP        Do any system-dependent setup     @SC87351
SHOLIN   LA    8,TRMLIN                                        @SC87166
         LA    9,L'TRMLIN                                      @SC87166
         BAL   14,SHOCHRN                                      @SC87166
          B    SETLIN                                          @SC87166
SHOTRM   LA    4,SETTRKW                                       @SC87166
         LA    6,TRMTP                                         @SC87166
         BAL   14,SHOBRV     Get full name from abbrev.        @SC87166
          NOP  0                                               @SC87166
SHOHND   SR    4,4                                             @SC87274
         IC    4,S1HND                                         @SC87274
         BAL   14,SHOCTL     Print it                          @SC87274
          B    RTRN0                                           @SC87274
SHOPRTY  LA    4,SETPAR                                        @SC87166
         BAL   14,SHOXY                                        @SC86316
          OI   FL2,DAT8                                        @SC87166
SHOWARN  BAL   14,SHOOO      On or off                         @SC86133
          OI   FL1,REN                                         @SC87166
SHOSYS   BAL   14,SHOOO      On or off                         @SC86295
          OI   FL2,PASS                                        @SC87166
SHOTTB   BAL   14,SHOOO      On or off                         @SC87117
          OI   FL4,TTAB                                        @SC87166
SHODLY   L     4,LCLDLY                                        @SC86164
         BAL   14,SHONBIG    Print it                          @SC86164
          B    RTRN0                                           @SC87166
SHOAPP   BAL   14,SHOOO      On or off                         @SC86203
          OI   FL3,APPN                                        @SC87166
SHOINC   LA    4,SETDSC      List of possibles                 @SC87166
         BAL   14,SHOXY                                        @SC86225
          OI   FL5,KEEP                                        @SC87166
SHOTST   BAL   14,SHOOO                                        @SC87166
          OI   FL1,TSTF      Turn on                           @SC87166
*
         KSHOPRC ,           System-specific options           @SC86355
*
SHOFIL   LA    4,SHOFILKW    Ptr to sublist                    @SC87166
         CLI   SETXI,X'97'   SET or SHOW   **COMPAT**          @SC87166
         BNE   *+8           SHOW          **COMPAT**          @SC87166
         LA    4,SETFKW      SET           **COMPAT**          @SC87166
         BAL   14,SHOGRP                                       @SC86295
SHOMRG   LA    4,SHOMRGKW    Ptr to sublist                    @SC87253
         BAL   14,SHOGRP                                       @SC87253
SHOFOR   LA    4,SHOFORKW    Ptr to sublist                    @SC87166
         BAL   14,SHOGRP                                       @SC86224
SHORETR  LA    4,SHORETKW    Ptr to sublist                    @SC87166
         BAL   14,SHOGRP                                       @SC86345
SHOTAK   LA    4,SHOTAKKW    Ptr to sublist                    @SC87166
         BAL   14,SHOGRP                                       @SC86224
SHORECV  SR    5,5           Index for recv                    @SC86224
         BAL   14,SHOGRPR                                      @SC86224
SHOSEND  LA    5,1           Index for send                    @SC86224
         LA    14,SHOZZW                                       @SC87166
SHOGRPR  LA    4,SHORECKW    Ptr to common sublist             @SC87166
SHOGRP   LR    2,14          Save return adr                   @SC87166
         STM   1,4,SHOTMP    Save top level ptr, return adr    @SC87166
SETSCN   LR    2,14          Copy return adr (again)           @SC87166
         TM    SFLG,ALLF     Doing all?                        @SC86295
         BO    SHORAL2       Yes                               @SC86133
         NTOKN N=SHORALL                                       @SC86133
         LR    9,2           ???                               @SC87166
         SCAN  (4),RTRN1                                       @SC87166
SHOHLP   HELP  (4),RTRN1                                       @SC87166
*
SHOFILKW KW    'TYPE',SHOFILT                                  @SC86295
         KW    'LRECL',SHOLR                                   @SC86133
        KFILKW                                                 @SC87166
         KW    ,                                               @SC87012
*
SHOMRGKW KW    'LEFT',SHOLFT                                   @SC87253
         KW    'RIGHT',SHORGT                                  @SC87253
         KW    ,                                               @SC87253
*
SHORECKW KW    'END-OF-LINE',SHOEOL                            @SC86133
         KW    'END-OF-PACKET',SHOEOL                          @SC86133
         KW    'EOL',SHOEOL,MIN=3                              @SC86133
SHOPSKW  KW    'PACKET-SIZE',SHOSIZ                            @SC86133
         KW    'PAD-CHAR',SHOPADC,MIN=5                        @SC86164
         KW    'PADDING',SHOPADN,MIN=3                         @SC86164
         KW    'QUOTE',SHOQUO                                  @SC86133
         KW    'START-OF-PACKET',SHOMARK                       @SC86133
         KW    'TIMEOUT',SHOTIMO                               @SC86164
         KW    ,                                               @SC86133
*
SHOTAKKW KW    'ECHO',SHOECO,MIN=3                             @SC86171
         KW    'ERROR-ACTION',SHOHLT,MIN=3                     @SC86171
         KW    ,                                               @SC86171
*
SHOFORKW KW    'PREFIX',SHOPFX                                 @HF86223
         KW    'SUFFIX',SHOSFX                                 @HF86223
         KW    ,                                               @HF86223
*
SHORETKW KW    'INITIAL',SHORETI                               @SC86345
         KW    'PACKETS',SHORETN                               @SC86345
         KW    ,                                               @SC86345
*
SHORALL  OI    SFLG,ALLF+ASRF Do just all send/recv items      @SC86295
         LA    14,SHOHLP     Just help if SET                  @SC87166
SHORAL2  BAL   2,SHOKW       Get ptr to kw send or receive     @SC86133
         BER   14            Help for SET                      @SC87166
         L     15,SHOPTR     Output line buffer ptr            @SC86227
         LA    1,CMD                                           @SC86227
         SR    15,1          Anything there?                   @SC86227
         BNP   SHORAL3       No                                @SC86227
         ST    1,SHOPTR      Yes, reset ptr                    @SC86227
         WTEXT (1),(15)      And write it out                  @SC86227
SHORAL3  DS    0H                                              @SC86227
         MVC   CMD(2),=C'  '                                   @SC86133
         MVC   CMD+2(7),0(6) Copy send or receive              @SC86133
         LA    0,CMD+2(7)    Point past category               @SC86316
         ST    0,SHOPTR      Save output ptr                   @SC86316
         L     1,SHOTMP+12   Start at beginning                @SC87166
         ICM   14,7,0(1)     Ptr to 1st routine                @SC86171
         BR    14                                              @SC86171
*
SETFT    ICM   15,15,LEN     SET F T ...   **COMPAT**          @SC87166
         BNP   SETFILET      Nothing after: 'SET FILE-TYPE T'  @SC87166
*
SHOFILT  LA    4,SETFIL      List of possibles                 @SC86151
         LA    6,TYPFIL                                        @SC87166
         BAL   14,SHOBRV     Get full name from abbrev.        @SC87166
          NOP  0                                               @SC87166
SHOLR    SR    4,4                                             @SC86133
         L     8,MAXLRC      Upper limit                       @SC87166
         ICM   4,3,LRECL                                       @SC86133
         BAL   14,SHONUM     Print it                          @SC86133
          B    SETLR                                           @SC87166
         KFILSHO ,                                             @SC87012
         B     SHOGRPZ                                         @SC86295
*
SHOLFT   L     4,LMARG                                         @SC87253
         BAL   14,SHONBIG    Print it                          @SC87253
          B    RTRN0                                           @SC87253
SHORGT   L     4,RMARG                                         @SC87253
         BAL   14,SHONBIG    Print it                          @SC87253
          B    RTRN0                                           @SC87253
         B     SHOGRPZ                                         @SC87253
*
SHOECO   BAL   14,SHOOO      On or off                         @SC86171
          OI   FL2,ECHO                                        @SC87166
SHOHLT   LA    4,SETSWT      List of possibles                 @SC87166
         BAL   14,SHOXY                                        @SC86171
          OI   FL5,TKHLT                                       @SC87166
         B     SHOGRPZ                                         @SC86171
*
SHOPFX   LA    8,PREFIX      Point to prefix                   @HF86223
         LA    4,FORMAXL     Max length                        @SC87268
         BAL   14,SHOSTR     Print message                     @SC86224
          B    RTRN0                                           @SC87268
SHOSFX   LA    8,SUFFIX      Point to suffix                   @HF86223
         LA    4,FORMAXL     Max length                        @SC87268
         BAL   14,SHOSTR     Print message                     @SC86224
          B    RTRN0                                           @SC87268
         B     SHOGRPZ                                         @HF86223
*
SHORETI  L     4,MAXTNT      Initial retry limit               @SC86345
         BAL   14,SHONBIG    Print it                          @SC87166
          B    RTRN0                                           @SC87166
SHORETN  L     4,MAXTRY      Normal retry limit                @SC86345
         BAL   14,SHONBIG    Print it                          @SC87166
          B    RTRN0                                           @SC87166
         B     SHOGRPZ                                         @SC86345
*
SHOEOL   SR    4,4                                             @SC86133
         IC    4,REOL(5)                                       @SC86133
         BAL   14,SHOCTL     Print it                          @SC87166
          B    SETEOL                                          @SC87166
         LA    1,SHOPSKW     Skip aliases                      @SC86133
SHOSIZ   L     8,=A(KMAXE)   Limit                             @SC87166
         LR    3,5                                             @SC87166
         SLA   3,2           Get fullword index                @SC87166
         L     4,RPSIZ(3)                                      @SC87166
         BAL   14,SHONUM     Print number                      @SC86133
          B    SETSIZ                                          @SC87166
SHOPADC  SR    4,4                                             @SC86164
         IC    4,RPADC(5)    Pad character                     @SC86164
         BAL   14,SHOCTL                                       @SC87166
          B    SETPADC                                         @SC87166
SHOPADN  SR    4,4                                             @SC86164
         LA    8,KMAX        Same upper limit as packets       @SC87166
         IC    4,RPADN(5)    Pad count                         @SC86164
         BAL   14,SHONUM                                       @SC86164
          B    SETPADN                                         @SC87166
SHOQUO   LA    8,RCTLQ(5)                                      @SC86133
         BAL   14,SHOCHRA    Print as ascii                    @SC86133
          B    SETRCTLQ                                        @SC87166
SHOMARK  SR    4,4                                             @SC86133
         IC    4,RMARK(5)                                      @SC86133
         BAL   14,SHOCTL                                       @SC87166
          B    RTRN0                                           @SC87166
SHOTIMO  SR    4,4                                             @SC86164
         IC    4,RTIMO(5)    Timeout limit                     @SC86164
         BAL   14,SHONBIG                                      @SC87166
          B    SETTIMO                                         @SC87166
*
SHOGRPZ  TM    SFLG,ASRF     Doing just receive/send?          @SC86295
         BO    SHOZZW        Yes, write last line              @SC86227
         LM    1,2,SHOTMP    Get top level ptr, return adr     @SC87166
         LR    14,2                                            @SC86224
         BAL   2,SHOKW       Get ptr to name                   @SC86133
         LA    1,0(7,6)      Advance to next                   @SC86133
         BR    14                                              @SC86224
*
SHOLIST  LTR   5,5           Length of list                    @SC86355
         BZ    SHOZZ         Empty, we're done                 @SC86355
         LA    0,CMD+75      Set right margin                  @SC86355
         MVI   0(15),C' '    Start with blank                  @SC86355
         B     *+8                                             @SC86355
SHOLSLP  MVI   0(15),C','    Insert delimiter                  @SC86355
         LA    15,1(15)                                        @SC86355
         CR    15,0          Any room?                         @SC86355
         BL    SHOLSED       Yes, ok                           @SC86355
         LA    1,CMD         No, dump line                     @SC86355
         SR    15,1                                            @SC86355
         WTEXT (1),(15)                                        @SC86355
         MVI   CMD,C' '                                        @SC86355
         LA    15,CMD+1      Start indented                    @SC86355
         LA    0,CMD+75                                        @SC86355
SHOLSED  SR    4,4                                             @SC86355
         IC    4,0(3)        Get 1-byte item                   @SC86355
         BAL   2,EDDEC       Format it                         @SC86355
         LA    3,1(3)        Point to next item in list        @SC86355
         BCT   5,SHOLSLP                                       @SC86355
         B     SHOZZ         Finished list                     @SC86355
*
SHOKW    MVC   SETXI+1(3),1(14) Copy instr operands            @SC87166
         CLI   SETXI,X'97'   'OI' if SET, but 'TM' if SHOW     @SC87166
         LA    6,5(1)        Ptr to name                       @SC86133
         LA    7,0           Preserve CC                       @SC86133
         IC    7,3(1)        Length (assumes high bytes clear) @SC86133
         LA    7,1(7)                                          @SC86133
         BR    2                                               @SC86133
*
SHOCTL   LA    8,ABL-1       Max control character (ASCII)     @SC87166
         TM    FL1,TSTF                                        @SC86295
         BZ    SHONUM                                          @SC87166
SHONBIG  L     8,=F'999999998' Almost anything                 @SC87166
SHONUM   BAL   2,SHOKW                                         @SC86133
         BE    SETNUM2       Get value for SET                 @SC87166
         BAL   2,SHONAM      Copy option name                  @SC86209
         BAL   2,EDDEC       Edit (R4) as decimal              @SC86295
         B     SHOZZ                                           @SC86133
*
SHOCHRA  MVC   TMP,0(8)      Copy ascii char                   @SC86133
         PTEXT SETCMDOO+5,3,AREG=8,LREG=9                      @SC87008
         TM    TMP,X'60'     Is it printable?                  @SC87008
         BZ    SHOCHRN       No, say it's OFF                  @SC87008
         TR    TMP,ATOE      Convert to ebcdic                 @SC86133
         LA    8,TMP                                           @SC86133
         B     SHOCHR                                          @SC86224
SHOSTR   BAL   2,SHOKW       Get ptrs to name                  @SC87268
         BE    SETSTR        Branch to dispatch for SET        @SC87268
         SR    9,9           Variable-length string            @SC86224
         IC    9,0(8)        Get length                        @SC86224
         LA    8,1(8)        Ptr to text                       @SC86224
         B     SHOCHRD                                         @SC87268
SHOCHR   LA    9,1           Length is 1                       @SC86224
SHOCHRN  BAL   2,SHOKW       Get ptrs to name                  @SC86224
         BER   14            Branch to dispatch for SET        @SC87166
SHOCHRD  BAL   2,SHONAM      Copy option name                  @SC87268
         BAL   2,EDCHAR      Append string at (R8)             @SC87034
         B     SHOZZ         Print message                     @SC87034
*
SHOBRV   CLI   SETXI,X'97'   SET or SHOW?                      @SC87166
         BE    SETSCN                                          @SC87166
         LR    9,14          Save return adr                   @SC87166
         LR    8,1           Save list ptr                     @SC87166
         LR    1,4           Use list of suboptions            @SC87166
         SR    7,7           Assume 1-char abbrev              @SC87166
         ICM   7,8,*         Indicate just search              @SC87166
         BAL   14,SCAN                                         @SC87166
          CR   0,0           These two skipped                 @SC87166
          LR   4,1            if bad value                     @SC87166
         LR    1,8           Retrieve ptrs                     @SC87166
         LR    14,9                                            @SC87166
         B     SHOXY         Display it                        @SC87166
*
SHOOO    LA    4,SETOOKW     Ptr to on/off                     @SC87166
SHOXY    BAL   2,SHOKW       Set up name                       @SC86133
         BE    SETSCN        Parse value for SET               @SC87166
         LA    8,5(4)        Value if off                      @SC86133
         SR    9,9                                             @SC87166
         IC    9,3(4)        Length of name                    @SC86133
         EX    0,SETXI       Test bit                          @SC87166
         BZ    *+12                                            @SC86133
         LA    8,6(9,8)      Flag is on, advance               @SC86133
         IC    9,9(9,4)                                        @SC86133
         LA    9,1(9)                                          @SC86133
SHOXL    BAL   2,SHONAM      Copy option name                  @SC86209
         BAL   2,EDCHAR      Append string at (R8)             @SC86295
         SR    15,9          Back up to string                 @SC87034
         TR    0(30,15),LOCASE And make it lower case          @SC87034
         AR    15,9          Resume                            @SC87034
SHOZZ    ST    15,SHOPTR     Save end address                  @SC86227
         LA    1,0(7,6)      Advance to next                   @SC86345
         LA    14,4(14)      Skip over SET branch              @SC87166
         CLM   14,7,=AL3(SHOTABSZ)                             @SC86355
         BER   14            Special treatment for tabs        @SC86355
         TM    SFLG,ALLF     Doing all?                        @SC86295
         BOR   14            And resume if yes                 @SC86227
SHOZZW   LA    1,CMD         No, get address of buffer         @SC86227
         SR    15,1          Get length                        @SC86227
         WTEXT (1),(15)      Write it out                      @SC86227
         B     RTRN0         That's all                        @SC86295
*
SHONAM   LA    15,CMD        Output message buffer             @SC86209
         L     0,SHOPTR      End of prev. msg                  @SC86227
         CR    0,15          Empty?                            @SC86227
         BE    SHON1         Yes, start here                   @SC86227
         LA    1,CMD+23      2nd column                        @SC86227
         SR    1,0           Far enough?                       @SC86227
         BP    SHONF         Yes, blank fill                   @SC86227
         AH    1,=H'23'      Try 3rd column                    @SC86227
         BP    SHONF         OK                                @SC86227
         SR    0,15          No room, dump line                @SC86227
         WTEXT (15),(0)                                        @SC86227
         LA    15,CMD        And start over                    @SC86227
         B     SHON1                                           @SC86227
SHONF    SR    15,15                                           @SC86295
         ICM   15,8,BLANK                                      @SC86295
         MVCL  0,14          Fill with blanks to next column   @SC86227
         LR    15,0          New output ptr                    @SC86227
SHON1    MVC   0(40,15),0(6) Copy option name                  @SC87034
         TR    1(39,15),LOCASE And beautify it                 @SC87034
         AR    15,7          Space over it                     @SC86209
         MVC   0(4,15),=C' is '                                @SC87034
         LA    15,4(15)      Space over 'is'                   @SC86209
         BR    2                                               @SC86209
*
         LOCALS ,                                              @SC86295
SHOTMP   DS    4F                                              @SC87166
SHOPTR   DS    A             More temporaries                  @SC86227
SETXI    DS    F             XI executable instr               @SC86273
SFLG     DS    X             Local flags                       @SC86295
ALLF     EQU   X'80'         Doing SHOW ALL                    @SC86295
ASRF     EQU   X'40'         Doing SHOW REC or SHOW SEND       @SC86295
SHOW     EXIT
         TITLE 'STATUS Routine - display latest error, etc.'   @SC86295
* Exit: R15=0.  ERRNUM unchanged.
STATUS   ENTER                                                 @SC86156
         CLI   ERRNUM,ERRNFT Actual error?                     @BS86090
         BNH   STAMSG        No                                @BS86090
         CLI   ERRNUM,ERRKCE Last command invalid?             @SC86295
         BE    STAMSG        Yes, do not show last file        @HF86232
         CLI   FILNAM,0      File name defined?                @BS86090
         BE    STAMSG        No                                @BS86090
         MVC   CMD(16),=CL16'Last file used:'                  @BS86090
         LA    7,CMD+16      Fill in name                      @BS86090
         LA    1,FILNAM                                        @SC86295
         BAL   2,STAFSP      Copy name and print               @SC86295
STAMSG   ICM   4,15,NSENT    Number of files sent              @SC86295
         BZ    STASNTZ                                         @SC86295
         LA    15,CMD        Start of message buffer           @SC86295
         BAL   2,EDDEC       Format number as decimal          @SC86295
         LA    0,17(15)      Tentative end of message          @SC86295
         MVC   0(17,15),=C' files sent last.'                  @SC86295
         BCT   4,STAPLR                                        @SC86295
         MVC   5(11,15),6(15) Only one file, make singular     @SC86295
         BCTR  0,0                                             @SC86295
STAPLR   BAL   2,STAPMSG     Show message                      @SC86295
STASNTZ  ICM   0,15,PAKCNT   Any transfer statistics?          @SC86295
         BZ    STADATR       No, skip it                       @SC86316
         ICM   6,7,=C'pkt'                                     @SC86295
         BAL   3,STADPR      Format msg                        @SC86295
         ICM   0,15,SECTOT   Any duration?                     @SC86295
         BZ    STADATR       No, must have been very short     @SC86316
         ICM   6,7,=C'sec'                                     @SC86295
         BAL   3,STADPR      Format msg                        @SC86295
         MVC   CMD(16),=C'Disk bytes/sec: '                    @SC86295
         LA    15,CMD+16                                       @SC86295
         L     0,SECTOT                                        @SC86295
         LM    4,5,DSKTOT                                      @SC86295
         BAL   2,STAVB       Format ratio                      @SC86295
         BAL   2,STAPM15     Print line                        @SC86295
STADATR  ICM   4,15,RTRCNT   Any retries?                      @SC86316
         BZ    STADATZ       No                                @SC86316
         LA    15,CMD        Yes, issue message                @SC86316
         BAL   2,EDDEC                                         @SC86316
         MVC   0(20,15),=C' repeat packets sent'               @SC86316
         LA    15,20(15)                                       @SC86316
         BAL   2,STAPM15     Print line                        @SC86316
         L     1,SECTOT                                        @SC86345
         BAL   9,OPTPKT      Get best packet size              @SC86345
         LTR   4,15          Valid?                            @SC86345
         BNP   STADATZ       No, skip it                       @SC86345
         MVC   CMD(23),=C'Optimumum packet size: '             @SC86345
         LA    15,CMD+23                                       @SC86345
         BAL   2,EDDEC       Format it                         @SC86345
         BAL   2,STAPM15                                       @SC86345
STADATZ  ICM   4,15,RECTRC   Any truncated records?            @SC87268
         BZ    STATRCZ       No, ok                            @SC87268
         LA    15,CMD        Yes, issue message                @SC87268
         BAL   2,EDDEC                                         @SC87268
         MVC   0(18,15),=C' records truncated'                 @SC87268
         LA    15,18(15)                                       @SC87268
         BAL   2,STAPM15                                       @SC87268
STATRCZ  DS    0H                                              @SC87268
         SR    5,5                                             @SC86156
         IC    5,ERRNUM      Get offset into error table       @SC86156
         SLL   5,2           Get fullword index                @SC86156
         A     5,AERRTAB     Pointer address                   @SC86156
         L     1,0(5)        Msg ptr                           @SC86156
         SR    0,0                                             @SC86268
         SLDL  0,8           Msg length                        @SC86316
         SRL   1,8           Realign adr                       @SC86316
         WTEXT (1),(0)       Print message                     @SC86268
         CLI   ERRNUM,ERRTRC Cancelled?                        @SC86316
         BNE   STACKAB       No                                @SC86316
         SR    1,1                                             @SC86316
         CLI   REASON,15     Within table?                     @SC86316
         BH    *+8           No, must be new                   @SC86316
         IC    1,REASON      Ok, get the complaint code        @SC86316
         SLL   1,3           Index into table                  @SC86316
         LA    1,STACNTB(1)                                    @SC86316
         LA    0,8           Length of items                   @SC86316
         WTEXT (1),(0)                                         @SC86316
STACKAB  CLI   ERRNUM,ERRABO Micro aborted?                    @BS86090
         BE    *+12          Yes                               @SC87338
         CLI   ERRNUM,ERRDIE No, disk I/O error?               @SC87338
         BNE   STARET        No                                @BS86090
         ICM   0,15,EMSGL    Yes, any message?                 @SC86268
         BZ    STARET        No                                @BS86090
         L     1,EMSGP                                         @BS86090
         WTEXT (1),(0)       Yes, show it                      @SC86268
STARET   RET                                                   @SC86295
*
STADPR   MVC   CMD(13),=C'Bytes/pkt: S='                       @SC86295
         LA    15,CMD+13                                       @SC86295
         STCM  6,7,CMD+6                                       @SC86295
         LM    4,5,TOUTOT                                      @SC86295
         BAL   2,STAVB       Format ratio                      @SC86295
         MVC   0(3,15),=C' R='                                 @SC86295
         LA    15,3(15)                                        @SC86295
         LM    4,5,TINTOT                                      @SC86295
         BAL   2,STAVB       Format ratio                      @SC86295
         MVC   0(11,15),=C' requiring '                        @SC86295
         LA    15,11(15)                                       @SC86295
         LR    4,0                                             @SC86295
         BAL   2,EDDEC       Format number of units            @SC86295
         MVI   0(15),C' '                                      @SC86295
         STCM  6,7,1(15)                                       @SC86295
         LA    0,4(15)       End of msg                        @SC86295
         BAL   2,STAPMSG     Print it                          @SC86295
         BR    3                                               @SC86295
*
STAVB    DR    4,0           Get ratio                         @SC86295
         AR    4,4                                             @SC86295
         CR    4,0                                             @SC86295
         BL    *+8                                             @SC86295
         A     5,F1          Round up                          @SC86295
         LR    4,5                                             @SC86295
         B     EDDEC         Format it                         @SC86295
*
* Table of reasons for rejecting Attribute packet              @SC86316
STACNTB  DC    C'-Unknown-Length -Type   -Date   '             @SC86316
         DC    C'-Creator-Account-Area   -Passwrd'             @SC86316
         DC    C'-Blksize-Access -Coding -Disp   '             @SC86316
         DC    C'-Protect-Protect-Origin -Format '             @SC86316
         TITLE 'DUMP Routine - print translation table'
* Display current values in STORAG.
* Entry: SCANPTR string has option
* Exit: R15=0 if ok, R15=1 if error or help needed. ERRNUM unchanged.
DUMP     ENTER ALT                                             @SC86156
         NTOKN N=DUMPH       A or E?                           @SC86156
         SCAN  DUMPKW,RTRN1                                    @SC86295
DUMPH    HELP  DUMPKW,RTRN1                                    @SC86295
*
DUMPKW   KW    'ATOE',DUMPA                                    @SC86156
         KW    'ETOA',DUMPE                                    @SC86156
         KW    'NAMES',DMPN                                    @SC86295
         KW    'TATOE',DUMPTA,MIN=2                            @SC87117
         KW    'TETOA',DUMPTE,MIN=2                            @SC87117
         KW    ,                                               @SC86156
*
DMPN     L     5,TSENT       Table ptr                         @SC86295
         ICM   6,15,NSENT    Number of files sent              @SC86295
         BNZ   DMPNL                                           @SC86295
         WTEXT 'No files sent'                                 @SC86295
         B     RTRN0                                           @SC86295
DMPNL    LA    7,CMD         Start of message buffer           @SC86295
         LR    1,5                                             @SC86295
         BAL   2,STAFSP      Show filespec                     @SC86295
         LA    5,LFID(5)     Advance ptr to next               @SC86295
         BCT   6,DMPNL                                         @SC86295
         B     RTRN0                                           @SC86295
*
DUMPA    LA    3,ATOE                                          @SC86156
         B     DUMPAE                                          @SC86156
DUMPE    LA    3,ETOA                                          @SC86156
         B     DUMPAE                                          @SC87117
DUMPTA   LA    3,TATOE                                         @SC87117
         B     DUMPAE                                          @SC87117
DUMPTE   LA    3,TETOA                                         @SC87117
DUMPAE   LA    4,4           Bytes per word                    @SC86156
         LA    5,15(3)       End of 1st line                   @SC86156
         LA    6,16          Bytes per line                    @SC86156
         LA    7,256(3)      2 before end of table             @SC86156
DUMPLL   LA    2,CMD         Output buffer                     @SC86156
DUMPLW   UNPK  0(9,2),0(5,3) Convert a word                    @SC86156
         TR    0(8,2),TRHEX  Hex notation                      @SC86156
         MVI   8(2),C' '     Leave a space between words       @SC86156
         LA    2,9(2)                                          @SC86156
         BXLE  3,4,DUMPLW    Do next word                      @SC86156
         LA    1,CMD         Done line of 4                    @SC86156
         LA    0,35                                            @SC86268
         WTEXT (1),(0)       Print it                          @SC86268
         BXLE  5,6,DUMPLL    Done line, go to next             @SC86156
         B     RTRN0
         TITLE 'GIVTAB Routine - save translation table'
* Save current values in STORAG into a TAKE file on disk
* Entry: SCANPTR string has option
* Exit: R15=0 if ok, R15=1 if error or help needed. ERRNUM unchanged.
GIVTAB   ENTER ALT                                             @SC87117
         NTOKN N=GIVH       A or E?                            @SC87117
         SCAN  GIVKW,RTRN1                                     @SC87117
GIVH     HELP  GIVKW,RTRN1                                     @SC87117
*
GIVKW    KW    'ATOE',GIVA                                     @SC87117
         KW    'ETOA',GIVE                                     @SC87117
         KW    'TATOE',GIVTA,MIN=2                             @SC87117
         KW    'TETOA',GIVTE,MIN=2                             @SC87117
         KW    ,                                               @SC87117
*
GIVA     LA    6,ATOE-1                                        @SC87117
         B     GIVA1                                           @SC87117
GIVE     LA    6,ETOA-1                                        @SC87117
         B     GIVE1                                           @SC87117
GIVTA    LA    6,TATOE-1                                       @SC87117
GIVA1    LA    0,ATOED                                         @SC87117
         B     GIVAE                                           @SC87117
GIVTE    LA    6,TETOA-1                                       @SC87117
GIVE1    LA    0,ETOAD                                         @SC87117
GIVAE    SR    15,15                                           @SC87117
         IC    15,3(1)       Get length of name                @SC87117
         MVC   GIVBUF(4),=C'SET '                              @SC87117
         MVC   GIVBUF+4(10),5(1)  Copy name to command         @SC87117
         LA    15,GIVBUF+5(15)                                 @SC87117
         MVI   0(15),C' '                                      @SC87117
         LA    15,1(15)      Get ptr for 1st argument          @SC87117
         LR    1,0                                             @SC87117
         BCTR  0,0           Back up to start at "difference"  @SC87117
         STM   15,1,GIVSV    Save ptrs: cmd, table, table start@SC87117
         LA    7,257         Table length + 1                  @SC87117
         LA    0,FFGIV                                         @SC87117
         KCALL FSPEC,FILNAM,E=GIVFNE  Error                    @SC87117
         MVI   ERRNUM,ERRNOE Ok now                            @SC87117
         OPENF O,FILNAM,LOGFDB,GIVPTR,E=GIVOPERR               @SC87117
GIVLP    LM    15,0,GIVSV    Get output ptr, table scan ptr    @SC87117
         LA    6,1(6)        Skip last difference              @SC87117
         AH    0,*-2                                           @SC87117
         BCTR  7,0           New length left                   @SC87117
         LR    1,7           Copy length                       @SC87117
         CLCL  0,6           Find next difference              @SC87117
         BE    GIVFIN        All done                          @SC87117
         ST    0,GIVSV+4     Save new ptr                      @SC87117
         LR    4,0           Get offset                        @SC87117
         S     4,GIVSV+8                                       @SC87117
         BAL   2,EDDEC       Write as decimal                  @SC87117
         MVI   0(15),C' '    Leave space                       @SC87117
         LA    15,1(15)                                        @SC87117
         IC    4,0(6)        Get tailored character            @SC87117
         BAL   2,EDDEC       Write as decimal                  @SC87117
         LA    2,GIVBUF                                        @SC87117
         SR    15,2          Length of line                    @SC87117
         WRITF GIVPTR,BUFFER=(2),BSIZE=(15),E=GIVWRERR         @SC87117
         B     GIVLP                                           @SC87117
GIVWRERR CLOSF GIVPTR        Close output file                 @SC87117
GIVOPERR PTEXT 'Unable to write file'                          @SC87117
GIVFNE   WTEXT (3),(4)       Show message                      @SC87117
         B     RTRN1                                           @SC87117
GIVFIN   CLOSF GIVPTR,E=GIVOPERR Close output file             @SC87117
         B     RTRN0                                           @SC86295
         LOCALS ,                                              @SC86295
GIVSV    DS    3F            Saved ptrs for saving table       @SC87117
GIVPTR   DS    A             Ticket for disk I/O               @SC87117
GIVBUF   DS    CL25          Buffer for new file               @SC87117
         EXIT                                                  @SC86164
         TITLE 'GENCMD Routine - send a Generic command'       @SC86155
* Entry: SCANPTR has string
* Exit: R15=0 if ok, 1 if help needed, 2 if bad parameter
*       ERRNUM set appropriately
GENCMD   ENTER                                                 @SC86155
         LA    8,1           One operand                       @SC86295
         LTR   1,1                                             @SC86295
         BZ    REMCMD        Parse REMOTE command              @SC86295
         LA    0,AG          Packet type = generic command     @SC86155
GENNUL   SR    5,5           NO ARGUMENTS                      @SC86316
GENFILL  STC   0,STYPE       Set packet type                   @SC86155
         L     3,RBUF        Put string here                   @SC86155
         CLI   STYPE,AG      Generic?                          @SC86155
         BNE   GENOTH1       No subcommand                     @SC86155
         STC   1,0(3)        Save subcommand byte              @SC86155
         LA    3,1(3)        Move to next character position   @SC86155
         B     GENOTH1                                         @SC86295
GENNXT   NTOKN N=RTRN1       Get next argument                 @SC86295
         LA    5,1(7)        Length                            @SC86295
         LR    4,6           Address                           @SC86295
GENOTH1  LTR   1,5           Any argument?                     @SC86155
         BZ    GENFILZ       No, done                          @SC86155
         CLI   STYPE,AG      Generic?                          @SC86155
         BNE   GENOTH2       No, skip length indicator         @SC86155
         TOCHR 1,,0(3)       Yes, do it                        @SC86155
         LA    3,1(3)                                          @SC86155
GENOTH2  MVC   0(96,3),0(4)  Copy argument                     @SC86155
         TR    0(96,3),ETOA  in ASCII                          @SC86155
         AR    3,5           Advance ptr                       @SC86155
         BCT   8,GENNXT                                        @SC86295
GENFILZ  S     3,RBUF        Length of buffer                  @SC86155
         ST    3,RBUFL       Set buffer size                   @SC86155
         BAL   8,IPKSET      Set state table, exchange parms   @SC86155
         DC    AL1(AY),AL3(0)        ACK'ed     Must be just   @SC86155
         DC    AL1(00),AL3(GENABR)   Error.    these 2 items.  @SC86155
         BAL   8,GENSET      Set state table                   @SC86155
* Server cmd Rpack interpret input table                       @SC86155
         DC    AL1(AY),AL3(0)        ACK'ed                    @SC86155
         DC    AL1(AS),AL3(GENRPL)   Long reply                @SC86155
         DC    AL1(00),AL3(GENABR)   Error                     @SC86155
GENSET   BAL   9,ENCODEN     Encode command                    @SC86295
         BAL   9,INPUTSPK    Send, get response                @SC86295
         MVI   ERRNUM,ERRNOE No errors                         @SC86155
         ICM   0,15,DATL     Any short reply?                  @SC86155
         BZ    GENRET        No, done                          @SC86155
         NI    FL1,255-EOF   Yes, set flags                    @SC86155
         XC    WBUFL,WBUFL   Clear old data                    @SC86155
         OI    LOGFLGS,APPN  DISP=MOD                          @SC86295
         BAL   2,GENRPS      Set up file name                  @SC86295
         OPENF O,REPNAM,LOGFDB,FILPTR,E=GENABR                 @SC86295
         USING FDBD,1                                          @SC86295
         SR    0,0                                             @SC86295
         ICM   0,3,FDBLRC                                      @SC86295
         ST    0,FSIZE       Copy LRECL                        @SC86295
         MVC   FRECF,FDBRCF  Copy RECFM                        @SC86295
         DROP  1                                               @SC86155
GENOPN   KCALL DECODE,E=GENAB2 Copy message to output          @SC86155
         CLC   WBUFL,F0      Any more?                         @SC86155
         BE    GENRPZ                                          @SC86155
         KCALL OUTBUF,E=GENAB2  Yes, copy that as well         @SC86155
GENRPZ   CLOSF FILPTR                                          @SC86295
         MVI   ERRNUM,ERRNOE No errors                         @SC86155
         B     GENFIN                                          @SC86295
*
GENRPL   OI    FL2,SRV       Pretend this is server mode       @SC86155
         BAL   2,GENRPS      Set up file name                  @SC86295
         KCALL RECEIV                                          @SC86155
         NI    FL2,255-SRV                                     @SC86155
         B     GENFIN                                          @SC86155
*
GENRPS   LA    0,L'REPNAM    Name string length                @SC86295
         LA    1,REPNAM      and address                       @SC86295
         STM   0,1,SCANPTR                                     @SC86295
         LA    0,FFRCF                                         @SC86295
         KCALL FSPEC,FILNAM  Convert to filespec               @SC86295
         IC    9,FL3         Save flags                        @SC86295
         OI    FL3,APPN      Don't erase it                    @SC86295
         BR    2                                               @SC86295
*
GENAB2   CLOSF FILPTR                                          @SC86295
GENABR   KCALL ERPACK                                          @SC86155
GENFIN   STC   9,FL3         Restore flags                     @SC86295
GENRET   KCALL INTINI,0                                        @SC86155
         B     RTRN0                                           @SC86295
*
* Make foreign Kermit execute command
REMCMD   NTOKN N=RTRN2                                         @SC86295
         SCAN  REMCMDKW,RTRN1                                  @SC86295
         B     RTRN2                                           @SC86295
*
REMCMDKW KW    'COPY',REMCOP,MIN=2                             @SC86295
         KW    'CWD',REMARG,MIN=3                              @SC86295
         KW    'DIRECTORY',REMARG,MIN=3                        @SC86155
         KW    'ERASE',REMARG                                  @SC86155
         KW    'HELP',REMARG                                   @SC86155
         KW    'HOST',REMHST,MIN=2                             @SC86155
         KW    'KERMIT',REMKRM                                 @SC86155
         KW    'RENAME',REMREN                                 @SC86295
         KW    'SPACE',REMSPA,MIN=2                            @SC86155
         KW    'TYPE',REMARG,MIN=2                             @SC86155
         KW    ,                                               @SC86155
*
REMHST   LA    0,AC          Host command                      @SC86155
         B     REMPRS                                          @SC86155
*
REMKRM   LA    0,AK          KERMIT command                    @SC86155
REMPRS   FTOKN N=RTRN1       See if anything given             @SC86295
         LR    4,7                                             @SC86295
         LR    5,6           Use whole string                  @SC86295
         B     GENFILL                                         @SC86295
*
REMSPA   LA    1,AU          Space command                     @SC86155
         B     REMPRSG                                         @SC86155
*
REMCOP   LA    8,2           Copy: two files                   @SC86295
         LA    1,AK                                            @SC86295
         B     REMPRSG                                         @SC86295
REMREN   LA    8,2           Rename: two files                 @SC86295
*
REMARG   SR    1,1                                             @SC86155
         IC    1,0(6)        1st letter is abbrev              @SC86155
         IC    1,ETOA(1)     ASCII                             @SC86155
REMPRSG  LA    0,AG          (generic)                         @SC86155
         NTOKN N=GENNUL      Skip any blanks                   @SC86295
         LA    5,1(7)        Save length                       @SC86295
         LR    4,6           Save ptr                          @SC86295
         B     GENFILL       Copy to output                    @SC86155
         LOCALS ,                                              @SC86295
REMCMD   EXIT  ,                                               @SC86155
