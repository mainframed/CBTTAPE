./ ADD NAME=IKJCT44B 0100-92115-92118-1606-000141-000120-000000-$A1019
IKJCCT4B TITLE 'ROCKWELL CLIST BUILT-IN FUNCTION EXIT'
***********************************************************************
*        Rockwell CLIST Built-in Function Exit
*
*        CLIST Function &SYSXSHOW Interface to SHOW
*
*        Use: refer to &SYSXSHOW(id), where "id" is a valid value for
*        the SHOW routine.
*        "id" may be a constant or a variable, and may be either
*        uppercase or lowercase.
*
*
*        Douglas H. Adams
*        Mail code 110-SE28
*        Information Systems Center
*        Rockwell International Corporation
*        2201 Seal Beach Boulevard
*        P. O. Box 2515
*        Seal Beach, California 90740-1515
*
*        (310) 797-2618
*
*        April 24, 1992
*
***********************************************************************
         PUNCH ' ALIAS IKJCT44B'
IKJCCT4B CSECT
IKJCCT4B AMODE 31
IKJCCT4B RMODE ANY
         EJECT
***********************************************************************
*                                  EXIT PARAMETERS
***********************************************************************
PARMS    DSECT                     PARAMETERS PASSED TO EXIT
PCBUF    DS    A                   COMMAND BUFFER
PNCBUF   DS    A                   NEW COMMAND BUFFER
PUPT     DS    A                   USER PROFILE TABLE
PECT     DS    A                   ENVIRONMENTAL CONTROL TABLE
PPSCB    DS    A                   PROTECTED STEP CONTROL BLOCK
PCOMM    DS    A                   EXIT-TO-EXIT COMMUNICATION WORD
PREASON  DS    A                   EXIT REASON CODE
         DS    A                   RESERVED
         DS    A                   RESERVED
PNAME    DS    A                   NAME OF BUILT-IN FUNCTION
PARG     DS    A                   ARGUMENT OF BUILT-IN FUNCTION
PRESULT  DS    A                   256 BYTE ANSWER AREA
PALTLEN  DS    A                   ALTERNATE BUFFER LENGTH
*
PARMENT  DSECT                     PARAMETER ENTRY
KEY      DS    XL4                 TYPE OF ENTRY
LENGTH   DS    F                   LENGTH OF DATA
DATA     DS    A                   DATA OR ADDRESS OF DATA
         EJECT
***********************************************************************
*                                  WORK AREA FOR REENTRANCE
***********************************************************************
WORKAREA DSECT
SAVE     DS    18F                 SAVE AREA
ID       DS    CL8
CALL     CALL  ,(ID,ILENGTH,,),MF=L  SHOW PARAMETERS
WORKSIZE EQU   *-WORKAREA
***********************************************************************
*                                  INITIALIZATION
***********************************************************************
IKJCCT4B CSECT
         USING IKJCCT4B,15
         SAVE  (14,12)             SAVE REGISTERS
         MODID
         DROP  15
         LR    12,15               SET BASE REGISTER
         USING IKJCCT4B,12
         LR    2,1                 SAVE PARAMETER POINTER
         USING PARMS,2
         LA    0,WORKSIZE          GET WORK AREA SIZE
         GETMAIN RU,LV=(0)         GET WORKAREA STORAGE
         XC    0(WORKSIZE,1),0(1)  CLEAR WORK AREA
         ST    1,8(,13)            CHAIN SAVE AREAS
         ST    13,4(,1)
         LR    13,1
         USING WORKAREA,13
***********************************************************************
*                                  GET FUNCTION VALUE
***********************************************************************
         L     3,PNAME             POINT TO FUNCTION NAME ENTRY
         USING PARMENT,3
         L     4,LENGTH            GET LENGTH OF FUNCTION  NAME
         C     4,=A(L'SYSXSHOW)    CORRECT LENGTH?
         BNE   ERROR1              NO, BRANCH
         L     5,DATA              POINT TO FUNCTION NAME
         CLC   SYSXSHOW,0(5)       CORRECT NAME?
         BNE   ERROR1              NO, BRANCH
         L     3,PARG              POINT TO ARGUMENT ENTRY
         ICM   4,15,LENGTH         GET LENGTH OF ARGUMENT
         BNP   ERROR2              NULL OR NEGATIVE, BRANCH
         C     4,=F'8'             IS ARG TOO BIG?
         BH    ERROR2              YES, BRANCH
         MVC   ID,=CL8' '          SET ID TO BLANKS
         BCTR  4,0                 GET MACHINE LENGTH OF ID
         L     5,DATA              POINT TO ID
         EX    4,MVC               MOVE ARG TO ID
         L     3,PRESULT           POINT TO RESULT ENTRY
         LA    4,LENGTH            POINT TO LENGTH OF RESULT
         L     5,DATA              POINT TO RESULT
         ICM   15,15,=V(SHOW)      GET ENTRY POINT OF SHOW
         BZ    LINK                BRANCH IF NOT RESOLVED
         CALL  (15),(ID,ILENGTH,(5),(4)),VL,MF=(E,CALL)
         B     TESTRET             GO TEST RETURN CODE
LINK     EQU   *
         LINK  EP=SHOW,PARAM=(ID,ILENGTH,(5),(4)),VL=1,MF=(E,CALL)
TESTRET  EQU   *
         LTR   15,15               SUCCESSFUL?
         BZ    RETURN              YES, BRANCH
         BNZ   ERROR2              NO, BRANCH
***********************************************************************
*                                  TERMINATION
***********************************************************************
         EJECT
ERROR1   EQU   *
         LA    15,8                NOT A SUPPORTED FUNCTION
ERROR2   EQU   *
         LA    15,12               EXIT PROCESSING UNSUCCESSFUL
*                                  CLIST PROCESSING ISSUES MESSAGE
RETURN   EQU   *
         LR    2,15                SAVE RETURN CODE
         LA    0,WORKSIZE          GET WORKAREA SIZE
         LR    1,13                UNCHAIN SAVE AREAS
         L     13,4(,1)
         FREEMAIN RU,LV=(0),A=(1)  FREE WORK AREA STORAGE
         LR    15,2                RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN
*
MVC      MVC   ID(0),0(5)          EXECUTED
         DROP  13
         DROP  12
         EJECT
***********************************************************************
*                                  DATA
***********************************************************************
SYSXSHOW DC    C'SYSXSHOW'         NAME OF FUNCTION
ILENGTH  DC    F'256'              CLIST RESULT LENGTH
         END
./ ADD NAME=IRXFLOC  0100-92114-92115-1615-000044-000026-000000-$A1019
IRXFLOC  TITLE 'ROCKWELL REXX FUNCTION PACKAGE DIRECTORY'
***********************************************************************
*        Rockwell REXX Function Package Directory
*
*        See macro IRXFPDIR for the format of this module.
*
*
*        Douglas H. Adams
*        Mail code 110-SE28
*        Information Systems Center
*        Rockwell International Corporation
*        2201 Seal Beach Boulevard
*        P. O. Box 2515
*        Seal Beach, California 90740-1515
*
*        (310) 797-2618
*
*        April 24, 1992
*
***********************************************************************
         PUNCH ' ALIAS IRXFLOC'
IRXFLOC  CSECT
IRXFLOC  AMODE 31
IRXFLOC  RMODE ANY
         EJECT
         IRXFPDIR                  REXX FUNCTION PACKAGE DIRECTORY
         EJECT
IRXFLOC  CSECT
HEADER   DC    CL8'IRXFPACK'       STRING IDENTIFYING DIRECTORY
         DC    A(ENTRIES-HEADER)   LENGTH OF HEADER
         DC    A((END-ENTRIES)/32) NUMBER OF ROWS IN DIRECTORY
         DC    F'0'                WORD OF ZEROS
         DC    A(FPCKDIR_NEXT-FPCKDIR_ENTRY)  LENGTH OF DIRECTORY ENTRY
*
ENTRIES  EQU   *
*
         DC    CL8'SHOWREXX'       NAME USED IN EXEC
         DC    V(SHOWREXX)         ADDRESS OF PRELOADED CODE
         DC    F'0'                RESERVED FIELD
         DC    CL8' '              NAME OF ENTRY POINT
         DC    CL8' '              DD FROM WHICH TO LOAD ENTRY POINT
*
END      EQU   *
         END
./ ADD NAME=SHOW     0147-90107-92118-1530-001517-000743-001171-$A1019
SHOW     TITLE 'SHOW - EXTRACT INFORMATION FROM SYSTEM CONTROL BLOCKS'
***********************************************************************
*
*        How to Use This Program.
*
*        The SHOW routine provides the caller with character string
*        information extracted from system control blocks. Numeric and
*        bit information is converted to character string format.
*
*        Entry linkage:
*              CALL SHOW,(ID,ILENGTH,VALUE,OLENGTH),VL
*
*        Parameters:
*              ID      - Character string. Input.  An 8 character
*                        identifier for the information requested by
*                        the caller, padded with blanks if necessary.
*
*              ILENGTH - Full word binary. Input.  The length of the
*                        VALUE character string provided by the caller.
*                        The VALUE will be padded with trailing blanks
*                        or truncated if necessary. A maximum of 32767
*                        is supported.
*
*              VALUE   - Character string. Output.  The returned
*                        information specified by ID. The caller must
*                        provide a receiving character string of
*                        sufficient length.
*
*              OLENGTH - Full word binary. Output.  The actual length
*                        of the value requested. This may be less than
*                        or greater than ILENGTH.
*
*        Return codes:
*               0 - Normal completion
*               4 - VALUE truncated, insufficient length
*               8 - ID not found
*              12 - Too few arguments
*              16 - System control block failed validity check
*
*
*        Douglas H. Adams          Lionel B. Dyck
*               Mail Code 110-SE28
*               Information Systems Center
*               Rockwell International Corporation
*               2201 Seal Beach Boulevard
*               P. O. Box 2515
*               Seal Beach, California 90740-1515
*
*        (310) 797-2618            (310) 797-1125
*
*               April 24, 1992
*
***********************************************************************
         EJECT
***********************************************************************
* Supported arguments are:                                 *LBD 02/84*
*                                                          *LBD 02/84*
* ID        ILENGTH Description                            *DHA 04/92*
*----------------------------------------------------------------------
* ACCT       11     Rockwell Job Card Account (with FDS #) *LBD 06/89*
* ACCTNO      8     Rockwell Job Card Account (no FDS #)   *LBD 02/84*
* ACCTNO#     8     Rockwell ACF2 User's Account (no FDS #)*LBD 12/86*
* ACF2ACCT   40     ACF2 User's Default Accounting Field   *LBD 02/84*
* BCPFMID     8     Basic Control Program FMID             *DHA 04/92*
* BCPLEVEL    8     Basic Control Program Product Name     *LBD 04/91*
* CENTER      8     Rockwell Center Name                   *LBD 05/84*
* COMPMAIL    4     Rockwell ACF2 User's Computer Mail Code*LBD 06/86*
* CPU         8     System Name from SYS1.PARMLIB(IEASYSxx)*LBD 06/89*
* CPUMODEL    4     CPU Model (e.g., 3090)                 *DHA 04/92*
* CPUSER    112     CPU Serial(s) (Maximum 16)             *DHA 04/92*
* CPUVER      2     CPU Version                            *DHA 04/92*
* CRTCD       2     Rockwell Job Card CRT Code             *LBD 02/84*
* DEPT        3     Rockwell ACF2 User's Department        *LBD 02/84*
* DEST        8     TSO User's Default Destination         *LBD 03/84*
* DFPLEVEL    5     Data Facility Product Level            *LBD 04/91*
* FDS         3     Rockwell Job Card FDS #                *LBD 02/84*
* FDS#        4     Rockwell ACF2 User's FDS #             *LBD 12/86*
* FRAMES      3     Rockwell Job Card CRT Frame Limit      *LBD 02/84*
* GROUP       3     Rockwell ACF2 User's Group (in Dept)   *LBD 02/84*
* GRP         3     Rockwell ACF2 User's Group (in Dept)   *DHA 04/92*
* HOMEDIV     4     Rockwell ACF2 User's Home Division     *LBD 10/90*
* JOBFUNC     2     Rockwell ACF2 User's Job Function Code *LBD 10/84*
* JOBNAME     8     Job Name                               *LBD 02/84*
* LINES       8     Rockwell Job Card FORTRAN Line Limit   *LBD 02/84*
* LOC         4     Rockwell ACF2 User's Physical Location *LBD 02/84*
* MAIL        8     Rockwell ACF2 User's Mail Code         *LBD 02/84*
* NAME       20     ACF2 User's Name                       *LBD 02/84*
* OPSYS       7     Operating System (MVS/xxx)              *LBD 05/89*
* OS          3     Operating System (xxx)                  *LBD 07/89*
* OSLEV       3     Operating System (xxx)                  *LBD 07/89*
* OSREL       4     Operating System Release Number        *LBD 03/84*
* PHONE      12     ACF2 User's Phone Number               *LBD 02/84*
* REFERVOL    6     Volume Serial of Dataset userid.REFER  *DHA 04/92*
* SHOWLEV    14     Level of SHOW (Date and Time)           *LBD 04/91*
* SYSNAME     8     System Name from SYS1.PARMIB(IEASYSxx) *LBD 03/84*
* TERMINAL    8     Terminal ID                            *LBD 02/84*
* TSOOPER     6     TSO OPERATOR Authority (YES or NO)      *LBD 06/89*
* TSOUNIT     8     TSO User's Default Unit Name           *LBD 03/87*
* UID        24     Rockwell ACF2 User's UID String         *LBD 06/89*
* USERID      8     User ID                                *LBD 02/84*
*----------------------------------------------------------------------
* The following are derived from control blocks or fields
* which are not official IBM programming interfaces. Use
* them at your own risk.
*
* ID        ILENGTH Description                            *DHA 04/92*
*----------------------------------------------------------------------
* BDTNODE     8     MVS/BDT Node Name                      *LBD 06/84*
* CLPA        3     CLPA Specified at last IPL (YES or NO) *DHA 04/92*
* CVIO        3     CVIO Specified at IPL (YES or NO)      *DHA 04/92*
* IPLDATE     8     Last IPL Date                          *DHA 04/92*
* IPLTIME    10     Last IPL Time                          *DHA 04/92*
* JOBID       8     Job ID (JOBnnnnn)                      *LBD 12/89*
* LPAR        3     Logical Partition Mode (YES or NO)     *DHA 04/92*
* PRTY        2     Job Scheduling Priority                 *LBD 07/89*
* REALSTOR   17     Real Storage Available at Last IPL     *DHA 04/92*
* REGION     17     Region Size (nK)                      *LBD 07/85*
* SMFID       4     SMF ID from SYS1.PARMLIB(SMFPRMxx)      *LBD 06/89*
* SMS         3     Status of SMS (ON or OFF)              *LBD 01/91*
* SWA         5     SWA Location (ABOVE or BELOW)          *LBD 04/91*
* SYSID       4     System ID from SYS1.PARMLIB(SMFPRMxx)  *LBD 03/84*
* SYSRES      6     System Residence Volume Serial         *DHA 04/92*
* VERID      16     Installation System Version (CVTVERID) *LBD 03/84*
***********************************************************************
         EJECT
***********************************************************************
*        Assembly Parameters                               *DHA 04/92*
***********************************************************************
         GBLC  &SYSSPLV            SPLEVEL                 *DHA 04/92*
         SPLEVEL SET               SET MVS/SP VERSION      *DHA 04/92*
&ACF2    SETB  1                   0 = NO ACF2             *DHA 04/92*
&JES3    SETB  1                   0 = NO JES3             *DHA 04/92*
&ROCKWELL SETB 1                   0 = NO ROCKWELL         *DHA 04/92*
         EJECT
***********************************************************************
*
*        How to Modify This Program.
*
*        This program has been designed to be easy to modify.  It
*        consists of 6 sections:
*              (1) Macro definitions
*              (2) Control block definitions (DSECTs)
*              (3) Work area definitions (DSECT)
*              (4) Initialization
*              (5) VALUE determination
*              (6) Termination
*
*        Modifications will normally be done in section 2 and section
*        5.  A modification may also require changes to section 3.
*        Sections 4 and 6 are designed to minimize the amount of code
*        required in section 5.
*
*        The functions  performed by the sections are:
*
*              1. The SHOW macro. There must be a SHOW macro call in
*                 section 5 for each ID. The syntax is:
*                    id SHOW <BLOCK=block,FIELD=field><,WORK={YES|NO}>
*                 Required:  id.
*                 Defaults:  BLOCK=0,FIELD=0,WORK=NO.
*                 id     is the ID name.
*                 BLOCK  is the name of an area containing a character
*                        string is to be copied to VALUE.  This is
*                        usually the name of a control block defined by
*                        a DSECT, but it may be a name in the work
*                        area (WORKAREA) or the name of a constant
*                        defined by a DC within this module.
*                 FIELD  is the name of the character string to be
*                        copied to VALUE.  This defines the offset from
*                        BLOCK and the length of the character string.
*                 WORK   specifies whether or not a GETMAIN should be
*                        issued for a work area.
*
*              2. Control block DSECTs.  Macro calls needed to define
*                 control blocks should be placed here.
*
*              3. Work area definitions. An optional 4K work area may
*                 be requested via the SHOW macro to allow reentrant
*                 coding. It is RMODE 24.  The work area begins with a
*                 standard 18 word save area, pointed to by register
*                 13.  If an ID requires a work area, an ORG WORKORG
*                 should be used and unique set of DS and EQU
*                 instructions should be defined.
*
*              4. Initialization. This section should not be modified.
*                 It obtains and validity checks the parameters.  It
*                 then initializes the caller's output area to blanks
*                 (for a length of ILENGTH) and sets OLENGTH to zero.
*                 Next it determines which ID was requested.  If the ID
*                 requires a work area, it issues a GETMAIN.  Finally,
*                 it branches to the appropriate code in section 5.
*                 The following registers are set and must not be
*                 changed:
*                    R8   Pointer to SHOW table entry
*                    R9   Pointer to OLENGTH
*                    R10  Pointer to VALUE
*                    R11  Value of ILENGTH
*                    R12  Base register
*                    R13  Pointer to save area and optional work area.
*                         If no work area was obtained, the save area
*                         is that of SHOW's caller.
*
*              5. Determination of the requested VALUE. The code
*                 receives control with RMODE 24 and AMODE 31. It must
*                 be reentrant.  There must be SHOW macro for each ID
*                 in this section. When multiple IDs refer to different
*                 fields within a specific control block, they should
*                 be grouped together with the same BLOCK value and
*                 different FIELD values. The code following the SHOW
*                 macro(s) must find the appropriate BLOCK value and
*                 point to it with register 1.  The BLOCK value may be
*                 a control block, the work area, or a constant within
*                 this module. The code must then branch to label
*                 FOUND.  Control blocks should be validity checked. If
*                 they fail, the code should branch to label INVALID.
*                 Normally, the length of FIELD is determined at
*                 assembly time. If the length must be determined
*                 dynamically, the code should load the length into
*                 register 2 and branch to label FOUNDLEN.
*
*              6. Termination.  This section should not be modified.
*                 When reached at label FOUND, it loads the length from
*                 the SHOW macro FIELD definition. This is bypassed if
*                 label FOUNDLEN is used. The code then strips off
*                 leading and trailing blanks and nulls and move the
*                 result to the caller's VALUE. If ILENGTH is
*                 insufficient the VALUE is truncated.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        Section 1 - Macro Definitions
*
***********************************************************************
         MACRO
&ID      SHOW  &BLOCK=0,           SHOW IDENTIFIER/CONTROL BLOCK       *
               &FIELD=0,           FIELD WITHIN CONTROL BLOCK          *
               &WORK=NO            WORK AREA NEEDED?       *DHA 04/90*
.*********************************************************************
.*       &ID    Specifies the external identifier used by the caller.
.*       &BLOCK Specifies the control block containing the required
.*              information.
.*       &FIELD Specifies the control block field which corresponds
.*              the external identifier. Note the length of field is
.*              obtained from the control block definition.
.*       &WORK  Specifies whether or not a GETMAIN should be issued
.*              for a work area.
.*********************************************************************
         GBLA  &SHOWN              NUMBER OF TABLE ENTRIES
         GBLC  &SHOWID(100)        IDS (PADDED WITH BLANKS)
         GBLC  &SHOWBLK(100)       BLOCKS (PADDED WITH BLANKS)
         GBLC  &SHOWFLD(100)       FIELDS (PADDED WITH BLANKS)
         GBLB  &SHOWWORK(100)      WORK AREA REQUIRED      *DHA 04/90*
&ID      DS    0H
&SHOWN            SETA &SHOWN+1    NUMBER OF IDS
&SHOWID(&SHOWN)   SETC '&ID'       SAVE ID
&SHOWBLK(&SHOWN)  SETC '&BLOCK'    SAVE BLOCK
&SHOWFLD(&SHOWN)  SETC '&FIELD'    SAVE FIELD
&SHOWWORK(&SHOWN) SETB ('&WORK' EQ 'YES')                  *DHA 04/90*
         MEND
         EJECT
***********************************************************************
         MACRO
&NAME    SHOWTBL &END              GENERATE SHOW ID TABLE
         GBLA  &SHOWN              NUMBER OF TABLE ENTRIES
         GBLC  &SHOWID(100)        IDS (PADDED WITH BLANKS)
         GBLC  &SHOWBLK(100)       BLOCKS (PADDED WITH BLANKS)
         GBLC  &SHOWFLD(100)       FIELDS (PADDED WITH BLANKS)
         GBLB  &SHOWWORK(100)      WORK AREA REQUIRED      *DHA 04/90*
         LCLA  &I,&L
         LCLC  &N
         SPACE
SHOWENT  DSECT                     SHOW TABLE
SHOWID   DS    CL8                 ID (FIXED LENGTH)
SHOWFLD  DS    Y                   OFFSET OF CONTROL BLOCK FIELD
SHOWRTN  DS    Y                   OFFSET TO EXECUTABLE ROUTINE
SHOWFLDL DS    H                   LENGTH OF CONTROL BLOCK FIELD
SHOWFLAG DS    X                   FLAGS                   *DHA 04/90*
SHOWWORK EQU   X'01'               WORK AREA REQUIRED      *DHA 04/90*
         DS    X                   PAD                     *DHA 04/90*
SHOWSIZE DS    0XL(*-SHOWENT)      SIZE OF TABLE ENTRY
         SPACE
&SYSECT  CSECT
         SPACE
         DS    0D
         SHOWSORT 1,&SHOWN         SORT ID'S
&N       SETC  '&NAME'             NAME START OF TABLE
.LOOP    ANOP
&I       SETA  &I+1                INCREMENT INDEX
         AIF   (&I GT &SHOWN).END  END OF TABLE?
         AIF   (&I LT &SHOWN).GEN  STILL WITHIN TABLE?
&N       SETC  '&END'              NAME END OF TABLE
.GEN     ANOP
&N       DC    CL8'&SHOWID(&I)'              ID
         DC    Y(&SHOWFLD(&I)-&SHOWBLK(&I))  OFFSET OF FIELD
         DC    Y(&SHOWID(&I)-&SYSECT)        OFFSET TO ROUTINE
         DC    AL2(L'&SHOWFLD(&I))           LENGTH OF FIELD
         DC    X'&SHOWWORK(&I)'              FLAGS
         DC    X'00'
&N       SETC  ''                  NULLIFY NAME FIELD
         AGO   .LOOP               LOOP FOR NEXT ID
.END     SPACE
         MEND
         EJECT
***********************************************************************
         MACRO
         SHOWSORT &M,&N            SORT ID'S
         GBLB  &SHOWWORK(100)      WORK AREA REQUIRED      *DHA 04/90*
         GBLC  &SHOWID(100)        IDS (PADDED WITH BLANKS)
         GBLC  &SHOWBLK(100)       BLOCKS (PADDED WITH BLANKS)
         GBLC  &SHOWFLD(100)       FIELDS (PADDED WITH BLANKS)
         LCLA  &I,&J,&F,&TEMPA
         LCLB  &TEMPB                                      *DHA 04/90*
         LCLC  &X,&TEMPC,&XI,&XJ
         AIF   (&M GE &N).SORTED   IS THIS SEGMENT SORTED?
&I       SETA  &M                  INITIALIZE START OF SEGMENT
&J       SETA  &N                  INITIALIZE END OF SEGMENT
&F       SETA  (&M+&N)/2           GET MIDDLE OF SEGMENT
&X       SETC  '&SHOWID(&F)'.(8-K'&SHOWID(&F))' '  GET MIDDLE ELEMENT
.UP      ANOP                      SEARCH FOR OUT OF PLACE ELEMENT
&XI      SETC  '&SHOWID(&I)'.(8-K'&SHOWID(&I))' '  GET UPPER ELEMENT
         AIF   ('&X' LE '&XI').DOWN  HIGH ELEMENT FOUND?
&I       SETA  &I+1                INCREMENT INDEX
         AGO   .UP                 LOOP FOR NEXT ELEMENT
.DOWN    ANOP                      SEARCH FOR OUT OF PLACE ELEMENT
&XJ      SETC  '&SHOWID(&J)'.(8-K'&SHOWID(&J))' '  GET LOWER ELEMENT
         AIF   ('&X' GE '&XJ').CHANGE  LOW ELEMENT FOUND?
&J       SETA  &J-1                DECREMENT INDEX
         AGO   .DOWN               LOOP FOR NEXT ELEMENT
.CHANGE  AIF   (&I GT &J).SPLIT    IS SEGMENT NOW SPLIT?
&TEMPC       SETC '&SHOWID(&I)'    EXCHANGE ID ELEMENTS
&SHOWID(&I)  SETC '&SHOWID(&J)'
&SHOWID(&J)  SETC '&TEMPC'
&TEMPC       SETC '&SHOWBLK(&I)'   EXCHANGE BLOCK ELEMENTS
&SHOWBLK(&I) SETC '&SHOWBLK(&J)'
&SHOWBLK(&J) SETC '&TEMPC'
&TEMPC       SETC '&SHOWFLD(&I)'   EXCHANGE FIELD ELEMENTS
&SHOWFLD(&I) SETC '&SHOWFLD(&J)'
&SHOWFLD(&J) SETC '&TEMPC'
&TEMPB       SETB (&SHOWWORK(&I))  EXCHANGE FLAG ELEMENTS  *DHA 04/90*
&SHOWWORK(&I) SETB (&SHOWWORK(&J))                         *DHA 04/90*
&SHOWWORK(&J) SETB (&TEMPB)                                *DHA 04/90*
&I       SETA  &I+1                INCREMENT LOW INDEX
&J       SETA  &J-1                DECREMENT HIGH INDEX
         AIF   (&I LT &J).UP       IS SEGMENT NOW SPLIT?
.SPLIT   ANOP                      SORT SEGMENTS RECURSIVELY
         SHOWSORT &M,&J            SORT UPPER SEGMENT
         SHOWSORT &I,&N            SORT LOWER SEGMENT
.SORTED  MEND
         EJECT
***********************************************************************
*
*        Section 2 - Control Block Definitions
*
***********************************************************************
         PUNCH ' ENTRY SHOW'
SHOW     CSECT
SHOW     AMODE 31                                          *DHA 04/90*
SHOW     RMODE 24                  For RMODE 24 callers    *DHA 04/90*
*----------------------------------------------------------------------
*                                  ACF2 Control Blocks     *DHA 04/92*
*----------------------------------------------------------------------
         AIF   (NOT &ACF2).ACF2A                           *DHA 04/92*
         ACCVT                     ACF2 CVT                *LBD 02/84*
         EJECT
         ACFASVT                   ACF2 Address Space      *LBD 02/84*
*                                  Vector Table
         EJECT
         ACUCB                     ACF2 Address Space C. B.*LBD 02/84*
         EJECT
ACUID    ACUID                     ACF2 UID String         *DHA 04/90*
         LIDREC DSECT=YES          ACF2 Logon ID Record    *LBD 02/84*
LIDACCT# EQU   LIDACCT+3,4                                 *LBD 02/84*
.ACF2A   ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*                                  JES3 Control Blocks     *DHA 04/92*
*----------------------------------------------------------------------
         AIF   (NOT &JES3).JES3A                           *DHA 04/92*
         IATYUSVT                  BDT User Subsystem     *LBD 06/84*
*                                  Vector Table
.JES3A   ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*                                  Rockwell Control Blocks *DHA 04/92*
*----------------------------------------------------------------------
         AIF   (NOT &ROCKWELL).ROCKWELL1                   *DHA 04/92*
         RATYJCD                   Rockwell JOB Card
FDSACCT  EQU   ABFDS,11            Rockwell Account Number  *LBD 06/89*
         EJECT
         RCVT                      Rockwell CVT            *DHA 04/92*
.ROCKWELL1 ANOP                                            *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*                                  MVS Control Blocks      *DHA 04/92*
*----------------------------------------------------------------------
         CVT   DSECT=YES,PREFIX=YES,LIST=YES  CVT          *LBD 03/84*
         EJECT
         IEESMCA                   SMF Control table       *LBD 03/84*
         EJECT
IEFAJCTB DSECT
         IEFAJCTB                  Job Control Table
         EJECT
         IEFJESCT                  JES Communication Table*LBD 06/84*
         EJECT
         IEFJSCVT                  Subsystem CVT          *LBD 06/84*
         EJECT
         IEFJSSIB ,                Subsystem ID Block      *LBD 12/89*
         EJECT
IEFTIOT1 DSECT                                             *DHA 04/92*
         IEFTIOT1                  Task Input/Output Table *DHA 04/92*
         EJECT
IEFUCBOB DSECT                                             *DHA 04/92*
         IEFUCBOB                  Unit Control Block      *DHA 04/92*
         EJECT
         IEZBITS                   Bit Definitions        *LBD 06/84*
         EJECT
         IEZJSCB                   Job Step Control Block
         EJECT                                            *
         IHAACEE                   Accessor Environment    *DHA 04/92*
*                                  Element                 *DHA 04/92*
         EJECT
         IHAASCB                   Address Space C. B.     *LBD 08/85*
         EJECT                                            *
         IHAASXB                   Address Space Extension *DHA 04/92*
*                                  Block                   *DHA 04/92*
         EJECT
         IHADFA ,                  Data Facilities Area    *LBD 01/91*
         EJECT
         IHALDA                    Local Data Area         *LBD 08/85*
         EJECT                                            *
         IHAPCCA                   Physical Configuration  *DHA 04/92*
*                                  Communication Area      *DHA 04/92*
VERSION  EQU   PCCACPID,2          CPU VERSION             *DHA 04/92*
         EJECT
         IHAPCCAT                  Physical Configuration  *DHA 04/92*
*                                  Communication Area      *DHA 04/92*
*                                  Vector Table            *DHA 04/92*
         EJECT
         IHAPSA LIST=YES           Prefixed Save Area
         EJECT
         IHASCCB                   Service Call C.B.       *DHA 04/92*
         EJECT
         IKJPSCB                   TSO Protected Step C.B. *DHA 04/92*
         EJECT
         IKJTCB LIST=YES           Task Control Block
         EJECT                                            *
*        ILRASMVT                  Asm Vector Table        *DHA 04/92*
ASMVT    DSECT                                             *DHA 04/92*
ASMFLAG1 DS    B                   ASM Global flag field 1 *DHA 04/92*
ASMFLAG2 DS    B                   ASM Global flag field 2 *DHA 04/92*
ASMQUICK EQU   B'00001000'         Quick Start (no CLPA)   *DHA 04/92*
ASMWARM  EQU   B'00000100'         Warm Start (no CVIO)    *DHA 04/92*
         EJECT
         REGEQU                    Registers
         EJECT
***********************************************************************
*
*        Parameter Definitions
*
***********************************************************************
PARMS    DSECT
AID      DS    A                   A(ID)
AILENGTH DS    A                   A(ILENGTH)
AVALUE   DS    A                   A(VALUE)
AOLENGTH DS    A                   A(OLENGTH)
PARM1    DSECT                     FIRST PARAMETER
ID       DS    CL8                 IDENTIFIER
PARM2    DSECT                     SECOND PARAMETER
ILENGTH  DS    F                   INPUT LENGTH OF VALUE
PARM3    DSECT                     THIRD PARAMETER
VALUE    DS    CL256               VALUE ASSOCIATED WITH IDENTIFIER
PARM4    DSECT                     THIRD PARAMETER
OLENGTH  DS    F                   OUTPUT LENGTH OF VALUE
         EJECT
***********************************************************************
*
*        Section 3 - Work Area for Reentrance
*
***********************************************************************
WORKAREA DSECT                     GETMAIN AREA            *DHA 04/90*
SAVEAREA DS    18F                 SAVE AREA               *DHA 04/90*
*----------------------------------------------------------------------
*                                                          *DHA 04/90*
*        Origin for Individual Work Areas                  *DHA 04/90*
*                                                          *DHA 04/90*
*        Each ID which requires a work area should begin   *DHA 04/90*
*        with an ORG WORKORG.                              *DHA 04/90*
*                                                          *DHA 04/90*
*----------------------------------------------------------------------
WORKORG  EQU   *                                           *DHA 04/90*
*----------------------------------------------------------------------
*        CPU MODEL NUMBER CONVERSION                       *DHA 04/92*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/92*
MODEL    DS    CL4                 UNPACKED MODEL NUMBER   *DHA 04/92*
*----------------------------------------------------------------------
*        PRTY CONVERSION                                   *DHA 04/90*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/90*
PRTYDWK  DS    D                   DOUBLE WORD WORKAREA    *DHA 04/90*
PRTYEDIT DS    0CL4                                        *DHA 04/90*
         DS    C' ',X'20'          PAD CHAR + EXTRA DIGIT  *DHA 04/90*
PRTYVAL  DS    XL2'2120'           PRTY                    *DHA 04/90*
*----------------------------------------------------------------------
*        REGION SIZE CONVERSION                            *DHA 04/90*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/90*
REGDWK   DS    D                DOUBLE WORD WORKAREA       *LBD 08/85*
REGSIZE  DS    0CL17               REGION SIZE             *DHA 04/92*
REGEDIT  DS    0CL16               REGION EDIT PATTERN     *DHA 04/90*
         DS    C' ',13X'20',X'2120',C'K'                   *DHA 04/92*
*----------------------------------------------------------------------
*        IKJEHCIR CATALOG INFO ROUTINE PARAMETER LIST      *DHA 04/90*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/90*
CIRPARM  DS    0D                                          *DHA 04/90*
CIROPT   DS    X'04'               ENTRY CODE (VOLUME INFO)*DHA 04/90*
         DS    2X                                          *DHA 04/90*
CIRLOCRC DS    X                   LOCATE RETURN CODE      *DHA 04/90*
CIRSRCH  DS    A(REFERDSN)         ADDRESS OF SEARCH ARG.  *DHA 04/90*
CIRCVOL  DS    A(0)                ADDRESS OF CVOL ID      *DHA 04/90*
CIRWA    DS    A(AREALN)           ADDRESS OF WORK AREA    *DHA 04/90*
CIRSAVE  DS    A(SAVE)             ADDRESS OF SAVE AREA    *DHA 04/90*
CIRPSWD  DS    A(0)                ADRESS OF PASSWORD      *DHA 04/90*
*                                                          *DHA 04/90*
REFERDSN DS    CL44'USERID.REFER'  CIR DSNAME              *DHA 04/90*
*        CIRWA WORK AREA                                   *DHA 04/90*
AREALN   DS    Y(DATASIZE)         LENGTH OF WORK AREA     *DHA 04/90*
DATALIN  DS    H                   LENGTH OF DATA RETURNED *DHA 04/90*
DATA     DS    X                   NUMBER OF VOLUMES       *DHA 04/90*
DEVTYP   DS    XL4                 DEVICE TYPE             *DHA 04/90*
VOLSER   DS    CL6                 VOLUME SERIAL NUMBER    *DHA 04/90*
FILESEQ  DS    XL2                 FILE SEQUENCE NUMBER    *DHA 04/90*
         DS    12X'FF'             END OF LIST             *DHA 04/90*
DATASIZE EQU   *-AREALN            SIZE OF VOLUME WORK AREA*DHA 04/90*
*                                                          *DHA 04/90*
SAVE     DS    18F                 CIR SAVE AREA           *DHA 04/90*
*----------------------------------------------------------------------
*        DFPLEVEL CONVERSION                               *DHA 04/92*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/92*
DFPLEV   DS    CL5                 DFP LEVEL               *DHA 04/92*
*----------------------------------------------------------------------
*        IPL DATE CONVERSION                               *DHA 04/92*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/92*
T001BL   DS    0XL24                   MONTH TABLE
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
W001X    DC    F'00'
MX001M   DC    PL2'00'                 MONTH WORK AREA
R1001ST  DC    F'00'                   REGISTER SAVE AREA FOR REG
YR001X   DC    PL2'00'                 YEAR WORK AREA
DYXX     DC    PL2'00'                 DAY WORK AREA
XDATE    DS    0CL8                                        *DHA 04/92*
XM1      DC    CL2'  '                 MONTH O/P AREA
XMD1     DC    C'/'
XD1      DC    CL2'  '                 DAY O/P AREA
XDY1     DC    C'/'
XY1      DC    CL2'  '                 YEAR O/P AREA
*----------------------------------------------------------------------
*        IPL TIME CONVERSION                               *DHA 04/92*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/92*
@DWORD   DS    D
@TWORK   DS    CL6
@TIME    DS    0CL10
@HRS     DS    CL4
         DC    C'.'
@MIN     DS    CL2
         DC    C'.'
@SEC     DS    CL2
*----------------------------------------------------------------------
*        REAL STORAGE SIZE CONVERSION                      *DHA 04/92*
*----------------------------------------------------------------------
         ORG   WORKORG                                     *DHA 04/92*
REALDEC  DS    D                                           *LBD 08/85*
REALSIZE DS    0CL17               REAL STORAGE SIZE       *DHA 04/92*
REALEDIT DS    0CL16               EDIT PATTERN            *DHA 04/90*
         DS    C' ',13X'20',X'2120',C'M'                   *DHA 04/92*
*----------------------------------------------------------------------
*        CPU SERIAL NUMBERS                                *DHA 04/92*
*----------------------------------------------------------------------
SERIAL   DS    0CL6                A CPU SERIAL            *DHA 04/92*
SERIALS  DS    CL(7*16)            ALL CPU SERIALS         *DHA 04/92*
         ORG   WORKORG                                     *DHA 04/92*
*----------------------------------------------------------------------
*        END OF INDIVIDUAL WORK AREAS                      *DHA 04/90*
*----------------------------------------------------------------------
         ORG   ,                                           *DHA 04/90*
         EJECT
***********************************************************************
*
*        Section 4 - Initialization
*
*              Input:  R1 -> PARMS.
*              Output: VALUE = blanks.
*                      OLENGTH = 0.
*                      R8 -> SHOW table entry.
*                      R9 -> OLENGTH.
*                      R10 -> VALUE.
*                      R11 = ILENGTH.
*                      R12 = base register.
*                      R13 -> WORKAREA/SAVEAREA if requested.
*
***********************************************************************
SHOW     CSECT
         USING PSA,R0              APPLIES TO ENTIRE MODULE*DHA 04/92*
         USING *,R15
         USING PARMS,R1
         BSM   R14,0               SAVE AMODE              *DHA 04/90*
         SAVE  (14,12)             SAVE REGISTERS
         MODID ,                   MODULE IDENTIFICATION
         DROP  R15
         LR    R12,R15             R12 = BASE REGISTER
         USING SHOW,R12
         SR    R8,R8               NO TABLE ENTRY YET      *DHA 04/92*
         LR    R9,R1              SAVE R1                   *LBD 06/89*
         XABSM MODE=31            ENTER 31-BIT MODE         *LBD 06/89*
         LR    R1,R9              RELOAD R1                 *LBD 06/89*
         TM    AID,X'80'           IS ID LAST PARAMETER?
         BO    ARGERROR            YES, ERROR
         TM    AILENGTH,X'80'      IS ILENGTH LAST PARAMETER?
         BO    ARGERROR            YES, ERROR
         TM    AVALUE,X'80'        IS VALUE LAST PARAMETER?
         BO    ARGERROR            YES, ERROR
         L     R9,AOLENGTH         R9 -> OLENGTH
         USING OLENGTH,R9
         XC    OLENGTH,OLENGTH     OLENGTH = 0 IN CASE OF ERROR
         L     R10,AVALUE          R10 -> VALUE
         USING VALUE,R10
         L     R11,AILENGTH        R11 -> ILENGTH
         USING ILENGTH,R11
         ICM   R11,15,ILENGTH      R11 = ILENGTH
         DROP  R11
         BNP   TRUNCATE            ERROR IF ILENGTH <= 0
         LA    R2,VALUE            POINT TO TARGET         *DHA 04/92*
         LR    R3,R11              GET LENGTH OF TARGET    *DHA 04/92*
         LA    R5,C' '             PAD BYTE = BLANK        *DHA 04/92*
         SLL   R5,24               SOURCE LENGTH = 0       *DHA 04/92*
         MVCL  R2,R4               SET VALUE TO BLANKS     *DHA 04/92*
         L     R14,AID             R14 -> ID
         USING ID,R14
         DROP  R1                                          *DHA 04/92*
         EJECT
***********************************************************************
*        Determine whether ID is in table.
*              A binary search is made to determine whether the ID
*              in the internal table.  If so, a branch is
*              made to the appropriate routine.
*              A 4K GETMAIN is done if required.           *DHA 04/90*
***********************************************************************
         LM    R14,R15,ID          GET ID                  *DHA 04/92*
         DROP  R14
         O     R14,=CL4' '         INSURE UPPERCASE        *DHA 04/92*
         O     R15,=CL4' '                                 *DHA 04/92*
         SR    R4,R4               CLEAR FOR DIVISION
         LA    R6,L'SHOWSIZE       GET +LENGTH OF TABLE ENTRY
         LNR   R2,R6               GET -LENGTH OF TABLE ENTRY
         LA    R3,SHOWTBL          POINT TO LOW END OF TABLE
         LA    R7,SHOWTBLE         POINT TO HIGH END OF TABLE
LOOP     DS    0H
         LR    R5,R7               HIGH END OF TABLE
         SR    R5,R3               HIGH-LOW
         SRL   R5,1                (HIGH-LOW)/2
         DR    R4,R6               ((HIGH-LOW)/2)/SHOWSIZE
         MR    R4,R6               (((HIGH-LOW)/2)/SHOWSIZE)*SHOWSIZE
         AR    R5,R3               POINT TO MIDDLE ENTRY IN TABLE
         USING SHOWENT,R5
         CL    R14,SHOWID          COMPARE LEFT HALF OF ID *DHA 04/90*
         BL    LOW                 LOW: BRANCH             *DHA 04/92*
         BH    HIGH                HIGH: BRANCH
         CL    R15,SHOWID+4        COMPARE RIGHT HALF OF ID*DHA 04/92*
         BE    EQUAL               EQUAL: BRANCH
         BH    HIGH                HIGH: BRANCH            *DHA 04/92*
LOW      DS    0H
         LR    R7,R5               POINT HIGH AT MIDPOINT
         BXH   R7,R2,LOOP          MINUS TESTED ENTRY; HIGH > LOW?
         CR    R7,R3               ELSE HIGH = LOW?
         BE    LOOP
         B     NOID                NO, ERROR (NOT MATCHED)
*
HIGH     DS    0H
         LR    R3,R5               POINT LOW AT MIDPOINT
         BXLE  R3,R6,LOOP          PLUS TESTED ENTRY; LOW <= HIGH?
         B     NOID                NO, ERROR (NOT MATCHED)
*
EQUAL    DS    0H
         LR    R8,R5               POINT TO SHOW ENTRY     *DHA 04/90*
         DROP  R5                                          *DHA 04/90*
         USING SHOWENT,R8                                  *DHA 04/90*
         LH    R6,SHOWRTN          GET BRANCH DISPLACEMENT *DHA 04/92*
         TM    SHOWFLAG,SHOWWORK   WORK AREA NEEDED?       *DHA 04/90*
         BZ    SHOW(R6)            NO, BRANCH TO PROCESS ID*DHA 04/92*
         GETMAIN RU,LV=4096,BNDRY=PAGE,LOC=(BELOW,ANY)     *DHA 04/90*
         LR    R2,R1               R2 -> WORKAREA          *DHA 04/92*
         LA    R3,2048             R3 = 2048*2 = 4096 =    *DHA 04/92*
         SLL   R3,1                     TARGET LENGTH      *DHA 04/92*
*                                  R4 IRRELEVANT           *DHA 04/92*
         SR    R5,R5               R5 = 0 = PAD/LENGTH     *DHA 04/92*
         MVCL  R2,R4               ZERO OUT WORKAREA       *DHA 04/92*
         ST    R13,4(,R1)          CHAIN SAVE AREAS        *DHA 04/90*
         ST    R1,8(R13)           ...                     *DHA 04/90*
         LR    R13,R1              ...                     *DHA 04/90*
         B     SHOW(R6)            BRANCH TO PROCESS ID    *DHA 04/92*
         EJECT
***********************************************************************
*
*        Section 5 - Determination of the requested VALUE
*
*              Input:  R0 -> Prefixed Save Area (PSA)
*                      R8 -> SHOW TABLE ENTRY.
*                      R9 -> OLENGTH.
*                      R10 -> VALUE.
*                      R11 = ILENGTH.
*                      R12 = base register.
*                      R13 -> WORKAREA/SAVEAREA if requested.
*              Output: R1 -> BLOCK (beginning of control block).
*                      Optionally, R2 = length of FIELD.
*                      R8 -> SHOW TABLE ENTRY.
*                      R9 -> OLENGTH.
*                      R10 -> VALUE.
*                      R11 = ILENGTH.
*                      R12 = base register.
*                      R13 -> WORKAREA/SAVEAREA if requested.
*
***********************************************************************
*----------------------------------------------------------------------
*                  Assembly date/time
*----------------------------------------------------------------------
SHOWLEV  SHOW  BLOCK=SHOWDATE,FIELD=SHOWDATE               *DHA 04/92*
         LA    R1,SHOWDATE         R1 -> SHOWDATE          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
SHOWDATE DS    0CL14               SHOW ASSEMBLY           *DHA 04/92*
         DC    CL8'&SYSDATE'         DATE                  *DHA 04/92*
         DC    C' '                    AND                 *LBD 04/91*
         DC    CL5'&SYSTIME'             TIME              *LBD 04/91*
         EJECT
*----------------------------------------------------------------------
*                   Volume Serial of Data Set userid.REFER
*----------------------------------------------------------------------
REFERVOL SHOW  BLOCK=WORKAREA,FIELD=VOLSER,WORK=YES        *LBD 03/87*
         USING WORKAREA,R13                                *DHA 04/92*
         MVC   REFERDSN,=CL44' '   CLEAR CIR DSNAME        *DHA 04/90*
         L     R1,PSATOLD          POINT TO CURRENT TCB    *DHA 04/92*
         USING TCB,R1                                      *DHA 04/92*
         L     R1,TCBTIO           POINT TO TIOT           *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING TIOT1,R1                                    *DHA 04/92*
         MVC   REFERDSN(8),TIOCNJOB MOVE USER-ID           *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         LA    R1,REFERDSN         -> DSN                  *LBD 03/87*
         LA    R2,7                MAX SEARCH              *LBD 03/87*
USERLP1  DS    0H                                          *LBD 03/87*
         LA    R1,1(R1)            INCR R1                 *LBD 03/87*
         CLI   1(R1),C' '          END OF USERID           *LBD 03/87*
         BE    MOVREFER                                    *LBD 03/87*
         BCT   R2,USERLP1                                  *LBD 03/87*
MOVREFER MVC   1(7,R1),=CL7'.REFER'                        *LBD 03/87*
         MVI   CIROPT,X'04'        RETRIEVE VOLUME INFO    *DHA 04/90*
         LA    R0,REFERDSN         POINT TO DSNAME         *DHA 04/90*
         ST    R0,CIRSRCH          ADDRESS OF SEARCH ARG.  *DHA 04/90*
         XC    CIRCVOL,CIRCVOL     CLEAR CVOL ADDRESS      *DHA 04/90*
         LA    R0,AREALN           POINT TO WORK AREA      *DHA 04/90*
         ST    R0,CIRWA            ADDRESS OF WORK AREA    *DHA 04/90*
         LA    R0,SAVE             POINT TO CIR SAVE AREA  *DHA 04/90*
         ST    R0,CIRSAVE          ADDRESS OF SAVE AREA    *DHA 04/90*
         XC    CIRPSWD,CIRPSWD     CLEAR PASSWORD ADDRESS  *DHA 04/90*
         LA    R0,DATASIZE         GET LENGTH OF WORK AREA *DHA 04/90*
         STH   R0,AREALN           LENGTH OF WORK AREA     *DHA 04/90*
         CALLTSSR EP=IKJEHCIR,     CALL CATALOG INFO RTN.  *DHA 04/90* *
               MF=(E,CIRPARM)                              *DHA 04/90*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/90*
         B     FOUND               AND LEAVE               *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
         EJECT
         EJECT
*----------------------------------------------------------------------
*        ACUID    - ACF2 UID String
*----------------------------------------------------------------------
         AIF   (NOT &ACF2).ACF2B                           *DHA 04/92*
UID      SHOW  BLOCK=ACUID,FIELD=ACUID                     *DHA 04/90*
         ACFINCVT R6,NONE=INVALID                          *LBD 02/84*
         USING ACCVT,R6            ADDRESSABILITY          *LBD 02/84*
         ACFGUCB R7,NONE=INVALID,INLINE=YES                *DHA 04/92*
         USING ACUCB,R7                                     *LBD 06/89*
         L     R1,ACUUIDP          R1 -> ACUID              *LBD 06/89*
         DROP  R6,R7                                        *LBD 06/89*
         B     FOUND                                       *DHA 04/90*
.ACF2B   ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        CVT      - Communications Vector Table Prefix
*----------------------------------------------------------------------
CPUMODEL SHOW  BLOCK=MODEL,FIELD=MODEL,WORK=YES            *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *DHA 04/92*
         LA    R0,CVTMAP-CVTFIX    GET LENGTH OF CVT PREFIX*DHA 04/92*
         SR    R1,R0               POINT TO CVTFIX         *DHA 04/92*
         USING CVTFIX,R1
         MVC   MODEL,=X'21202020'  GET EDIT PATTERN        *DHA 04/92*
         ED    MODEL,CVTMDL        EDIT MODEL              *DHA 04/92*
         DROP  R1
         LA    R1,MODEL            R1 -> MODEL             *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        CVT      - Communications Vector Table Prefix
*----------------------------------------------------------------------
BCPFMID  SHOW  BLOCK=CVTFIX,FIELD=CVTPRODI                 *DHA 04/92*
BCPLEVEL SHOW  BLOCK=CVTFIX,FIELD=CVTPRODN                 *LBD 04/91*
OSREL    SHOW  BLOCK=CVTFIX,FIELD=CVTRELNO                 *LBD 03/84*
SYSNAME  SHOW  BLOCK=CVTFIX,FIELD=CVTSNAME                 *LBD 03/84*
VERID    SHOW  BLOCK=CVTFIX,FIELD=CVTVERID                 *LBD 03/84*
         L     R1,CVTPTR           POINT TO CVT            *LBD 03/84*
         LA    R0,CVTMAP-CVTFIX    GET LENGTH OF CVT PREFIX*DHA 04/92*
         SR    R1,R0               R1 -> CVTFIX            *DHA 04/92*
         B     FOUND                                       *DHA 04/90*
         EJECT
*----------------------------------------------------------------------
*        CVT      - Communications Vector Table
*----------------------------------------------------------------------
CPU      SHOW  BLOCK=CVTMAP,FIELD=CVTSNAME                 *DHA 04/90*
         L     R1,CVTPTR           R1 -> CVTMAP             *LBD 06/89*
         B     FOUND                                       *DHA 04/90*
         EJECT
*----------------------------------------------------------------------
*        CVT      - Communications Vector Table
*----------------------------------------------------------------------
OPSYS    SHOW  BLOCK=MVSOSLEV,FIELD=MVS                    *DHA 04/90*
OS       SHOW  BLOCK=MVSOSLEV,FIELD=OSLEVEL                *DHA 04/90*
OSLEV    SHOW  BLOCK=MVSOSLEV,FIELD=OSLEVEL                *DHA 04/90*
         L     R3,CVTPTR                                   *DHA 04/90*
         USING CVTMAP,R3                                   *DHA 04/90*
         LA    R1,MVS370           R1 -> MVSOSLEV (370)    *DHA 04/90*
         AIF   (&SYSSPLV EQ 1).OS                          *DHA 04/92*
         TM    CVTDCB,CVTMVSE     XA IF ON                  *LBD 05/89*
         BZ    FOUND                                       *DHA 04/90*
         LA    R1,MVSXA            R1 -> MVSOSLEV (XA)     *DHA 04/90*
         AIF   (&SYSSPLV EQ 2).OS                          *DHA 04/92*
         TM    CVTOSLV0,CVTXAX    TEST FOR ESA              *LBD 05/89*
         BZ    FOUND                                        *DHA 04/92*
         LA    R1,MVSESA           R1 -> MVSOSLEV (ESA)    *DHA 04/90*
.OS      ANOP                                              *DHA 04/92*
         B     FOUND                                       *DHA 04/90*
         DROP  R3                                          *DHA 04/90*
MVSOSLEV DSECT                                             *DHA 04/92*
MVS      DS    0CL7                MVS/XXX                 *DHA 04/92*
         DS    CL4                 MVS/                    *DHA 04/92*
OSLEVEL  DS    CL3                 XXX (370, XA, ESA)      *DHA 04/92*
SHOW     CSECT                                             *DHA 04/92*
MVSESA   DC    CL7'MVS/ESA'                                 *LBD 05/89*
MVSXA    DC    CL7'MVS/XA'                                  *LBD 05/89*
MVS370   DC    CL7'MVS/370'                                 *LBD 05/89*
         EJECT
*----------------------------------------------------------------------
*        CVT      - Communications Vector Table
*----------------------------------------------------------------------
REALSTOR SHOW  BLOCK=WORKAREA,FIELD=REALSIZE,WORK=YES      *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R1                                   *DHA 04/92*
         L     R2,CVTRLSTG         REAL STORAGE AT IPL (K) *DHA 04/92*
         SRL   R2,10               CONVERT KILO TO MEG     *DHA 04/92*
         CVD   R2,REALDEC          CONVERT TO DECIMAL      *DHA 04/92*
         MVC   REALSIZE,REALPTRN   COPY EDIT PATTERN       *DHA 04/92*
         ED    REALEDIT,REALDEC    EDIT VALUE              *DHA 04/92*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
REALPTRN DC    C' ',13X'20',X'2120',C'M'                   *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEESMCA  - SMF Control Table
*----------------------------------------------------------------------
SMFID    SHOW  BLOCK=SMCABASE,FIELD=SMCASID                *LBD 06/89*
SYSID    SHOW  BLOCK=SMCABASE,FIELD=SMCASID                *LBD 03/84*
         L     R1,CVTPTR           POINT TO CVT            *LBD 03/84*
         USING CVTMAP,R1                                   *LBD 03/84*
         L     R1,CVTSMCA          R1 -> SMCABASE          *LBD 03/84*
         DROP  R1                                          *DHA 04/92*
         USING SMCABASE,R1                                 *DHA 04/92*
         CLC   SMCASMCA,=C'SMCA'   VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         DROP  R1                                          *LBD 03/84*
         B     FOUND                                       *DHA 04/90*
         EJECT
*----------------------------------------------------------------------
*        IEESMCA  - SMF Control Table - IPL Date
*----------------------------------------------------------------------
IPLDATE  SHOW  BLOCK=WORKAREA,FIELD=XDATE,WORK=YES         *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R1                                   *DHA 04/92*
         L     R1,CVTSMCA          R1 -> SMCABASE          *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING SMCABASE,R1                                 *DHA 04/92*
         CLC   SMCASMCA,=C'SMCA'   VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         L     R1,SMCAIDTE         R1 = IPL DATE           *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         MVC   T001BL,T001BLC          INITIALIZE TABLE    *DHA 04/92*
         ZAP   MX001M,=P'01'           INITIALIZE MONTH
         ZAP   T001BL+2(2),=P'28'      INITIALIZE FEB
         ST    R1,R1001ST              STORE JULIAN DATE
         XC    W001X,W001X             ZERO WORK AREA
         MVC   W001X+3(1),R1001ST+1    MOVE YEAR INTO WORK
         L     R1,W001X                LOAD YEAR INTO REGISTER
         SLL   R1,4                    SHIFT LEFT 1/2 BYTE
         ST    R1,W001X                STORE YEAR INTO WORK AGAIN
         OI    W001X+3,X'0F'           CONVERT YEAR INTO PACKED FORMAT
         ZAP   YR001X,W001X            SAVE YEAR
         DP    W001X,=P'04'            DIVIDE YEAR BY 4
         CP    W001X+2(2),=P'00'       IF REMAINDER THEN LEAP YEAR
         BNE   XNOLEAPX                NOT A LEAP YEAR
         ZAP   T001BL+2(2),=P'29'      SET MONTH 2 EQUAL TO 29 DAYS
XNOLEAPX LA    1,T001BL                SET POINTER TO MONTH TABLE
         L     2,=F'02'                SET COUNTER
         LA    3,MX001M-1              SET POINTER TO END OF TABLE
XCPR01   CP    R1001ST+2(2),0(2,1)     COMPARE DAYS TO MONTH DABLE
         BNH   XDATEX                  LESS THAN TABLE
         AP    MX001M,=P'01'           INCRIMENT MONTH COUNTER
         SP    R1001ST+2(2),0(2,1)     SUB TABLE DAY FORM JULIAN DAY
         BXLE  1,2,XCPR01              CONTINUE SCAN
XDATEX   ZAP   DYXX,R1001ST+2(2)       SET DAY
         UNPK  W001X,DYXX              UNPACK DAY
         OI    W001X+3,X'F0'           RESET SIGN
         MVC   XD1(2),W001X+2          MOVE DAY TO O/P AREA
         UNPK  W001X,MX001M            UNPACK MONTH
         OI    W001X+3,X'F0'           RESET SIGN
         MVC   XM1(2),W001X+2          MOVE MONTH TO O/P AREA
         UNPK  W001X,YR001X            UNPACK YEAR
         OI    W001X+3,X'F0'           RESER SIGN
         MVC   XY1(2),W001X+2          MOVE YEAR TO O/P AREA
         MVI   XMD1,C'/'                                   *DHA 04/92*
         MVI   XDY1,C'/'                                   *DHA 04/92*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
T001BLC  DS    0XL24                   MONTH TABLE
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         EJECT
*----------------------------------------------------------------------
*        IEESMCA  - SMF Control Table - IPL Time
*----------------------------------------------------------------------
IPLTIME  SHOW  BLOCK=WORKAREA,FIELD=@TIME,WORK=YES         *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R1                                   *DHA 04/92*
         L     R1,CVTSMCA          R1 -> SMCABASE          *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING SMCABASE,R1                                 *DHA 04/92*
         CLC   SMCASMCA,=C'SMCA'   VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         L     R7,SMCAITME         R1 = IPL TIME           *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         MVI   @HRS+4,C'.'
         MVI   @MIN+2,C'.'
         SR    R6,R6                    CLEAR R6
         D     R6,=F'100'               DIVIDE OUT 100TH SEC
         SR    R6,R6
         D     R6,=F'60'                DIVIDE OUT SECONDS
         LR    R5,R6                    STORE SEC IN R5
         SR    R6,R6
         D     R6,=F'60'                DIVIDE OUT MINUTES
         CVD   R7,@DWORD                CVD
         MVC   @TWORK,@TPAT               &
         ED    @TWORK,@DWORD+5            EDIT
         MVC   @HRS,@TWORK+2              @HRS
         CVD   R6,@DWORD                CVD
         MVC   @TWORK,@TPAT               &
         ED    @TWORK,@DWORD+5            EDIT
         MVC   @MIN,@TWORK+4             @MIN
         CVD   R5,@DWORD                CVD
         MVC   @TWORK,@TPAT               &
         ED    @TWORK,@DWORD+5            EDIT
         MVC   @SEC,@TWORK+4              SEC
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
@TPAT    DC    X'402020212020'
         EJECT
*----------------------------------------------------------------------
*        IEFAJCTB - Job Control Table (Priority)
*----------------------------------------------------------------------
PRTY     SHOW  BLOCK=WORKAREA,FIELD=PRTYVAL,WORK=YES       *DHA 04/90*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,PSATOLD          R1 -> CURRENT TASK CONTROL BLOCK
         USING TCB,R1
         L     R1,TCBJSCB          R1 -> JOB STEP CONTROL BLOCK
         DROP  R1                                          *DHA 04/92*
         USING IEZJSCB,R1
         L     R1,JSCBJCT          R1 -> JOB CONTROL TABLE
         DROP  R1                                          *DHA 04/92*
         USING INJMJCT-16,R1
         CLI   JCTIDENT,JCTID      VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         IC    R2,JCTJPRTY         LOAD JOB PRIORITY       *DHA 04/90*
         DROP  R1                                          *DHA 04/92*
         SRDL  R2,4                EXTRACT PRIORITY        *DHA 04/90*
         SRL   R3,32-4             POSITION FOR DIVISION   *DHA 04/90*
         CVD   R3,PRTYDWK          CONVERT TO DECIMAL      *DHA 04/90*
         MVC   PRTYEDIT,=X'40202120'  EDIT PATTERN         *DHA 04/90*
         ED    PRTYEDIT,PRTYDWK+6  CONVERT TO EBCDIC       *DHA 04/90*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/90*
         B     FOUND                                       *DHA 04/90*
         DROP  R13                                         *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFAJCTB - Job Control Table (SWA)
*----------------------------------------------------------------------
SWA      SHOW  BLOCK=SWASTAT,FIELD=SWASTAT                 *DHA 04/92*
         L     R4,PSATOLD         -> TCB                   *DHA 04/92*
         USING TCB,R4                                      *DHA 04/92*
         L     R4,TCBJSCB           -> JSCB                *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
         USING IEZJSCB,R4                                  *DHA 04/92*
         L     R4,JSCBJCT               -> JCT             *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
         USING INJMJCT-16,R4                               *DHA 04/92*
         CLI   JCTIDENT,JCTID      VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         LA    R1,SWAABOVE         R1 -> SWASTAT (ABOVE)   *DHA 04/92*
         TM    JCTSTAT2,JCTSWAUP   IS SWA ABOVE THE LINE?  *DHA 04/92*
         BO    FOUND               YES, BRANCH             *DHA 04/92*
         LA    R1,SWABELOW         R1 -> SWASTAT (BELOW)   *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
SWASTAT  DS    0CL5                SWA STATUS              *DHA 04/92*
SWAABOVE DC    CL5'ABOVE'                                  *DHA 04/92*
SWABELOW DC    CL5'BELOW'                                  *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFJESCT - JES Communication Table Extension
*----------------------------------------------------------------------
         AIF   (&SYSSPLV LT 3).SMS                         *DHA 04/92*
SMS      SHOW  BLOCK=SMSSTAT,FIELD=SMSSTAT                 *DHA 04/92*
         L     R2,CVTPTR           POINT TO CVT            *LBD 01/91*
         USING CVTMAP,R2                                   *LBD 01/91*
         L     R2,CVTJESCT         POINT TO JES CONTROL TBL*LBD 01/91*
         DROP  R2                                          *DHA 04/92*
         USING JESCT,R2                                    *LBD 01/91*
         L     R2,JESCTEXT         POINT JESCT EXTENSION   *LBD 01/91*
         DROP  R2                                          *DHA 04/92*
         USING JESPEXT,R2                                  *LBD 01/91*
         CLC   JESSID,=C'JESPEXT'  CHECK VALIDITY          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         LA    R1,SMSON            R1 -> SMSSTAT (ON)      *DHA 04/92*
         CLC   JESSMSIB,=F'0'     SMS ENABLED ?            *LBD 01/91*
         BNE   FOUND               YES, BRANCH             *DHA 04/92*
         LA    R1,SMSOFF           R1 -> SMSSTAT (OFF)     *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R2                                          *LBD 01/91*
SMSSTAT  DS    0CL3                SMS STATUS              *DHA 04/92*
SMSON    DC    CL3'ON'                                     *DHA 04/92*
SMSOFF   DC    CL3'OFF'                                    *DHA 04/92*
.SMS     ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFJSCVT - BDT Subsystem Communications Vector Table
*----------------------------------------------------------------------
         AIF   (NOT &JES3).JES3B                           *DHA 04/92*
BDTNODE  SHOW  BLOCK=USVTBGN,FIELD=USVTNDID               *LBD 06/84*
         L     R1,CVTPTR                                  *LBD 06/84*
         USING CVTMAP,R1                                  *LBD 07/84*
         L     R1,CVTJESCT             -> JESCT           *LBD 06/84*
         DROP  R1                                          *DHA 04/92*
         USING JESCT,R1                                   *LBD 07/84*
         L     R7,JESSSCT              -> SSCVT           *LBD 06/84*
         USING SSCTBEGN,R7                                *LBD 06/84*
DONODE   DS    0H                                         *LBD 06/84*
         CLC   SSCTSNAM,=C'JES3'  JES3 ?                    *LBD 02/88*
         BE    NSSCT              YES - SKIP                *LBD 02/88*
         CLC   SSCTSUSE,=F'0'     NULL SSCTSUSE ?           *LBD 02/88*
         BE    NSSCT              YES                       *LBD 02/88*
         L     R1,SSCTSUSE                                *LBD 06/84*
         CLC   =C'USVT',0(R1)          BDT USVT ?         *LBD 07/84*
         BNE   NSSCT                   NO - KEEP LOOKING  *LBD 07/84*
         USING USVTBGN,R1                                 *LBD 06/84*
USVTDEF  EQU   BIT2               * SYMBOL REMOVED OZ80124 *LBD 12/85*
         TM    USVTFLGS,USVTDEF        DEFAULT BDT        *LBD 06/84*
         BZ    NSSCT                   NO - KEEP GOING    *LBD 06/84*
         B     FOUND                                      *LBD 06/84*
NSSCT    DS    0H                                         *LBD 06/84*
         ICM   R7,15,SSCTSCTA      POINT TO NEXT SSCVT    *DHA 04/92*
         BNZ   DONODE              BRANCH IF PRESENT      *LBD 06/84*
         B     INVALID             BRANCH IF END OF CHAIN   *DHA 04/92*
         DROP  R1,R7                                      *LBD 06/84*
.JES3B   ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFJSSIB - Subsystem Identification Block
*----------------------------------------------------------------------
JOBID    SHOW  BLOCK=SSIBEGIN,FIELD=SSIBJBID               *LBD 12/89*
         L     R1,PSATOLD         -> CURRENT TCB WORD LIST *LBD 12/89*
         USING TCB,R1                                      *LBD 12/89*
         L     R1,TCBJSCB         -> JSCB                  *LBD 12/89*
         DROP  R1                                          *DHA 04/92*
         USING IEZJSCB,R1                                  *LBD 12/89*
         L     R1,JSCBSSIB        -> SSIBEGIN              *LBD 12/89*
         DROP  R1                                          *DHA 04/92*
         USING SSIBEGIN,R1                                 *LBD 12/89*
         CLC   SSIBID,=C'SSIB'    VALIDITY CHECK           *LBD 12/89*
         BNE   INVALID                                     *LBD 12/89*
         DROP  R1                                          *LBD 12/89*
         B     FOUND                                       *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFTIOT1 - Task Input/Output Table                *DHA 04/92*
*----------------------------------------------------------------------
JOBNAME  SHOW  BLOCK=TIOT1,FIELD=TIOCNJOB                  *DHA 04/92*
         L     R1,PSATOLD          POINT TO CURRENT TCB    *DHA 04/92*
         USING TCB,R1                                      *DHA 04/92*
         L     R1,TCBTIO           R1 -> TIOT1             *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IEFUCBOB - System Residence UCB                   *DHA 04/92*
*----------------------------------------------------------------------
SYSRES   SHOW  BLOCK=UCBOB,FIELD=UCBVOLI                   *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R1                                   *DHA 04/92*
         L     R1,CVTSYSAD         R1 -> UCBOB             *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IHAACEE  - Accessor Environment Element           *DHA 04/92*
*----------------------------------------------------------------------
TERMINAL SHOW  BLOCK=ACEE,FIELD=ACEETRID                   *DHA 04/92*
USERID   SHOW  BLOCK=ACEE,FIELD=ACEEUSRI                   *DHA 04/92*
         L     R1,PSAAOLD          POINT TO ASCB           *DHA 04/92*
         USING ASCB,R1                                     *DHA 04/92*
         L     R1,ASCBASXB         POINT TO ASXB           *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING ASXB,R1                                     *DHA 04/92*
         L     R1,ASXBSENV         R1 -> ACEE              *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING ACEE,R1                                     *DHA 04/92*
         CLC   ACEEACEE,=C'ACEE'   VALIDITY CHECK          *DHA 04/92*
         BE    FOUND                                       *DHA 04/92*
         BNE   INVALID                                     *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IHADFA   - Data Facilities Area
*----------------------------------------------------------------------
DFPLEVEL SHOW  BLOCK=WORKAREA,FIELD=DFPLEV,WORK=YES        *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R1,CVTPTR           POINT TO CVT            *LBD 04/91*
         USING CVTMAP,R1
         L     R1,CVTDFA             POINT TO DFA          *LBD 04/91*
         DROP  R1                                          *DHA 04/92*
         USING DFA,R1
         CLC   DFAACRON,=C'DFA '   CHECK VALIDITY          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         MVC   DFPLEV,DFPLEVPT     MOVE EDIT PATTERN       *DHA 04/92*
         ED    DFPLEV,DFAREL          EDIT DFP LEVEL       *DHA 04/92*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R1
         DROP  R13                                         *DHA 04/92*
DFPLEVPT DC    X'214B204B20'       EDIT PATTERN            *LBD 04/91*
         EJECT
*----------------------------------------------------------------------
*        IHALDA   - Local Data Area
*----------------------------------------------------------------------
REGION   SHOW  BLOCK=WORKAREA,FIELD=REGSIZE,WORK=YES       *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R2,PSAAOLD          -> TO MY ASCB           *DHA 04/92*
         USING ASCB,R2                                     *LBD 08/85*
         L     R2,ASCBLDA       -> LDA                     *LBD 08/85*
         DROP  R2                                          *LBD 08/85*
         USING LDA,R2                                      *LBD 08/85*
         CLC   LDAID,=C'LDA '      VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF INVALID       *DHA 04/92*
         L     R2,LDAREGRQ        REGION REQUESTED -XA-    *LBD 12/85*
         DROP  R2                                          *LBD 08/85*
         SRL   R2,10               REGION/1K               *LBD 08/85*
         CVD   R2,REGDWK                                   *LBD 08/85*
         MVC   REGSIZE,REGPTRN     COPY EDIT PATTERN + K   *DHA 04/92*
         ED    REGEDIT,REGDWK      CONVERT TO EBCDIC       *DHA 04/92*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/90*
REGPTRN  DC    C' ',13X'20',X'2120',C'K'                   *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IHAPCCA  - Physical Configuration Communication Area
*----------------------------------------------------------------------
CPUVER   SHOW  BLOCK=PCCA,FIELD=VERSION                    *DHA 04/92*
         L     R3,CVTPTR                                   *DHA 04/92*
         USING CVTMAP,R3                                   *DHA 04/92*
         L     R3,CVTPCCAT         POINT TO PCCAVT
         DROP  R3                                          *DHA 04/92*
         USING PCCAVT,R3
         LA    R6,L'PCCAT00P       LENGTH OF ENTRY         *DHA 04/92*
         LA    R7,PCCAT15P         POINT TO LAST ENTRY     *DHA 04/92*
CPUMOD1  EQU   *                                           *DHA 04/92*
         ICM   R1,15,PCCAVT        R1 -> PCCA              *DHA 04/92*
         BNZ   CPUMOD2             BRANCH IF IT EXISTS     *DHA 04/92*
         BXLE  R3,R6,CPUMOD1       LOOP TO NEXT ENTRY      *DHA 04/92*
         B     INVALID             BRANCH IN NO ENTRY      *DHA 04/92*
CPUMOD2  EQU   *                                           *DHA 04/92*
         USING PCCA,R1                                     *DHA 04/92*
         CLC   PCCAPCCA,=C'PCCA'   VALID PCCA?             *DHA 04/92*
         BE    FOUND               YES, FOUND              *DHA 04/92*
         BNE   INVALID             NO, INVALID             *DHA 04/92*
         DROP  R3                                          *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IHAPCCA  - Physical Configuration Communication Area
*----------------------------------------------------------------------
CPUSER   SHOW  BLOCK=WORKAREA,FIELD=SERIALS,WORK=YES       *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         L     R3,CVTPTR                                   *DHA 04/92*
         USING CVTMAP,R3                                   *DHA 04/92*
         L     R3,CVTPCCAT         POINT TO PCCAVT
         DROP  R3                                          *DHA 04/92*
         USING PCCAVT,R3
         LA    R6,L'PCCAT00P       LENGTH OF ENTRY         *DHA 04/92*
         LA    R7,PCCAT15P         POINT TO LAST ENTRY     *DHA 04/92*
         LA    R1,SERIALS          POINT TO CPU SERIALS    *DHA 04/92*
CPUSER1  EQU   *                                           *DHA 04/92*
         ICM   R2,15,PCCAVT        R2 -> PCCA              *DHA 04/92*
         BZ    CPUSER2             BRANCH IF NO ENTRY      *DHA 04/92*
         USING PCCA,R2                                     *DHA 04/92*
         CLC   PCCAPCCA,=C'PCCA'   VALID PCCA?             *DHA 04/92*
         BNE   INVALID             NO, BRANCH              *DHA 04/92*
         MVC   0(L'SERIAL,R1),PCCACPID+2  MOVE CPU SERIAL  *DHA 04/92*
         LA    R1,L'SERIAL+1(,R1)  POINT TO NEXT SLOT      *DHA 04/92*
CPUSER2  EQU   *                                           *DHA 04/92*
         BXLE  R3,R6,CPUSER1       LOOP TO NEXT ENTRY      *DHA 04/92*
         LA    R1,WORKAREA         R1 -> WORKAREA          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
         DROP  R3                                          *DHA 04/92*
         DROP  R13                                         *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IHASCCB  - Service Call Control Block             *DHA 04/92*
*----------------------------------------------------------------------
LPAR     SHOW  BLOCK=LPARSTAT,FIELD=LPARSTAT               *DHA 04/92*
         L     R4,CVTPTR          -> CVT                    *LBD 11/88*
         USING CVTMAP,R4                                   *DHA 04/92*
         ICM   R4,15,CVTSCPIN        -> SCCP                *LBD 11/88*
         BZ    INVALID            NO SCCP                   *LBD 11/88*
         DROP  R4                                          *DHA 04/92*
         USING SCCB,R4                                      *LBD 11/88*
         LA    R1,LPARYES          R1 -> LPARSTAT (YES)    *DHA 04/92*
         TM    SCCBCON1,SCCBBFY   LPAR MODE ?               *LBD 11/88*
         BO    FOUND                                       *DHA 04/92*
         LA    R1,LPARNO           R1 -> LPARSTAT (NO)     *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
LPARSTAT DS    0CL3                                        *DHA 04/92*
LPARYES  DC    CL3'YES'                                    *DHA 04/92*
LPARNO   DC    CL3'NO'                                     *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IKJPSCB  - TSO Protected Step Control Block
*----------------------------------------------------------------------
DEST     SHOW  BLOCK=PSCB,FIELD=PSCBDEST                   *DHA 04/92*
TSOUNIT  SHOW  BLOCK=PSCB,FIELD=PSCBGPNM                   *DHA 04/92*
         L     R1,PSATOLD          -> TCB                  *DHA 04/92*
         USING TCB,R1                                      *DHA 04/92*
         L     R1,TCBJSCB          -> JSCB                 *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         USING IEZJSCB,R1                                  *DHA 04/92*
         L     R1,JSCBPSCB         R1 -> PSCB              *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        IKJPSCB  - TSO Protected Step Control Block (OPER)
*----------------------------------------------------------------------
TSOOPER  SHOW  BLOCK=OPERAUTH,FIELD=OPERAUTH               *DHA 04/92*
         L     R4,PSATOLD          -> TCB                  *DHA 04/92*
         USING TCB,R4                                      *DHA 04/92*
         L     R4,TCBJSCB          -> JSCB                 *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
         USING IEZJSCB,R4                                  *DHA 04/92*
         L     R4,JSCBPSCB         -> PSCB                 *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
         USING PSCB,R4                                     *DHA 04/92*
         LA    R1,NOOPER           R1 -> OPERAUTH (NOOPER) *DHA 04/90*
         TM    PSCBATR1,PSCBCTRL   OPERATOR AUTHORITY?     *DHA 04/92*
         BZ    FOUND               NO                      *DHA 04/92*
         LA    R1,OPER             R1 -> OPERAUTH (OPER)   *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R4                                          *DHA 04/92*
OPERAUTH DS    0CL6                TSO OPERATOR AUTHORITY  *DHA 04/92*
OPER     DC    CL6'OPER'                                   *DHA 04/92*
NOOPER   DC    CL6'NOOPER'                                 *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        ILRASMVT - CLPA (Not Quick Start)                 *DHA 04/92*
*----------------------------------------------------------------------
CLPA     SHOW  BLOCK=CLPASTAT,FIELD=CLPASTAT               *DHA 04/92*
         L     R2,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R2                                   *DHA 04/92*
         L     R2,CVTASMVT         POINT TO ASMVT          *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
         USING ASMVT,R2                                    *DHA 04/92*
         LA    R1,CLPAYES          R1 -> CLPASTAT (YES)    *DHA 04/92*
         TM    ASMFLAG2,ASMQUICK   QUICK START?            *DHA 04/92*
         BZ    FOUND               NO, CLPA                *DHA 04/92*
         LA    R1,CLPANO           R1 -> CLPASTAT (NO)     *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
CLPASTAT DS    0CL3                CLPA STATUS             *DHA 04/92*
CLPAYES  DC    CL3'YES'                                    *DHA 04/92*
CLPANO   DC    CL3'NO'                                     *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        ILRASMVT - CVIO (Not Warm Start)                  *DHA 04/92*
*----------------------------------------------------------------------
CVIO     SHOW  BLOCK=CVIOSTAT,FIELD=CVIOSTAT               *DHA 04/92*
         L     R2,CVTPTR           POINT TO CVT            *DHA 04/92*
         USING CVTMAP,R2                                   *DHA 04/92*
         L     R2,CVTASMVT         POINT TO ASMVT          *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
         USING ASMVT,R2                                    *DHA 04/92*
         LA    R1,CVIOYES          R1 -> CVIOSTAT (YES)    *DHA 04/92*
         TM    ASMFLAG2,ASMWARM    WARM START?             *DHA 04/92*
         BZ    FOUND               NO, CVIO                *DHA 04/92*
         LA    R1,CVIONO           R1 -> CVIOSTAT (NO)     *DHA 04/92*
         B     FOUND                                       *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
CVIOSTAT DS    0CL3                CVIO STATUS             *DHA 04/92*
CVIOYES  DC    CL3'YES'                                    *DHA 04/92*
CVIONO   DC    CL3'NO'                                     *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        LIDREC   - ACF2 Logon ID Record
*----------------------------------------------------------------------
         AIF   (NOT &ACF2).ACF2C                           *DHA 04/92*
ACCTNO#  SHOW  BLOCK=LIDREC,FIELD=LIDACCT#                 *LBD 12/86*
ACF2ACCT SHOW  BLOCK=LIDREC,FIELD=LIDACCT                  *LBD 02/84*
COMPMAIL SHOW  BLOCK=LIDREC,FIELD=LIDUMAIL                 *LBD 06/86*
DEPT     SHOW  BLOCK=LIDREC,FIELD=LIDDEPT                  *LBD 02/84*
FDS#     SHOW  BLOCK=LIDREC,FIELD=LIDFDS#                  *LBD 12/86*
GROUP    SHOW  BLOCK=LIDREC,FIELD=LIDGRP                   *DHA 04/92*
GRP      SHOW  BLOCK=LIDREC,FIELD=LIDGRP                   *DHA 04/92*
HOMEDIV  SHOW  BLOCK=LIDREC,FIELD=LIDHMED                  *LBD 10/90*
JOBFUNC  SHOW  BLOCK=LIDREC,FIELD=LIDJOBF                 *LBD 10/84*
LOC      SHOW  BLOCK=LIDREC,FIELD=LIDLOCAT                 *LBD 02/84*
MAIL     SHOW  BLOCK=LIDREC,FIELD=LIDMAILC                 *LBD 02/84*
NAME     SHOW  BLOCK=LIDREC,FIELD=LIDNAME                  *LBD 02/84*
PHONE    SHOW  BLOCK=LIDREC,FIELD=LIDPHONE                 *LBD 02/84*
         ACFINCVT R6,NONE=INVALID                          *LBD 02/84*
         USING ACCVT,R6           ADDRESSABILITY           *LBD 02/84*
         ACFGUCB R7,NONE=INVALID,INLINE=YES                *DHA 04/92*
         DROP  R6                                          *LBD 02/84*
         USING ACUCB,R7                                    *LBD 02/84*
         L     R1,ACULRECP         R1 -> LIDREC            *LBD 02/84*
         DROP  R7                                          *LBD 02/84*
         B     FOUND                                       *DHA 04/92*
.ACF2C   ANOP                                              *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        RATYJCD  - Rockwell ACT Accounting Field
*----------------------------------------------------------------------
         AIF   (NOT &ROCKWELL).ROCKWELL2                   *DHA 04/92*
ACCT     SHOW  BLOCK=ABSTART,FIELD=FDSACCT                 *LBD 06/89*
ACCTNO   SHOW  BLOCK=ABSTART,FIELD=ABACCTNO                *LBD 02/84*
CRTCD    SHOW  BLOCK=ABSTART,FIELD=ABCRTCD
FDS      SHOW  BLOCK=ABSTART,FIELD=ABFDS                   *LBD 02/84*
FRAMES   SHOW  BLOCK=ABSTART,FIELD=ABFRAMES
LINES    SHOW  BLOCK=ABSTART,FIELD=ABLINES
         L     R1,PSATOLD          R1 -> CURRENT TASK CONTROL BLOCK
         USING TCB,R1
         L     R1,TCBJSCB          R1 -> JOB STEP CONTROL BLOCK
         DROP  R1                                          *DHA 04/92*
         USING IEZJSCB,R1
         L     R1,JSCBJCT          R1 -> JOB CONTROL TABLE
         DROP  R1                                          *DHA 04/92*
         USING INJMJCT-16,R1
         ICM   R1,B'0111',JCTACTAD R1 -> ACCOUNT CONTROL TABLE
         DROP  R1                                          *DHA 04/92*
         USING IEFAACTB-16,R1
         CLI   ACTIDENT,ACTID      VALIDITY CHECK          *DHA 04/92*
         BNE   INVALID             BRANCH IF FAILURE
         CLI   ACTACCNT,ABSIZE     VALIDITY CHECK
         BNE   INVALID             BRANCH IF FAILURE
         LA    R1,ACTACCNT+1       R1 -> ABSTART
         DROP  R1
         B     FOUND                                       *LBD 02/84*
.ROCKWELL2 ANOP                                            *DHA 04/92*
         EJECT
*----------------------------------------------------------------------
*        RCVT     - Rockwell Communications Vector Table
*----------------------------------------------------------------------
         AIF   (NOT &ROCKWELL).ROCKWELL3                   *DHA 04/92*
CENTER   SHOW  BLOCK=RCVT,FIELD=RCVTHOME                   *DHA 04/90*
         L     R1,CVTPTR           POINT TO CVT            *LBD 04/84*
         USING CVTMAP,R1                                   *LBD 04/84*
         ICM   R1,15,CVTUSER      R1 -> RCVT               *DHA 04/92*
         DROP  R1                                          *LBD 04/84*
         BZ    INVALID             BRANCH IF ABSENT        *DHA 04/92*
         USING RCVT,R1                                     *DHA 04/92*
         CLC   RCVTID,=C'RCVT'     VALID?                  *DHA 04/92*
         BE    FOUND               YES, BRANCH             *DHA 04/92*
         BNE   INVALID             NO, BRANCH              *DHA 04/92*
         DROP  R1                                          *DHA 04/92*
.ROCKWELL3 ANOP                                            *DHA 04/92*
         EJECT                                            *
***********************************************************************
*
*        Section 6 - Termination
*
*              Strip leading and trailing blanks and nulls from VALUE.
*              Store length in OVALUE.
*              Insure ILENGTH is not exceeded.
*              Move requested information to VALUE.
*
***********************************************************************
FOUND    DS    0H                                          *LBD 02/84*
         LH    R2,SHOWFLDL         R2 = OLENGTH            *DHA 04/92*
FOUNDLEN DS    0H                                          *DHA 04/90*
         LTR   R2,R2               NULL STRING?            *DHA 04/92*
         BZ    STORE               YES, BRANCH             *DHA 04/92*
         AH    R1,SHOWFLD          R1 -> REQUESTED SUBFIELD
LEADING  DS    0H                                          *DHA 04/92*
         TM    0(R1),X'FF'-C' '    LEADING BLANK OR NULL?  *DHA 04/92*
         BNZ   TRAILING            NO, BRANCH              *DHA 04/92*
         LA    R1,1(,R1)           YES, INCREMENT POINTER  *DHA 04/92*
         BCT   R2,LEADING          DECREMENT LENGTH        *DHA 04/92*
         B     NORMAL              BRANCH, NULL STRING     *DHA 04/92*
TRAILING DS    0H                                          *DHA 04/92*
         LR    R3,R2               R3 = LENGTH             *DHA 04/92*
         BCTR  R3,0                R3 = LENGTH-1           *DHA 04/92*
         LA    R4,0(R3,R1)         POINT TO LAST BYTE      *DHA 04/92*
         TM    0(R4),X'FF'-C' '    TRAILING BLANK OR NULL? *DHA 04/92*
         BNZ   STORE               NO, BRANCH              *DHA 04/92*
         BCT   R2,TRAILING         DECREMENT LENGTH        *DHA 04/92*
STORE    DS    0H                                          *DHA 04/92*
         ST    R2,OLENGTH          STORE OLENGTH
         LA    R4,VALUE            POINT TO TARGET         *DHA 04/92*
         LR    R5,R11              GET LENGTH OF TARGET    *DHA 04/92*
         LR    R6,R1               POINT TO SOURCE         *DHA 04/92*
         LR    R7,R2               GET LENGTH OF SOURCE    *DHA 04/92*
         ICM   R7,8,=C' '          PAD = BLANK             *DHA 04/92*
         MVCL  R4,R6               COPY SOURCE TO TARGET   *DHA 04/92*
         BNL   NORMAL              NORMAL RETURN           *DHA 04/92*
         BL    TRUNCATE            BRANCH, TRUNCATION      *DHA 04/92*
         EJECT
***********************************************************************
*        RETURN
***********************************************************************
INVALID  DS    0H                  VALIDITY CHECK FAILED
         LA    R15,16              RETURN CODE = 16
         B     RETURN
ARGERROR DS    0H                  CALLER ARGUMENT LIST ERROR
         LA    R15,12              RETURN CODE = 12
         B     RETURN
NOID     DS    0H                  ID NOT FOUND
         LA    R15,8               RETURN CODE = 8
         B     RETURN
TRUNCATE DS    0H                  VALUE TRUNCATED
         LA    R15,4               RETURN CODE = 4
         B     RETURN
NORMAL   DS    0H                  NORMAL COMPLETION
         SR    R15,R15             RETURN CODE = 0
RETURN   DS    0H
         LTR   R8,R8               DOES TABLE ENTRY EXIST? *DHA 04/92*
         BZ    RETURN1             NO, BRANCH              *DHA 04/90*
         TM    SHOWFLAG,SHOWWORK   WAS GETMAIN ISSUED?     *DHA 04/90*
         BZ    RETURN1             NO, BRANCH              *DHA 04/90*
         LR    R2,R15              SAVE RETURN CODE        *DHA 04/90*
         LR    R1,R13              UNCHAIN SAVE AREAS      *DHA 04/90*
         L     R13,4(R1)           ...                     *DHA 04/90*
         FREEMAIN RU,LV=4096,A=(1) FREE WORK AREA          *DHA 04/90*
         LR    R15,R2              RESTORE RETURN CODE     *DHA 04/90*
RETURN1  DS    0H                                          *DHA 04/90*
         L     14,12(13,0)        RESTORE REGISTER 14      *LBD 12/89*
         LM    0,12,20(13)        RESTORE THE REGISTERS    *LBD 12/89*
         BSM   0,14               RETURN TO THE CALLER     *LBD 12/89*
*                                                          *DHA 04/92*
         DROP  R9
         DROP  R10
         DROP  R12
         DROP  R0                                          *DHA 04/92*
         EJECT
***********************************************************************
*        ID TABLE
*              ENTRIES ARE FORMED BY SHOW MACRO CALLS IN THE PRECEDING
*              ACTION ROUTINES.  THE TABLE IS USED BY THE BINARY SEARCH
*              CODE IN THE PROLOG TO THE ACTION ROUTINES.
***********************************************************************
SHOWTBL  SHOWTBL SHOWTBLE
         EJECT
         END   SHOW
./ ADD NAME=SHOWREXX 0100-92112-92115-1920-000171-000088-000000-$A1019
SHOWREXX TITLE 'REXX FUNCTION INTERFACE TO SHOW'
***********************************************************************
*        REXX Function Interface to SHOW
*
*        Use: refer to SHOWREXX(id) or CALL SHOWREXX id,
*        where "id" is a constant or variable which has a valid value
*        for SHOW. "id" may be lowercase or uppercase.
*
*
*
*        Douglas H. Adams
*        Mail code 110-SE28
*        Information Systems Center
*        Rockwell International Corporation
*        2201 Seal Beach Boulevard
*        P. O. Box 2515
*        Seal Beach, California 90740-1515
*
*        (310) 797-2618
*
*        April 24, 1992
*
***********************************************************************
         PUNCH ' ALIAS SHOWREXX'
SHOWREXX CSECT
SHOWREXX AMODE 31
SHOWREXX RMODE ANY
         EJECT
         IRXEFPL                   REXX EXTERNAL FUNCTIONS PARM LIST
         EJECT
         IRXARGTB                  REXX ARGUMENT TABLE
         EJECT
         IRXENVB                   REXX ENVIRONMENT BLOCK
         EJECT
         IRXEVALB                  REXX EVALUATION BLOCK
         EJECT
         IRXEXTE                   REXX VECTOR OF EXTERNAL ENTRY POINTS
         EJECT
***********************************************************************
*                                  WORK AREA FOR REENTRANCE
***********************************************************************
WORKAREA DSECT
SAVE     DS    18F
ID       DS    CL8                 FUNCTION ARGUMENT
CALL     CALL  ,(ID,ILENGTH,,),MF=L  SHOW PARAMETERS
WORKSIZE EQU   *-WORKAREA
         EJECT
***********************************************************************
*                                  INITIALIZATION
***********************************************************************
SHOWREXX CSECT
         USING SHOWREXX,15
         SAVE  (14,12)             SAVE REGISTER
         MODID                     IDENTIFICATION
         DROP  15
         LR    12,15               SET BASE REGISTER
         USING SHOWREXX,12
         LR    10,0                SAVE ENVBLOCK POINTER
         USING ENVBLOCK,10
         LR    2,1                 SAVE EFPL POINTER
         USING EFPL,2
         LA    0,WORKSIZE          GET WORKAREA SIZE
         GETMAIN RU,LV=(0)         GET WORKAREA STORAGE
         USING WORKAREA,1
         XC    WORKAREA(WORKSIZE),WORKAREA  CLEAR WORKAREA
         ST    1,8(,13)            CHAIN SAVE AREAS
         ST    13,SAVE+4
         LR    13,1
         DROP  1
         USING WORKAREA,13
***********************************************************************
*                                  GET FUNCTION VALUE
***********************************************************************
         L     3,EFPLARG           POINT TO ARGUMENT TABLE
         USING ARGTABLE_ENTRY,3
         L     4,EFPLEVAL          POINT TO A(EVALUATION BLOCK)
         L     4,0(,4)             POINT TO EVALUATION BLOCK
         USING EVALBLOCK,4
         L     5,ARGTABLE_ARGSTRING_PTR     POINT TO ARG
         ICM   6,15,ARGTABLE_ARGSTRING_LENGTH  GET LENGTH OF ARG
         BZ    ERROR3              NULL, BRANCH
         BM    ERROR2              MISSING, BRANCH
         C     6,=F'8'             IS ARG TOO BIG?
         BH    ERROR3              YES, BRANCH
         ICM   7,15,ARGTABLE_NEXT  IS THERE A SECOND ARG?
         BNM   ERROR1              YES, BRANCH
         MVC   ID,=CL8' '          SET ID TO BLANKS
         BCTR  6,0                 GET MACHINE LENGTH OF ARG
         EX    6,MVC               MOVE ARG TO ID
         LA    7,EVALBLOCK_EVLEN   POINT TO OUTPUT LENGTH
         LA    8,EVALBLOCK_EVDATA  POINT TO OUTPUT RESULT
         ICM   15,15,=V(SHOW)      GET ENTRY POINT OF SHOW
         BZ    LINK                BRANCH IF NOT RESOLVED
         CALL  (15),(ID,ILENGTH,(8),(7)),VL,MF=(E,CALL)
         B     TESTRET             GO TEST RETURN CODE
LINK     EQU   *
         LINK  EP=SHOW,PARAM=(ID,ILENGTH,(8),(7)),VL=1,MF=(E,CALL)
TESTRET  EQU   *
         LTR   15,15               SUCCESSFUL?
         BZ    RETURN              YES, BRANCH
         C     15,=F'8'            ID NOT SUPPORTED?
         BE    ERROR3              YES, BRANCH
         BNE   ERROR4              NO, OTHER ERROR
***********************************************************************
*                                  ERROR MESSAGE PROCESSING
***********************************************************************
ERROR1   EQU   *
         LA    1,ERRMSG1           MULTIPLE ARGUMENTS
         B     ERRORSAY
ERROR2   EQU   *
         LA    1,ERRMSG2           NO ARGUMENT
         B     ERRORSAY
ERROR3   EQU   *
         LA    1,ERRMSG3           UNSUPPORTED ARGUMENT
         B     ERRORSAY
ERROR4   EQU   *
         LA    1,ERRMSG4           INVALID ENVIRONMENT
         B     ERRORSAY
ERRORSAY EQU   *
         LR    0,10                POINT TO ENVIRONMENT BLOCK
         L     15,ENVBLOCK_IRXEXTE POINT TO ENTRY POINT VECTOR
         USING IRXEXTE,15
         CLC   IRXEXTE_ENTRY_COUNT,=A((IRXSAY-IRXEXTE)/4) IRXSAY EXIST?
         BL    RETURN              NO, BRANCH
         L     15,IRXSAY           POINT TO SAY ROUTINE
         DROP  15
         CALL  (15),MF=(E,(1))     CALL IRXSAY
         LA    15,4                UNSUCCESSFUL RETURN CODE
***********************************************************************
*                                  TERMINATION
***********************************************************************
RETURN   EQU   *
         LR    1,13                POINT TO WORKAREA
         L     13,SAVE+4           UNCHAIN SAVE AREAS
         LR    2,15                SAVE RETURN CODE
         LA    0,WORKSIZE          GET WORKAREA SIZE
         FREEMAIN RU,LV=(0),A=(1)  FREE WORKAREA
         LR    15,2                RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN
         EJECT
***********************************************************************
*                                  ERROR MESSAGES
***********************************************************************
ERRMSG1  CALL  ,(WRITEERR,ERRPTR1,ERRLEN1),VL,MF=L
ERRPTR1  DC    A(ERRTXT1)
ERRLEN1  DC    A(L'ERRTXT1)
ERRTXT1  DC    C'Muliple arguments are not allowed for the SHOW functio*
               n.'
*
ERRMSG2  CALL  ,(WRITEERR,ERRPTR2,ERRLEN2),VL,MF=L
ERRPTR2  DC    A(ERRTXT2)
ERRLEN2  DC    A(L'ERRTXT2)
ERRTXT2  DC    C'No arguments were specified for the SHOW function.'
*
ERRMSG3  CALL  ,(WRITEERR,ERRPTR3,ERRLEN3),VL,MF=L
ERRPTR3  DC    A(ERRTXT3)
ERRLEN3  DC    A(L'ERRTXT3)
ERRTXT3  DC    C'This ID is not supported for SHOW processing.'
*
ERRMSG4  CALL  ,(WRITEERR,ERRPTR4,ERRLEN4),VL,MF=L
ERRPTR4  DC    A(ERRTXT4)
ERRLEN4  DC    A(L'ERRTXT4)
ERRTXT4  DC    C'Invalid environment detected by SHOW.'
         EJECT
***********************************************************************
*                                  DATA
***********************************************************************
MVC      MVC   ID(0),0(5)          EXECUTED
ILENGTH  DC    F'250'              REXX EVALBLOCK_EVDATA LENGTH
WRITEERR DC    CL8'WRITEERR'
         END
./ ADD NAME=SHOWSPF  0103-85344-92115-1618-000388-000368-000029-$A1019
SHOWSPF  TITLE 'INFORMATION EXTRACTION SPF COMMAND'
***********************************************************************
*
*              INFORMATION EXTRACTION SPF COMMAND
*
*        THIS IS AN INTERFACE TO THE INFORMATION EXTRACTION ROUTINE
*        FOR SPF CLISTS UNDER SPF. IT IS INVOKED AS FOLLOWS:
*
*              ISPEXEC SELECT PGM(SHOWSPF) PARM(ID,NAME)
*              ISPEXEC VGET NAME SHARED
*
*        ID   - MUST BE A VALID IDENTIFIER FOR THE SHOW ROUTINE.
*        NAME - WILL BE USED TO CREATE A VARIABLE IN THE
*               SPF SHARED VARIABLE POOL. THE VARIABLE WILL BE SET TO
*               THE VALUE CORRESPONDING TO THE SHOW INDENTIFIER.
*
*
*        Douglas H. Adams
*        Mail Code 110-SE28
*        Information Systems Center
*        Rockwell International Corporation
*        2201 Seal Beach Boulevard
*        P. O. Box 2515
*        Seal Beach, California 90740-1515
*
*        (310) 797-2618
*
*        April 24, 1992
*
***********************************************************************
         PUNCH ' ALIAS SHOWSPF'
SHOWSPF  CSECT
SHOWSPF  AMODE 31
SHOWSPF  RMODE ANY
         EJECT
         REGEQU ,                  REGISTER EQUATES
         EJECT
***********************************************************************
*              PARAMETER
***********************************************************************
PARM     DSECT
PARMSIZE DS    H                   LENGTH OF PARAMETER STRING
PARMVAL  DS    CL100               PARAMETER CHARACTER STRING
         EJECT
***********************************************************************
*              WORKING STORAGE
***********************************************************************
WORK     DSECT                     WORK AREA
SAVE     DS    18F                 SAVE AREA
NAMELIST DS    C'('                ISPLINK VPUT NAME LIST
NAME     DS    CL8                 ISPLINK VPUT VARIABLE NAME
NAMEEND  DS    C')'                END OF NAME LIST
ID       DS    CL8                 SHOW IDENTIFIER
ILENGTH  DS    A(L'VALUE)          SHOW VALUE INPUT LENGTH
VALUE    DS    CL256               SHOW VALUE
OLENGTH  DS    F                   SHOW VALUE OUTPUT LENGTH
CALLSHOW CALL  ,                   CALL SHOW                           *
               (ID,                ID                                  *
               ILENGTH,            ILENGTH                             *
               VALUE,              VALUE                               *
               OLENGTH),           OLENGTH                             *
               VL,                                                     *
               MF=L
DEFINE   CALL  ,                   CALL ISPLINK                        *
               (,                  'VDEFINE'                           *
               NAMELIST,           NAMELIST                            *
               VALUE,              VALUE                               *
               ,                   'CHAR'                              *
               OLENGTH),           OLENGTH                             *
               VL,                                                     *
               MF=L
DELETE   CALL  ,                   CALL ISPLINK                        *
               (,                  'VDELETE'                           *
               NAMELIST),          NAMELIST                            *
               VL,                                                     *
               MF=L
PUT      CALL  ,                   CALL ISPLINK                        *
               (,                  'VPUT'                              *
               NAMELIST,           NAMELIST                            *
               ),                  'SHARED'                            *
               VL,                                                     *
               MF=L
WORKSIZE DS    0XL(*-WORK)         LENGTH OF WORK AREA
         EJECT
***********************************************************************
*              INITIALIZATION
***********************************************************************
SHOWSPF  CSECT
         USING SHOWSPF,R15
         SAVE  (14,12)             SAVE REGISTERS
         MODID ,                   IDENTIFICATION
         DROP  R15
         LR    R12,R15             R12 = BASE REGISTER
         USING SHOWSPF,R12
         L     R11,0(R1)           R11 -> PARM
         USING PARM,R11
         LA    R0,L'WORKSIZE       R0 = LENGTH OF WORK AREA
         GETMAIN R,LV=(0)          GET MAIN STORAGE FOR WORK AREA
         LR    R2,R1               R2 -> STORAGE
         LA    R3,L'WORKSIZE       R3 = LENGTH OF STORAGE
         SR    R5,R5               R5 = 0
         MVCL  R2,R4               CLEAR WORK AREA
         ST    R13,4(R1)           CHAIN SAVE AREAS
         ST    R1,8(R13)           ...
         LR    R13,R1              R13 -> WORKING STORAGE
         USING WORK,R13
         MVI   ID,C' '             ID = BLANK
         MVC   ID+1(L'ID-1),ID     ...
         MVI   NAME,C' '           NAME = BLANK
         MVC   NAME+1(L'NAME-1),NAME ...
         MVI   NAMELIST,C'('       NAMELIST = (NAME)
         MVI   NAMEEND,C')'        ...
         LA    R0,L'VALUE          ILENGTH = LENGTH OF VALUE
         ST    R0,ILENGTH          ...
         EJECT
***********************************************************************
*              PARSE PARAMETER STRING VIA SYNTAX MATRIX
***********************************************************************
*----------------------------------------------------------------------
*        POINT TO BEGINNING OF INPUT STRING.
*        POINT TO INITIAL ROW OF SYNTAX MATRIX.
*----------------------------------------------------------------------
         LH    R6,PARMSIZE         R6 = SIZE OF PARM FIELD
         BNP   ARGERROR            BRANCH IF MISSING
         BCTR  R6,0                R6 = SIZE - 1
         LA    R1,PARMVAL          R1 -> PARAMETER STRING
         BCTR  R1,0                R1 -> STRING - 1 FOR TRT
         SR    R2,R2               R2 = 0 FOR TRT
         LA    R4,MATRIX           R4 -> INITIAL MATRIX ROW
*----------------------------------------------------------------------
*        POINT TO NEXT INPUT CHARACTER
*----------------------------------------------------------------------
NEXT     DS    0H                  SCAN FOR NEXT PARAMETER
         LA    R7,PARAMETR         R7 -> TRT TABLE FOR PARAMETER
POINT    DS    0H                  POINT TO NEXT INPUT CHARACTER
COMA2##  DS    0H                  COMMA AFTER DELIMITING BLANK
         LTR   R6,R6               END OF PARAMETER STRING?
         BM    PROCESS             YES, BRANCH
         LA    R5,1(R1)            R5 -> NEXT INPUT CHARACTER
*----------------------------------------------------------------------
*        SCAN FOR NEXT SIGNIFICANT CHARACTER IN INPUT STRING.
*        TRANSLATE CHARACTER TO ITS CATEGORY (MATRIX COLUMN NUMBER).
*        CHECK FOR END OF PARAMETER STRING.
*        OBTAIN MATRIX ELEMENT USING CURRENT ROW AND COLUMN.
*        USE THAT ELEMENT AS NEW MATRIX ROW NUMBER.
*        BRANCH TO ACTION ROUTINE INDEXED BY THAT ELEMENT.
*----------------------------------------------------------------------
         EX    R6,TRT              R1 -> DELIMITING CHARACTER
*                                  R2 = MATRIX COLUMN NUMBER
         BNZ   SET                 END OF INPUT STRING?
         LA    R1,2(R6,R1)         R1 -> END OF INPUT STRING + 1
         LA    R2,ENDOS            R2 = ENDING MATRIX COLUMN NUMBER
SET      DS    0H
         BCTR  R6,0                R6 = REMAINING LENGTH
         AR    R6,R5               ...
         SR    R6,R1               ...
         SR    R3,R3               R3 = 0
         IC    R3,0(R2,R4)         R3 = MATRIX ELEMENT
         LR    R4,R3               R4 = NEW MATRIX ROW NUMBER
         MH    R4,=Y(L'MATRIX)     R4 = NUMBER * MATRIX ROW LENGTH
         LA    R4,MATRIX(R4)       R4 -> NEW MATRIX ROW
         SLL   R3,2                R3 = BRANCH INDEX
         B     ACTION(R3)          GO TO ACTION ROUTINE
*
TRT      TRT   1(0,R1),0(R7)       EXECUTED
*----------------------------------------------------------------------
*        BRANCH TABLE TO ACTION ROUTINES.
*----------------------------------------------------------------------
ACTION   DS    0H
         B     ABEND               SHOULD NEVER GET HERE
         B     PARM1##             FIRST PARAMETER
         B     BLNK1##             BLANK DELIMITING FIRST PARAMETER
         B     COMA1##             COMMA DELIMITING FIRST PARAMETER
         B     COMA2##             COMMA AFTER DELIMITING BLANK
         B     PARM2##             SECOND PARAMETER
         B     BLNK2##             BLANK DELIMITING SECOND PARAMETER
         B     ENDP1##             END OF SECOND PARAMETER
         B     ENDP2##             END OF PARAMETER STRING
         B     DELIM##             DELIMITER ERROR
         B     ABEND##             LOGIC ERROR
         EJECT
***********************************************************************
*              TAKE ACTION BASED ON SYNTACTIC SIGNIFICANCE
***********************************************************************
PARM1##  DS    0H                  FIRST PARAMETER
PARM2##  DS    0H                  SECOND PARAMETER
         LR    R9,R1               R9 -> PARAMETER
         LA    R7,DELIMITR         R7 -> TRT TABLE FOR DELIMITER
         B     POINT               POINT TO NEXT INPUT CHARACTER
*----------------------------------------------------------------------
BLNK1##  DS    0H                  BLANK DELIMITING FIRST PARAMETER
COMA1##  DS    0H                  COMMA DELIMITING FIRST PARAMETER
         LR    R8,R1               R8 = LENGTH OF FIELD SCANNED - 1
         SR    R8,R9               ...
         BCTR  R8,0                ...
         C     R8,=A(L'ID-1)       IS LENGTH OF PARAMETER > ID?
         BH    ARGERROR            YES, BRANCH
         EX    R8,MVCID            MOVE ID TO WORKING STORAGE
         B     NEXT                SCAN FOR PARAMETER
*
MVCID    MVC   ID(0),0(R9)         EXECUTED
*----------------------------------------------------------------------
ENDP1##  DS    0H                  END OF SECOND PARAMETER
BLNK2##  DS    0H                  BLANK DELIMITING SECOND PARAMETER
         LR    R8,R1               R8 = LENGTH OF FIELD SCANNED - 1
         SR    R8,R9               ...
         BCTR  R8,0                ...
         C     R8,=A(L'NAME-1)     IS LENGTH OF PARAMETER > NAME?
         BH    ARGERROR            YES, BRANCH
         EX    R8,MVCNAME          MOVE NAME TO WORKING STORAGE
         B     NEXT                SCAN FOR PARAMETER
*
MVCNAME  MVC   NAME(0),0(R9)       EXECUTED
         EJECT
***********************************************************************
ENDP2##  DS    0H                  END OF PARAMETER STRING
PROCESS  DS    0H                  PROCESS PARAMETERS
         ICM   R15,15,=V(SHOW)     POINT TO SHOW ROUTINE
         BZ    LINK                BRANCH IF NOT RESOLVED  *DHA 04/92*
         CALL  (15),               INVOKE SHOW                         *
               (ID,                IDENTIFIER FOR INFORMATION REQUESTED*
               ILENGTH,            LENGTH PROVIDED FOR VALUE           *
               VALUE,              INFORMATION REQUESTED               *
               OLENGTH),           LENGTH OF INFORMATION PROVIDED      *
               VL,                                                     *
               MF=(E,CALLSHOW)
         B     TESTRET             GO TEST RETURN CODE     *DHA 04/92*
LINK     DS    0H                                          *DHA 04/92*
         LINK  EP=SHOW,                                    *DHA 04/92* *
               (ID,                IDENTIFIER FOR INFORMATION REQUESTED*
               ILENGTH,            LENGTH PROVIDED FOR VALUE           *
               VALUE,              INFORMATION REQUESTED               *
               OLENGTH),           LENGTH OF INFORMATION PROVIDED      *
               VL=1,                                                   *
               MF=(E,CALLSHOW)
TESTRET  DS    0H                                          *DHA 04/92*
         LTR   R15,R15             TEST RETURN CODE
         BNZ   SHOWERR             BRANCH IF ERROR
         CALL  ISPLINK,            DEFINE FUNCTION VARIABLE            *
               (=C'VDEFINE',       'VDEFINE'                           *
               NAMELIST,           (VARIABLE-NAME)                     *
               VALUE,              VALUE                               *
               =C'CHAR',           'CHAR'                              *
               OLENGTH),           VARIABLE-LENGTH                     *
               VL,                                                     *
               MF=(E,DEFINE)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ISPERROR            BRANCH IF ERROR
         CALL  ISPLINK,            COPY TO SHARED VARIABLE             *
               (=C'VPUT',          'VPUT'                              *
               NAMELIST,           (VARIABLE-NAME)                     *
               =C'SHARED'),        'SHARED'                            *
               VL,                                                     *
               MF=(E,PUT)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ISPERROR            BRANCH IF ERROR
         CALL  ISPLINK,            DELETE FUNCTION VARIABLE            *
               (=C'VDELETE',       'VDELETE'                           *
               NAMELIST),          (VARIABLE-NAME)                     *
               VL,                                                     *
               MF=(E,DELETE)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ISPERROR            BRANCH IF ERROR
         B     NORMAL              NORMAL RETURN
         EJECT
***********************************************************************
*              TERMINATION
***********************************************************************
SHOWERR  DS    0H                  SHOW ERROR
ISPERROR DS    0H                  SPF ERROR
         LR    R2,R15              SAVE RETURN CODE
         B     RETURN
DELIM##  DS    0H                  DELIMITER ERROR
         LA    R2,20               RETURN CODE = 20
         B     RETURN
ARGERROR DS    0H                  CALLER ARGUMENT LIST ERROR
         LA    R2,12               RETURN CODE = 12
         B     RETURN
NORMAL   DS    0H                  NORMAL COMPLETION
         SR    R2,R2               RETURN CODE = 0
RETURN   DS    0H
         LA    R0,L'WORKSIZE       R0 = LENGTH OF WORK AREA
         LR    R1,R13              R1 -> DYNAMIC STORAGE
         L     R13,SAVE+4          R13 -> PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE MAIN STORAGE FOR WORK AREA
         LR    R15,R2              R15 = RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
*
ABEND##  ABEND 0,DUMP              LOGIC ERROR
*
         DROP  R11
         DROP  R12
         DROP  R13
         EJECT
***********************************************************************
*              PARSE TABLES
***********************************************************************
*----------------------------------------------------------------------
*        CATEGORIES OF CHARACTERS:
*              TRANSLATE-AND-TEST TABLE VALUES;
*              MATRIX COLUMN NUMBERS.
*----------------------------------------------------------------------
FIELD    EQU   0                   VALID CHARACTERS BETWEEN DELIMITERS
BLANK    EQU   1  ' '              BLANK
ALPHA    EQU   2  $#@ A-Z          ALPHABETIC AND NATIONAL
NUMBR    EQU   3  0-9              NUMERIC
COMMA    EQU   4  ,                COMMA
ENDOS    EQU   5                   END OF STRING
OTHER    EQU   6                   ANY OTHER CHARACTER
*----------------------------------------------------------------------
*        CORRESPONDENCE BETWEEN CHARACTERS AND CATEGORIES:
*              SCAN FOR START OF PARAMETER
*              TRANSLATE-AND-TEST TABLE
*----------------------------------------------------------------------
PARAMETR DS    0CL256
         DC    64AL1(OTHER)
         DC      AL1(FIELD)  ' '   BLANK
         DC    26AL1(OTHER)
         DC      AL1(ALPHA)  $     NATIONAL
         DC    15AL1(OTHER)
         DC      AL1(COMMA)  ,     COMMA
         DC    15AL1(OTHER)
         DC     2AL1(ALPHA)  #@    NATIONAL
         DC    68AL1(OTHER)
         DC     9AL1(ALPHA)  A-I   ALPHABETIC
         DC     7AL1(OTHER)
         DC     9AL1(ALPHA)  J-R   ALPHABETIC
         DC     8AL1(OTHER)
         DC     8AL1(ALPHA)  S-Z   ALPHABETIC
         DC     6AL1(OTHER)
         DC    10AL1(NUMBR)  0-9   NUMERIC
         DC     6AL1(OTHER)
*----------------------------------------------------------------------
*        CORRESPONDENCE BETWEEN CHARACTERS AND CATEGORIES:
*              SCAN FOR DELIMITER BETWEEN PARAMETERS
*              TRANSLATE-AND-TEST TABLE
*----------------------------------------------------------------------
DELIMITR DS    0CL256
         DC    64AL1(OTHER)
         DC      AL1(BLANK)  ' '   BLANK
         DC    26AL1(OTHER)
         DC      AL1(FIELD)  $     NATIONAL
         DC    15AL1(OTHER)
         DC      AL1(COMMA)  ,     COMMA
         DC    15AL1(OTHER)
         DC     2AL1(FIELD)  #@    NATIONAL
         DC    68AL1(OTHER)
         DC     9AL1(FIELD)  A-I   ALPHABETIC
         DC     7AL1(OTHER)
         DC     9AL1(FIELD)  J-R   ALPHABETIC
         DC     8AL1(OTHER)
         DC     8AL1(FIELD)  S-Z   ALPHABETIC
         DC     6AL1(OTHER)
         DC    10AL1(FIELD)  0-9   NUMERIC
         DC     6AL1(OTHER)
*----------------------------------------------------------------------
*        SYNTACTIC SIGNIFICANCE:
*              MATRIX ROW NUMBERS;
*              ACTION INDEX NUMBERS.
*----------------------------------------------------------------------
BEGIN    EQU   0                   BEGINNING OF INPUT STRING
PARM1    EQU   1                   FIRST PARAMETER
BLNK1    EQU   2                   BLANK DELIMITING FIRST PARAMETER
COMA1    EQU   3                   COMMA DELIMITING FIRST PARAMETER
COMA2    EQU   4                   COMMA AFTER DELIMITING BLANK
PARM2    EQU   5                   SECOND PARAMETER
BLNK2    EQU   6                   BLANK DELIMITING SECOND PARAMETER
ENDP1    EQU   7                   END OF SECOND PARAMETER
ENDP2    EQU   8                   END OF PARAMETER STRING
DELIM    EQU   9                   DELIMITER ERROR
ABEND    EQU   10                  LOGIC ERROR
*----------------------------------------------------------------------
*        CORRESPONDENCE BETWEEN CHARACTER CATEGORIES AND SYNTACTIC
*        SIGNIFICANCE:
*              NEW MATRIX ROW NUMBERS;
*              ACTION INDEXES.
*----------------------------------------------------------------------
MATRIX   DS    0XL6,AL1
*                  BLANK ALPHA NUMBR COMMA ENDOS OTHER
BEGIN#   DC    AL1(ABEND,PARM1,DELIM,DELIM,DELIM,DELIM)
PARM1#   DC    AL1(BLNK1,PARM1,PARM1,COMA1,DELIM,DELIM)
BLNK1#   DC    AL1(ABEND,PARM2,DELIM,COMA2,DELIM,DELIM)
COMA1#   DC    AL1(ABEND,PARM2,DELIM,DELIM,DELIM,DELIM)
COMA2#   DC    AL1(ABEND,PARM2,DELIM,DELIM,DELIM,DELIM)
PARM2#   DC    AL1(BLNK2,PARM2,PARM2,DELIM,ENDP1,DELIM)
BLNK2#   DC    AL1(ABEND,DELIM,DELIM,DELIM,ENDP2,DELIM)
         EJECT
         END   ,
./ ADD NAME=SHOWTSO  0133-85354-92115-1618-000293-000134-000293-$A1019
         TITLE 'SHOWTSO - SET CLIST VARIABLES WITH SHOW INFO'
***********************************************************************
*  NAME:     SHOWTSO                                                  *
*  FUNCTION: TO INVOKE THE SHOW UTILITY AND CREATE TSO/E CLIST        *
*            VARIABLES WITH INFORMATION FROM ACF2 AND SYSTEM CONTROL  *
*            BLOCKS FOR USE BY A TSO/E CLIST.                         *
*                                                                     *
*  SYNTAX:   SHOWTSO OPERAND(S)                                       *
*                                                                     *
*  NOTE:       OPERANDS MAY BE SPECIFIED SINGLY OR IN PAIR SEPERATED  *
*              BY AN EQUAL SIGN.   IF AN OPERAND IS NOT EQUATED WITH  *
*              ANOTHER NAME BY AN '=' THEN THE OPERAND VALUE IS ALSO  *
*              THE NAME OF THE CLIST VARIABLE TO BE GENERATED.        *
*              IF AN OPERAND IS SPECIFIED AS OPERAND=NAME THEN THE    *
*              SPECIFIED NAME WILL BE THE NAME OF THE CLIST VARIABLE. *
*                                                                     *
*  OPERANDS:   ALL SUPPORTED VALUES OF THE SHOW PROGRAM.              *
*                                                                     *
*  DEPENDENCIES:  TSO/E AND THE SHOW PROGRAM.                         *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* AUTHOR:      LIONEL DYCK                DOUGLAS H. ADAMS            *
*              ROCKWELL INTERNATIONAL                                 *
*              2201 SEAL BEACH BLVD.                                  *
*              PO BOX 2515                                            *
*              SEAL BEACH, CALIF  90740                               *
*              MAIL CODE 110-SE28                                     *
*              PHONE (310) 797-1125       (310) 797-2618              *
*              COMNET 797-1125                                        *
*                                                                     *
***********************************************************************
         EJECT                                             *LBD 12/85*
         PUNCH ' ALIAS SHOWTSO'                            *DHA 04/92*
SHOWTSO  CSECT                                             *DHA 04/92*
SHOWTSO  AMODE 31                                          *DHA 04/92*
SHOWTSO  RMODE ANY                                         *DHA 04/92*
         REGEQU                                            *DHA 04/92*
         EJECT                                             *DHA 04/92*
************************************************************DHA 04/92*
*              WORK AREA FOR REENTRANCE                    *DHA 04/92*
************************************************************DHA 04/92*
WORKAREA DSECT                                             *DHA 04/92*
SAVE     DS    18F                                         *DHA 04/92*
IOPLADS  DS    4F                                          *DHA 04/92*
ECBADS   DS    F                                           *DHA 04/92*
         DS    0F                                          *DHA 04/92*
PUTBLOK  PUTLINE MF=L              PUTLINE PTPB            *DHA 04/92*
         BITSW DOIT,END,ERR                                *DHA 04/92*
SHOWEPA  DS    A                   EPA OF SHOW             *DHA 04/92*
OPERANDS DS    CL256               INPUT PARAMETERS        *DHA 04/92*
ILENGTH  DS    A                   SHOW INPUT LENGTH       *DHA 04/92*
VALUE    DS    CL256               SHOW OUTPUT VALUE       *DHA 04/92*
VALUELEN DS    F                   SHOW OUTPUT LENGTH      *DHA 04/92*
NAMELEN  DS    F                   LENGTH OF VARIABLE NAME *DHA 04/92*
VALUEPTR DC    A(VALUE)            A(VARIABLE VALUE)       *DHA 04/92*
NAMEPTR  DC    A(CLSTNAME)         A(VARIABLE NAME)        *DHA 04/92*
*                                                          *DHA 04/92*
OLDCEMSG DS    F'2'                PUTLINE OLD             *DHA 04/92*
ACEMSG   DS    A(CEMSG)            A(PRIMARY MESSAGE)      *DHA 04/92*
ACLSTLEN DS    A(CLSTLEN)          A(TEXT INSERT)          *DHA 04/92*
CLSTLEN  DS    Y(L'CLSTNAME)       TEXT LENGTH             *DHA 04/92*
CLSTOFF  DS    Y(CEMSGNM)          TEXT OFFSET             *DHA 04/92*
CLSTNAME DS    CL8                 NAME OF CLIST VAR       *DHA 04/92*
*                                                          *DHA 04/92*
OLDSEMSG DS    F'2'                PUTLINE OLD             *DHA 04/92*
ASEMSG   DS    A(SEMSG)            A(PRIMARY MESSAGE)      *DHA 04/92*
ASHOWLEN DS    A(SHOWLEN)          A(TEXT INSERT)          *DHA 04/92*
SHOWLEN  DS    Y(L'SHOWNAME)       TEXT LENGTH             *DHA 04/92*
SHOWOFF  DS    Y(SEMSGNM)          TEXT OFFSET             *DHA 04/92*
SHOWNAME DS    CL8                 NAME OF SHOW VALUE      *DHA 04/92*
*                                                          *DHA 04/92*
CALLSHOW CALL  ,(SHOWNAME,ILENGTH,VALUE,VALUELEN),VL,MF=L  *DHA 04/92*
CALLVACC CALL  ,(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,  *DHA 04/92* *
               TOKEN),VL,MF=L                              *DHA 04/92*
WORKSIZE EQU   *-WORKAREA                                  *DHA 04/92*
         EJECT                                             *DHA 04/92*
SHOWTSO  CSECT                                             *DHA 04/92*
         USING *,R15                                       *DHA 04/92*
         MODID ,                                           *DHA 04/92*
         SAVE  (14,12)             SAVE REGISTERS          *DHA 04/92*
         LR    R12,R15                                     *DHA 04/92*
         DROP  R15                                         *DHA 04/92*
         USING SHOWTSO,R12                                 *DHA 04/92*
         LR    R11,R1              SAVE PARM ADDRESS       *DHA 04/92*
         GETMAIN RU,LV=WORKSIZE    GET WORK AREA           *DHA 04/92*
         LR    R2,R1               ZERO OUT WORK AREA      *DHA 04/92*
         L     R3,=A(WORKSIZE)                             *DHA 04/92*
         SR    R5,R5                                       *DHA 04/92*
         MVCL  R2,R4                                       *DHA 04/92*
         ST    R13,4(R1)           CHAIN SAVE AREAS        *DHA 04/92*
         ST    R1,8(R13)                                   *DHA 04/92*
         LR    R13,R1                                      *DHA 04/92*
         USING WORKAREA,R13                                *DHA 04/92*
         LA    R2,IOPLADS          POINT TO IOPL           *DHA 04/92*
         USING IOPL,R2                                     *DHA 04/92*
         L     R3,4(,R11)          COPY A(UPT)             *DHA 04/92*
         ST    R3,IOPLUPT                                  *DHA 04/92*
         L     R3,12(,R11)         COPY A(ECT)             *DHA 04/92*
         ST    R3,IOPLECT                                  *DHA 04/92*
         L     R3,ECBADS           POINT TO ECB            *DHA 04/92*
         ST    R3,IOPLECB                                  *DHA 04/92*
         DROP  R2                                          *DHA 04/92*
         EJECT
         L     R3,0(,R11)          -> COMMAND BUFFER       *DHA 04/92*
         LH    R1,2(R3)           LOAD OFFSET TO 1ST OPER
         LH    R2,0(R3)           LOAD LENGTH
         SR    R2,R1              GET LEN
         SH    R2,=H'04'          LESS HEADER
         LA    R3,4(R3)           -> PAST HEADER
         AR    R3,R1              -> 1ST OPERAND
         BCTR  R2,R0              LESS 1 FOR MVC            *LBD 05/89*
         EX    R2,MOVEOPER        MOVE OPERANDS
         EJECT
         ICM   R0,15,=V(SHOW)      GET POINTER TO SHOW     *DHA 04/92*
         BNZ   STSHOW              BRANCH IF RESOLVED      *DHA 04/92*
         LOAD  EP=SHOW
STSHOW   DS    0H                                          *DHA 04/92*
         ST    R0,SHOWEPA
         OC    OPERANDS,BLANKS    SET UPPER CASE
         LA    R5,OPERANDS
         LR    R7,R5
         AR    R7,R2              -> END OF DATA
         MVC   CLSTNAME,BLANKS    INITIALIZE CLSTNAME
         MVC   SHOWNAME,BLANKS    INITIALIZE SHOWNAME
         LA    R4,SHOWNAME
LOOP1    DS    0H
         IFON  END,EXIT
         CLI   0(R5),C'='         START OF CLIST NAME ?
         BE    LOOPP              YES
         CLI   0(R5),C' '         BLANK
         BE    INCR2              YES
         MVC   0(1,R4),0(R5)      MOVE CHAR OF NAME
         LA    R4,1(R4)
         SETON DOIT
INCR     DS    0H
         LA    R5,1(R5)           BUMP R5
         CR    R5,R7              AT END ?
         BNH   LOOP1              NO
         IFON  DOIT,PROCESS
         SETON END
         B     EXIT
INCR2    DS    0H
         IFON  DOIT,PROCESS
         B     INCR
         EJECT
LOOPP    DS    0H
         LA    R4,CLSTNAME
         B     INCR
         EJECT
PROCESS  DS    0H
         SETOF DOIT
         CLI   CLSTNAME,C' '
         BNE   SKMV
         MVC   CLSTNAME,SHOWNAME
SKMV     DS    0H
         LA    R1,SHOWNAME+7      -> END OF SHOWNAME
         LA    R2,8               SET LEN
SCKLEN   CLI   0(R1),C' '         END OF DSN
         BNE   SETLEN
         BCTR  R1,R0              LESS 1
         BCT   R2,SCKLEN
SETLEN   ST    R2,NAMELEN
         LA    R1,L'VALUE
         ST    R1,ILENGTH
         L     R15,SHOWEPA         SHOW ENTRY POINT
         LA    R8,SHOWNAME         SHOW VALUE NAME         *DHA 04/92*
         LA    R9,ILENGTH          SHOW INPUT LENGTH       *DHA 04/92*
         LA    R10,VALUE           SHOW OUTPUT VALUE       *DHA 04/92*
         LA    R11,VALUELEN        SHOW OUTPUT LENGTH      *DHA 04/92*
         CALL  (15),((R8),(R9),(R10),(R11)),VL,            *DHA 04/92* *
               MF=(E,CALLSHOW)                             *DHA 04/92*
         CH    R15,=H'04'
         BNH   SETVAR
         LA    R0,2                NUMBER OF MSG SEGMENTS  *DHA 04/92*
         ST    R0,OLDSEMSG                                 *DHA 04/92*
         LA    R0,SEMSG            A(PRIMARY MESSAGE)      *DHA 04/92*
         ST    R0,ASEMSG                                   *DHA 04/92*
         LA    R0,SHOWLEN          A(TEXT INSERT)          *DHA 04/92*
         ST    R0,ASHOWLEN                                 *DHA 04/92*
         LA    R0,L'SHOWNAME       TEXT LENGTH             *DHA 04/92*
         STH   R0,SHOWLEN                                  *DHA 04/92*
         LA    R0,SEMSGNM          TEXT OFFSET             *DHA 04/92*
         STH   R0,SHOWOFF                                  *DHA 04/92*
         PUTLINE PARM=PUTBLOK,OUTPUT=(OLDSEMSG),           *DHA 04/92* *
               MF=(E,IOPLADS)                              *DHA 04/92*
         SETON ERR
         LA    R4,SHOWNAME
         MVC   CLSTNAME,BLANKS
         MVC   SHOWNAME,BLANKS
         B     INCR
         EJECT
SETVAR   DS    0H
         SPACE 1
         CLC   CLSTNAME,SHOWNAME  SAME NAME ?
         BE    DOCLST             YES
         LA    R1,CLSTNAME+7
         LA    R2,8
VLP1     CLI   0(R1),C' '         LAST SIGNIFICANT CHAR
         BNE   SETVLP1
         BCTR  R1,R0
         BCT   R2,VLP1
SETVLP1  ST    R2,NAMELEN
         SPACE 1
DOCLST   DS    0H                                          *LBD 12/85*
         L     R15,CVTPTR       -> CVT
         USING CVTMAP,R15
         L     R15,CVTTVT         ACCESS THE TSVT
         DROP  R15
         USING TSVT,R15
         TM    TSVTVACC,X'80'     31-BIT ADDRESS ?          *LBD 12/88*
         BZ    CALLPTGT           NO                        *LBD 12/88*
         L     R1,BIT31           LOAD 31-BIT ADDRESS       *LBD 12/88*
         BSM   R0,R1              SET MODE TO 31-BIT        *LBD 12/88*
BIT31    DC    A(CALLPTGT+X'80000000')                      *LBD 12/88*
CALLPTGT DS    0H                                           *LBD 12/88*
         L     R15,TSVTVACC
         DROP  R15
*
         LA    R0,CLSTNAME         A(VARIABLE NAME)        *DHA 04/92*
         ST    R0,NAMEPTR                                  *DHA 04/92*
         LA    R0,VALUE            A(VARIABLE VALUE)       *DHA 04/92*
         ST    R0,VALUEPTR                                 *DHA 04/92*
         LA    R8,NAMEPTR          A(VARIABLE NAME)        *DHA 04/92*
         LA    R9,NAMELEN          LENGTH OF VARIABLE      *DHA 04/92*
         LA    R10,VALUEPTR        A(VARIABLE VALUE)       *DHA 04/92*
         LA    R11,VALUELEN        LENGTH OF VARIABLE VALUE*DHA 04/92*
         CALL  (15),(ECODE,(R8),(R9),(R10),(R11),TOKEN),   *DHA 04/92* *
               VL,MF=(E,CALLVACC)                          *DHA 04/92*
         LA    R1,LTR2            LOAD R1 WITH 24-BIT ADDR  *LBD 12/88*
         BSM   R0,R1              AND RETURN TO 24-BIT      *LBD 12/88*
LTR2     LTR   R15,R15
         BZ    RET
         LA    R0,2                NUMBER OF MSG SEGMENTS  *DHA 04/92*
         ST    R0,OLDCEMSG                                 *DHA 04/92*
         LA    R0,CEMSG            A(PRIMARY MESSAGE)      *DHA 04/92*
         ST    R0,ACEMSG                                   *DHA 04/92*
         LA    R0,CLSTLEN          A(TEXT INSERT)          *DHA 04/92*
         ST    R0,ACLSTLEN                                 *DHA 04/92*
         LA    R0,L'CLSTNAME       TEXT LENGTH             *DHA 04/92*
         STH   R0,CLSTLEN                                  *DHA 04/92*
         LA    R0,CEMSGNM          TEXT OFFSET             *DHA 04/92*
         STH   R0,CLSTOFF                                  *DHA 04/92*
         PUTLINE PARM=PUTBLOK,OUTPUT=(OLDCEMSG),           *DHA 04/92* *
               MF=(E,IOPLADS)                              *DHA 04/92*
         SETON ERR
RET      LA    R4,SHOWNAME
         MVC   CLSTNAME,BLANKS
         MVC   SHOWNAME,BLANKS
         MVC   VALUE,BLANKS
         B     INCR
         EJECT
EXIT     DS    0H
         ICM   R0,15,=V(SHOW)      GET POINTER TO SHOW     *DHA 04/92*
         BNZ   NODELETE            BRANCH IF RESOLVED      *DHA 04/92*
         DELETE EP=SHOW
NODELETE DS    0H                                          *DHA 04/92*
         SR    R15,R15
         IFOFF ERR,EXIT0
         LA    R15,8
EXIT0    DS    0H
         LR    R1,R13              UNCHAIN SAVE AREAS      *DHA 04/92*
         L     R13,4(,R1)                                  *DHA 04/92*
         FREEMAIN RU,LV=WORKSIZE,A=(1)  FREE WORKAREA      *DHA 04/92*
         RETURN (14,12),RC=0                               *DHA 04/92*
MOVEOPER MVC   OPERANDS(0),0(R3)  ***** EXECUTED
         DROP  R13                                         *DHA 04/92*
         DROP  R12                                         *DHA 04/92*
         EJECT
         LTORG
         SPACE 2
CEMSG    DC    AL2(CEMSGL),AL2(0)
CEMSGTXT DC    C' ERROR SETTING CLIST VARIABLE '
CEMSGNM  EQU   *-CEMSGTXT
CEMSGL   EQU   *-CEMSG
         SPACE 1
SEMSG    DC    AL2(SEMSGL),AL2(0)
SEMSGTXT DC    C' INVALID INFORMATION REQUEST FOR VALUE '
SEMSGNM  EQU   *-SEMSGTXT
         DC    C' SEE HELP FOR VALID KEYWORD OPTIONS'
SEMSGL   EQU   *-SEMSG
         SPACE 2
BLANKS   DC    CL256' '
ECODE    DC    A(TSVEUPDT)      ENTRY CODE
TOKEN    DC    F'0'
         EJECT
         IKJTSVT
         EJECT
         IKJIOPL ,
         EJECT
         CVT   DSECT=YES,LIST=YES
         END
