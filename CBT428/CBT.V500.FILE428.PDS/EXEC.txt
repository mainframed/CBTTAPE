./ ADD NAME=RSHOWTSO
/*REXX******************************************************************
*                                                                      *
* Display system information from various sources:                     *
*                                                                      *
*    SHOWREXX function                                                 *
*    RATJESUP command (JES3)                                           *
*    REXX (miscellaneous instructions)                                 *
*    SYSVAR function (REXX/TSO)                                        *
*    ISPF (VGET service)                                               *
*                                                                      *
* Douglas H. Adams                                                     *
* Mail Code 110-SE28                                                   *
* Rockwell International Corporation                                   *
* 2201 Seal Beach Boulevard                                            *
* P. O. Box 2515                                                       *
* Seal Beach, California 90740-1515                                    *
*                                                                      *
* Internet:  $A1019@WCC1A.Rockwell.Com                                 *
*                                                                      *
* (310) 797-2618                                                       *
*                                                                      *
* October 30, 1992                                                     *
*                                                                      *
***********************************************************************/

Trace "Off"
"NEWSTACK"
Address ISPEXEC
"TBCREATE RSHOWTSO NAMES(ID VALUE) NOWRITE REPLACE"

/***********************************************************************
* Obtain data via SHOWREXX function                                    *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from SHOW ",80,"*"),70)
"TBADD RSHOWTSO MULT(125)"

Queue "ACCT"
Queue "ACCTNO"
Queue "ACCTNO#"
Queue "ACF2ACCT"
Queue "BCPFMID"
Queue "BCPLEVEL"
Queue "CENTER"
Queue "COMPMAIL"
Queue "CPU"
Queue "CPUMODEL"
Queue "CPUSER"
Queue "CPUVER"
Queue "CRTCD"
Queue "DEPT"
Queue "DEST"
Queue "DFPLEVEL"
Queue "FDS"
Queue "FDS#"
Queue "FRAMES"
Queue "GROUP"
Queue "GRP"
Queue "HOMEDIV"
Queue "JOBFUNC"
Queue "JOBNAME"
Queue "LINES"
Queue "LOC"
Queue "MAIL"
Queue "NAME"
Queue "OPSYS"
Queue "OS"
Queue "OSLEV"
Queue "OSREL"
Queue "PHONE"
Queue "REFERVOL"
Queue "SHOWLEV"
Queue "SYSNAME"
Queue "TERMINAL"
Queue "TSOOPER"
Queue "TSOUNIT"
Queue "UID"
Queue "USERID"

Do queued()
   Parse pull id .
   Value = showrexx(id)
   "TBADD RSHOWTSO"
   End

/***********************************************************************
* Obtain data via SHOWREXX function (not supported by IBM)             *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from SHOW (Unsupported by IBM) ",,
        80,"*"),70)
"TBADD RSHOWTSO MULT(120)"

Queue "BDTNODE"
Queue "CLPA"
Queue "CVIO"
Queue "IPLDATE"
Queue "IPLTIME"
Queue "JOBID"
Queue "LPAR"
Queue "PRTY"
Queue "REALSTOR"
Queue "REGION"
Queue "SMFID"
Queue "SMS"
Queue "SWA"
Queue "SYSID"
Queue "SYSRES"
Queue "VERID"

Do queued()
   Parse pull id .
   Value = showrexx(id)
   "TBADD RSHOWTSO"
   End

/***********************************************************************
* Obtain data from JES3 via RATJESUP                                   *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from JES3/RATJESUP ",80,"*"),70)
"TBADD RSHOWTSO"

Do n = 1 to 6
   Id = "MAIN"n
   "SELECT CMD(RATJESUP" id")"
   If rc = 4
   Then leave
   Value = d2c(rc,4)
   Suffix = "system" substr(value,2,1) ,
            word("? ASP global local",pos(substr(value,3,1),"AGL")+1) ,
            word("? IPLed notIPLed",pos(right(value,1),"INU")+1)
   Value = value "("strip(suffix)")"
   "TBADD RSHOWTSO"
   End

Id = "RC"
"SELECT CMD(RATJESUP)"
Value = rc "("word("global local down ?",min(rc/4+1,4))")"
"TBADD RSHOWTSO"

Id = "VER"
"SELECT CMD(RATJESUP VER)"
Value = rc "(version "left(rc,1)"."substr(rc,2,1)"."right(rc,1)")"
"TBADD RSHOWTSO"

/***********************************************************************
* Obtain data from REXX                                                *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from REXX ",80,"*"),70)
"TBADD RSHOWTSO"

Parse value date("B")";"date("C")";"date("D")";" ||,
            date("E")";"date("J")";"date("M")";" ||,
            date("N")";"date("O")";"date("S")";" ||,
            date("U")";"date("W")";"             ||,
            time("C")";"time("H")";"time("L")";" ||,
            time("M")";"time("N")";"time("S"),
      with  dateb ";" datec ";" dated ";" datee ";" datej ";" datem ";",
            daten ";" dateo ";" dates ";" dateu ";" datew ";",
            timec ";" timeh ";" timel ";" timem ";" timen ";" times

Id = "DATE(B)"
Value = dateb "("comma(dateb) "days since January 1, 0001)"
"TBADD RSHOWTSO"

Id = "DATE(C)"
Value = datec "("comma(datec),
        "days since January 1," left(date("Standard"),2)"00)"
"TBADD RSHOWTSO"

Id = "DATE(D)"
Value = dated "(days since January 1," left(date("Standard"),4)")"
"TBADD RSHOWTSO"

Id = "DATE(E)"
Value = datee "(dd/mm/yy)"
"TBADD RSHOWTSO"

Id = "DATE(J)"
Value = datej "(yyddd)"
"TBADD RSHOWTSO"

Id = "DATE(M)"
Value = datem
"TBADD RSHOWTSO"

Id = "DATE(N)"
Value = daten
"TBADD RSHOWTSO"

Id = "DATE(O)"
Value = dateo "(yy/mm/dd)"
"TBADD RSHOWTSO"

Id = "DATE(S)"
Value = dates "(yyyymmdd)"
"TBADD RSHOWTSO"

Id = "DATE(U)"
Value = dateu "(mm/dd/yy)"
"TBADD RSHOWTSO"

Id = "DATE(W)"
Value = datew
"TBADD RSHOWTSO"

Id = "TIME(C)"
Value = timec
"TBADD RSHOWTSO"

Id = "TIME(H)"
Value = timeh "(hours since midnight)"
"TBADD RSHOWTSO"

Id = "TIME(L)"
Value = timel "(hh:mm:ss.uuuuuu)"
"TBADD RSHOWTSO"

Id = "TIME(M)"
Value = timem "("comma(timem) "minutes since midnight)"
"TBADD RSHOWTSO"

Id = "TIME(N)"
Value = timen "(hh:mm:ss)"
"TBADD RSHOWTSO"

Id = "TIME(S)"
Value = times "("comma(times) "seconds since midnight)"
"TBADD RSHOWTSO"

Id = "USERID"
Value = userid()
"TBADD RSHOWTSO"

Parse version value
Id = "VERSION"
"TBADD RSHOWTSO"

/***********************************************************************
* Obtain data from REXX/TSO SYSVAR function                            *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from REXX/TSO SYSVAR ",80,"*"),70)
"TBADD RSHOWTSO"

Queue "SYSCPU" "seconds"
Queue "SYSHSM"
Queue "SYSLRACF"
Queue "SYSLTERM" "lines"
Queue "SYSPREF"
Queue "SYSPROC"
Queue "SYSRACF"
Queue "SYSSRV" "service units"
Queue "SYSTSOE"
Queue "SYSUID"
Queue "SYSWTERM" "columns"

Do queued()
   Parse pull id suffix
   Value = strip(sysvar(id))
   If wordpos(id,"SYSHSM SYSLRACF SYSTSOE") ¬= 0
   Then suffix = "version" left(value,1)"."format(substr(value,2,2)) ||,
                 "."right(value,1) suffix
   If id == "SYSSRV"
   Then suffix = comma(value) suffix
   If suffix ¬= ""
   Then value = value "("strip(suffix)")"
   "TBADD RSHOWTSO"
   End

/***********************************************************************
* Obtain data from ISPF via VGET                                       *
***********************************************************************/
Id = copies("*",8)
Value = right(center(" Information from ISPF ",80,"*"),70)
"TBADD RSHOWTSO"

Queue "ZACCTNUM"
Queue "ZCOLORS"
Queue "ZDATE"
Queue "ZDAY"
Queue "ZENVIR"
Queue "ZHILITE"
Queue "ZJDATE"
Queue "ZKEYS"
Queue "ZLOGON"
Queue "ZMONTH"
Queue "ZPDFREL"
Queue "ZPREFIX"
Queue "ZSCREEND" "rows"
Queue "ZSCREENW" "columns"
Queue "ZSCRMAXD" "rows"
Queue "ZSCRMAXW" "columns"
Queue "ZSTDYEAR"
Queue "ZSYSID"
Queue "ZTERM"
Queue "ZTIME"
Queue "ZUSER"
Queue "ZYEAR"

Do queued()
   Parse pull id suffix
   "VGET" id
   Value = value(id)
   If datatype(value,"W") & left(value,1) == "0" & suffix ¬== ""
   Then suffix = format(value) suffix
   If id == "ZENVIR"
   Then suffix = left(value,8)"/"strip(substr(value,9,8))"/" || ,
                 substr(value,17)
   If suffix ¬= ""
   Then value = value "("strip(suffix)")"
   "TBADD RSHOWTSO"
   End

/***********************************************************************
* Clean up and display information                                     *
***********************************************************************/
Address TSO "DELSTACK"
"TBTOP RSHOWTSO"
"TBDISPL RSHOWTSO PANEL(RSHOWTSO)"
Do while rc = 0
   "CONTROL DISPLAY SAVE"
   "TBSKIP RSHOWTSO NUMBER("ztdtop") NOREAD"
   Parse upper var zcmd command id .
   If abbrev("LOCATE",command)
   Then do
      If id = ""
      Then id = save_id
      Else do
         Id = id"*"
         Save_id = id
         "TBTOP RSHOWTSO"
         End
      "TBSCAN RSHOWTSO ARGLIST(ID) NOREAD"
      If rc = 8
      Then "TBSKIP RSHOWTSO NUMBER("ztdtop") NOREAD"
      End
   "CONTROL DISPLAY RESTORE"
   "TBDISPL RSHOWTSO PANEL(RSHOWTSO)"
   End

/***********************************************************************
* Terminate                                                            *
***********************************************************************/
Exit

/***********************************************************************
* Insert commas into numbers (nnnnnnnnn -> nnn,nnn,nnn)                *
***********************************************************************/
Comma:
Return right(translate("12304560789",","right(arg(1),9),"0123456789"),,
       (length(arg(1))*4-1)%3)
