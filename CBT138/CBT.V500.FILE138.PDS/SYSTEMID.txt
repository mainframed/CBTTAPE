 TITLE 'S Y S T E M   C O N F I G U R A T I O N   D I S P L A Y'
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                                                                     *
*        THIS CODE HAS WORKED AT SUNGARD.  THERE IS NO GURANTEE       *
*        THAT THE CODE WILL WORK ANYWHERE ELSE.  NEITHER SUNGARD      *
*        NOR THE AUTHOR MAY BE HELD RESPONSIBLE FOR ANY DAMAGE        *
*        CAUSED BY THIS CODE.                                         *
*                                                                     *
*  SYSMVS IS THE OS/MVS VERSION OF THE SYSTEM CONFIGURATION DISPLAY   *
*     COMMAND.  IT SHOWS A TSO USER THE OPERATING SYSTEM, RELEASE,    *
*     CPU MODEL, SERIAL NUMBER(S), AND MAIN STORAGE SIZE, AND DATE.   *
*                                                                     *
*        MODIFIED TO GIVE MODEL NUMBERS FOR 308X, 3090, AND 4381      *
*                                                                     *
*        MODIFIED BY ART TANSKY AT SUNGARD FOR 3090                A.T.
*        PROCESSORS.  IT MUST NOW BE AUTHORIZED AS                 A.T.
*        IT MAKES USE OF THE "STIDP" PRIVELEGED                    A.T.
*        INSTRUCTION                                               A.T.
*                                                                     *
*        COMES FROM CBT FILE#300                                      *
*                                                                     *
*        RE-MODIFIED TO LIST THE AMOUNT OF MEG ONLINE INSTEAD OF   A.T.
*        THE AMOUNT OF K.  SYSTEMS KEEP GETTING BIGGER.            A.T.
*                                                                     *
*        MODIFIED TO GIVE INFOMATION ABOUT THE VM SYSTEM MVS IS    A.T.
*        RUNNING UNDER.                                            A.T.
*                                                                     *
*        MODIFIED FORMAT OF OUTPUT LINE.                              *
*                                                                     *
*        MODIFIED TO SHOW AMOUNT OF ESTOR AVAILABLE.                  *
*                                                                     *
*        MODIFIED TO RUN AS A TSO COMMAND PROCESSOR.  USE PUTLINE  TSO*
*        INSTEAD OF TPUT.  CODE ADDED (CHANGED) IS MARKED "TSO".   TSO*
*                                                                     *
*        NOTE:  THIS NEED NO LONGER BE RUN AUTHORIZED, UNLESS         *
*        YOU WANT THE INFORMATION WHEN RUNNING UNDER VM, BUT THERE    *
*        IS A RESTRICTION THAT THE DATA IN THE CVT PREFIX ARE WAS     *
*        NOT THERE UNDER SP, IT STARTS WITH XA.  IF THIS PROGRAM IS   *
*        TO RUN UNDER SP, THE DATA MUST BE RETRIEVED WITH THE STIDP   *
*        COMMAND, AND THUS IT WILL HAVE TO BE AUTHORIZED, THOUGH      *
*        THE REQUIRED INFORMATION MIGHT BE AVAILABLE IN THE PSA OR    *
*        PCCA.                                                        *
*                                                                     *
*        MODIFIED TO COUNT ONLINE PROCESSORS.                         *
*                                                                     *
*        MODIFIED AGAIN FOR 2-LINE FORMAT & ADDITIONAL DATA           *
*                                                                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
         SPACE 2
         MACRO
&LBL     DIAG &R1,&R2,&CODE ISSUE DIAGNOSE MACHINE COMMAND
         DS    0H                  ASSURE HALFWORD ALIGNMENT
&LBL     DC    X'83'               OPCODE
         DC    AL.4(&R1,&R2)       OPERANDS 4 BITS EACH
         DC    Y(&CODE)            DIAGNOSE CODE
         MEND
         EJECT
SYSMVS   PRIME R12,AMODE=31,GETMAIN=NO,LV=144  AMODE REQ'D FOR RCE TSO
         LUSE  CPPL,R1                                             TSO
         LA    R10,MYIOPL                                          TSO
         LUSE  IOPL,R10                                            TSO
         L     R0,CPPLUPT                                          TSO
         ST    R0,IOPLUPT                                          TSO
         L     R0,CPPLECT                                          TSO
         ST    R0,IOPLECT                                          TSO
         LA    R0,MYECB                                            TSO
         ST    R0,IOPLECB                                          TSO
         LDROP R10                                                 TSO
         LDROP R1                                                  TSO
         EJECT
         L     R10,CVTPTR
         LA    R5,CVTMAP-CVTFIX    BACK TO PREFIX
         SR    R10,R5
         LUSE  CVTFIX,R10
         L     R3,CVTSYSAD
         MVC   OPSYS,CVTPRODN
         MVC   FMID,CVTPRODI
         IC    R14,CVTDCB
         N     R14,VSMASK
         SRL   R14,1
         L     R9,CVTEORM
         SRL   R9,20
         A     R9,=F'1'
         CVD   R9,DWD
         ED    CSTOR,DWD+5
         L     R8,CVTRCEP          @ RCE
         LUSE  RCE,R8
         L     R8,RCEESPL          # FRAMES OF ESTOR AVAILABLE
         LDROP R8
         SRL   R8,8                CONVERT TO MEG
         CVD   R8,DWD
         ED    ESTOR,DWD+5
         MVI   CSTOR+L'CSTOR,C'M'
         MVI   ESTOR+L'ESTOR,C'M'
         MVC   CUU,UCBNAME-UCBOB(R3)
         MVC   VOLSER,UCBVOLI-UCBOB(R3)
         L     R9,CVTDFA
         LUSE  DFA,R9
         ED    DFP,DFAREL
         LDROP R9
         L     R9,CVTSMCA
         LUSE  SMCABASE,R9
         MVC   SMFID,SMCASID
         ED    YYDDD,SMCAIDTE+1
         L     R1,SMCAITME
         SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DWD
         OI    DWD+7,X'0F'
         UNPK  HH,DWD+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'6000'
         CVD   R1,DWD
         OI    DWD+7,X'0F'
         UNPK  MM,DWD+6(2)
         LDROP R9
         UNPK  WORK(5),CVTMDL(3)
         MVC   CPU,WORK                                            A.T.
         L     R9,CVTPCCAT         @ PCCAVT
*  FIND HOW MANY CP'S (CPU'S) ARE ONLINE
         LA    R15,16              UP TO 16 POSSIBLE
         XR    R1,R1               COUNTER
CPUON    DS    0H
         CLC   =F'0',0(R9)         THIS CP ONLINE?
         BE    NOTON                 NO
         LA    R1,1(R1)              YES
NOTON    DS    0H
         LA    R9,4(R9)            POINT TO NEXT ADDRESS
         BCT   R15,CPUON           AND CHECK NEXT
         CVD   R1,DWD
         ED    NUMCPS,DWD+6
         CLC   =C' 1',NUMCPS+2     ONLY ONE CP?
         BNE   MORECPS               NO, OKAY
         MVC   CPS,=C'CP  '        GET RID OF THE PLURAL
MORECPS  DS    0H
         LUSE  PSA,R0
         L     R9,PSAPCCAV         @ PCCA
         LDROP R0
         LUSE  PCCA,R9
         MVC   SERIAL,PCCACPID+2
         MVC   DWD+2(3),PCCACPID   CONVERT PRINTABLE HEX TO HEX
         TR    DWD+2(3),PRHEXTBL
         PACK  DWD(2),DWD+2(3)
         LDROP R9
         CLI   DWD,X'FF'           ARE WE UNDER VM?
         BE    DOVM                  YES
TYPELKUP DS    0H                  LOOK UP CPU TYPE
         LA    R9,TBL23A1          ASSUME A 23A1
         CLC   =C'23Â³1',CPU        IS IT?
         BE    FINDTYPE
         LA    R9,TBL9000          ASSUME A 9000
         CLC   =C'9000',CPU        IS IT?
         BE    FINDTYPE
         LA    R9,TBL3090          ASSUME A 3090
         CLC   =C'3090',CPU        IS IT?
         BE    FINDTYPE
         LA    R9,TBL4381          ASSUME A 4381
         CLC   =C'4381',CPU        IS IT?
         BE    FINDTYPE
         LA    R9,TBL308X          ASSUME A 308X
         CLC   =C'30',CPU          IS IT?
         BNE   NOTYPE              NO, CAN'T DETERMINE MODEL TYPE
         CLC   =C'81',CPU+2        3081?
         BE    FINDTYPE            YES
         CLC   =C'83',CPU+2        3083?
         BE    FINDTYPE            YES
         CLC   =C'84',CPU+2        3084?
         BE    FINDTYPE            YES
         B     NOTYPE              CAN'T DETERMINE TYPE
FINDTYPE DS    0D
*  FIND CPU TYPE BASED ON INTERNAL (SEMI-RANDOM) NUMBERS IN THE
*  FIRST BYTE OF VALUE GIVEN BY STIDP.  VALUES CAN BE FOUND IN THE
*  FUNCTIONAL CHARACTERISTICS MANUAL FOR EACH CPU MODEL.
         CLC   0(1,R9),DWD
         BE    MOVETYPE
         BH    NOTYPE              NO MATCH - GIVE UP
         LA    R9,7(R9)            BUMP TO NEXT ENTRY
         B     FINDTYPE            AND KEEP LOOKING
*  WE GOT THE TYPE - MOVE IT INTO THE PRINT LINE
MOVETYPE DS    0D
         MVC   CPUTYPE,1(R9)
NOTYPE   DS    0H
         EJECT
         PUTLINE PARM=PUTLPB,                                      TSO X
               OUTPUT=(LINE1PTR,MULTLIN,DATA),                     TSO X
               ENTRY=LINKPUTL,                                     TSO X
               MF=(E,MYIOPL)                                       TSO
         B     LEAVE               EXIT
DOVM     DS    0D                  GET INFO FROM VM
         TESTAUTH FCTN=1           ARE WE AUTHORIZED
         LTR   R15,R15
         BZ    AUTHOK                YES
         MVC   CPUTYPE,=C' SUBVM'
         B     NOTYPE
AUTHOK   DS    0D
         MODESET MODE=SUP,KEY=ZERO
         LRA   R1,VMBUFF
         LA    R2,VMBUFFLN
         DIAG  R1,R2,X'0000'       X'83120000' IF NO DIAG MACRO
         MODESET MODE=PROB,KEY=NZERO
         MVC   DWD(1),IPUVC        MOVE IN VERSION
         MVC   VMSYS,HPID
         MVC   VMID,USID
         XR    R7,R7
         IC    R7,VC
         CVD   R7,DWD2
         ED    VMVERS,DWD2+6
         IC    R7,LVL
         CVD   R7,DWD2
         ED    VMLVL,DWD2+6
         ICM   R7,B'0011',MCEL
         CVD   R7,DWD2
         ED    VMPUT,DWD2+5
*        LA    R7,VMLINEND-VMLINE                                  TSO
*        TPUT  VMLINE,(R7)                                         TSO
         PUTLINE PARM=PUTLPB,                                      TSO ?
               OUTPUT=(VMRDW,SINGLE,DATA),                         TSO ?
               ENTRY=LINKPUTL,                                     TSO ?
               MF=(E,MYIOPL)                                       TSO
         B     TYPELKUP            GO BACK AND PUT OUT 1ST LINE, SECOND
         SPACE
LEAVE    DS    0H
*
*     DEBUGGING STUFF:
*
*        MODESET MODE=SUP
*        STIDP DWD
*        MODESET MODE=PROB
*        LUSE  PSA,R0
*        L     R9,PSAPCCAV         @ PCCA
*        LDROP R0
*        LUSE  PCCA,R9
*        MVC   DWD+1(7),PCCACPID
*        LDROP R9
*        EX    15,*                ABEND 0C3
*
*     END OF DEBUGGING STUFF
*
         TERME RC=0
LINKPUTL SAVE  (14,12)
         LA    R15,72(,R13)
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         LINK  SF=(E,CALLPUTL),MF=(E,(1))
         L     R13,4(,R13)
         RETURN (14,12),T,RC=(15)
         EJECT
CALLPUTL LINK  EP=IKJPUTL,SF=L                                     TSO
MYECB    DC    F'0'                                                TSO
MYIOPL   DC    XL(IOPLLEN)'0',0F'0'                                TSO
PUTLPB   PUTLINE MF=L                                              TSO
VSMASK   DC    F'2'
F8       DC    F'8'
*
*  NOTE: THESE TABLES MUST EACH BE SORTED ASCENDING
*        (WATCH OUT THAT A-F SHOULD SORT AFTER 0-9)
*
TBL9000  EQU   *                   MODEL NUMBERS FOR 9000'S
         DC    X'A0',CL6' 190  '
         DC    X'A1',CL6' 210  '
         DC    X'A3',CL6' 260  '
         DC    X'A4',CL6' 320  '
         DC    X'A5',CL6' 440  '
         DC    X'A6',CL6' 480  '
TBL23A1  EQU   *      MODEL NUMBERS FOR 23A1'S (AIR-COOLED 9000'S?)
         DC    X'28',CL6' 500  '
         DC    X'38',CL6' 580  '
         DC    X'39',CL6' 330  '
         DC    X'45',CL6' 620  '
         DC    X'49',CL6' 340  '
         DC    X'65',CL6' 720  '
TBL3090  EQU   *                   MODEL NUMBERS FOR 3090'S
         DC    X'01',CL6' 120S '
         DC    X'02',CL6' 150J '
         DC    X'03',CL6' 120E '
         DC    X'05',CL6' 150  '
         DC    X'06',CL6' 150E '
         DC    X'08',CL6' 280S '
         DC    X'09',CL6' 280E '
         DC    X'10',CL6' 180  '
         DC    X'11',CL6' 180E '
         DC    X'12',CL6' 180S '
         DC    X'13',CL6' 180J '
         DC    X'14',CL6' 150S '
         DC    X'15',CL6' 170S '
         DC    X'16',CL6' 250S '
         DC    X'17',CL6' 170J '
         DC    X'18',CL6' 280J '
         DC    X'20',CL6' 200  '
         DC    X'21',CL6' 200E '
         DC    X'22',CL6' 200S '
         DC    X'23',CL6' 200J '
         DC    X'25',CL6' 250J '
         DC    X'31',CL6' 300E '
         DC    X'32',CL6' 300S '
         DC    X'33',CL6' 300J '
         DC    X'35',CL6' 380J '
         DC    X'40',CL6' 400  '
         DC    X'41',CL6' 400E '
         DC    X'42',CL6' 400S '
         DC    X'43',CL6' 400J '
         DC    X'50',CL6' 500S '
         DC    X'51',CL6' 500E '
         DC    X'52',CL6' 500J '
         DC    X'55',CL6' 110J '
         DC    X'56',CL6' 120J '
         DC    X'57',CL6' 150JH'
         DC    X'58',CL6' 170JH'
         DC    X'59',CL6' 250JH'
         DC    X'61',CL6' 600E '
         DC    X'62',CL6' 600S '
         DC    X'63',CL6' 600J '
         DC    X'FF',CL6' SUBVM'
TBL308X  EQU   *                   MODEL NUMBERS FOR 308X'S
         DC    X'01',CL6' E    '
         DC    X'03',CL6' D    '
         DC    X'11',CL6' B    '
         DC    X'13',CL6' G    '
         DC    X'21',CL6' J    '
         DC    X'23',CL6' K    '
         DC    X'26',CL6' Q    '
         DC    X'53',CL6' G+   '
         DC    X'63',CL6' K+   '
         DC    X'66',CL6' Q+   '
         DC    X'81',CL6' EX   '
         DC    X'91',CL6' BX   '
         DC    X'93',CL6' GX   '
         DC    X'A1',CL6' JX   '
         DC    X'A3',CL6' KX   '
         DC    X'A6',CL6' QX   '
         DC    X'B1',CL6' CX   '
         DC    X'FF',CL6' SUBVM'
TBL4381  EQU   *                   MODEL NUMBERS FOR 4381'S
         DC    X'00',CL6'-2    '
         DC    X'01',CL6'-3    '
         DC    X'02',CL6'-1    '
         DC    X'03',CL6'-13   '
         DC    X'04',CL6'-14   '
         DC    X'06',CL6'-11   '
         DC    X'07',CL6'-23   '
         DC    X'08',CL6'-12   '
         DC    X'09',CL6'-21   '
         DC    X'0A',CL6'-22   '
         DC    X'FF',CL6' SUBVM'
VMBUFFLC DC    0F'0',AL2(VMBUFFLN),XL2'0'
PRHEXTBL TABLE
         ORG   PRHEXTBL+C'A'
         DC    X'FAFBFCFDFEFF'     TRANSLATE HEX LETTERS TO "PACK-ABLE"
         ORG   PRHEXTBL+C'0'
         DC    C'0123456789'
         ORG   ,
         LTORG
         TITLE 'OUTPUT LINES'
LINE1PTR DC    A(LINE2PTR)         @ 2ND LINE
LINE1RDW DC    Y(LINE1END-LINE1RDW,0)
LINE1    EQU   *
         DC    C'SMFID'
NUMCPS   DC    X'40202120'
         DC    C' '
CPS      DC    C'CP''S'
         DC    C'  SERIAL CSTOR ESTOR  SYSTEM    FMID   DFP  '
         DC    C'SYSRES   IPL='
YYDDD    DC    X'40212061202020'   ' YY/DDD'                       A.T.
LINE1END EQU   *
LINE2PTR DC    A(0)                NO NEXT LINE
LINE2RDW DC    Y(LINE2END-LINE2RDW,0)
LINE2    EQU   *
SMFID    DC    C'C309'
         DC    C' '
CPU      DC    CL4' '
CPUTYPE  DC    CL6' '
         DC    C' '
SERIAL   DC    CL6' '
         ORG   *-1                 OVERLAY CSTOR OVER LAST BYTE OF SER#
CSTOR    DC    X'402020202120'     STORAGE AMOUNT                  A.T.
ESTOR    DC    X'402020202120'     STORAGE AMOUNT                  A.T.
         DC    C'  '
OPSYS    DC    C'SP3.1.3'
         DC    C' '
FMID     DC    CL7' '
DFP      DC    X'40214B204B20'
         DC    C' '
CUU      DC    CL3' '
         DC    C'='
VOLSER   DC    CL6' '
         DC    CL5'    @'
HH       DC    C'  '
         DC    C':'
MM       DC    C'  '
LINE2END EQU   *
BESTORPL EQU   *,1                 PUT A PLUS HERE AFTER THE EDIT
BCPHLD   DC    X'40202120'
DWD      DS    D
DWD2     DS    D
VMBUFF   DS    0D
HPID     DS    CL8                 SYSTEM NAME
         DS    XL3                 RESERVED
IPUVC    DS    XL1                 CPU VERS. CODE (1ST BYTE COP STIDP)
MMCEL    DS    XL2                 370 GUESTS: MAX MCEL LENGTH
*                                  XA GUESTS: RESERVED
IPUAD    DS    XL2                 INSTR PROC UNIT
USID     DS    CL8                 VM USERID
PPFLG    DS    XL8                 PROG PROD BIT MAP
TIMEZONE DS    XL4                 TIME ZONE DIFFERENTIAL (IN SECONDS)
LVL      DS    XL1                 RELEASE NUMBER
VC       DS    XL1                 VERSION NUMBER
MCEL     DS    XL2                 PLC TAPE NUMBER (MVS PUT EQUIV.)
VMBUFFLN EQU   *-VMBUFF
WORK     DS    CL7                                                 A.T.
VMRDW    DC    Y(VMLINEND-VMRDW,0)
VMLINE   DC    C'IPLED UNDER '
VMSYS    DC    CL8'VM/XA/SP'
         DC    C' VERSION '
VMLVL    DC    X'40202120'
         DC    C'.'
VMVERS   DC    X'F0202120'
         DC    C' AT TAPE#'
VMPUT    DC    X'402020202120'
         DC    C'.  VM USERID='
VMID     DC    CL8'VMID'
VMLINEND EQU   *
         SPACE
         EJECT
         CVT   DSECT=YES,PREFIX=YES
         EJECT
         PRINT NOGEN
         IEESMCA
         IEFUCBOB
         IHADFA
         IHAPCCA
         IHAPSA
         IARRCE
         SPACE 2
         IKJCPPL ,                                                 TSO
         SPACE 5                                                   TSO
         IKJIOPL ,                                                 TSO
IOPLLEN  EQU   *-IOPL                                              TSO
         END   SYSMVS
