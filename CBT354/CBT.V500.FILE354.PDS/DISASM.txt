//DISASM JOB (87944,348496),HALL.S1BL,MSGCLASS=A,
//             USER=RH87944,PASSWORD=*
/*ROUTE  PRINT R7
//STEP1  EXEC HLASMC,CLASS='*',
//            PARMC='SYSPARM(1,ESD)'
//ASM.SYSLIB DD DSN=HALL.DISASM.SOURCE,DISP=SHR
//   DD DSN=SYS1.MACLIB,DISP=SHR
//   DD DSN=SYS1.AMODGEN,DISP=SHR
//ASM.SYSLIN   DD DSN=HALL.OBJECT(DISASM),DISP=SHR
//ASM.SYSIN DD *
*                                                                   RH
*   MODIFIED JAN 1999 BY RANDY HALL                                 RH
*      HOPEFULLY ALL MODS HAVE "RH" IN COL 69-70...                 RH
*      1) MADE THE HEX DUMP AN OPTION WITH PARM=TEXT                RH
*      2) CLEANED UP THE PARM PROCESSING                            RH
*      3) CHANGED SYSPRINT DCB TO LRECL=133 FROM 121                RH
*      4) AT LABEL GETCTL1, USE RDJFCB ON SYSLIB TO GET MBR NAME    RH
*         IF SPECIFIED AND TEST FOR SYSIN OPEN                      RH
*      5) AT LABEL FREESTRG, TEST FOR "TEXT" PARM TO PRINT HEX DUMP RH
*      6) ADDED CODE TO PRINT AN ASSEMBLER COMPILER TYPE HEADING    RH
*         WITH PAGE COUNT AND STATEMENT NUMBER                      RH
*      7) AT LABEL EOJ, ADDED CODE TO PRINT REGISTER EQUATES,       RH
*         RATHER THAN "REQU".                                       RH
*      8) AT LABEL DFMTFLD/CFL1, ADDED CODE TO PRINT DSECTS WITH    RH
*         "DS" RATHER THAN "EQU". CHANGE "AGO" FOR EQU VERSION...   RH
*      9) ADDED MORE CHARACTERS TO "PRTCHAR" TABLE                  RH
*     10) INCLUDE DSECTS FROM SYSLIB WITH "DISASPRM" COPY AT END    RH
*                                                                   RH
*                                                                   RH
*                                                                   RH
DIS      TITLE '***** DISASSEMBLY PHASE 0 *****'
         MACRO
&NAME    SVLNK &REG1,&REG2,&REG3,&IND,&LV=
         GBLC  &EQU
         GBLC  &GM
         GBLC  &CSECT
         LCLC  &REGA
         AIF   (T'&NAME EQ 'O').NOCSECT
         AIF   ('&NAME' EQ '*').NOCSECT
&CSECT   SETC  '&NAME'
&CSECT   CSECT                         NAME OF PROGRAM
         AGO   .RST
.NOCSECT ANOP
&CSECT   SETC  '*'
         CSECT                         BEGINNING OF PROGRAM
.RST     ANOP
         AIF   ('&EQU' EQ 'ON').NRQ
&EQU     SETC  'ON'
*
***REGISTER EQUATES***
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.NRQ     ANOP
*
*******************  PROGRAM INITIALIZATION  *************************
*
         AIF   (T'&REG1 EQ 'O').NOREG
&REGA    SETC  '&REG1'
         AIF   (T'&REG2 EQ 'O').ONEREG
         USING &CSECT,&REGA,&REG2          ESTABLISH ADDRESSABILITY
         AGO   .BYUSE
.NOREG   ANOP
&REGA    SETC  'R3'
.ONEREG  USING &CSECT,&REGA              ESTABLISH ADDRESSABILITY
.BYUSE   ANOP
         STM   R14,R12,12(R13) :       STORE REGS IN HIGH SAVE AREA
         LR    &REGA,R15                  INITIALIZE BASE REG
         AIF   (T'&REG2 EQ 'O').NOLA
         LA    &REG2,4095(&REGA)       INITIALIZE THE SECOND
         LA    &REG2,1(&REG2)                BASE REGISTER
.NOLA    ANOP
         AIF   (T'&REG3 EQ 'O').NOLNK
         L     &REG3,0(R1)                GET PARAMETER LIST ADDR
.NOLNK   ANOP
         AIF (T'&IND EQ 'O').GETIT
         CNOP  0,4
         BAL   R15,SAVEAREA+72         BRANCH AROUND SAVE AREA
SAVEAREA DS    18F                     REGISTER SAVE AREA
         ST    R15,8(R13)
         ST    R13,SAVEAREA+4
         LR    R13,R15
         AGO   .NOSAVE
.GETIT   ANOP
         AIF   (T'&LV EQ 'O').GMD
&GM      SETC  '&LV'
         AGO   .DGM
.GMD     ANOP
&GM      SETC  '72'
.DGM     ANOP
*
***GET MAIN STORAGE FOR SAVE AREA***
*
         AIF   ('&GM' LT '4096').LA
         GETMAIN R,LV=&GM              GET CORE FOR SAVE AREA
         AGO   .EGM
.LA      ANOP
         LA    R0,&GM                  GET &GM BYTES
         GETMAIN R,LV=(0)
.EGM     ANOP
*
***SET UP SAVE AREA POINTERS***
*
         ST    R1,8(R13)               STORE LOW SAVE POINTER
         ST    R13,4(R1)               STORE HIGH SAVE POINTER
         LR    R13,R1                  INITIALIZE SAVE POINTER
         AIF   (T'&REG3  NE 'O').NOSAVE
         L     R1,4(R13)               GET POINTER TO RESTORE PARA REG
         L     R1,24(R1)               RESTORE PARAMETER REGISTER
.NOSAVE  ANOP
         AIF   ('&CSECT' EQ '*').EXIT
         B     *+12
         DC    CL8'&CSECT'             END INITIAL., BEGIN THIS PROG.
.EXIT    ANOP
*
*********************  END INITIALIZATION  ***************************
*
         MEND
         EJECT
DISASM   SVLNK R3,R4
         PRINT   GEN
*DISASSEMBLER PROGRAM TO CREATE AN ASSEMBLER SOURCE PROGRAM
*FROM A LOAD MODULE IN A PDS. DD CARDS REQUIRED INCLUDE
*SYSPRINT FOR MESSAGES AND DIAGNOSTICS USING BLKSIZE A MULTIPLE
*OF 121, SYSLIB SPECIFYING THE PDS CONTAINING THE MEMBER,        FIX***
*WHICH MUST BE A PDS, SYSPUNCH FOR THE ASSEMBLER SOURCE
*PROGRAM, HAVING BLKSIZE A MULTIPLE OF 80, AND SYSIN
*FOR THE CONTROL CARD INPUT. CONTROL INPUT MAY OPTIONALLY
*BE ENTERED VIA THE PARM ON THE EXECUTE CARD. CONTROL
*INFORMATION INCLUDES THE MEMBER NAME AND CSECT NAME
*TO BE DISASSEMBLED. IF CSECT NAME IS OMITTED, THE CSECT
*FOR ESD-ID 0001 WILL BE USED.
*
*
* FILES USED BY THIS PROGRAM INCLUDE THE FOLLOWING:
*
* DDNAME SYSLIB: RECFM=U. INPUT LOAD MODULE LIBRARY.
*
* DDNAME SYSPUNCH: RECFM=FB,LRECL=80. OUTPUT FILE CONTAINING
*                 DISASSEMBLED TEXT. (MAXBLK=18,960)
*
* DDNAME SYSPRINT: RECFM=FBA,LRECL=121.  PRINTED OUTPUT CONTAINING A
*                LIST OF THE ESD TABLE, RLD TABLE, AND TEXT.
*                (MAXBLK=18997)
* DDNAME LOADLIB: (OPTIONAL) NAMES A PDS CONTAINING THE
*                 MODULES OF DISASM IF DIFFERENT FROM
*                 THE STEPLIB. USED PRIMARILY FOR TSO.
*
* DDNAME SYSIN: RECFM=FB, LRECL=80. CONTROL CARD INPUT.
*
*
* THE CONTROL CARD PROVIDES THE MEMBER NAME AND CSECT NAME
* OF THE PROGRAM TO BE DISASSEMBLED. MEMBER NAME IS REQUIRED.
* IF CSECT NAME IS OMITTED, THE CSECT WITH ESDID 0001 WILL
* BE USED. FORMAT IS FREE-FORM. MEMBER NAME MUST PRECEDE CSECT
* NAME. ANY NUMBER OF BLANKS MAY PRECEDE AND FOLLOW MEMBER NAME.
* A COMMA MAY IMMEDIATELY FOLLOW MEMBER NAME IF DESIRED.
*
*
* PARM DATA FROM THE EXECUTE CARD MAY BE USED TO SPECIFY
* VALIDITY OF PRIVILEGED OR FLOATING POINT INSTRUCTIONS.
* IF NOT SPECIFIED, PRIVILEGED AND FLOATING POINT OPERATION
* CODES WILL NOT BE TREATED AS INSTRUCTION OP-CODES. TO
* INCLUDE THESE INSTRUCTIONS, CODE:
*     PARM=(SUPVR)      PRIVILEGED INSTRUCTIONS
*     PARM=(FLTPT)      FLOATING POINT INSTRUCTIONS
*     PARM=(SUPVR,FLTPT) BOTH
*
*
*PROCESSING FLOW:
* 1. PROCESS THE CONTROL INFORMATION TO OBTAIN THE MEMBER
*    AND CSECT NAMES.
* 2. ISSUE BLDL AGAINST SYSLIB TO OBTAIN DIRECTORY INFO FOR
*    THE MEMBER SPECIFIED. IF THE SPECIFIED MEMBER IS AN
*    ALIAS, RE-ISSUE A BLDL FOR THE REAL MEMBER. PRINT
*    DIRECTORY INFORMATION.
* 3. POINT TO THE MEMBER IN THE SYSLIB PDS, AND PROCESS THE
*    MEMBER. LOAD MODULES CONTAIN AN EXTERNAL SYMBOL DICTIONARY
*    FOLLOWED BY TEXT AND RELOCATION DICTIONARY INFORMATION.
*    ALL ESD INFO FOR THE MODULE PRECEDES THE FIRST CONTROL
*    RECORD. A CONTROL RECORD PRECEDES EACH BLOCK OF TEXT.
*    RLD INFO FOR THE TEXT FOLLOWS EACH TEXT BLOCK. PROCESSING
*    OF LOAD MODULE INFORMATION IS AS FOLLOWS:
*    A. BUILD AN EXTERNAL SYMBOL TABLE, USING THE CESD BLOCKS.
*    B. SEARCH FOR THE DESIRED CSECT AS THE TABLE IS BEING
*       BUILT. THIS CSECT MUST BE FOUND BEFORE THE FIRST
*       CONTROL RECORD.
*    C. READ BLOCKS UNTIL A CONTROL RECORD FOR THE DESIRED
*       CSECT IS FOUND (BY ESD-ID). WHEN FOUND, SAVE THE
*       TEXT FOR DISASSEMBLY, AND USE THE FOLLOWING RLD
*       INFORMATION TO BUILD A RELOCATION DICTIONARY TO BE USED
*       DURING DISASSEMBLY.
* 4. DISASSEMBLY USES THE EXTERNAL SYMBOL TABLE, RELOCATION
*    DICTIONARY, AND TEXT BUILT BY THE PREVIOUS STEPS.
*
*
* AUTHOR R THORNTON - NOV 1977
*
* UPDATES BY R. STYMA  JAN 1989
* UPDATE SGLOP TABLES IN PHASE 1 AND PHASE2 TO CONTAIN XA ONLY
* INSTRUCTIONS LIKE BSM, BAS, AND SO ON.  ALSO IDENTIFY BSM AND
* BAS AS BRANCH INSTRUCTIONS IN PHASES 1 AND 2.
* UPDATE INPUT BUFFER LENGTH TO CONTAIN MAX BSAM BLOCK SIZE.
* CHANGE GETMAIN LENGTHS TO CONSTANTS.
*
* UPDATED ALAN C FIELD - APR 1990
*
* UPDATES BY R. STYMA  MAR 1991
* MERGED CHANGES BY ALAN C FIELD WITH PREVIOUS CHANGES BY MYSELF
*
*
****************************************************************
*                                                              *
* PROCESS THE PARM FIELD, IF ANY.                              *
*                                                              *
****************************************************************
*
         L     R12,0(R1)               GET PARM FIELD ADDRESS
         SR    R1,R1                   CLEAR WORK REG
         ICM   R1,3,0(R12)             PICK UP PARM LENGTH
         BZ    OPENS                   NO PARM INFO ENTERED
*        CH    R1,H5                   5-BYTES OF PARM
*        BE    SGLPARM                 YES, SINGLE PARM FIELD
*        CH    R1,H11                  11-BYTES OF PARM
*        BE    DBLPARM                 YES, BOTH PARMS
*ADPARM  WTO   'INVALID PARM, SPECIFY ONLY SUPVR AND/OR FLTPT',        X
               ROUTCDE=11
*        ABEND 777,DUMP
*SGLPARM CLC   2(5,R12),=C'SUPVR'      IS PARM=SUPVR
*        BE    SUPARM                  YES
*        CLC   2(5,R12),=C'FLTPT'      PARM IS FLTPT
*        BNE   BADPARM                 NO, ERROR
*        MVI   FLPTASM,1               SET FLOATING POINT O.K.
*        B     OPENS                   CONTINUE
*DBLPARM CLC   2(5,R12),=C'SUPVR'      IS 1ST PARM SUPVR
*        BE    CK2FPT                  YES
*        CLC   2(5,R12),=C'FLTPT'      IS 1ST PARM FLTPT
*        BNE   BADPARM                 NO, ERROR
*        CLC   8(5,R12),=C'SUPVR'      IS 2ND PARM SUPVR         FIX***
*        BNE   BADPARM                 NO, ERROR
*        B     FPT2PARM                BOTH O.K.
*CK2FPT  CLC   8(5,R12),=C'FLTPT'      IS 2ND PARM FLTPT         FIX***
*        BNE   BADPARM                 YES
*FPT2PARM MVI   FLPTASM,1               SET FLOATING POINT O.K.
*SUPARM  MVI   PRIVASM,1               SET PRIVILEGED INSTR O.K.
*
*
SGLPARM  DS    0H                 PROCESS PARM                      RH
         CLC   2(4,R12),=C'TEXT'  DOES HE WANT A HEX DUMP OF MODULE RH
         BNE   SGLPARM2           NO, SEE IF FLTPT                  RH
         MVI   TEXTFLAG,1         SAY HE WANTS A DUMP               RH
         SH    R1,=H'5'           TAKE LTH OFF TOTAL PARM LTH       RH
         AH    R12,=H'5'          R12 -> TO NEXT PARM               RH
         SPACE 1                                                    RH
SGLPARM2 DS    0H                                                   RH
         CLC   2(5,R12),=C'FLTPT' IS PARM FLTPT                     RH
         BNE   SGLPARM3           NO, SEE IF SUPVR
         MVI   FLPTASM,1               SET FLOATING POINT O.K.
         SH    R1,=H'6'           TAKE LTH OFF TOTAL PARM LTH       RH
         AH    R12,=H'6'          R12 -> TO NEXT PARM               RH
         SPACE 1                                                    RH
SGLPARM3 DS    0H                                                   RH
         CLC   2(5,R12),=C'SUPVR' IS PARM SUPVR                     RH
         BNE   SGLPARM4           NO, SEE IF END                    RH
         MVI   PRIVASM,1          SET PRIVILEGED INSTR O.K.         RH
         SH    R1,=H'6'           TAKE LTH OFF TOTAL PARM LTH       RH
         AH    R12,=H'6'          R12 -> TO NEXT PARM               RH
         SPACE 1                                                    RH
SGLPARM4 DS    0H                                                   RH
         LTR   R1,R1              ANY PARMS LEFT ?                  RH
         BP    SGLPARM            YES, GO CHECK IT OUT              RH
*                                                                   RH
OPENS    RDJFCB LOADLIB                WAS LOADLIB DD CARD INCLUDED
         LTR   R15,R15                 JFCB READ O.K.
         BNZ   OPEN1                   NO, DONT OPEN
         OPEN  LOADLIB                 GOT DD CARD, OPEN FILE
*
****************************************************************
*                                                              *
* GET STORAGE FOR THE SYMBOL TABLE, RLD TABLE, AND DATA-ONLY   *
* TABLES, AND OPEN FILES.                                      *
*                                                              *
****************************************************************
*
OPEN1    OPEN  (SYSPRINT,OUTPUT,SYSPUNCH,OUTPUT)
         OPEN  (SYSLIB,,SYSIN)         OPEN FILES
*        TM    SYSIN+48,X'10'          DID SYSIN OPEN O.K.          RH
*        BZ    NOCTL                   NO                           RH
         BAL   R9,GETCTL               EXTRACT DESIRED MEMBER AND CSECT
         BAL   R9,BLDL                 ISSUE BLDL AND PRINT
         MVI   CCAT,0                  INSURE TTR0
         POINT SYSLIB,TTRMOD           POINT TO 1ST BLOCK OF MODULE
*        GETMAIN R,LV=20000            GET BUFFER STORAGE
*        GET BUFFER STORAGE
         GETMAIN RU,LV=32768,BNDRY=PAGE,LOC=BELOW
         ST    R1,BUFAD                SAVE BUFFER ADDRESS
*        GETMAIN R,LV=38000            GET SYMBOL TABLE STORAGE
         GETMAIN R,LV=SYMTABL          GET SYMBOL TABLE STORAGE
         ST    R1,SYMTBAD              SAVE SYMBOL TABLE ADDRESS
         ST    R1,CURRSYM              SAVE CURRENT SYMBOL ADDR
         MVI   0(R1),X'FF'             TABLE END INDIC
         A     R1,ENDSYM               COMPUTE END ADDR
         ST    R1,ENDSYM               STORE TBL END ADDR
*        GETMAIN R,LV=60000            GET RLD TABLE STORAGE
         GETMAIN R,LV=RLDTABL          GET RLD TABLE STORAGE
         ST    R1,RLDTBL               SAVE RLD TABLE ADDRESS
         ST    R1,CURRLD               SAVE CURRENT RLD ADDR
         MVI   0(R1),X'FF'             TABLE END INDIC
         A     R1,ENDRLD               COMPUTE END ADDR
         ST    R1,ENDRLD               STORE TBL END ADDR
*        GETMAIN R,LV=1544             GET DATA ONLY TABLE STORAGE
         GETMAIN R,LV=DATOLEN+8        GET DATA ONLY TABLE STORAGE
         ST    R1,DATONLY              SAVE TBL ADDRESS
         MVC   0(3,R1),HIVAL           SET END VALUE
         ST    R1,DATOCUR              SET CURRENT ADDRESS
         A     R1,ENDATO               COMPUTE TABLE END ADDR
         ST    R1,DATOEND              SAVE END ADDR
         MVC   PRT+15(29),=C'*** EXTERNAL SYMBOL TABLE ***'
         MVI   PCC,C'-'                SET SKIP 3 LINES
         BAL   R9,PRINT                PRINT
         MVC   PRTLINE(LSYMHDR),SYMHDR SYM TBL HEADER               RH
         BAL   R9,PRINT                PRINT
*
*
*
****************************************************************
*                                                              *
* MAINLINE PROCESSING FOR THE LOAD MODULE. THE DIRECTORY ENTRY *
* AND TEXT ARE PROCESSED.                                      *
*                                                              *
****************************************************************
*
MAINLINE EQU   *                       MAINLINE ROUTINE
         L     R6,BUFAD                GET BUFFER ADDRESS
         READ  DECB,SF,SYSLIB,(6),'S'  READ BLOCK FROM MEMBER
         CHECK DECB                    AWAIT COMPLETION
         CLI   0(R6),X'20'             CESD RECORD
         BNE   TESTOTHR                NO
         BAL   R9,CESDREC              PROCESS CESD RECORDS
         B     MAINLINE                GO READ AGAIN
TESTOTHR LA    R9,CNTLRECS             ASSUME CONTROL RECORD
         CLI   0(R6),1                 IT IS CONTROL
         BE    PERFORM                 YES
         CLI   0(R6),5                 IS IT CONTROL
         BE    PERFORM                 YES
         CLI   0(R6),13                IS IT CONTROL
         BE    PERFORM                 YES
         LA    R9,RLDRECS              ASSUME RLD RECORD
         CLI   0(R6),2                 IS IT RLD
         BE    PERFORM                 YES
         CLI   0(R6),6                 IS IT RLD
         BE    PERFORM                 YES
         CLI   0(R6),14                IS IT RLD
         BE    PERFORM                 YES
         LA    R9,CTRLRECS             ASSUME CONTROL AND RLD
         CLI   0(R6),3                 IS IT CTL AND RLD
         BE    PERFORM                 YES
         CLI   0(R6),7                 IS IT CTL AND RLD
         BE    PERFORM                 YES
         CLI   0(R6),15                IS IT CTL AND RLD
         BE    PERFORM                 YES
         B     MAINLINE                NONE OF THESE, SKIP
PERFORM  TM    PROCESS,X'80'           WAS CSECT FOUND
         BZ    MISSCS                  NO, ERROR
         BALR  R9,R9                   PERFORM APPROPRIATE ROUTINE
         TM    PROCESS,X'40'           MODULE PROCESSING DONE
         BZ    MAINLINE                NO, GO READ AGAIN
         MVI   EOFSW+1,0               SET END OF FILE SWITCH
         B     ENDINIT                 GO COMPLETE PROCESSING
*
*
*
****************************************************************
*                                                              *
* PROCESS LOAD MODULE CONTROL RECORDS. THESE RECORDS PRECEDE   *
* TEXT RECORDS, WHICH ARE READ AND PLACED IN STORAGE IN CONTIG-*
* UOUS LOCATIONS SO THAT THE TEXT FOR THE DESIRED CSECT WILL   *
* ALL BE IN STORAGE FOR THE REMAINDER OF DISASSEMBLY.          *
*                                                              *
****************************************************************
*
CNTLRECS EQU   *                       CONTROL RECORD PROCESSING
         ST    R9,CT9                  SAVE RETURN ADDR
         SR    R8,R8                   CLEAR WORK
         ICM   R8,7,9(R6)              LKED ASGND @ OF TXT
         TM    0(R6),X'08'             RECORD PRECEDES LAST RECORD OF M
         BZ    CNCKTYP                 NO
         OI    PROCESS,X'40'           YES, SHOW PROCESSING COMPLETE
CNCKTYP  TM    0(R6),X'02'             CONTROL AND RLD
         BO    CNPASRLD                YES
         LA    R12,16(R6)              @ CESD ENTRY NBR
         B     CNCKESD                 CONTINUE
CNPASRLD LH    R12,6(R6)               GET RLD SECTION LENGTH
         LA    R12,16(R6,R12)          @ CESD ENTRY NBR
CNCKESD  LH    R11,4(R6)               LENGTH OF CONTROL INFO SECTION
         SRL   R11,2                   COMPUTE NBR CNTL ENTRIES
         SR    R10,R10                 OFFSET TO 1ST BYTE
         SR    R9,R9                   LENGTH OF TEXT
CNCKESD1 CLC   ESDID,0(R12)            THIS THE DESIRED ESD   FIX***
         BE    CNFNDIT                 YES
         AH    R10,2(R12)              MAINTAIN OFFSET TO 1ST TEXT BYTE
         LA    R12,4(R12)              TO NEXT CNTL ENTRY
         BCT   R11,CNCKESD1            LOOP THRU CNTL ENTRIES  FIX***
         B     READTEXT                GO READ FOLLOWING TEXT
CNFNDIT  LH    R9,2(R12)               GET TEXT LENGTH
READTEXT READ  DECB,SF,,(6),MF=E       READ FOLLOWING TEXT RECORD
         CHECK DECB                    AWAIT COMPLETION
         LTR   R9,R9                   DOES IT CONTAIN DESIRED TEXT
         BZ    CTXIT                   NO, SKIP IT
         S     R8,START                (-) OFFSET IN MODULE    FIX***
         AR    R8,R10                  ADD OFFSET OF PORTION IN RCD  **
*           R8 SHOULD NOW CONTAIN THE OFFSET WITHIN THE CSECT
*           THAT THIS BLOCK CONTAINS (TRICKY).
*
         A     R10,BUFAD               @ 1ST TEXT BYTE
***      S     R8,START    MOVED UP    RELATIVIZE TEXT ADDR    FIX***
         A     R8,TXTSTRT              @ PLACE TO MOVE TEXT
         LR    R11,R9                  COPY LENGTH TO MOVE
         MVCL  R8,R10                  MOVE TEXT TO STORAGE
CTXIT    L     R9,CT9                  GET RETURN ADDR
         BR    R9                      EXIT
*
*
*
****************************************************************
*                                                              *
* PROCESS RLD RECORDS. A TABLE OF RLD DATA IS BUILT WHICH WILL *
* LATER BE USED TO BUILD PROGRAM LABELS AND ADCONS.            *
*                                                              *
****************************************************************
*
RLDRECS  EQU   *                       RLD RECORD PROCESSING
         TM    0(R6),X'08'             LAST RECORD OF MODULE
         BZ    RLSV9                   NO
         OI    PROCESS,X'40'           SHOW PROCESSING COMPLETE
RLSV9    ST    R9,RL9                  SAVE RETURN ADDR
RLDSW    NOP   RLDST                   FIRST TIME SWITCH
         MVI   RLDSW+1,C'0'            RESET 1ST TIME SW
         MVC   PRT+15(20),=C'***** RLD INFO *****'
         MVI   PCC,C'-'                SKIP 3 LINES
         BAL   R9,PRINT                PRINT
         MVC   PRTLINE(LRLDHDR),RLDHDR RLD HEADER                   RH
         BAL   R9,PRINT                PRINT RLD HEADER
RLDST    LH    R8,6(R6)                NBR BYTES OF RLD DATA
         LA    R6,16(R6)               @ 1ST BYTE OF RLD DATA
         AR    R8,R6                   END OF RLD DATA ADDR
         L     R7,CURRLD               @ NEXT RLD TBL ENTRY
         USING RLDTBLD,R7
         LH    R10,0(R6)               RELOCATION POINTER
         LH    R11,2(R6)               POSITION POINTER
         LA    R6,4(R6)                PASS POINTERS
RLDNXT   CLC   1(3,R6),START+1         RLD ADDR BELOW CSECT
         BL    RLDSTEP                 LOW, IGNORE
         CLC   1(3,R6),END+1           RLD ADDR ABOVE CSECT
         BH    RLDSTEP                 HIGH, IGNORE
         C     R7,ENDRLD               END OF RLD TBL
         BE    RLDFULL                 YES, ERROR
         STH   R10,RLDRP               SAVE RELOC PTR
         STH   R11,RLDPP               POS PTR
         PACK  RLDTYPE,0(1,R6)         INVERT FLAG BYTE
         NI    RLDTYPE,X'0F'           CLEAR HI-ORDER
         CLI   RLDTYPE,9               UNRESOLVED
         BNE   RLDMOVLN                NO
         MVI   RLDTYPE,8               YES, USE PREFERRED VALUE
RLDMOVLN MVC   RLDLEN,0(R6)            FLAG BYTE
         NI    RLDLEN,X'0F'            CLEAR HI-ORDER
         SR    R12,R12                 CLEAR WORK
         IC    R12,RLDLEN              PICK UP BYTE
         SRL   R12,2                   SHIFT OUT DIR, IND BITS
         LA    R12,1(R12)              ADD 1 = LENGTH
         STC   R12,RLDLEN              LENGTH CODE
         MVI   RLDDIR,C'+'             ASSUME POS RELOC
         TM    0(R6),2                 IS IT POSITIVE
         BZ    RLADMV                  YES
         MVI   RLDDIR,C'-'             NO, SHOW NEGATIVE
RLADMV   SR    R1,R1                   CLEAR WORK REG
         ICM   R1,7,1(R6)              PICK UP ADDRESS
         S     R1,START                RELATIVIZE WITHIN CSECT
         STCM  R1,7,RLDADDR            SAVE RELATIVE ADDRESS
         LA    R12,RLDRP               @ RELOC PTR
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+6(4),PRTABL         RELOC PTR
         LA    R12,RLDPP               @ POS PTR
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+14(4),PRTABL        POS PTR
         MVC   PRT+24(4),=C'ACON'      ASSUME A TYPE ADCON
         CLI   RLDTYPE,0               IS IT ADCON
         BE    RLDLN                   YES
         MVC   PRT+24(4),=C'VCON'      ASSUME VCON
         CLI   RLDTYPE,1               IS IT VCON
         BE    RLDLN                   YES
         MVC   PRT+22(6),=C'PRDISP'    ASSUME PSEUDO REG DISPL
         CLI   RLDTYPE,2               IS IT P.R. DISPL
         BE    RLDLN                   YES
         MVC   PRT+22(6),=C'PRCUM'     ASSUME PSEUDO REG CUMUL DISPL
         CLI   RLDTYPE,3               IS IT P.R. CUM DISPL
         BE    RLDLN                   YES
         MVC   PRT+21(10),=C'UNRESOLVED' ASSUME UNRESOLVED
         CLI   RLDTYPE,8               IS IT UNRESOLVED
         BE    RLDLN                   YES
         MVC   PRT+21(10),BLANX        CLEAR FIELD
         MVC   PRT+24(3),=C'??:'
         LA    R12,RLDTYPE             @ TYPE
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+27(1),PRTABL+1      TYPE
RLDLN    MVC   PRT+35(1),RLDLEN        LENGTH
         OI    PRT+35,C'0'             CLEAR ZONE
         MVC   PRT+42(1),RLDDIR        RELOCATION DIRECTION
         LA    R12,RLDADDR             @ ADDRESS
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+46(6),PRTABL        ADDRESS
         XC    RLDNAME(9),RLDNAME      CLEAR
         LH    R15,RLDRP               GET RELOCATION POINTER
         BCTR  R15,R0                  DEDUCT 1
         LTR   R15,R15                 TEST DIFFERENCE
         BM    RLPRT                   NEG, ERROR
         MH    R15,SYMLEN              TIMES SYM TBL ENTRY LENG
         A     R15,SYMTBAD             @ ESD SYMBOL TBL ENTRY
         C     R15,CURRSYM             PAST END OF TABLE
         BH    RLPRT                   YES, ERROR
         USING SYMTBL,R15
         MVC   RLDNAME,EXTSYM          ESD SYMBOL TO RLD TBL ENTRY
         MVC   RLDESDTP,TYPSYM         ESD TYPE TO RLD ENTRY
         DROP  R15
         MVC   PRT+60(8),RLDNAME       NAME TO PRINT
         MVC   PRT+70(2),=C'LR'        ASSUME LR
         CLI   RLDESDTP,3              IS IT LR
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'SD'        ASSUME SD
         CLI   RLDESDTP,0              IS IT SD
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'ER'        ASSUME ER
         CLI   RLDESDTP,2              IS IT ER
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'PC'        ASSUME PC
         CLI   RLDESDTP,4              IS IT PC
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'PR'        ASSUME PR
         CLI   RLDESDTP,6              IS IT PR
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'CM'        ASSUME CM
         CLI   RLDESDTP,5              IS IT CM
         BE    RLPRT                   YES
         MVC   PRT+70(2),=C'WX'        ASSUME WX
         CLI   RLDESDTP,X'0A'          IS IT WX
         BE    RLPRT                   YES
         MVC   PRT+70(4),=C'NULL'      ASSUME NULL
         CLI   RLDESDTP,7              IS IT NULL
         BE    RLPRT                   YES
         MVC   PRT+70(6),=C'E/STAB'    ASSUME E/STAB
         CLI   RLDESDTP,X'0F'          IS IT E/STAB
         BE    RLPRT                   YES
         MVC   PRT+70(6),=C'??????'    UNKNOWN TYPE
RLPRT    BAL   R9,PRINT                PRINT
         CLC   RLDNAME,CSECT           RLD NAME IS CSECT NAME
         BNE   GOODRLD                 NO
         CLC   RLDADDR,XZROS           RELATIVE OFFSET IS ZERO
         BE    RLDSTEP                 YES, IGNORE ENTRY
GOODRLD  LA    R7,L'RLDENT(R7)         TO NEXT TBL ENTRY
RLDSTEP  TM    0(R6),1                 NEXT ITEM HAS REL AND POS PTRS
         BO    RLSAME                  NO
         LH    R10,4(R6)               PICK UP NEW REL PTR
         LH    R11,6(R6)               PICK UP NEW POS PTR
         LA    R6,4(R6)                STEP OVER 4 BYTES
RLSAME   LA    R6,4(R6)                TO NEXT RLD ITEM
         CR    R6,R8                   END OF RLD DATA
         BL    RLDNXT                  NO
         MVI   0(R7),X'FF'             SET TBL END INDIC
         ST    R7,CURRLD               SET NEW CURRENT ADDRESS
         L     R9,RL9                  GET RETURN ADDR
         BR    R9                      EXIT
         DROP  R7
*
*
*
****************************************************************
*                                                              *
* PROCESS CONTROL AND RLD RECORDS. THESE RECORDS CONTAIN BOTH  *
* RLD AND CONTROL INFORMATION, AND ARE PROCESSED BY BOTH       *
* THE RLDRECS AND CNTLRECS ROUTINES.                           *
*                                                              *
****************************************************************
*
CTRLRECS EQU   *                       CONTROL AND RLD RECORDS
         ST    R9,CR9                  SAVE RETURN ADDR
         BAL   R9,RLDRECS              PROCESS RLD DATA
         L     R6,BUFAD                RESET BUFFER ADDRESS
         BAL   R9,CNTLRECS             PROCESS CONTROL DATA
         L     R9,CR9                  GET RETURN ADDR
         BR    R9                      EXIT
*
*
*
*
*
*
****************************************************************
*                                                              *
* PROCESS COMPOSITE ESD RECORDS. DATA FROM THESE RECORDS IS    *
* PLACED IN THE SYMBOL TABLE, AND IS USED TO CREATE PROGRAM    *
* ENTRY STATEMENTS, AND TO IDENTIFY THE NAMES OF EXTERNAL      *
* SYMBOLS USED BY THE PROGRAM.                                 *
*                                                              *
****************************************************************
*
CESDREC  EQU   *                       CESD RECORD PROCESSING
         ST    R9,CES9                 SAVE RETURN
         L     R7,CURRSYM              GET SYMBOL TBL ADDR
         USING SYMTBL,R7
         LH    R10,4(R6)               GET ESD ID OF 1ST ITEM
         LH    R8,6(R6)                NBR BYTES OF ESD DATA
         SRL   R8,4                    COMPUTE NBR ENTRIES
         LA    R6,8(R6)                STEP TO 1ST RECORD ESD ITEM
CESDNXT  C     R7,ENDSYM               END OF TABLE
         BNL   SYMFULL                 YES, ERROR
         MVC   EXTSYM,0(R6)            SYMBOL NAME
         MVC   TYPSYM,8(R6)            TYPE
         NI    TYPSYM,X'0F'            CLEAR BITS 0-3
         MVC   SYMIND,8(R6)            INDICATOR BYTE
         NI    SYMIND,X'0F'            CLEAR BITS 4-7
         MVC   SYMARMOD,12(R6)         COPY RSECT/AMODE/RMODE BYTE
         TM    8(R6),X'14'             POSSIBLE ENTAB/SEGTAB
         BNO   CEMVAD                  NO
         TM    8(R6),X'03'             IS IT ENTAB/SEGTAB
         BNZ   CEMVAD                  NO
         MVC   TYPSYM(2),=X'0F00'      SHOW ENTAB/SEGTAB
CEMVAD   MVC   SYMADDR,9(R6)           ADDRESS OF SYMBOL
         MVC   SYMSEG,12(R6)           SEGMENT WHERE DEFINED
         MVC   SYMLENG,13(R6)          LENGTH OR LR ESD ID
         STCM  R10,3,SYMESDID          ESD ID
         MVC   PRT+5(8),EXTSYM         SYMBOL NAME
         MVC   PRT+20(2),=C'SD'        ASSUME SD
         CLI   TYPSYM,0                IS IT SD
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'LR'        ASSUME LR
         CLI   TYPSYM,3                IS IT LR
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'ER'        ASSUME ER
         CLI   TYPSYM,2                IS IT ER
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'PC'        ASSUME PC
         CLI   TYPSYM,4                IS IT PC
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'PR'        ASSUME PR
         CLI   TYPSYM,6                IS IT PR
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'CM'        ASSUME CM
         CLI   TYPSYM,5                IS IT CM
         BE    CEPIND                  YES
         MVC   PRT+20(2),=C'WX'        ASSUME WX
         CLI   TYPSYM,X'0A'            IS IT WX
         BE    CEPIND                  YES
         MVC   PRT+19(4),=C'NULL'      ASSUME NULL
         CLI   TYPSYM,7                IS IT NULL
         BE    CEPIND                  YES
         MVC   PRT+18(6),=C'E/STAB'    ASSUME ENTAB/SEGTAB
         CLI   TYPSYM,X'0F'            IS IT ENTAB/SEGTAB
         BE    CEPIND                  YES
         MVC   PRT+18(4),=C' ??: '     UNIDENTIFIABLE TYPE
         LA    R12,TYPSYM              @ TYPE
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+23(2),PRTABL        TYPE
CEPIND   LA    R12,SYMIND              @ INDICATOR
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+27(1),PRTABL        INDICATOR
         LA    R12,SYMADDR             @ SYMBOL ADDR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+30(6),PRTABL        SYMBOL ADDR
         LA    R12,SYMSEG              @ SEGMENT NBR
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+39(2),PRTABL        SEGMENT NBR
         CLI   TYPSYM,2                IS IT ER
         BE    CEESD                   YES
         CLI   TYPSYM,3                IS IT AN LR
         BNE   CENOTLR                 NO
         LA    R12,SYMLRID             @ LR ESD ID
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+43(4),PRTABL        LR ESD ID
         B     CEESD                   CONTINUE
CENOTLR  LA    R12,SYMLENG             @ LENGTH
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+49(6),PRTABL        SYMBOL LENGTH
CEESD    LA    R12,SYMESDID            @ ESD ID
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+57(4),PRTABL        ESD ID
         MVC   PRT+65(3),=C'YES'       RSECT
         TM    SYMARMOD,X'08'          YES
         BO    CEAMODE
         MVC   PRT+65(3),=C'NO '       RSECT
CEAMODE  MVC   PRT+72(3),=C'ANY'       AMODE
         TM    SYMARMOD,X'03'
         BO    CERMODE
         MVC   PRT+72(3),=C'31 '       31
         TM    SYMARMOD,X'20'
         BO    CERMODE
         MVC   PRT+72(3),=C'24 '       24
CERMODE  MVC   PRT+79(3),=C'ANY'       RMODE
         TM    SYMARMOD,X'04'
         BO    CECONT
         MVC   PRT+79(3),=C'24 '
CECONT   TM    PROCESS,X'80'           FOUND CSECT
         BO    CESDPT                  YES
         CLI   TYPSYM,0                SD
         BE    CECKCSB                 YES
         CLI   TYPSYM,4                PC
         BNE   CESDPT                  NO
CECKCSB  CLC   CSECT,BLANX             ANY CSECT NAME ENTERED
         BNE   CECKNM                  YES
         MVC   CSECT,EXTSYM            NAME TO CSECT
         B     CSGOTCS                 CONTINUE
CECKNM   CLI   TYPSYM,4                PC
         BE    CESDPT                  YES
         CLC   CSECT,EXTSYM            FOUND DESIRED NAME
         BNE   CESDPT                  NO
         TM    PROCESS,X'80'           ALREADY FOUND CSECT
         BZ    CSGOTCS                 NO
         MVC   PRT+15(2),=C'??'        SHOW DUPL
         B     CESDPT                  CONTINUE
CSGOTCS  OI    PROCESS,X'80'           SHOW CSECT FOUND
         MVC   PRT+15(2),=C'**'        FLAG ON PRINTOUT
         MVC   ESDID,SYMESDID          SAVE ESD ID FOUND
         MVC   START+1,SYMADDR         SAVE CSECT START ADDR
         MVC   LENGTH+1(3),SYMLENG     SAVE CSECT LENGTH
         L     R1,LENGTH               PICK UP LENGTH
         A     R1,START                COMPUTE CSECT END ADDR
         ST    R1,END                  SAVE CSECT END ADDR
         L     R11,LENGTH              TEXT LENGTH
         LA    R11,256(R11)            ADD FOR SAFETY
         GETMAIN R,LV=(11)             GET STORAGE FOR TEXT
         ST    R1,TXTSTRT              SAVE TEXT ADDR
         SH    R11,H256                DEDUCT SAFETY FACTOR
         AR    R1,R11                  TXT END ADDR
         ST    R1,TXTEND               SAVE TEXT END ADDR
CESDPT   BAL   R9,PRINT                PRINT
         LA    R7,L'SYMENT(R7)         TO NEXT TBL ENTRY LOCATION
         LA    R10,1(R10)              ADD 1 TO ESD COUNTER
         LA    R6,16(R6)               TO NEXT ESD ITEM IN INPUT
         BCT   R8,CESDNXT              LOOP THRU INPUT RECORD
         MVI   0(R7),X'FF'             SET END OF TABLE INDIC
         ST    R7,CURRSYM              SAVE NEXT TABLE ADDR
         L     R9,CES9                 GET RETURN ADDR
         BR    R9                      EXIT
         DROP  R7
*
*
*
****************************************************************
*                                                              *
* ISSUE BLDL FOR THE MAIN MODULE, AND PRINT MODULE RELATED INFO*
*                                                              *
****************************************************************
*
BLDL     EQU   *                       ISSUE BLDL AND PRINT INFO
         ST    R9,BL9                  SAVE RETURN ADDR
ISSBLDL  BLDL  SYSLIB,BLDLIST          ISSUE BLDL
         LTR   R15,R15                 ANY ERRORS
         BNZ   MISSMEM                 YES
         LA    R1,MEMBER+35            END OF BASIC PORTION
         TM    ATTR2,X'10'             SSI PRESENT
         BZ    BLREFA1                 NO
         LA    R1,4(R1)                ADD FOR SSI
         LR    R15,R1                  COPY FOR HALF WORD CHECK   ACF
         SRL   R15,1                   SHIFT OUT LAST BIT         ACF
         SLL   R15,1                   AND BACK AGAIN             ACF
         CR    R1,R15                  CHECK AGAINST ORIGINAL     ACF
         BE    BLREFA1                 NO ADJUSTMENT NECESSARY    ACF
         LA    R1,1(R1)                NOW PTS TO HALFWORD BNDRY  ACF
BLREFA1  TM    ALIASIND,X'80'          ALIAS
         BZ    BLREFA2                 NO
         LA    R1,11(R1)               ADD FOR ALIAS
BLREFA2  TM    ATTR1A,X'04'            SCATTER FORMAT
         BZ    BLREFA3                 NO
         LA    R1,8(R1)                ADD FOR SCATTER
BLREFA3  MVC   AUTHLEN(2),0(R1)        AUTH LENGTH AND CODE
         TM    ATTR2,X'10'             SSI PRESENT
         BZ    BLCKALI                 NO
         LA    R1,MEMBER+35            END OF BASIC PORTION
         TM    ATTR1A,X'04'            SCATTER FORMAT
         BZ    BLSSI2                  NO
         TM    ALIASIND,X'80'          ALIAS
         BO    BLFMTED                 YES, NO REFORMAT NEEDED
         LA    R1,8(R1)                NO, STEP PAST SCATTER SECTION
         B     BLMVSSI                 GO MOVE SSI
BLSSI2   TM    ALIASIND,X'80'          ALIAS
         BZ    BLMVSSI                 NO
         LA    R1,11(R1)               YES, STEP PAST ALIAS SECTION
BLMVSSI  EQU   *                                                  ACF
         LR    R15,R1                  COPY FOR HALF WORD CHECK   ACF
         SRL   R15,1                   SHIFT OUT LAST BIT         ACF
         SLL   R15,1                   AND BACK AGAIN             ACF
         CR    R1,R15                  CHECK AGAINST ORIGINAL     ACF
         BE    BLMVSSI1                NO ADJUSTMENT NECESSARY    ACF
         LA    R1,1(R1)                NOW PTS TO HALFWORD BNDRY  ACF
BLMVSSI1 MVC   SSI,0(R1)               MOVE SSI DATA
BLCKALI  TM    ALIASIND,X'80'          ALIAS
         BO    BLASC                   YES
         B     BLFMTED                 FINISHED
BLASC    TM    ATTR1A,X'04'            SCATTER FORMAT
         BO    BLFMTED                 YES, NO REFORMAT NEEDED
         MVC   ALMEM,MEMBER+38         MOVE ALIAS MEMBER
         MVC   ALEPA(3),MEMBER+35      YES, MOVE ALIAS DATA
BLFMTED  MVC   PRT(26),=C'DIRECTORY INFO FOR MODULE '
         MVC   PRT+27(8),MEMBER        MEMBER NAME TO PRINT
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(14),=C'TTR OF MODULE='
         LA    R12,TTRMOD              @ TTR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+24(6),PRTABL        TTR TO PRINT
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(18),=C'CONCATENATION NBR='
         LA    R12,CCAT                @ CONCATENATION NBR
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+28(2),PRTABL        CONCATENATION NBR
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(12),=C'ALIAS INDIC='
         LA    R12,ALIASIND            @ ALIAS INDICATOR
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+22(2),PRTABL        ALIAS INDICATOR
         TM    ALIASIND,X'80'          IS IT AN ALIAS
         BZ    BLALPRT                 NO
         MVC   PRT+50(13),=C'*** ALIAS ***'
BLALPRT  BAL   R9,PRINT                PRINT
         MVC   PRT+10(18),=C'TTR, 1ST TXT RECD='
         LA    R12,TTR1TXT             @ TTR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+28(6),PRTABL        TTR OF 1ST TXT BLOCK
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(25),=C'TTR OF NOTE/SCATTER LIST='
         LA    R12,TTRNS               @ TTR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+35(6),PRTABL        TTR OF NOTE/SCATTER
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(17),=C'NBR NOTE ENTRIES='
         LA    R12,NNOTE               @ NBR NOTES
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+27(2),PRTABL        NBR NOTE ENTRIES
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(13),=C'ATTRIBUTES 1='
         LA    R12,ATTR1A              @ ATTRIBUTES
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+23(4),PRTABL        ATTRIBUTES 1
         TM    ATTR1A,X'80'            RENT
         BZ    BLAT1A                  NO
         MVC   PRT+30(4),=C'RENT'
BLAT1A   TM    ATTR1A,X'40'            REUS
         BZ    BLAT1B                  NO
         MVC   PRT+35(4),=C'REUS'
BLAT1B   TM    ATTR1A,X'20'            OVLY
         BZ    BLAT1C                  NO
         MVC   PRT+40(4),=C'OVLY'
BLAT1C   TM    ATTR1A,X'10'            TEST
         BZ    BLAT1D                  NO
         MVC   PRT+45(4),=C'TEST'
BLAT1D   TM    ATTR1B,X'08'            OL
         BZ    BLAT1E                  NO
         MVC   PRT+50(2),=C'OL'
BLAT1E   TM    ATTR1A,X'04'            SCTR
         BZ    BLAT1F                  NO
         MVC   PRT+53(4),=C'SCTR'
BLAT1F   TM    ATTR1A,X'02'            EXEC
         BZ    BLAT1G                  NO
         MVC   PRT+58(4),=C'EXEC'
BLAT1G   TM    ATTR1B,X'01'            REFR
         BZ    BLAT1PRT                NO
         MVC   PRT+63(4),=C'REFR'
BLAT1PRT BAL   R9,PRINT                PRINT
         MVC   PRT+10(13),=C'TOTAL LENGTH='
         LA    R12,TOTVIRT             @ TOTAL LENGTH
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+23(6),PRTABL        TOTAL LENGTH OF MODULE
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(23),=C'LENGTH OF 1ST TXT RECD='
         LA    R12,LENG1               @ 1ST TXT LENG
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+33(4),PRTABL        LENGTH OF 1ST TXT BLOCK
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(11),=C'E. P. ADDR='
         LA    R12,LKEPA               @ E.P. ADDR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+21(6),PRTABL        E.P. ADDR
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(13),=C'ATTRIBUTES 2='
         LA    R12,ATTR2               @ ATTRIBUTES 2
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+23(2),PRTABL        ATTRIBUTES 2
         TM    ATTR2,X'20'             PAGE ALIGNMENT
         BZ    BLAT3A                  NO
         MVC   PRT+30(16),=C'PAGE ALIGN REQRD'
BLAT3A   TM    ATTR2,X'10'             SSI PRESENT
         BZ    BLAT3PRT                NO
         MVC   PRT+50(11),=C'SSI PRESENT'
BLAT3PRT BAL   R9,PRINT                PRINT
         TM    ATTR1A,X'04'            SCATTER FORMAT
         BZ    BLFAL                   NO
         MVC   PRT+10(8),=C'SCTRLEN='
         LA    R12,SCTRLEN             @ SCATTER LIST LENGTH
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+18(4),PRTABL        SCATTER LIST LENGTH
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(18),=C'TRANSLATE TBL LEN='
         LA    R12,TTLEN               @ TRANS TBL LEN
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+28(4),PRTABL        TRANSLATION TABLE LENGTH
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(18),=C'ESD ID OF 1ST TXT='
         LA    R12,SCESDID             @ ESD ID
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+28(4),PRTABL        ESD ID OF 1ST TXT
         BAL   R9,PRINT                PRINT
         MVC   PRT+10(23),=C'ESD ID CONTAINING E.P.='
         LA    R12,SCEPESD             @ ESD ID
         BAL   R9,HEXPRT2              CONVERT
         MVC   PRT+33(4),PRTABL        @ ESD ID OF CSECT CONTAINING E.P
         BAL   R9,PRINT                PRINT
BLFAL    TM    ALIASIND,X'80'          ALIAS
         BZ    BLFSSI                  NO
         MVC   PRT+10(28),=C'E.P. ADDRESS OF THIS MEMBER='
         LA    R12,ALEPA               @ E.P. ADDR
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT+38(6),PRTABL        E.P. ADDR
         BAL   R9,PRINT
         MVC   PRT+10(17),=C'REAL MEMBER NAME='
         MVC   PRT+27(8),ALMEM         REAL MEMBER NAME
         BAL   R9,PRINT
BLFSSI   TM    ATTR2,X'10'             ANY SSI INFO
         BZ    BLAUTHC                 NO
         MVC   PRT+10(9),=C'SSI INFO='
         LA    R12,SSI                 @ SSI INFO
         BAL   R9,HEXPRT4              CONVERT
         MVC   PRT+19(8),PRTABL        SSI INFO
         BAL   R9,PRINT                PRINT
BLAUTHC  MVC   PRT+10(10),=C'AUTH CODE='
         LA    R12,AUTHCOD             @ AUTH CODE
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+20(2),PRTABL        AUTH CODE
         BAL   R9,PRINT                PRINT
BLMODES  MVC   PRT+10(9),=C'AMODE=24 '
         LA    R9,3                    BIT MASK FOR TM OF MAIN MEMBER
         TM    ALIASIND,X'80'          ALIAS
         BZ    *+8
         LA    R9,12                   BIT MASK FOR TM OF ALIAS
         EX    R9,EXTM                 AMODE OF MAIN/ALIAS
         BZ    BLMODEP
         MVC   PRT+16(3),=C'ANY'
         BO    BLMODEP
         MVC   PRT+16(3),=C'31 '
BLMODEP  BAL   R9,PRINT                PRINT
         MVC   PRT+10(9),=C'RMODE=24 '
         TM    ATTR2A,X'10'            TEST FOR ANY RESIDENCE MODE
         BZ    *+10                    BR ROUND NEXT MVC INSTR
         MVC   PRT+16(3),=C'ANY'       MOVE IN ANY
         BAL   R9,PRINT                PRINT
         TM    ALIASIND,X'80'          ALIAS
         BZ    BLXIT                   NO
         MVC   PRT+5(38),=C'***** REAL MEMBER DIRECTORY INFO *****'
         MVI   PCC,C'0'                DOUBLE SPACE
         BAL   R9,PRINT                PRINT
         MVC   MEMBER,ALMEM            REAL MEMBER NAME TO LIST
         B     ISSBLDL                 DO OVER FOR REAL MEMBER
BLXIT    L     R9,BL9                  GET RETURN ADDR
         BR    R9                      EXIT
*
EXTM     TM    ATTR2A,X'00'            AMODE OF MAIN X'03', ALIAS X'0C'
*
*
****************************************************************
*                                                              *
* CREATE PRINTABLE HEX FROM HEX. ON ENTRY, REG 12 CONTAINS THE *
* ADDRESS OF THE DATA TO BE REFORMATTED. ENTRY POINT USED      *
* DETERMINES THE SIZE OF THE FIELD. OUTPUT DATA IS PLACED IN   *
* THE PRTABL FIELD, 2 CHARACTERS PER BYTE.                     *
*                                                              *
****************************************************************
*
HEXPRT   EQU   *                       HEX TO PRINTABLE ROUTINE
HEXPRT1  UNPK  PRTABL(3),0(2,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT2  UNPK  PRTABL(5),0(3,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT3  UNPK  PRTABL(7),0(4,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT4  UNPK  PRTABL(9),0(5,R12)      UNPACK HEX
HEXCLTR  MVZ   PRTABL(8),XZROS         CLEAR FOR TRANSLATE
         TR    PRTABL(8),TRTBL         MAKE PRINTABLE
         BR    R9                      EXIT
*
*
*
****************************************************************
*                                                              *
* PRINT USING SYSPRINT.                                        *
*                                                              *
****************************************************************
*
PRINT    EQU   *                       PRINT ROUTINE
         TM    SYSPRINT+48,X'10'       IS SYSPRINT OPEN
         BNO   TPUT                    NO
         PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
CLRPRT   MVC   PRT,BLANX               CLEAR PRINT LINE
         AP    LINECT,P1               INCR LINE COUNTER
         CLI   PCC,C' '                SINGLE SPACED
         BE    SETSGL                  YES
         AP    LINECT,P1               INCR LINE COUNTER
         CLI   PCC,C'0'                DOUBLE SPACED
         BE    SETSGL                  YES
         AP    LINECT,P1               INCR LINE COUNTER
         CLI   PCC,C'-'                TRIPLE SPACED
         BE    SETSGL                  YES
         ZAP   LINECT,P0               NO, MUST BE NEW PAGE
SETSGL   MVI   PCC,C' '                SET SINGLE SPACING
         CP    LINECT,P58              PAST END OF PAGE
         BH    NEWPAGE                 YES
         BR    R9                      EXIT
NEWPAGE  MVI   PCC,C'1'                SET SKIP TO HOF
         ZAP   LINECT,P0               RESET LINE COUNTER
         BR    R9                      EXIT
TPUT     DS    0H
         TPUT  PRT,L'PRT
         BR    R9                      EXIT
*
*
****************************************************************
*                                                              *
* PROCESS THE CONTROL CARD CONTAINING MODULE NAME AND CSECT.   *
* THIS MUST BE THE FIRST CARD IN THE SYSIN DECK.               *
*                                                              *
****************************************************************
*
GETCTL   EQU   *                       EXTRACT DESIRED MEMBER/CSECT
         SPACE 1                                                    RH
         RDJFCB SYSLIB                   GET MEMBER NAME FROM JFCB  RH
         LTR   R15,R15                   RDJFCB SUCCESSFUL ?        RH
         BNZ   GETCTL2                   NO, GO READ SYSIN          RH
         SPACE 1                                                    RH
         CLI   JFCBMBR,C' '              ANY MEMBER NAME ?          RH
         BE    GETCTL2                   NO, GO READ SYSIN          RH
         SPACE 1                                                    RH
         MVC   MEMBER,JFCBMBR          MOVE MEMBER TO BLDL LIST     RH
         BR    R9                      RETURN TO CALLER             RH
         SPACE 1                                                    RH
GETCTL2  DS    0H                                                   RH
         TM    SYSIN+48,X'10'          DID SYSIN OPEN O.K.          RH
         BZ    NOCTL                   NO                           RH
         GET   SYSIN                   READ THE CONTROL CARD
         LA    R12,72                  LENGTH OF CONTROL CARD
         LA    R11,8                   MAX LENGTH OF MEMBER NAME
         LA    R10,MEMBER              @ MEMBER NAME FIELD
CKBLK1   CLI   0(R1),C' '              CONTROL BYTE IS BLANK
         BNE   GCMEMOV                 NO, GO MOVE MEMBER NAME
         LA    R1,0(R1)                TO NEXT CONTROL BYTE
         BCT   R12,CKBLK1              SUBTRACT 1 FROM REMAINING LENGTH
         B     GCEND                   ALL BLANKS, EXIT
GCMEMOV  MVC   0(1,R10),0(R1)          CONTROL BYTE TO MEMBER NAME
         LA    R1,1(R1)                TO NEXT CONTROL BYTE
         BCTR  R12,R0                  SUBTRACT 1 FROM LENGTH
         LA    R10,1(R10)              TO NEXT NAME BYTE
         BCTR  R11,R0                  SUBTRACT 1 FROM LENGTH
         CLI   0(R1),C' '              GOT A BLANK
         BE    GCSETUP2                YES
         CLI   0(R1),C','              GOT A COMMA
         BE    GCSETUP2                YES
         LTR   R12,R12                 END OF CONTROL DATA
         BE    GCEND                   YES
         LTR   R11,R11                 NAME FULL
         BNZ   GCMEMOV                 NO, CONTINUE
         B     NAMEOV8                 YES, NAME TOO LONG
GCSETUP2 LA    R11,8                   CSECT NAME MAX LENGTH
         LA    R10,CSECT               @ CSECT NAME FIELD
GCSTEP2  LA    R1,1(R1)                STEP PAST BLANK
         BCT   R12,GCHKBK2             CHECK NEXT FOR BLANK
         B     GCEND                   END OF SCAN
GCHKBK2  CLI   0(R1),C' '              CONTROL DATA IS BLANK
         BE    GCSTEP2                 YES
GCCSMOV  MVC   0(1,R10),0(R1)          CONTROL BYTE TO CSECT NAME
         LA    R1,1(R1)                TO NEXT CONTROL BYTE
         LA    R10,1(R10)              TO NEXT NAME BYTE
         BCTR  R12,R0                  DEDUCT 1 FROM CONTROL LENGTH
         BCTR  R11,R0                  DEDUCT 1 FROM NAME LENGTH
         LTR   R12,R12                 ANY CONTROL BYTES LEFT
         BZ    GCEND                   NO
         CLI   0(R1),C' '              NEXT CONTROL BYTE BLANK
         BE    GCEND                   YES
         LTR   R11,R11                 ANY NAME BYTES LEFT
         BNZ   GCCSMOV                 YES, LOOP
         B     NAMEOV8                 NAME TOO LONG, ERROR
GCEND    CLC   MEMBER,BLANX            ANY MEMBER NAME FOUND
         BE    NOMBR                   NO
         BR    R9                      EXIT
*
*
*
****************************************************************
*                                                              *
* MISCELLANEOUS ERROR MESSAGES.                                *
*                                                              *
****************************************************************
*
ERRORS   EQU   *                       ERROR END MESSAGES
NOCTL    MVC   PRT(L'NOCNTRL),NOCNTRL  NO CONTROL INFO MESSAGE
         B     ERREND                  GO PRINT
NAMEOV8  MVC   PRT(L'OVER8),OVER8      NAME OVER 8 MESSAGE
         B     ERREND                  GO PRINT
NOMBR    MVC   PRT(L'NOMEM),NOMEM      MISSING MEMBER NAME MESSAGE
         B     ERREND                  GO PRINT
MISSMEM  MVC   PRT(L'INVMEM),INVMEM    MEMBER NOT IN PDS MESSAGE
         B     ERREND                  GO PRINT
MISSCS   MVC   PRT(L'INVCSECT),INVCSECT CSECT NOT IN MEMBER MESSAGE
         B     ERREND                  GO PRINT
SYMFULL  MVC   PRT(L'FULLSYM),FULLSYM  FULL SYM TBL MSG
         B     ERREND                  GO PRINT
RLDFULL  MVC   PRT(L'FULLRLD),FULLRLD  RLD TBL FULL MSDG
         B     ERREND                  GO PRINT
ERREND   DS    0H
         TPUT  PRT,L'PRT
         BAL   R9,PRINT                GO PRINT MESSAGE
         CLOSE (SYSPRINT)
         ABEND 888,DUMP                ABEND
*
*
*
*
****************************************************************
*                                                              *
* COUNT THE ENTRIES IN THE ESD TABLE WHICH WILL RESULT IN      *
* ENTRIES IN THE LABEL TABLE.                                  *
*                                                              *
****************************************************************
ENDINIT  EQU   *                       *** FINAL PROCESSING ***
*
         L     R6,SYMTBAD              GET @ ESD TABLE
         USING SYMTBL,R6
         LA    R7,4                    INITIAL LABEL COUNT VALUE
SYMCNT   CLI   0(R6),X'FF'             END OF ESD TBL
         BE    RLDCNT                  YES
         CLI   TYPSYM,3                IS ESD AN LR ENTRY
         BNE   SCSTP                   NO, IGNORE
         CLC   SYMADDR,START+1         ADDR BELOW DESIRED CSECT
         BL    SCSTP                   YES, IGNORE
         CLC   SYMADDR,END+1           ADDR ABOVE DESIRED CSECT
         BH    SCSTP                   YES, IGNORE
         LA    R7,1(R7)                ADD TO LABEL COUNT
         MVC   SYMLENG,=C'***'         FLAG AS USABLE
SCSTP    LA    R6,L'SYMENT(R6)         TO NEXT ESD ENTRY
         B     SYMCNT
         DROP  R6
*
****************************************************************
*                                                              *
* COUNT THE ENTRIES IN THE RLD TABLE WHICH WILL RESULT IN      *
* ENTRIES IN THE LABEL TABLE.                                  *
*                                                              *
****************************************************************
*
RLDCNT   L     R6,RLDTBL               GET RLD TBL ADDR
         USING RLDTBLD,R6
RLDCEND  CLI   0(R6),X'FF'             END OF RLD TABLE
         BE    LBLGET                  YES
         CLC   RLDRP,ESDID             ESDID SAME AS DESIRED CSECT
         BNE   RLDC1                   NO, EXTRN REF
         LA    R7,1(R7)                ADD 1 TO LABEL COUNT
RLDC1    LA    R7,1(R7)                ADD 1 TO LABEL COUNT
         LA    R6,L'RLDENT(R6)         TO NEXT RLD ENTRY
         B     RLDCEND                 LOOP THRU RLD TBL
         DROP  R6
LBLGET   MH    R7,LBLLGTH              COMPUTE LABEL TABLE SIZE
         L     R1,LENGTH               GET CSECT LENGTH
         SRL   R1,2                    DIVIDE BY 4
         MH    R1,LBLLGTH              TIMES LABEL ENTRY LENGTH
         AR    R7,R1                   TOTAL LABEL TABLE LENGTH
*
****************************************************************
*                                                              *
* CREATE INITIAL ENTRIES IN THE LABEL TABLE USING DATA FROM    *
* THE ESD AND RLD TABLES.                                      *
*                                                              *
****************************************************************
*
         GETMAIN R,LV=(7)              GET LABEL TBL STORAGE
         ST    R1,LBLTBL               SAVE LABEL TBL ADDR
         ST    R1,CURRLBL              SAVE CURRENT LABEL ADDR
         AR    R1,R7                   COMPUTE LABEL TBL END ADDR
         ST    R1,ENDLBL               SAVE END OF LBL TBL ADDR
         L     R6,LBLTBL               GET @ LABEL TABLE
         USING LABELD,R6
         L     R7,SYMTBAD              GET ESD TBL ADDR
         USING SYMTBL,R7
         CLC   LKEPA,XZROS             E.P. ADDR ZERO
         BE    LRENTS                  YES
         SR    R1,R1                   CLEAR WORK
         ICM   R1,7,LKEPA              PICK UP E.P. ADDR
         S     R1,START                RELATIVIZE IN CSECT
         BM    LRENTS                  NEG, ERROR
         STCM  R1,7,LBLADR             SAVE OFFSET
         MVI   LBLTYP,C'L'             SET LABEL TYPE IN ENTRY
         LA    R12,LBLADR              POINT TO OFFSET
         BAL   R9,HEXPRT3              CONVERT TO PRINTABLE
         MVI   LBLNAME,C'A'            1ST CHAR OF LABEL IS 'A'
         MVC   LBLNAME+1(6),PRTABL     END OF LABEL IS OFFSET
         LA    R6,L'LABEL(R6)          TO NEXT LABEL ENTRY
         ST    R6,CURRLBL              SAVE IT'S ADDRESS
LRENTS   CLI   0(R7),X'FF'             END OF ESD TBL
         BE    RLDLBLS                 YES
         CLC   SYMLENG,=C'***'         THIS DESIRED LR ENTRY
         BE    GOTLR                   YES
LRESTP   LA    R7,L'SYMENT(R7)         TO NEXT ESD ENTRY
         B     LRENTS                  LOOP THRU ESD TBL
GOTLR    SR    R12,R12                 CLEAR WORK REG
         ICM   R12,7,SYMADDR           GET SYMBOL ADDR
         S     R12,START               RELATIVIZE IN CSECT
         STCM  R12,7,LBLADR            SAVE RELATIVE ADDR
         MVC   LBLNAME,EXTSYM          SYMBOL NAME TO OUTPUT
         MVI   LBLTYP,C'L'             SHOW LABEL ENTRY
         LA    R6,L'LABEL(R6)          TO NEXT LABEL ENTRY
         ST    R6,CURRLBL              SAVE NEXT LABEL ENTRY ADDR
         B     LRESTP                  CONTINUE ESD PROCESSING
         DROP  R7
RLDLBLS  L     R7,RLDTBL               GET RLD TBL ADDR
         USING RLDTBLD,R7
RLDLBND  CLI   0(R7),X'FF'             END OF RLD TABLE
         BE    PHASE1                  YES
         CLC   RLDRP,ESDID             RLD ESDID = DESIRED CSECT ESDID
         BE    INTREFS                 YES, INTERNAL ADCON
         CLI   RLDTYPE,1               VCON
         BE    EXTREFS                 YES
         CLI   RLDTYPE,8               IS IT UNRESOLVED
         BE    EXTREFS                 YES
RLLSTP   LA    R7,L'RLDENT(R7)         TO NEXT RLD ENTRY
         B     RLDLBND                 LOOP THRU RLD TABLE
EXTREFS  CLI   RLDESDTP,X'0A'          W-CON
         BNE   VCONLBL                 NO, BUILD V-CON
         MVI   LBLTYP,C'W'             SHOW W-CON
         B     FXTLBL                  CONTINUE LABEL ENTRY FORMAT
VCONLBL  MVI   LBLTYP,C'V'             SHOW V-CON
FXTLBL   MVC   LBLNAME,RLDNAME         NAME TO LABEL ENTRY
FINLBL   MVC   LBLADR,RLDADDR          ADDRESS TO LABEL ENTRY
         MVC   LBLLEN,RLDLEN           LENGTH TO LABEL ENTRY
         LA    R6,L'LABEL(R6)          TO NEXT LABEL TBL ENTRY
         ST    R6,CURRLBL              SAVE CURRENT LABEL TBL ADDR
         B     RLLSTP                  CONTINUE LABEL TABLE BUILD
INTREFS  CLI   RLDTYPE,0               A-CON
         BNE   RLLSTP                  NO, IGNORE
         MVI   LBLTYP,C'L'             SHOW LABEL ENTRY TYPE
         SR    R12,R12                 CLEAR WORK REG
         ICM   R12,7,RLDADDR           GET RLD ADDR
         A     R12,TXTSTRT             FIND LOC IN TEXT
         MVC   LBLADR,1(R12)           MOVE TO LABEL ENTRY
         CLI   RLDLEN,4                ADCON IS 4-BYTES
         BE    INTGOTL                 YES
         MVC   LBLADR,0(R12)           TEXT TO LABEL ENTRY
         CLI   RLDLEN,3                ADCON IS 3-BYTES
         BE    INTGOTL                 YES
         MVC   LBLADR+1(2),0(R12)      TEXT TO LABEL ENTRY
         MVI   LBLADR,0                CLEAR 1ST BYTE
         CLI   RLDLEN,2                ADCON IS 2-BYTES
         BE    INTGOTL                 YES
         XC    LBLADR,LBLADR           CLEAR LABEL ENTRY ADDR
         MVC   LBLADR+2(1),0(R12)      TEXT TO LABEL ENTRY
INTGOTL  SR    R12,R12                 CLEAR WORK
         ICM   R12,7,LBLADR            GET ADDRESS
         S     R12,START               RELATIVIZE IN CSECT
         BM    RLLSTP                  NEGATIVE, IGNORE
         STCM  R12,7,LBLADR            STORE RELATIVE ADDRESS
         LA    R12,LBLADR              POINT TO ADDRESS
         BAL   R9,HEXPRT3              CONVERT TO PRINTABLE
         MVI   LBLNAME,C'A'            SET LABEL ENTRY TYPE
         MVC   LBLNAME+1(6),PRTABL     LOW ORDER NAME POSITIONS
         MVI   LBLNAME+7,C' '          CLEAR LAST NAME BYTE
         MVC   L'LABEL(L'LABEL,R6),0(R6) COPY THIS ENTRY TO NEXT
         MVI   LBLLEN,0                SET LENGTH = 0
         LA    R6,L'LABEL(R6)          STEP TO NEXT
         MVI   LBLTYP,C'A'             SHOW TYPE
         B     FINLBL                  FINISH LABEL
         DROP  R6
         DROP  R7
*
****************************************************************
*                                                              *
* LOAD AND EXECUTE PHASE 1: DISASM1                            *
*                                                              *
****************************************************************
*
PHASE1   MVC   0(3,R6),HIVAL           SET END OF LABEL TABLE  FIX***
*        TM    LOADLIB+48,X'10'        LOADLIB DD CARD ENTERED
*        BZ    NODCB1                  NO
*        LOAD  EP=DISASM1,DCB=LOADLIB  LOAD PHASE1
*        B     EXEC1                   CONTINUE
*NODCB1   LOAD  EP=DISASM1              LOAD PHASE1
*EXEC1    LR    R15,R0                  COPY E.P. ADDRESS
*         LA    R1,=A(COMMPARM)         @ PARAMETER LIST
*         BALR  R14,R15                 LINK TO PHASE1
*         DELETE EP=DISASM1             DELETE AFTER USE
****************************************************************
*        CALLING SEQUENCE CODE
         CALL  DISASM1,(COMMPARM)
****************************************************************
         CLI   USERR,0                 ANY ERRORS
         BNE   CLOSES                  YES, STOP THE RUN
*
****************************************************************
*                                                              *
* PRINT THE LABEL TABLE AFTER PHASE 1 COMPLETION.              *
*                                                              *
****************************************************************
*
         MVC   PRTLINE(LPH1LBL),PH1LBL HEADER 1                     RH
         BAL   R9,PRINT                PRINT IT
         MVC   PRTLINE(LPH1HDR),PH1HDR HEADER 2                     RH
         BAL   R9,PRINT                PRINT IT
         L     R6,LBLTBL               GET LABEL TABLE ADDR
         USING LABELD,R6
LPEND    C     R6,CURRLBL              END OF TABLE       FIX****
         BNL   FREESTRG                YES                FIX****
         LA    R12,LBLADR              @ LABEL ADDRESS
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT(6),PRTABL           ADDRESS TO PRINT
         MVC   PRT+9(1),LBLTYP         TYPE TO PRINT
         MVC   PRT+16(8),LBLNAME       SYMBOL TO PRINT
         CLI   LBLLEN,0                ANY LENGTH
         BE    LTPPRT                  NO
         LA    R12,LBLLEN              GET @ LENGTH
         BAL   R9,HEXPRT1              CONVERT
         MVC   PRT+26(2),PRTABL        LENGTH TO PRINT
LTPPRT   BAL   R9,PRINT                PRINT TBL ENTRY
         LA    R6,L'LABEL(R6)          TO NEXT ENTRY
         B     LPEND                   LOOP THRU TABLE
FREESTRG L     R12,RLDTBL              @ RLD TABLE
         FREEMAIN R,A=(12),LV=RLDTABL  FREE RLD TABLE
*
****************************************************************
*                                                              *
* PRINT THE TEXT FOR THE CSECT TO BE DISASSEMBLED,             *
* IF HE  WANTS  IT,                                            *    RH
*                                                              *
****************************************************************
*
         CLI   TEXTFLAG,1         DOES HE WANT A HEX DUMP ?         RH
         BNE   NOHEX              NO, BYPASS SOME CODE              RH
         MVI   PCC,C'1'                SET SKIP TO HEAD
         MVC   PRT+50(19),=C'***** T E X T *****'
         BAL   R9,PRINT                PRINT TEXT HEADER
         MVI   PCC,C'0'                SET DOUBLE SPACING
         L     R11,TXTSTRT             GET TEXT START ADDRESS
PNEXLIN  LA    R10,2                   GROUPS PER LINE
         LA    R12,POFSET+1            @ OFFSET
         BAL   R9,HEXPRT3              CONVERT
         MVC   PRT(6),PRTABL           OFFSET TO PRINT
         MVC   PRT+85(32),0(R11)       TEXT TO PRINT
         TR    PRT+85(32),PRTCHAR      TRANSLATE TO PRINTABLE
         LA    R8,PRT+9                @ 1ST PRINT WORD
         LA    R7,4                    4 WORDS PER GROUP
PGRP     LA    R12,0(R11)              @ TEXT WORD
         BAL   R9,HEXPRT4              CONVERT
         MVC   0(8,R8),PRTABL          TEXT TO PRINT WORD
         LA    R11,4(R11)              TO NEXT TEXT WORD
         LA    R8,9(R8)                TO NEXT PRINT LOC
         BCT   R7,PGRP                 DO 4 TIMES
         LA    R8,2(R8)                SPACE BETWEEN GROUPS
         LA    R7,4                    FOR 2ND GROUP
         BCT   R10,PGRP                DO 4 MORE TIMES
         BAL   R9,PRINT                PRINT THE LINE
         L     R9,POFSET               GET OFFSET
         LA    R9,32(R9)               ADD 32 BYTES
         ST    R9,POFSET               UPDATE OFFSET
         C     R11,TXTEND              END OF TEXT
         BL    PNEXLIN                 NO, CONTINUE
         SPACE 1                                                    RH
NOHEX    DS    0H                                                   RH
         MVI   PCC,C'1'                SET SKIP TO HEAD
         MVC   WORKREC,BLANX           CLEAR OUTPUT RECORD
         MVC   NAME,CSECT              CSECT NAME
*
****************************************************************
*                                                              *
* CREATE THE INITIAL CSECT INSTRUCTION, AND ANY ENTRY STATE-   *
* MENTS WHICH MAY BE INDICATED BY ESD TABLE ENTRIES.           *
*                                                              *
****************************************************************
*
         SPACE 3                                                    RH
********************************************************************RH*
*                                                                   RH*
*        ROUTINE TO PRINT HEADING.                                  RH*
*                                                                   RH*
********************************************************************RH*
         SPACE 2                                                    RH
L10HEAD  DS    0H                                                   RH
         ZAP   LINECT,P0          SET LINE COUNTER TO 0             RH
         AP    PAGECTR,P1         ADD 1 TO PAGE COUNTER             RH
         L     R1,PRINTDCB             @ SYSPRINT DCB               RH
         TM    48(R1),X'10'            IS SYSPRINT OPEN             RH
         BNO   CLRPRT1                 NO                           RH
         PUT   (1),PRTLINE        SKIP TO NEW PAGE                  RH
         SPACE 1                                                    RH
         MVC   PAGENO,=X'40202020'      EDIT PAGE NUMBER            RH
         ED    PAGENO,PAGECTR           *                           RH
         MVC   PRT,HEAD1                MOVE HEADING TO PRINT       RH
         MVI   PCC,C' '           PRINT, SPACE 1                    RH
         AP    LINECT,P1          ADD 1 TO LINE COUNT               RH
         L     R1,PRINTDCB             @ SYSPRINT DCB               RH
         PUT   (1),PRTLINE        PRINT HEADING                     RH
         MVI   PRT,C' '           CLEAR PRINT LINE                  RH
         MVC   PRT+1(131),PRT     CLEAR PRINT LINE                  RH
         MVI   PCC,C'0'           SPACE 2 LINES                     RH
         SPACE 1                                                    RH
CLRPRT1  DS    0H                                                   RH
*        BR    R9                       RETURN TO CALLER
*                                                                   RH*
********************************************************************RH*
*                                                                   RH*
         MVC   MNEMONIC,=C'CSECT'      SET MNEMONIC
         BAL   R9,WRTOUT               WRITE OUTPUT RECORD
         AP    STMTCTR,P1               ADD 1 TO STATEMENT COUNTER  RH
         MVC   PSTMT-2(6),=X'402020202120' EDIT STATEMENT COUNTER   RH
         ED    PSTMT-2(6),STMTCTR       *                           RH
         BAL   R9,PRINT                AND PRINT IT
         L     R7,SYMTBAD              GET ESD TBL ADDR
         USING SYMTBL,R7
LREFSS   CLI   0(R7),X'FF'             END OF ESD TBL
         BE    CKLDLB                  YES
         CLC   SYMLENG,=C'***'         THIS DESIRED LR ENTRY
         BE    MAKEXT                  YES
ESDTSTP  LA    R7,L'SYMENT(R7)         TO NEXT ESD ENTRY
         B     LREFSS                  LOOP THRU ESD TBL
MAKEXT   MVC   MNEMONIC,=C'ENTRY'      MNEMONIC IS ENTRY
         MVC   OPNDS(8),EXTSYM         SYMBOL TO OPERAND
         BAL   R9,WRTOUT               WRITE OUTPUT RECORD
         AP    STMTCTR,P1               ADD 1 TO STATEMENT COUNTER  RH
         MVC   PSTMT-2(6),=X'402020202120' EDIT STATEMENT COUNTER   RH
         ED    PSTMT-2(6),STMTCTR       *                           RH
         BAL   R9,PRINT                AND PRINT IT
         B     ESDTSTP                 CONTINUE ESD PROCESSING
         DROP  R7
         DROP  R6
CKLDLB   L     R12,SYMTBAD             @ ESD SYMBOL TABLE
*
****************************************************************
*                                                              *
* LOAD AND EXECUTE PHASE 2: DISASM2                            *
*                                                              *
****************************************************************
*
         FREEMAIN R,A=(12),LV=SYMTABL  FREE SYMBOL TABLE
*         TM    LOADLIB+48,X'10'        LOADLIB DD CARD SUPPLIED
*         BZ    NODCB                   NO
*         LOAD  EP=DISASM2,DCB=LOADLIB  LOAD DISASM2
*         B     EXEC2                   CONTINUE
*NODCB    LOAD  EP=DISASM2              LOAD DISASM2
*EXEC2    LR    R15,R0                  COPY E.P. ADDRESS
*         LA    R1,=A(COMMPARM)         GET PARAMETER ADDRESS
*         BALR  R14,R15                 CALL DISASM2
*         DELETE EP=DISASM2             DELETE AFTER USE
****************************************************************
*        CALLING SEQUENCE CODE
         CALL  DISASM2,(COMMPARM)
****************************************************************
         B     EOJ                     GO FINISH
*
*
*
*
*
*
****************************************************************
*                                                              *
* WRITE OUTPUT TO SYSPUNCH.                                    *
*                                                              *
****************************************************************
*
WRTOUT   EQU   *                       WRITE OUTPUT RECORDS
*        MVC   PRT+30(80),WORKREC      SAVE IN PRINT            ACF RH
         MVC   PLABEL(80),WORKREC SAVE IN PRINT                     RH
         TM    SYSPUNCH+48,X'10'       IS FILE OPEN
         BZ    CLRWKR                  NO
         AP    CARDNO,=P'10'           INCREMENT CARD NO
         UNPK  SEQNBR,CARDNO           UNPACK INTO CARD
         OI    SEQNBR+7,C'0'           CLEAR SIGN
         MVC   PSEQ,SEQNBR        PUT SEQ NBR ON PRINTOUT RECORD    RH
         PUT   SYSPUNCH,WORKREC        WRITE SOURCE CARD
CLRWKR   MVC   WORKREC,BLANX           CLEAR OUTPUT RECORD
         BR    R9                      RETURN
*
*
*
****************************************************************
*                                                              *
* END OF JOB. DISASSEMBLY COMPLETE.                            *
*                                                              *
****************************************************************
EOJ      EQU   *                       END OF JOB
EOFSW    B     NOCTL                   RESET IF CTL CARD FOUND
*        MVC   MNEMONIC,=CL5'REQU'     SET MNEMONIC                 RH
*        BAL   R9,WRTOUT               WRITE RECORD                 RH
*        BAL   R9,PRINT                GO PRINT IT                  RH
*                                                                   RH
****************************************************************    RH
*                                                              *    RH
* PRINT  REGISTER EQUATES.                                     *    RH
*                                                              *    RH
****************************************************************    RH
         BAL   R9,PRINT           PRINT A BLANK LINE                RH
         LA    R12,ENDTAB         R12 -> TO END TABLE               RH
         LA    R11,16             R11 = # OF ENTRIES IN TABLE       RH
         SPACE 1                                                    RH
J92      DS    0H                                                   RH
         MVC   NAME(17),0(R12)           MOVE ENTRY TO PRINT        RH
         LA    R12,17(0,R12)             R12 -> TO NEXT ENTRY       RH
         BAL   R9,WRTOUT               WRITE RECORD                 RH
         BAL   R9,PRINT                GO PRINT IT                  RH
         BCT   R11,J92                 IF R11 > 0, BRANCH           RH
         SPACE 1                                                    RH
*                                                                   RH
****************************************************************    RH
*                                                              *    RH
* PRINT  DSECTS.                                               *    RH
*                                                              *    RH
****************************************************************    RH
         L     R12,DTBSTRT             @ DSECT HEADER TBL
CENDSTB  CLI   0(R12),X'FF'            END OF TABLE
         BE    ENDSTMT                 YES
         BAL   R9,PRINT           PRINT A BLANK LINE                RH
         MVC   NAME,0(R12)             DSECT NAME TO RECORD
         MVC   MNEMONIC(5),=C'DSECT'   OPERATION IS DSECT
         BAL   R9,WRTOUT               WRITE DSECT HEADER
         BAL   R9,PRINT                AND PRINT IT
         ICM   R11,7,8(R12)            GET FIELD TABLE ADDR
         USING DSECTD,R11
CENDFTB  CLI   0(R11),X'FF'            END OF FIELD TABLE
         BNE   DFMTFLD                 NO
         LA    R12,11(R12)             TO NEXT DSECT HEADER
         B     CENDSTB                 LOOP
DFMTFLD  MVC   NAME,DSNAME             NAME TO RECORD
         AGO   .DROP1                                               RH
****************************************************************    RH
*                                                              *    RH
* THIS CODE PRODUCES DSECTS LIKE SO:                           *    RH
*                                                              *    RH
* A   DSECT                                                    *    RH
* B   EQU A,10                                                 *    RH
* C   EQU A+10,10                                              *    RH
* C   EQU A+20,10                                              *    RH
* C   EQU A+30,10                                              *    RH
*            . .....ETC.                                       *    RH
*                                                              *    RH
*                                                              *    RH
*                                                              *    RH
****************************************************************    RH
         MVC   MNEMONIC(3),=C'EQU'     OPERATION IS EQU
         MVC   OPNDS(8),0(R12)         BASE NAME TO OPERAND
         LA    R10,OPNDS+7             @ NAME RHE
CFRHE    CLI   0(R10),C' '             AT RHE
         BNE   CFPLUS                  YES
         BCT   R10,CFRHE               BACK UP 1 AND LOOP
CFPLUS   LA    R10,1(R10)              TO NEXT POS
         MVI   0(R10),C'+'             DELIMITER
         SR    R1,R1                   CLEAR WORK
         ICM   R1,7,DSOFSET            GET OFFSET
         CVD   R1,DBLWD                CONVERT
         UNPK  DBLWD(4),DBLWD+4(4)     UNPACK
         OI    DBLWD+3,C'0'            CLEAR SIGN
         CH    R1,H1000                OFFSET < 1000
         BL    CFO100                  YES
         MVC   1(4,R10),DBLWD          NO, MOVE 4-DIGIT OFFSET
         LA    R10,5(R10)              TO NEXT POS
         B     CFCMA                   CONTINUE
CFO100   CH    R1,H100                 OFFSET < 100
         BL    CFO10                   YES
         MVC   1(3,R10),DBLWD+1        NO, MOVE 3-DIGIT OFFSET
         LA    R10,4(R10)              TO NEXT POS
         B     CFCMA                   CONTINUE
CFO10    CH    R1,H10                  OFFSET < 10
         BL    CFO1                    YES
         MVC   1(2,R10),DBLWD+2        MOVE 2-DIGIT OFFSET
         LA    R10,3(R10)              TO NEXT POS
         B     CFCMA                   CONTINUE
CFO1     LTR   R1,R1                   ANY OFFSET
         BZ    CFCMA                   NO
         MVC   1(1,R10),DBLWD+3        YES, MOVE 1-DIGIT OFFSET
         LA    R10,2(R10)              TO NEXT POS
CFCMA    MVI   0(R10),C','             DELIMITER
         SR    R1,R1                   CLEAR WORK
         ICM   R1,1,DSLENG             PICK UP LENGTH               RH
         BNZ   CFL30              IF LTH = 0, IT'S REALLY 256       RH
         LA    R1,256             SO SET R1 TO 256                  RH
CFL30    DS    0H                                                   RH
         CVD   R1,DBLWD                CONVERT
         UNPK  DBLWD(3),DBLWD+4(4)     UNPACK
         OI    DBLWD+2,C'0'            CLEAR SIGN
         CH    R1,H100                 LENGTH < 100
         BL    CFL10                   YES
         MVC   1(3,R10),DBLWD          NO, MOVE 3-DIGIT LENGTH
         B     CFWRT                   CONTINUE
CFL10    CH    R1,H10                  LENGTH < 10
         BL    CFL1                    YES
         MVC   1(2,R10),DBLWD+1        NO, MOVE 2-DIGIT LENGTH
         B     CFWRT                   CONTINUE
CFL1     MVC   1(1,R10),DBLWD+2        MOVE 1-DIGIT LENGTH
         AGO   .DROP2                                               RH
.DROP1   ANOP                                                       RH
****************************************************************    RH
*                                                              *    RH
* THIS CODE PRODUCES DSECTS LIKE SO:                           *    RH
*                                                              *    RH
* A   DSECT                                                    *    RH
* B   DS  10X                                                  *    RH
* C   DS  10X                                                  *    RH
* C   DS  10X                                                  *    RH
* C   DS  10X                                                  *    RH
*            . .....ETC.                                       *    RH
*                                                              *    RH
*                                                              *    RH
*                                                              *    RH
****************************************************************    RH
         MVC   MNEMONIC(2),=C'DS'      OPERATION IS DS              RH
         LA    R10,OPNDS          R10 -> TO WHERE OPNDS GO          RH
         SR    R1,R1                   CLEAR WORK                   RH
         ICM   R1,1,DSLENG        R1 = LENGTH OF DS                 RH
         BNZ   CFO20              LENGTH IS OKAY                    RH
         LA    R1,256             R1 = 256                          RH
*        LA    R9,L'DSECT(R11)    R9 -> TO NEXT DSECT FIELD         RH
*        SR    R8,R8                   CLEAR WORK                   RH
*        ICM   R1,7,DSOFSET-DSECT(R9) R1 = OFFSET OF NEXT FIELD     RH
*        ICM   R8,7,DSOFSET       R8 = OFFSET OF THIS FIELD         RH
*        SR    R1,R8              R1 = LTH OF THIS FIELD            RH
CFO20    DS    0H                                                   RH
         CVD   R1,DBLWD                CONVERT                      RH
         UNPK  DBLWD(4),DBLWD+4(4)     UNPACK                       RH
         OI    DBLWD+3,C'0'            CLEAR SIGN                   RH
         CH    R1,H1000                OFFSET < 1000                RH
         BL    CFO100                  YES                          RH
         MVC   1(4,R10),DBLWD          NO, MOVE 4-DIGIT OFFSET      RH
         LA    R10,5(R10)              TO NEXT POS                  RH
         B     CFCMA                   CONTINUE                     RH
CFO100   CH    R1,H100                 OFFSET < 100                 RH
         BL    CFO10                   YES                          RH
         MVC   1(3,R10),DBLWD+1        NO, MOVE 3-DIGIT OFFSET      RH
         LA    R10,4(R10)              TO NEXT POS                  RH
         B     CFCMA                   CONTINUE                     RH
CFO10    CH    R1,H10                  OFFSET < 10                  RH
         BL    CFO1                    YES                          RH
         MVC   1(2,R10),DBLWD+2        MOVE 2-DIGIT OFFSET          RH
         LA    R10,3(R10)              TO NEXT POS                  RH
         B     CFCMA                   CONTINUE                     RH
CFO1     LTR   R1,R1                   ANY OFFSET                   RH
         BZ    CFCMA                   NO                           RH
         MVC   1(1,R10),DBLWD+3        YES, MOVE 1-DIGIT OFFSET     RH
         LA    R10,2(R10)              TO NEXT POS                  RH
CFCMA    DS    0H                                                   RH
         MVI   0(R10),C'X'        FIELD TYPE = X                    RH
.DROP2   ANOP                                                       RH
CFWRT    BAL   R9,WRTOUT               WRITE THE RECORD
         BAL   R9,PRINT                PRINT THE RECORD
         LA    R11,L'DSECT(R11)        TO NEXT FIELD ENTRY
         B     CENDFTB                 LOOP
ENDSTMT  MVC   MNEMONIC,=CL5'END'      MNEMONIC TO OUTPUT RECORD
         MVC   OPNDS(8),ENDLBLNM       POINT END STMT TO BEGIN LOC
         BAL   R9,WRTOUT               WRITE THE RECORD
         BAL   R9,PRINT                GO PRINT IT
CLOSES   CLOSE (SYSLIB,,SYSPUNCH,,SYSPRINT,,SYSIN) CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
         DROP  R11
*
*
*
*                 *** DATA AND WORK AREAS ***
*
*
*
BUFAD    DS    F                       SYSLIB BUFFER ADDR
SYMTBAD  DS    F                       SYMBOL TABLE ADDRESS
CURRSYM  DS    F                       CURRENT SYM TBL ADDR
ENDSYM   DC    A(SYMTABL)              SYM TBL END ADDR
SYMTABL  EQU   80000       *** ACF - WAS 40000
RLDTBL   DS    F                       ADDR OF RLD TABLE
CURRLD   DS    F                       CURR RLD TBL ADDR
ENDRLD   DC    A(RLDTABL)              RLD TBL END ADDR
RLDTABL  EQU   60000
ENDATO   DC    A(DATOLEN)              DATA ONLY TBL LENGTH
DATOLEN  EQU   1536
PROCESS  DC    XL1'00'                 PROCESS INDIC 80=SYM TBL BUILT
CES9     DS    F                       CESDREC RETURN ADDR
BL9      DS    F                       BLDL RTN RETURN ADDR
CR9      DS    F                       CTRLRECS RETURN ADDR
CT9      DS    F                       CNTLRECS RETURN ADDR
RL9      DS    F                       RLDRECS RETURN ADDR
TX9      DS    F                       TXTFMT RETURN ADDR
HIVAL    DC    4X'FF'                  CONSTANT F'S
H1000    DC    H'1000'                 CONSTANT 1000
H100     DC    H'100'                  CONSTANT 100
H10      DC    H'10'                   CONSTANT 10
SYMLEN   DC    AL2(L'SYMENT)           LENGTH OF SYMTBL ENTRY        */
PRTABL   DS    CL9                     HEX-PRINTABLE CONVERSION AREA
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
P58      DC    P'58'                   CONSTANT 58
JFCBAD   DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB, END OF LIST
         DC    AL3(JFCB)               ADDRESS OF JFCB READ AREA
JFCB     DS    CL176                   JFCB
         ORG   JFCB                                                 RH
JFCBDSN  DC    CL44' '                                              RH
JFCBMBR  DC    CL8' '                                               RH
         DC    CL124' '                                             RH
         ORG   ,                                                    RH
         SPACE 1                                                    RH
SYMHDR   DC    C'0     SYMBOL        TYPE   IND  ADDR'
         DC    C'    SEG  LRID   LENG  ESDID  RSECT  AMODE  RMODE'
*        DC    CL64' '
LSYMHDR  EQU   *-SYMHDR                                             RH
*
HEAD1    DS    0CL132                                               RH
         DC    C'   LOC  OBJECT CODE    ADDR1 ADDR2  STMT'          RH
         DC    C'    SOURCE STATEMENT'                              RH
         DC    CL17' '                                              RH
         DC    C'CHARACTER REP'                                     RH
         DC    CL20' '                                              RH
         DC    C'PAGE'                                              RH
PAGENO   DS    CL4                                                  RH
         DC    CL14' '                                              RH
*
*
RLDHDR   DC    C'0     RELPTR  POSPTR      TYPE     LEN'
         DC    C'    DIR   ADDR'
*        DC    CL90' '
LRLDHDR  EQU   *-RLDHDR                                             RH
*
*
*
PH1LBL   DC    C'1***** PHASE     1 LABEL TABLE *****'              RH
LPH1LBL  EQU   *-PH1LBL                                             RH
PH1HDR   DC    C'0  ADDR TYPE      SYMBOL  LEN'                     RH
LPH1HDR  EQU   *-PH1HDR                                             RH
*PH1LBL   DC    CL121'1***** PHASE 1 LABEL TABLE *****'             RH
*PH1HDR   DC    CL121'0  ADDR TYPE  SYMBOL  LEN'                    RH
*                                                                   RH
         SPACE 1                                                    RH
*              REGISTER EQUATES TABLE                               RH
         SPACE 1                                                    RH
ENDTAB   DC    CL17'R0       EQU   0 '                              RH
         DC    CL17'R1       EQU   1 '                              RH
         DC    CL17'R2       EQU   2 '                              RH
         DC    CL17'R3       EQU   3 '                              RH
         DC    CL17'R4       EQU   4 '                              RH
         DC    CL17'R5       EQU   5 '                              RH
         DC    CL17'R6       EQU   6 '                              RH
         DC    CL17'R7       EQU   7 '                              RH
         DC    CL17'R8       EQU   8 '                              RH
         DC    CL17'R9       EQU   9 '                              RH
         DC    CL17'R10      EQU   10'                              RH
         DC    CL17'R11      EQU   11'                              RH
         DC    CL17'R12      EQU   12'                              RH
         DC    CL17'R13      EQU   13'                              RH
         DC    CL17'R14      EQU   14'                              RH
         DC    CL17'R15      EQU   15'                              RH
         SPACE 1                                                    RH
*
*
H1       DC    H'1'                    CONSTANT 1
H5       DC    H'5'                    CONSTANT 5
H11      DC    H'11'                   CONSTANT 11
H16      DC    H'16'                   CONSTANT 16
H256     DC    H'256'                  CONSTANT 256
BLANX    DC    CL133' '                CONSTANT BLANKS              RH
XZROS    DC    8X'00'                  CONSTANT ZEROS
TRTBL    DC    C'0123456789ABCDEF'     TRANSLATE TBL
NOCNTRL  DC    C'MEMBER AND CSECT MUST BE ENTERED VIA SYSIN'
OVER8    DC    C'MEMBER OR CSECT NAME OVER 8 CHARACTERS'
NOMEM    DC    C'NO MEMBER NAME FOUND IN CONTROL CARD OR PARM'
INVMEM   DC    C'SPECIFIED MEMBER NOT FOUND IN SYSLIB PDS'
INVCSECT DC    C'SPECIFIED CSECT NOT FOUND IN MEMBER'
FULLSYM  DC    C'SYMBOL TABLE FULL: OVER 4000 ENTRIES'
FULLRLD  DC    C'RLD TABLE FULL: OVER 3000 ENTRIES'
POFSET   DC    F'0'                    OFFSET FOR TEXT PRINT
PRTCHAR  DC    256C'.'                 CHARACTER TRANSLATE TABLE
         ORG   PRTCHAR+C' '
         DC    C' '
         ORG   PRTCHAR+74                                           RH
         DC    7X'00'             .<(+|&                           RH
         ORG   PRTCHAR+90                                           RH
         DC    8X'00'             !$*);-/                          RH
         ORG   PRTCHAR+106                                          RH
         DC    6X'00'             |,%_>?                            RH
         ORG   PRTCHAR+121                                          RH
         DC    7X'00'             ':#@'="                           RH
         ORG   PRTCHAR+C'A'
         DC    C'ABCDEFGHI'
         ORG   PRTCHAR+C'J'
         DC    C'JKLMNOPQR'
         ORG   PRTCHAR+C'S'
         DC    C'STUVWXYZ'
         ORG   PRTCHAR+C'0'
         DC    C'0123456789'
         ORG
*
*
*
SYSPUNCH DCB   DSORG=PS,MACRF=(PM,GM),DDNAME=SYSPUNCH,                 X
               RECFM=FB,LRECL=80
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80,EODAD=NOCTL
SYSLIB   DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,                         X
               RECFM=U,NCP=1,EXLST=JFCBAD                           RH
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133                                  RH
*              RECFM=FBA,LRECL=121
LOADLIB  DCB   DSORG=PO,MACRF=R,DDNAME=LOADLIB,EXLST=JFCBAD
*
         PRINT GEN                                                  RH
         DISASPRM DSECT=NO                                          RH
*
*
*                                                                   RH
         END
/*
//
//*            END IF JUST WANT OBJECT MODULE
//*
//LKED.SYSLMOD  DD DISP=SHR,DSN=HALL.LOAD
//LKED.SYSIN DD *
     SETCODE AC(0)
     NAME DISASMM(R)
//
//
