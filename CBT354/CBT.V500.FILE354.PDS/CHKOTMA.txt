//CHKOTMA  JOB
/*ROUTE  PRINT  R7
//S1        EXEC  HLASMCL,CLASS='*',        PARMC=RENT,PARML=RENT,
//   COND.LKED=(7,LT,ASM)
//ASM.SYSLIB  DD DSN=SYS1.MACLIB
//  DD DSN=IMSVS.GENLIB,DISP=SHR
//  DD DSN=IMSVS.GENLIBA,DISP=SHR
//  DD DSN=IMSVS.GENLIBB,DISP=SHR
//  DD DSN=SYS1.AMODGEN,DISP=SHR
//  DD DSN=IMSVS.OTMA.V2R1M3.GENLIBB,DISP=SHR  NOT NEEDED??
//ASM.SYSIN DD  *
*********************************************************************
*   CHKOTMA - ACCEPT PARMS FROM CALL AND VERIFY IMS TCPIP IMSTOC    *
*   OPERATION BY SUBMITTING IMS TRANSACTION "WHOAMI" AND RECEIVING  *
*   RESPONSE                                                        *
*                                                                   *
*   DEFAULTS: IMSTOC - OTMAHWSD, PORT - 7010, HOST - BCSC01D        *
*                                                                   *
*   TO RUN AS A TSO CMD...CHKOTMA OTMAHWSD,7010(,PSWD=,DEBUG)       *
*         YOU WILL BE PROMPTED FOR YOUR PASSWORD IF NO PSWD=        *
*                                                                   *
*   TO RUN AS A BATCH JOB...PARM='OTMAHWSD,7010,PSWD=(,DEBUG)       *
*         YOU MUST SPECIFY YOUR PASSWORD WITH PSWD=                 *
*                                                                   *
*                                                                   *
*   CREATED: DEC 98 BY R. HALL......THANKS TO DAN AND IMS LISTENER  *
*                                                                   *
*   MODIFIED:                                                       *
*                                                                   *
*                                                                   *
*********************************************************************
*
*              THIS PGM CAN BE CALLED AS A TSO COMMAND, A BATCH JOB,
*              OR FROM AN IMF EXEC
*
*              THIS PGM IS NOT RE-ENTRANT AND RUNS IN 31 BIT MODE
*
*        PARMS PASSED TO THIS PROGRAM AS AN IMF EXEC ARE:
*
*  OFFSET 0    1) A COUNT OF THE NUMBER OF PARAMETERS...ALWAYS 5
*         4    2) A POINTER TO AN 8 BYTE FIELD OF THE EXEC NAME
*         8    3) A POINTER TO A  4 BYTE FIELD OF THE BBI SS
*        12    4) THE LTH OF THE  PARAMETER STRING
*        16    5) A POINTER TO THE PARAMETER STRING
*                 IT IS THE IMSTOC NAME, THE PORT, AND DEBUG
*
*        PARMS PASSED TO THIS PROGRAM AS A BATCH JOB ARE:
*
*  OFFSET 0    1) A POINTER TO THE PARAMETER STRING
*                 IT IS THE IMSTOC NAME, THE PORT, AND "DEBUG"
*                 IF IMSTOC IS RACF'D ALSO PSWD= FOR YOUR USERID
*
*        IF RUN AS A TSO CMD, THE 1ST WORD OF THE CPPL POINTS TO
*        THE COMMAND AND PARMS. IT IS USED TO SIMULATE PARMS LIKE
*        A BATCH JOB TO USE THE PARM PROCESSOR.
*
*
*********************************************************************
         TITLE '******* PROGRAM TO CHECK IMSTOC *****'
         SPACE 3
         ENTRY CHKOTMA            ESTABLISH ENTRY POINT
CHKOTMA  CSECT
CHKOTMA  AMODE 31
CHKOTMA  RMODE 24
         USING *,R15              SET TEMP BASE
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    AL1(EYELTH)        LTH OF EYE CATCHER
EYECATCH DS    0C
         DC    C'CHKOTMA  '       PROGRAM NAME
         DC    C' VERSION 1.0 '   VERSION
         DC    CL8'&SYSDATE'      DATE WRITTEN
         DC    CL6'&SYSTIME'      TIME WRITTEN
         DC    C'..CHECK IF OTMA  IS WORKING'  COMMENT
EYELTH   EQU   *-EYECATCH         LTH OF EYE CATCHER
AROUND   DS    0H
*        LA    R1,0(0,R1)         CLEAR HI-ORDER BYTE FOR 31 BIT
*        LA    R13,0(0,R13)       CLEAR HI-ORDER BYTE FOR 31 BIT
         BSM   R14,0              SAVE CURRENT AMODE
         STM   R14,R12,12(R13)
         LR    R12,R15            SET 1ST BASE REG....R12
         DROP  R15
         USING CHKOTMA,R12,R9
         SPACE 1
         LA    R9,4095(0,R12)     SET 2ND BASE REG...R9
         LA    R9,1(0,R9)         ''
         SPACE 1
         LR    R11,R1             R11 -> TO A(PARMS)
         GETMAIN R,LV=LDYNAMIC,LOC=(RES,ANY)  GET CORE FOR WORK AREA
         LR    R10,R1             R10 -> TO WORK AREA
         USING WORK,R10           SET WORK BASE
         LA    R4,SAVE            R4 -> TO SAVE AREA
         ST    R13,SAVE+4         SAVE A(CALLERS SAVE AREA)
         ST    R4,8(0,R13)        CHAIN OUR SAVE AREA
         LR    R13,R4             R13 -> TO OUR SAVE AREA
         SPACE 1
*
*        BECAUSE SOME OF THE TCP MODULES CAN BE ACCESSED IN 31
*        BIT MODE PUT US INTO 31 BIT MODE ADDRESSING
*
*        CNOP  0,4                PUT ON FULLWORD BOUNDARY
*        L     R1,*+8             GET 31 BIT MODE ADDRESS
*        BSM   0,R1               GET INTO 31 BIT MODE
*        DC    H'0'               ALIGN ON A FULLWORD BOUNDARY
*        DC    X'80',AL3(*+3)     NEXT ADDRESS IS 31 BIT MODE
*
*********************************************************************
*
*        INIT  SOME STUFF IN THE  WORKAREA....
*
*********************************************************************
         SPACE 1
         MVI   WORKINIT,X'00'     CLEAR WORK AREA TO ZERO....
         LA    R2,WORKINIT+1      R2 -> TO WHERE DATA GOES
         LA    R3,LINIT-1         R3 = TOTAL LTH TO MOVE TO
         LA    R4,WORKINIT        R4 -> TO WHERE DATA COMES FROM
         LA    R5,1               R5 = TOTAL LTH TO MOVE FROM
         SPACE 1
         MVCL  R2,R4              SET UP NEW RECORD DEFAULTS
*                IF NECESSARY, RELOAD R2 'CAUSE MVCL BUGGERS IT
*        LA    R2,WRKAREA         R2 -> TO NEW RECORD
         SPACE 1
         MVC   SYSPRINT(PDCBLTH),PRINTDCB   INIT DCB...
         MVC   DSOPEN(OPENLTH),OPENMAC    INIT OPEN MACRO CODE
         MVC   DSCLOSE(CLOSLTH),CLOSMAC   INIT CLOSE MACRO CODE
         MVC   TITLE_IMSTOC,=CL8'OTMAHWSD'    INIT IMSTOC
         MVC   HOSTNAME(24),=CL24'BCSC01D' INIT HOSTNAME
         MVC   TITLE_PORT,=CL4'7010'     INIT PORT
*        LA    R8,7010            R8 = PORT 7010
*        STH   R8,PORT            SET DEFAULT PORT
         MVC   TITLE_DEBUG,=CL2'NO'   ASSUME WE DON'T DO DEBUG...
         MVI   HOSTNAME_VALUE,C' '     INIT HOSTNAME_VALUE
         MVC   HOSTNAME_VALUE+1(254),HOSTNAME_VALUE  ''
         MVC   HOSTALIAS_VALUE,HOSTNAME_VALUE  INIT ALIAS...
         MVC   RESERVED,HOSTNAME_VALUE  INIT RESERVED AREA
         LA    R8,7               R8 = 7
         ST    R8,NAMELEN         SET HOST NAME LTH
         LA    R8,2               R8 = 2
         STH   R8,FAMILY          SET FAMILY
*
*
         LA    R6,0               ASSUME EVERYTHING WORKS
         ST    R6,CPKECB          SET RETURN CODE
*
***********************************************************************
*
* RETRIEVE THE USERS RACF USERID AND GROUP FROM THE ACEE.             *
*
***********************************************************************
*
GETUSER  DS    0H
         L     R8,PSAAOLD-PSA          LOAD ADDRESS OF CURRENT ASCB
         L     R8,ASCBASXB-ASCB(R8)    LOAD ASXB ADDRESS
         L     R8,ASXBSENV-ASXB(R8)    LOAD ACEE ADDRESS
*                                      PUT USERID IN OTMA HEADER
         MVC   IRM_RACF_USERID,ACEEUSRI-ACEE(R8)
*                                      PUT GROUP IN OTMA HEADER
         MVC   IRM_RACF_GRNAME,ACEEGRPN-ACEE(R8)
*                                      PUT LTERM IN OTMA HEADER
         MVC   IRM_LTERM,ACEETRID-ACEE(R8)
*                                                                   RH
         SPACE 1
         L    R15,16               CVT POINTER
         L    R15,0(R15)           TCB PTR PTR
         L    R15,0(R15)           TCB POINTER
         L     R15,180(0,R15)     R15 -> TO JSCB                    RH
         L     R15,264(0,R15)     R15 -> TO TSO PSCB                RH
         LTR   R15,R15            IS THERE A PSCB?                  RH
         BP    BYPASSF            YES, IT'S A TSO TERMINAL          RH
*        BZ    NOTTSO             NO, IT'S A BATCH JOB.             RH
*
SAYBATCH DS    0H
         OI    BFLAG,X'80'        NO, SAY IT'S A BATCH JOB
         B     NOTTSO             ON WE GO
*
BYPASSF  DS    0H
*        L     R7,0(0,R11)        R7 -> AT 1ST PARM
*        LA    R7,4(0,R7)         R7 -> WHERE CP NAME COULD BE
*        CLC   =C'CHKOTMA',0(R7)  IS IT THERE?
*        BE    CPCMD              YES, IT MUST BE A TSO CMD
         TM    0(R11),X'80'       ONLY 1 PARM?
         BO    SAYBATCH           YES, TREAT AS A BATCH JOB...
         LA    R3,5               R3 = NBR PARMS FOR IMF
         C     R3,0(0,R11)        1ST PARM = 5?
         BE    SAYBATCH           YES, CALLED FROM IMF...
*
CPCMD    DS    0H
         ST    R11,CPPLADDR         SAVE A(CPPL)
         USING CPPL,R11           ADDRESS IT
         SPACE 1
         L     R3,=V(IOPLADD)     R3 -> TO IOPL
         USING IOPL,R3            SET IOPL BASE
         L     R4,CPPLUPT
         ST    R4,IOPLUPT         SET UP IOPL
         L     R4,CPPLECT
         ST    R4,IOPLECT
         L     R4,=V(ECB)
         ST    R4,IOPLECB
         SR    R4,R4
         ST    R4,IOPLIOPB
         DROP  R3                 DROP IOPL BASE....
         SPACE 1
         L     R11,0(0,R11)       R11 -> AT CP BUFFER
         LH    R7,0(0,R11)        R7 = LTH OF BUFFER
         LH    R6,2(0,R11)        R6 = OFFSET TO PARMS
         SR    R7,R6              R7 = LTH OF PARMS
         SH    R7,=H'4'           -4 FOR LLLL
         LTR   R7,R7              ANY PARM?
         BZ    ATTACHIT           NONE, USE DEFAULTS
         LA    R6,4(R6,R11)       SET R6 FOR PARM ANALYSIS
         SH    R6,=H'2'           SET R6 FOR PARM ANALYSIS
         EX    R7,MAKEUPR         MAKE SURE PARMS ARE UPPER CASE    RH
         B     PARM1              GO DO IT
MAKEUPR  OC    2(0,R6),HOSTNAME_VALUE MAKE PARMS UPPER CASE         RH
         SPACE 1
NOTTSO   DS    0H
*********************************************************************
*
*              SEE IF THERE IS A  PARM
*
*        PARMS PASSED TO THIS PROGRAM FROM IMF ARE:
*  OFFSET 0    1) A COUNT OF THE NUMBER OF PARAMETERS...ALWAYS 5
*         4    2) A POINTER TO AN 8 BYTE FIELD OF THE EXEC NAME
*         8    3) A POINTER TO A  4 BYTE FIELD OF THE BBI SS
*        12    4) THE LTH OF THE  PARAMETER STRING
*        16    5) A POINTER TO THE PARAMETER STRING
*                 IT IS THE IMSTOC NAME, THE PORT, AND DEBUG
*
*        PARMS PASSED TO THIS PROGRAM AS A BATCH JOB ARE:
*
*  OFFSET 0    1) A POINTER TO THE PARAMETER STRING
*                 IT IS THE IMSTOC NAME, THE PORT, AND "DEBUG"
*                 IF IMSTOC IS RACF'D ALSO PSWD= FOR YOUR USERID
*
*        IF RUN AS A TSO CMD, THE 1ST WORD OF THE CPPL POINTS TO
*        THE COMMAND AND PARMS. IT IS USED TO SIMULATE PARMS LIKE
*        A BATCH JOB TO USE THE PARM PROCESSOR.
*
*
*********************************************************************
         SPACE 1
*              GET PARM, IF ANY
         SPACE 1
         LR    R6,R11             R6 -> TO PARMS
         TM    0(R11),X'80'       ONLY 1 PARM?
         BZ    PARM4              NO, MUST BE IMF CALL...
*
         L     R6,0(0,R11)        R6 -> TO PARMS
         SR    R7,R7              CLEAR R7
         ICM   R7,3,0(R6)         R7 = PARM LTH
         BZ    ATTACHIT           NONE, USE DEFAULTS
*
PARM1    DS    0H
         LA    R2,2(0,R6)         R2 -> TO OUR PARMS
         LR    R4,R2              R4 -> TO IMSTOC NAME
         B     PARM1B             GO PROCESS PARMS
*
PARM4    DS    0H
         ICM   R7,15,12(R6)       R7 = PARM LTH
         BZ    ATTACHIT           NONE, USE DEFAULTS
*
         L     R2,16(0,R6)        R2 -> TO OUR PARMS
         L     R4,16(0,R6)        R4 -> TO IMSTOC NAME
         SPACE 1
PARM1B   DS    0H
         CLI   0(R4),C','         AT END OF NAME?
         BE    GOTPGM             YES, GO SET IT UP
         CLI   0(R4),C' '         AT END OF NAME FOR TSO CMD?
         BE    GOTPGM             YES, GO SET IT UP
         LA    R4,1(0,R4)         R4 -> TO NEXT KEY BYTE, IF ANY
         BCT   R7,PARM1B          CHECK NEXT BYTE
         SPACE 1
GOTPGM   DS    0H
         LR    R8,R4              R8 -> AT COMMA
         SR    R8,R2              R8 = LTH OF PGM NAME
         CH    R8,=H'8'           TOO LONG?
         BNH   GOTPGM2            NO
         LA    R8,8               YES, USE 1ST 8 BYTES...
         SPACE 1
GOTPGM2  DS    0H
         BCTR  R8,0               R8 = HEX LTH
         EX    R8,SETNAME         SET UP ATTACH NAME
         SPACE 1
         CH    R7,=H'1'           ANY MORE PARM?
         BL    ATTACHIT           NO, GO DO ATTACH
         SPACE 1
         BCTR  R7,0               -1 FOR COMMA...
         LA    R4,1(0,R4)         R4 -> TO NEXT PARM
         LR    R2,R4              R2 -> AT NEXT PARM...PORT #..
         SPACE 1
PARM2B   DS    0H
         CLI   0(R4),C','         AT END OF PORT
         BE    GOTPORT            YES, GO SET IT UP
         CLI   0(R4),C' '         AT END OF PORT FOR TSO CMD?
         BE    GOTPORT            YES, GO SET IT UP
         LA    R4,1(0,R4)         R4 -> TO NEXT KEY BYTE, IF ANY
         BCT   R7,PARM2B          CHECK NEXT BYTE
         SPACE 1
GOTPORT  DS    0H
         LR    R8,R4              R8 -> AT COMMA
         SR    R8,R2              R8 = LTH OF PORT NAME
         CH    R8,=H'4'           TOO LONG?
         BNH   GOTPORT2           NO
         LA    R8,4               YES, USE 1ST 4 BYTES...
         SPACE 1
GOTPORT2 DS    0H
         BCTR  R8,0               R8 = HEX LTH
         EX    R8,SETPORT         SET UP PORT NUMBER
         SPACE 1
         CH    R7,=H'1'           ANY MORE PARM?
         BL    ATTACHIT           NO, GO DO ATTACH
         LA    R4,1(0,R4)         R4 -> TO NEXT PARM
         LR    R2,R4              R2 -> AT NEXT PARM
         SPACE 1
CHKPSWD  DS    0H
         CLC   =C'PSWD=',0(R4)    IS IT THE 'PSWD=' PARM?
         BNE   PARM5              NO, CHECK FOR NEXT PARM
         SPACE 1
         LA    R4,5(0,R4)         R4 -> TO PSWD= PARM, IF ANY
         SH    R7,=H'5'           R7 = LTH OF REST OF PARM
         SPACE 1
         LA    R15,IRM_RACF_PW    R15 -> TO PSWD SAVE AREA
         MVC   0(8,R15),=CL8' '   CLEAR OUT DEFAULT
         SPACE 1
PARM3    DS    0H
         MVC   0(1,R15),0(R4)     SAVE PSWD BYTE
         LA    R15,1(0,R15)       R15 -> TO NEXT SAVE BYTE
         LA    R4,1(0,R4)         R4 -> TO NEXT KEY BYTE, IF ANY
         BCTR  R7,0               R2 = LTH OF REST OF PARM
         CLI   0(R4),C','         AT END OF PSWD?
         BE    NEXTPARM           YES, GO GET NEXT PARM
         CLI   0(R4),C' '         AT END OF PSWD FOR TSO CMD?
         BE    NEXTPARM           YES, GO GET NEXT PARM
         LTR   R7,R7              ANY MORE PARM?
         BP    PARM3              YES, GO CHECK IT
*        B     NEXTPARM           GO CHECK FOR ANOTHER PARM
         SPACE 1
PARM5    DS    0H
NEXTPARM DS    0H
         SPACE 1
         MVC   TITLE_DEBUG,=CL2'  '   CAN ONLY BE DEBUG...
         SPACE 1
ATTACHIT DS    0H
         CLI   IRM_RACF_PW,C' '   IS THERE A PSWD ?
         BNE   PSWDOK             YES.
         TM    BFLAG,X'80'        IS IT A BATCH JOB ?
         BO    PSWDOK             YES, CAN'T PROMPT FOR PASSWORD
         SPACE 1
*                                                                   RH
*              ASK FOR PASSWORD                                     RH
*                                                                   RH
READPSWD DS    0H                                                   RH
         MVC   USERID,IRM_RACF_USERID MOVE USRID TO MSG
*
PSWRD_01 DS    0H
PSWRD_02 DS    0H
         LA    R0,1               NBR MSGS TO SEND
         LA    R1,GETPSWD         R1 -> TO MSG TO SEND
         LA    R2,0
*
*              PROMPT FOR AND READ A PARAMETER
*
PSWRD_03 DS    0H
         STM   R0,R2,MSG_ADDR
        PUTGET MF=(E,IOPLADD),                                         X
               OUTPUT=(,,PTBYPS),                                      X
               PARM=MSG_PGPB
         SPACE 1
         LTR   R15,R15            WAS PUTGET OKAY?
         BNZ   PSWDOK             NO, IGNORE IT AND GET RACF'D
*
PARSPASS DS    0H
*              SET-UP TO PARSE THE RESPONSE
*
         L     R1,MSG_PGPB+PGPBIBUF-PGPB
         LA    R15,3(,R1)
         LA    R0,1
         AH    R1,0(,R1)
*
*              LOCATE THE FIRST NON-BLANK CHARACTER
*
         CLI   1(R15),C' '
         BXH   R15,R0,PSWRD_02
         BE    *-8
*
*              CALCULATE THE LENGTH REMAINING TO BE PARSED
*
         LR    R14,R1
         SR    R14,R15
         BZ    PSWRD_01
         BCTR  R14,0
         AGO   .ENDPARS           END OF PARSING FOR NOW....
*
*              LOCATE THE DELIMINATING CHARACTER
*
         LA    R2,4
*        EX    R14,TRTINSTR
         B     *(R2)
         B     PSWRD_04                 04 - BLANK
*        BC    0,*                      08 - INVALID
*
*
         L     R1,MSG_PGPB+PGPBIBUF-PGPB
         AH    R1,0(,R1)
         B     PSWRD_01
*
*RTINSTR TRT   0(*-*,R15),$TRTABLE
*
*              CALCULATE & SAVE THE LENGTH OF THE PARSED PARAMETER
.ENDPARS ANOP
*
PSWRD_04 SR    R1,R15
         STC   R1,PASSWORD
*
*              VALIDATE THE LENGTH OF THE PARSED PARAMETER
*
         CLI   PASSWORD,8
         BH    PSWRD_01
*
*              INITIALIZE THE PASSWORD OPERAND
*
         MVI   PASSWORD+1,C' '
         MVC   PASSWORD+1(7),PASSWORD+1
         SPACE 1
         LA    R14,PASSWORD+1
         BCTR  R1,0
         EX    R1,OC_INSTR
*        OC    RACPSWD(8),OLDNAME MAKE SURE ANSWER IS UPPER CASE    RH
*
*              SET UP THE PASSWORD FOR IMSTOC
*
         MVC   IRM_RACF_PW,PASSWORD+1 SET UP PSWD FOR TOC
         B     PSWDOK
*
OC_INSTR OC    0(*-*,R14),0(R15)
*
         SPACE 1                                                    RH
PSWDOK   DS    0H

*   /*****************************************************************/
*   /*                                                               */
*   /* END OF PARM ANALYSIS...                                       */
*   /*                                                               */
*   /*****************************************************************/
*
         AGO   .NOLOAD
*
*              USE THE FOLLOWING  CODE TO DYNAMICALLY LOAD
*              THE REQUIRED MODULES
*
*              LOAD EZASOKET AND EZACIC08 IF NOT ALREADY LOADED
*
         LA    R4,EZASOKET        R4 -> TO MODULE NAME
         LOAD  EPLOC=((R4))       LOAD PGM...
         LTR   R15,R15            DID IT LOAD?
         BNZ   NOMORE             NO.
         ST    R0,SOKETAD         SAVE ITS ENTRY ADDRESS
*
         LA    R4,EZACIC08        R4 -> TO MODULE NAME
         LOAD  EPLOC=((R4))       LOAD PGM...
         LTR   R15,R15            DID IT LOAD?
         BNZ   NOMORE             NO.
         ST    R0,CIC08AD         SAVE ITS ENTRY ADDRESS
*
         SPACE 1
.NOLOAD  ANOP
         SPACE 1
         LA    R2,PRINTDCB        R2 -> TO SYSPRINT DCB
*        LA    R2,SYSPRINT        R2 -> TO SYSPRINT DCB
*                                 OPEN IT FOR OUTPUT
         OPEN  ((R2),(OUTPUT)),MODE=24,MF=(E,DSOPEN)
         TM    48(R2),X'10'       DID SYSPRINT DATASET OPEN         RH
         BNO   NOSYSOUT           NO, CAN'T PRINT INFO MSG THEN     RH
         MVC   LINE1,HEADINFO     PRINT INFO MSG                    RH
         BAL   R8,PRINT2               ''                           RH
         MVI   LINE1,C'-'         PUT CARRIAGE CTL BACK             RH
         SPACE 1                                                    RH
NOSYSOUT DS    0H                                                   RH

*/*******************************************************************/
*/* INITIALIZE SCREEN OUTPUT                                        */
*/*******************************************************************/

         MVC   TCPNAME,=CL8'TCPIP '
         MVC   ADSNAME,TITLE_IMSTOC
         PACK  DWORD,TITLE_PORT   CONVERT PORT NBR TO PACKED
         CVB   R8,DWORD           CONVERT PORT NBR TO HEX
         STH   R8,PORT            SAVE IT FOR TESTING
         MVC   HOSTNAME(8),=CL8'BCSC01A'
         CLC   =CL8'OTMAHWSA',ADSNAME    IS IMSTOC OTMAHWSA?
         BE    HOSTSET                  YES, HOSTNAME IS SET
         MVC   HOSTNAME(24),=CL24'BCSC01D.GOV.BC.CA'
*        MVC   HOSTNAME(8),=CL8'BCSC01D'
*        B     HOSTSET            BETTER BE ON BCSC01D THEN...
*
*        CLC   =CL8'IMSTLN1',ADSNAME    IS IMSTOC IMSTLN1?
*        BE    HOSTSET                  YES, HOSTNAME IS SET
*        MVC   HOSTNAME(8),=CL8'BCSC01D'
*        CLC   =CL8'IMSDLN1',ADSNAME    IS IMSTOC IMSDLN1?
*        BE    HOSTSET                  YES, HOSTNAME IS SET
*        MVC   HOSTNAME(8),=CL8'BCSC01D'
*        CLC   =CL8'IMSELN1',ADSNAME    IS IMSTOC IMSELN1?
*        BNE   NOMORE                   NO, TOO BAD...
*
HOSTSET  DS    0H
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB1              NO
         MVC   LINE1(DB1LTH),DB1  PRINT INFO MSG                    RH
         MVC   LINE1+(DB1NAME-DB1)(8),ADSNAME
         MVC   LINE1+(DB1PORT-DB1)(4),TITLE_PORT
         MVC   LINE1+(DB1HOST-DB1)(24),HOSTNAME
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB1    DS    0H
*
*        USE THE DATE/TIME TO MAKE A UNIQUE SUBTASK ID....
*
*/*       SVC INVOCATIONS                                            */
*/*         THE TIME OF DAY IS RETURNED IN R0 FOR DEC, BIN, AND      */
*/*         TU. FOR MIC AND STCK, THE TIME OF DAY IS RETURNED        */
*/*         IN THE SPECIFIED ADDRESS.                                */
*/*         THE DATE IS RETURNED IN R1 AS PACKED DECIMAL IN THE      */
*/*         FORM CCYYDDDF, WHERE:                                    */
*/*              CC  - IS THE CENTURY                                */
*/*              YY  - IS THE LAST TWO DIGITS OF THE YEAR            */
*/*              DDD - IS THE DAY OF THE YEAR                        */
*/*              F   - ALLOWS FOR UNPACKING                          */
*/*                                                                  */
*/*       PC INVOCATIONS                                             */
*/*         THE TIME OF DAY IS RETURNED TO THE FIRST TWO WORDS OF    */
*/*         A USER SPECIFIED FOURWORD AREA.  THE DATE IS RETURNED    */
*/*         TO THE THIRD WORD IN DECIMAL FORMAT (NOT PACKED DECIMAL) */
*/*         IN THE FORMS GIVEN BELOW.                                */
*
*
TOD3     DS    0H                                                   RH
         TIME  DEC,SAVETIME,            GET THE SYS TIME            Y2 +
               LINKAGE=SYSTEM,DATETYPE=YYYYMMDD,    IN THIS CASE    Y2 +
               MF=(E,TIMED)
         XC    DWORD(8),DWORD           CLEAR DWORD                 Y2
         UNPK  DWORD(9),SAVETIME+8(5)      MAKE IT DECIMAL          Y2
         MVC   SUBTASK(8),DWORD         SET UP FOR EZASOKET
*
*/*******************************************************************/
*/* ESTABLISH TCP/IP WITH OTMAHWS?                                  */
*/*******************************************************************/
*
*CHECK_IMSTOC: PROC;
*
*       PUT FILE (SYSPRINT)
*                EDIT ('TESTING IMSTOC ',ADSNAME,', PORT=',
*                       PORT,', HOSTNAME=',HOSTNAME)
*              (SKIP(1), A, A, A, F(4), A, A) ;
*
         MVC   LINE1(MSG1LTH),MSG1 PRINT INFO MSG                    RH
         MVC   LINE1+(MS1NAME-MSG1)(8),ADSNAME
         MVC   LINE1+(MS1PORT-MSG1)(4),TITLE_PORT
         MVC   LINE1+(MS1HOST-MSG1)(24),HOSTNAME
         BAL   R8,PRINT2          PRINT MSG                         RH
*
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (INITAPI,MAXSOC,IDENT,SUBTASK,MAXSNO,ERRNO,RETCODE),VL, +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    INITOK             IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADINIT),BADINIT PRINT INFO MSG              RH
         LA    R14,LINE1+(INITRC-BADINIT)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     NOMORE             END FOR NOW
*
**********************************************************************
*
*              CONVERT RETCODE AND ERRNO TO DECIMAL
*
**********************************************************************
*
*              CONVERT RETCODE TO DISPLAY
*
DECRC    DS    0H
         MVC   0(10,R14),=C'RETCODE = '  PUT ERR ID IN MSG
         LA    R14,10(0,R14)      R14 -> TO NBR GOES
         UNPK  WORKERR(9),RETCODE(5)
         MVI   WORKERR+8,X'EF'  TRANSLATES TO BLANK
         TR    WORKERR(9),TRTAB-239 TRANS TO DISPLAY
         LA    R15,WORKERR        R15 -> AT ERROR CODE
         LA    R1,8               R1 = MAX CHARS TO CHECK
*
DEC10    DS    0H
         CLI   0(R15),C'0'        LEADING 0?
         BNE   DEC11              NO
         MVI   0(R15),C' '        YES, BLANK IT OUT
*
DEC11    DS    0H
         CLI   0(R15),C' '        LEADING BLANK?
         BNE   DEC20              NO
         LA    R15,1(0,R15)       R15 -> AT NEXT BYTE
         BCT   R1,DEC10           GO CHECK IT
         MVI   0(R15),C'0'        ALL BLANK, SO RC=0...
*
DEC20    DS    0H
         CH    R1,=H'1'           MORE THAN 1 CHAR?
         BL    DEC21              NO, DON'T SUBTRACT 1
         BCTR  R1,0               R1 = HEX LTH TO MOVE
*
DEC21    DS    0H
         EX    R1,MVCRC1          PUT RC IN MSG
         LA    R14,1(R1,R14)      R14 -. WHERE ( GOES...
         CLI   0(R15),C'F'        FIRST CHAR F?
         BNE   DEC22              NO, ON WE GO
         MVC   0(3,R14),=C'(<1'   PUT IN (<1
         LA    R14,3(0,R14)       R14 -> WHERE ) GOES...
         BE    DEC42              YES, NO DECIMAL NBR....
*
DEC22    DS    0H
         MVI   0(R14),C'('        PUT IN (
         LA    R14,1(0,R14)       R14 -> WHERE DECIMAL GOES...
         L     R15,RETCODE        R15 = ERROR CODE
         CVD   R15,DWORD          CONVERT RC TO DECIMAL
         MVC   WORKERR(6),EDPATN  PUT EDIT PATTERN IN MSG
         ED    WORKERR(6),DWORD+5 CONVERT TO DISPLAY
         LA    R15,WORKERR        R15 -> AT ERROR CODE
         LA    R1,6               R1 = MAX CHARS TO CHECK
*
DEC30    DS    0H
         CLI   0(R15),C'0'        LEADING 0?
         BNE   DEC31              NO
         MVI   0(R15),C' '        YES, BLANK IT OUT
*
DEC31    DS    0H
         CLI   0(R15),C' '        LEADING BLANK?
         BNE   DEC40              NO
         LA    R15,1(0,R15)       R15 -> AT NEXT BYTE
         BCT   R1,DEC30           GO CHECK IT
         MVI   0(R15),C'0'        ALL BLANK, SO RC=0...
*
DEC40    DS    0H
         CH    R1,=H'1'           MORE THAN 1 CHAR?
         BL    DEC41              NO, DON'T SUBTRACT 1
         BCTR  R1,0               R1 = HEX LTH TO MOVE
*
DEC41    DS    0H
         EX    R1,MVCRC1          PUT RC IN MSG
         LA    R14,1(R1,R14)      R14 -> WHERE ) GOES...
*
DEC42    DS    0H
         MVC   0(3,R14),=C'), '   PUT IN )
*
DEC43    DS    0H
         LA    R14,3(0,R14)       R14 -> WHERE ERRNO GOES...
*
*              CONVERT ERRNO TO DISPLAY
*
DEC50    DS    0H
         MVC   0(8,R14),=C'ERRNO = '  PUT ERR ID IN MSG
         LA    R14,8(0,R14)       R14 -> TO NBR GOES
         UNPK  WORKERR(9),ERRNO(5)
         MVI   WORKERR+8,X'EF'  TRANSLATES TO BLANK
         TR    WORKERR(9),TRTAB-239 TRANS TO DISPLAY
         LA    R15,WORKERR        R15 -> AT ERROR CODE
         LA    R1,8               R1 = MAX CHARS TO CHECK
*
DEC60    DS    0H
         CLI   0(R15),C'0'        LEADING 0?
         BNE   DEC61              NO
         MVI   0(R15),C' '        YES, BLANK IT OUT
*
DEC61    DS    0H
         CLI   0(R15),C' '        LEADING BLANK?
         BNE   DEC70              NO
         LA    R15,1(0,R15)       R15 -> AT NEXT BYTE
         BCT   R1,DEC60           GO CHECK IT
         MVI   0(R15),C'0'        ALL BLANK, SO RC=0...
*
DEC70    DS    0H
         CH    R1,=H'1'           MORE THAN 1 CHAR?
         BL    DEC71              NO, DON'T SUBTRACT 1
         BCTR  R1,0               R1 = HEX LTH TO MOVE
*
DEC71    DS    0H
         EX    R1,MVCRC1          PUT RC IN MSG
         LA    R14,1(R1,R14)      R14 -. WHERE ( GOES...
*
DEC72    DS    0H
         MVI   0(R14),C'('        PUT IN (
         LA    R14,1(0,R14)       R14 -> WHERE DECIMAL GOES...
         L     R15,ERRNO          R15 = ERROR CODE
         CVD   R15,DWORD          CONVERT RC TO DECIMAL
         MVC   WORKERR(6),EDPATN  PUT EDIT PATTERN IN MSG
         ED    WORKERR(6),DWORD+5 CONVERT TO DISPLAY
         LA    R15,WORKERR        R15 -> AT ERROR CODE
         LA    R1,6               R1 = MAX CHARS TO CHECK
*
DEC73    DS    0H
         CLI   0(R15),C'0'        LEADING 0?
         BNE   DEC74              NO
         MVI   0(R15),C' '        YES, BLANK IT OUT
*
DEC74    DS    0H
         CLI   0(R15),C' '        LEADING BLANK?
         BNE   DEC80              NO
         LA    R15,1(0,R15)       R15 -> AT NEXT BYTE
         BCT   R1,DEC73           GO CHECK IT
         MVI   0(R15),C'0'        ALL BLANK, SO RC=0...
*
DEC80    DS    0H
         CH    R1,=H'1'           MORE THAN 1 CHAR?
         BL    DEC81              NO, DON'T SUBTRACT 1
         BCTR  R1,0               R1 = HEX LTH TO MOVE
*
DEC81    DS    0H
         EX    R1,MVCRC1          PUT RC IN MSG
         LA    R14,1(R1,R14)      R14 -> WHERE ) GOES...
*
         MVI   0(R14),C')'        PUT IN )
*
DEC90    DS    0H
         BR    R8                 RETURN TO CALLER
*
EDPATN   DC    X'402020202120'
MVCRC1   MVC   0(0,R14),0(R15)    PUT RC IN MSG
*
INITOK   DS    0H
*
*   CALL EZASOKET('SOCKET          ',
*                 TWO,             /* I/P PARMS        */
*                 ONE,
*                 ZERO,
*                 ERRNO,
*                 RETCODE);
*
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (SOCKET,TWO,ONE,ZERO,ERRNO,RETCODE),VL,                 +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BNM   SOCKOK             IT'S ZERO OR GREATER...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADSOCK),BADSOCK PRINT INFO MSG              RH
         LA    R14,LINE1+(SOCKRC-BADSOCK)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
SOCKOK   DS    0H
         STH   R15,SOCDESC        SAVE IT FOR LATER
*
*   SOCDESC = RETCODE;
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*      PUT FILE (SYSPRINT) SKIP(1)
*          EDIT ('SOCKET SUCCESSFUL.  RETCODE=',RETCODE)
*             (SKIP(1), A, F(3)) ;
*      PUT SKIP(1) DATA (SOCDESC) ;
*   END ;
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB2              NO
         MVC   LINE1(24),=CL24'-SOCKET CALL SUCCESSFUL.'            RH
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB2    DS    0H
*
*   CALL EZASOKET('GETHOSTBYNAME   ',
*                  NAMELEN,
*                  HOSTNAME,
*                  HOSTENT,
*                  RETCODE);
*
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (GETHOST,NAMELEN,HOSTNAME,HOSTENT,RETCODE),VL,          +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    HOSTOK             IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   ERRNO,HOSTENT      ERROR IS IN HOSTENT???            RH
         MVC   LINE1(L'BADHOST),BADHOST SET UP ERROR MSG            RH
         LA    R14,LINE1+(HOSTRC-BADHOST)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
HOSTOK   DS    0H
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*      PUT FILE (SYSPRINT)
*          EDIT ('GETHOSTBYNAME SUCESSFUL')
*             (SKIP(2), A);
*   END ;
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB3              NO
         MVC   LINE1(26),=CL26'-GETHOSTBYNAME SUCCESSFUL.'
         BAL   R8,PRINT2          PRINT MSG
*
NODB3    DS    0H
*
*     CALL EZACIC08(HOSTENT,
*                    HOSTNAME_LENGTH,
*                    HOSTNAME_VALUE,
*                    HOSTALIAS_COUNT,
*                    HOSTALIAS_SEQ,
*                    HOSTALIAS_LENGTH,
*                    HOSTALIAS_VALUE,
*                    HOSTADDR_TYPE,
*                    HOSTADDR_LENGTH,
*                    HOSTADDR_COUNT,
*                    HOSTADDR_SEQ,
*                    HOSTADDR_VALUE,
*                    RETCODE);
*
         L     R15,CIC08AD        R15 -> TO EZACIC08 ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (HOSTENT,                                               +
               HOSTNAME_LENGTH,                                        +
               HOSTNAME_VALUE,                                         +
               HOSTALIAS_COUNT,                                        +
               HOSTALIAS_SEQ,                                          +
               HOSTALIAS_LENGTH,                                       +
               HOSTALIAS_VALUE,                                        +
               HOSTADDR_TYPE,                                          +
               HOSTADDR_LENGTH,                                        +
               HOSTADDR_COUNT,                                         +
               HOSTADDR_SEQ,                                           +
               HOSTADDR_VALUE,                                         +
               RETCODE),VL,                                            +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    HOSTOK3            IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADHOSTE),BADHOSTE SET UP ERROR MSG          RH
         LA    R14,LINE1+(HOSTRCE-BADHOSTE)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
HOSTOK3  DS    0H
         CLC   HOSTALIAS_COUNT,=H'1'   MORE THAN 1 ALIAS?
         BH    NODB3              YES, GO GET THEM ALL
         CLC   HOSTADDR_COUNT,=H'1' MORE THAN 1 HOST?
         BH    NODB3              YES, GO GET THEM ALL
*
         L     R15,HOSTADDR_VALUE R15 = HOST ADDRESS
         ST    R15,SERVIADD       SAVE IT FOR NEXT CALL
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*     PUT FILE (SYSPRINT) LIST ('EZACIC08 CALL:') SKIP(2);
*     PUT FILE (SYSPRINT) LIST(HOSTENT,
*                     HOSTNAME_LENGTH,
*                     HOSTNAME_VALUE,
*                     HOSTALIAS_COUNT,
*                     HOSTALIAS_SEQ,
*                     HOSTALIAS_LENGTH,
*                     HOSTALIAS_VALUE,
*                     HOSTADDR_TYPE,
*                     HOSTADDR_LENGTH,
*                     HOSTADDR_COUNT,
*                     HOSTADDR_SEQ,
*                     HOSTADDR_VALUE,
*                     RETCODE);
*   END ;
*
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB3A             NO
         MVC   LINE1(21),=CL21'-EZACIC08 SUCCESSFUL.'
         MVC   LINE1+21(14),=CL14' HOST ADDRESS='
         LA    R14,LINE1+35
         UNPK  0(9,R14),HOSTADDR_VALUE(5)
         MVI   8(R14),X'EF'  TRANSLATES TO BLANK
         TR    0(9,R14),TRTAB-239 TRANS TO DISPLAY
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB3A   DS    0H
*
*   NAME.SERVIADD = HOSTADDR_VALUE;
*
*   CALL EZASOKET('CONNECT         ',
*                 SOCDESC,          /* I/P PARMS        */
*                 NAME,
*                 ERRNO,
*                 RETCODE);
*
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (CONNECT,SOCDESC,NAME,ERRNO,RETCODE),VL,                +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    CONNOK             IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADCONN),BADCONN SET UP ERROR MSG            RH
         LA    R14,LINE1+(CONNRC-BADCONN)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
CONNOK   DS    0H
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*      PUT FILE (SYSPRINT) LIST ('CONNECT SUCCESSFUL') SKIP(2);
*      PUT FILE (SYSPRINT)
*          EDIT ('   SOCDESC=',SOCDESC,' BUFFLEN=',BUFFLEN)
*             (SKIP(1), A, F(3), A, F(3)) ;
*      PUT DATA(TRM) SKIP(1) ;
*   END ;
*
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB4              NO
         MVC   LINE1(20),=CL20'-CONNECT SUCCESSFUL.'
         BAL   R8,PRINT2          PRINT MSG                         RH
         MVC   LINE1(11),=CL11'   SOCDESC='
         LA    R14,LINE1+11
         UNPK  0(5,R14),SOCDESC(3)
         MVI   4(R14),X'EF'  TRANSLATES TO BLANK
         TR    0(5,R14),TRTAB-239 TRANS TO DISPLAY
         MVC   LINE1+16(10),=CL10' BUFFLEN='
         UNPK  LINE1+26(9),BUFFLEN(5)
         MVI   LINE1+34,X'EF'  TRANSLATES TO BLANK
         TR    LINE1+26(9),TRTAB-239 TRANS TO DISPLAY
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB4    DS    0H
*
*   CALL EZASOKET('WRITE           ',
*                 SOCDESC,          /* I/P PARMS        */
*                 BUFFLEN,
*                 HWSDATA,
*                 ERRNO,
*                 RETCODE);
*
*        LA    R15,IROLTH         SET BUFFER LENGTH
*        LA    R15,256            SET BUFFER LENGTH
*        LA    R15,HWSDATAL       SET BUFFER LENGTH
         LA    R15,INBUFL         SET BUFFER LENGTH
         ST    R15,BUFFLEN        SAVE IT
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (WRITE,SOCDESC,BUFFLEN,HWSDATA,ERRNO,RETCODE),VL,       +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BNM   WRIT1OK            IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADWRIT1),BADWRIT1 SET UP ERROR MSG          RH
         LA    R14,LINE1+(WRIT1RC-BADWRIT1)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
WRIT1OK  DS    0H
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*      PUT FILE (SYSPRINT) LIST ('WRITE1 SUCCESSFUL') SKIP(2);
*   END ;
*
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB5              NO
         MVC   LINE1(20),=CL20'-WRITE1 SUCCESSFUL. '
         BAL   R8,PRINT2          PRINT MSG                         RH
         LA    R14,LINE1+20       R14 -> WHERE RC GOES
         BAL   R8,DECRC           CONVERT RC TO DISPLAY             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB5    DS    0H
*
*   CALL EZASOKET('RECV            ',
*                 SOCDESC,          /* I/P PARMS        */
*                 FLAG,
*                 BUFFLEN,
*                 OUTBUFF,
*                 ERRNO,
*                 RETCODE);
*
         LA    R15,OUTBUFL        SET BUFFER LENGTH
         ST    R15,BUFFLEN        SAVE IT
*
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (RECV,SOCDESC,FLAG,BUFFLEN,OUTBUFF,ERRNO,RETCODE),VL,   +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    SOKCLOS            IT'S ZERO...SOCKET CLOSED.
         BNM   RECVOK             IT'S > THAN ZERO...OKAY...
*
RECVMSG  DS    0H
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADRECV),BADRECV SET UP ERROR MSG            RH
         LA    R14,LINE1+(RECVRC-BADRECV)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
SOKCLOS  DS    0H
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADSOCK2),BADSOCK2 SET UP ERROR MSG          RH
         LA    R14,LINE1+(SOCK2RC-BADSOCK2)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     CALLCLOS           END FOR NOW
*
RECVOK   DS    0H
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB8              NO
         MVC   LINE1(10),=CL10'-AT RECV. '
         LA    R14,LINE1+10
         BAL   R8,DECRC           CONVERT RC TO DISPLAY             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB8    DS    0H
         MVC   LINE1(RECVMSGL),RECVMSGD PUT MSG IN PRINT LINE       RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         BAL   R8,PRINT2          PRINT A BLANK LINE...             RH
         LA    R6,OUTBUFF         R6 -> AT OUTPUT DATA
*
RECVOK1  DS    0H
         SR    R8,R8              CLEAR R8
         ICM   R8,3,0(R6)         R8 = LTH OF DATA SEGMENT
         BZ    RECVOK8            IF 0, ALL DONE
*
RECVOK2  DS    0H
*        CH    R8,=H'132'         TOO MUCH DATA?
*        BNH   RECVOK3            NO
*        LA    R8,132             YES, PRINT 1ST 132
*
RECVOK3  DS    0H
         BCTR  R8,0               MAKE LTH HEX...
         EX    R8,MOVEDATA        PUT DATA IN PRINT LINE
*OVEDATA MVC   LINE1+2(0),4(R6)   PUT RECV DATA IN MSG              RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         CLC   =C'*REQSTS*',4(R6) BAD READ?
         BE    RECVOK4            YES, PRINT ERROR MSG
         CLC   =C'SECFAIL',4(R6)  SECURITY FAIL?
         BNE   RECVOK7            NO, LOOKS OKAY. ON WE GO
*
RECVOK4  DS    0H
         MVC   RETCODE,12(R6)     YES, SET UP RETCODE
         MVC   ERRNO,16(R6)       YES, SET UP ERRNO
         B     RECVMSG            GO PRINT THEM
*
RECVOK7  DS    0H
         SR    R8,R8              CLEAR R8
         ICM   R8,3,0(R6)         R8 = LTH OF DATA SEGMENT
         AR    R6,R8              R6 -> AT NEXT DATA SEGMENT
         B     RECVOK1            GO PRINT IT
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*       PUT FILE (SYSPRINT)
*           EDIT ('AT RECV. ERRNO=',ERRNO,' RETCODE=',RETCODE)
*             (SKIP(1), A, F(3), A, F(3)) ;
*   END ;
*
*
RECVOK8  DS    0H
*
*   CALL EZASOKET('CLOSE           ',
*                 SOCDESC,          /* I/P PARMS        */
*                 ERRNO,
*                 RETCODE);
*
CALLCLOS DS    0H
         L     R15,SOKETAD        R15 -> TO EZASOKET ROUTINE
         LA    R1,CALLD           R1 -> TO CALL LIST
*                                 CALL IT
         CALL  (15),                                                   +
               (CLOSE,SOCDESC,ERRNO,RETCODE),VL,                       +
               MF=(E,(R1))
*
         ICM   R15,15,RETCODE     R15 = RETURN CODE
         BZ    CLOSOK             IT'S ZERO...OKAY...
*
         LA    R15,16             SET BAD RETURN CODE
         ST    R15,CPKECB         ''
         MVC   LINE1(L'BADCLOS),BADCLOS SET UP ERROR MSG            RH
         LA    R14,LINE1+(CLOSRC-BADCLOS)
         BAL   R8,DECRC           CONVERT RC TO DECIMAL             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
         B     NOMORE             END FOR NOW
*
CLOSOK   DS    0H
*
*   IF TITLE_DEBUG   = '  ' THEN  DO ;   /* IF FLAG IS BLANK,       */
*       PUT FILE (SYSPRINT)
*           EDIT ('AT CLOSE. ERRNO=',ERRNO,' RETCODE=',RETCODE)
*             (SKIP(1), A, F(3), A, F(3)) ;
*   END ;
         CLI   TITLE_DEBUG,C' '   PRINT DEBUG INFO?
         BNE   NODB10             NO
         MVC   LINE1(11),=CL11'-AT CLOSE. '
         LA    R14,LINE1+11
         BAL   R8,DECRC           CONVERT RC TO DISPLAY             RH
         BAL   R8,PRINT2          PRINT MSG                         RH
*
NODB10   DS    0H
*
NOMORE   DS    0H
         BAL   R8,PRINT2          PRINT A BLANK LINE...             RH
         MVC   LINE1,LINE2        SET UP END MSG                    RH
         BAL   R8,PRINT2          SAY END OF CHKOTMA                RH
*
*        MAKE SURE WE ARE IN 24 BIT MODE ADDRESSING
*
*        LA    R1,*+4+2           GET BACK INTO 24 BIT MODE
*        BSM   0,R1               ''
*
         LA    R2,PRINTDCB        R2 -> TO SYSPRINT DCB
*        LA    R2,SYSPRINT        R2 -> TO SYSPRINT DCB
         CLI   48(R2),X'40'       DCB SCREWED?                      RH
         BNE   CLOSE2             NO, DON'T TRY TO CLOSE IT         RH
         DC    H'0'
         SPACE 1
CLOSE2   DS    0H
         TM    48(R2),X'10'       DID THE SYSPRINT FILE OPEN?       RH
         BZ    CLOSE3             NO, DON'T TRY TO CLOSE IT         RH
*        CLOSE (R2),MF=(E,DSCLOSE)                             DBUG RH
         CLOSE (R2),MODE=24,MF=(E,DSCLOSE)
         SPACE 1
CLOSE3   DS    0H
         AGO   .NODEL
*
*              USE THE FOLLOWING  CODE TO DELETE DYNAMICALLY LOADED
*              MODULES
*
*              DELETE EZASOKET AND EZACIC08
*
         L     R4,SOKETAD         R4 -> TO MODULE NAME
         DELETE EPLOC=((R4))      DELETE PGM...
*
         L     R4,CIC08AD         R4 -> TO MODULE NAME
         DELETE EPLOC=((R4))      DELETE PGM...
*
         SPACE 1
.NODEL   ANOP
         L     R1,CPKECB          R1 = RETURN CODES
         LA    R1,0(0,R1)         CLEAR HIGH BYTE
         SPACE 1
         L     R13,SAVE+4         POINT R13 BACK TO CALLER SAVEAREA
         ST    R1,16(0,R13)       PASS RC BACK TO CALLING PLI PGM
         FREEMAIN R,LV=LDYNAMIC,A=((R10))  FREE GOTTEN CORE
         LM    R14,R12,12(R13)    RELOAD CALLERS REGS
*              GET INTO PROPER MODE AND GO BACK TO CALLER
         BSM   0,R14              RETURN TO CALLING PROGRAM
         SPACE 1
SETNAME  MVC   TITLE_IMSTOC(0),0(R2) SET UP IMSTOC NAME
SETPORT  MVC   TITLE_PORT(0),0(R2) SET UP IMSTOC PORT
MOVEDATA MVC   LINE1+2(0),4(R6)      PUT READ DATA IN MSG           RH
         SPACE 1                                                    RH
*
*              PRINT ROUTINE
*
PRINT2   DS    0H                                                   RH
         LA    R2,PRINTDCB        R2 -> TO SYSPRINT DCB
*        LA    R2,SYSPRINT        R2 -> TO SYSPRINT DCB
         TM    48(R2),X'10'       DID SYSPRINT DATASET OPEN         RH
         BNO   NOPRINT2           NO, CAN'T PRINT INFO MSG THEN     RH
*
*        GO TO 24 BIT MODE ADDRESSING FOR PRINTING
*
*        LA    R1,*+4+2           GET BACK INTO 24 BIT MODE
*        BSM   0,R1               ''
*
         PUT   (R2),LINE1         PRINT LINE                        RH
*
*        NOW GO BACK TO 31 BIT MODE ADDRESSING
*
*        CNOP  0,4                PUT ON FULLWORD BOUNDARY
*        L     R1,*+8             GET 31 BIT MODE ADDRESS
*        BSM   0,R1               GET INTO 31 BIT MODE
*        DC    H'0'               ALIGN ON A FULLWORD BOUNDARY
*        DC    X'80',AL3(*+3)     NEXT ADDRESS IS 31 BIT MODE
*
NOPRINT2 DS    0H                                                   RH
         MVI   LINE1+1,C' '       CLEAR LINE 1                      RH
         MVC   LINE1+2(131),LINE1+1  *                              RH
         SPACE 1                                                    RH
         BR    R8                 RETURN                            RH
         SPACE 3
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*        WORK AREAS
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
DUMPTITL DC    C'WORKING STORAGE SECTION'
         SPACE 1                                               DBUG RH
BFLAG    DC    X'00'
BFLAG80  EQU   X'80'              THIS IS A BATCH JOB
         ENTRY ECB,IOPLADD
ECB      DC    F'0'
IOPLADD  DC    A(*-*)                   ADDRESS OF THE UPT
         DC    A(*-*)                   ADDRESS OF THE ECT
         DC    A(MSG_ECB)               ADDRESS OF THE ECB
         DC    A(*-*)                   ADDRESS OF THE --PB
         SPACE 1                                               DBUG RH
SOKETAD  DC    V(EZASOKET)
CIC08AD  DC    V(EZACIC08)
EZASOKET DC    CL8'EZASOKET'
EZACIC08 DC    CL8'EZACIC08'
TRTAB    DC    C' 0123456789ABCDEF'
         SPACE 1                                               DBUG RH
PASSWORD DC    AL1(0),CL8' '
         SPACE 1
GETPSWD  DC    AL2(LGETPSWD,0)
         DC    C'ENTER PASSWORD FOR '
USERID   DC    CL7' '
LGETPSWD EQU   *-GETPSWD
         SPACE 3
OLDNAME  DC    CL8' '
RACPSWD  DC    CL11' '            RACF PASSWORD                     RH
         SPACE 1
MSG_PGPB PUTGET MF=L,                                                  +
               OUTPUT=(MSG_ADDR,SINGLE,PROMPT)
         SPACE 2
MSG_ADDR DC    A(1)
         DC    3A(*-*)              ADDRESS OF THE MESSAGE TEXT
         SPACE 2
MSG_ECB  DC    A(0)
         SPACE 1                                               DBUG RH
HEADINFO DS    0CL133
         DC    CL2'1'
         DC    CL72' '
         ORG   *-72
         DC    C'DIVERSIFIED DEVELOPMENTS IMSTOC/OTMA CHECKER '
         DC    C'UTILITY, VERSION 1.0'
         ORG
         DC    CL(133-(*-HEADINFO))' '
         SPACE 1
LINE2    DC    CL133'- END OF IMSTOC/OTMA CHECKER...'
*
DB1      DC    C'- ADSNAME='
DB1NAME  DC    CL8' '
         DC    C', PORT='
DB1PORT  DC    CL4' '
         DC    C', HOSTNAME='
DB1HOST  DC    CL24' '
DB1LTH   EQU   *-DB1
*
RECVMSGD DC    C'-RECV DATA FOR WHOAMI TRAN IS...'                  RH
RECVMSGL EQU   *-RECVMSGD
*
BADRECV  DC    C'- RECV CALL FAILED. '
RECVRC   DC    CL8' '
*
BADCLOS  DC    C'- CLOSE CALL FAILED. '
CLOSRC   DC    CL8' '
*
BADWRIT1 DC    C'- WRITE1 CALL FAILED. '
WRIT1RC  DC    CL8' '
*
BADCONN  DC    C'- CONNECT CALL FAILED. '
CONNRC   DC    CL8' '
*
BADINIT  DC    C'- INITAPI CALL FAILED. '
INITRC   DC    CL8' '
*
BADHOST  DC    C'- GETHOSTBYNAME CALL FAILED. '
HOSTRC   DC    CL8' '
*
BADHOSTE DC    C'- INVALID HOSTENT ADDRESS. '
HOSTRCE  DC    CL8' '
*
BADSOCK  DC    C'- SOCKET CALL FAILED. '
SOCKRC   DC    CL8' '
*
BADSOCK2 DC    C'- RECV CALL FAILED. SOCKET CLOSED. '
SOCK2RC  DC    CL8' '
*
MSG1     DC    C'- TESTING IMSTOC '
MS1NAME  DC    CL8' '
         DC    C', PORT='
MS1PORT  DC    CL4' '
         DC    C', HOSTNAME='
MS1HOST  DC    CL24' '
MSG1LTH  EQU   *-MSG1
         SPACE 1                                                    RH
OPENMAC  OPEN  (PRINTDCB,(OUTPUT)),MF=L
OPENLTH  EQU   *-OPENMAC
         SPACE 1
CLOSMAC  CLOSE PRINTDCB,MF=L                                   DBUG RH
CLOSLTH  EQU   *-CLOSMAC
         SPACE 1
         DC    F'0'
PRINTDCB DCB   DSORG=PS,DDNAME=SYSPRINT,LRECL=133,RECFM=FBA,   XXXXXXXXX
               MACRF=(PM),BLKSIZE=3990
PDCBLTH  EQU   *-PRINTDCB                                      DBUG RH
*
CLOSE    DC    CL16'CLOSE          '
WRITE    DC    CL16'WRITE          '
READ     DC    CL16'READ           '
RECV     DC    CL16'RECV           '
CONNECT  DC    CL16'CONNECT        '
GETHOST  DC    CL16'GETHOSTBYNAME  '
SOCKET   DC    CL16'SOCKET         '
INITAPI  DC    CL16'INITAPI        '
*
MAXSOC   DC    H'55'
MAXSNO   DC    F'54'
THREE    DC    F'3'   FIXED BIN (31) INIT (3);
TWO      DC    F'2'   FIXED BIN (31) INIT (2);
ONE      DC    F'1'   FIXED BIN (31) INIT (1);
ZERO     DC    F'0'   FIXED BIN (31) INIT (0);
         SPACE 1
               SPACE 1
***********************************************************************
*  IRM   V2.1.3 IMS REQUEST MESSAGE
*                                                                    *
*        INPUT: THE DATA SENT TO TOC/OTMA LOOKS AS FOLLOWS:
*
*        |LLLL|IRM|LLZZDATA|LLZZDATA|LLZZDATA...|00040000 WHERE
*
*        LLLL     = FULLWORD FIELD CONTAINING TOTAL LEN OF ALL DATA
*        IRM      = THE STRUCTURE IRM(SEE HWSIMSCB FOR MAPPING)
*        LLZZDATA = USER DATA FROM THE CLIENT DESTINED FOR IMS TRAN
*        00040000 = TERMINATING STRING FROM CLIENT
*
***********************************************************************
HWSDATA        DS   0F            DATA SENT TO HWS
*
INBUFF         DS   0C            IMS REQUEST MESSAGE BUFFER
IRM_TOTLEN     DC   A(INBUFL)     LENGTH OF BUF, INCLUDING THIS FIELD
*
IRM_FIXED32    DS   0CL32         TCPIP OTMA 32 BYTE HEADER
IRMMASK        DS   0F            IMS REQUEST MESSAGE DSECT
IRM_LEN        DC   AL2(IRMMASK_LEN) LTH OF IRM, INCLUDING RACF DATA
IRM_RSV        DC   H'0'          RESERVED
IRM_ID         DC   CL8'*SAMPLE*' TRM ID '*IRMREQ*' ASCII/EBCDIC
IRM_RES_WORD   DC   F'0'          RESERVED WORD
IRM_F5         DC   X'00'         FLAG 5 - MESSAGE STRUCTURE
IRM_F5_OTMA    EQU  X'80'         OTMA HEADERS BUILT BY CLIENT
IRM_F5_TRANS   EQU  X'40'         TRANSLATION PERFORMED BY CLIENT
IRM_RSV02      DC   3X'00'        RESERVED FOR FUTURE USE
IRM_CLIENTID   DC   CL8'      '   8 BYTE UNIQUE CLIENT ID - IF
*                                 BLANK, EXIT WILL BUILD IT
*                                 END OF 32 BYTE HEADER
IRM_F1         DC   X'00'         FLAG 1
IRM_F1_MFSREQ  EQU  X'80'         USER REQUESTS MFS MOD NAME RETURNED
IRM_F2         DC   X'20'         FLAG 2
IRM_F2_CMODE0  EQU  X'40'         CLIENT REQUESTS COMMIT MODE 0
IRM_F2_CMODE1  EQU  X'20'         CLIENT REQUESTS COMMIT MODE 1
IRM_F3         DC   X'00'         FLAG 3
IRM_F3_CONFIRM EQU  X'01'         CLIENT REQ SYNCH LEVEL CONFIRM
*                                 IF COMMIT MODE = 0 THEN
*                                    SYNCH LEVEL MUST BE "CONFIRM"
*                                 IF COMMIT MODE = 1 THEN
*                                    SYNCH LEVEL CAN BE "CONFIRM"
*                                    OR "NONE"
IRM_F4         DC   C' '          FLAG 4 - CONVERSATION FLAG
IRM_F4_ACK     EQU  C'A'          CLIENT ACK WHEN MSG RECEIVED
*                                 SUCCESSFULLY AND CONV TO CONTINUE
IRM_F4_NACK    EQU  C'N'          CLIENT NEG ACK WHEN MSG RECEIVED
*                                 BUT CLIENT WANTS CONV ABORTED
IRM_F4_DEALLOC EQU  C'D'          CLIENT WISHED TO DEALLOC CONNECTION
*
IRM_F4_RESUMET EQU  C'R'          RESUME OUTPUT FOR TPIPES
*
IRM_F4_SENDONLY EQU  C'S'         SENDONLY WORK WITH COMMIT MODE 0 ONLY
*
IRM_TRNCOD     DC   CL8'WHOAMI '  IMS TRAN CODE
IRM_IMSDESTID  DC   CL8'IMSDEVL'  IMS DESTINATION ID FOR THIS TRAN CODE
IRM_LTERM      DC   CL8'       '  USER PROVIDED LTERM OR BLANKS
*                                 DEFAULT IS IRM_IMSDESTID....      RH
*
IRM_USRDAT      DS   0C      BEGINNING OF USER SECURITY DATA - OPTIONAL
IRM_RACF_USERID DC  CL8'RH87944'   RACF USERID - OPTIONAL
IRM_RACF_GRNAME DC  CL8'IMSVS'     RACF GROUP NAME - OPTIONAL
IRM_RACF_PW     DC   CL8'      '   RACF PASSWORK - OPTIONAL
IRMMASK_LEN    EQU  *-IRMMASK     SIZE OF REQUIRED IRM
*
IRM_LEN2       DC   AL2(IRMLTH2)  FIRST DATA SEGMENT
IRM_RSV2       DC   AL2(0)        RESERVED
IRM_TRNCOD2    DC   CL8'WHOAMI '  IMS TRAN CODE
IRMLTH2  EQU   *-IRM_LEN2         TOTAL LTH
*
IRM_LEN3       DC   AL2(IRMLTH3)  2ND SEGMENT, IS ANY
IRM_RSV3       DC   AL2(0)        RESERVED
IRM_DATA3      DC   CL1' '        IMS TRAN CODE
IRMLTH3  EQU   *-IRM_LEN3         TOTAL LTH
*
IRM_LEN9       DC   XL2'0004'     END OF MSG....
IRM_RSV9       DC   AL2(0)        RESERVED
DATALTH9 EQU   *-IRM_LEN2         TOTAL LTH
*
INBUFL   EQU   *-INBUFF           INPUT BUFFER LTH
HWSDATAL EQU   *-HWSDATA          LTH OF DATA SENT TO HWS
*
*
*              END OF DATA TO OTMA EXIT....
*
*
         LTORG
         SPACE 1
*
******** ***** ****************** ****************************
*
*              DSECT USED FOR ALL DATA ACCESS...TO BE RE-ENTRANT
*
******** ***** ****************** ****************************
WORK     DSECT
SAVE     DC    18F'-1'
WORKINIT DS    0H                 START INIT HERE...
CPKECB   DS    F                  RETURN CODE
CPPLADDR DC    F'0'
         SPACE 1                                               DBUG RH
DSOPEN   OPEN  (2,(OUTPUT)),MF=L
         SPACE 1
DSCLOSE  CLOSE (R2),MF=L                                       DBUG RH
         SPACE 1
LINE1    DC    CL133'- '   THIS IS USED FOR DEBUGGING.
         SPACE 1                                                    RH
         DC    F'0'
SYSPRINT DCB   DSORG=PS,DDNAME=SYSPRINT,LRECL=133,RECFM=FBA,   XXXXXXXXX
               MACRF=(PM),BLKSIZE=3990
         SPACE 1                                               DBUG RH
TITLE_IMSTOC DS CL8                 INIT('OTMAHWSA') ;
TITLE_PORT     DS CL4               INIT('7010') ;
TITLE_DEBUG    DS CL2               INIT('NO') ;
*
WORKERR  DS    CL10               FOR TRANSLATING ERROR CODES
*
*SOKETAD DS    F      FIXED BIN(31); USE IF LOAD PGM, NOT INCLUDE
*CIC08AD DS    F      FIXED BIN(31); USE IF LOAD PGM, NOT INCLUDE
ERRNO    DS    F      FIXED BIN(31);
RETCODE  DS    F      FIXED BIN(31);
BUFFLEN  DS    F      FIXED BIN(31);
FLAG     DS    F      FIXED BIN(31) INIT(0);
HOSTNAME DS    CL24   CHAR(24) INIT('BCSC01A');
NAMELEN  DS    F      FIXED BIN(31) INIT(7);
HOSTENT  DS    F      FIXED BIN(31) INIT(0);
HOSTNAME_LENGTH DS H  FIXED BIN(15) INIT(0);
HOSTNAME_VALUE  DS CL255  CHAR(255) INIT(' ');
HOSTALIAS_COUNT DS H  FIXED BIN(15) INIT(0);
HOSTALIAS_SEQ   DS H  FIXED BIN(15) INIT(0);
HOSTALIAS_LENGTH DS H FIXED BIN(15) INIT(0);
HOSTALIAS_VALUE DS CL255  CHAR(255) INIT(' ');
HOSTADDR_TYPE   DS H  FIXED BIN(15) INIT(0);
HOSTADDR_LENGTH DS H  FIXED BIN(15) INIT(0);
HOSTADDR_COUNT  DS H  FIXED BIN(15) INIT(0);
HOSTADDR_SEQ    DS H  FIXED BIN(15) INIT(0);
HOSTADDR_VALUE  DS F  FIXED BIN(31) INIT(0);
*
IDENT    DS    0C
TCPNAME  DS    CL8    CHAR(08),
ADSNAME  DS    CL8    CHAR(08);
*
NAME     DS    0H
FAMILY   DS    H      FIXED BIN (15) INIT (2),
PORT     DS    H      FIXED BIN (15) INIT (3083),
SERVIADD DS    F      FIXED BIN (31) INIT (0),
RESERVED DS    CL8    CHAR(08);
*
SUBTASK  DS    CL8    CHAR(08);
*
SOCDESC  DS    H      FIXED BIN (15) INIT (0);
*
DWORD    DS    D
SAVETIME DC    XL16'00'                                             Y2
*
CALLD    CALL  ,(0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0),MF=L
*
TIMED    TIME  LINKAGE=SYSTEM,MF=L      GET THE SYS TIME            Y2
*
OUTBUFF  DS    CL512              OUTPUT BUFFER
OUTBUFL  EQU   *-OUTBUFF          OUTPUT BUFFER LTH
LINIT    EQU   *-WORKINIT         LTH OF INIT AREA
LDYNAMIC EQU   *-WORK             LTH OF WORK DSECT
         SPACE 1
*        HWSIMSCB
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         PRINT GEN
         IKJCPPL
         IKJIOPL
         IKJPSCB
         IKJPGPB
*
         IHAACEE
         IHAPSA
         IHAASCB
         IHAASXB
*
         END   CHKOTMA            END OF CHKOTMA
/*
//*
//LKED.SYSLMOD DD DSN=HALL.LINKLIB,DISP=SHR
//INCLUD       DD DISP=SHR,DSN=TCPIP.SEZATCP
//LKED.SYSIN DD *
  INCLUDE INCLUD(EZASOKET,EZACIC08)
  ENTRY CHKOTMA
  SETCODE AC(0)
  MODE   AMODE(31) RMODE(24)
  NAME CHKOTMA(R)
/*
//
