         MACRO ,
         MACLIST &LIST                                 NEW 2004.234 GYP
.*--------------------------------------------------------------------*
.*   MACLIST IS USED TO COMPENSATE FOR ASSEMBLER F/XF INABILITY TO    *
.*     PROVIDE N'&LIST(&I,&N) SUPPORT.                                *
.*   THIS MACRO STRIPS UP TO 10 INNER VALUES                          *
.*   E.G.       MYMAC OPTS=(A,(B1,B2,B3),(C1,C2))                     *
.*    HLASM N'&OPTS(2) IS 3, N'&OPTS(3) IS 2                          *
.*    XFASM FAILS ALL BUT N'&OPTS                                     *
.*                                                                    *
.*    USE:   MACLIST &OPTS(2)                                         *
.*    RESULTS RETURNED IN &MACP1-&MACP10, COUNT IN &MACP#             *
.*--------------------------------------------------------------------*
         GBLA  &MACP#        NUMBER OF (SUB)LIST ARGUMENTS
         GBLC  &MACP1,&MACP2,&MACP3,&MACP4,&MACP5
         GBLC  &MACP6,&MACP7,&MACP8,&MACP9,&MACP10
         LCLA  &I,&J,&K,&N
         LCLC  &STR,&LAB
         LCLC  &VAL(10)
&STR     SETC  '&LIST'
&J       SETA  1             OUTPUT SUBSCRIPT
&K       SETA  K'&LIST
         AIF   (&K LT 1).DONE
         AIF   ('&STR'(1,1) NE '(' OR '&STR'(&K,1) NE ')').NOPARS
&STR     SETC  '&STR'(2,&K-2)
&K       SETA  K'&STR
.NOPARS  AIF   (&I GE &K).DONE
&I       SETA  &I+1
         AIF   ('&STR'(&I,1) EQ ',').NEWPRM
&VAL(&J) SETC  '&VAL(&J)'.'&STR'(&I,1)
         AGO   .NOPARS
.NEWPRM  AIF   (&J GE 10).TOOMANY
&J       SETA  &J+1
         AGO   .NOPARS
.TOOMANY MNOTE 8,'MACLIST SUPPORTS MAX OF 10 SUBLIST ITEMS'
.DONE    ANOP  ,
&MACP#   SETA  &J            GET NUMBER OF OPERANDS
&MACP1   SETC  '&VAL(1)'
&MACP2   SETC  '&VAL(2)'
&MACP3   SETC  '&VAL(3)'
&MACP4   SETC  '&VAL(4)'
&MACP5   SETC  '&VAL(5)'
&MACP6   SETC  '&VAL(6)'
&MACP7   SETC  '&VAL(7)'
&MACP8   SETC  '&VAL(8)'
&MACP9   SETC  '&VAL(9)'
&MACP10  SETC  '&VAL(10)'
.MEND    MEND  ,
