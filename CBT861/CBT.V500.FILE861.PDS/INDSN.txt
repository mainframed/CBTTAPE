         MACRO ,
&NM      INDSN &DSN=DRDSN,&OUT=R1,&LEN=R2,&LV=44,&IN=R5,&SAVE=   84289
         LCLA  &I,&LONG
         LCLB  &SV                                              GP01013
&I       SETA  &SYSNDX
&SV      SETB  ('&SAVE' EQ 'Y')                                 GP01013
&SV      SETB  (&SV OR '&SAVE' EQ 'YES')                        GP01013
&NM      MACPARM R14,&DSN
         MACPARM R15,&LV
         MACPARM R1,(&IN)
         AIF   (NOT &SV).NSAV1                                  GP01013
         MACPARM R10,0(R13),OP=ST                               GP01013
.NSAV1   MACPARM R10,15,EXRESAD,OP=ICM,MODE=THREE  RESIDENT MODULE ?
         BZ    ZZI&I.A       NO; JUST CHECK SYS1
         BALS  R9,20(,R10)   CALL LPA RESIDENT FUNCTION          93046
         B     ZZI&I.L       SKIP AROUND IF PREFIX FOUND
ZZI&I.A  DS    0H                                                93046
         AIF   (NOT &SV).NSAV2                                  GP01013
         MACPARM R10,0(R13),OP=L                                GP01013
.NSAV2   AIF   (NOT &LONG).SYS                                   93046
         CLI   0(R1),C'.'    UID PREFIX ?                        93002
         BE    ZZI&I.U       YES                                 93002
         CLI   0(R1),C';'    UID+ACCT PREFIX ?                   93002
         BE    ZZI&I.U       YES                                 93002
         CLI   0(R1),C'Â¢'    ACCOUNT ONLY ?                      93002
         BE    ZZI&I.N       YES                                 93002
.SYS     CLI   0(R1),C'&&'   SPECIAL INPUT NAME ?
         BNE   ZZI&I.L       NO
         MVC   0(4,R14),=C'SYS1' MOVE INDEX LEVEL                93002
         B     ZZI&I.4       JOIN COMMON                         93002
         AIF   (NOT &LONG).SYSC                                  93046
ZZI&I.U  CLI   EXWUID,C' '   IS THERE A USER ID ?                93002
         BNH   ZZI&I.L       NO; FAIL                            93002
         MVC   0(8,R14),EXWUID  MOVE USER ID                     93002
         LA    R0,8          SET LENGTH TO SKIP                  93002
         ST    R14,12(,R13)  SAVE START                          93002
         AR    R14,R0        PAST LAST BYTE                      93002
ZZI&I.V  BCTR  R14,0         BACK-UP ONE                         93002
         CLI   0(R14),C' '   TRAILING GUNK ?                     93002
         BH    ZZI&I.W       NO                                  93002
         BCT   R0,ZZI&I.V                                        93002
ZZI&I.W  L     R14,12(,R13)  RESTORE START                       93002
         CLI   0(R1),C';'    UID PLUS ACCOUNT ?                  93002
         BNE   ZZI&I.X       NO; GO TO COMMON EXIT               93002
         CLI   EXWACCT,C' '  ANY ACCOUNT ?                       93002
         BNH   ZZI&I.X       NO; GO TO COMMON                    93002
         AR    R14,R0        GET END AGAIN                       93002
         MVI   0(R14),C'.'   MAKE INDEX POINT                    93002
         MVC   1(8,R14),EXWACCT  MOVE ACCOUNT                    93002
ZZI&I.9  LA    R14,9(R14,0)  MAKE HALF-WORD CONSTANT             93002
         AH    R0,ZZI&I.9+2  SET NEW LENGTH                      93002
ZZI&I.C  BCTR  R14,0         BACK-SPACE ONE                      93002
         CLI   0(R14),C' '   ANY THERE THERE ?                   93002
         BH    ZZI&I.D       YES                                 93002
         BCT   R0,ZZI&I.C    GO AGAIN                            93002
ZZI&I.D  L     R14,12(,R13)  RESTORE START                       93002
         B     ZZI&I.X       ADD INDEX POINT, ETC.               93002
ZZI&I.N  CLI   EXWACCT,C' '   ANY ACCOUNT ?                      93002
         BNH   ZZI&I.L       NO; WILL FAIL                       93002
         MVC   0(4,R14),EXWACCT  MAKE MAJOR ACCOUNT              93002
.SYSC    ANOP  ,                                                 93046
ZZI&I.4  LA    R0,4          SET LENGTH TO FOUR                  93002
ZZI&I.X  LA    R1,1(,R1)     SKIP OVER SPECIAL CHARACTER
         AR    R14,R0        ADJUST OUTPUT ADDRESS
         SR    R15,R0        ADJUST OUTPUT LENGTH
         MVI   0(R14),C'.'   MAKE INDEX POINT                    93002
         LA    R14,1(,R14)   SKIP IT                             93002
         BCTR  R15,0         AND SET FINAL LENGTH                93002
ZZI&I.L  MACPARM &IN,(R1)    RESTORE INPUT ADDRESS REGISTER
         MACPARM &OUT,(R14)  AND OUTPUT
         MACPARM &LEN,(R15)  AND RESIDUAL LENGTH
         AIF   (NOT &SV).NSAV3                                  GP01013
         MACPARM R10,0(R13),OP=L                                GP01013
.NSAV3   MEND  ,                                                GP01013
