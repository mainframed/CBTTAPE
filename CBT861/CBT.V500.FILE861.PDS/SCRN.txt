         MACRO
&L       SCRN  &DUMMY,&WIDTH=                                    89298
.*
.*             COPYRIGHT 1978 BY SHMUEL (SEYMOUR J.) METZ
.*                        ALL RIGHTS RESERVED.
.*
.*             THIS MACRO IS NOT TO BE DISTRIBUTED WITHOUT PERMISSION,
.*             AS DESCRIBED IN MEMBER $$RIGHTS.
.*
.*             ANYONE MAKING ENHANCEMENTS IS REQUEST TO FORWARD THEM
.*             TO ME, IDENTIFIED BY "** XXX **' IN COLUMN 63,
.*             WHERE XXX IS THE SHARE INSTALLATION CODE.
.*                                                               89298
.*       PERFORMANCE ENHANCEMENT (?) - LINESZE=80 REPLACED BY    89298
.*       WIDTH= AND LINESZE MADE GLOBAL. REQUIRES ONLY ONE WIDTH 89298
.*       OVERRIDE FOR AN EXTENDED SERIES OF MACROS.  *GYP*       89298
.*       FIX FOR 'RA' ORDER POSITIONING.             *GYP*       89300
.*
.*
         GBLA  &ROW,&COL,&LINESZE,&SCRNRAF                       89300
         GBLB  &TAB70IN                                          87070
         GBLC  &TAB3270(64)                                      87070
         LCLA  &I,&I1,&I2,&J,&K
         LCLB  &LN40,&LN64,&LN80,&STARTPR,&SAL,&RSTKBY,&RSTMDT
         LCLB  &PROT,&NUMERIC,&DETCT,&INT,&MDT                   81328
         LCLB  &NONDISP,&SKIP      COMPOSITES                    81328
         LCLB  &OK
         LCLC  &LABEL,&LABEL1,&CH1                               87070
&LABEL   SETC  '&L'
         AIF   (&TAB70IN).TABINIT                                87070
&LINESZE SETA  80            DEFAULT SCREEN WIDTH                89298
&TAB3270(1)   SETC  ' '                                          89271
&TAB3270(2)   SETC  'A'                                          89271
&TAB3270(3)   SETC  'B'                                          89271
&TAB3270(4)   SETC  'C'                                          89271
&TAB3270(5)   SETC  'D'                                          89271
&TAB3270(6)   SETC  'E'                                          89271
&TAB3270(7)   SETC  'F'                                          89271
&TAB3270(8)   SETC  'G'                                          89271
&TAB3270(9)   SETC  'H'                                          89271
&TAB3270(10)  SETC  'I'                                          89271
&TAB3270(11)  SETC  '¢'                                          89271
&TAB3270(12)  SETC  '.'                                          89271
&TAB3270(13)  SETC  '<'                                          89271
&TAB3270(14)  SETC  '('                                          89271
&TAB3270(15)  SETC  '+'                                          89271
&TAB3270(16)  SETC  '|'                                          89271
&TAB3270(17)  SETC  '&&'                                         81335
&TAB3270(18)  SETC  'J'                                          89271
&TAB3270(19)  SETC  'K'                                          89271
&TAB3270(20)  SETC  'L'                                          89271
&TAB3270(21)  SETC  'M'                                          89271
&TAB3270(22)  SETC  'N'                                          89271
&TAB3270(23)  SETC  'O'                                          89271
&TAB3270(24)  SETC  'P'                                          89271
&TAB3270(25)  SETC  'Q'                                          89271
&TAB3270(26)  SETC  'R'                                          89271
&TAB3270(27)  SETC  '!'                                          89271
&TAB3270(28)  SETC  '$'                                          89271
&TAB3270(29)  SETC  '*'                                          89271
&TAB3270(30)  SETC  ')'                                          89271
&TAB3270(31)  SETC  ';'                                          89271
&TAB3270(32)  SETC  '¬'                                          89271
&TAB3270(33)  SETC  '-'                                          89271
&TAB3270(34)  SETC  '/'                                          89271
&TAB3270(35)  SETC  'S'                                          89271
&TAB3270(36)  SETC  'T'                                          89271
&TAB3270(37)  SETC  'U'                                          89271
&TAB3270(38)  SETC  'V'                                          89271
&TAB3270(39)  SETC  'W'                                          89271
&TAB3270(40)  SETC  'X'                                          89271
&TAB3270(41)  SETC  'Y'                                          89271
&TAB3270(42)  SETC  'Z'                                          89271
&TAB3270(43)  SETC  '¦'                                          89271
&TAB3270(44)  SETC  ','                                          89271
&TAB3270(45)  SETC  '%'                                          89271
&TAB3270(46)  SETC  '_'                                          89271
&TAB3270(47)  SETC  '>'                                          89271
&TAB3270(48)  SETC  '?'                                          89271
&TAB3270(49)  SETC  '0'                                          89271
&TAB3270(50)  SETC  '1'                                          89271
&TAB3270(51)  SETC  '2'                                          89271
&TAB3270(52)  SETC  '3'                                          89271
&TAB3270(53)  SETC  '4'                                          89271
&TAB3270(54)  SETC  '5'                                          89271
&TAB3270(55)  SETC  '6'                                          89271
&TAB3270(56)  SETC  '7'                                          89271
&TAB3270(57)  SETC  '8'                                          89271
&TAB3270(58)  SETC  '9'                                          89271
&TAB3270(59)  SETC  ':'                                          89271
&TAB3270(60)  SETC  '#'                                          89271
&TAB3270(61)  SETC  '@'                                          89271
&TAB3270(62)  SETC  ''''''                                       81328
&TAB3270(63)  SETC  '='                                          89271
&TAB3270(64)  SETC  '"'                                          89271
&TAB70IN SETB  1             INITIALIZATION DONE                 87070
.TABINIT AIF   ('&WIDTH' EQ '').SIZED                            89298
&LINESZE SETA  &WIDTH        RESET WIDTH FOR CURRENT SCREEN      89298
.SIZED   AIF   (N'&SYSLIST EQ 0).NOLIST                          89271
.NXTI    AIF   (&I EQ N'&SYSLIST).END
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I)'(1,1) EQ '(').ADDR
&SCRNRAF SETA  0             CANCEL 'RA' ADJUSTMENT - NOT ADDRESS
         AIF   ('&SYSLIST(&I)'(1,1) EQ '''').STRING
         AIF   ('&SYSLIST(&I)' EQ 'WDC').WDC
         AIF   ('&SYSLIST(&I)' EQ 'W').WDC                       82273
         AIF   ('&SYSLIST(&I)' EQ 'WRE').WRE
         AIF   ('&SYSLIST(&I)' EQ 'EW').WRE                      82273
         AIF   ('&SYSLIST(&I)' EQ 'EWA').EWA                     82273
         AIF   ('&SYSLIST(&I)' EQ 'EAU').EAU
         AIF   ('&SYSLIST(&I)' EQ 'WSF').WSF                     82273
         AIF   ('&SYSLIST(&I)' EQ 'SBA').SBA
         AIF   ('&SYSLIST(&I)' EQ 'SF').SF
         AIF   ('&SYSLIST(&I)' EQ 'SFE').SFE                     82273
         AIF   ('&SYSLIST(&I)' EQ 'MF').MF                       83165
         AIF   ('&SYSLIST(&I)' EQ 'SA').SA                       83165
         AIF   ('&SYSLIST(&I)' EQ 'IC').IC
         AIF   ('&SYSLIST(&I)' EQ 'PT').PT
         AIF   ('&SYSLIST(&I)' EQ 'RA').RA
         AIF   ('&SYSLIST(&I)' EQ 'EUA').EUA
         AIF   ('&SYSLIST(&I)' EQ 'COLOR').COLOR                 89258
         AIF   ('&SYSLIST(&I)' EQ 'HIGHLITE').HIGHLIT            89258
         AIF   ('&SYSLIST(&I)' EQ 'ATTR').ATTR                   89258
         AIF   ('&SYSLIST(&I)' EQ 'REVERSE').REVERSE             89258
         AIF   ('&SYSLIST(&I)' EQ 'BLINK').BLINK                 89258
         AIF   ('&SYSLIST(&I)' EQ 'UNDER').UNDER                 89258
         AIF   ('&SYSLIST(&I)' EQ 'DFLT').NULL                   89258
         AIF   ('&SYSLIST(&I)' EQ 'BLUE').BLUE                   89258
         AIF   ('&SYSLIST(&I)' EQ 'RED').RED                     89258
         AIF   ('&SYSLIST(&I)' EQ 'PINK').PINK                   89258
         AIF   ('&SYSLIST(&I)' EQ 'GREEN').GREEN                 89258
         AIF   ('&SYSLIST(&I)' EQ 'CYAN').TURQ                   89258
         AIF   ('&SYSLIST(&I)' EQ 'TURQ').TURQ                   89258
         AIF   ('&SYSLIST(&I)' EQ 'TURQOISE').TURQ               89258
         AIF   ('&SYSLIST(&I)' EQ 'YELLOW').YELLOW               89258
         AIF   ('&SYSLIST(&I)' EQ 'WHITE').WHITE                 89258
         AIF   ('&SYSLIST(&I)' EQ 'NULL').NULL
         AIF   ('&SYSLIST(&I)' EQ 'BLANK').BLANK                 81328
         AIF   ('&SYSLIST(&I)'(K'&SYSLIST(&I),1) EQ ':').LABEL
         AIF   ('&SYSLIST(&I)'(K'&SYSLIST(&I),1) EQ '-').LABEL1
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'CL').DSC
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'XL').DSC
         AIF   ('&SYSLIST(&I)'(1,4) EQ 'AL2(').DSA
         MNOTE 12,'PARAMETER #&I INVALID - &SYSLIST(&I) NOT A VALID KEY*
               WORD'
         MEXIT
.*
.STRING  ANOP
&J       SETA  2
&K       SETA  0
.STRLOOP AIF   ('&SYSLIST(&I)'(&J,2) EQ '''''').STRDBL
         AIF   ('&SYSLIST(&I)'(&J,2) NE '&&&&').STRSNGL
.STRDBL  ANOP
&J       SETA  &J+1
.STRSNGL ANOP
&J       SETA  &J+1
&K       SETA  &K+1
         AIF   (&J LT K'&SYSLIST(&I)).STRLOOP
         AIF   (&COL+&K LE &LINESZE).STRFITS
&ROW     SETA  &ROW+1
&COL     SETA  1
.STRFITS ANOP
&LABEL   DC    C&SYSLIST(&I)
.SETL1   AIF   ('&LABEL1' EQ '').NOL1
&I2      SETA  (&ROW-1)*&LINESZE+&COL-1
         AIF   (&I2 LT 4096).ADDR2#6                            GP06262
&LABEL1  DC    AL2(&I2)      14-BIT ADDRESS                     GP06262
&LABEL1  SETC  ''                                               GP06262
         AGO   .NOL1                                            GP06262
.ADDR2#6 ANOP  ,                                                GP06262
&I1      SETA  &I2/64
&CH1     SETC  '&TAB3270(&I1+1)'.'&TAB3270(&I2-64*&I1+1)'        87070
&LABEL1  EQU   C'&CH1'
&LABEL1  SETC  ''
.NOL1    ANOP
&LABEL   SETC  ''
&COL     SETA  &COL+&K
.SLDEC   AIF   (&COL LE &LINESZE).NXTI
&ROW     SETA  &ROW+1
&COL     SETA  &COL-&LINESZE
         AGO   .SLDEC
.*
.ADDR    AIF   (N'&SYSLIST(&I) NE 2).CONBYTE                     90149
         AIF   ('&SYSLIST(&I,1)' EQ '*').RSAME
         AIF   ('&SYSLIST(&I,1)'(1,1) EQ '+').RINC
         AIF   ('&SYSLIST(&I,1)'(1,1) EQ '-').RDEC
         AIF   ('&SYSLIST(&I,1)'(1,1) LT '0').CONBYTE
         AIF   (K'&SYSLIST(&I,1) GT 3).CONBYTE                   90149
&ROW     SETA  &SYSLIST(&I,1)
         AGO   .RSAME
.RINC    ANOP
&CH1     SETC  '&SYSLIST(&I,1)'(2,K'&SYSLIST(&I,1)-1)
&ROW     SETA  &ROW+&CH1
&COL     SETA  1
         AGO   .RSAME
.RDEC    ANOP
&CH1     SETC  '&SYSLIST(&I,1)'(2,K'&SYSLIST(&I,1)-1)
&ROW     SETA  &ROW-&CH1
&COL     SETA  1
.RSAME   AIF   ('&SYSLIST(&I,2)' EQ '*').CSAME
         AIF   ('&SYSLIST(&I,2)'(1,1) EQ '+').CINC
         AIF   ('&SYSLIST(&I,2)'(1,1) EQ '-').CDEC
&COL     SETA  &SYSLIST(&I,2)
         AGO   .CSAME
.CINC    ANOP
&CH1     SETC  '&SYSLIST(&I,2)'(2,K'&SYSLIST(&I,2)-1)
&COL     SETA  &COL+&CH1
.TINC    AIF   (&COL LE &LINESZE).CSAME
&COL     SETA  &COL-&LINESZE
&ROW     SETA  &ROW+1
         AGO   .TINC
.CDEC    ANOP
&CH1     SETC  '&SYSLIST(&I,2)'(2,K'&SYSLIST(&I,2)-1)
&COL     SETA  &COL-&CH1
.TDEC    AIF   (&COL GE 0).CSAME
&COL     SETA  &COL+&LINESZE
&ROW     SETA  &ROW-1
         AGO   .TDEC
.CSAME   ANOP
&I2      SETA  (&ROW-1)*&LINESZE+&COL-1
         AIF   (&I2 LT 4096).ADDR6#6                            GP06262
&LABEL   DC    AL2(&I2)      14-BIT ADDRESS                     GP06262
         AGO   .ADDRAF                                          GP06262
.ADDR6#6 ANOP  ,                                                GP06262
&I1      SETA  &I2/64
&CH1     SETC  '&TAB3270(&I1+1)'.'&TAB3270(&I2-64*&I1+1)'        87070
&LABEL   DC    C'&CH1'
.ADDRAF  ANOP  ,                                                GP06262
&LABEL   SETC  ''
&COL     SETA  &COL-&SCRNRAF   FIX FOR 'RA' ORDER ADJUSTMENT     89300
&SCRNRAF SETA  0             DONE WITH ADJUSTMENT                89300
         AGO   .NXTI
.*
.CONBYTE ANOP
&SCRNRAF SETA  0             CANCEL 'RA' ADJUSTMENT - NOT ADDRESS
&K       SETA  0
&J       SETA  1
.CBLOOP  AIF   (&J GT N'&SYSLIST(&I)).CBEND
&LN40    SETB  ('&SYSLIST(&I,&J)' EQ '40CHAR')
&LN64    SETB  ('&SYSLIST(&I,&J)' EQ '64CHAR')
&LN80    SETB  ('&SYSLIST(&I,&J)' EQ '80CHAR')
&LN40    SETB  ('&SYSLIST(&I,&J)' EQ 'CHAR40' OR &LN40)          81328
&LN64    SETB  ('&SYSLIST(&I,&J)' EQ 'CHAR64' OR &LN64)          81328
&LN80    SETB  ('&SYSLIST(&I,&J)' EQ 'CHAR80' OR &LN80)          81328
&STARTPR SETB  ('&SYSLIST(&I,&J)' EQ 'STARTPR')
&SAL     SETB  ('&SYSLIST(&I,&J)' EQ 'RING')
&SAL     SETB  ('&SYSLIST(&I,&J)' EQ 'ALARM' OR &SAL)
&SAL     SETB  ('&SYSLIST(&I,&J)' EQ 'SAL'   OR &SAL)
&RSTKBY  SETB  ('&SYSLIST(&I,&J)' EQ 'RESTKBY')
&RSTKBY  SETB  ('&SYSLIST(&I,&J)' EQ 'KBYRSTOR' OR &RSTKBY)
&RSTMDT  SETB  ('&SYSLIST(&I,&J)' EQ 'RESTMDT')
&RSTMDT  SETB  ('&SYSLIST(&I,&J)' EQ 'RESETMDT' OR &RSTMDT)
&K       SETA  &K+&LN40*16+&LN64*32+&LN80*48+&STARTPR*8
&K       SETA  &K+&SAL*4+&RSTKBY*2+&RSTMDT
.*
&PROT    SETB  ('&SYSLIST(&I,&J)' EQ 'PROT')
&NUMERIC SETB  ('&SYSLIST(&I,&J)' EQ 'NUM')
&SKIP    SETB  ('&SYSLIST(&I,&J)' EQ 'SKIP')                     81328
&SKIP    SETB  ('&SYSLIST(&I,&J)' EQ 'AUTOSKIP' OR &SKIP)        81328
&PROT    SETB  (&PROT OR &SKIP)                                  81328
&NUMERIC SETB  (&NUMERIC OR &SKIP)                               81328
&DETCT   SETB  ('&SYSLIST(&I,&J)' EQ 'DETCTABLE')
&DETCT   SETB  ('&SYSLIST(&I,&J)' EQ 'DECTABLE' OR &DETCT)       83165
&INT     SETB  ('&SYSLIST(&I,&J)' EQ 'INTENSE')
&INT     SETB  ('&SYSLIST(&I,&J)' EQ 'INT' OR &INT)
&NONDISP SETB  ('&SYSLIST(&I,&J)' EQ 'NONDISP')
&DETCT   SETB  (&DETCT OR &NONDISP)                               *GPP*
&INT     SETB  (&INT OR &NONDISP)                                 *GPP*
&MDT     SETB  ('&SYSLIST(&I,&J)' EQ 'MDT')
&K       SETA  &K+&PROT*32+&NUMERIC*16+&DETCT*4+&INT*8+&MDT       *GPP*
&OK      SETB  ('&SYSLIST(&I,&J)' EQ 'NONE' OR &LN40 OR &LN64 OR &LN80)
&OK      SETB  (&OK OR &STARTPR OR &SAL OR &RSTKBY OR &RSTMDT)
&OK      SETB  (&OK OR &PROT OR &NUMERIC OR &DETCT OR &INT OR &NONDISP)
&OK      SETB  (&OK OR &MDT)
         AIF   (&OK).CBBUMP
         MNOTE 12,'SUBPARAMETER #&J OF PARAMETER #&I INVALID - ''&SYSLI*
               ST(&I,&J)'' NOT RECOGNIZED'
.CBBUMP  ANOP
&J       SETA  &J+1
         AGO   .CBLOOP
.CBEND   ANOP
&CH1     SETC  '&TAB3270(&K+1)'                                  87070
&LABEL   DC    C'&CH1'
&LABEL   SETC  ''
         AGO   .NXTI
.*
.WDC     ANOP
&LABEL   DC    X'27'                    ESCAPE
         DC    C'1'                     WRITE DISPLAY CURSOR
&K       SETA  0
         AGO   .SETL1
.*
.WRE     ANOP
&LABEL   DC    X'27'                    ESCAPE
         DC    C'5'                     ERASE/WRITE
         AGO   .SETL1
.*
.EWA     ANOP  ,                                                 82273
&LABEL   DC    X'27'                    ESCAPE                   82273
         DC    C'='                     ERASE/WRITE ALTERNATE    82273
         AGO   .SETL1                                            82273
.*                                                               82273
.EAU     ANOP
&LABEL   DC    X'27'                    ESCAPE
         DC    C'?'                     ERASE ALL UNPROTECTED
&K       SETA  0
         AGO   .SETL1
.*
.WSF     ANOP                                                    82273
&LABEL   DC    X'27'                    ESCAPE                   82273
         DC    C'3'                     WRITE STRUCTURED FIELD   82273
&K       SETA  0                                                 82273
         AGO   .SETL1                                            82273
.*                                                               82273
.SBA     ANOP
&LABEL   DC    X'11'                    SET BUFFER ADDRESS
&K       SETA  0
         AGO   .SETL1
.*
.SF      ANOP
&LABEL   DC    X'1D'                    START FIELD
&K       SETA  1
         AGO   .SETL1
.*
.SFE     ANOP                                                    82273
&LABEL   DC    X'29'                    START FIELD EXTENDED     82273
&K       SETA  1                                                 82273
         AGO   .SETL1                                            82273
.*                                                               82273
.SA      ANOP                                                    82273
&LABEL   DC    X'28'                    SET ATTRIBUTE            82273
&K       SETA  0                                                 82273
         AGO   .SETL1                                            82273
.*                                                               82273
.MF      ANOP                                                    82273
&LABEL   DC    X'2C'                    MODIFY FIELD             82273
&K       SETA  1                                                 82273
         AGO   .SETL1                                            82273
.*                                                               82273
.IC      ANOP
&LABEL   DC    X'13'                    INSERT CURSOR
&K       SETA  0
         AGO   .SETL1
.*
.PT      ANOP
&LABEL   DC    X'05'                    PROGRAM TAB
&K       SETA  0
         AGO   .NOL1
.*
.RA      ANOP
&LABEL   DC    X'3C'                    REPEAT TO ADDRESS
&K       SETA  0
&SCRNRAF SETA  1             AFTER NEXT ADDRESS, ADJUST BY 1     89300
         AGO   .NOL1
.*
.EUA     ANOP
&LABEL   DC    X'12'                    ERASE UNPROTECTED TO ADDRESS
&K       SETA  0
         AGO   .NOL1
.*                                                               89258
.COLOR   ANOP  ,                                                 89258
&LABEL   DC    X'42'         FOREGROUND COLOR                    89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.HIGHLIT ANOP  ,                                                 89258
&LABEL   DC    X'41'         EXTENDED HIGHLIGHTING               89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.ATTR    ANOP  ,                                                 89258
&LABEL   DC    X'C0'         FIELD ATTRIBUTE                     89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.UNDER   ANOP  ,                                                 89258
&LABEL   DC    X'F4'         UNDERLINE (NORMAL, NOT FIELD)       89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.REVERSE ANOP  ,                                                 89258
&LABEL   DC    X'F2'         REVERSE                             89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.BLINK   ANOP  ,                                                 89258
&LABEL   DC    X'F1'         BLINK                               89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.BLUE    ANOP  ,                                                 89258
&LABEL   DC    X'F1'         DARK (UNREADABLE) BLUE              89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.RED     ANOP  ,                                                 89258
&LABEL   DC    X'F2'         RED                                 89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.PINK    ANOP  ,                                                 89258
&LABEL   DC    X'F3'         MAGENTA ?                           89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.GREEN   ANOP  ,                                                 89258
&LABEL   DC    X'F4'         GREEN                               89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.TURQ    ANOP  ,                                                 89258
&LABEL   DC    X'F5'         CYAN (TURQOISE IS TOO FANCY)        89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.YELLOW  ANOP  ,                                                 89258
&LABEL   DC    X'F6'         YELLOW                              89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*                                                               89258
.WHITE   ANOP  ,                                                 89258
&LABEL   DC    X'F7'         WHITE/TRIPLE-PLANE SELECT           89258
&K       SETA  0                                                 89258
         AGO   .NOL1                                             89258
.*
.NULL    ANOP
&LABEL   DC    X'00'                    NULL
&K       SETA  0                                                 81328
         AGO   .SETL1                                            81328
.*                                                               81328
.BLANK   ANOP  ,                                                 81328
&LABEL   DC    C' '                BLANK                         81328
&K       SETA  0                                                 81328
         AGO   .SETL1                                            81328
.*
.LABEL   AIF   ('&LABEL' EQ '').SETL                             80203
&LABEL   EQU   *                                                 80203
.SETL    ANOP  ,                                                 80203
&LABEL   SETC  '&SYSLIST(&I)'(1,K'&SYSLIST(&I)-1)
         AGO   .NXTI
.*
.LABEL1  ANOP
&LABEL1  SETC  '&SYSLIST(&I)'(1,K'&SYSLIST(&I)-1)
         AGO   .NXTI
.*
.DSC     ANOP
&J       SETA  3
.DSLOOP  AIF   (&J GT K'&SYSLIST(&I)).DSEND
         AIF   ('&SYSLIST(&I)'(&J,1) EQ '''').DSQ
         AIF   ('&SYSLIST(&I)'(&J,1) LT '0').DSL
&J       SETA  &J+1
         AGO   .DSLOOP
.DSL     MNOTE 12,'PARAMETER #&I INVALID - &SYSLIST(&I) NOT DS OPERAND'
         MEXIT
.DSQ     ANOP
&CH1     SETC  '&SYSLIST(&I)'(3,&J-3)
&K       SETA  &CH1
&LABEL   DC    &SYSLIST(&I)
         AGO   .SETL1
.DSEND   ANOP
&CH1     SETC  '&SYSLIST(&I)'(3,&J-3)
&K       SETA  &CH1
&LABEL   DS    &SYSLIST(&I)
         AGO   .SETL1
.DSA     ANOP
&LABEL   DC    &SYSLIST(&I)
&LABEL   SETC  ''
         AGO   .NXTI
.*
.END     AIF   ('&LABEL' EQ '').SOF                              80203
&LABEL   EQU   *                                                 80203
         MEXIT ,                                                 80203
.*
.NOLIST  MNOTE 12,'BUFFER LIST OMITTED'
.SOF     MEND  ,                                                 80203
