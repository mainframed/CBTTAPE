         MACRO
&REF     BUILDCDE &LENGTH=,&ADDR=,&EP=,&SYS=MVS,&LIST=(1)       GP06320
.**********************************************************************
.*                                                                    *
.* MACRO NAME = BUILDCDE                                              *
.*                                                                    *
.* DESCRIPTIVE NAME = BUILD A MAJOR CDE TO IDENTIFY A STORAGE AREA    *
.*                                                                    *
.* FUNCTION = MAKES A STORAGE AREA EASIER TO LOCATE IN A              *
.*            DUMP BY MAKING IT APPEAR AS A LOAD_MODULE.              *
.*                                                                    *
.* STATUS = R200                                                      *
.*                                                                    *
.* AUTHOR = GILBERT SAINT-FLOUR <GSF@POBOX.COM>                       *
.*                                                                    *
.* ENVIRONMENT = SEE BELOW                                            *
.*                                                                    *
.*     AMODE = ANY                                                    *
.*     SCP   = MVS/XA OR MVS/ESA   |  MVS 3.8J WITH SYS=MVS           *
.*     KEY   = USER                                                   *
.*     MODE  = PROBLEM                                                *
.*     APF   = OFF                                                    *
.*                                                                    *
.* OPERATION = BUILDCDE USES THE "LOADER" FORM OF IDENTIFY TO CREATE  *
.*             A MAJOR CDE AND CORRESPONDING XL, THEN ISSUES A LOAD   *
.*             SVC TO CREATE AN LLE AND ASSOCIATE THE CDE WITH THE    *
.*             CURRENT TCB.                                           *
.*                                                                    *
.* INVOCATION = SEE BELOW                                             *
.*                                                                    *
.*          (BEFORE)     GETMAIN RU,LV=20000                          *
.*                                                                    *
.*          (AFTER)      GETMAIN RU,LV=20000                          *
.*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM        *
.*                                                                    *
.* NOTES = SEE BELOW                                                  *
.*                                                                    *
.*        BUILDCDE MAY BE ISSUED MORE THAN ONCE, PROVIDED THE EP      *
.*        KEY-WORD HAS A DIFFERENT VALUE EACH TIME.                   *
.*                                                                    *
.*        IF STORAGE WAS ALLOCATED IN SUB-POOL ZERO (SP=0 IS          *
.*        SPECIFIED OR DEFAULTED TO IN THE GETMAIN MACRO),            *
.*        THE DELETE MACRO MAY BE ISSUED AGAINST THE SPECIFIED EP     *
.*        TO DELETE THE CDE AND FREE UP THE STORAGE.                  *
.*                                                                    *
.*        IF GETMAIN SPECIFIES A NON-ZERO SUBPOOL, THE DELETE MACRO   *
.*        DELETES THE CDE BUT DOES NOT FREE UP THE STORAGE:  YOU      *
.*        MUST ISSUE FREEMAIN YOURSELF.                               *
.*                                                                    *
.*        IN A MAJOR CDE CREATED BY THE "LOADER" FORM OF IDENTIFY,    *
.*        THE "RENT" AND "REUS" ATTRIBUTES ARE OFF.  THIS MEANS       *
.*        THAT YOU MAY NOT ISSUE LOAD AGAINST THE EP NAME (BUILDCDE   *
.*        HAS ALREADY ISSUED LOAD), BUT YOU MAY ISSUE LINK, ATTACH    *
.*        OR XCTL IF YOU WANT TO (ONCE ONLY).  YOU MAY ALSO ISSUE     *
.*        THE STANDARD FORM OF IDENTIFY TO DEFINE SECONDARY ENTRY     *
.*        POINTS WHICH HAVE THE "RENT" ATTRIBUTE.                     *
.*                                                                    *
.*        IF THIS MACRO IS USED BY AN AUTHORIZED PROGRAM, THEN        *
.*        A S306 ABEND WILL OCCUR (THE CDE CREATED HAS ITS CDSYSLIB   *
.*        BIT OFF).  LOOK FOR A SECOND VERSION OF THE MACRO THAT MAY  *
.*        BE USED IN AN AUTHORIZED PROGRAM AT THE END OF THIS JOB.    *
.*                                                                    *
.*        THE IDENTIFY MACRO SUPPLIED IN SYS1.MACLIB SUPPORTS THE     *
.*        "LOADER" FORM, BUT THE FORMAT OF THE PARAMETER LIST IS      *
.*        NOT DOCUMENTED, AS FAR AS I KNOW.  THE RETURN CODES X'18'   *
.*        OR X'1C' MAY BE RETURNED IN R15 WHEN THE PARAMETER LIST     *
.*        IS INVALID.  THESE RETURN CODES ARE DOCUMENTED ALONG WITH   *
.*        THE IDENTIFY MACRO.                                         *
.*                                                                    *
.*        WHEN THE LOAD-LIST IS EMPTY (I.E. NO LOAD HAS BEEN ISSUED   *
.*        FOR THIS TCB YET), IDENTIFY FAILS WITH A RETURN CODE 12.    *
.*        THIS CAN BE CIRCUMVENTED BY ISSUING THE FOLLOWING SEQUENCE: *
.*                                                                    *
.*                       LOAD    EP=IEFBR14                           *
.*                       GETMAIN RU,LV=20000                          *
.*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM        *
.*                       DELETE  EP=IEFBR14                           *
.*                                                                    *
.*        I'VE NOTICED THAT ON MVS SYSTEMS PRIOR TO MVS/ESA 4.3, THE  *
.*        "LOADER" FORM OF IDENTIFY RETURNS THE ADDRESS OF THE CDE IT *
.*        CREATED IN REG1.  IN MVS/ESA 4.3 THIS IS NO LONGER TRUE.    *
.*                                                                    *
.**********************************************************************
.*     CORRECTIONS AND CHANGES BY GERHARD POSTPISCHIL  2006-11-15     *
.*        BY DEFAULT, BUILDCDE USES THE GETMAINED AREA TO BUILD THE   *
.*          IDENTIFY LIST. IF YOU USE THIS AREA BETWEEN THE GETMAIN   *
.*          AND BUILDCDE (E.G., SAVE AREA, OR INIT TO ZERO) THEN      *
.*          USE LIST= FOR AN ALTERNATE LIST ADDRESS. FOR EXAMPLE,     *
.*          TRY LIST=20(R13) - USE ANY RS FORMAT ADDRESS LENGTH 32    *
.*                                                                    *
.*        RETROFIT TO FUNCTION CORRECTLY UNDER MVS 3.8J (SYS=MVS)     *
.*        CHANGE TO ASSEMBLE UNDER ASM/XF                             *
.**********************************************************************
         LCLA  &O16                    OFFSETS IN PARM LIST     GP06320
&O16     SETA  16                      OFFSET TO EXTENT LIST    GP06320
         AIF   ('&SYS' NE 'MVS').NEWSYS                         GP06320
&O16     SETA  12                      OFFSET TO EXTENT LIST    GP06320
.NEWSYS  ANOP  ,                       XA, ESA, .....           GP06320
&REF     IHBINNRA &ADDR,&LENGTH        SET R0,R1
         LR    R15,R1                  PRESERVE ENTRY ADDRESS   GP06320
         IHBINNRA &LIST                SET LIST ADDRESS INTO R1 GP06320
         ST    R15,0(,R1)              STORE ENTRY-POINT ADDR   GP06320
         ST    R15,&O16+12(,R1)        ALSO AS LOAD ADDRESS     GP06320
         MVC   4(8,R1),=CL8'&EP '      CDNAME                   GP06320
         AIF   ('&SYS' EQ 'MVS').OLDSYS   SHORTER LIST IN 3.8   GP06320
         XC    12(4,R1),12(R1)         CLEAR BYTES 12-15
.OLDSYS  LA    R14,16                  LENGTH OF PSEUDO XL      GP06320
         LA    R15,1                   NUMBER OF RELOCATION FACTORS
         STM   R14,R0,&O16.(R1)        FORMAT BYTES 16-23       GP06320
         MVI   &O16+8(R1),X'80'        END OF LIST              GP06320
         MNOTE *,'       IDENTIFY MF=(E,(R1))'
         SLR   R0,R0                   LOADER-TYPE IDENTIFY
         SVC   41                      ISSUE IDENTIFY SVC
         LR    R14,R1                  PASS CDE ADDRESS
         MNOTE *,'       LOAD EP=&EP,DCB=0'
         LA    R0,=CL8'&EP '           POINT TO EP NAME         GP06320
         SLR   R1,R1                   NO DCB FOR LOAD
         SVC   8                       ISSUE LOAD SVC
         LR    R1,R0                   EP ADDRESS IN R1
         SLR   R0,R0
         ICM   R0,B'0111',&O16+9(R1)  LENGTH IN R0              GP06320
         MEND
