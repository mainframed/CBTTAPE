         MACRO ,
&NM      #TRAP &LIST,&OPT=(PSW),&ID=,&REGS=YES,&MF=A   ADDED ON GP09277
.*                                                                    *
.*  THIS MACRO INVOKES EXTERNAL LOAD MODULE @SPIEDER TO PRODUCE       *
.*  TRACING, REGISTER CONTENTS, AND VARIABLES. PRIOR TO USE, @SPIEDER *
.*  MUST BE INITIALIZED WITH '  BANDAID SPIE ' OR ' #TRACE INIT '     *
.*                                                                    *
.*  ALTERNATIVELY, IT MAY BE USED IN ANY SECTION OF                   *
.*  CODE THAT HAS THE PGMTRACE FACILITY ACTIVE (SEE #TRC AND #TRACE   *
.*  MACROS).                                                          *
.*                                                                    *
.*    PRINT REQUIRES A SYSDEBUG DD CARD.                              *
.*                                                                    *
.*                                                                    *
.*  OPTIONAL:  ID=name   IDENTIFIES THE DEBUG PACKET                  *
.*                                                                    *
.*                REGS= (DEFAULT) | REGS=NO - NO REGISTERS            *
.*                REGS=YES  -  REGISTERS R0 THROUGH R15               *
.*                REGS=(R1,R2) - REGISTERS R1 THROUGH R2              *
.*                REGS=SHORT   - R14 THROUGH R1                       *
.*                                                                    *
.*             OPT=      LIST OF OPTIONAL FORMATTING ITEMS:           *
.*                PSW, GPR, FPR, CTL                                  *
.*                later (PGM, BUF, MAP) mini-dump information         *
.*                                                                    *
.*             MF=A      POINTER TO ITEM LIST IS ADDRESS CONSTANT     *
.*             MF=S      POINTER TO ITEM LIST IS BASE/DISPLACEMENT    *
.*                       (NEEDED FOR WYLBUR OVERLAYS)                 *
.*             MF=L      PRODUCES ID AND VARIABLE LIST ONLY           *
.*             MF=(E,list)  USES S CONSTANT TO PROCESS REMOTE LIST    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*       Positional operands (0 to nnn):                              *
.*           (OP1,LN1,FM1),(OP2,LN2,FM2), ...                         *
.*                                                                    *
.*                OP - ADDRESS EXPRESSION VALID IN S CONSTANT         *
.*                LN - LENGTH EXPRESSION; DEFAULT IS L'OP             *
.*                FM - TEXT | CTEXT | HEX | PACK - DEFAULT IS HEX     *
.*                     OR ABBREVIATED   T | CT | H | P                *
.*                                                                    *
.*    EACH TRAP WILL EXPAND 8 BYTES IN THE CALLER'S SECTION, AND A    *
.*    REQUEST LIST IN RSECT TRPnnnnn: B2FF0000/AL4(traplist)          *
.*                 OR          B2FF0000,FFFF /SL2(traplist)           *
.*                                                                    *
.*                                                                    *
.*  ALLOW *var AS INDIRECT LOOKUP REQUEST - 24-BIT ADDRESS IN WORD    *
.*  ALLOW /var AS INDIRECT INDIRECT LOOKUP                            *
.*                2008-04-18  GYP                                     *
.*  ALLOW REGISTER (ONLY) AS A LENGTH FIELD OPERAND - CODE AS 800r    *
.*                                                                    *
.*                                                                    *
.**********************************************************************
.*  MAINTENANCE:                                                      *
.*  2011-12-1n  GYP  ADDED MF= SUPPORT (ALSO IN @SPIEDER & PGMTRACE)  *
.**********************************************************************
         GBLA  &MACP#        NUMBER OF SUBLIST PARAMETERS       GP04234
         GBLB  &BUGBEAR                                         GP09301
         GBLC  &MACP1,&MACP2,&MACP3,&MACP4,&MACP5,&LOCAL        GP09301
         GBLC  &MACP6,&MACP7,&MACP8,&MACP9,&MACP10              GP04234
         GBLC  &V
         LCLA  &LN,&I,&EN,&EM,&EO,&J,&NL                        GP95235
         LCLB  &B80,&B40,&B20,&B10,&B08,&B04,&B02,&B01,&EXEC    GP11352
         LCLC  &L,&ET,&EL,&EK,&CURSECT,&TRPSECT,&TAG,&NMLST     GP11352
&L       SETC  'L'''                                            GP95235
&V       SETC  'ZZT'.'&SYSNDX'
&NMLST   SETC  'XTL'.'&SYSNDX'                                  GP11352
         AIF   (&BUGBEAR).DOSOME                                GP09301
         AIF   ('&LOCAL' EQ '').DOSOME  NON-ESP ENVIRONMENT     GP09301
         AIF   ('&NM' EQ '').MEND
&NM      DS    0H            DEBUG SWITCH NOT ON
         AGO   .MEND
.DOSOME  ANOP  ,                                                 95067
&CURSECT SETC  '&SYSECT'                                        GP09301
&TRPSECT SETC  '&SYSECT'.'ZZZZZZZZ'                             GP09301
&TRPSECT SETC  'TRP'.'&TRPSECT'(4,5)                            GP09301
&TAG     SETC  '&ID'         USE OVERRIDE LABEL
         AIF   ('&TAG' NE '').HVTAG
&TAG     SETC  '&NM'         ELSE USE NAME FIELD
.HVTAG   ANOP  ,
&B80     SETB  ('&TAG' NE '' OR &B80)
.*       B01 RESERVED FOR FULL MEMORY DUMP
&I       SETA  N'&OPT
         AIF   (&I GT 0).OPTLOOP
*DEFER*  MNOTE 0,'OPTIONS OMITTED - PSW DEFAULTED'
&B40     SETB  1
         AGO   .OPTDONE
.OPTLOOP AIF   (&J GE &I).OPTDONE
&J       SETA  &J+1
&B40     SETB  ('&OPT(&J)' EQ 'PSW' OR &B40)
&B20     SETB  ('&OPT(&J)' EQ 'REGS' OR &B20)
&B20     SETB  ('&OPT(&J)' EQ 'GPR' OR &B20)                    GP10092
&B10     SETB  ('&OPT(&J)' EQ 'FPR' OR &B10)                    GP10092
&B08     SETB  ('&OPT(&J)' EQ 'CTL' OR &B08)   AUTH ONLY        GP10092
&B04     SETB  ('&OPT(&J)' EQ 'PGM' OR &B04)
&B02     SETB  ('&OPT(&J)' EQ 'MAP' OR &B02)
&B01     SETB  ('&OPT(&J)' EQ 'BUF' OR &B01)
         AGO   .OPTLOOP
.OPTDONE AIF   ((&B40+&B20+&B10+&B08+&B04+&B02+&B01) GE &I).OPTOK
         MNOTE 4,'UNRECOGNIZED OPTION SPECIFIED'
.OPTOK   AIF   ('&REGS' EQ 'YES').FGREGS                        GP12336
         AIF   (N'&REGS NE 2).BGREGS                            GP12336
         AIF   ('&REGS(1)' NE 'R0' OR '&REGS(1)' NE '0').BGREGS GP12336
         AIF ('&REGS(2)' NE 'R15' OR '&REGS(2)' NE '15').BGREGS GP12336
.FGREGS  ANOP  ,                                                GP12336
&B20     SETB  1                                                GP12336
.BGREGS  ANOP  ,                                                GP12336
&I       SETA  N'&SYSLIST
&J       SETA  0
         AIF   ('&REGS' EQ '' OR '&REGS' EQ 'NO').COUNTER
         AIF   (&B20).COUNTER                                   GP12336
&NL      SETA  1
.COUNTER AIF   (&J GE &I).COUNTED
&J       SETA  &J+1
         AIF   ('&SYSLIST(&J)' EQ '').COUNTER
&NL      SETA  &NL+1
         AGO   .COUNTER
.COUNTED ANOP  ,
&J    SETA  128*&B80+64*&B40+32*&B20+16*&B10+8*&B08+4*&B04+2*&B02+&B01
         AIF   (N'&MF NE 2).MFONE                               GP11352
         AIF   ('&MF(1)' NE 'E').MFONE                          GP11352
&EXEC    SETB  1                                                GP11352
&NMLST   SETC  '&MF(2)'                                         GP11352
         AGO   .POINTS                                          GP11352
.MFONE   AIF   ('&MF' EQ 'A').POINTA                            GP11346
         AIF   ('&MF' EQ 'S').POINTS                            GP11346
         AIF   ('&MF' EQ 'L').POINTL                            GP11352
         MNOTE 4,'MF= NOT RECOGNIZED; MF=A ASSUMED'             GP11346
.POINTA  ANOP  ,                                                GP11346
&NM      DC    0H'0',X'B2FF0000',AL4(&NMLST)                    GP11352
         AGO   .CPOINT                                          GP11346
.POINTS  ANOP  ,                                                GP11346
&NM      DC    0H'0',X'B2FF0000,FFFF',SL2(&NMLST)               GP11352
.CPOINT  AIF   (&EXEC).MEND                                     GP11352
&TRPSECT RSECT ,                                                GP09301
         AGO   .POINTCM                                         GP11352
.POINTL  ANOP  ,                                                GP11352
&NMLST   SETC  '&NM'                                            GP11352
.POINTCM ANOP  ,                                                GP11352
&NMLST   DC    0H'0',AL1(&J,&NL)  TRAP LIST                     GP11352
         AIF   (NOT &B80).EXPAND
         DC    CL8'&TAG '
.EXPAND  AIF   ('&REGS' EQ '' OR '&REGS' EQ 'NO').NOREGS         95079
         AIF   (&B20).NOREGS      ALREADY REQUESTED             GP12336
         AIF   ('&REGS' EQ 'R15' OR '&REGS' EQ 'SHORT'                 *
               OR '&REGS' EQ 'RET').RETREG                      GP97225
         AIF   ('&REGS' EQ 'YES' OR '&REGS' EQ 'ALL').REGSALL   GP02246
         AIF   (N'&REGS EQ 2).REGS2                             GP97225
         DC    AL1(0,128),SL2(&REGS(1),&REGS(1))                GP09301
         AGO   .NOREGS                                          GP97225
.REGS2   DC    AL1(0,128),SL2(&REGS(1),&REGS(2))                GP09301
         AGO   .NOREGS                                          GP97225
.REGSALL DC    AL1(0,128),SL2(0,15)                             GP09301
         AGO   .NOREGS                                          GP97225
.RETREG  DC    SL2(128,14,1)    R15-R1 ONLY                     GP09301
.NOREGS  ANOP  ,                                                GP09301
&I       SETA  0                                                GP09301
&LN      SETA  N'&SYSLIST                                       GP95235
.DOLIST  AIF   (&I GE &LN).LISTDON  DONE WITH LIST              GP95235
&I       SETA  &I+1          BUMP LOOP INDEX                    GP95235
&EN      SETA  K'&EK         GET LENGTH                         GP04234
&EN      SETA  N'&SYSLIST(&I)
&MACP1   SETC  '&SYSLIST(&I,1)'
&MACP2   SETC  '&SYSLIST(&I,2)'
&MACP3   SETC  '&SYSLIST(&I,3)'
         AIF   (&EN LT 1).DOLIST  USER IN COMA?                 GP95235
         AIF   (&EN LT 4).TOOLIST WARN                          GP95235
         MNOTE 4,'MORE THAN 3 SUBPARMS IN &SYSLIST(&I) '        GP95235
.TOOLIST ANOP  ,                                                GP95235
&EK      SETC  '&MACP1'                                         GP04234
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
&EO      SETA  0             PRESET FOR NORMAL ADDRESSING MODE
&ET      SETC  '03'          PRESET FOR HEX DEFAULT             GP95235
         AIF   (&EM GT 0).TPFX                                  GP04234
&EK      SETC  '0'           ALLOW EXPANSION WITHOUT ERROR      GP04234
&EM      SETA  1                                                GP04234
         MNOTE 4,'TRAP: PARAMETER &I REQUIRES AN ADDRESS'       GP09301
.TPFX    AIF   (&EM LT 2).NOTA31                                GP04234
         AIF   ('&EK'(1,1) NE '/').NOTIND
&EO      SETA  &EO+1         REQUEST INDIRECT ADDRESSING
&EK      SETC  '&EK'(2,&EM-1)  DELETE LEADING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTIND  AIF   ('&EK'(&EM,1) NE '%').NOTA24
&EO      SETA  &EO+2         REQUEST FORCED 24-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA24  AIF   ('&EK'(&EM,1) NE '?').NOTA31
&EO      SETA  &EO+4         REQUEST FORCED 31-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA31  AIF   (&EN LT 3 OR '&MACP3' EQ 'HEX').HTYPE            GP95235
         AIF   ('&MACP3' EQ 'X').HTYPE                          GP97225
         AIF   ('&MACP3' EQ 'HEX').HTYPE
         AIF   ('&MACP3' EQ 'T').TTYPE                          GP98189
         AIF   ('&MACP3' EQ 'TEXT').TTYPE                       GP95235
         AIF   ('&MACP3' EQ 'TXT').TTYPE
         AIF   ('&MACP3' EQ 'C').CTYPE                          GP97225
         AIF   ('&MACP3' EQ 'CT').CTYPE
         AIF   ('&MACP3' EQ 'CTEXT').CTYPE                      GP97225
         AIF   ('&MACP3' EQ 'PACK').PTYPE                       GP97225
         AIF   ('&MACP3' EQ 'PACKED').PTYPE                     GP97225
         AIF   ('&MACP3' EQ 'P').PTYPE                          GP97225
         AIF   ('&MACP3' EQ 'PD').PTYPE
         AIF   ('&MACP3' EQ 'D').PTYPE                          GP97225
 MNOTE 4,'TYPE MUST BE TEXT, CTEXT, HEX, OR PACKED, NOT &MACP3'
         AGO   .HTYPE                                           GP95235
.TTYPE   ANOP  ,                                                GP95235
&ET      SETC  '01'          SET FOR TEXT                       GP95235
         AGO   .HTYPE                                           GP95235
.CTYPE   ANOP  ,                                                GP97225
&ET      SETC  '02'          SET FOR CONDITIONAL TEXT, ELSE HEX GP97225
         AGO   .HTYPE                                           GP97225
.PTYPE   ANOP  ,                                                GP97225
&ET      SETC  '04'          SET FOR PACKED                     GP97225
.HTYPE   ANOP  ,                                                GP97225
&EL      SETC  '&MACP2'                                         GP95235
         AIF   ('&EL' NE '').HLEN                               GP95235
&EL      SETC  '&L'.'&EK'
         AGO   .BDLEN                                           GP10160
.HLEN    AIF   ('&EL'(1,1) NE '(' OR '&EL'(K'&EL,1) NE ')').BDLEN 10160
         AIF   (K'&EL LT 3).BDLEN                               GP10160
         AIF   ('&EL'(2,1) EQ '(' OR '&EL'(K'&EL-1,1) EQ ')').BDLEN
         DC    AL1(&ET,&EO),SL2(&EK),X'80',AL1(&EL),CL8'&MACP1' GP10160
         AGO   .DOLIST                                          GP95235
.BDLEN   DC    AL1(&ET,&EO),SL2(&EK,&EL),CL8'&MACP1 '
         AGO   .DOLIST                                          GP95235
.LISTDON ANOP  ,                                                GP09301
&CURSECT CSECT ,                                                GP09301
.MEND    MEND  ,
