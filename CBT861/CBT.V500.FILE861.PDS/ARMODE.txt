         MACRO ,
&NM      ARMODE &OP,&@ALET,&AR=R14,&WK=R14
.*                                                                    *
.*   ACCESS SPACE CONTROL SUPPORT                                     *
.*                                                                    *
.*   ARMODE 0 | P | PRI   SETS PRIMARY MODE                           *
.*   ARMODE     S | SEC   SETS SECONDARY MODE                         *
.*   ARMODE     H | HOME  SETS HOME SPACE MODE (PRIVILEGED)           *
.*   ARMODE AR            SETS ACCESS REGISTER MODE                   *
.*   ARMODE AR,@ALET,AR=N SETS AR#N FROM @ALET, THEN AR MODE          *
.*   ARMODE ANY           CONTENTS OF WORD ANY TO WK, THEN SAC 0(WK)  *
.*   ARMODE (R)           SAC 0(R)                                    *
.*                                                                    *
.*   ALL OF THE ABOVE ISSUE A SYSSTATE =P (PRIMARY), ELSE =AR         *
.*                                                                    *
.*   ARMODE SAVE          COPIES IAC MODE INTO WK REGISTER            *
.*   ARMODE SAVE,WORD     IAC MODE INTO WK REGISTER AND STORE TO WORD *
.*                                                                    *
         GBLC  &MACPLAB
         GBLB  &MVSXA                                           GP04234
         AIF   (&MVSXA).ENOUGH                                  GP04234
&NM      MACPARM MODE=LBL                                       GP04234
         MEXIT ,                                                GP04234
.ENOUGH  ANOP  ,                                                GP04234
&MACPLAB SETC  '&NM'
         LCLC  &EXP,&AEO
         LCLA  &K
&EXP     SETC  '0'
&AEO     SETC  'P'
         AIF   (T'&OP NE 'O').HAVEOP
         MNOTE 8,'ARMODE: FUNCTION OR OPERAND REQUIRED'
         MEXIT ,
.*
.HAVEOP  AIF   ('&OP' EQ '0').SAC
         AIF   ('&OP' EQ 'P').SAC
         AIF   ('&OP' EQ 'PRI').SAC
.*
&AEO     SETC  'AR'
&EXP     SETC  'X''100'''
         AIF   ('&OP' EQ 'S').SAC
         AIF   ('&OP' EQ 'SEC').SAC
.*
&EXP     SETC  'X''300'''
         AIF   ('&OP' EQ 'H').SAC
         AIF   ('&OP' EQ 'HOME').SAC
.*
&EXP     SETC  'X''200'''
         AIF   ('&OP' EQ 'AR').SAR
         AIF   ('&OP' EQ 'SAVE').IAC
.*  NOT A PREDEFINED OPERAND
&K       SETA  K'&OP
         AIF   (&K LT 3).LOAD   ?
         AIF   ('&OP'(1,1) NE '(' OR '&OP'(2,1) EQ '(').LOAD
         AIF   ('&OP'(&K,1) NE ')' OR '&OP'(&K-1,1) EQ ')').LOAD
         MACPARM 0(&OP(1)),OP=SAC,MODE=ONE
         AGO   .STATE
.LOAD    MACPARM &WK,&OP,OP=L  LOAD STORED VALUE (FROM IAC?)
         MACPARM 0(&WK),OP=SAC,MODE=ONE  IF 0, NEED ASCENV=P
         AGO   .STATE
.IAC     MACPARM &WK,OP=IAC,MODE=ONE
         AIF   (T'&@ALET EQ 'O').MEND
         MACPARM &WK,&@ALET,OP=ST,MODE=REV
         MEXIT ,
.SAR     AIF   (T'&@ALET EQ 'O').SAC
         MACPARM &WK,@ALET,OP=L
         MACPARM &AR,(&WK),OP=SAR,OPR=SAR,MODE=EQU
.SAC     MACPARM &EXP,OP=SAC,MODE=ONE
.STATE   SYSSTATE ASCENV=&AEO
.MEND    MEND  ,
