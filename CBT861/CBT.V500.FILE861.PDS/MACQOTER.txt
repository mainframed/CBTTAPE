         MACRO ,
        MACQOTER  &STR,&OPT=,&USE=,&FILL=,&NULL=,&NAME=,&TYPE=C
.*-------------------------------------------------------------------*.
.*  NAME:                M A C Q O T E R                             *.
.* PURPOSE:                                                          *.
.*                                                                   *.
.*  INTERNAL MACRO TO DETERMINE IF A PARAMETER IS A QUOTED STRING    *.
.*    AND OPTIONALLY QUOTE IT OR STRIP QUOTES.                       *.
.*                                                                   *.
.*  INPUT :                                                          *.
.*    THE STRING PARAMETER                                           *.
.*                                                                   *.
.*  OUTPUT:                                                          *.
.*   MACPERR  GLOBAL FLAG TO INDICATE AN ERROR OCCURRED              *.
.*   MACQUOT  GLOBAL FLAG TO INDICATE THAT INPUT WAS A QUOTED STRING *.
.*   MACPNUL  GLOBAL FLAG TO INDICATE THAT INPUT WAS NULL            *.
.*   MACQSTR  ORIGINAL OR MODIFIED STRING                            *.
.*-------------------------------------------------------------------*.
.* KEYWORD PARAMETERS:                                               *.
.*    OPT=   USE=   FILL=   NULL=   NAME=                            *.
.*  RESERVED FOR LATER:  USE=LITERAL (CREATE =C'PARM')               *.
.*  RESERVED FOR LATER:  TYPE=G  (SPECIAL CHARACTER SET SUPPORT)     *.
.*********************************************************************.
.* POSITIONAL PARAMETERS:                                            *.
.*    THE STRING TO BE TESTED / CHANGED                              *.
.*********************************************************************.
.* EXAMPLES:                                                         *.
.*    MACQOTER HAM&EGGS,USE=HEX,OPT=QUOTE,NULL=YES,NAME=OUTER        *.
.*                                                                   *.
.* &MACQSTR  SET TO  'HAM&&EGGS'                                     *.
.* &MACQUOT  SET TO  1                                               *.
.* &MACPERR  SET TO  0                                               *.
.* &MACPNUL  SET TO  0                                               *.
.*                                                                   *.
.*********************************************************************.
.* CHANGE LOG:                                                       *.
.*                                                                   *.
.* YYYY-MM-DD INT COMMENTS                                           *.
.* ---------- --- --------                                           *.
.* 1999-03-12 GYP CREATED                                            *.
.* 2003-05-24 GYP CHANGED MACERR TO MACPERR TO MATCH MACPARM         *.
.*                                                                   *.
.*********************************************************************.
.* THIS INNER MACRO CONVERTS AN ARGUMENT STRING INTO ANOTHER ONE.    *.
.* WHEN NO CONVERT OPTIONS ARE USED, ONLY GLOBAL SETB VALUES ARE SET.*.
.*                                                                   *.
.*  OPT=QUOTE    SPECIFIES THAT A QUOTED STRING IS NEEDED            *.
.*                 WITH USE=/USE=QUOTE WILL NOT CHANGE QUOTED STRING *.
.*  USE= OR USE=QUOTE    ADD QUOTES TO STRING                        *.
.*  USE=PROC     SPECIFIES THAT QUOTES ARE DOUBLED AGAIN             *.
.*  USE=HEX      ALLOWS X'NNN', B'010101', AND SIMILAR EXPRESSIONS   *.
.*                 WITHOUT ALTERATION (OTHERS AS USE=QUOTE)          *.
.*  OPT=STRIP    HALVES THE QUOTES IN A QUOTED STRING AND UNQUOTES   *.
.*  FILL='X' OR FILL=X    REPLACES BLANKS BY THE SPECIFIED CHARACTER *.
.*                 DOES NOT SUPPORT AMPERSAND OR APOSTROPHE.         *.
.*  NULL=YES     SPECIFIES THAT AN EMPTY STRING DOES NOT SET AN      *.
.*                 ERROR; THE MACPNUL FLAG IS SET, MACPERR IS NOT.   *.
.*                 RETURNS EMPTY STRING UNLESS QUOTING, THEN ''      *.
.*                 IGNORED IF STRING IS NOT NULL. OPT=STRIP FOR ''   *.
.*                 GENERATES A NULL STRING AND SETS THE FLAGS.       *.
.*                                                                   *.
.*  TYPE=C (DEFAULT) OR TYPE=G - NOT SUPPORTED YET                   *.
.*                                                                   *.
.* REQUEST:    OPT=  OPT=QUOTE  USE=HEX  USE=PROC  OPT=STRIP RESULT  *.
.*  STRING        Y                Y                      STRING     *.
.*  'STRING'      Y                Y                      'STRING'   *.
.*  X'123'        Y                Y                      X'123'     *.
.*  STRING              Y          Y                      'STRING'   *.
.*  'STRING'            Y          Y                      'STRING'   *.
.*  X'123'              Y          Y                      X'123'     *.
.*  X'123'              Y                                 'X''123''' *.
.*  STRING              Y                   Y             ''STRING'' *.
.*  'STRING'            Y                   Y             ''STRING'' *.
.*  X'123'              Y                   Y       ''X''''123'''''' *.
.*  X'123'              Y          Y        Y             X'123'     *.
.*  'IT''S &&'                                        Y   IT'S &     *.
.*  ..ANY..    NO FLAGS ON (OTHER THAN NULL AND FILL)  SETB ONLY     *.
.*                                                                   *.
.*  (STRIP AND QUOTE CANCEL; STRIP AND PROC ACTS AS QUOTE)           *.
.*                                                                   *.
.* N.B.: STRIPPING AND DOUBLING APPLIES TO APOSTROPHES AND AMPERSANDS*.
.*********************************************************************.
.*  LIMITATIONS:  MAXIMUM INPUT LENGTH: 255 (ARBITRARY, <256)        *.
.* >>>>> DOES NOT CHECK FOR STRING LENGTH EXCESSION (>255) <<<<<     *.
.*********************************************************************.
         GBLB  &MACPNUL,&MACPERR,&MACQUOT
         GBLC  &MACQSTR
         LCLB  &DEL,&DOUB,&FOUR
         LCLC  &PAD,&MYSTR,&CH
         LCLA  &I,&J,&K,&L,&M,&N
&MACPERR SETB  0
&MACPNUL SETB  0
&MACQUOT SETB  0
&MYSTR   SETC  '&STR'
&MACQSTR SETC  '&STR'        PRESET AS NULL IF EMPTY
&K       SETA  K'&STR
&DEL     SETB  ('&OPT' EQ 'STRIP')
&DOUB    SETB  ('&OPT' EQ 'QUOTE')
&FOUR    SETB  ('&USE' EQ 'PROC')
         AIF   (&K LT 2).NOQUOCH
&MACQUOT SETB  ('&STR'(1,1) EQ '''' AND '&STR'(&K,1) EQ '''')
.NOQUOCH AIF   (NOT &MACQUOT).UNQUO
.*  ALREADY QUOTED STRING CANCELS ONE LEVEL OF QUOTING
&DOUB    SETB  (&FOUR)
&FOUR    SETB  0
.UNQUO   AIF   (NOT &DEL OR (NOT &DOUB AND NOT &FOUR)).NODEL
.*  STRIP CANCELS ONE LEVEL OF QUOTING
&DOUB    SETB  (&FOUR)
&FOUR    SETB  0
&DEL     SETB  0             CANCEL STRIP REQUEST
.NODEL   AIF   ('&NULL' EQ '' OR '&NULL' EQ 'Y' OR '&NULL' EQ 'YES').NU
         AIF   ('&NULL' EQ 'N' OR '&NULL' EQ 'NO').NU
        MNOTE  4,'&NAME/MACQOTER *****  INVALID NULL OPTION ''&NULL'''
        MNOTE  4,'  USE NULL= (DEFAULT), OR NULL=YES'
&MACPERR SETB  1
.NU      AIF   (&K GT 0 AND &K LE 255).OKLEN
         AIF   (&K NE 0).TOOLONG
&MACPNUL SETB  1
         AIF   (&FOUR).DO4NU
         AIF   (NOT &DOUB).NO2NU
&MACQSTR SETC  ''''''        MAKE NULL STRING
         AGO   .NO2NU
.DO4NU   ANOP  ,
&MACQSTR SETC  ''''''''''    MAKE NULL STRING
.NO2NU   AIF   ('&NULL' EQ 'YES').TEST
&MACPERR SETB  1
         AGO   .TEST
.TOOLONG MNOTE 8,'&NAME/MACQOTER INPUT EXCEEDS DESIGN LENGTH OF 255'
&MACPERR SETB  1
         MEXIT ,
.OKLEN   AIF   ('&OPT' EQ '' OR '&OPT' EQ 'QUOTE' OR                   *
               '&OPT' EQ 'STRIP').OKOPT
        MNOTE 4,'&NAME/MACQOTER  *****  INVALID STRING OPTION ''&OPT'''
        MNOTE 4,'  SUPPORTED OPTIONS ARE OPT=QUOTE AND OPT=STRIP'
&MACPERR SETB  1
         MEXIT ,
.OKOPT   AIF   ('&USE'  EQ ''  OR  '&USE'  EQ  'PROC'                  *
                 OR  '&USE'  EQ  'HEX'  OR '&USE' EQ 'QUOTE').OKUSE
        MNOTE 4,'&NAME/MACQOTER  *****  INCORRECT USE OPTION ''&USE'''
        MNOTE 4,'  SUPPORTED OPTIONS ARE USE=HEX AND USE=PROC'
&MACPERR SETB  1
         MEXIT ,
.OKUSE   AIF   ('&FILL' EQ '').NOPAD
&J       SETA  2
         AIF   (K'&FILL EQ 3 AND '&FILL'(1,1) EQ '''').PADQ
&J       SETA  1
         AIF   (K'&FILL EQ 1).PADQ
.BADFILL MNOTE 4,'&NAME/MACQOTER  *****  INVALID FILL OPTION ''&FILL'''
         MNOTE 4,'  SPECIFY SINGLE CHARACTER WITH FILL=X OR FILL=''X'''
&MACPERR SETB  1
         MEXIT ,
.PADQ    ANOP
&PAD     SETC  '&FILL'(&J,1)
.*
.*  DO CURSORY CHECK FOR X'NNN' AND B'0101', ETC. (ONLY APOSTROPHES)
.*
.NOPAD   AIF   (&MACQUOT).NOTHEX
         AIF   (&K LT 4).NOHEX
         AIF   ('&STR'(2,1) NE '''' OR '&STR'(&K,1) NE '''').NOHEX
         AIF   ('&USE' EQ 'HEX').MEND   BYPASS .TEST CHECKS (FAIL)
.*  FOR UNQUOTED STRING AND STRIP OPTION, RETURN
.NOHEX   AIF   (&DEL).TEST
.NOTHEX  ANOP  ,
&I       SETA  0
.*
.*  WHEN CALLER SPECIFIES FILL CHARACTER, REPLACE BLANKS
.*
&MYSTR   SETC  '&STR'        USE WORKING STRING
&K       SETA  K'&MYSTR      JUST IN CASE
         AIF   ('&PAD' EQ '').PADDED
.SCANBLK AIF   (&I GE &K).PADDED
&I       SETA  &I+1
&CH      SETC  '&MYSTR'(&I,1)
         AIF   ('&CH' NE ' ').SCANBLK
         AIF   (&I EQ &K).PADRITE
         AIF   (&I GT 1).PADMID
&MYSTR   SETC  '&PAD'.'&MYSTR'(2,&K-1)
         AGO   .SCANBLK
.PADMID  ANOP  ,
&MYSTR   SETC  '&MYSTR'(1,&I-1).'&PAD'.'&MYSTR'(&I+1,&K-&I)
         AGO   .SCANBLK
.PADRITE ANOP  ,
&MYSTR   SETC  '&MYSTR'(1,&I-1).'&PAD'
         AGO   .SCANBLK
.*
.*  NOW PROCESS THE STRING:
.*
.*  1) STRIP - FOR EACH DOUBLED AMPERSAND AND APOSTROPHE, COPY ONLY ONE
.*     FOR QUOTED STRING, DOUB AND FOUR OFF - STRIP FRAMING QUOTES
.*  2) DOUB/FOUR - FOR EACH, ADD TWO/FOUR AMPERSANDS OR APOSTROPHES
.*
.PADDED  ANOP  ,
&MACQSTR SETC  '&MYSTR'      PROVISIONAL
&K       SETA  K'&MYSTR      JUST IN CASE
         AIF   (&DOUB OR &FOUR).PROCEED
         AIF   (NOT &DEL).TEST
         AIF   (NOT &MACQUOT OR &K LT 2).PROCEED
&MYSTR   SETC  '&MYSTR'(2,&K-2)  ELIMINATE FRAME
&K       SETA  K'&MYSTR      ADJUST
&MACQSTR SETC  '&MYSTR'      RETURN
         AIF   (&K EQ 0).NU  SET NULL FLAG IF DIMINISHED TOO MUCH
.PROCEED ANOP  ,
&I       SETA  1
&MACQSTR SETC  ''
.CHARLUP ANOP  ,
&CH      SETC  '&MYSTR'(&I,1)
         AIF   ('&CH' NE '''' AND '&CH' NE '&&').COPYONE
         AIF   (&DEL).STRIPPR
&CH      SETC  '&CH'.'&CH'
         AIF   (NOT &FOUR).COPYONE
&CH      SETC  '&CH'.'&CH'
         AGO   .COPYONE
.STRIPPR AIF   (&I GE &K).COPYONE
         AIF   ('&MYSTR'(&I+1,1) NE '&CH').COPYONE  ERROR?
&I       SETA  &I+1          SKIP DOUBLED CHARACTER
.COPYONE ANOP  ,
&MACQSTR SETC  '&MACQSTR'.'&CH'
&I       SETA  &I+1
         AIF   (&I LE &K).CHARLUP
.*
.*  NOW SEE WHETHER AND WHAT WE NEED TO DO TO THE STRING
.*
.*  1) UNQUOTED STRING, AND DOUB OR FOUR ON - FRAME IN QUOTES
.*       FOR FOUR, MUST DO PRIOR TO DOUBLING
.*  2) EXAMINE THE RESULT STRING - SET MACQUOT FLAG IF QUOTED,
.*       OTHERWISE CHECK FOR ILLEGAL APOSTROPHES AND SET MACPERR
.*
         AIF   (NOT &DOUB AND NOT &FOUR AND &MACQUOT).TEST
&MACQSTR SETC  ''''.'&MACQSTR'.''''
         AIF   (NOT &FOUR).TEST
&MACQSTR SETC  ''''.'&MACQSTR'.''''
.TEST    ANOP  ,
&K       SETA  K'&MACQSTR
&MACQUOT SETB  0             RESET FOR SHORT OR NULL STRING
         AIF   (&K LT 2).SKPQUOT
&MACQUOT SETB  ('&MACQSTR'(1,1) EQ '''' AND '&MACQSTR'(&K,1) EQ '''')
.SKPQUOT AIF   (&MACQUOT).MEND
&I       SETA  0
.UNBALUP AIF   (&I GE &K).MEND
&I       SETA  &I+1
         AIF   ('&MACQSTR'(&I,1) NE '''').UNBALUP
&MACPERR SETB  1
.MEND    MEND
