         MACRO ,
&NM      VOLREAD &TYPE,&OPERAND,&USE=,&SIZE=,&MODE=   UPDATED ON 85350
         GBLC  &MACPLAB
         LCLA  &I,&J
&MACPLAB SETC  '&NM'
&I       SETA  0
         AIF   ('&TYPE' EQ 'CLOSE').CLOSE
&I       SETA  1
         AIF   ('&TYPE' EQ 'TCLOSE').CLOSE
&I       SETA  2
         AIF   ('&TYPE' EQ 'OPEN').CHKUSE
&I       SETA  3
         AIF   ('&TYPE' EQ 'DSCB').TEST1
&I       SETA  4
         AIF   ('&TYPE' EQ 'DSC3').TEST1
&I       SETA  5
         AIF   ('&TYPE' EQ 'SEARCH').LOAD1
&I       SETA  6
         AIF   ('&TYPE' EQ 'SEEK').LOAD1
&I       SETA  7
         AIF   ('&TYPE' EQ 'FIND').LOAD1
&I       SETA  8
         AIF   ('&TYPE' EQ 'GETDE').TEST1
&I       SETA  9
         AIF   ('&TYPE' EQ 'READ').TEST1
&I       SETA  10
         AIF   ('&TYPE' EQ 'NOTE').TEST1
&I       SETA  11
         AIF   ('&TYPE' EQ 'POINT').LOAD1
&I       SETA  12
         AIF   ('&TYPE' EQ 'GET').TEST1
&I       SETA  13                                                85350
         AIF   ('&TYPE' EQ 'UPDATE').LOAD1                       85350
&I       SETA  14                                                85350
         AIF   ('&TYPE' EQ 'FEOM').CLOSE                        GP02247
&I       SETA  15                                                85350
         AIF   ('&TYPE' EQ 'TRACK').LOAD1                       GP09158
         AIF   ('&TYPE' EQ 'LOAD').INIT
         MNOTE 8,'TYPE OPERAND REQUIRED'
         MEXIT ,
.INIT    ANOP  ,
&I       SETA  &SYSNDX
         MACPARM R15,15,@VOLREAD,OP=ICM,OPR=ICM,MODE=THREE PRIOR LOAD?
         BNZ   VLR@&I
         SERVCALL LPALD,=CL8'@VOLREAD'
         ST    R0,@VOLREAD
VLR@&I   DS    0H
         MEXIT ,
.TEST1   AIF   (T'&OPERAND EQ 'O').CLOSE
         AGO   .LR1
.CHKUSE  AIF   (T'&USE EQ 'O').MODE1
&J       SETA  128
         AIF   ('&USE' EQ 'VTOC').MODE1
&J       SETA  64
         AIF   ('&USE' EQ 'PDS').MODE1
&J       SETA  32
         AIF   ('&USE' EQ 'DATA').MODE1
&J       SETA  0
         MNOTE 4,'INVALID USE PARAMETER &USE'
.MODE1   AIF   (T'&MODE EQ 'O').LOAD1                            85350
         AIF   ('&MODE' EQ 'INPUT').LOAD1                        85350
&J       SETA  &J+1          SET OUTPUT FLAG                     85350
         AIF   ('&MODE' EQ 'UPDAT' OR '&MODE' EQ 'OUTPUT' OR           *
               '&MODE' EQ 'UPDATE').LOAD1                        85350
         MNOTE 4,'INVALID MODE OPERAND &MODE'                    85350
.LOAD1   AIF   (T'&OPERAND NE 'O').LR1P
         MNOTE 8,'FUNCTION &TYPE REQUIRES AN OPERAND'
         MEXIT ,
.LR1P    AIF   ('&TYPE' NE 'POINT').LR1
         MACPARM R1,&OPERAND,OP=L   LOAD TTR VALUE
         AGO   .LR1CM
.LR1     MACPARM R1,&OPERAND,NULL=0
.LR1CM   AIF   (&J EQ 0).CLOSE
         ICM   R1,8,=AL1(&J)
.CLOSE   AIF   (&I NE 2 OR T'&SIZE EQ 'O').CLOSET
         MACPARM R0,&SIZE
         IC    R0,=AL1(&I)
         AGO   .BAL
.CLOSET  MACPARM R0,&I
.BAL     ANOP  ,
&MACPLAB L     R15,@VOLREAD
         BALSR R14,R15
&MACPLAB SETC  ''
         MEND  ,
