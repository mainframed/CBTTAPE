         MACRO
&NM      FDLINE &ARGS,&END=
.*
.*   BUILD ONE DISPLAY LINE DEFINITION FOR THE SCLINE SERVICE
.*
.*   FDLINE (FLAGS),LABEL:,(FIELD ATTRIBS),OFF-,TEXT.....   SINGLE DEF.
.*   FDLINE (FLAGS),fields.....,    (trailing comma)  1/n
.*   DC or FDLITEM ...                                m/n
.*   FDLINE *END             GENERATE CLOSING NAME    n/n
.*
.*   ATTRIBUTES:
.*
.*   IN, OUT, INT(ENSE OUT),  N.B. INTENSE INPUT NOT IMPLEMENTED
.*   RED, BLUE, GREEN, CYAN, TURQ(OISE), WHITE, YELLOW
.*   BLACK (NON-DISPLAY)     FORCES INPUT MODE                  GP09355
.*
.*   FLAGS:  LADJ   RADJ
.*           UPPER  SCROLL
.*           MDT
.*
         GBLC  &ZZZLEND      PRIOR/NEXT END LABEL
         GBLC  &ZZZFLAB      NEXT FIELD LABEL
         GBLC  &ZZZFLAN      FDLINE STATEMENT LABEL             GP09358
         LCLC  &LABEL,&C,&D                                     GP09358
         LCLB  &F0,&F1,&F2,&F3,&F4,&F5,&F6,&F7
         LCLB  &INP,&INT,&BLACK                                 GP09353
         LCLA  &I,&J,&K,&L,&M,&N
         AIF   ('&ARGS' EQ '*END').CLOSED
         AIF   ('&ZZZLEND' EQ '').PRVENDD
&ZZZLEND DC    X'00'         PRIOR LINE UNCLOSED
&ZZZLEND SETC  ''            SHOW LABEL USED
         MNOTE *,'FDLINE: PRIOR END= NOT DEFINED; GENERATED'
.PRVENDD ANOP  ,
&N       SETA  N'&SYSLIST
&ZZZLEND SETC  '&END'
         AIF   ('&ZZZLEND' NE '').HAVELAB
&ZZZLEND SETC  'ZZL'.'&SYSNDX'.'Z'
.HAVELAB ANOP  ,
&LABEL   SETC  '&NM'
         AIF   ('&LABEL' NE '').HAVELOC
&LABEL   SETC  'ZZL'.'&SYSNDX'.'L'
.HAVELOC ANOP  ,
&LABEL   DC    0A(0),AL2(&ZZZLEND-&LABEL-3)  TEXT AND CONTROL LENGTH
&ZZZFLAN SETC  '&LABEL'      REMEMBER FOR OFFSET                GP09358
&I       SETA  0
         AIF   (&N GT 0).ARGSOME  HAVE MULTIPLE ARGUMENTS
         DC    AL1(0)        FLAGS - NONE
         AGO   .MEND         NEEDS END= ?                       GP09355
.ARGSOME AIF   ('&SYSLIST(1)' EQ '').ARGFLGS
         AIF   ('&SYSLIST(1)'(1,1) EQ '(').ARGFLGS
         DC    AL1(0)        FLAGS - NONE
         AGO   .LOOP         START WITH FIRST ARGUMENT
.ARGFLGS ANOP  ,
&I       SETA  1             FIRST ARG IS FLAGS; START LOOP AT 2
&J       SETA  0
&L       SETA  N'&SYSLIST(1)  NUMBER OF SUBARGUMENTS
.FGLOOP  AIF   (&J GE &L).EXPFLAG
&J       SETA  &J+1
&C       SETC  '&SYSLIST(&I,&J)'
         AIF   ('&C' EQ '').FGLOOP
&M       SETA  &M+1
&F3      SETB  (&F3 OR ('&C' EQ 'LADJ'))
&F4      SETB  (&F4 OR ('&C' EQ 'RADJ'))
&F5      SETB  (&F5 OR ('&C' EQ 'UPPER'))
&F5      SETB  (&F5 OR ('&C' EQ 'UPP'))
&F6      SETB  (&F6 OR ('&C' EQ 'SCROLL'))
&F6      SETB  (&F6 OR ('&C' EQ 'SCR'))
&F7      SETB  (&F7 OR ('&C' EQ 'MDT'))
         AIF   (&M EQ (&F0+&F1+&F2+&F3+&F4+&F5+&F6+&F7)).FGLOOP
         MNOTE 4,'FDLINE: INVALID FLAG PARM &C '
         AGO   .FGLOOP
.EXPFLAG ANOP  ,
&ZZZFLAB DC    B'&F0&F1&F2&F3&F4&F5&F6&F7'
&ZZZFLAB SETC  ''
.LOOP    AIF   (&I GE &N).DONE
&I       SETA  &I+1
&C       SETC  '&SYSLIST(&I)'
         AIF   ('&C' EQ '').LOOP
         AIF   ('&C'(1,1) EQ '(').FIELD
         AIF   ('&C'(1,1) EQ '''').STRING
         AIF   ('&C'(K'&C,1) EQ ':').LABEL
         AIF   ('&C'(K'&C,1) EQ '-').OFFSET                     GP09358
         AIF   ('&C'(1,1) EQ 'C').COUNT
         AIF   ('&C'(1,1) EQ 'X').HEXER
         AIF   ('&C'(1,1) GE '0' AND '&C'(1,1) LE '9').COUNT
.ARGBAD  MNOTE 8,'FDLINE: ARGUMENT &I INVALID - &C NOT A VALID ENTRY'
         AGO   .LOOP
.*
.STRING  ANOP  ,
&ZZZFLAB DC    C&C
&ZZZFLAB SETC  ''
         AGO   .LOOP
.*
.COUNT   AIF   ('&C'(K'&C,1) NE '''').COUNTS
&ZZZFLAB DC    &C
&ZZZFLAB SETC  ''
         AGO   .LOOP
.COUNTS  ANOP  ,
&ZZZFLAB DC    &C' '
&ZZZFLAB SETC  ''
         AGO   .LOOP
.HEXER   AIF   ('&C'(K'&C,1) NE '''').HEXED
&ZZZFLAB DC    &C
&ZZZFLAB SETC  ''
         AGO   .LOOP
.HEXED   ANOP  ,
&ZZZFLAB DC    &C'0'
&ZZZFLAB SETC  ''
         AGO   .LOOP
.*
.FIELD   ANOP  ,
&J       SETA  0
&L       SETA  N'&SYSLIST(&I)  NUMBER OF SUBARGUMENTS
&F0      SETB  0             MUST BE OFF
&F1      SETB  0             MUST BE OFF (RANGE 01-3F)
&F2      SETB  0
&F3      SETB  0        1 0  OUTPUT  ;  1 1  OUT INTENSE
&F4      SETB  0        0 1  INPUT   ;  1 1  OUT INTENSE
&F5      SETB  0             COLOR: GREEN
&F6      SETB  0             COLOR: RED
&F7      SETB  0             COLOR: BLUE
&INP     SETB  0             INPUT                              GP09355
&INT     SETB  0             INTENSE                            GP09355
&BLACK   SETB  0             NON-DISPLAY                        GP09355
.FDLOOP  AIF   (&J GE &L).EXPFLD
&J       SETA  &J+1
&C       SETC  '&SYSLIST(&I,&J)'
         AIF   ('&C' EQ '').FDLOOP
.*DEFER* AIF   ('&C' EQ 'REVERSE').REVERSE
.*DEFER* AIF   ('&C' EQ 'BLINK').BLINK
.*DEFER* AIF   ('&C' EQ 'UNDER').UNDER
.*DEFER* AIF   ('&C' EQ 'DFLT').NULL
         AIF   ('&C' EQ 'BL').BLUE
         AIF   ('&C' EQ 'RE').RED
         AIF   ('&C' EQ 'PI').PINK
         AIF   ('&C' EQ 'MA').PINK
         AIF   ('&C' EQ 'GR').GREEN
         AIF   ('&C' EQ 'CY').TURQ
         AIF   ('&C' EQ 'TU').TURQ
         AIF   ('&C' EQ 'YE').YELLOW
         AIF   ('&C' EQ 'WH').WHITE
         AIF   ('&C' EQ 'BK').BLACK                             GP09355
         AIF   ('&C' EQ 'ND').BLACK                             GP09355
         AIF   ('&C' EQ 'BLUE').BLUE
         AIF   ('&C' EQ 'RED').RED
         AIF   ('&C' EQ 'PINK').PINK
         AIF   ('&C' EQ 'MAGENTA').PINK
         AIF   ('&C' EQ 'GREEN').GREEN
         AIF   ('&C' EQ 'CYAN').TURQ
         AIF   ('&C' EQ 'TURQ').TURQ
         AIF   ('&C' EQ 'TURQOISE').TURQ
         AIF   ('&C' EQ 'YELLOW').YELLOW
         AIF   ('&C' EQ 'WHITE').WHITE
         AIF   ('&C' EQ 'BLACK').BLACK                          GP09355
         AIF   ('&C' EQ 'IN').INPUT
         AIF   ('&C' EQ 'INPUT').INPUT
         AIF   ('&C' EQ 'OUT').OUTPUT
         AIF   ('&C' EQ 'INT').INTENSE
         AIF   ('&C' EQ 'INTENSE').INTENSE
       MNOTE 8,'FDLINE: ARGUMENT &I,&J INVALID - &C NOT A VALID ENTRY'
         AGO   .FDLOOP
.BLUE    ANOP  ,
&F5      SETB  0
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.RED     ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.PINK    ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.GREEN   ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  0
         AGO   .FDLOOP
.*
.TURQ    ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.YELLOW  ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.WHITE   ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.BLACK   ANOP  ,                                                GP09355
&F5      SETB  0                                                GP09355
&F6      SETB  0                                                GP09355
&F7      SETB  1             FORCE NON-ZERO                     GP09355
&BLACK   SETB  1             NON-DISPLAY FLAG                   GP09355
         AGO   .FDLOOP                                          GP09355
.*
.INPUT   ANOP
&INP     SETB  1                                                GP08356
&F3      SETB  0
&F4      SETB  1
         AIF   (&INT).CONFII                                    GP08356
         AGO   .FDLOOP
.*
.OUTPUT  ANOP  ,
&F3      SETB  1
&F4      SETB  0
         AGO   .FDLOOP
.*
.INTENSE ANOP  ,
&INT     SETB  1                                                GP08356
&F4      SETB  1
         AIF   (&INP).CONFII                                    GP08356
&F3      SETB  1
         AGO   .FDLOOP
.CONFII  MNOTE *,'FDLINE: INTENSE INPUT NOT IMPLEMENTED'        GP08356
         AGO   .FDLOOP                                          GP08356
.*
.EXPFLD  AIF   (NOT &BLACK).EXPFLD1                             GP09355
&F4      SETB  0             FORCED INPUT MODE                  GP09353
         AIF   (NOT &F3).EXPFLD2                                GP09353
    MNOTE 4,'FDLINE: CONFLICTING NON-DISPLAY AND OUTPUT ATTR &I'
.EXPFLD1 AIF   (&F3 OR &F4).EXPFLD2
&F3      SETB  1             OUTPUT IS DEFAULT
.EXPFLD2 ANOP  ,
&ZZZFLAB DC    B'&F0&F1&F2&F3&F4&F5&F6&F7'
&ZZZFLAB SETC  ''
&INT     SETB  0                                                GP08356
&INP     SETB  0                                                GP08356
         AGO   .LOOP
.*
.LABEL   AIF   ('&ZZZFLAB' EQ '').LABNEW
&ZZZFLAB DC    X'00'         ERROR ?
         MNOTE 0,'FDLINE: CONSECUTIVE LABELS &ZZZFLAB AND &C '
.LABNEW  ANOP  ,
&ZZZFLAB SETC  '&C'(1,K'&C-1)
         AGO   .LOOP
.*
.OFFSET  ANOP  ,                                                GP09358
&D       SETC  '&C'(1,K'&C-1)                                   GP09358
&D       EQU   *-&ZZZFLAN    FIELD OFFSET (NO LEN)              GP09358
         AGO   .LOOP                                            GP09358
.*
.DONE    AIF   ('&ZZZFLAB' EQ '').DONEL
&ZZZFLAB DC    X'00'         ERROR ?                            GP09355
&ZZZFLAB SETC  ''
.DONEL   AIF   ('&ZZZLEND' EQ '').DONED
         AIF   ('&SYSLIST(&N)' EQ '').MEND   CONTINUATION       GP09355
.*       AGO   .CLOSED                                          GP09355
.DONED   AIF   ('&END' NE '').MEND   REMOTE END
.*
.CLOSER  AIF   ('&ZZZFLAB' EQ '').CLOSED
&ZZZFLAB DC    X'00'         ERROR ?
&ZZZFLAB SETC  ''
         AGO   .MEND
.CLOSED  AIF   ('&ZZZLEND' EQ '').MEND
&ZZZLEND DC    X'00'
&ZZZLEND SETC  ''
.*
.MEND    MEND  ,
