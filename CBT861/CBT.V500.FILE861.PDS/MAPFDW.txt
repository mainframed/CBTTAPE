         MACRO ,
&NM      MAPFDW &DSECT=,&PFX=FDW                        ADDED ON 84237
         LCLC  &P,&Q
&P       SETC  '&PFX'
&Q       SETC  '&P'.'SECT'
         AIF   ('&NM' EQ '').DFDS
&Q       SETC  '&NM'
.DFDS    AIF   ('&DSECT' EQ 'NO').NODS
&Q       DSECT ,
         AGO   .CMDS
.NODS    AIF   ('&NM' EQ '').CMDS
&Q       DS    0A
.CMDS    ANOP  ,
&P.FDA   DC    A(0)          ADDRESS OF FD/FDIN SEQUENCE
&P.FWA   DC    A(0)          ADDRESS OF INPUT WORK AREA (FIW)
&P.FWL   DC    F'0'          LENGTH OF FIW
&P.SCAN  DC    A(0)          CURRENT INPUT SCANNER ADDRESS       86209
&P.SCAL  DC    F'0'          RESIDUAL INPUT LENGTH TO SCAN       86209
&P.BIA   DC    A(0)          ADDRESS OF INPUT BUFFER
&P.BIL   DC    F'0'          CURRENT LENGTH OF INPUT
&P.BIM   DC    Y(0)          MAXIMUM INPUT SIZE                  86209
&P.BOM   DC    Y(0)          MAXIMUM OUTPUT SIZE                 86209
&P.BOA   DC    A(0)          ADDRESS OF OUTPUT BUFFER
&P.BOL   DC    F'0'          LENGTH OF OUTPUT BUFFER
&P.BOS   DC    Y(0)          SCREEN CAPACITY USED BY OUTPUT      86209
&P.CUR   DC    X'FFFF' 1/3   0 OR REQUESTED CURSOR ADDRESS
&P.CUD   DC    X'FFFF' 2/3   DEFAULT CURSOR ADDRESS
&P.ICUD  DC    Y(0)    3/3   (LAST) INPUT CURSOR ADDRESS         85034
&P.IAID  DC    C' '          (LAST) INPUT AID                    85034
&P.ICOD  DC    AL1(0)        (LAST) INPUT AID CONVERTED          85034
&P.FG    DC    X'00'         ENTRY FLAG (OR'ED FROM ALL FIWFG)
&P.FTXT  EQU   X'80'           ENTRY CONTAINS TEXT
&P.FERR  EQU   X'40'           TEXT IS IN ERROR
&P.FPEN  EQU   X'20'           FIELD SELECTED BY LIGHT-PEN
&P.FHLP  EQU   X'10'           TEXT IS HELP OR ?                 88221
&P.FF1   EQU   X'08'           FIRST FDIN MODIFIED               86338
&P.FF2   EQU   X'04'           OTHER FDIN MODIFIED               86338
&P.FINT  EQU   X'02'           INTENSIFIED DISPLAY FIELD         86338
&P.FINV  EQU   X'01'           INVERT INTENSITY                  86338
&P.OPT   DC    X'00'         PROCESSING OPTIONS
&P.OKEEP EQU   X'80'           SCINIT NOT TO ERASE FIW AREA      86231
&P.MKEEP EQU   X'40'           SCMOVE TO KEEP FIW FLAGS          87159
&P.MKDEL EQU   X'20'           SCMARK TO CLEAR NON-ESSENTIAL IWS 87360
&P.OSA   EQU   X'10'           SCLIST TO USE SA, NOT SFE         87336
&P.LOCK  EQU   X'08'           IF OFF, COND. INPUT IS LOCKED     87156
&P.OHELP EQU   X'04'           LOOK FOR 'HELP' AND '?'           88221
&P.SFTAB EQU   X'02'           DON'T MERGE SF - TABLE PROC.     GP02363
&P.NREAD EQU   X'02'           SCLINE WRITE ONLY; NO READ       GP13068
&P.KYFLP EQU   X'00' >LATER<   SWAP KEYS 13-24 WITH 1-12        GP12305
&P.KEY12 EQU   X'01'           CONVERT KEYS 13-24 TO 1-12        88211
&P.SCAC  DC    A(0)          SCANNER CONTROL LIST ADDRESS        87312
&P.SCMSG DC    A(0)          ERROR MESSAGE ADDRESS               87360
&P.DEVIC DC    A(0)          DEVICE ATTRIBUTE POINTER (MAPCMPR)  93344
&P.FDRST DC    A(0)          FD ADDR - RESTART AFT. FULL SCREEN GP02363
.*                             (FDOPT ADDR. IF FDKEEP SEQUENCE) GP02363
&P.RELOC DC    A(0)          ADDRESS OF REGISTER AREA FOR FD    GP03269
.*                             ADDRESS RESOLUTION (SUBXSCRN)    GP03269
&P.PROFG DC    X'00'         PROCESSING FLAG FOR SUBXSCRN, ETC. GP03273
&P.PFHED EQU   X'80'           NEED PAGE HEADER                 GP03273
&P.PFSHO EQU   X'40'           DISPLAY PAGE                     GP03273
&P.PFENT EQU   X'20'           RETURN TO CALLER ON ENTER ONLY   GP04108
&P.PFCUR EQU   X'10'           USE CURSOR                       GP12297
&P.PFARC EQU   X'08'           CURSORS IN (ROW,COL) FORMAT      GP09356
&P.PFCAN EQU   X'04'           CAN COMMAND AFTER DISPLAY        GP09356
&P.PFEND EQU   X'02'           END COMMAND AFTER DISPLAY        GP09350
&P.PFDAT EQU   X'01'           DATA ON PAGE                     GP03273
         DC    X'00'           RESERVED                         GP03273
&P.SVCUR DC    H'0'          SCINIT SAVED CURSOR ADDRESS        GP05338
.*       ZAP SPACE                                               87360
&P.SVPAG DC    A(0)          ADDRESS OF INITIAL (PAGE) FD CHAIN GP03277
&P.@HLP  DC    A(0)          FD OR FDLINE FOR HELP SCREEN       GP05080
&P.CATR  DC    XL2'0'  1/3   CURSOR ADDRESS FOR SPECIAL SA      GP10021
&P.CATL  DC    XL1'0'  2/3     LENGTH OF SA FIELD               GP10021
&P.CATC  DC    CL1'6'          SA COLOR CODE                    GP10021
         DC    1A(0)           RESERVED                         GP05080
&P.FDWLN EQU   *-&P.FDA      SIZE TO BE CLEARED                  93344
         MEND  ,
