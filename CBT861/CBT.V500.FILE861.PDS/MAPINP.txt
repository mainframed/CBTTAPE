         MACRO ,                                        ADDED ON 82116
&NM      MAPINP &PREFIX=PN,&DSECT=YES  INPWORK MAPPING
.*    UPDATED FOR ESA AND OS/390 BY G.P.
         LCLC  &DS,&P
&P       SETC  '&PREFIX'
&DS      SETC  '&P'.'PARM'
         AIF   ('&NM' EQ '').HAVEDS
&DS      SETC  '&NM'
.HAVEDS  AIF   ('&DSECT' NE 'YES').NODSECT
&DS      DSECT ,             MAPPING OF INPWORK EXPANSION
         AGO   .COMDS
.NODSECT ANOP  ,
&DS      DS    0H            MAPPING OF INPWORK EXPANSION
.COMDS   ANOP  ,
&P.DDNAM  DC   CL8' '        PRIMARY DDNAME
&P.DDALT  DC   CL8' '        ALTERNATE DDNAME
&P.EODAD  DC   A(1)          EODAD OR 0,1 IF NONE
&P.EODAM  EQU  X'80'         WHEN ON, EODAD ENTERED AMODE 31, ELSE 24
&P.PPDE   DC   A(0)          POINTER TO RETURNED DIRECTORY DATA  89351
&P.WIDTH  DC   H'0'          LINE WIDTH FOR TRUNCATION
          DC   H'0'               RESERVED
&P.PRFG   DC   X'00'         OPTION FLAGS
&P.FGABE  EQU  X'80'           ABEND IF OPEN FAILS/DD MISSING
&P.FGDUM  EQU  X'40'           ALLOW DD DUMMY (ELSE ABEND)
&P.FGNWTO EQU  X'20'           SUPPRESS WTO IF BAD OPEN AND NO ABEND
&P.FGJFCB EQU  X'10'           WORD FOLLOWING INPWORK IS JFCB FOR OPEN
&P.FGPDE  EQU  X'08'           USER REQUESTED PDE FEEDBACK       89351
&P.FG1BUF EQU  X'01'           SINGLE BUFFER (SHORT ON MEMORY)  GP08088
&P.FILL   DC   X'00'         FILL BYTE FOR PADDED RECORDS
&P.EDIT   DC   X'80'         EDIT OPTIONS (MATCH DCMFG1)
&P.FESET  EQU  X'80'           OPTION BITS SET (ELSE USE DEFAULTS)
&P.FEOSI  EQU  X'40'           OSI LINE NUMBER FORMAT
&P.FEHWD  EQU  X'20'           HALF-WORD LINE NUMBERS
&P.FEINT  EQU  X'10'           CREATE INTEGER LINE NUMBERS
&P.FEEDT  EQU  X'08'           CREATE EDIT LINE NUMBERS 4C.3C
&P.FETSO  EQU  X'04'           LINE NUMBERS LEFT-ADJUSTED IN RECFM=V
&P.FENB#  EQU  X'01'           INSERT LINE NUMBERS EVEN IF NON-BLANK
&P.PDS    DC   X'00'         PDS PROCESSING OPTIONS              89351
&P.FPDIR  EQU  X'80'           OPEN PDS DIRECTORY                89351
&P.FPMEM  EQU  X'40'           PROCESS ALL MEMBERS               89351
&P.FPALI  EQU  X'20'           ALSO PROCESS ALIAS ENTRIES        89351
&P.FPUPA  EQU  X'01'           BUILD ./ ADD CARDS                89351
&P.JFCB   DC   0A(0)         OPTIONAL JFCB ADDRESS FOR OPENJ OR 0
         MEND  ,
