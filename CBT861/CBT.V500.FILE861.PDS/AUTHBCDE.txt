         MACRO (AUTHORIZED VERSION)
&REF     AUTHBCDE &RU,&LENGTH=,&SP=0,&WORKREG=,&BNDRY=DBLWD,           X
               &EP=BUILDCDE,&LOC=,&AUTH=YES,&RENT=,&SYS=MVS     GP06320
.**********************************************************************
.*                                                                    *
.* MACRO NAME = BUILDCDE - RENAMED TO AUTHBCDE FOR AC=1 USERS         *
.*                                                                    *
.* DESCRIPTIVE NAME = GET STORAGE AND BUILD A CDE TO NAME IT          *
.*                                                                    *
.* FUNCTION = MAKES A STORAGE AREA EASIER TO LOCATE IN A              *
.*            DUMP BY MAKING IT APPEAR AS A LOAD_MODULE.              *
.*                                                                    *
.* STATUS = R200                                                      *
.*                                                                    *
.* AUTHOR = GILBERT SAINT-FLOUR <GSF@POBOX.COM>                       *
.*                                                                    *
.* ENVIRONMENT = SEE BELOW                                            *
.*                                                                    *
.*     AMODE = ANY                                                    *
.*     SCP   = MVS/XA OR MVS/ESA   |  MVS 3.8J WITH SYS=MVS           *
.*     KEY   = USER                                                   *
.*     MODE  = PROBLEM                                                *
.*     APF   = OFF                                                    *
.*                                                                    *
.* OPERATION = BUILDCDE USES THE "LOADER" FORM OF IDENTIFY TO CREATE  *
.*             A MAJOR CDE AND CORRESPONDING XL, THEN ISSUES A LOAD   *
.*             SVC TO CREATE AN LLE AND ASSOCIATE THE CDE WITH THE    *
.*             CURRENT TCB.                                           *
.*                                                                    *
.* INVOCATION = SEE BELOW                                             *
.*                                                                    *
.*          (BEFORE)     GETMAIN RU,LV=20000                          *
.*                                                                    *
.*          (AFTER)      GETMAIN RU,LV=20000                          *
.*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM        *
.*                                                                    *
.* NOTES = SEE BELOW                                                  *
.*                                                                    *
.*        BUILDCDE MAY BE ISSUED MORE THAN ONCE, PROVIDED THE EP      *
.*        KEY-WORD HAS A DIFFERENT VALUE EACH TIME.                   *
.*                                                                    *
.*        IF STORAGE WAS ALLOCATED IN SUB-POOL ZERO (SP=0 IS          *
.*        SPECIFIED OR DEFAULTED TO IN THE GETMAIN MACRO),            *
.*        THE DELETE MACRO MAY BE ISSUED AGAINST THE SPECIFIED EP     *
.*        TO DELETE THE CDE AND FREE UP THE STORAGE.                  *
.*                                                                    *
.*        IF GETMAIN SPECIFIES A NON-ZERO SUBPOOL, THE DELETE MACRO   *
.*        DELETES THE CDE BUT DOES NOT FREE UP THE STORAGE:  YOU      *
.*        MUST ISSUE FREEMAIN YOURSELF.                               *
.*                                                                    *
.*        IN A MAJOR CDE CREATED BY THE "LOADER" FORM OF IDENTIFY,    *
.*        THE "RENT" AND "REUS" ATTRIBUTES ARE OFF.  THIS MEANS       *
.*        THAT YOU MAY NOT ISSUE LOAD AGAINST THE EP NAME (BUILDCDE   *
.*        HAS ALREADY ISSUED LOAD), BUT YOU MAY ISSUE LINK, ATTACH    *
.*        OR XCTL IF YOU WANT TO (ONCE ONLY).  YOU MAY ALSO ISSUE     *
.*        THE STANDARD FORM OF IDENTIFY TO DEFINE SECONDARY ENTRY     *
.*        POINTS WHICH HAVE THE "RENT" ATTRIBUTE.                     *
.*        YOU MAY USE RENT=YES TO SET RENT/REUS IF AUTHORIZED   GP    *
.*                                                                    *
.*        IF THIS MACRO IS USED BY AN AUTHORIZED PROGRAM, THEN        *
.*        A S306 ABEND WILL OCCUR UNLESS AUTH=YES IS SPECIFIED OR     *
.*        DEFAULTED.                                            GP    *
.*                                                                    *
.*        THE IDENTIFY MACRO SUPPLIED IN SYS1.MACLIB SUPPORTS THE     *
.*        "LOADER" FORM, BUT THE FORMAT OF THE PARAMETER LIST IS      *
.*        NOT DOCUMENTED, AS FAR AS I KNOW.  THE RETURN CODES X'18'   *
.*        OR X'1C' MAY BE RETURNED IN R15 WHEN THE PARAMETER LIST     *
.*        IS INVALID.  THESE RETURN CODES ARE DOCUMENTED ALONG WITH   *
.*        THE IDENTIFY MACRO.                                         *
.*                                                                    *
.*        WHEN THE LOAD-LIST IS EMPTY (I.E. NO LOAD HAS BEEN ISSUED   *
.*        FOR THIS TCB YET), IDENTIFY FAILS WITH A RETURN CODE 12.    *
.*        THIS CAN BE CIRCUMVENTED BY ISSUING THE FOLLOWING SEQUENCE: *
.*                                                                    *
.*                       LOAD    EP=IEFBR14                           *
.*                       L     R0,length                              *
.*                       AUTHBCDE RU,LENGTH=(0),ADDR=(1),EP=DYNAM     *
.*                       DELETE  EP=IEFBR14                           *
.*                                                                    *
.**********************************************************************
.*     CORRECTIONS AND CHANGES BY GERHARD POSTPISCHIL  2006-11-15     *
.*        RETROFIT TO FUNCTION CORRECTLY UNDER MVS 3.8J (SYS=MVS)     *
.*        CHANGE TO ASSEMBLE UNDER ASM/XF                             *
.*        ADDED AUTH=  RENT=  AND SYS=  KEYWORDS                      *
.**********************************************************************
         LCLA  &O16                    OFFSETS IN PARM LIST     GP06320
         LCLC  &LABEL,&R               FOR ASM XF               GP06320
.*   USE SYS=MVS TO EXPAND FOR MVS 3.8J                         GP06320
      MNOTE *,'       GETMAIN &RU,LV=&LENGTH,SP=&SP,BNDRY=&BNDRY,&LOC='
&REF     GETMAIN &RU,LV=&LENGTH,SP=&SP,BNDRY=&BNDRY,&LOC=
&O16     SETA  16                      OFFSET TO EXTENT LIST    GP06320
         AIF   ('&SYS' NE 'MVS').NEWSYS                         GP06320
&O16     SETA  12                      OFFSET TO EXTENT LIST    GP06320
.NEWSYS  ANOP  ,                       XA, ESA, .....           GP06320
         ST    R1,0(,R1)               STORE ENTRY-POINT ADDR
         MVC   4(8,R1),=CL8'&EP '      CDNAME                   GP06320
         AIF   ('&SYS' EQ 'MVS').OLDSYS   SHORTER LIST IN 3.8   GP06320
         XC    12(4,R1),12(R1)         CLEAR BYTES 12-15
.OLDSYS  LA    R14,16                  LENGTH OF PSEUDO XL      GP06320
         LA    R15,1                   NUMBER OF RELOCATION FACTORS
         STM   R14,R1,&O16.(R1)        FORMAT BYTES 16-23       GP06320
         MVI   &O16+8(R1),X'80'        END OF LIST              GP06320
         LR    R14,R1                  SAVE GETMAINED ADDRESS
         MNOTE *,'       IDENTIFY EPLOC=0,ENTRY=(R1)'
         SLR   R0,R0                   LOADER-TYPE IDENTIFY
         SVC   41                      ISSUE IDENTIFY SVC
&LABEL   SETC  'IHB&SYSNDX'
&R       SETC  '&WORKREG(1)'           SHORT FORM
         AIF   (T'&WORKREG NE 'O').WKREG2
         MNOTE 4,'WORKREG OPERAND OMITTED, WORKREG=(R2) ASSUMED'
&R       SETC  'R2'
.WKREG2  L     &R,X'021C'              PSATOLD (MY TCB)
         L     &R,TCBJSCB-TCB(,&R)     MY JSCB
         USING IEZJSCB,&R
         TM    JSCBOPTS,JSCBAUTH       CHECK AUTHORIZATION
         BZ    &LABEL.N                JUMP IF NOT AUTHORIZED
         MODESET KEY=ZERO              SWITCH TO KEY ZERO
         NI    JSCBOPTS,255-JSCBAUTH   TURN APF FLAG OFF
         SYNCH &LABEL.L                EXECUTE ROUTINE WITH TCB KEY
         OI    JSCBOPTS,JSCBAUTH       TURN APF FLAG BACK ON
         AIF   ('&AUTH' NE 'YES' AND '&RENT' NE 'YES').NOAPF    GP06320
.*   IN MVS 3.8J, THE TEST PROGRAM'S LINK FAILS WITH IEA703I 306-C
.*     TO AVOID THIS, WE SET THE CDE TO AUTH LIB & AUTH MODULE
.*     WHEN THE USER REQUESTS AUTH=YES                          GP06320
.*     WHEN RENT=YES, CDE IS FLAGGED REENTRANT AND REUSABLE     GP06320
         L     &R,X'021C'              PSATOLD (MY TCB)         GP06320
         ICM   &R,15,TCBJPQ-TCB(&R)    GET JOB PACK QUEUE       GP06320
         BZ    &LABEL.C                                         GP06320
         USING CDENTRY,&R              CDE MAPPING (IHACDE)     GP06320
&LABEL.A CLC   =CL8'&EP ',CDNAME       MATCHING MODULE ?        GP06320
         BE    &LABEL.B                YES; SET IT              GP06320
         ICM   &R,15,CDCHAIN           ANOTHER CDE ON CHAIN?    GP06320
         BNZ   &LABEL.A                YES; TRY IT              GP06320
         B     &LABEL.C                OOPS - WHAT HAPPENED?    GP06320
&LABEL.B DS    0H                                               GP06320
         AIF   ('&AUTH' NE 'YES').NOAPFL                        GP06320
         OI    CDATTR2,CDSYSLIB+CDAUTH  FLAG AUTHORIZED         GP06320
.NOAPFL  AIF   ('&RENT' NE 'YES').NOAPF                         GP06320
         OI    CDATTR,CDREN+CDSER      SET RENT/REUS            GP06320
.NOAPF   DROP  &R
&LABEL.C LR    &R,R0                   SAVE EP ADDRESS
         MODESET KEY=NZERO             SWITCH TO TCB KEY
         LR    R0,&R                   EP ADDRESS
         B     &LABEL.X
         AIF   ('&SYS' EQ 'MVS').OLDASM                         GP06320
&LABEL.I EQU   &R-2                    WORKREG MUST BE R2-R13
&LABEL.J EQU   13-&R                   WORKREG MUST BE R2-R13
.OLDASM  ANOP  ,           WORKS ONLY IF &R PREVIOUSLY DEFINED  GP06320
&LABEL.N LA    R14,&LABEL.X            RET FROM LOAD
         MNOTE *,'       LOAD EP=&EP,DCB=0'
&LABEL.L LA    R0,=CL8'&EP'            POINT TO EP NAME         GP06320
         SLR   R1,R1                   NO DCB FOR LOAD
         SVC   8                       ISSUE LOAD SVC
         BR    R14                     RETURN NEXT OR SYNCH EX
&LABEL.X LR    R1,R0                   ADDRESS IN R1
         SLR   R0,R0
         ICM   R0,B'0111',&O16+9(R14)  LENGTH IN R0             GP06320
         MEND
