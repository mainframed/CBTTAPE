         MACRO ,
&NM      DEBTRACE &LBL,&TEXT=,&REGS=,&HEX=,&MODE=S,     ADDED ON 85360 *
               &WK=R9,&DEV=1,&TCB=,         WTO VS @PRT  CHANGED 94011 *
               &ROUT=13,&DES=4,&BUGPARM=NO,                ADDED 95067 *
               &CTEXT=,&PACK=,                             ADDED 96081 *
               &PRTMODE=0,&DCB=0,   USER PRINT DCB/MODE    ADDED 99058 *
               &COUNT=,&CALL=DYN,&OPT=,                  CHANGED 98222 *
               &WA=DBTSAVE,                                ADDED 99114 *
               &LIST=                                      ADDED 95235
.**********************************************************************
.*>>>>>>>>> KEPT FOR OLD CODE ONLY - NEW CODE SHOULD USE DBT <<<<<<<<<*
.**********************************************************************
.*                                                                    *
.*  THIS MACRO INVOKES EXTERNAL LOAD MODULE DEBTROLD TO PRODUCE       *
.*  TRACING, REGISTER CONTENTS, AND VARIABLES. (DEBTROLD SHOULD BE IN *
.*  A LINKLIB; AUTHORIZATION IS NOT REQUIRED). OUTPUT WILL BE BY WTO  *
.*  UNLESS A DEBTRACE DD CARD IS SUPPLIED.                            *
.*                                                                    *
.*  REQUIRED:  IN A CSECT OR RSECT:   DEBTRACE MODE=C  DEFINES CODE   *
.*             IN A CSECT OR DSECT:   DEBTRACE MODE=D  DEFINES DATA   *
.*             IN A CSECT (Â¬RENT) :   DEBTRACE MODE=DC   BOTH         *
.*    (NOTE: REQUIRED FORMS MUST NOT APPEAR PRIOR TO FIRST OPTIONAL)  *
.*                                                                    *
.*  OPTIONAL:  LABEL DEBTRACE ...                                     *
.*                TAG OR ,  -  IDENTIFIER ON OUTPUT LISTING / CONSOLE *
.*                                                                    *
.*                REGS= (DEFAULT) | REGS=NO - NO REGISTERS            *
.*                REGS=YES  -  REGISTERS R0 THROUGH R15               *
.*                REGS=(R1,R2) - REGISTERS R1 THROUGH R2              *
.*                REGS=SHORT   - R14 THROUGH R1                       *
.*                                                                    *
.*                TEXT=NAME -  TEXT STRING TO BE SHOWN                *
.*                TEXT=(NAME,LEN) - TEXT W/EXPLICIT LENGTH            *
.*                                                                    *
.*                CTEXT=NAME - CONDITIONAL TEXT STRING TO BE SHOWN    *
.*                CTEXT=(NAME,LEN) - TEXT W/EXPLICIT LENGTH           *
.*                              OUTPUT IN HEX IF NOT PRINTABLE        *
.*                                                                    *
.*                HEX=NAME   -  DATA TO BE SHOWN IN HEXADECIMAL       *
.*                HEX=(NAME,LEN) - TEXT W/EXPLICIT LENGTH             *
.*                                                                    *
.*                PACK=NAME  -  DATA TO BE CONVERTED FROM PACKED      *
.*                PACK=(NAME,LEN) - TEXT W/EXPLICIT LENGTH (LEN IGNRD)*
.*                                                                    *
.*           LIST=((OP1,LN1,FM1),(OP2,LN2,FM2), ... )                 *
.*                                                                    *
.*                OP - ADDRESS EXPRESSION VALID IN S CONSTANT         *
.*                LN - LENGTH EXPRESSION; DEFAULT IS L'OP             *
.*                FM - TEXT | CTEXT | HEX | PACK - DEFAULT IS HEX     *
.*                     OR ABBREVIATED   T | CT | H | P                *
.*                                                                    *
.*  THE REQUIRED FORMS MAY BE OMITTED WHEN PGMTRACE WILL ALSO BE USED *
.*  AND ACTIVATED. IN THAT CASE THE FIRST OPTIONAL FORM MUST INCLUDE  *
.*  CALL=TRC TO GENERATE SHORTER PARAMETER LISTS.                     *
.*                                                                    *
.**********************************************************************
.*  MAINTENANCE:                                                      *
.*                                                                    *
.*  2000/01/03  GYP  REMOVED IN-LINE DEBUG CODE;                      *
.*                   FIXED MODE=C AND MODE=D FOR USE WITH REENTRANT   *
.*                     PROGRAMS.                                      *
.*                                                                    *
.**********************************************************************
     GBLB  &BUGBEAR,&BUGTCB,&BUGSWCH,&BUGSWRT,&BUGFAR,&BUGEXT,&BUGDYN
     GBLB  &BUGTRC           USED WITH ACTIVE PGMTRACE (ESPIE)  GP99113
         GBLA  &MACP#        NUMBER OF SUBLIST PARAMETERS       GP04234
         GBLC  &MACP1,&MACP2,&MACP3,&MACP4,&MACP5               GP04234
         GBLC  &MACP6,&MACP7,&MACP8,&MACP9,&MACP10              GP04234
         GBLC  &V
         LCLA  &LN,&I,&EN,&EM,&EO                               GP95235
         LCLC  &L,&ET,&EL,&EK                                   GP95235
&L       SETC  'L'''                                            GP95235
&V       SETC  'DBT'.'&SYSNDX'
&BUGFAR  SETB  (&BUGFAR OR ('&CALL' EQ 'FAR'))                   95079
&BUGEXT  SETB  (&BUGEXT OR ('&CALL' EQ 'EXTRN'))                 95227
&BUGDYN  SETB  (&BUGDYN OR ('&CALL' EQ 'DYN'))                  GP97261
&BUGDYN  SETB  (&BUGDYN OR ('&CALL' EQ ''))  DROP LOCAL CODE    GP00004
&BUGDYN  SETB  (&BUGDYN OR ('&CALL' EQ 'DYNAMIC'))              GP97261
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRC'))                  GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRACE'))                GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'PGMTRACE'))             GP99113
         AIF   (&BUGBEAR OR '&BUGPARM' EQ 'NO').DOSOME
         AIF   ('&NM' EQ '').MEND
&NM      DS    0H            DEBUG SWITCH NOT ON
         AGO   .MEND
.DOSOME  ANOP  ,                                                 95067
&BUGSWCH SETB  1                                                 95067
         AIF   ('&MODE' EQ 'D' OR '&MODE' EQ 'M').DATA           95228
         AIF   ('&MODE' EQ 'C').CODE
         AIF   ('&MODE' EQ 'DC').CODE   EXPAND BOTH              95067
         AIF   ('&MODE' EQ 'ON').SWON                            95079
         AIF   ('&MODE' EQ 'OFF').SWOFF                          95079
         AIF   ('&MODE' EQ 'CLOSE').SWEND  CLOSE AND QUIT       GP98222
         AIF   (NOT &BUGTRC).NOTTRC                             GP99113
&NM      DC    X'83CD',S(&WA,&V.X-*)             INVOKE TRACE   GP99113
         AGO   .DONEBAS                                         GP99113
.NOTTRC  ANOP  ,                                                GP99113
&NM      STM   R0,R15,&WA    SAVE ALL REGISTERS
         AIF   ('&COUNT' EQ '').DONECNT                          95079
.*  COUNT(3) - SKIP FIRST N CALLS                                95079
         AIF   ('&COUNT(3)' EQ '').CNTNO3                        95079
         ICM   R14,15,&V.3   LOAD SKIP COUNT                     95079
         BNP   &V.C          LIMIT REACHED - PROCESS             95079
         BCTR  R14,0         DECREMENT                           95079
         STCM  R14,15,&V.3   SAVE FOR NEXT TIME                  95079
         B     &V.X          AND SKIP CALL                       95079
&V.3     DC    AL4(&COUNT(3))  INITIAL SKIP COUNT                95079
&V.C     DS    0H                                                95079
.CNTNO3  AIF   ('&COUNT(2)' EQ '').CNTNO2                        95079
         AIF   ('&COUNT(2)' EQ '1').CNTNO2                       95079
         AIF   ('&COUNT(2)' EQ '0').CNTNO2                       95079
.*  COUNT(2) - PROCESS EVERY NTH CALL ONLY                       95079
         ICM   R14,15,&V.2   LOAD COUNTER                        95079
         BNP   &V.L          BAD - PROCESS CALL                  95079
         BCT   R14,&V.N      NON-ZERO; SAVE AND SKIP             95079
         MVC   &V.2,=AL4(&COUNT(2))  REFRESH                     95079
         B     &V.L          AND GO                              95079
&V.2     DC    AL4(1)        INTERVAL COUNTER (DO FIRST ONE)     95079
&V.N     STCM  R14,15,&V.2   UPDATE COUNTER                      95079
         B     &V.X          AND EXIT                            95079
.CNTNO2  AIF   ('&COUNT(1)' EQ '').DONECNT                       95079
         AIF   ('&COUNT(1)' EQ '0').DONECNT                      95079
         ICM   R14,15,&V.1   LOAD LIMIT COUNT                    95079
         BNP   &V.X          SKIP OUT IF NOT VALID               95079
         BCTR  R14,0         DECREMENT                           95079
         B     &V.M          SAVE, AND CONTINUE                  95079
&V.1     DC    AL4(&COUNT(1))  MAXIMUM CALLS TO MAKE             95079
&V.M     STCM  R14,15,&V.1   SAVE FOR NEXT TIME                  95079
.DONECNT ANOP  ,                                                 95079
&V.L     BAS   R1,&V.B                                           95079
.DONEBAS AIF   ('&LBL' EQ '' AND (&BUGEXT OR &BUGDYN OR &BUGTRC)).NOLBL
         DC    CL8'&LBL '
.NOLBL   AIF   ('&REGS' EQ '' OR '&REGS' EQ 'NO').NOREGS         95079
         AIF   ('&REGS' EQ 'R15' OR '&REGS' EQ 'SHORT'                 *
               OR '&REGS' EQ 'RET').RETREG                      GP97225
         AIF   ('&REGS' EQ 'YES' OR '&REGS' EQ 'ALL').REGSALL   GP02246
         AIF   (N'&REGS EQ 2).REGS2                             GP97225
         DC    AL1(0,0),SL2(&REGS(1),&REGS(1))                  GP97225
         AGO   .NOREGS                                          GP97225
.REGS2   DC    AL1(0,0),SL2(&REGS(1),&REGS(2))                  GP97225
         AGO   .NOREGS                                          GP97225
.REGSALL DC    AL1(0,0),SL2(0,15)                               GP97225
         AGO   .NOREGS                                          GP97225
.RETREG  DC    SL2(0,14,1)    R15-R1 ONLY                       GP97225
.NOREGS  AIF   ('&TEXT' EQ '').NOTEXT
         AIF   (N'&TEXT GE 2).TEXT2                             GP97225
         DC    AL1(1,0),SL2(&TEXT(1)),AL2(&L&TEXT(1))           GP97225
         AGO   .NOTEXT                                          GP97225
.TEXT2   DC    AL1(1,0),SL2(&TEXT(1),&TEXT(2))
.NOTEXT  AIF   ('&CTEXT' EQ '').NOCTEXT                         GP97225
         AIF   (N'&CTEXT GE 2).CTEXT2                           GP97225
         DC    AL1(2,0),SL2(&CTEXT(1)),AL2(&L&CTEXT(1))         GP97225
         AGO   .NOCTEXT                                         GP97225
.CTEXT2  DC    AL1(2,0),SL2(&CTEXT(1),&CTEXT(2))                GP97225
.NOCTEXT AIF   ('&HEX' EQ '').NOHEX                             GP97225
         AIF   (N'&HEX GE 2).HEX2                               GP97225
         DC    AL1(3,0),SL2(&HEX(1)),AL2(&L&HEX(1))             GP97225
         AGO   .NOHEX                                           GP97225
.HEX2    DC    AL1(3,0),SL2(&HEX(1),&HEX(2))                    GP97225
.NOHEX   AIF   ('&PACK' EQ '').NOPACK                           GP97225
         AIF   (N'&PACK GE 2).PACK2                             GP97225
         DC    AL1(4,0),SL2(&PACK(1)),AL2(&L&PACK(1))           GP97225
         AGO   .NOPACK                                          GP97225
.PACK2   DC    AL1(4,0),SL2(&PACK(1),&PACK(2))                  GP97225
.NOPACK  AIF   ('&LIST' EQ '' OR N'&LIST LT 1).NOLIST           GP95235
&LN      SETA  N'&LIST                                          GP95235
.DOLIST  AIF   (&I GE &LN).NOLIST   DONE WITH LIST              GP95235
&I       SETA  &I+1          BUMP LOOP INDEX                    GP95235
&EN      SETA  K'&EK         GET LENGTH                         GP04234
         MACLIST &LIST(&I)   GET SUBLIST ITEMS                  GP04234
&EN      SETA  &MACP#        NUMBER OF ENTRIES (CHG FOR XF ASM) GP04234
         AIF   (&EN LT 1).DOLIST  USER IN COMA?                 GP95235
         AIF   (&EN LT 4).TOOLIST WARN                          GP95235
         MNOTE 4,'MORE THAN 3 SUBPARMS IN &LIST(&I) '           GP95235
.TOOLIST ANOP  ,                                                GP95235
&EK      SETC  '&MACP1'                                         GP04234
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
&EO      SETA  0             PRESET FOR NORMAL ADDRESSING MODE
&ET      SETC  '03'          PRESET FOR HEX DEFAULT             GP95235
         AIF   (&EM GT 0).TPFX                                  GP04234
&EK      SETC  '0'           ALLOW EXPANSION WITHOUT ERROR      GP04234
&EM      SETA  1                                                GP04234
         MNOTE 4,'DEBTRACE: PARAMETER &I REQUIRES AN ADDRESS'   GP04234
.TPFX    AIF   (&EM LT 2).NOTA31                                GP04234
         AIF   ('&EK'(1,1) NE '/').NOTIND
&EO      SETA  &EO+1         REQUEST INDIRECT ADDRESSING
&EK      SETC  '&EK'(2,&EM-1)  DELETE LEADING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTIND  AIF   ('&EK'(&EM,1) NE '%').NOTA24
&EO      SETA  &EO+2         REQUEST FORCED 24-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA24  AIF   ('&EK'(&EM,1) NE '?').NOTA31
&EO      SETA  &EO+4         REQUEST FORCED 31-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA31  AIF   (&EN LT 3 OR '&MACP3' EQ 'HEX').HTYPE            GP95235
         AIF   ('&MACP3' EQ 'X').HTYPE                          GP97225
         AIF   ('&MACP3' EQ 'HEX').HTYPE
         AIF   ('&MACP3' EQ 'T').TTYPE                          GP98189
         AIF   ('&MACP3' EQ 'TEXT').TTYPE                       GP95235
         AIF   ('&MACP3' EQ 'TXT').TTYPE
         AIF   ('&MACP3' EQ 'C').CTYPE                          GP97225
         AIF   ('&MACP3' EQ 'CT').CTYPE
         AIF   ('&MACP3' EQ 'CTEXT').CTYPE                      GP97225
         AIF   ('&MACP3' EQ 'PACK').PTYPE                       GP97225
         AIF   ('&MACP3' EQ 'PACKED').PTYPE                     GP97225
         AIF   ('&MACP3' EQ 'P').PTYPE                          GP97225
         AIF   ('&MACP3' EQ 'PD').PTYPE
         AIF   ('&MACP3' EQ 'D').PTYPE                          GP97225
 MNOTE 4,'TYPE MUST BE TEXT, CTEXT, HEX, OR PACKED, NOT &MACP3'
         AGO   .HTYPE                                           GP95235
.TTYPE   ANOP  ,                                                GP95235
&ET      SETC  '01'          SET FOR TEXT                       GP95235
         AGO   .HTYPE                                           GP95235
.CTYPE   ANOP  ,                                                GP97225
&ET      SETC  '02'          SET FOR CONDITIONAL TEXT, ELSE HEX GP97225
         AGO   .HTYPE                                           GP97225
.PTYPE   ANOP  ,                                                GP97225
&ET      SETC  '04'          SET FOR PACKED                     GP97225
.HTYPE   ANOP  ,                                                GP97225
&EL      SETC  '&MACP2'                                         GP95235
         AIF   ('&EL' NE '').HLEN                               GP95235
&EL      SETC  '&L'.'&EK'
.HLEN    DC    X'0800',CL8'&MACP1',AL1(&ET,&EO),S(&EK,&EL)
         AGO   .DOLIST                                          GP95235
.NOLIST  AIF   (&BUGFAR).FARCL                                   95079
         AIF   (&BUGDYN).FARCL                                  GP97261
         AIF   (&BUGTRC).TRCCL                                  GP99113
         AIF   (&BUGEXT).EXTCL                                   95215
&V.B     BAS   R14,DBTRACE                                       92271
         AGO   .CMCAL                                            95079
.EXTCL   ANOP  ,                                                 95215
&V.B     L     R15,=V(DEBTRACE)    MEMBER DEBTROLD              GP05013
         LA    R0,&WA        PASS ADDRESS OF WORK AREA           95215
         AGO   .FARCM                                            95215
.FARCL   ANOP  ,                                                 95079
&V.B     L     R15,=A(DBTRACE)                                   95079
.FARCM   BASR  R14,R15                                           95079
.CMCAL   ANOP  ,                                                 95079
&V.X     LM    R0,R15,&WA
         AGO   .MEND
.TRCCL   ANOP  ,             INVOKE PGMTRACE VIA ESPIE          GP99113
&V.X     DS    0H            END OF TRACE LIST                  GP99113
         AGO   .MEND                                            GP99113
.SWON    OI    DBTFLAG,DBTFLON  SET TRACING ON                   95079
         AGO   .MEND                                             95079
.SWEND   OI    DBTFLAG,DBTFLEND  CLOSE DCB AND STOP TRACE       GP98222
.SWOFF   NI    DBTFLAG,255-DBTFLON  SET TRACING OFF              95079
         AGO   .MEND                                             95079
.CODE    AIF   (&BUGFAR OR &BUGEXT).TESTDC                      GP97262
         AIF   ('&NM' EQ '').NONAME
&NM      DS    0H
.NONAME  AIF   (NOT &BUGDYN).NOLODYN                            GP97262
         AIF   ('&MODE' NE 'DC').NOPUP                          GP00004
         PUSH  PRINT                                            GP00004
         PUSH  USING                                            GP00004
.*       PRINT GEN                                              GP00004
         DROP  ,                                                GP00004
         USING DBTRACE,R15                                      GP97265
.NOPUP   ANOP  ,                                                GP00004
DBTRACE  LA    R0,&WA        PASS ADDRESS OF WORK AREA          GP97262
         STM   R12,R1,DBTLOCSV  SAVE BASE AND RETURN            GP97265
         ICM   R15,15,@DEBTRAC  SEE IF PREVIOUSLY LOADED        GP97265
         BNZR  R15           INVOKE; RETURN VIA R14 TO CALLER   GP97265
         AIF   ('&MODE' NE 'DC').NODRP                          GP00004
         BASR  R12,0         MAKE LOCAL BASE                    GP97262
         DROP  R15                                              GP97265
         USING *,R12                                            GP97265
         AGO   .CMDRP                                           GP00004
.NODRP   MVC   #DEBTRAC,=CL8'DEBTROLD'                          GP00004
         MVI   DBTFLAG,&BUGSWCH*DBTFLON+DBTFLWID                GP00004
.CMDRP   LOAD  EPLOC=#DEBTRAC  LOAD EXTERNAL MODULE             GP97261
         ST    R0,@DEBTRAC   SAVE FOR NEXT TIME                 GP97261
         AIF   ('&MODE' NE 'DC').NOCLB                          GP00004
         ST    R0,DBTLOCSV+(15-12)*4  UPDATE TARGET ADDRESS     GP97265
.NOCLB   LM    R12,R1,DBTLOCSV  RESTORE                         GP97265
         BR    R15           RETURN TO CALLER VIA R14           GP97262
         AIF   ('&MODE' NE 'DC').NOPOP                          GP00004
         POP   USING                                            GP97262
         POP   PRINT                                            GP97262
.NOPOP   AGO   .TESTDC                                          GP97262
.NOLODYN MNOTE 4,'INLINE EXPANSION NOT SUPPORTED - USE MACRO DEBINLIN'
.TESTDC  AIF   ('&MODE' NE 'DC').MEND                            95067
         AGO   .NODRTE                                           95067
.DATA    AIF   ('&NM' EQ '').NODLBL
&NM      DS    0D
.NODLBL  AIF   ('&ROUT' EQ '').NODRTE                            95067
&BUGSWRT SETB  1                                                 95067
.NODRTE  AIF   (&BUGFAR OR '&MODE' EQ 'M').MEND                  95230
DBTPRESV DC    2F'0'  1/2    FOR SHORT FORMATTING                95230
.NOSVPFX ANOP  ,                                                 95230
&WA      DC    16F'0' 2/2    DEBUG SAVE AREA                    GP97265
DBTFLAG  DC    AL1(&BUGSWCH*DBTFLON+DBTFLWID)  DEBUG FLAG       GP98222
DBTFLTCB EQU   128             INCLUDE TCB ADDRESS IN MESSAGE
DBTFLWTO EQU   64              USE WTO INSTEAD OF PRT            95240
DBTFLWID EQU   32              USE WIDE FORMAT WHEN PRINTING    GP98222
DBTFLPRO EQU   16              PRODUCTION MODE / NEED DD TO PRT GP99113
DBTFLEND EQU   2               THIS IS A TERMINATION CALL       GP98222
DBTFLON  EQU   1               DEBUG BIT
DBTFLAG2 DC    AL1(0)        ..RESERVED..                       GP99062
DBTFLAG3 DC    AL1(0)        ..RESERVED..                       GP99062
DBTFLAG4 DC    AL1(0)        ..RESERVED..                       GP99062
DBTCNT1  DC    A(&COUNT(1)+0)  COUNT OPTION                      95228
DBTCNT2  DC    A(&COUNT(2)+0)  COUNT OPTION                      95228
DBTCNT3  DC    A(&COUNT(3)+0)  COUNT OPTION                      95228
         MAPCMPRT PFX=DBT,DCB=&DCB,PRTMODE=&PRTMODE,DEV=&DEV    GP99113
         AIF   (NOT &BUGDYN).NODYNS                             GP97261
.BUGDYN  WXTRN DEBTRACE      SUPPORT LINKED-IN VERSION          GP97262
@DEBTRAC DC    A(DEBTRACE)   ADDRESS OF LOADED DEBTRACE         GP97261
#DEBTRAC DC    CL8'DEBTROLD'  LOAD MODULE NAME                  GP97261
DBTLOCSV DC    6A(0)         SAVE AREA                          GP97265
         AGO   .MEND                                            GP97262
.NODYNS  AIF   (&BUGFAR OR '&MODE' EQ 'M').MEND                  95228
DBTLOCSV DC    4F'0'         BASE SAVE
DBTWTO   DC    Y(56,0)       VCON                                93357
DBTWTOM  DC    C'MSG666 '    DEBUG HEADER                        93357
DBTWTON  DC    CL8' ',C' '   USER'S LABEL
DBTWTOT  DC    CL36' '       USER'S HEX OR EBCDIC TEXT
         AIF   ('&TCB' NE 'YES' AND NOT &BUGTCB).NOTCB           94011
         DC    C' '          EXTRA FOR UNPACK                    94011
DBTWTCB  DC    CL8' '        CURRENT TCB ADDRESS                 94011
.NOTCB   ANOP  ,                                                 94011
DBTWTOC  DC    C' '          EXTRA FOR UNPACK
         AIF   (NOT &BUGSWRT).MEND  NO ROUTING CODE              95067
         DC    XL3'0'        EXTRA FOR DESCRIPTOR/ROUTING CODES  95067
.MEND    MEND  ,
