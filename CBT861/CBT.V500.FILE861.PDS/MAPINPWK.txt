         MACRO ,                                        ADDED ON 90260
&NM      MAPINPWK &PREFIX=IP,&WIDTH=500 INPUT READER WORK AREA MAPPING
.*   UPDATED FOR ESA AND OS/390                                 GP98365
.*   UPDATED FOR DESERV AND CONCATENATED PDS & PDS/E SUPPORT    GP03244
         LCLC  &DS
         LCLC  &P            SHORT PREFIX
&P       SETC  '&PREFIX'
&DS      SETC  '&P'.'WORK'
         AIF   ('&NM' EQ '').NONAME
&DS      SETC  '&NM'
.NONAME  ANOP  ,
&DS      DSECT ,             MAPPING OF PRINT DCB AND WORK AREA
&P.LINK  DS    F             LINK TO NEXT GETMAINED AREA
&P.ID    DS    C'INP-'       ID OF INPUT FILE 'N'
&P.SPLEN DS    F             SUBPOOL/LENGTH OF THIS AREA
&P.TCB   DS    A             ADDRESS OF OWNING TCB
&P.DCB@  DS    X'90',AL3(&P.DCB)  DCB POINTER (24-BIT ADDRESS)
&P.PU@   DS    A             ADDRESS OF USER'S OPEN WORK AREA
&P.@SERV DS    A             ADDRESS OF @SERVICE ROUTINE
         SPACE 1
&P.O2LVL DCBEXITD DSECT=NO,PREFIX=IX,LEVEL2=2                    90260
&P.WLIST SERVCOMP DSECT=NO,PFX=&P.D                              90260
&P.UFLAG DS    X             USER'S PROCESSING FLAGS
&P.UFILL DS    C             FILL BYTE FOR SHORT RECORDS         81200
&P.FLAG  DS    X             PROCESSING FLAGS
&P.FEOF  EQU   X'80'           END FILE READ
&P.FEOM  EQU   X'40'           END MEMBER READ                   89352
&P.FEXT  EQU   X'20'           PDS EXTENSION PRESENT            GP03244
&P.FKEEP EQU   X'10'           RE-GET OF CURRENT RECORD
&P.FWYL  EQU   X'08'           POTENTIAL WYLBUR EDIT BLOCK
&P.FWIL  EQU   X'04'           WYLBUR RECORD READY FOR DEBLOCK
&P.FACB  EQU   X'02'           PROCESSING ACB, NOT DCB           89360
&P.FASCI EQU   X'01'           TRANSLATE ASCII TO EBCDIC         82175
&P.BUFSP DC    X'00'         GETPOOL SUBPOOL NUMBER              85157
&P.ACB   ACB   DDNAME=SYSUT1,MACRF=(ADR,SEQ,IN)                  89360
&P.RPLEN DC    F'0'    1/2   LENGTH OF GOTTEN RPL                89360
&P.RPL@  DC    A(0)    2/2   ADDRESS OF GOTTEN RPL               89360
&P.RRN   DC    A(0)          RELATIVE RECORD NO. FEEDBACK        90201
         ORG   &P.ACB         RE-USE                             89360
&P.DCB   DCB   DDNAME=ANY,DSORG=PS,MACRF=GL,EROPT=ACC, OPTCD=Z,        *
               EODAD=1,EXLST=&P.XLIST ,BFTEK=A                   82178
         ORG   ,             COMMON END OF ACB/DCB               89360
&P.XLIST DC    0A(0),X'87',AL3(0),A(0,0,0) +JFCB+@DCBEXIT+ABND   86272
&P.VCON  DC    F'0'          V-FORMAT RECORD HEADER
&P.RECCC DC    C' '          CARRIAGE CONTROL
&P.REC   DC    2CL(&WIDTH/2)' '     TEXT RECORD
&P.TRUPP DS    A             UPPER CASE TRANSLATE OR NO-OP      GP04114
&P.WKEND DS    0D            END OF WORK AREA
         SPACE 1                                                 89352
*        EXTENSION PRESENT WHEN USER REQUESTS PDS SERVICES       89352
&P.POD   DCB   DDNAME=ANY,DSORG=PS,MACRF=GL,RECFM=U,EODAD=1 DEODAD
         ORG   &P.POD         RE-USE                            GP03244
&P.DIR   DCB   DDNAME=ANY,DSORG=PS,MACRF=GL,EROPT=ACC,                 *
               EODAD=1 EXLST=&P.XLIST                           GP03244
         ORG   ,             COMMON END OF ACB/DCB               89360
&P.PCARD DC    CL15'./       ADD   '   IEBUPDTE CARD             89352
&P.PCAME DC    CL65'SSI=XXXXXXXX,NAME=XXXXXXXX'  VARIABLE PART  GP10192
&P.CONCT DC    X'00'         CONCATENATION # FROM SMDEMLTK (0->255)
&P.PFLAG DC    X'00'         MORE FLAGS                         GP03244
&P.PFDE  EQU   X'80'           DESERV BUFFERS AVAILABLE         GP03244
&P.PFPDS EQU   X'20'           PROCESSING A PDS                 GP03244
&P.RSVD8 DC    X'00'         RESERVED                           GP03244
&P.@DSAB DC    A(0)          ADDRESS OF DSAB FOR INPUT DD       GP03244
&P.@POD  DC    A(&P.POD)      OPEN/CLOSE LIST                    89352
&P.PXLST DC    2A(0)         POD EXIT LIST/JFCB                  89352
&P.@ROOT DC    A(0)          ROOT OF DESERV BUFFERS             GP03244
&P.@DBXL DC    4A(0)         BXLE/@BUF FOR DESERV LOOP          GP03244
&P.@DSRV DC    A(0)          ADDRESS OF SUBDSERV ROUTINE        GP03244
&P.PDDNM DC    CL8' '        NAME OF DYNAMICALLY ALLOCATED DD   GP03244
&P.PJFCB DS    XL(JFCBLGTH)  JFCB FOR INDIVIDUAL MEMBER          89352
&P.PDSDE DS    XL(8+1+3+62)  (FAKE) DIRECTORY ENTRY             GP03244
&P.PBXLE SERVPDS PFX=PE,DSECT=NO,RETURN=YES,VER=1                89352
&P.PBXLN EQU   *-&P.PBXLE    LENGTH OF PDE WORK AREA             89352
&P.PWKND EQU   *             END OF PDS EXTENSION                89352
         MEND  ,                                                 90260
