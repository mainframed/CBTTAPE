         MACRO
&NM      FDIN  &S,&EXLEN,&VALUES,&LEN=0,&MAX=,&EXIT=             89095
         GBLA  &FDCNTR,&FDOFFS                                   84146
         LCLA  &C,&I,&J,&K,&N,&U                                 84146
         LCLA  &T,&E,&O7,&O9,&DATA,&EXFG                         89095
         LCLC  &L,&FDCHAIN,&W,&UEX                               89095
         LCLB  &NOP,&IN,&SKPLEN,&INDAD                           81270
         LCLB  &NL,&DEBL,&DEBR,&DEBZ,&PADL,&PADR,&RADJ,&UP
         LCLB  &BLUE,&GREEN,&PINK,&RED,&TURQ,&WHITE,&YELLOW,&C1,&C2,&C3
         LCLB  &UNDER,&BLINK,&REVERSE,&MONO,&MDT                 87313
         LCLB  &INTENSE,&DETECT,&NONDISP,&NUMERIC,&SKIP,&PROTECT,&NULL
         LCLB  &DEFAULT,&PREV
&U       SETA  &FDOFFS                                           84146
&UEX     SETC  'AL2('.'&U'.')'                                   89095
&FDCNTR  SETA  &FDCNTR+1
&FDCHAIN SETC  'ZFD'.'&FDCNTR'
&C       SETA  &FDCNTR+1
         AIF   ('&S' EQ '*END' OR '&S' EQ 'END').NOOFF  END OF CHAIN
         AIF   ('&S' NE '*EXPAND').NOEXPND                       84214
         AIF   ('&NM' EQ '').NOXNAM                              84214
&NM      DC    (&FDOFFS)X'00'                                    84214
         AGO   .NOXCOM                                           84214
.NOXNAM  AIF   (&FDOFFS LT 1).NOXCOM                             84214
         DC    (&FDOFFS)X'00'                                    84214
.NOXCOM  AIF   ('&SYSLIST(2)' EQ 'NORESET').MEND                 84214
&FDOFFS  SETA  0                                                 84214
         AGO   .MEND                                             84214
.NOEXPND AIF   (T'&EXIT EQ 'O').NOUEX                            89095
         AIF   (N'&EXIT GE 2).CKUEXR                            GP07008
&UEX     SETC  '&UEX'.',SL2('.'&EXIT'.')'                        89095
&EXFG    SETA  1                                                 89095
         AGO   .NOUEX                                           GP07008
.CKUEXR  AIF   ('&EXIT(1)' NE 'R').BADUEX                       GP07008
&UEX     SETC  '&UEX'.',SL2('.'&EXIT(2)'.'-*)'                  GP07008
&EXFG    SETA  1                                                 89095
         AGO   .NOUEX                                           GP07008
.BADUEX  MNOTE 8,'FDIN: EXIT= ADDRESS MALFORMED'                GP07008
.NOUEX   AIF   (T'&MAX EQ 'O').NOMAXQ                            89095
         AIF   (T'&MAX EQ 'N').CKMAX                             89095
         MNOTE 4,'NON-NUMERIC MAX= NOT SUPPORTED'                89095
         AGO   .NOMAXQ                                           89095
.CKMAX   AIF   (&MAX LT 0).NOMAXQ                                89095
&I       SETA  &MAX                                              84146
         AGO   .HAVMAX                                           84146
.NOMAXQ  AIF   (T'&LEN NE 'N' OR '&LEN' EQ '0').NOLENQ           84146
         AIF   (&LEN LE 0).NOLENQ                                84146
&I       SETA  &LEN                                              84146
         AGO   .HAVMAX                                           84146
.NOLENQ  MNOTE 0,'LEN=/MAX= MISSING - WIDTH DEFAULTED TO 255'    84146
&I       SETA  255                                               84146
.HAVMAX  ANOP  ,                                                 84146
&FDOFFS  SETA  &FDOFFS+4+&I  SET OFFSET OF NEXT ENTRY            84146
         AIF   (K'&SYSLIST(0) LT 1 OR K'&SYSLIST(0) GT 4).NOOFF  84146
OFFS&SYSLIST(0) EQU &U,&I                                        84214
.NOOFF   AIF   ('&NM' EQ '').NONAME                              84146
&NM      EQU   *
.NONAME  AIF   ('&S' NE 'END' AND '&S' NE '*END').PROCESS        84146
&FDCHAIN DC    AL1(0)        END OF FD LIST
         MEXIT ,
.PROCESS ANOP  ,
&I       SETA  N'&SYSLIST
&J       SETA  1
         AIF   ('&S'(1,1) NE '''').NOLIT                         84146
&SKPLEN  SETB  1
.NOLIT   AIF   (&J GE &I).CHECK
&J       SETA  &J+1
&L       SETC  '&SYSLIST(&J)'
         AIF   ('&L' EQ '').NOLIT
&NOP     SETB  (&NOP  OR '&L' EQ 'NOP')
&NL      SETB  (&NL  OR '&L' EQ 'NL' OR '&L' EQ 'NEWLINE')
&DEBL SETB (&DEBL OR '&L' EQ 'DEBL' OR '&L' EQ 'DEB' OR '&L' EQ 'DEBZ')
&DEBR    SETB  (&DEBR OR '&L' EQ 'DEBR' OR '&L' EQ 'DEB')
&DEBZ    SETB  (&DEBZ OR '&L' EQ 'DEBZ')
&PADL    SETB  (&PADL OR '&L' EQ 'PADL' OR '&L' EQ 'PAD')
&PADR    SETB  (&PADR OR '&L' EQ 'PADR' OR '&L' EQ 'PAD')
&RADJ    SETB  (&RADJ OR '&L' EQ 'RADJ')
&UP      SETB  (&UP OR '&L' EQ 'UP')
&UP      SETB  (&UP OR '&L' EQ 'UPPER')
&BLUE    SETB  (&BLUE OR '&L' EQ 'BLUE')
&GREEN   SETB  (&GREEN OR '&L' EQ 'GREEN')
&PINK    SETB  (&PINK OR '&L' EQ 'PINK')
&RED     SETB  (&RED  OR '&L' EQ 'RED')
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TURQ' OR '&L' EQ 'CYAN')      90326
&WHITE   SETB  (&WHITE OR '&L' EQ 'WHITE')
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YELLOW')
&BLUE    SETB  (&BLUE OR '&L' EQ 'BL')                          GP10240
&GREEN   SETB  (&GREEN OR '&L' EQ 'GR')                         GP10240
&PINK    SETB  (&PINK OR '&L' EQ 'PI')                          GP10240
&RED     SETB  (&RED  OR '&L' EQ 'RE')                          GP10240
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TU' OR '&L' EQ 'CY')         GP10240
&WHITE   SETB  (&WHITE OR '&L' EQ 'WH')                         GP10240
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YE')                        GP10240
&MONO    SETB  (&MONO OR '&L' EQ 'MONO')                         87313
&UNDER   SETB  (&UNDER OR '&L' EQ 'UL' OR '&L' EQ 'UNDER')
&BLINK   SETB  (&BLINK OR '&L' EQ 'BLINK')
&REVERSE SETB  (&REVERSE OR '&L' EQ 'REVERSE')
&INTENSE SETB  (&INTENSE OR '&L' EQ 'INTENSE' OR '&L' EQ 'INT')
&DETECT  SETB  (&DETECT OR '&L' EQ 'DETECT' OR '&L' EQ 'LP')
&NONDISP SETB  (&NONDISP OR '&L' EQ 'NONDISP' OR '&L' EQ 'NDISP')
&NUMERIC SETB  (&NUMERIC OR '&L' EQ 'NUMERIC' OR '&L' EQ 'NUM')
&SKIP    SETB  (&SKIP OR '&L' EQ 'SKIP')
&PROTECT SETB  (&PROTECT OR '&L' EQ 'PROTECT')
&MDT     SETB  (&MDT OR '&L' EQ 'MDT')                           87313
&NULL    SETB  (&NULL OR '&L' EQ 'NULL')                         84146
&DEFAULT SETB  (&DEFAULT OR '&L' EQ 'DEFAULT' OR '&L' EQ 'DFLT')
&PREV    SETB  (&PREV OR '&L' EQ 'PREVIOUS' OR '&L' EQ 'PREV')
&INDAD   SETB  (&INDAD OR '&L' EQ '*')                           81270
         AIF   (&DATA NE 0).NOLIT
         AIF   (K'&L GT 4).OMLEN
&K       SETA  0
&L       SETC  '&L'.'    '
&L       SETC  '&L'(1,4)
         AIF   ('&L' NE 'X   ').DT                              GP10240
&L       SETC  'HEX '                                           GP10240
.DT      AIF   (&K GE 35).OMLEN                                 GP07004
&K       SETA  &K+1
&N       SETA  (&K-1)*4+1
&W      SETC  'CHARCON ASISADDRHEX SHEXBIT I   $I  D   $D  F   TIMETIMD*
               DATEDATJWDAYMTH DAY MD  DMY MDY CHEXICM ICN IZ  IA  DCM *
               DCN DZ  DA  EDATDATDCCHHTTR '(&N,4)              GP07004
         AIF   ('&L' NE '&W').DT
&DATA    SETA  &K
         AIF   (&J EQ 2).OMSET
         AGO   .NOLIT
.OMLEN   AIF   (&J NE 2 OR &SKPLEN).NOLIT
 AIF (&NOP OR &IN OR &NL OR &DEBL OR &DEBR OR &DEBZ OR &PADL).OMSET
 AIF (&UP OR &PADR OR &RADJ OR &BLUE OR &GREEN OR &PINK OR &RED).OMSET
 AIF (&TURQ OR &WHITE OR &YELLOW OR &UNDER OR &BLINK OR &NULL).OMSET
 AIF (&REVERSE OR &INTENSE OR &DETECT OR &NONDISP OR &INDAD).OMSET
 AIF (&NUMERIC OR &SKIP OR &PROTECT OR &DEFAULT OR &PREV).OMSET
         AIF   (&MDT OR &MONO).OMSET                             87313
         AGO   .NOLIT
.OMSET   ANOP  ,             EXPLICIT LENGTH OMITTED
&SKPLEN  SETB  1             USE L'
         AGO   .NOLIT
.CHECK   ANOP  ,
&L       SETC  'L'''
&IN      SETB  1                                                 84146
&T       SETA  128*&IN+64*&NOP+4*&EXFG+&INDAD                    89095
&E SETA 128*&NL+64*&DEBL+32*&DEBR+16*&DEBZ+8*&PADL+4*&PADR+2*&RADJ+&UP
&O7      SETA  128*&DEFAULT+64*&PREV+&MDT                        87313
&PROTECT SETB  (&PROTECT OR &SKIP)
&NUMERIC SETB  (&NUMERIC OR &SKIP)
&DETECT  SETB  (&DETECT  OR &NONDISP)
&INTENSE SETB  (&INTENSE OR &NONDISP)
&O7      SETA  &O7+32*&PROTECT+16*&NUMERIC+8*&INTENSE+4*&DETECT+2*&NULL
&C1      SETB  (&GREEN OR &TURQ OR &WHITE OR &YELLOW)
&C2      SETB  (&RED OR &PINK OR &WHITE OR &YELLOW)
&C3      SETB  (&BLUE OR &PINK OR &TURQ OR &WHITE)
&O9      SETA  64*&C1+32*&C2+16*&C3+8*&MONO+4*&UNDER+2*&REVERSE+&BLINK
         AIF   ((&O7 NE 0 AND &O7 NE 128) OR &O9 NE 0).LONG      86244
&T       SETA  &T+48         USE FDPRT SHORT FORM                86244
         AIF   ('&S'(1,1) EQ '''').CSTRPRT                       86244
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLPRT           86244
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&L&S),SL2(&S),&UEX      89095
         MEXIT ,                                                 86244
.EXLPRT  AIF   (K'&EXLEN LT 2).NORLPRT                           86244
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLPRT
&T       SETA  &T+2          ILEN IS REGISTER FORM               86244
.NORLPRT ANOP  ,                                                 86244
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&EXLEN),SL2(&S),&UEX    89095
         MEXIT ,                                                 86244
.CSTRPRT ANOP  ,                                                 86244
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,1,&LEN,&L.ZFD&C.C),SL2(ZFD&C.C),&UEX
ZFD&C.C  DC    C&S                                               89095
         MEXIT ,                                                 86244
.LONG    AIF   ('&S'(1,1) EQ '''').CSTRING                       86244
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLEN
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&L&S),SL2(&S),&UEX
         MEXIT
.EXLEN   AIF   (K'&EXLEN LT 2).NORLEN                            81270
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLEN
&T       SETA  &T+2          ILEN IS REGISTER FORM               81270
.NORLEN  ANOP  ,                                                 81270
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&EXLEN),SL2(&S),&UEX
         MEXIT ,
.CSTRING ANOP  ,
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,1,&LEN,&L.ZFD&C.C),SL2(ZFD&C.C),&*
               UEX                                               89095
ZFD&C.C  DC    C&S                                               89095
.MEND    MEND  ,                                                 84214
