         MACRO
&NM      FD    &STR,&EXLEN,&VALUES,&LEN=0,&TYPE=                GP03287
.*--------------------------------------------------------------------*
.*   TYPE= ADDED FOR FDFLAG AND FDBAR SUPPORT                         *
.*--------------------------------------------------------------------*
         GBLA  &FDCNTR
         LCLA  &C,&I,&J,&K,&N
         LCLA  &T,&E,&O7,&O9,&DATA
         LCLC  &L,&FDCHAIN,&W
         LCLB  &NOP,&IN,&SKPLEN,&INDAD,&CNLOCK                   92086
         LCLB  &NL,&DEBL,&DEBR,&DEBZ,&PADL,&PADR,&RADJ,&UP
         LCLB  &BLUE,&GREEN,&PINK,&RED,&TURQ,&WHITE,&YELLOW,&C1,&C2,&C3
         LCLB  &UNDER,&BLINK,&REVERSE,&MDT,&MONO                 87313
         LCLB  &INTENSE,&DETECT,&NONDISP,&NUMERIC,&SKIP,&PROTECT
         LCLB  &DEFAULT,&PREV
&FDCNTR  SETA  &FDCNTR+1
&FDCHAIN SETC  'ZFD'.'&FDCNTR'
&C       SETA  &FDCNTR+1
         AIF   ('&NM' EQ '').NONAME
&NM      EQU   *
.NONAME  AIF   ('&STR' NE 'END' AND '&STR' NE '*END').PROCESS
&FDCHAIN DC    AL1(0)        END OF FD LIST
         MEXIT ,
.PROCESS ANOP  ,
&I       SETA  N'&SYSLIST
&J       SETA  1
         AIF   ('&STR'(1,1) NE '''').PRMLOOP                    GP04048
&SKPLEN  SETB  1
.*--------------------------------------------------------------------*
.*   LOOP THROUGH POSITIONAL PARAMETERS:                              *
.*   #1 - VARIABLE NAME OR QUOTED STRING                              *
.*   #2 - IF UNRECOGNIZED, EXPLICIT VARIABLE LENGTH                   *
.*--------------------------------------------------------------------*
.PRMLOOP AIF   (&J GE &I).CHECK                                 GP04048
&J       SETA  &J+1
&L       SETC  '&SYSLIST(&J)'
         AIF   ('&L' EQ '').PRMLOOP                             GP04048
&NOP     SETB  (&NOP  OR '&L' EQ 'NOP')
&CNLOCK  SETB  (&CNLOCK OR '&L' EQ 'LOCK' OR '&L' EQ 'LOCKED')   92086
&NL      SETB  (&NL  OR '&L' EQ 'NL' OR '&L' EQ 'NEWLINE')
&DEBL SETB (&DEBL OR '&L' EQ 'DEBL' OR '&L' EQ 'DEB' OR '&L' EQ 'DEBZ')
&DEBR    SETB  (&DEBR OR '&L' EQ 'DEBR' OR '&L' EQ 'DEB')
&DEBZ    SETB  (&DEBZ OR '&L' EQ 'DEBZ')
&PADL    SETB  (&PADL OR '&L' EQ 'PADL' OR '&L' EQ 'PAD')
&PADR    SETB  (&PADR OR '&L' EQ 'PADR' OR '&L' EQ 'PAD')
&RADJ    SETB  (&RADJ OR '&L' EQ 'RADJ')
&UP    SETB  (&UP OR '&L' EQ 'UP')
&UP      SETB  (&UP OR '&L' EQ 'UPPER')
&BLUE    SETB  (&BLUE OR '&L' EQ 'BLUE')
&GREEN   SETB  (&GREEN OR '&L' EQ 'GREEN')
&PINK    SETB  (&PINK OR '&L' EQ 'PINK')
&RED     SETB  (&RED  OR '&L' EQ 'RED')
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TURQ' OR '&L' EQ 'CYAN')      90326
&WHITE   SETB  (&WHITE OR '&L' EQ 'WHITE')
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YELLOW')
&BLUE    SETB  (&BLUE OR '&L' EQ 'BL')                          GP10240
&GREEN   SETB  (&GREEN OR '&L' EQ 'GR')                         GP10240
&PINK    SETB  (&PINK OR '&L' EQ 'PI')                          GP10240
&RED     SETB  (&RED  OR '&L' EQ 'RE')                          GP10240
&TURQ    SETB  (&TURQ  OR '&L' EQ 'TU' OR '&L' EQ 'CY')         GP10240
&WHITE   SETB  (&WHITE OR '&L' EQ 'WH')                         GP10240
&YELLOW  SETB  (&YELLOW OR '&L' EQ 'YE')                        GP10240
&MONO    SETB  (&MONO OR '&L' EQ 'MONO')                         87313
&UNDER   SETB  (&UNDER OR '&L' EQ 'UL' OR '&L' EQ 'UNDER')
&BLINK   SETB  (&BLINK OR '&L' EQ 'BLINK')
&REVERSE SETB  (&REVERSE OR '&L' EQ 'REVERSE')
&INTENSE SETB  (&INTENSE OR '&L' EQ 'INTENSE' OR '&L' EQ 'INT')
&DETECT  SETB  (&DETECT OR '&L' EQ 'DETECT' OR '&L' EQ 'LP')
&NONDISP SETB  (&NONDISP OR '&L' EQ 'NONDISP' OR '&L' EQ 'NDISP')
&NUMERIC SETB  (&NUMERIC OR '&L' EQ 'NUMERIC' OR '&L' EQ 'NUM')
&SKIP    SETB  (&SKIP OR '&L' EQ 'SKIP')
&PROTECT SETB  (&PROTECT OR '&L' EQ 'PROTECT')
&MDT     SETB  (&MDT OR '&L' EQ 'MDT')                           87313
&DEFAULT SETB  (&DEFAULT OR '&L' EQ 'DEFAULT' OR '&L' EQ 'DFLT')
&PREV    SETB  (&PREV OR '&L' EQ 'PREVIOUS' OR '&L' EQ 'PREV')
&INDAD   SETB  (&INDAD OR '&L' EQ '*')                           81270
         AIF   (K'&L GT 4).OMLEN
&K       SETA  0
&L       SETC  '&L'.'    '
&L       SETC  '&L'(1,4)
         AIF   ('&L' NE 'X   ').DT                              GP10240
&L       SETC  'HEX '                                           GP10240
.DT      AIF   (&K GE 35).OMLEN                                 GP07004
&K       SETA  &K+1
&N       SETA  (&K-1)*4+1
&W      SETC  'CHARCON ASISADDRHEX SHEXBIT I   $I  D   $D  F   TIMETIMD*
               DATEDATJWDAYMTH DAY MD  DMY MDY CHEXICM ICN IZ  IA  DCM *
               DCN DZ  DA  EDATDATDCCHHTTR '(&N,4)              GP07004
         AIF   ('&L' NE '&W').DT
&DATA    SETA  &K
         AIF   (&J EQ 2).OMSET
         AGO   .PRMLOOP                                         GP04048
.OMLEN   AIF   (&J NE 2 OR &SKPLEN).PRMLOOP                     GP04048
 AIF (&NOP OR &IN OR &NL OR &DEBL OR &DEBR OR &DEBZ OR &PADL).OMSET
 AIF (&UP OR &PADR OR &RADJ OR &BLUE OR &GREEN OR &PINK OR &RED).OMSET
 AIF (&TURQ OR &WHITE OR &YELLOW OR &UNDER OR &BLINK).OMSET
 AIF (&REVERSE OR &INTENSE OR &DETECT OR &NONDISP OR &INDAD).OMSET
 AIF (&NUMERIC OR &SKIP OR &PROTECT OR &DEFAULT OR &PREV).OMSET
         AIF   (&MDT OR &MONO OR &CNLOCK).OMSET                  92086
         AGO   .PRMLOOP                                         GP04048
.OMSET   ANOP  ,             EXPLICIT LENGTH OMITTED
&SKPLEN  SETB  1             USE L'
         AGO   .PRMLOOP                                         GP04048
.*--------------------------------------------------------------------*
.*   END OF PARAMETER LOOP                                            *
.*--------------------------------------------------------------------*
.CHECK   AIF   ('&TYPE' EQ '').NOTYPE                           GP03287
.*FDBAR  AIF   (T'&TYPE NE 'N').NOTYPE                          GP03287
&DATA    SETA  &TYPE                                            GP03287
.NOTYPE  ANOP  ,                                                GP04048
&T       SETA  128*&IN+64*&NOP+8*&CNLOCK+&INDAD                  92086
&E SETA 128*&NL+64*&DEBL+32*&DEBR+16*&DEBZ+8*&PADL+4*&PADR+2*&RADJ+&UP
&O7      SETA  128*&DEFAULT+64*&PREV
&PROTECT SETB  (&PROTECT OR &SKIP)
&NUMERIC SETB  (&NUMERIC OR &SKIP)
&DETECT  SETB  (&DETECT  OR &NONDISP)
&INTENSE SETB  (&INTENSE OR &NONDISP)
&O7      SETA  &O7+32*&PROTECT+16*&NUMERIC+8*&INTENSE+4*&DETECT+&MDT
&C1      SETB  (&GREEN OR &TURQ OR &WHITE OR &YELLOW)
&C2      SETB  (&RED OR &PINK OR &WHITE OR &YELLOW)
&C3      SETB  (&BLUE OR &PINK OR &TURQ OR &WHITE)
&O9      SETA  64*&C1+32*&C2+16*&C3+8*&MONO+4*&UNDER+2*&REVERSE+&BLINK
         AIF   (&O7 NE 0 OR &O9 NE 0).LONG                       81138
&T       SETA  &T+48         USE FDPRT SHORT FORM                81138
         AIF   ('&STR'(1,1) EQ '''').CSTRPRT                     81138
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).PRTLEN           81138
&L       SETC  'L'''                                             81138
&FDCHAIN DC    AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&L&STR),SL2(&STR)    81138
         MEXIT ,                                                 81138
.PRTLEN  AIF   (K'&EXLEN LT 2).NOPLEN                            81270
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NOPLEN
&T       SETA  &T+2          ILEN IS REGISTER FORM               81270
.NOPLEN  ANOP  ,                                                 81270
&FDCHAIN DC    AL1(ZFD&C-*,&T,&E,&DATA,&LEN,&EXLEN),SL2(&STR)    81138
         MEXIT ,                                                 81138
.CSTRPRT ANOP  ,                                                 81138
&FDCHAIN DC    AL1(ZFD&C-*,&T,&E,129,&LEN,ZFD&C-*-1),C&STR       81138
         MEXIT ,                                                 81138
.LONG    AIF   ('&STR'(1,1) EQ '''').CSTRING                     81138
         AIF   ('&EXLEN' NE '' AND NOT &SKPLEN).EXLEN
&L       SETC  'L'''
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&L&STR),SL2(&STR)
         MEXIT
.EXLEN   AIF   (K'&EXLEN LT 2).NORLEN                            81270
         AIF   ('&EXLEN'(1,1) NE '(' OR '&EXLEN'(2,1) EQ '(').NORLEN
&T       SETA  &T+2          ILEN IS REGISTER FORM               81270
.NORLEN  ANOP  ,                                                 81270
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,&DATA,&LEN,&EXLEN),SL2(&STR)
         MEXIT ,
.CSTRING ANOP  ,
&FDCHAIN DC AL1(ZFD&C-*,&T,&E,&O9,&O7,129,&LEN,ZFD&C-*-1),C&STR
         MEND  ,
