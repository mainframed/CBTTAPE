         MACRO ,
&NM    XDEVOPT &SW1=,&SW2=,&SW3=,&SW4=,&ULV=,&EXIT=,&ID=,&LAYOUT=,     *
               &LOOP=,&WAIT=,&INPUT=,&PRIV=,&DELAY=NO,&GROUP=DFL,      *
               &COLIN='46464646232300004646464623230000',        88364 *
               &COLOUT='11115555777700001111555577770000',       88364 *
               &EXPART=0,&IMPART=L,&COLREP='1234567',            88364 *
               &GAMES=NO,&DASKIP=,&TRAN=,&TRGE=,&PROMPT=        GP99106
.*
.*       MACRO ADDED 76060
.*       PROVIDES SUPPORT FOR USER OPTIONS IN MODULES EXHPXXXX
.*       SEE EXHAINIT, XDEVPFK AND EXHPDFLT FOR ADDITIONAL COMMENTS
.*
         COPY  GBLDEF
         GBLB  &PFKONE,&PFKEND,&OPTONE,&OPTEND
         GBLC  &OPTNAM(20),&OPTVAL(20),&OPTCOM(20)
         GBLC  &OPTCIN,&OPTCOT,&OPTCRP,&OPTEXP,&OPTIMP           88364
         GBLA  &OPTMAX
         LCLC  &PRM
         LCLA  &I,&J,&K
         AIF   (&OPTONE).INIT
&OPTONE  SETB  1
&OPTNAM(01) SETC 'LAYOUT'
&OPTVAL(01) SETC 'DFLT'
&OPTCOM(01) SETC ''
&OPTNAM(02) SETC 'LOOP TIME'
&OPTVAL(02) SETC '3'
&OPTCOM(02) SETC 'SECONDS'
&OPTNAM(03) SETC 'WAIT TIME'
&OPTVAL(03) SETC '30'
&OPTCOM(03) SETC 'SECONDS'
&OPTNAM(04) SETC 'PRIVILEGES'
&OPTVAL(04) SETC '00000000'
&OPTCOM(04) SETC ''
&OPTNAM(05) SETC ''
&OPTVAL(05) SETC '00000000'
&OPTCOM(05) SETC ''
&OPTNAM(06) SETC 'SCREEN REFRESH TIME'
&OPTVAL(06) SETC '5'
&OPTCOM(06) SETC 'SECONDS'
&OPTNAM(07) SETC 'SW2'
&OPTVAL(07) SETC '2'
&OPTCOM(07) SETC 'SECOND CYCLE UPDATES'
&OPTNAM(08) SETC 'SW3'
&OPTVAL(08) SETC '12'
&OPTCOM(08) SETC 'THIRD LEVEL UPDATES'
&OPTNAM(09) SETC 'SW4'
&OPTVAL(09) SETC '5'
&OPTCOM(09) SETC 'FOURTH LEVEL UPDATES'
&OPTNAM(10) SETC 'USER EXIT LEVEL'
&OPTVAL(10) SETC '3'
&OPTCOM(10) SETC '(SW NUMBER - 1)'
&OPTNAM(11) SETC 'OPTION FLAGS'
&OPTVAL(11) SETC '00000001'
&OPTCOM(11) SETC ''
&OPTVAL(12) SETC ''                                              79156
&OPTCOM(12) SETC 'LPA RESIDENT GROUP'                            79156
&OPTNAM(13) SETC 'DASKIP'                                        82361
&OPTVAL(13) SETC '00000000'                                      82361
&OPTCOM(13) SETC 'DISPLAY ACTIVE SKIP OPTIONS'                   82361
&OPTNAM(14) SETC 'TRAN'                                          82361
&OPTVAL(14) SETC ''                                              82361
&OPTCOM(14) SETC 'LPA/LSQA CRT TRANSLATE TABLES'                 82361
&OPTMAX  SETA  14                                                82361
.INIT    AIF   (NOT &PFKONE AND NOT &PFKEND).POK
         MNOTE 8,'OUT OF SEQUENCE'
         MEXIT
.POK     ANOP  ,
.P1      AIF   ('&SW1' EQ '').P2
      EXGCHECK &SW1,&OPTVAL(6),OPT=NUM
         AIF   (NOT &OK).BOO1
         AIF   (&DUPL).P2
         AIF   (&SW1 LT 1 OR &SW1 GT 655).BOO1
&OPTVAL(6) SETC '&SW1'
         AGO   .P2
.BOO1    MNOTE 8,'INVALID OPTION  SW1=''&SW1'''
.P2      AIF   ('&SW2' EQ '').P3
      EXGCHECK &SW2,&OPTVAL(7),OPT=NUM
         AIF   (NOT &OK).BOO2
         AIF   (&DUPL).P3
         AIF   (&SW2 LT 1 OR &SW2 GT 255).BOO2
&OPTVAL(7) SETC '&SW2'
         AGO   .P3
.BOO2    MNOTE 8,'INVALID OPTION  SW2=''&SW2'''
.P3      AIF   ('&SW3' EQ '').P4
      EXGCHECK &SW3,&OPTVAL(8),OPT=NUM
         AIF   (NOT &OK).BOO3
         AIF   (&DUPL).P4
         AIF   (&SW3 LT 1 OR &SW3 GT 255).BOO3
&OPTVAL(8) SETC '&SW3'
         AGO   .P4
.BOO3    MNOTE 8,'INVALID OPTION  SW3=''&SW3'''
.P4      AIF   ('&SW4' EQ '').P5
      EXGCHECK &SW4,&OPTVAL(9),OPT=NUM
         AIF   (NOT &OK).BOO4
         AIF   (&DUPL).P5
         AIF   (&SW4 LT 1 OR &SW4 GT 255).BOO4
&OPTVAL(9) SETC '&SW4'
         AGO   .P5
.BOO4    MNOTE 8,'INVALID OPTION  SW4=''&SW4'''
.P5      AIF   ('&ULV' EQ '').P6
      EXGCHECK &ULV,&OPTVAL(10),OPT=NUM
         AIF   (NOT &OK).BOO5
         AIF   (&DUPL).P6
         AIF   (&ULV GT 255).BOO5
&OPTVAL(10) SETC '&ULV'
         AGO   .P6
.BOO5    MNOTE 8,'INVALID OPTION  ULV=''&ULV'''
.P6      AIF   ('&EXIT' EQ '').P7
      EXGCHECK NONE,&EXIT,OPT=YES
         AIF   (NOT &OK).BOO6
         AIF   ('&EXIT' EQ 'NO').P6NO
&OPTVAL(11) SETC '&OPTVAL(11)'(1,4).'1'.'&OPTVAL(11)'(6,3)
         AGO   .P7
.P6NO    ANOP  ,
&OPTVAL(11) SETC '&OPTVAL(11)'(1,4).'0'.'&OPTVAL(11)'(6,3)
         AGO   .P7
.BOO6    MNOTE 8,'INVALID OPTION  EXIT=''&EXIT'''
.P7      AIF   ('&ID' EQ '').P8
      EXGCHECK NONE,&ID,OPT=YES
         AIF   (NOT &OK).BOO7
         AIF   ('&ID' EQ 'NO').P7NO
&OPTVAL(11) SETC '&OPTVAL(11)'(1,7).'1'
         AGO   .P8
.P7NO    ANOP  ,
&OPTVAL(11) SETC '&OPTVAL(11)'(1,7).'0'
         AGO   .P8
.BOO7    MNOTE 8,'INVALID OPTION  ID=''&ID'''
.P8      AIF   ('&LAYOUT' EQ '').P9
      EXGCHECK DFLT,&LAYOUT,OPT=ALPHA
         AIF   (NOT &OK).BOO8
         AIF   (&DUPL).P9
         AIF   (K'&LAYOUT NE 4).BOO8
&OPTVAL(1) SETC '&LAYOUT'
         AGO   .P9
.BOO8    MNOTE 8,'INVALID OPTION  LAYOUT=''&LAYOUT'''
.P9      AIF   ('&LOOP' EQ '').P10
      EXGCHECK &OPTVAL(2),&LOOP,OPT=NUM
         AIF   (NOT &OK).BOO9
         AIF   (&DUPL).P10
         AIF   (&LOOP LT 1 OR &LOOP GT 655).BOO9
&OPTVAL(2) SETC '&LOOP'
         AGO   .P10
.BOO9    MNOTE 8,'INVALID OPTION  LOOP=''&LOOP'''
.P10     AIF   ('&WAIT' EQ '').P11
      EXGCHECK &OPTVAL(3),&WAIT,OPT=NUM
         AIF   (NOT &OK).BOO10
         AIF   (&DUPL).P11
         AIF   (&WAIT LT 10 OR &WAIT GT 655).BOO10
&OPTVAL(3) SETC '&WAIT'
         AGO   .P11
.BOO10    MNOTE 8,'INVALID OPTION  WAIT=''&WAIT'''
.P11     AIF   ('&INPUT' EQ '').P12
      EXGCHECK NONE,&INPUT,(YES,NO,KEYS,PSWD),OPT=LIST
         AIF   (NOT &OK).BOO11
         AIF   (&DUPL).P12
         AIF   ('&INPUT' EQ 'YES').P11YES
         AIF   ('&INPUT' EQ 'KEYS').P11KEYS
         AIF   ('&INPUT' EQ 'PSWD').P11PSWD
&OPTVAL(11) SETC '&OPTVAL(11)'(1,5).'1'.'&OPTVAL(11)'(7,2)
         AGO   .P12
.P11YES  ANOP  ,
&OPTVAL(11) SETC '&OPTVAL(11)'(1,5).'0'.'&OPTVAL(11)'(7,2)
         AGO   .P12
.P11KEYS ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,2).'1'.'&OPTVAL(4)'(4,5)
         AGO   .P12
.P11PSWD ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,1).'1'.'&OPTVAL(4)'(3,5).'1'
         AGO   .P12
.BOO11    MNOTE 8,'INVALID OPTION  INPUT=''&INPUT'''
.P12     AIF   ('&PRIV' EQ '').P13
&J       SETA  N'&PRIV
&I       SETA  0
.P12LOOP AIF   (&I GE &J).P13
&I       SETA  &I+1
&PRM     SETC  '&PRIV(&I)'
         AIF   ('&PRM' EQ '').P12LOOP
      EXGCHECK NONE,&PRM,(SYS,CON,CAN,UNL,FAIL),OPT=LIST
         AIF   (NOT &OK).BOO12
         AIF   (&DUPL).P12LOOP
         AIF   ('&PRM' EQ 'SYS').P12SYS
         AIF   ('&PRM' EQ 'CON').P12CON
         AIF   ('&PRM' EQ 'CAN').P12CAN
         AIF   ('&PRM' EQ 'UNL').P12UNL
.*       DROP THROUGH TO 'FAIL'
&OPTVAL(4) SETC '1'.'&OPTVAL(4)'(2,7)
         AGO   .P12LOOP
.P12SYS  ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,6).'1'.'&OPTVAL(4)'(8,1)
         AGO   .P12LOOP
.P12CON  ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,4).'1'.'&OPTVAL(4)'(6,3)
         AGO   .P12LOOP
.P12CAN  ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,5).'1'.'&OPTVAL(4)'(7,2)
         AGO   .P12LOOP
.P12UNL  ANOP  ,
&OPTVAL(4) SETC '&OPTVAL(4)'(1,7).'1'
         AGO   .P12LOOP
.BOO12    MNOTE 8,'INVALID OPTION  PRIV=''&PRM'''
         AGO   .P12LOOP
.P13     AIF   ('&DELAY' EQ 'NO').P14                            77117
      EXGCHECK NONE,&DELAY,(YES,DELAY,IGNORE,END),OPT=LIST      GP11230
         AIF   (NOT &OK).BOO13                                   77117
         AIF   (&INDX EQ 4).DELEND                              GP11230
&OPTVAL(5) SETC '1'.'&OPTVAL(5)'(2,7)                            77117
         AIF   (&INDX NE 3).P14                                 GP11230
&OPTVAL(5) SETC '11'.'&OPTVAL(5)'(3,6)                           79156
         AGO   .P14                                              77117
.DELEND  ANOP  ,             END TASK ON DISCONNECT (TN3270)    GP11230
&OPTVAL(5) SETC '&OPTVAL(5)'(1,5).'1'.'&OPTVAL(5)'(7,2)         GP11230
         AGO   .P14                                             GP11230
.BOO13   MNOTE 8,'INVALID OPTION  DELAY=''&DELAY'''              77117
.P14  EXGCHECK NONE,&GROUP,OPT=ALPHA,NULL=YES                    79156
         AIF   (NOT &OK).BOO14                                   79156
&OPTVAL(12) SETC ''                                              79156
         AIF   ('&GROUP' EQ '').P15                              79156
         AIF   (K'&GROUP NE 3).BOO14                             79156
&OPTVAL(12) SETC '&GROUP'                                        79156
         AGO   .P15                                              79156
.BOO14   MNOTE 4,'INVALID LPA GROUP=''&GROUP'''                  79156
.P15  EXGCHECK NONE,&GAMES,(NO,YES,GAMES),OPT=LIST,NULL=YES      79194
         AIF   (NOT &OK).BOO15                                   79194
         AIF   ('&GAMES' NE 'NO').P15A                           89020
&OPTVAL(5) SETC '&OPTVAL(5)'(1,4).'1'.'&OPTVAL(5)'(6,3)          79194
         AGO   .P15A                                             89020
.BOO15   MNOTE 8,'INVALID OPTION  GAMES=''&GAMES'''              79194
.P15A EXGCHECK NONE,&PROMPT,(NO,YES,ID),OPT=LIST,NULL=YES        89020
         AIF   (NOT &OK).BOO15A                                  89020
         AIF   ('&PROMPT' NE 'ID' AND '&PROMPT' NE 'YES').P16    89020
&OPTVAL(5) SETC '&OPTVAL(5)'(1,3).'1'.'&OPTVAL(5)'(5,4)          89020
         AGO   .P16                                              89020
.BOO15A  MNOTE 8,'INVALID OPTION  PROMPT=''&PROMPT'''            89020
.P16     AIF   ('&DASKIP' EQ '').P17                             82361
&J       SETA  N'&DASKIP                                         82361
&I       SETA  0                                                 82361
.P16LOOP AIF   (&I GE &J).P17                                    82361
&I       SETA  &I+1                                              82361
&PRM     SETC  '&DASKIP(&I)'                                     82361
         AIF   ('&PRM' EQ '').P16LOOP                            82361
 EXGCHECK NONE,&PRM,(IDLE,DRAIN,DRAINED,STC,TSU,START,TSO,TS,PRM,PERM),*
               OPT=LIST                                         GP99016
         AIF   (NOT &OK).BOO16                                   82361
         AIF   (&DUPL).P16LOOP                                   82361
         AIF   ('&PRM' EQ 'IDLE').P16IDLE                        82361
         AIF   ('&PRM' EQ 'DRAIN' OR '&PRM' EQ 'DRAINED').P16DRAN
         AIF   ('&PRM' EQ 'STC' OR '&PRM' EQ 'START').P16STC     82361
         AIF   ('&PRM' EQ 'TSO' OR '&PRM' EQ 'TS').P16TSU       GP99016
.*       DROP THROUGH TO 'PRM'                                  GP99016
&OPTVAL(13) SETC '&OPTVAL(13)'(1,4).'1'.'&OPTVAL(13)'(6,3)      GP99016
         AGO   .P16LOOP                                         GP99016
.P16TSU  ANOP  ,                                                GP99016
&OPTVAL(13) SETC '&OPTVAL(13)'(1,3).'1'.'&OPTVAL(13)'(5,4)       82361
         AGO   .P16LOOP                                          82361
.P16STC  ANOP  ,                                                 82361
&OPTVAL(13) SETC '&OPTVAL(13)'(1,2).'1'.'&OPTVAL(13)'(4,5)       82361
         AGO   .P16LOOP                                          82361
.P16IDLE ANOP  ,                                                 82361
&OPTVAL(13) SETC '&OPTVAL(13)'(1,1).'1'.'&OPTVAL(13)'(3,6)       82361
         AGO   .P16LOOP                                          82361
.P16DRAN ANOP  ,                                                 82361
&OPTVAL(13) SETC '1'.'&OPTVAL(13)'(2,7)                          82361
         AGO   .P16LOOP                                          82361
.BOO16    MNOTE 8,'INVALID OPTION  DASKIP=''&PRM'''              82361
         AGO   .P16LOOP                                          82361
.P17  EXGCHECK NONE,&TRAN,OPT=ALPHA,NULL=YES                     82361
         AIF   (NOT &OK).BOO17                                   82361
&OPTVAL(14) SETC ''                                              82361
         AIF   ('&TRAN' EQ '').P17A                             GP99106
         AIF   (K'&TRAN GT 4).BOO17                              82361
&OPTVAL(14) SETC '&TRAN'                                         82361
         AGO   .P17A                                            GP99106
.BOO17   MNOTE 4,'INVALID LPA/LSQA TRAN=''&TRAN'''               82361
.P17A EXGCHECK NONE,&TRGE,OPT=ALPHA,NULL=YES                    GP99106
         AIF   (NOT &OK).BOO17A                                 GP99106
&OPTVAL(15) SETC '&OPTVAL(14)'                                  GP99106
         AIF   ('&TRGE' EQ '').P18                              GP99106
         AIF   (K'&TRGE GT 4).BOO17A                            GP99106
&OPTVAL(15) SETC '&TRGE'                                        GP99106
         AGO   .P18                                             GP99106
.BOO17A  MNOTE 4,'INVALID LPA/LSQA TRGE=''&TRAN'''              GP99106
.P18     AIF   (K'&COLIN EQ 34).P19                              88364
         MNOTE 8,'COLIN STRING INVALID'                          88364
         AGO   .MEND                                             88364
.P19     AIF   (K'&COLOUT EQ 34).P20                             88364
         MNOTE 8,'COLOUT STRING INVALID'                         88364
         AGO   .MEND                                             88364
.P20     AIF   (K'&COLREP EQ 9).P21                              88364
         MNOTE 8,'COLREP STRING INVALID'                         88364
         AGO   .MEND                                             88364
.P21     EXGCHECK NONE,&EXPART,(0,1,2,3,4,5),OPT=LIST            88364
         AIF   (&OK).P22                                         88364
         MNOTE 8,'INVALID EXPART'                                88364
         AGO   .MEND                                             88364
.P22     EXGCHECK NONE,&IMPART,(S,L,C,D),OPT=LIST                88364
         AIF   (&OK).NEWOK                                       88364
         MNOTE 8,'INVALID IMPART'                                88364
         AGO   .MEND                                             88364
.NEWOK   ANOP  ,                                                 88364
&OPTCIN  SETC  '&COLIN'                                          88364
&OPTCOT  SETC  '&COLOUT'                                         88364
&OPTCRP  SETC  '&COLREP'                                         88364
&OPTEXP  SETC  '&EXPART'                                         88364
&OPTIMP  SETC  '&IMPART'                                         88364
.MEND    MEND  ,                                                 88364
