         MACRO
&NM      PRTCOM &OM,&B0=,&B1=0,&DEV=0,&FUN=,&A0=,&A1=,&OPT=,&A80=OFF
.*--------------------------------------------------------------------*
.*                                                                    *
.*   PRTCOM PROVIDES THE INTERFACE TO THE @PRINTER SERVICE ROUTINE    *
.*   ARGUMENTS ARE PASSED IN R0, R1, AND ACR0 (PRTF,PRTS)             *
.*                                                                    *
.*   IN AM24, THE REGISTER USE IS:                                    *
.*                                                                    *
.*   R0:0           R0:1           R0:2           R0:3                *
.*                                                                    *
.*   OPT FLAGS      TITLE/FOOTER#  DEVICE MASK    PRT FUNCTION REQ.   *
.*   (EITHER B0 OR OPT=)                                              *
.*                                                                    *
.*   R1:0           R1:1           R1:2           R1:3                *
.*                                                                    *
.*   LENGTH/ENDCH   ADDRESS-OF-LIST-OR-PRINT-DATA-ETC.                *
.*                                                                    *
.*--------------------------------------------------------------------*
.*                                                                    *
.*   IN AM31, THE REGISTER USE IS:                                    *
.*                                                                    *
.*   R0:0           R0:1           R0:2           R0:3                *
.*                                                                    *
.*   FLAG           TITLE/FOOTER#  DEVICE MASK    PRT FUNCTION REQ.   *
.*   (EITHER B0 OR OPT=)                                              *
.*                                                                    *
.*   R1:0           R1:1           R1:2           R1:3                *
.*                                                                    *
.*   ADDRESS-OF-LIST-OR-PRINT-DATA-ETC.---------------                *
.*                                                                    *
.*                                                                    *
.*   ACR0:0         ACR0:1         ACR0:2         ACR0:3              *
.*                                                                    *
.*                                                LENGTH/ENDCH        *
.*                                                                    *
.*--------------------------------------------------------------------*
.*                                                                    *
.*   FLAGS :                                                          *
.*                                                                    *
.*     80 - ABEND IF NOT OPENED         (PRTOPEN)                     *
.*     40 - (DON'T USE)                 (PRTOPEN)                     *
.*     20 - SUPPRESS WTO IF NOT OPENED  (PRTOPEN)                     *
.*     10 - ABEND IF DD DUMMY           (PRTOPEN)                     *
.*     20 - NEW SHEET ON NEXT PAGE EJECT                              *
.*     04 - THIS RECORD CONTAINS ASA                                  *
.*     02 - THIS RECORD CONTAINS MACHINE CODE                         *
.*     01 - NO CONTROL CHARACTER IN RECORD                            *
.*                                                                    *
.*   TITLE/FOOTER #:   4 BITS EACH; TOTAL OF EACH (PRTOPEN); ELSE     *
.*     NUMBER OF TITLE/FOOTER THIS RECORD IS TO BE USED FOR           *
.*                                                                    *
.*   DEVICE MASK:  (0 TREATED AS 1)                                   *
.*                                                                    *
.*   NUMBER OF DEVICE (1-8) FOR PRTOPEN; R1 POINTS TO PRTWORK/PUNWORK *
.*   ONE BIT PER DEVICE TO RECEIVE THIS RECORD (E.G. DEV=3 WRITES THE *
.*     REQUEST TO DEVICES 1 AND 2)                                    *
.*                                                                    *
.*   FUNCTION:  INDEX TO REQUESTED FUNCTION:                          *
.*                                                                    *
.*   0 - CLOSE      1 - TCLOSE     2 - OPEN       3 - ROOM (COND.SPC) *
.*   4 - SPACE N    5 - FD LIST    6 - V-RECORD   7 - FIXED REC.      *
.*   8 - SEPARATE   9 - SNAP      10 - FD ITEM   11 - STRING RECORD   *
.*  12 - AM31 FREC                                                    *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB,&PRTMODE
         GBLB  &MVSESA                                          GP04234
         LCLA  &I,&J,&K,&OPA,&VD,&D(8)
         LCLB  &F01,&F02,&F04,&F08,&F10,&F20,&F40,&F80,&B0Z
         LCLB  &INDEV                                            81259
         LCLC  &DC,&LNR,&OP                                      81259
.*   FOR AM31 SUPPORT, THE A0 FIELD IS NOW PLACED INTO ACCESS REGISTER
.*   0, BYTE 3
.*
&MACPLAB SETC  '&NM'                                             81259
&K       SETA  N'&OPT                                            90309
&B0Z     SETB  ('&B0' EQ '' OR '&B0' EQ '0')  OPTION FLAGS ?
         AIF   (&B0Z AND &K EQ 0).DEFOPT      NO FLAGS, NO OPTIONS
         AIF   (&B0Z OR  &K EQ 0).WHATOPT
         MNOTE 8,'&OM: BOTH B0 AND OPT SPECIFIED; B0=&B0 IGNORED'
.WHATOPT AIF   (NOT &B0Z).ITMNOPT    B0 - USE IT
&J       SETA  0             COUNT OF PROCESSED OPERANDS
.ITMLOOP AIF   (&I GE &K).ITMTEST                                90309
&I       SETA  &I+1                                              90309
&DC      SETC  '&OPT(&I)'
         AIF   ('&DC' EQ '').ITMLOOP  IGNORE NULLS
         AIF   ('&DC' EQ 'WTO').ITMLOOP  IGNORE SEMANTIC NULLS  GP03027
         AIF   ('&DC' EQ 'DUMMY').ITMLOOP  IGNORE SEMANTIC NULL
&J       SETA  &J+1                                              90309
&F80     SETB  (&F80 OR '&DC' EQ 'ABE' OR '&DC' EQ 'LIST')
&F80     SETB  (&F80 OR '&DC' EQ 'ABEND')
&F40     SETB  (&F40 OR '&DC' EQ 'X9700')
&F20     SETB  (&F20 OR '&DC' EQ 'SHEET' OR '&DC' EQ 'PAGE')
&F20     SETB  (&F20 OR '&DC' EQ 'AUX' OR '&DC' EQ 'TRAY2')
&F20     SETB  (&F20 OR '&DC' EQ 'AUXTRAY' OR '&DC' EQ 'TRAY')
&F20     SETB  (&F20 OR '&DC' EQ 'NOWTO')
&F10     SETB  (&F40 OR '&DC' EQ 'NODUMMY')
&F10     SETB  (&F40 OR '&DC' EQ 'ABDUMMY')
&F04     SETB  (&F04 OR '&DC' EQ 'ASA')   (DEFAULT)
&F02     SETB  (&F02 OR '&DC' EQ 'MC' OR '&DC' EQ 'MCC')
&F02     SETB  (&F02 OR '&DC' EQ 'SKIPEJE' OR '&DC' EQ 'NOEJE')
&F01     SETB  (&F01 OR '&DC' EQ 'NO' OR '&DC' EQ 'NOCC')
&F01     SETB  (&F01 OR '&DC' EQ 'EJECT2' OR '&DC' EQ '2EJECT')
         AGO   .ITMLOOP                                          90309
.ITMTEST ANOP  ,
&OPA     SETA  &F80+&F40+&F20+&F10+&F08+&F04+&F02+&F01
         AIF   (&OPA EQ &J).DONOPT  EACH OPERAND VALID ?
.BADOPT  MNOTE 4,'&OM: ERROR - OPT PARAMETER BAD: &OPT'
.DONOPT  ANOP  ,
&OPA     SETA  &F80*128+&F40+&F20*32+&F10*16+&F08*8+&F04*4+&F02*2+&F01
&OP      SETC  '&OPA'
         AGO   .POSTOPT                                         GP99029
.ITMNOPT ANOP  ,                                                 90309
&OP      SETC  '&B0'         USE USER'S PASSED VALUE             90309
         AIF   ('&OP' NE '').POSTOPT
.DEFOPT  ANOP  ,
&OP      SETC  '0'           MAKE IT NON-BLANK
.POSTOPT AIF   ('&DEV' EQ '' OR '&DEV' EQ '0').NODV
         AIF   ('&DEV' NE 'ALL' AND '&DEV' NE '255').DVSOM      GP03240
&VD      SETA  255
         AGO   .NODV
.DVSOM   AIF   (K'&DEV LT 2).DVSOL                               81259
         AIF   ('&DEV'(1,1) NE '=').DVSOL                        81259
&INDEV   SETB  1             SET INDIRECT DEVICE NUMBER          81259
         AGO   .NODV                                             81259
.DVSOL   ANOP  ,                                                 81259
&I       SETA  0
&K       SETA  N'&DEV
.DEVLOOP ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT &K).DVEND
         AIF   ('&DEV(&I)' EQ '').DEVLOOP
         AIF   ('&DEV(&I)' EQ '0').DEVLOOP
         AIF   ('&DEV(&I)' LT '1' OR '&DEV(&I)' GT '8').DVERR
&D(&DEV(&I)) SETA  1
         AGO   .DEVLOOP
.DVERR   MNOTE 8,'*** INVALID DEVICE NUMBER &DEV(&I)'
         AGO   .DEVLOOP
.DVEND   ANOP  ,
&VD      SETA  128*&D(8)+64*&D(7)+32*&D(6)+16*&D(5)+8*&D(4)
&VD      SETA  &VD+4*&D(3)+2*&D(2)+&D(1)
.NODV    AIF   ('&OP' NE '0' OR '&B1' NE '0').LONG              GP99029
         AIF   (&VD GT 15).LONG
&J       SETA  &VD*256+&FUN
         MACPARM R0,&J       LOAD DEVICE/FUNCTION INDEX          81259
         AGO   .POST0                                            81259
.LONG    ANOP  ,                                                 82326
&MACPLAB L     R0,=AL1(&OP,&B1+0,&VD,&FUN)                       81259
&MACPLAB SETC  ''            CANCEL LABEL                        81259
.POST0   AIF   (NOT &INDEV).LOAD1                                81259
&VD      SETA  K'&DEV-1                                          81259
&DC      SETC  '&DEV'(2,&VD)                                     81259
&MACPLAB ICM   R0,2,&DC                                          81259
&MACPLAB SETC  ''                                                81259
.LOAD1   AIF   ('&FUN' EQ '0' OR '&FUN' EQ '1').BAL  CLOSE?
         MACPARM R1,&A1      LOAD PARAMETER REGISTER
         AIF   ('&A80' EQ 'OFF').NOTHIGH                        GP03025
         O     R1,=X'80000000'  SET LIST BIT (PRTBIG)           GP03025
.NOTHIGH AIF   (NOT &MVSESA).VER24                              GP04234
         AIF   (&FUN EQ 11 OR &FUN EQ 12).ACR                   GP03025
.VER24   AIF   ('&A0' EQ '' OR '&A0' EQ '0').BAL                 90309
&K       SETA  K'&A0                                            GP05190
         AIF   (&K LT 3).VER24I                                 GP05190
         AIF   ('&A0'(1,1) NE '(' OR '&A0'(2,1) EQ '(').VER24I  GP05190
         AIF   ('&A0'(&K,1) NE ')' OR '&A0'(&K-1,1) EQ ')').VER24I
         LA    R1,0(,R1)     CLEAR HIGH BYTE                    GP05190
         MACPARM R14,&A0,OP=LR,OPR=LR                           GP05190
         SLL   R14,24                                           GP05190
         OR    R1,R14        INSERT LENGTH                      GP05190
         AGO   .BAL                                             GP05190
.VER24I  ICM   R1,8,=AL1(&A0)                                    90309
         AGO   .BAL
.ACR     AIF   ('&A0' NE '' AND '&A0' NE '0').ACRLOAD
         AIF   (&FUN EQ 11 AND '&A0' EQ '0').ACRLOAD
         MNOTE 8,'&OM: REQUIRED LENGTH VALUE MISSING'
.ACRLOAD MACPARM R15,&A0
.*NEED(R) N     R15,=X'000000FF'  FOR FUTURE USE
         SAR   R0,R15        LOAD INTO ACCESS REGISTER
.BAL     AIF   ('&PRTMODE' EQ 'V').VCON
         L     R15,@PRINTER
         AGO   .BALR
.VCON    L     R15,=V(@PRINTER)
.BALR    BASR  R14,R15                                          GP99020
         MEND  ,
