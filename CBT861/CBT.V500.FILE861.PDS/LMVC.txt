         MACRO
&NM      LMVC  &R1,&R3
         GBLC  &MODEL
         LCLC  &N,&A1,&A3
         LCLA  &I
.*
.*       THIS MACRO SUPPORTS LONG MOVES OF EQUAL LENGTH AREAS
.*       REGISTERS MUST BE SPECIFIED AS FOR MVCL
.*       AND ARE RESTRICTED TO ABSOLUTE VALUES OR THE FORM
.*       R||ABSOLUTE; E.G. 2 4 OR R4, R6, ETC.
.*
&I       SETA  K'&R1
         AIF   (&I LT 1 OR &I GT 3).BADONE
&A1      SETC  '&R1'
         AIF   ('&A1'(1,1) NE 'R').HAV1
&A1      SETC  '&A1'(2,&I-1)
.HAV1    AIF   ('&A1' EQ '2' OR '&A1' EQ '4').GOOD1
         AIF   ('&A1' EQ '6' OR '&A1' EQ '8' OR '&A1' EQ '10').GOOD1
         AIF   ('&A1' EQ '12' OR '&A1' EQ '14').GOOD1
.BADONE  MNOTE 8,'INVALID TO REGISTER ''&R1'''
         MEXIT
.GOOD1   ANOP  ,
&I       SETA  K'&R3
         AIF   (&I LT 1 OR &I GT 3).BADTHRE
&A3      SETC  '&R3'
         AIF   ('&A3'(1,1) NE 'R').HAV3
&A3      SETC  '&A3'(2,&I-1)
.HAV3    AIF   ('&A3' EQ '2' OR '&A3' EQ '4').GOOD3
         AIF   ('&A3' EQ '6' OR '&A3' EQ '8' OR '&A3' EQ '10').GOOD3
         AIF   ('&A3' EQ '12' OR '&A3' EQ '14').GOOD3
.BADTHRE MNOTE 8,'INVALID FROM REGISTER ''&R3'''
         MEXIT
.GOOD3   AIF   ('&A1' NE '&A3').LAPOK
         MNOTE 8,'TO AND FROM REGISTERS EQUAL'
         MEXIT
.LAPOK   ANOP  ,
&N       SETC  '&NM' .
         AIF   ('&N' NE '').NOK                                  86010
&N       SETC  'LM'.'&SYSNDX'                                    86010
.NOK     AIF   ('&MODEL' NE '360').MVCL
&N       LA    &R3+1,256 .   SET MVC MAX LENGTH
&N.1     CR    &R1+1,&R3+1 . FULL BLOCK ?
         BNH   &N.2 .        NO, MOVE PART
         MVC   0(256,&R1),0(&R3) MOVE BLOCK
         AR    &R1,&R3+1 .   UP TO ADDRESS
         AR    &R3,&R3+1 .   UP FROM
         SR    &R1+1,&R3+1 . REMAINDER
         BP    &N.1 .        CHECK RESIDUAL LENGTH
         B     &N.4 .        DONE
&N.3     MVC   0(0,&R1),0(&R3)  EXECUTE PATTERN
&N.2     BCTR  &R1+1,0 .     REMAINING LENGTH - 1
         EX    &R1+1,&N.3 .  MOVE REMAINDER
&N.4     EQU   *
         MEXIT
.MVCL    ANOP  ,
&N       MVCL  &R1,&R3 .     MOVE EQUAL AREAS
         MEND
