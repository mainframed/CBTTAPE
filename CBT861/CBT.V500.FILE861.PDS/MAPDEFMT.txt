         MACRO ,
&NM      MAPDEFMT &DSECT=YES,&PFX=PDD,&RETURN=,&VER=1           GP05180
.*
.*  MAPPING MACRO FOR SUBROUTINE SUBDEFMT (ADAPTED FROM @SERVICE PDSDE)
.*   1) WHENEVER USER READS A DIRECTORY BLOCK, STORE ADDRESS (OF LENGTH
.*      FIELD) IN pddNEXT, 2 (LENGTH OF LENGTH) IN pddINCR, AND
.*      BYTE -1 (BLOCK-1+(length)) IN pddLAST
.*   2) COPY THE RECFM TO pddRECFM (NEEDED FOR LKED/BINDER vs OTHER)
.*   3) CALL SUBDEFMT, USE RESULTS, DO BXLE ON pddNEXT/pddINCR/pddLAST
.*      READ NEXT BLOCK IF BXLE FALLS THROUGH
.*
.*   CHANGED FOR Y2K SUPPORT, ETC.                              GP98365
.*
         LCLC  &P
&P       SETC  'PDD'
         AIF   ('&PFX' EQ '').HAVEP
&P       SETC  '&PFX'
.HAVEP   ANOP  ,
&NM      MACMAPHD MAP,PFX=&PFX,DSECT=&DSECT,DFLT=DEFMT          GP05180
&P.NEXT  DC    A(0)          ADDRESS OF CURRENT MEMBER
&P.INCR  DC    A(0)          BXLE INCREMENT FOR CURRENT MEMBER
&P.LAST  DC    A(0)          LAST USED BYTE IN BLOCK
&P.RECFM DC    AL1(0)        RECFM
&P.TYPE  DC    X'00'         ENTRY TYPE
&P.TLKED EQU   X'80'           LINKAGE EDITOR ENTRY
&P.TSPF  EQU   X'40'           SPF ENTRY
&P.TWYL  EQU   X'20'           WYLBUR ENTRY (LOCAL)
&P.TDTX  EQU   X'10'           IEBUPDTX ENTRY
&P.FLAGS DC    X'00'         ENTRY FLAGS (FIELDS PRESENT)
&P.AOSLE EQU   X'80'           PRODUCED BY VS LINKAGE EDITOR
&P.FREAL EQU   X'20'           MAIN MEMBER/EPA PRESENT
&P.FSSI  EQU   X'10'           SSI FIELD PRESENT
&P.FAPF  EQU   X'08'           APF FIELD PRESENT
&P.FSCTR EQU   X'04'           SCATTER LOAD MODULE
&P.FBIND EQU   X'01'           PRODUCED BY BINDER               GP05180
&P.FLAG2 DC    X'00'         SECOND FLAG BYTE                   GP98365
&P.FLAG3 DC    X'00'         THIRD FLAG BYTE                    GP98365
&P.UDLEN DC    X'00'         NUMBER OF HALFWORDS OF USER DATA
&P.OSSI  DC    H'0'          OFFSET TO SSI FROM NAME
&P.OAPF  DC    H'0'          OFFSET TO APF FROM NAME
         DC    4H'0'         EXTRA FOR EXPANSION                GP98365
&P.CLRLN EQU   *-&P.TYPE       LENGTH TO CLEAR
&P.RALIS DC    C' '          BLANK OR * FOR ALIAS OR Â¬ FOR SCLM GP98365
&P.RNAME DC    CL8' '        EDITED MEMBER NAME                 GP98365
&P.RTTR  DC    CL6' '        PRIMARY TTR                        GP05180
&P.RSSI  DC    CL8' '        SSI OR BLANKS                      GP98365
&P.RYMD  DC    CL10' '       CHANGE DATE YYYYMMDD               GP98365
&P.RJDAY DC    CL3' '        CHANGE DATE JJJ (JULIAN FORM)      GP98365
&P.RTIME DC    CL5' '        TIME MODIFED/ADDED   HH:MM         GP05180
&P.VVMM  DC    0CL5' '       SPF VERSION/MODIFICATION           GP98365
&P.RAPF  DC    CL4'AC=N',C' '  APF CODE > 0 OR BLANKS           GP98365
&P.SIZE  DC    0CL8' '       LOAD MODULE SIZE                   GP05180
&P.RLINE DC    CL8' '        CURRENT LINE COUNT                 GP05180
&P.RMAIN DC    CL8' '        MAIN MEMBER OR SPF UID OR BLANKS   GP98365
&P.REPAD DC    CL8' '        ENTRY POINT OFFSET OF MODULE       GP98365
&P.RMODE DC    CL4' '        AaRa/A3R2/etc. MODE                GP98365
&P.RATTR DC    6CL4' '       REFR/RENT/REUS/OVLY/NXEC/PAGE      GP98365
&P.RATTN EQU   (*-&P.RATTR)/L'&P.RATTR  NUMBER OF ENTRIES       GP98365
&P.RBLNK EQU   *-&P.RALIS      LENGTH TO BLANK                  GP98365
.MEND    MEND  ,
