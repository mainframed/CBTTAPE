/******* IDCAMS FROM ISPF - USE WITH PANEL IDCAML10
CONTROL  MSG
ISPEXEC  VGET (ZSCREEN)
SET      SI = TMPIN&ZSCREEN
SET      SP = TMPPRIN&ZSCREEN
/******* ALLOCATE IDCAMS FILES; DISPLAY PANEL
ALLOC    FI(&SI) NEW SP(1) TRACKS UNIT(VIO) LRECL(80) RECFM(F) REUSE
ALLOC    FI(&SP) NEW SP(1 5) TRACKS REUSE +
         BLKSIZE(2004) LRECL(125) DSORG(PS) RECFM(V B A)
ISPEXEC  LMINIT DATAID(MYDATA) DDNAME(&SP)
DISPLAY: ISPEXEC  DISPLAY PANEL(IDCAML10)
SET      &TESTCC = &LASTCC
IF       &TESTCC ¬= 0 THEN GOTO ENDER
IF       .&P ¬= . THEN GOTO PRINTER
IF       .&M ¬= . THEN GOTO MODELER
OPENFILE &SI OUTPUT
SET      &&SI = &STR( &UCMMD -)
PUTFILE  &SI
SET      &&SI = &STR( &UENTITY -)
PUTFILE  &SI
SET      &&SI = &STR( &UNPARM -)
PUTFILE  &SI
SET      &&SI = &STR( &UNPARM2 -)
PUTFILE  &SI
SET      &&SI = &STR( &UNPARM3)
PUTFILE  &SI
CLOSFILE &SI
/******* IDCAMS EXECUTION
IDCAMS:  CALL 'SYS1.LINKLIB(IDCAMSPF)' '&ZSCREEN'
ISPEXEC  BROWSE DATAID(&MYDATA)
GOTO     DISPLAY
PRINTER: %IDCAMPRT
GOTO     DISPLAY
MODELER: %IDCAMLM&M
ISPEXEC  VGET (UCMMD UENTITY UNPARM UNPARM2 UNPARM3 SP1 SP2 SP3 SP4)
GOTO     DISPLAY
ENDER:   ISPEXEC  VPUT (UCMMD UENTITY UNPARM UNPARM2 UNPARM3) PROFILE
IF       .&MYDATA > . THEN ISPEXEC LMFREE DATAID(&MYDATA)
CONTROL  NOMSG
FREE     FI(&SP &SI)
END
