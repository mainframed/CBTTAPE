CONTROL  NOMSG NOFLUSH MAIN
CONTROL  MSG
PANELER: +
ISPEXEC  DISPLAY PANEL(ISPCOMP)
SET      &TESTCC = &LASTCC
IF       &TESTCC ¬= 0 THEN GOTO ENDER
/******* INITIALIZE WORKING STORAGE VARIABLES
SET      SLSHASK = &STR(/*)
ISPEXEC  VGET (PRJ1,TYP1,LIB1)
SET      JDSN = &DSN
IF       &LENGTH(&DSN) = 0 THEN SET JDSN = &STR('&PRJ1..&LIB1..&TYP1')
IF       &SUBSTR(1:1,&JDSN) ¬= ' THEN SET JDSN = &STR('&SYSUID..&DSN')
LISTDSI  &JDSN DIRECTORY
WRITE    &JDSN SELECTED FOR COMPRESS!
IF       &LENGTH(&SYSUNITS) > 0 THEN GOTO JCLER.
WRITE    *****
WRITE    ***** WARNING - FILE ERROR!!  *****
WRITE    ***** VERIFY FILE ACTUALLY RESIDES ON DISK. *****
WRITE    *****
GOTO     PANELER
JCLER:   +
IF       &SYSUNITS = CYLINDER THEN SET &UNIT = CYL
IF       &SYSUNITS = TRACK THEN SET &UNIT = TRK
IF       &SYSUNITS = BLOCK THEN SET &UNIT = &SYSBLKSIZE
SET      &SPACE = &STR('(&UNIT,(&SYSPRIMARY,&SYSSECONDS,&SYSADIRBLK))'
JCLER2:  +
IF       .&Q = .Y THEN GOTO FASTER
CONTROL  NOMSG
WRITE    SUBMITTING BATCH COMPRESS JOB, PLEASE WAIT.
SUBMIT * END($$)
//&STR(&SYSUID)CMP JOB (0000,0000),'ISPF BATCH COMP',
//        COND=(0,NE),
//        MSGCLASS=X,
//        NOTIFY=&SYSUID,
//        CLASS=&JCLASS
//JOBOUT  OUTPUT JESDS=ALL,DEFAULT=YES,
//        &JPARM
/&SLSHASK      CALLS PROC TO COMPRESS LIBRARY
//SY010CMP EXEC PROC=ISPCOMPP,
//         DSN=&JDSN,
//         SPACE=&SPACE
$$
WRITE    PRESS 'ENTER' TO CONTINUE.
GOTO     PANELER
FASTER:  +
CONTROL  NOMSG
WRITE    SUBMITTING FAST COMPRESS JOB, PLEASE WAIT.
SUBMIT * END($$)
//&STR(&SYSUID)CMP JOB (0000,0000),'ISPF BATCH FAST COMP',
//        COND=(0,NE),
//        MSGCLASS=X,
//        NOTIFY=&SYSUID,
//        CLASS=&JCLASS
//JOBOUT  OUTPUT JESDS=ALL,DEFAULT=YES,
//        &JPARM
/&SLSHASK      CALLS PROC TO FAST COMPRESS LIBRARY
//SY010CMP EXEC PROC=ISPCOMPF,DSN=&JDSN
$$
WRITE    PRESS 'ENTER' TO CONTINUE.
GOTO     PANELER
ENDER:   +
ISPEXEC  VPUT (JPARM JCLASS) PROFILE
END
