/* REXX    MEMBERS    Produce a concise member-list for a PO dataset.
                      MEMBERS will return its output to the terminal
                      (by default), or via the stack (option STACK)
                      either as a vertical list (option LIST) or as a
                      single line (option LINE).

           Written by Frank Clarke, Oldsmar, FL

     Modification History
     19941026 FXC a dataset with no members should return the string
                  "(EMPTY)"; current version fails by sending the
                  message "Invalid DSName".
     19960410 fxc upgrade to REXXSKEL; handle aliases: if "ALIAS" is
                  specified in <opts> aliasnames are returned in the
                  same way as the main membernames and immediately
                  following their main member, and have "(*)"
                  appended;
     19970820 bab upgrade to REXXSKEL from ver.960119 to ver.970818;
                  correct problem when Alias Names exists w/o true
                  names.
     19971108 fxc upgrade from ver.970818 to v.19971030; y2k
                  compliance; decomm; restore in-line HELP text;
                  minor cosmetics;

*/
address TSO                            /* REXXSKEL ver.19971030      */
arg parms "((" opts

signal on syntax
signal on novalue

call TOOLKIT_INIT                      /* conventional start-up     -*/
rc     = trace(tv)
info   = parms                         /* to enable parsing          */

if info = "" then call HELP            /*                           -*/

call A_INIT                            /*                           -*/
                                   if \sw.0error_found then,
call B_LISTD                           /*                           -*/
                                   if \sw.0error_found then,
call C_BUILD                           /*                           -*/

if \sw.nested then call DUMP_QUEUE     /*                           -*/
exit                                   /*@ MEMBERS                   */
/*
.  ----------------------------------------------------------------- */
A_INIT:                                /*@                           */
   if branch then call BRANCH
   address TSO

   if sw.0list + sw.0line <> 1 then,
      parse value "0 1" with sw.0list sw.0line
   parse var info  dsname info
   parse value "0" with sw.0error_found .

return                                 /*@ A_INIT                    */
/*
.  ----------------------------------------------------------------- */
B_LISTD:                               /*@                           */
   if branch then call BRANCH
   address TSO

   trc = Outtrap("L.")
   "LISTD" dsname "M"
   trc = Outtrap("OFF")
   if l.0 < 7 then,
   if sw.0stack then queue "(EMPTY)"   /* no members !               */
   else do
      say "No members in" dsname
      exit
      end

   do ii = 1 to l.0 until Word(l.ii,1) = "--MEMBERS--"
   end

   /*
      Process the memberlist bottom-up.  <slug> is formed of "anything
      accumulated so far" preceeded by the line above it.  When the
      first 3 bytes of <slug> is blank, it's part of an aliaslist;
      keep it.  When the first three bytes are NOT blank, a member
      name has been found; push the accumulated data onto the stack
      and reinitialize <slug>.
   */
   slug = ""
   do bx = L.0 to ii+1  by  -1
      slug = L.bx Strip(slug)
      if Left(slug,3) <> "" then do; push slug; slug = ""; end
   end

return                                 /*@ B_LISTD                   */
/*
   The memberlist has been pushed onto the stack.
.  ----------------------------------------------------------------- */
C_BUILD:                               /*@                           */
   if branch then call BRANCH
   address TSO

   parse value 0               with ,
               no_more_q  stak .

/*  The phrase "THE FOLLOWING ALIAS NAMES EXIST WITHOUT TRUE NAMES"
    indicates aliases are in the PDS without true member names to match
    on.  Skip this line and remaining lines in queue for member names
                                           Change add on 970820, BAB */
   do queued()
      pull full_qline
      if no_more_q then iterate        /* Clear the queue out        */
      if POS("THE FOLLOWING ALIAS NAMES",full_qline) > 0 then do
         no_more_q = 1 ; iterate
         end
      parse var full_qline mbr . "ALIAS(" aliaslist ")"
      call CA_STORE                    /* put the mbr on the list   -*/

      if sw.0alias then,
      if aliaslist <> "" then do
         /* diagnose here */           /*                            */
         aliaslist = Translate(aliaslist , " " , ",")
         do while aliaslist  <> ""
            parse var aliaslist mbr aliaslist
            mbr = mbr"(*)"
            call CA_STORE              /*                           -*/
         end                           /* while aliaslist not blank  */
         end
   end

   if stak <> "" then,                 /* we loaded it               */
      if sw.0stack then,
         queue stak
      else,
         say stak

return                                 /*@ C_BUILD                   */
/*
   Given: <mbr> and <aliaslist>
.  ----------------------------------------------------------------- */
CA_STORE:                              /*@                           */
   if branch then call BRANCH
   address TSO

   if sw.0line then,                   /* LINE                       */
      stak = stak mbr
   else,                               /* LIST vertically            */
      if sw.0stack then,               /* sw.0list & sw.0stack       */
         queue    mbr
      else,                            /* sw.0list & \sw.0stack      */
         say      mbr

return                                 /*@ CA_STORE                  */
/*
.  ----------------------------------------------------------------- */
LOCAL_PREINIT:                         /*@ customize opts            */
   if branch then call BRANCH
   address TSO

   sw.0alias = SWITCH("ALIAS")         /* show aliases               */
   sw.0stack = SWITCH("STACK")         /* return via the stack       */
   sw.0list  = SWITCH("LIST")          /* arrange in a vertical list */
   sw.0line  = SWITCH("LINE")          /* arrange on one line        */

return                                 /*@ LOCAL_PREINIT             */

/*-------------------------------------------------------------------*/
HELP:                                  /*@                           */
address TSO;"CLEAR"
if helpmsg <> "" then do ; say helpmsg; say ""; end
say "  MEMBERS       Produces a concise member-list for a PO dataset. "
say "                MEMBERS will return its output to the terminal   "
say "                (by default), or via the stack (option STACK)    "
say "                either as a vertical list (option LIST) or as a  "
say "                single line (option LINE), default=LINE.         "
say "                                                                 "
say "  Syntax:   MEMBERS   <dsname>  <(( options>                     "
say "                      <options> are separated from <dsname> by   "
say "                      a double open parenthesis '(('.            "
say "                                                                 "
say "            <STACK>   causes the resultant member list to be     "
say "                      returned via the stack.  If STACK is not   "
say "                      specified, return is to the terminal.      "
say "            <LIST>    causes the returned value(s) to be         "
say "                      presented one member per line (a vertical  "
say "                      list).                                     "
say "            <LINE>    causes the returned value(s) to be         "
say "                      presented as a single string containing all"
say "                      the members in order.                      "
say "            <ALIAS>   requests that alias entries also be        "
say "                      returned.  MEMBERS ignores aliases by      "
say "                      default.  Alias entries returned by MEMBERS"
say "                      will have '(*)' appended to the aliasname. "
pull
"CLEAR"
say "   Debugging tools provided include:"
say "                                                                 "
say "        BRANCH:   show all paragraph entries."
say "                                                                 "
say "        TRACE tv: will use value following TRACE to place"
say "                  the execution in REXX TRACE Mode."
say "                                                                 "
say "                                                                 "
say "   Debugging tools can be accessed in the following manner:"
say "                                                                 "
say "        TSO" exec_name"  parameters  ((  debug-options"
say "                                                                 "
say "   For example:"
say "                                                                 "
say "        TSO" exec_name " (( MONITOR TRACE ?R"
address ISPEXEC "CONTROL DISPLAY REFRESH"
exit                                   /*@ HELP                      */

/* ----------------------------------------------------------------- */
BRANCH: Procedure expose,              /*@                           */
        sigl exec_name
   rc = trace("O")                     /* we do not want to see this */
   arg brparm .

   $a#y = sigl                         /* where was I called from ?  */
   do $b#x = $a#y to 1 by -1           /* inch backward to label     */
      if Right(Word(Sourceline($b#x),1),1) = ":" then do
         parse value sourceline($b#x) with $l#n ":" . /* Paragraph   */
         leave ; end                   /*                name        */
   end                                 /* $b#x                       */

   select
      when brparm = "NAME" then return($l#n) /* Return full name     */
      when brparm = "ID"      then do  /*        Return prefix       */
         parse var $l#n $l#n "_" .     /* get the prefix             */
         return($l#n)
         end                           /* brparm = "ID"              */
      otherwise
         say left(sigl,6) left($l#n,40) exec_name "Time:" time("L")
   end                                 /* select                     */

return                                 /*@ BRANCH                    */

/* ----------------------------------------------------------------- */
DUMP_QUEUE:                            /*@ Take whatever is in stack */
   rc = trace("O")                     /*  and write to the screen   */
   address TSO

   "QSTACK"                            /* how many stacks?           */
   stk2dump    = rc - tk_init_stacks   /* remaining stacks           */
   if stk2dump = 0 & queued() = 0 then return
   say "Total Stacks" rc ,             /* rc = #of stacks            */
       "Begin Stacks" tk_init_stacks , /* Stacks present at start    */
       "Stacks to DUMP" stk2dump

   do dd = rc to tk_init_stacks by -1  /* empty each one.            */
      say "Processing Stack #" dd "Total Lines:" queued()
      do queued();pull line;say line;end /* pump to the screen       */
      "DELSTACK"                       /* remove stack               */
   end                                 /* dd = 1 to rc               */

return                                 /*@ DUMP_QUEUE                */

/* ----------------------------------------------------------------- */
KEYWD: Procedure expose info           /*@ hide all vars, except info*/
   arg kw
   kw_pos = wordpos(kw,info)           /* find where it is, maybe    */
   if kw_pos = 0 then return ""        /* send back a null, not found*/
   kw_val = word(info,kw_pos+1)        /* get the next word          */
   info   = Delword(info,kw_pos,2)     /* remove both                */
return kw_val                          /*@ KEYWD                     */

/* ----------------------------------------------------------------- */
KEYPHRS: Procedure expose,             /*@                           */
         info helpmsg exec_name        /*  except these three        */
   arg kp
   wp    = wordpos(kp,info)            /* where is it?               */
   if wp = 0 then return ""            /* not found                  */
   front = subword(info,1,wp-1)        /* everything before kp       */
   back  = subword(info,wp+1)          /* everything after kp        */
   parse var back dlm back             /* 1st token must be 2 bytes  */
   if length(dlm) <> 2 then            /* Must be two bytes          */
      helpmsg = helpmsg "Invalid length for delimiter("dlm") with KEYPHRS("kp")"
   if wordpos(dlm,back) = 0 then       /* search for ending delimiter*/
      helpmsg = helpmsg "No matching second delimiter("dlm") with KEYPHRS("kp")"
   if helpmsg <> "" then call HELP     /* Something is wrong         */
   parse var back kpval (dlm) back     /* get everything b/w delim   */
   info =  front back                  /* restore remainder          */
return Strip(kpval)                    /*@ KEYPHRS                   */

/* ----------------------------------------------------------------- */
NOVALUE:                               /*@                           */
   say exec_name "raised NOVALUE at line" sigl
   say " "
   say "The referenced variable is" condition("D")
   say " "
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ NOVALUE                   */

/* ----------------------------------------------------------------- */
SHOW_SOURCE:                           /*@                           */
   call DUMP_QUEUE                     /* Spill contents of stacks  -*/
   if sourceline() <> "0" then         /* to screen                  */
      say sourceline(zsigl)
   rc =  trace("?R")
   nop
   exit                                /*@ SHOW_SOURCE               */

/* ----------------------------------------------------------------- */
SS: Procedure                          /*@ Show Source               */
   arg  ssbeg  ssend  .
   if ssend = "" then ssend = 10
   if \datatype(ssbeg,"W") | \datatype(ssend,"W") then return
   address TSO "CLEAR"
   ssend = ssbeg + ssend
   do ssii = ssbeg to ssend ; say sourceline(ssii) ; end
   address TSO "CLEAR"
return                                 /*@ SS                        */

/* ----------------------------------------------------------------- */
SWITCH: Procedure expose info          /*@                           */
   arg kw
   sw_val = Wordpos(kw,info) > 0       /* exists = 1; not found = 0  */
   if sw_val then                      /* exists                     */
      info = Delword(info,Wordpos(kw,info),1) /* remove it           */
return sw_val                          /*@ SWITCH                    */

/* ----------------------------------------------------------------- */
SYNTAX:                                /*@                           */
   errormsg = exec_name "encountered REXX error" rc "in line" sigl":",
                        errortext(rc)
   say errormsg
   zsigl = sigl
   signal SHOW_SOURCE                  /*@ SYNTAX                    */

/* ----------------------------------------------------------------- */
TOOLKIT_INIT:                          /*@                           */
   address TSO
   info = Strip(opts,"T",")")          /* clip trailing paren        */

   parse source  sys_id  how_invokt  exec_name  DD_nm  DS_nm  as_invokt,
                   cmd_env  addr_spc  usr_tokn
   parse value "" with  tv  helpmsg  .
   parse value 0   "ISR00000  YES"     "Error-Press PF1"    with,
               sw.  zerrhm    zerralrm  zerrsm

   if SWITCH("TRAPOUT") then do
      "TRAPOUT" exec_name parms "(( TRACE R" info
      exit
      end                              /* trapout                    */

   if Word(parms,1) = "?" then call HELP /* I won't be back          */

   "QSTACK" ; tk_init_stacks = rc      /* How many stacks?           */

   parse value SWITCH("BRANCH") SWITCH("MONITOR") SWITCH("NOUPDT") with,
               branch           monitor           noupdt    .

   parse value mvsvar("SYSNAME") sysvar("SYSNODE") with,
               #tk_cpu           node          .

   sw.nested    = sysvar("SYSNEST") = "YES"
   sw.batch     = sysvar("SYSENV")  = "BACK"
   sw.inispf    = sysvar("SYSISPF") = "ACTIVE"

   "NEWSTACK"
   "DFLTHLQ"  ; pull hlq.  hlqdata
   "DELSTACK"

   do while hlqdata <> ""
      parse var hlqdata site hlq.site hlqdata
   end

   tk_hlq  = KEYWD("USEHLQ")
   parse value tk_hlq hlq.node with hlq . /* default to prod         */
   parse value KEYWD("TRACE")  "O"    with   tv  .

   call LOCAL_PREINIT                  /* for more opts             -*/

return                                 /*@ TOOLKIT_INIT              */

