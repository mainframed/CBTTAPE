         PRINT NOGEN
***********************************************************************
*                                                                     *
* THIS IS THE CLEAN PROGRAM WHICH WILL DELETE OLD VSAM RECORDS. IT    *
* WILL ALSO SYNC. THE SYSOUT DATASETS/TAPES AND THE VSAM RECORDS.     *
*                                                                     *
*                                                                     *
* THE MAIN FUNCTION OF THIS PROGRAM IS TO DELETE OLD SYSOUT ENTRIES   *
* BASED ON A NUMBER OF DAYS THAT YOU SELECT TO KEEP REPORTS ON THE    *
* DISKWTR SYSTEM.                                                     *
*                                                                     *
* ANY VSAM RECORDS FOUND THAT POINTS TO A NON-EXISTING DAILY/ARCHIVE  *
* DATASET WILL BE DELETED.                                            *
* (THIS COULD HAPPEN IF YOU LOST A SYSOUT TAPE. JUST UNCATALOG THE    *
*  TAPE AND RUN THIS CLEAN PROGRAM. ANY VSAM RECORD POINTING TO THAT  *
*  TAPE WILL BE DELETED.                                              *
*                                                                     *
* IT WILL ALSO DETERMINE IF ANY DAILY DISK/TAPE AND ARCHIVE TAPE ARE  *
* EMPTY ANY WILL THEN DYNAMICALLY DELETE IT.                          *
*                                                                     *
* IF THE OPTIONAL "SPECLDEL" DD IS SUPPLIED, THE REQUESTED REPORTS    *
* WILL BE DELETED FROM THE JOURNAL FILE                               *
*   NOTE: THE SPECIAL DELETE DATASET SHOULD BE  LRECL=80              *
*         FORMAT OF THE FILE IS:                                      *
*                         1         2         3                       *
*          COL   123456789012345678901234567890                       *
*                JJJJJJJJ XXX#####             <== JOB# IS OPTIONAL)  *
*                   |      |   |-----JOB NUMBER                       *
*                   |      |---------JOB LIT  (IE: JOB STC TSU)       *
*                   |----------------JOB NAME (IE: PAYROLL            *
*                                                  PAY*  <= GENERIC)  *
*                                                                     *
* AN OPTIONAL REPORT ON LARGE SYSOUTS IS CREATED IF THE "LINES"       *
* PARAMETER IS SUPPLIED.                                              *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
* THE FOLLOWING EXPLAINS THE PARM THAT IS REQUIRED. TAKE NOTE THAT    *
* A "TEST" OPTION IS SUPPLIED IN WHICH THIS PROGRAM WILL ONLY         *
* REPORT ON ACTION THAT IT WOULD HAVE TAKEN IF IT WERE RUN.           *
*                                                                     *
*                                                                     *
*                                                                     *
*      PARM='CLEAN,XXXXX,YYY'                                         *
*            TEST    |    | - NUMBER OF LINES  *1000 ON WHICH THIS    *
*              |     |        PROGRAM WILL REPORT ON IF SYSOUT        *
*              |     |        REPORTS EXCEED IT. REPORT WILL CONTAIN  *
*              |     |        DATA FROM THE LAST 7 DAYS ONLY.         *
*              |     |                                                *
*              |     |------- NUMBER OF OF DAYS (MUST PUT IN LEADING  *
*              |              ZEROES; IE: 00014) THAT YOU WANT THE    *
*              |              VSAM JOURNAL RECORDS KEPT. ANY RECORDS  *
*              |              OLDER THAN THAT NUMBER ARE DELETED AND  *
*              |              THE EMPTY SYSOUT DATASETS ARE DELETED   *
*              |                                                      *
*              |------------- MUST BE CONSTANT "CLEAN"                *
*                             (SET TO "TEST" TO TEST CLEANUP;         *
*                              REPORTS PRODUCED - NOTHING DELETED)    *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
* THE FOLLOWING EXPLAINS THE PROCESSING THAT TAKES PLACE:             *
*                                                                     *
*  1) VERIFY PARM                                                     *
*  2) GET //PREFIX DD                                                 *
*  3) GET //OLDPFX DD  (IF ANY)                                       *
*  4) BUILD A DATASET ENTRY TABLE                                     *
*     A) BUILD ACTIVE DATASET ENTRY                                   *
*     B) BUILD A ENTRY FOR ALL DAILY AND ARCHIVE DATASETS WITH THE    *
*        PREFIX SUPPLIED IN THE  //PREFIX DD                          *
*     C) BUILD A ENTRY FOR ALL ARCHIVE DATASETS WITH THE PREFIX       *
*        SUPPLIED IN THE  //OLDPFX DD                                 *
*  5) OPEN THE VSAM JOURNAL                                           *
*  6) PROCESS THE VSAM JOURNAL SEQUENTIALLY; FOR EACH RECORD DO:      *
*     A) FIND ENTRY IN THE DATASET TABLE FOR THAT RECORD              *
*        1) IF FOUND,  ADD 1 TO COUNTER FOR THAT DATASET              *
*        2) IF NOT FOUND, DELETE THE VSAM RECORD                      *
*     B) COMPARE DATE OF RECORD TO DELETE DATE IN PARM                *
*        1) IF DATE IS OLDER, DELETE THE VSAM RECORD AND              *
*           SUBTRACT 1 FROM THE DATASET ENTRY TABLE.                  *
*        2) IF DATE IS GOOD - COMPARE AGAINST SPECIAL DELETE TABLE    *
*     C) IF SPECIAL DELETE DD CARD IS PRESENT, COMPARE THE JOBNAME    *
*        AND NUMBER; IF MATCH DELETE THE VSAM RECORD                  *
*     C) IF DELACT DD CARD IS PRESENT, DELETE ALL THE ACTIVE RECORDS  *
*        ON THE JOURNAL.                                              *
*     E) LOOK AT NUMBER OF LINES AND OPTIONALLY REPORT ON LARGE RPTS. *
*     F) PERFORM THIS PROCESS UNTIL EOF ON VSAM JOURNAL.              *
*  7) WALK THRU THE DATASET ENTRY TABLE.                              *
*     A) IF COUNTER IS ZERO, DYNAMICALLY DELETE THAT FILE. THIS WILL  *
*        HAPPEN WHEN ALL OF THE RECORDS ON THAT TAPE HAVE REACHED     *
*        THE NUMBER OF DAYS OLD SUPPLIED IN THE PARM.                 *
*     B) IF COUNTER IS NOT ZERO, THERE ARE STILL GOOD REPORTS ON THAT *
*        FILE. GO PROCESS THE NEXT ENTRY                              *
*                                                                     *
* NOTE: THE DATASET ENTRY TABLE CAN HANDLE  5000  DATASET ENTRIES.    *
*       IF YOU HAVE MORE, INCREASE THE GETMAINED AREA SIZE.           *
*                                                                     *
***********************************************************************
*        REGISTERS USED:                                              *
*         R0 = WORK REG                                               *
*         R1 = WORK REG                                               *
*         R2 = WORK REG - READING PARM                                *
*         R3 = WORK REG - READING PARM                                *
*         R4 = WORK REG                                               *
*         R5 = WORK REG                                               *
*         R6 = USED FOR LOCINDEX                                      *
*         R7 = -------- UNUSED                                        *
*         R8 = -------- UNUSED                                        *
*         R9 = -------- UNUSED                                        *
*        R10 = DATASET TABLE POINTER                                  *
*        R11 = BASE REG                                               *
*        R12 = BASE REG                                               *
*        R13 = SAVE AREA                                              *
*        R14 = WORK REG                                               *
*        R15 = WORK REG                                               *
*                                                                     *
*                                                                     *
* ABEND  REASONS:                                                     *
*                                                                     *
*        U0001 - INVALID PARM (CLEAN OR TEST)                         *
*        U0002 - INVALID PARM (DAYS OLD)                              *
*        U0003 - SOUTDATE PROGRAM MISSING                             *
*        U0004 - INVALID PARM (LARGE REPORT SIZE)                     *
*        U0005 - NO PREFIX DD CARD                                    *
*        U0006 - LOCINDEX PROGRAM MISSING                             *
*        U0007 - LOCINDEX CATALOG ERROR                               *
*        U0008 - VSAM JOURNAL OPEN ERROR                              *
*        U0009 - DELETE RECORD REPORT OPEN FAILURE                    *
*        U0010 - DELETE DATASET REPORT OPEN FAILURE                   *
*        U0011 - LARGE SYSOUT REPORT OPEN FAILURE                     *
*        U0012 - READ VSAM JOURNAL ERROR                              *
*        U0013 - ERASE VSAM JOURNAL RECORD ERROR                      *
*        U0014 - OPEN FAILED FOR SPECLDEL DATASET                     *
*        U0015 - MORE THAN 99 ENTRIES IN SPECLDEL DATASET             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        INITIALIZATION AND PARM ISOLATION                            *
*                                                                     *
***********************************************************************
SOUTCLN  $PROLOG R12,R11       SPECIFY BASE REGISTERS
         L     R2,0(R1)        GET PARM POINTER
         LH    R3,0(R2)        GET PARM SIZE
         LTR   R3,R3           ANY PARM?
         BZ    ABEND1          NO, ERROR
         CH    R3,=H'10'       MIN SIZE OF PARM
         BL    ABEND1          NOT ENOUGH - ERROR
         LA    R2,2(R2)        BUMP PAST LENGTH
         CLC   0(6,R2),=C'CLEAN,' CLEAN FILE REQUESTED
         BE    AAA010           OK
         CLC   0(5,R2),=C'TEST,' ONLY TESTING
         BNE   ABEND1           NO - ERROR
         OI    FLAG,TESTING    SET TESTING ON
         MVC   HEADDEL1+45(24),TESTIT        INFORM
         MVC   HEADDEL2+45(24),TESTIT      OF TESTING
         LA    R2,5(R2)        BUMP PAST CLEAN,
         SH    R3,=H'5'        DECREASE LENGTH OF PARM
         B     ENTRY020        CHECK NEXT PARM
AAA010   LA    R2,6(R2)        BUMP PAST CLEAN,
         SH    R3,=H'6'        DECREASE LENGTH OF PARM
         EJECT
* ******************************************************************
*       GET AND VERIFY NUMBERS OF DAYS OLD FOR DELETION
* ******************************************************************
ENTRY020 EQU   *
         MVC   DAYOLD,0(R2)    MOVE IN DAYS OLD PARM AREA
         TM    DAYOLD,X'F0'    IS IT NUMERIC
         BNO   ABEND2          NO- ERROR
         TM    DAYOLD+1,X'F0'  IS IT NUMERIC
         BNO   ABEND2          NO- ERROR
         TM    DAYOLD+2,X'F0'  IS IT NUMERIC
         BNO   ABEND2          NO- ERROR
         TM    DAYOLD+3,X'F0'  IS IT NUMERIC
         BNO   ABEND2          NO- ERROR
         TM    DAYOLD+4,X'F0'  IS IT NUMERIC
         BNO   ABEND2          NO- ERROR
* GET BACKUP DATE ROUTINE
         BLDL  0,BLDLDATE          ISSUE BLDL
         LTR   R15,R15             DOES IT EXIST
         BNZ   ABEND3              ERROR - NEED ROUTINE
         LOAD  EP=SOUTDATE         LOAD ROUTINE
         ST    R0,DATEADDR        SAVE ADDRESS
         ICM   R15,15,DATEADDR    SET R15 TO DATE PGM ADDR
         LA    R1,DATEPARM        SET R1 TO PARM
         BALR  R14,R15            GET BACKUP DATE
         LTR   R15,R15            TEST RC
         BNZ   ABEND3             NO ZERO - ERROR
         MVC   HOLDDATE(4),DATELMT+6 UNFORMAT
         MVC   HOLDDATE+4(2),DATELMT       DATE
         MVC   HOLDDATE+6(2),DATELMT+3        FOR
         PACK  PACK5,HOLDDATE                   PACKING
         ZAP   DELDATE,=P'099999999' SET DATE TO HIGH
         SP    DELDATE,PACK5   SUBTRACT DATE FROM IT
         SH    R3,=H'5'        SUBTRACT DAYS FROM LENGTH
         CH    R3,=H'1'        ANY PARM LEFT?
         BL    ENTRY030        NO - START PROCESSING
         LA    R2,5(R2)        BUMP PAST DAY PARM
         CLI   0(R2),C','      IS THERE COMA
         BNE   ABEND4          NO -ERROR
         EJECT
* *********************************************************
*       DOES USER WANT REPORT BY LARGE REPORTS
* *********************************************************
         LA    R2,1(R2)        BUMP PAST COMA
         SH    R3,=H'1'        SUBTRACT COMA FROM LENGTH
         CH    R3,=H'1'        ANY PARM LEFT?
         BL    ABEND4          NO - ERROR
         OI    FLAG,RPTBIG     SET TO REPORT ON LARGE FILES
         MVC   RPTLIM+1(3),0(R2) MOVE IN SIZE (X-1000)
         TM    RPTLIM+1,X'F0'  IS IT NUMERIC
         BNO   ABEND4          NO- ERROR
         TM    RPTLIM+2,X'F0'  IS IT NUMERIC
         BNO   ABEND4          NO- ERROR
         TM    RPTLIM+3,X'F0'  IS IT NUMERIC
         BNO   ABEND4          NO- ERROR
         PACK  MAXRPT,RPTLIM   PACK LIMIT
         MVC   DAYOLD,=C'00007' REPORT ON LAST WEEK ONLY
         LA    R1,DATEPARM     SET R1 TO PARM AREA
         ICM   R15,15,DATEADDR    SET R15 TO DATE PGM ADDR
         BALR  R14,R15            GET BACKUP DATE
         LTR   R15,R15            TEST RC
         BNZ   ABEND3             NO ZERO - ERROR
         MVC   HOLDDATE(4),DATELMT+6   UNFORMAT
         MVC   HOLDDATE+4(2),DATELMT       DATE
         MVC   HOLDDATE+6(2),DATELMT+3         FOR
         PACK  PACK5,HOLDDATE                     PACKING
         ZAP   RPTDATE,=P'099999999' SET DATE TO HIGH
         SP    RPTDATE,PACK5   SUBTRACT DATE FROM IT
         EJECT
***********************************************************************
*        GET PREFIX AND OLDPFX JFCB'S TO GET PREFIX FOR DSNS          *
***********************************************************************
ENTRY030 L     R3,16               GET CVT ADDRESS
         L     R3,0(R3)            GET OLD/NEW TCB POINTERS
         L     R3,4(R3)            GET OUR TCB ADDRESS
         L     R3,12(R3)           GET OUR TIOT ADDRESS
         LA    R2,24               SIZE OF TIOT HEADER
AAA030   AR    R3,R2               BUMP TO NEXT ENTRY
         CLC   4(6,R3),=CL6'PREFIX' PREFIX DATASET?
         BE    BBB030              YES, GET DSN
         CLC   4(6,R3),=CL6'OLDPFX' OLD PREFIX (REBUILD OPTION ONLY)
         BE    EEE030              YES, OK
         CLC   4(8,R3),=CL8'SPECLDEL' SPECIAL DELETE REPORTS
         BE    HHH030              YES, OK
         CLC   4(8,R3),=CL8'DELACT' DEL ACTIVE REPORTS
         BE    III030              YES, OK
         B     OOO030              GET NEXT
BBB030   RDJFCB PREFIX               GET JFCB
         MVC   PREFIX1,JFCB        MOVE IN PREFIX
         LA    R4,PREFIX1         SET R4 TO BEGINNING
         LA    R4,20(R4)          SET R4 TO END
         LA    R5,21              SET R5 TO MAX LENGTH
CCC030   CLI   0(R4),C' '         FIND BLANK
         BNE   DDD030             NO -FOUND END
         SH    R4,=H'1'           DECREMENT
         BCT   R5,CCC030          TRY AGAIN
DDD030   ST    R5,PREFIX1L        SAVE LENGTH OF PREFIX
         B     OOO030             TRY FOR NEXT DD
EEE030   RDJFCB OLDPFX            GET OLD JFCB
         MVC   PREFIX2,JFCB        MOVE IN PREFIX
         LA    R4,PREFIX2         SET R4 TO BEGINNING
         LA    R4,24(R4)          SET R4 TO END
         LA    R5,25              SET R5 TO MAX LENGTH
FFF030   CLI   0(R4),C' '         FIND BLANK
         BNE   GGG030             NO -FOUND END
         SH    R4,=H'1'           DECREMENT
         BCT   R5,FFF030          TRY AGAIN
GGG030   ST    R5,PREFIX2L        SAVE LENGTH OF PREFIX
         B     OOO030             TRY FOR NEXT DD
HHH030   OI    FLAG,SPECDEL       DELETE REPORTS ON CLEAN
         B     OOO030             TRY FOR NEXT DD
III030   OI    FLAG,DELACT        DELETE ANY RECORD ON ACTIVE
         B     OOO030             TRY FOR NEXT DD
OOO030   ICM   R2,1,0(R3)          SIZE OF ENTRY
         BZ    XXX030              0 - DONE
         B     AAA030              GET NEXT
XXX030   CLI   PREFIX1,C' '        IS THERE A PREFIX
         BE    ABEND5              ERROR
EXIT030  EQU   *
         EJECT
* *********************************************************
*       BUILD TABLE OF ALL SYSOUT DATASETS IN THE SYSTEM
* *********************************************************
ENTRY040 DS    0H
         GETMAIN R,LV=60000    GETMAIN FOR MAX TABLE (5000 ENTRIES)
         ST    R1,TBLAREA      SAVE ADDRESS
         LR    R0,R1           SET R0 = R1
         LR    R10,R1          SET R10= R1
         SR    R1,R1           ZERO R1
         A     R1,=F'60000'    SET R1 TO LENGTH
         SR    R14,R14         CLEAR R14 FOR MVCL
         SR    R15,R15         CLEAR R15 FORMVCL
         MVCL  R0,R14          SET AREA TO X'00'
*                              SET UP RECORD FOR ACTIVE FILE
         MVC   0(12,R10),=X'C199999999999FC400000000'
         LA    R10,12(R10)     BUMP TO NEXT RECORD
*
*  CALL LOCINDEX TO GET ALL THE DAILY DATASET NAMES
*
         MVC   PASSTYPE,=CL8'.DAILY   '
         MVC   PASSPFX(21),PREFIX1 MOVE PREFIX NAME
         MVC   PASSLEN,PREFIX1L    MOVE PREFIX NAME LENGTH
         MVI   DRO,C'D'            SET TYPE TO DAILY
         BAL   R14,ENTRY050        GET ALL DAILY DATASETS IF ANY?
*
*  CALL LOCINDEX TO GET ALL THE ARCHIVE DATASET NAMES
*
         MVC   PASSTYPE,=CL8'.ARCHIVE '
         MVC   PASSPFX(21),PREFIX1 MOVE PREFIX NAME
         MVC   PASSLEN,PREFIX1L    MOVE PREFIX NAME LENGTH
         MVI   DRO,C'R'            SET TYPE TO ARCHIVE
         BAL   R14,ENTRY050        GET ALL ARCHIVE DATASETS IF ANY?
*
*  CALL LOCINDEX TO GET ALL THE OLD ARCHIVE DATASET NAMES
*
         CLI   PREFIX2,C' '        IS THERE AN OLD PREFIX
         BE    EXIT040             NO -GET OUT
         MVC   PASSTYPE,=CL8'.ARCHIVE ' #####  OLD=ARCHIVE
*        MVC   PASSTYPE,=CL8'.DAILY   ' #####  OLD=DAILY
         MVC   PASSPFX,PREFIX2     MOVE PREFIX NAME
         MVC   PASSLEN,PREFIX2L    MOVE PREFIX NAME LENGTH
         MVI   DRO,C'O'            SET TYPE TO OLD
         BAL   R14,ENTRY050        GET ALL OLD ARCH DATASETS IF ANY?
EXIT040  EQU   *
         B     ENTRY060            PROCESS DELETE DATASET
         EJECT
***********************************************************************
*        CALL TO LOCINDEX ROUTINE TO LIST ALL DSNS WITH PREFIX        *
***********************************************************************
ENTRY050 DS    0H
         ST    R14,SAVE050
         ICM   R6,15,SAVEWORK      RESTORE WORK ADDRESS
         BNZ   AAA050              HAVE ALREADY - BR AROUND GETMAIN
* GET LOCINDEX ROUTINE
         BLDL  0,BLDLLIST          ISSUE BLDL
         LTR   R15,R15             DOES IT EXIST
         BNZ   ABEND6              ERROR - NEED ROUTINE
         LOAD  EP=LOCINDEX         LOAD ROUTINE
         ST    R0,LOCIADDR        SAVE ADDRESS
* GETMAIN FOR LOCINDEX WORK AREA
         GETMAIN R,LV=4096
         LR    R6,R1               SAVE ADDR OF WORK AREA
         ST    R6,SAVEWORK         SAVE ADDR OF WORKAREA
         USING WORKSPC,R6          INFORM ASSEMBLER
AAA050   LA    R0,LOCINDXW         POINT TO LOCINDEX WORK AREA
         ST    R0,LOCIPARM+12      SAVE ADDR IN PARM LIST
         MVC   LOCIPFX(25),PASSPFX MOVE IN PREFIX
         LA    R4,LOCIPFX          SET R4 TO ADDR OF PREFIX
         L     R5,PASSLEN          SET R5 TO LENGTH
         LA    R4,0(R5,R4)         BUMP R4 TO END OF PREFIX
         MVC   0(8,R4),PASSTYPE    ADD TYPE DAY/ARCH/ACTIVE
         MVI   LOCIPARM+12,X'80'   INDICATE END OF PARM LIST
BBB050   ICM   R15,15,LOCIADDR     POINT TO PARM LIST
         LA    R1,LOCIPARM         POINT TO PARM LIST
         BALR  R14,R15             GO TO LOCINDEX
         B     *+4(R15)            BRANCH , DEP ON RETURN CODE
         B     CCC050              HAVE A DASD DSN
         B     EXIT050             INDEX STRUCTURE NOT FOUND
         B     DDD050              DSN ON TAPE
         B     CCC050              MULTI VOLUME DSN
         B     EXIT050             DONE
         B     ABEND7              CATALOG ERROR
CCC050   MVC   7(5,R10),=X'C400000000' SET TO DISK AND INIT ZERO
         B     EEE050              SAVE DSN AND TRY AGAIN
DDD050   MVC   7(5,R10),=X'E300000000' SET TO TAPE AND INIT ZERO
EEE050   LA    R4,LOCIDSN          SET R4 TO BEGINNING
         LA    R4,43(R4)        SET TO END
         LA    R5,44            MAX DSN = 44
FFF050   CLC   0(2,R4),=C'.D'      DATE STAMP
         BE    GGG050           YES - DONE
         CLC   0(2,R4),=C'.G'      GENERATION NUMBER (OLD SYSTEM)
         BE    HHH050           YES - DONE
         SH    R4,=H'1'         BACK UP ONE
         BCT   R5,FFF050        TRY AGAIN
         B     BBB050           INVALID DSN - GET NEXT
GGG050   MVC   HOLDSTP(7),2(R4)  MOVE IN DATE STAMP
         MVC   HOLDSTP+7(4),11(R4)  MOVE IN TIME STAMP
         B     III050           BRANCH
HHH050   MVC   HOLDSTP(5),=C'99999' SET UP HIGH
         MVC   HOLDSTP+5(4),2(R4) MOVE IN GDG
         MVC   HOLDSTP+9(2),7(R4) MOVE IN VERSION
III050   PACK  HSTAMP,HOLDSTP   PACK IT
         MVC   0(1,R10),DRO     MOVE IN DATASET TYPE
         MVC   1(6,R10),HSTAMP  MOVE IN STAMP
         LA    R10,12(R10)      BUMP TO NEXT ENTRY
         B     BBB050           TRY FOR NEXT
EXIT050  L     R14,SAVE050
         BR    R14
         DROP  R6
         EJECT
* ********************************************************************
*   GET THE SPECIAL DELETE DATASET IF ANY
* ********************************************************************
ENTRY060 DS    0H
         TM    FLAG,SPECDEL        DELETE REPORTS  WANTED
         BZ    EXIT060             NO BYPASS OPEN
         GETMAIN R,LV=2000
         LR    R6,R1               SAVE ADDR OF DELETE AREA
         ST    R6,SAVESPEC         SAVE ADDR OF DELETE AREA
         LR    R0,R1               SET R0 TO BEGINNING OF GETMAIN
         LA    R1,2000             SET R1 TO LENGTH
         XR    R14,R14             CLEAR R14
         XR    R15,R15             CLEAR R15
         MVCL  R0,R14              SET GETMAINED TO ALL 00
         USING SPECAREA,R6         INFORM ASSEMBLER
         OPEN  (SPECLDEL,(INPUT))  OPEN DELETE DSN
         TM    SPECLDEL+48,X'10'   DID IT OPEN
         BZ    ABEND14             NO - ERROR
         LA    R7,99               99  RECORDS MAXIMUM
AAA060   GET   SPECLDEL,DELAREA    GET DELETE RECORD
         LA    R2,DELAREA          SET R2 TO JOBNAME
         LA    R3,SPECNAME         SET R3 TO SPECIAL NAME
         LA    R4,8                SET TO MAX LENGTH
         XR    R5,R5               SET TO ZERO
BBB060   CLI   0(R2),C'*'          GENERIC NAME
         BE    CCC060              YES - DONE
         MVC   0(1,R3),0(R2)       MOVE IN 1 CHARACTER
         LA    R2,1(R2)            BUMP TO NEXT CHAR
         LA    R3,1(R3)            BUMP TO NEXT CHAR
         LA    R5,1(R5)            BUMP LENGTH COUNTER
         BCT   R4,BBB060           MOVE NEXT CHARACTER
CCC060   SH    R5,=H'1'            DECREMENT FOR COMPARE
         ST    R5,SPECLGTH         SAVE LENGTH OF JOB NAME
         MVC   SPECNUM,DELJNUM     SAVE JOB NUMBER IF ANY
         LA    R6,20(R6)           BUMP TO NEXT ENTRY
         BCT   R7,AAA060           READ NEXT IF LESS THAN 100
         B     ABEND15             MORE THAN 99 ENTRIES - ERROR
XXX060   EQU   *
         CLOSE SPECLDEL
EXIT060  EQU   *
         DROP  R6
         EJECT
* ********************************************************************
*   PROCESS THE VSAM FILE FOR CLEAN AND DELETE OF DATASET ENTRIES
* ********************************************************************
ENTRY100 DS    0H
*
         OPEN  JOURNACB         OPEN VSAM JOURNAL
         LTR   R15,R15          OPEN OK?
         BNZ   ABEND8           NO - ERROR
AAA100   EQU   *
         OPEN  (DELREC,(OUTPUT)) OPEN DELETE REPORT FILE
         TM    DELREC+48,X'10' CHECK IF IT OPENED
         BZ    ABEND9          NO, OUTPUT FAILED TO OPEN
         PUT   DELREC,HEADDEL1 PUT OUT HEADER
         PUT   DELREC,HEADING  PUT OUT HEADINGS
         PUT   DELREC,BLANKS   PUT OUT HEADINGS
         ZAP   LINECNT1,=P'3'  SET COUNTER TO 3
*
         OPEN  (DELDSN,(OUTPUT)) OPEN DELETE REPORT FILE
         TM    DELDSN+48,X'10' CHECK IF IT OPENED
         BZ    ABEND10         NO, OUTPUT FAILED TO OPEN
         PUT   DELDSN,HEADDEL2 PUT OUT HEADER
         PUT   DELDSN,HEADDEL3 PUT OUT HEADER
         PUT   DELDSN,BLANKS   PUT OUT HEADINGS
         ZAP   LINECNT2,=P'3'  SET COUNTER TO 3
*
         TM    FLAG,RPTBIG      BIG REPORT REQUESTED
         BZ    BBB100           NO BR AROUND
         OPEN  (BIGRPT,(OUTPUT)) OPEN DELETE REPORT FILE
         TM    BIGRPT+48,X'10' CHECK IF IT OPENED
         BZ    ABEND11         NO, OUTPUT FAILED TO OPEN
         PUT   BIGRPT,HEADBIG  PUT OUT HEADER
         PUT   BIGRPT,HEADING  PUT OUT HEADINGS
         PUT   BIGRPT,BLANKS   PUT OUT HEADINGS
         ZAP   BIGCNT,=P'3'    SET COUNTER TO 3
         EJECT
* *********************************************************
*        READ JOURNAL RECORDS SEQUENTIALLY AND PROCESS    *
* *********************************************************
BBB100   GET   RPL=JOURNRPL
         LTR   R15,R15
         BNZ   EXIT100
*  CHECK RECORD AGAINST TABLE
         L     R10,TBLAREA     RESET R10 TO BEGIN OF TABLE
CCC100   CLI   0(R10),X'00'    END OF TABLE REACHED
         BNE   CCC101          NO - CHECK SUFFIX
*                              DAILY/ARCH DATASET NOT THERE
         MVC   OUTRESON,REASON#1
         BAL   R14,ENTRY600    GO REPORT ON IT
         BAL   R14,ENTRY750    GO DELETE VSAM RECORD
         B     BBB100          READ NEXT RECORD
CCC101   CLC   JSUFFIX,0(R10)  VSAM REC MATCHES TABLE??
         BE    DDD100          YES - ADD TO COUNTER
         LA    R10,12(R10)     BUMP R10
         B     CCC100          GO TRY AGAIN
* *********************************************************
*       ADD ONE TO DATASET ENTRIES
* *********************************************************
DDD100   L     R9,8(R10)       SET R9 TO CURRENT COUNT
         A     R9,=F'1'        BUMP BY ONE
         ST    R9,8(R10)       STORE IT BACK
* *********************************************************
*       DOES JOURNAL DATE MATCH DELETE DATE REQUESTED
* *********************************************************
         CP    DELDATE,JDATE   COMPARE JOURN DATE TO DEL DATE
         BH    FFF100          GO CHECK DELETE RPT; IF ANY
EEE100   MVC   OUTRESON,REASON#2
         BAL   R14,ENTRY700    DELETE RECORD
         B     BBB100          GET NEXT RECORD - DO NOT REPORT
FFF100   TM    FLAG,SPECDEL    SPECIAL DELETE PROCESSING
         BZ    LLL100          NO - BYPASS TABLE COMPARE
*
         USING SPECAREA,R15
         L     R15,SAVESPEC    SET TO DELETE GETMAINED AREA
GGG100   CLI   SPECNAME,X'00'  END OF NAME LIST
         BE    LLL100          YES - NO MATCH
         L     R14,SPECLGTH    SET R14 TO LENGTH OF COMPARE
         EX    R14,SPECOMPR    COMPARE JOBNAME
         BNE   III100          NO MATCH - TRY NEXT
         CLI   SPECNUM,C' '    JOB NUMBER COMPARE ??
         BE    HHH100          NO - DELETE IT
         CLC   SPECNUM(3),JJOB#+5 JOB LIT MATCHES
         BNE   III100          NO - GET NEXT
         CLC   SPECNUM+3(5),JJOB# JOB NUMBER MATCHES
         BNE   III100          NO - GET NEXT
HHH100   MVC   OUTRESON,REASON#3
         BAL   R14,ENTRY700    DELETE RECORD
         B     BBB100          GET NEXT RECORD - DO NOT REPORT
III100   LA    R15,20(R15)     BUMP TO NEXT ENTRY
         B     GGG100          TRY NEXT COMPARE
*
SPECOMPR CLC   JJOBN(0),SPECNAME COMPARE FOR DELETE JOBNAME
         DROP  R15
* *********************************************************
*       DELETE ALL JOURNAL RECORDS FOR THE ACTIVE FILE ???
* *********************************************************
LLL100   TM    FLAG,DELACT
         BZ    MMM100
         CLI   JADR,C'A'       ACTIVE RECORD
         BNE   MMM100
         MVC   OUTRESON,REASON#4
         BAL   R14,ENTRY700    DELETE RECORD
         B     BBB100          GET NEXT RECORD - DO NOT REPORT
* *********************************************************
*       DOES JOURNAL RECORD EXCEED MAX LINES; REPORT ON IT
* *********************************************************
MMM100   EQU   *
         TM    FLAG,RPTBIG     DOES USER WANT REPORTS
         BZ    BBB100          NO - BYPASS REPORTING
         CP    RPTDATE,JDATE   COMPARE JOURN DATE TO REPORT DATE
         BNH   BBB100          OLDER THAN 7 DAYS - BYPASS
         CP    MAXRPT,JLINES   LARGE REPORT
         BH    BBB100          NO - GET NEXT
         BAL   R14,ENTRY400    PRINT IT
         B     BBB100          GET NEXT RECORD
EXIT100  EQU   *
         C     R15,=F'8'       END OF FILE
         BNE   ABEND12         NO - MAJOR VSAM ERROR
         EJECT
* *********************************************************
*       END OF FILE PROCESSING
*         1) DELETE DATASETS IF EMPTY
*         2) REPORT ON DELETED DATASETS
*         3) CLOSE FILES AND END
* *********************************************************
ENTRY200 DS    0H
         L     R10,TBLAREA      RESTORE TABLE
*        LA    R10,12(R10)      BYPASS ACTIVE RECORD
AAA200   CLI   0(R10),X'00'     ANY TABLE ENTRIES LEFT
         BE    EEE200           NO - DONE
*        L     R7,8(R10)        SET R7 TO COUNTER
*        C     R7,=F'0'         ANY ENTRIES LEFT ON DSN
*        BH    DDD200           YES - DONT DELETE
         CLI   0(R10),C'O'      IS IT OLD SYSTEM
         BE    BBB200           YES - MOVE IN OLD PREFIX
         MVC   HOLDPREF(21),PREFIX1 MOVE PREFIX
         MVC   HOLDLEN,PREFIX1L    MOVE PREFIX LENGTH
         B     CCC200
BBB200   MVC   HOLDPREF,PREFIX2    MOVE PREFIX
         MVC   HOLDLEN,PREFIX2L    MOVE PREFIX LENGTH
CCC200   MVC   HOLDSUF,0(R10)      MOVE SUFFIX
         MVC   HOLDTYPE,7(R10)     MOVE IN TYPE(DISK/TAPE)
         BAL   R14,ENTRY800     GO DELETE IT
DDD200   LA    R10,12(R10)      BUMP R10 TO NEXT ENTRY
         B     AAA200           TRY AGAIN
EEE200   CLOSE DELREC           CLOSE THE OUTPUT FILES
         CLOSE DELDSN           CLOSE THE OUTPUT FILES
         TM    FLAG,RPTBIG      REPORTING ON LARGE?
         BZ    FFF200           NO - BR AROUND
         CLOSE BIGRPT           CLOSE THE OUTPUT FILES
FFF200   CLOSE JOURNACB         CLOSE THE JOURNAL VSAM
         ICM   R1,15,TBLAREA
         FREEMAIN R,LV=60000,A=(1)
         ICM   R1,15,SAVEWORK
         FREEMAIN R,LV=4096,A=(1)
         ICM   R1,15,SAVESPEC  SPECIAL DELETE AREA
         BZ    EXIT200         NON - GET OUT
         FREEMAIN R,LV=2000,A=(1)
EXIT200  SR    R15,R15         CLEAR RETCODE
         $EPILOG ,             RETURN TO CALLER
         EJECT
***********************************************************************
*        ABEND ROUTINES
***********************************************************************
ABEND1   WTO   'SOUTCLN - INVALID PARM (CLEAN OR TEST)'
         ABEND 1
ABEND2   WTO   'SOUTCLN - INVALID PARM (DAYS OLD)'
         ABEND 2
ABEND3   WTO   'SOUTCLN - SOUTDATE PROGRAM MISSING'
         ABEND 3
ABEND4   WTO   'SOUTCLN - INVALID PARM (LARGE REPORT SIZE)'
         ABEND 4
ABEND5   WTO   'SOUTCLN - NO PREFIX DD CARD'
         ABEND 5
ABEND6   WTO   'SOUTCLN - LOCINDEX PROGRAM MISSING'
         ABEND 6
ABEND7   WTO   'SOUTCLN - LOCINDEX CATALOG ERROR'
         ABEND 7
ABEND8   WTO   'SOUTCLN - VSAM JOURNAL OPEN ERROR'
         ABEND 8
ABEND9   WTO   'SOUTCLN - DELETE RECORD REPORT OPEN FAILURE'
         ABEND 9
ABEND10  WTO   'SOUTCLN - DELETE DATASET REPORT OPEN FAILURE'
         ABEND 10
ABEND11  WTO   'SOUTCLN - LARGE SYSOUT REPORT OPEN FAILURE'
         ABEND 11
ABEND12  WTO   'SOUTCLN - READ VSAM JOURNAL ERROR'
         ABEND 12
ABEND13  WTO   'SOUTCLN - ERASE VSAM JOURNAL RECORD ERROR'
         ABEND 13
ABEND14  WTO   'SOUTCLN - SPECIAL DELETE DATASET OPEN FAILURE'
         ABEND 14
ABEND15  WTO   'SOUTCLN - SPECIAL DELETE DATASET - 99 MAX REACHED'
         ABEND 15
         EJECT
* *********************************************************
*       PRINT REPORT ON LARGE SYSOUT REPORTS ON DISKWTR
* *********************************************************
ENTRY400 DS    0H
         ST    R14,SAVE400
         BAL   R14,ENTRY500   EXPAND RECORD FOR REPORTING
         MVC   OUTRESON,BLANKS CLEAR REASON FIELD
         PUT   BIGRPT,OUTREC  WRITE IT OUT
         AP    BIGCNT,=P'1'   ADD 1 TO LINE COUNTER
         CP    BIGCNT,=P'59'  PAST 59 LINES
         BL    EXIT400        NO -EXIT
         ZAP   BIGCNT,=P'3'   SET LINES TO 3
         PUT   BIGRPT,HEADBIG  PUT OUT HEADER
         PUT   BIGRPT,HEADING  PUT OUT HEADINGS
         PUT   BIGRPT,BLANKS   PUT OUT HEADINGS
EXIT400  L     R14,SAVE400
         BR    R14
         EJECT
* *********************************************************
*       FORMAT JOURNAL RECORD FOR REPORTING
* *********************************************************
ENTRY500 DS    0H
         ST    R14,SAVE500
         MVC   OUTJN,JJOBN          MOVE JOBNAME
         MVC   OUTJ#(3),JJOB#+5     MOVE JOB TYPE
         MVC   OUTJ#+3(5),JJOB#     MOVE JOB TYPE
         ZAP   PACK5,=P'099999999'  SET UP TO HIGH VALUE
         SP    PACK5,JDATE          REVERSE DATE
         MVC   OUTDT,DATEPAT        MOVE IN DATE PATTERN
         EDMK  OUTDT,PACK5          MOVE IN DATE
         ZAP   PACK4,=P'0999999'    SET UP TO HIGH VALUE
         SP    PACK4,JTIME          REVERSE TIME
         MVC   OUTTM,TIMEPAT        MOVE IN TIME PATTERN
         EDMK  OUTTM,PACK4          MOVE IN TIME
         MVC   OUTLINE,LINEPAT      MOVE IN LINE PATTERN
         EDMK  OUTLINE,JLINES       MOVE IN LINE
         MVI   OUTDSN,C' '          CLEAR
         MVC   OUTDSN+1(43),OUTDSN    OUT AREA
         MVC   OUTDSN(21),PREFIX1   MOVE IN PREFIX
         LA    R4,OUTDSN            SET R4 TO BEGINNING
         L     R5,PREFIX1L           SET R5 TO LENGTH
         LA    R4,0(R5,R4)          BUMP TO END
         CLI   JADR,C'A'            IS IT ACTIVE
         BE    AAA500
         CLI   JADR,C'D'            IS IT DAILY
         BE    BBB500
         CLI   JADR,C'R'            IS IT ARCHIVE
         BE    CCC500
         MVC   OUTDSN(25),PREFIX2   MOVE IN OLD PREFIX
         LA    R4,OUTDSN            SET R4 TO BEGINNING
         L     R5,PREFIX2L          SET R5 TO LENGTH
         LA    R4,0(R5,R4)          BUMP TO END
         MVC   0(8,R4),=C'.ARCHIVE' #####  OLD=ARCHIVE
         LA    R8,8(R4)             #####  BUMP 6 BYTES
*        MVC   0(6,R4),=C'.DAILY'   #####  OLD=DAILY
*        LA    R4,6(R4)             #####  BUMP 8 BYTES
         B     DDD500
AAA500   MVC   0(7,R4),=C'.ACTIVE'  EXPAND CODE
         LA    R4,7(R4)             BUMP R4
         B     DDD500
BBB500   MVC   0(6,R4),=C'.DAILY'  EXPAND CODE
         LA    R4,6(R4)             BUMP R4
         B     DDD500
CCC500   MVC   0(8,R4),=C'.ARCHIVE' EXPAND CODE
         LA    R4,8(R4)             BUMP R4
DDD500   MVC   DSPLYSTP,STPATRN     MOVE IN PATTERN
         EDMK  DSPLYSTP,JSTP        EXPAND IT
         CLC   DSPLYSTP,HIGH9       IS IT ACTIVE
         BE    EXIT500              YES - NO SUFFIX
         CLC   DSPLYSTP(6),HIGH9    IS IT GDG
         BE    EEE500               YES - EXPAND GDG
         MVC   0(2,R4),=C'.D'       MOVE IN "D"
         MVC   2(7,R4),DSPLYSTP+1   MOVE IN DATE
         MVC   9(2,R4),=C'.T'       MOVE IN ".T"
         MVC   11(4,R4),DSPLYSTP+8  MOVE IN TIME
         B     EXIT500              DONE
EEE500   MVC   0(2,R4),=C'.G'       MOVE IN "G"
         MVC   2(4,R4),DSPLYSTP+6   MOVE IN GDG
         MVC   6(1,R4),=C'V'        MOVE IN "V"
         MVC   7(2,R4),DSPLYSTP+10  MOVE IN VERSION
EXIT500  L     R14,SAVE500
         BR    R14
         EJECT
* *********************************************************
*       WRITE RECORD THAT WAS DELETED
* *********************************************************
ENTRY600 DS    0H
         ST    R14,SAVE600
         BAL   R14,ENTRY500    GO EXPAND RECORD FOR REPORTING
         PUT   DELREC,OUTREC   WRITE IT IN REPORT
         AP    LINECNT1,=P'1' ADD 1 TO LINE COUNTER
         CP    LINECNT1,=P'59' PAST 59 PER PAGE
         BL    EXIT600         NO - GO TO END
         ZAP   LINECNT1,=P'3'  RESET TO 3
         PUT   DELREC,HEADDEL1 PUT OUT HEADER
         PUT   DELREC,HEADING  PUT OUT HEADINGS
         PUT   DELREC,BLANKS   PUT OUT HEADINGS
EXIT600  L     R14,SAVE600 RESTORE RETURN
         BR    R14
         EJECT
* *********************************************************
*       DELETE VSAM RECORD AND DECREMENT RECORD COUNT
*       IF USING "CLEAN" OPTION
* *********************************************************
ENTRY700 DS    0H
         ST    R14,SAVE700
         L     R9,8(R10)       SET R9 TO CURRENT COUNT
         S     R9,=F'1'        SUBTRACT ONE
         ST    R9,8(R10)       STORE IT BACK
         BAL   R14,ENTRY600    GO REPORT ON IT
         BAL   R14,ENTRY750    ERASE RECORD
EXIT700  L     R14,SAVE700     RESTORE RETURN ADDR
         BR    R14             GO THERE
         EJECT
* *********************************************************
*       DELETE VSAM RECORD
* *********************************************************
ENTRY750 DS    0H
         ST    R14,SAVE750
         TM    FLAG,TESTING    IN TEST MODE
         BO    EXIT750         BR AROUND ERASE
         ERASE RPL=JOURNRPL    ERASE RECORD
         LTR   R15,R15         ERASE OK?
         BNZ   ABEND13         NO - MAJOR ERROR
EXIT750  L     R14,SAVE750     RESTORE RETURN ADDR
         BR    R14             GO THERE
         EJECT
* *********************************************************
*       EXPAND  THE DATASET NAME FROM THE JOURNAL AND
*       PREFIX PASSED
* *********************************************************
ENTRY800 DS    0H
         ST    R14,SAVE800
         MVI   HOLDDSN,C' '         CLEAR HOLDDSN
         MVC   HOLDDSN+1(43),HOLDDSN CLEAR HOLDDSN
         MVC   HOLDDSN(25),HOLDPREF MOVE IN PREFIX
         LA    R4,HOLDDSN           SET R4 TO BEGINING
         L     R5,HOLDLEN           SET R5 TO PREFIX LEN
         LA    R4,0(R5,R4)          BUMP PAST PREFIX
         CLI   HOLDSUF,C'A'         IS THIS ACTIVE FILE
         BE    AAA800
         CLI   HOLDSUF,C'D'         IS THIS DAILY FILE
         BE    BBB800
         CLI   HOLDSUF,C'R'         IS THIS ARCHIVE FILE
         BE    CCC800
         CLI   HOLDSUF,C'O'         IS THIS OLD SYSTEM FILES
         BE    CCC810
AAA800   MVC   0(7,R4),=C'.ACTIVE'  MOVE IN ACTIVE CONSTANT
         LA    R4,7(R4)             BUMP R4
         B     DDD800               CHECK STP IF ANY
BBB800   MVC   0(6,R4),=C'.DAILY'   MOVE IN DAILY CONSTANT
         LA    R4,6(R4)             BUMP R4
         B     DDD800               CHECK DATE/TIME STAMP
CCC800   MVC   0(8,R4),=C'.ARCHIVE' MOVE IN ARCHIVE CONSTANT
         LA    R4,8(R4)             BUMP R4
         B     DDD800               CHECK DATE/TIME STAMP
CCC810   MVC   0(8,R4),=C'.ARCHIVE' ##### OLD=ARCHIVE
         LA    R4,8(R4)             ##### OLD=ARCHIVE
*CCC810   MVC   0(6,R4),=C'.DAILY'  ##### OLD=DAILY
*         LA    R4,6(R4)            ##### OLD=DAILY
         B     DDD800               CHECK DATE/TIME STAMP
DDD800   EQU   *
         MVC   DSPLYSTP,STPATRN     MOVE IN PATTERN
         EDMK  DSPLYSTP,HOLDSUF+1   EXPAND DATE/TIME STAMP
         CLC   DSPLYSTP,HIGH9       IS IT ACTIVE
         BE    FFF800               YES - DONE
         CLC   DSPLYSTP(6),HIGH9    IS IT GDG
         BE    EEE800               YES - EXPAND GDG
         MVC   0(2,R4),=C'.D'       MOVE IN "D"
         MVC   2(7,R4),DSPLYSTP+1   MOVE IN DATE
         MVC   9(2,R4),=C'.T'   MOVE IN ".T"
         MVC   11(4,R4),DSPLYSTP+8   MOVE IN DATE
         B     FFF800               DONE
EEE800   MVC   0(2,R4),=C'.G'       MOVE IN "G"
         MVC   2(4,R4),DSPLYSTP+6   MOVE IN GDG
         MVC   6(1,R4),=C'V'     MOVE IN "V"
         MVC   7(2,R4),DSPLYSTP+10  MOVE IN VERSION
FFF800   EQU   *
         MVC   DELTYPE,=CL11'KEEP'  CLEAR ACTION SECTION OF REPORT
         L     R7,8(R10)            SET R7 TO COUNTER
         C     R7,=F'0'             ANY ENTRIES LEFT ON DSN
         BH    HHH800               YES - DONT DELETE
         CLI   HOLDSUF,C'A'         IS THIS ACTIVE
         BE    HHH800               YES - DONT DELETE
         MVC   DELTYPE,=C'UNCATLOGED '  REPORT UNCATLOGE DONE
         CLI   HOLDTYPE,C'T'        IS IT TAPE DSN
         BE    GGG800               BRANCH TO UNCAT ONLY
         MVC   DELTYPE,=C'SCR + UNCAT'  REPORT UNCATLOGED DONE
         TM    FLAG,TESTING         IN TEST MODE
         BO    GGG800               BR AROUND SCRATCH
         LOCATE CATLOC              LOCATE CATALOG ENTRY FOR DATA SET
         LTR   R15,R15
         BZ    FFF800A
         MVC   DELTYPE,=C'LOCATE FAIL'  REPORT UNCATLOGED DONE
         B     HHH800
FFF800A  SCRATCH CATSCR             SCRATCH IT
         LTR   R15,R15
         BZ    GGG800
         MVC   DELTYPE,=C'SCRTCH FAIL' REPORT UNCATLOGED DONE
         B     HHH800
GGG800   EQU   *
         TM    FLAG,TESTING         IN TEST MODE
         BO    HHH800               BR AROUND UNCATLG
         CATALOG CATUNC             LOCATE CATALOG ENTRY FOR DATA SET
         LTR   R15,R15
         BZ    HHH800
         MVC   DELTYPE,=C'UNCAT FAIL '  REPORT UNCATLOGED DONE
HHH800   CVD   R7,DWORK             CONVERT COUNTER
         MVC   RPTCOUNT,PATTERN     MOVE IN UNPACK PATTERN
         EDMK  RPTCOUNT,DWORK+4     UNPACK COUNT
III800   MVC   DDSN,HOLDDSN         MOVE DSN TO REPORT LINE
         PUT   DELDSN,DSNDEL        WRITE IT
         AP    LINECNT2,=P'1' ADD 1 TO LINE COUNTER
         CP    LINECNT2,=P'59' PAST 59 PER PAGE
         BL    EXIT800         NO - GO TO END
         ZAP   LINECNT2,=P'3'  RESET TO 3
         PUT   DELDSN,HEADDEL2 PUT OUT HEADER
         PUT   DELDSN,HEADDEL3 PUT OUT HEADER
         PUT   DELDSN,BLANKS   PUT OUT HEADER
EXIT800  L     R14,SAVE800          RESTORE RETURN ADDR
         BR    R14                  GO BACK
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS AND WORK AREAS                                     *
*                                                                     *
***********************************************************************
         LTORG
SAVE050  DC    F'0'
SAVE400  DC    F'0'
SAVE500  DC    F'0'
SAVE600  DC    F'0'
SAVE700  DC    F'0'
SAVE750  DC    F'0'
SAVE800  DC    F'0'
TBLAREA  DC    F'0'
SAVEWORK DC    F'0'
LOCIADDR DC    F'0'
DATEADDR DC    F'0'
HOLDLEN  DC    F'0'
DWORK    DC    D'0'
SAVESPEC DC    F'0'
*
FLAG     DC    X'00'
RPTBIG   EQU   X'01'
SPECDEL  EQU   X'04'
DELACT   EQU   X'08'
TESTING  EQU   X'80'
*
DATEPARM DS    0F
DP1      DC    A(DAYOLD)
DP2      DC    A(DATELMT)
DATELMT  DC    CL10' '
*
HOLDDATE DC    CL8' '
HOLDSTP  DC    CL11' '
DRO      DC    CL1' '
HSTAMP   DC    PL6'0'
PASSPFX  DC    CL25' '
PASSLEN  DC    F'0'
PASSTYPE DC    CL8' '
PATTERN  DC    XL8'4020202020202120'
DELDATE  DC    PL5'0'
RPTDATE  DC    PL5'0'
PACK5    DC    PL5'0'
PACK4    DC    PL4'0'
MAXRPT   DC    PL4'0'
LINECNT1 DC    PL2'0'
LINECNT2 DC    PL2'0'
BIGCNT   DC    PL2'0'
*
HOLDTYPE DC    CL1' '
HOLDSUF  DC    CL7' '
HOLDPREF DC    CL25' '
HOLDDSN  DC    CL44' '
PREFIX1  DC    CL21' '
PREFIX1L DC    F'0'
PREFIX2  DC    CL25' '
PREFIX2L DC    F'0'
DAYOLD   DC    CL5' '
RPTLIM   DC    CL7'0000000'
HIGH9    DC    CL12' 99999999999'
DSPLYSTP DC    CL12' '
STPATRN  DC    XL12'402120202020202020202020'
DATEPAT  DC    X'402120202020612020612020'
TIMEPAT  DC    X'402120207A20207A2020'
LINEPAT  DC    X'40206B2020206B202120'
TESTIT   DC    CL24'T E S T I N G    O N L Y'
HEADBIG  DC    CL133'1  SOUTCLN - LARGE SYSOUT REPORTS'
HEADING  DS    0CL133
         DC    CL40' JOBNAME    JOB #      DATE         TIME'
         DC    CL93'      LINE COUNT   SYSOUT DATASET'
HEADDEL1 DC    CL133'1  SOUTCLN - DELETE VSAM RECORDS'
HEADDEL2 DC    CL133'1  SOUTCLN - LIST ALL / DELETE EMPTY FILES'
HEADDEL3 DC    CL133' ACTION           DATASETS (DISK AND TAPE)'
DSNDEL   DS    0CL133
         DC    CL1' '
DELTYPE  DC    CL11' '
         DC    CL5' DSN='
DDSN     DC    CL44' '
         DC    CL2' '
         DC    CL8'REPORTS='
RPTCOUNT DC    CL8' '
         DC    CL54' '
BLANKS   DC    CL133' '
OUTREC   DS    0CL133
OUTCC    DC    C' '
OUTJN    DC    CL8' ',CL3' '
OUTJ#    DC    CL8' ',CL1' '
OUTDT    DC    CL12' ',CL1' '
OUTTM    DC    CL10' ',CL2' '
OUTLINE  DC    CL10' ',CL3' '
OUTDSN   DC    CL44' '
         DC    CL1' '
OUTRESON DC    CL30' '
         DS    D
REASON#1 DC    CL30'DAILY/ARCH DATASET NOT THERE'
REASON#2 DC    CL30'RETENTION DATE HAS EXPIRED'
REASON#3 DC    CL30'SPECIAL DELETE RPT. REQUESTED'
REASON#4 DC    CL30'DELETE ACTIVE RECORD REQUESTED'
         DS    D
*        THE FOLLOWING IS THE JOURNAL RECORD FOR OUTPUT
JOURNREC DS    0CL42              JOURNAL RECORD
JKEY     DS    0CL22
JJOBN    DC    CL8' '
JDATE    DC    PL5'0'
JTIME    DC    PL4'0'
JJOB#    DC    CL8' '
JMCLASS  DC    CL1' '
JTTR     DC    XL3'000000'
JDAY     DC    X'FF'
JLINES   DC    PL4'0'
JDEVT    DC    XL1'00'
JSUFFIX  DS    0CL7
JADR     DC    CL1' '
JSTP     DC    PL6'0'
*
CATLOC   CAMLST NAME,HOLDDSN,,LOCAREA
CATSCR   CAMLST SCRATCH,HOLDDSN,,LOCAREA,,OVRD
CATUNC   CAMLST UNCAT,HOLDDSN
LOCAREA  DS    0D
         DC    265C' '
BLDLLIST DC    H'1',H'58',CL8'LOCINDEX',XL52'00' BLDL LIST
BLDLDATE DC    H'1',H'58',CL8'SOUTDATE',XL52'00' BLDL LIST
         EJECT
*
JOURNACB ACB   AM=VSAM,DDNAME=JOURNAL,MACRF=(KEY,SEQ,OUT)
JOURNRPL RPL   ACB=JOURNACB,AREA=JOURNREC,AREALEN=42,                  X
               OPTCD=(KEY,SEQ,SYN,UPD,MVE)
DELREC   DCB   MACRF=(PM),DSORG=PS,DDNAME=DELREC,LRECL=133,RECFM=FBA
DELDSN   DCB   MACRF=(PM),DSORG=PS,DDNAME=DELDSN,LRECL=133,RECFM=FBA
BIGRPT   DCB   MACRF=(PM),DSORG=PS,DDNAME=BIGRPT,LRECL=133,RECFM=FBA
PREFIX   DCB   MACRF=(GM),DSORG=PS,DDNAME=PREFIX,EXLST=OPENL
OLDPFX   DCB   MACRF=(GM),DSORG=PS,DDNAME=OLDPFX,EXLST=OPENL
SPECLDEL DCB   MACRF=(GM),DSORG=PS,DDNAME=SPECLDEL,EODAD=XXX060,       X
               EXLST=OPENL
*
DELAREA  DS    0CL80
DELJNAME DC    CL8' '
DELFILL1 DC    CL1' '
DELJNUM  DC    CL8' '
DELFILL2 DC    CL63' '
*
OPENL    DC    X'87',AL3(JFCB)   OPEN EXIT LIST
JFCB     DC    CL176' '          HOLD AREA FOR PREFIX JFCB
*
LOCIPARM DC    A(LOCIPFX)
         DC    A(LOCIDSN)
         DC    A(LOCIVOL)
         DC    A(0)
LOCIPFX  DC    CL44' '
LOCIDSN  DC    CL44' '
LOCIVOL  DC    CL6' '
*
SPECAREA DSECT
SPECNAME DS    CL8
SPECNUM  DS    CL8
SPECLGTH DS    F
*
WORKSPC  DSECT
LOCINDXW EQU   *
         END   SOUTCLN
