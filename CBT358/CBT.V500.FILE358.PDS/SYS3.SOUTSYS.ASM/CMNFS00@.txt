***********************************************************************
* CMNFS00 - FOR VSAM DISKWTR SYSTEM                                   *
*                                                                     *
* NOTE: THE DEFAULT BACKUP DATE FOR THE SEARCH LIMIT IS SET TO 3 DAYS.*
*       IF YOU WOULD LIKE TO CHANGE IT, FIND "BACKUP#' IN COL 1       *
*       AND CHANGE ACCORDINGLY. THIS LIMIT REPLACES THE USE OF        *
*       "L" AND "LA" OPTIONS IN THE OLD SYSTEM.                       *
*                                                                     *
*                                                                     *
*        THIS IS THE TSO/SPF COMMAND PROCESSOR WHICH SUPPORTS BROWSE  *
*        OF DATA WRITTEN TO THE DISKWTR SYSOUT DATASET.  IT'S         *
*        FUNCTION IS TO LOCATE THE SYSOUT FOR A NAMED JOB, PLACE IT   *
*        INTO A DATASET AND INVOKE THE SPF USER BROWSE INTERFACE.     *
*        IT WILL ALSO SUBMIT A BATCH JOB IF THE USER DOES NOT WANT    *
*        TO WAIT FOR TAPE MOUNTS.                                     *
*                                                                     *
*        THIS ROUTINE IS DEPENDENT UPON THE USE OF THE 'SOUTSEP'      *
*        JES-2 SEPARATOR (SUPPLIED) WHICH CREATES A 'JOURNAL'         *
*        VSAM JOURNAL FOR USE IN LOCATING THE NAMED JOB.              *
*                                                                     *
*        THIS ISPF PROGRAM WILL MAKE CALLS TO "SOUTSUB" SUBROUTINE    *
*        PASSING IT THE APPROPRIATE PARM. SEE THE DOCUMENTATION       *
*        IN THE "SOUTSUB" PROGRAM FOR A DESCRIPTION OF THE PARM.      *
*                                                                     *
*        THIS ISPF PROGRAM WILL ALSO BUILD THE PROPER PARM NEEDED     *
*        PASSED TO SOUTPRT WHEN A USER SELECTS THE BATCH PRINT OPTION *
*                                                                     *
*                                                                     *
*     THE FOLLOWING DESCRIBES THE REQUIRED DATASETS FOR THIS ROUTINE: *
*                                                                     *
*        1) DSNAME.QUALIFIER.JOURNAL                                  *
*                THIS IS THE JOURNAL DATASET THAT CONTAINS A RECORD   *
*                FOR EACH SYSOUT WRITTEN TO THIS SYSTEM.              *
*                                                                     *
*        2) DSNAME.QUALIFIER.ACTIVE                                   *
*                THIS IS THE DATASET THAT THE JES EXTERNAL WRITER     *
*                IS WRITTING SYSOUT REPORTS TO. IT IS SHARED BETWEEN  *
*                DISKWTR (UPDATE MODE) AND THIS PROGRAM (READ ONLY)   *
*                                                                     *
*        3) DSNAME.QUALIFIER.DAILY.D9YYMMDD.THHMM (DATE/TIME STAMP)   *
*                THIS IS THE BASE NAME FOR THE 'DAILY' SET OF         *
*                SYSOUT REPORTS. (CREATED BY SAVEWTR). THIS DATESET   *
*                IS CREATED WHEN THE ACTIVE FILE FILLS UP AND/OR IS   *
*                OFFLOADED.                                           *
*                                                                     *
*        4) DSNAME.QUALIFIER.ARCHIVE.D9YYMMDD.THHMM (DATE/TIME STAMP) *
*                THIS IS THE BASE NAME FOR THE 'ARCHIVE' SET OF       *
*                SYSOUT REPORTS. (CREATED BY SAVEWTR). IT IT USED     *
*                TO MERGE MULTIPLE "DAILY" DATASETS ONTO ONE TAPE     *
*                DATASET. (OPTIONAL; ONLY CREATED IF DAILY IS ON DISK)*
*                                                                     *
*        NOTE: THE DESCRIBED DATASETS MUST HAVE THE SUFFIXES DESCRIBED*
*              ABOVE ('ACTIVE', 'DAILY...' AND 'ARCHIVE...') IN       *
*              ORDER TO ALLOW DYNAMIC ALLOCATION OF SAME.             *
*                                                                     *
*                                                                     *
*      RETURN CODES:                                                  *
*             0 - NORMAL RETURN                                       *
*                 ALL ERROR CONDITIONS ARE DISPLAYED VIA MESSAGES     *
*                                                                     *
*        REGISTER USAGE:                                              *
*                                                                     *
*            R0 = WORK REG                                            *
*            R1 = WORK REG                                            *
*            R2 = WORK REG                                            *
*            R3 = WORK REG                                            *
*            R4 = WORK REG                                            *
*            R5 = WORK REG                                            *
*            R6 = WORK REG                                            *
*            R7 = WORK REG                                            *
*            R8 = WORK REG                                            *
*            R9 = WORK REG                                            *
*           R10 = BASE REG                                            *
*           R11 = BASE REG                                            *
*           R12 = BASE REG                                            *
*           R13 = SAVE AREA                                           *
*           R14 = WORK REG                                            *
*           R15 = WORK REG                                            *
*                                                                     *
***********************************************************************
         PRINT ON
         EJECT
**********************************************************************
*                                                                     *
*       SET UP SPF VARIABLES AND DISPLAY FIRST PANEL                  *
*                                                                     *
**********************************************************************
CMNFS00  SPFINIT R12,R11,R10,R9,USER=Y      ,ERRORS=RETURN
         CALL  ISPLINK,(CONTROL,ERRORS,RETURN),VL
         SPFVDEF 1,(CMNFSYNO)
         SPFVDEF 5,(CMNFSJNO)
         SPFVDEF 8,(CMNFSJNM,CMNFSDST,OUTDEST)
         SPFVDEF 8,(CMNFSNAM,CMNFSJNU,CMNFSTIM)
         SPFVDEF 10,(CMNFSDAT,CMNFSLMT)
         SPFVDEF 1,(CMNFSSEL,CMNFSADR,CMNFSCLA,CMNFSOPT,OUTCLASS)
         SPFVDEF 3,(CMNFSDAY)
         SPFVDEF 6,(CMNFSDEV)
         SPFVDEF 9,(CMNFSLIN)
         SPFVDEF 15,(CMNFSSTP)
         SPFVDEF 44,(CMNFSDSN,CMNFSJDS)
         SPFVDEF 39,(CMNFSPR1)
         SPFVDEF 22,(CMNFSPR2)
         SPFVDEF 35,(CMNFSPR3)
         SPFVDEF 21,(CMNFSWTR)
         SPFVDEF 25,(CMNFSWTO)
         SPFVDEF 72,(CMNBSVA,CMNBSVB,CMNBSVC,CMNBSVD)
         SPFVGET (CMNFSJNM,CMNFSOPT),PROFILE
         SPFVGET (CMNFSDST,CMNFSCLA),PROFILE
         SPFVGET (CMNBSVA,CMNBSVB,CMNBSVC,CMNBSVD),PROFILE
*
         CLI   CMNBSVA,C'/'           HAS JCL BEEN PROVIDED YET?
         BE    ENTRY030               YES, CONTINUE
         SPFVDEF 72,(ZLLGJOB1,ZLLGJOB2,ZLLGJOB3,ZLLGJOB4)
         SPFVGET (ZLLGJOB1,ZLLGJOB2,ZLLGJOB3,ZLLGJOB4),PROFILE
         MVC   CMNBSVA,ZLLGJOB1       UPDATE JOB CARD FROM LIST/LOG
         MVC   CMNBSVB,ZLLGJOB2       UPDATE JOB CARD FROM LIST/LOG
         MVC   CMNBSVC,ZLLGJOB3       UPDATE JOB CARD FROM LIST/LOG
         MVC   CMNBSVD,ZLLGJOB4       UPDATE JOB CARD FROM LIST/LOG
         EJECT
*----------------------------------------------------------------------
*        GET DECREMENTED DATE AND DISPLAY PANEL
*----------------------------------------------------------------------
ENTRY030 DS    0H
         LA    R1,DATEPARM        SOUTDATE PARAMETER LIST
         LINK  EP=SOUTDATE        CALL THE DATE SUBROUTINE
         LTR   R15,R15            SOUTDATE RETCODE OK?
         BZ    AAA030             GOT DATE - DECREMENTED BY 2
         MVC   CMNFSLMT,=CL10' '  CLEAR DATE LIMIT
AAA030   SPFSPLAY 'CMNFS00',,'CMNFSJNM',ENTRY040,EOJ
EXIT030  EQU   *
*----------------------------------------------------------------------
*        ALLOCATE WORK DATA SET FOR SOUTSUB ROUTINE
*----------------------------------------------------------------------
ENTRY040 DS    0H
         CLI   CMNFSDD+8,X'FF'    IS IT ALREADY ALLOCATED ?
         BE    EXIT040            YES - OK
         SPFTEMP ,INDSN,'CMNFS00' CREATE A TEMP DSN NAME
         SPFALLOC A,RETDD=CMNFSDD,DSN=INDSN,DISP='SHR',                +
               BLKSIZE='6118',LRECL='133',RECFM='FB'
         CLC   SPFDYN15,=4C'0'    ALLOCATED ?
         BE    AAA040             YES - OK
         CLC   SPFDYNER,=C'1708'  DATA SET NOT FOUND ?
         BNE   ERROR1             NO - SERIOUS ERROR
         SPFALLOC A,RETDD=CMNFSDD,DSN=INDSN,DISP='(NEW,CATLG,DELETE)', +
               SPACE='(9000,(750,750))',BLKSIZE='6118',                +
               LRECL='133',RECFM='FB'
         CLC   SPFDYNER,=4C'0'    DATA SET ALLOCATED ?
         BNE   ERROR1             NO - SERIOUS ERROR
AAA040   EQU   *
         MVC   SOUTDCB+40(8),CMNFSDD    USE SYSTEM ALLOCATED DDNAME
         MVI   CMNFSDD+8,X'FF'    INDICATE ALLOCATED
EXIT040  EQU   *
         EJECT
***********************************************************************
*
*        MOVE PANEL VARIABLES TO PARM AREA FOR BROWSE
*
***********************************************************************
ENTRY050 EQU   *
         MVI   TBLSW,C'N'                NOT IN TABLE PROCESSING
         CLI   CMNFSJNM,C' '             WAS JOBNAME MORE THAN * ?
         BE    ERROR5                    NO - ERROR
         CLI   CMNFSJNM,C'*'             WAS JOBNAME MORE THAN * ?
         BE    ERROR5                    NO - ERROR
         LA    R4,CMNFSJNM              SET R4 EQ JOBNAME
         LA    R4,7(R4)                 BUMP TO END
LOOPA5   CLI   0(R4),C' '               IT LAST CHAR NON-BLANK
         BNE   ENDLPA5                  YES - GO CHECK IT
         SH    R4,=H'1'                 BACK UP ONE
         B     LOOPA5                   TRY AGAIN
ENDLPA5  CLI   0(R4),C'*'               IS LAST CHAR *
         BNE   AAA050                   NO - CONTINUE
         CLC   CMNFSOPT,S                SELECT JOB SPECIFIED?
         BE    ERROR8                   CAN'T HAVE *
         CLC   CMNFSOPT,P                PRINT ONLY SPECIFIED ?
         BE    ERROR8                   CAN'T HAVE *
AAA050   CLC   CMNFSOPT,S                SELECT JOB SPECIFIED?
         BE    BBB050
         CLC   CMNFSOPT,P                PRINT ONLY SPECIFIED ?
         BE    ENTRY200
         CLC   CMNFSOPT,L                LIST ALL SPECIFIED?
         BNE   ERROR4
         MVC   HOLDKEY,BLANKS            CLEAR KEY FOR LIST
BBB050   EQU   *
         MVC   JOBNAME,CMNFSJNM          MOVE JOBNAME TO PARM AREA
         MVC   JOBNUM,CMNFSJNO           MOVE JOB NO. TO PARM AREA
         MVC   DATE,BLANKS               CLEAR DATE
         MVC   TIME,BLANKS               CLEAR TIME
         MVC   PARMDD,CMNFSDD            MOVE TEMP DSN DD TO PARM AREA
         MVC   PARMQUAL,CMNFSWTR         MOVE OPSWTR QUALIFIER TO PARM
         MVI   FLAG,TSO                  INIT PARM FLAG TO SPECIFY TSO
         MVC   PARMDATL,CMNFSLMT         MOVE DATELIMIT TO PARM
         MVC   PARMOLDQ,CMNFSWTO         MOVE OLD QUALIFIER TO PARM
         CLI   CMNFSOPT,C'L'             IS A LIST SPECIFIED
         BNE   EXIT050                   NO - GET OUT
         OI    FLAG,LST                  SET UP FLAG FOR LISTALL
EXIT050  EQU   *
         EJECT
***********************************************************************
*
*        PROCESS THE LIST/BROWSE REQUEST VIA CALL TO SOUTSUB
*
***********************************************************************
ENTRY060 EQU   *
         MVC   CURSOR,=CL8'CMNFSJNM'     SET CURSOR FOR ERROR IF ANY
         MVC   PANEL,=CL8'CMNFS00'       SET PANEL FOR ERROR IF ANY
         CLC   HOLDKEY,PARMKEY           IS THIS THE SAME JOB
         BE    AAA060                    YES - JUST BROWSE DATASET
         BAL   R14,ENTRY900              CALL TO SOUTSUB
AAA060   CLI   CMNFSOPT,C'L'             IS LIST SPECIFIED?
         BE    ENTRY100                  YES - BUILD TABLE
         MVC   HOLDKEY(8),CMNFSJNM       BUILD HOLDKEY FOR PRINT?
         MVC   HOLDKEY+8(5),CMNFSJNO      "
         MVC   HOLDKEY+13(18),BLANKS      "
         BAL   R14,ENTRY910              BROWSE DATASET
         B     ENTRY800                  NO - GET OUT
EXIT060  EQU   *
         EJECT
***********************************************************************
*
*        DISPLAY TABLE
*
***********************************************************************
ENTRY100 EQU   *
         MVI   TBLSW,C'Y'                  SET SWITCH FOR TABLE PROCESS
         MVI   PRTSW,C'N'                  SET SWITCH TO BUILD TABLE
         MVC   HOLDKEY,BLANKS              CLEAR HOLD KEY AREA
         BAL   R14,ENTRY920                GOTO BUILD TABLE
         CLI   EMPTY,C'Y'                  DOES TABLE HAVE ENTRIES
         BE    ERROR6                      NO - ERROR
         CALL  ISPLINK,(TBTOP,FS00TBL1),VL
AAA100   EQU   *
         MVC   SMSG,=CL24'SELECT JOB BELOW'
         MVC   LMSG,=CL72'SELECT A JOB FOR BROWSE, PRINT OR END'
         B     AAA103
AAA101   EQU   *
         MVC   SMSG,=CL24'SELECT SUCCESSFUL'
         MVC   LMSG,=CL72'JOB SELECTION SUCCESSFUL; SELECT ANOTHER JOB'
         B     AAA103
AAA102   EQU   *
         MVC   SMSG,=CL24'PRINT SUCCESSFUL'
         MVC   LMSG,=CL72'JOB PRINT SUCCESSFUL; SELECT ANOTHER JOB'
         B     AAA103
AAA103   MVC   PANEL,=CL8'CMNFS01'
         MVC   MSG,=CL8'CMN000'
BBB100   EQU   *
         CALL  ISPLINK,(TBDISPL,FS00TBL1,PANEL,MSG),VL
         C     R15,=F'8'                   END KEY HIT
         BE    ENTRY800                    YES DISPLAY FIRST PANEL
         OC    CMNFSSEL,=C' '              SET SELECT SW TO CAPS
         CLI   CMNFSSEL,C' '               WAS ANY ENTERED
         BE    AAA100                      NO - DISPLAY AGAIN
         CLC   CMNFSSEL,S                  JOB SELECTED FROM TABLE
         BE    CCC100                      YES - CONTINUE
         CLC   CMNFSSEL,P                  PRINT ONLY SPECIFIED
         BE    ENTRY200                    GO TO PRINT
         CLC   CMNFSSEL,Q                  QUERY DATASET NAME
         BE    EEE100                      YES - GO BUILD DSN
         B     ERROR3                      ELSE ERROR
CCC100   EQU   *
         MVC   JOBNAME,CMNFSNAM            MOVE JOBNAME TO PARM
         MVC   JOBNUM,CMNFSJNU+3           MOVE JOB # NUMERICS TO PARM
         MVC   DATE,CMNFSDAT               MOVE DATE TO PARM
         MVC   TIME,CMNFSTIM               MOVE TIME TO PARM
         MVI   FLAG,TSO                    SET PARM FLAG FOR TSO
         MVC   CURSOR,=CL8'ZCMD'           SET UP CURSOR POS
         MVC   PANEL,=CL8'CMNFS01'         SET UP DISPLAY PANEL
         CLC   HOLDKEY,PARMKEY             IS SAME JOB SELECTED?
         BE    DDD100                      YES - BYPASS SOUTSUB CALL
         BAL   R14,ENTRY900                CALL SOUTSUB FOR THIS JOB
         MVC   HOLDKEY,PARMKEY             SET UP HOLDKEY
DDD100   BAL   R14,ENTRY910                BROWSE DATASET
         CLI   CMNFSDEV,C'T'               WAS TAPE MOUNTED
         BNE   AAA101                      NO - DISPLAY TABLE AGAIN
         B     NNN100                      BR TO PRINT QUESTION
EEE100   EQU   *                           BUILD DSN
         MVC   CMNFSDSN,BLANKS             CLEAR NAME
         CLI   CMNFSADR,C'O'               IS THIS A OLD DISKWTR DSN
         BE    FFF100                      YES - BR
         MVC   CMNFSDSN(21),CMNFSWTR       MOVE IN DSN PREFIX
         B     GGG100                      BR AROUND
FFF100   MVC   CMNFSDSN(25),CMNFSWTO       MOVE IN OLD DSN PREFIX
GGG100   LA    R4,CMNFSDSN                 SET R4 TO BEGINNING
LOOPF1   CLI   0(R4),C' '                  BLANK FOUND
         BE    ENDLPF1                     YES - END OF PREFIX
         LA    R4,1(R4)                    BUMP R4
         B     LOOPF1                      TRY AGAIN
ENDLPF1  CLI   CMNFSADR,C'A'               IS DSN ACTIVE?
         BE    HHH100                      YES - BR
         CLI   CMNFSADR,C'D'               IS DSN DAILY?
         BE    III100                      YES - BR
         CLI   CMNFSADR,C'R'               IS DSN ARCHIVE?
         BE    JJJ100                      YES - BR
         B     JJJ100                      OLD = ARCHIVE  #####
*        B     III100                      OLD = DAILY    #####
HHH100   MVC   0(7,R4),=C'.ACTIVE'         MOVE IN ACTIVE
         LA    R4,7(R4)                    BUMP R4 PAST SUFFIX
         B     KKK100                      BR
III100   MVC   0(6,R4),=C'.DAILY'          MOVE IN DAILY
         LA    R4,6(R4)                    BUMP R4 PAST SUFFIX
         B     KKK100                      BR
JJJ100   MVC   0(8,R4),=C'.ARCHIVE'        MOVE IN ARCHIVE
         LA    R4,8(R4)                    BUMP R4 PAST SUFFIX
KKK100   MVC   0(15,R4),CMNFSSTP           MOVE IN DATE/TIME STAMP
LLL100   MVC   CMNFSJDS(21),CMNFSWTR       MOVE PREFIX TO JOURN DSN
         LA    R4,CMNFSJDS                 SET R4 TO BEGINNING
LOOPM1   CLI   0(R4),C' '                  FOUND BLANK?
         BE    ENDLPM1                     YES -BR
         LA    R4,1(R4)                    BUMP R4
         B     LOOPM1                      TRY AGAIN
ENDLPM1  MVC   0(8,R4),=C'.JOURNAL'        MOVE IN JOURNAL NAME
         SPFSPLAY 'CMNFS03',,,AAA100,AAA100  DISPLAY PANEL
*
NNN100   EQU   *
         SPFSPLAY 'CMNFS02',,'CMNFSYNO',,AAA100  DISPLAY PANEL
         CLI   CMNFSYNO,C' '               YES- NO ENTERED
         BE    NNN100                      NO - DISPLAY AGAIN
         CLI   CMNFSYNO,C'N'               NO PRINT?
         BE    AAA101                      DISPLAY TABLE AGAIN
         CLI   CMNFSYNO,C'Y'               YES PRINT?
         BNE   NNN100                      NO - DISPLAY QUES AGAIN
         MVI   PRTSW,C'Y'                  TURN ON PRINT SW
         BAL   R14,ENTRY920                PRINT DATASET
         B     AAA102                      BR TO DISPLAY TABLE
EXIT100  EQU   *
         EJECT
*----------------------------------------------------------------------
*        PRINT REQUESTED
*
*        1) IF YOU JUST LOOKED AT JOB, PRINT IT
*        2) IF IT IS A NEW JOB TO PRINT - SUBMIT BATCH
*
*----------------------------------------------------------------------
ENTRY200 DS    0H
         CLI   TBLSW,C'Y'                  IN TABLE PROCESSING
         BE    AAA200                      YES - BR
         MVC   JOBNAME,CMNFSJNM            MOVE JOBNAME
         MVC   JOBNUM,CMNFSJNO             MOVE JOB NUMBER
         MVC   DATE,BLANKS                 CLEAR DATE
         MVC   TIME,BLANKS                 CLEAR TIME
         B     BBB200
AAA200   MVC   JOBNAME,CMNFSNAM            MOVE JOBNAME TO PARM
         MVC   JOBNUM,CMNFSJNU+3           MOVE JOB # NUMERICS TO PARM
         MVC   DATE,CMNFSDAT               MOVE DATE TO PARM
         MVC   TIME,CMNFSTIM               MOVE TIME TO PARM
BBB200   CLC   HOLDKEY,PARMKEY             DID USER JUST "S" THIS JOB
         BNE   CCC200                      NO - BR AROUND
         MVI   PRTSW,C'Y'                  TURN ON PRINT SW
         BAL   R14,ENTRY920                BRANCH TO ONLINE PRINT
         CLI   TBLSW,C'Y'                  IN TABLE PROCESSING
         BE    AAA102                      YES - RE-DISPLAY TABLE
         B     ENTRY800                    DISPLAY FIRST PANEL
CCC200   EQU   *
         MVC   OUTCLASS,=C'*'              BATCH DEFAULT *
         CLI   CMNFSCLA,C' '               IS CLASS SPECIFIED?
         BE    DDD200
         MVC   OUTCLASS,CMNFSCLA           MOVE IN CLASS
DDD200   MVC   OUTDEST,=CL8'R0'            DEFAULT DEST IS R0
         CLI   CMNFSDST,C' '               IS BATCH DEST SPECIFIED
         BE    EEE200                      NO - SET DEFAULT
         MVC   OUTDEST,CMNFSDST            MOVE IN SPECIFIED DEST
EEE200   EQU   *
         MVC   CMNFSPR1(31),PARMKEY         SET UP
         MVC   CMNFSPR1+31(8),=CL8'SYSPRINT'    PARM
         MVC   CMNFSPR2(21),CMNFSWTR               FOR
         MVI   CMNFSPR2+21,X'04'                     BATCH
         MVC   CMNFSPR3(10),BLANKS                      PRINT
         MVC   CMNFSPR3+10(25),CMNFSWTO
*----------------------------------------------------------------------
*
*        CREATE JCL IN THE ZTEMP FILE
*
*----------------------------------------------------------------------
         MVC   PROC,=CL8'CMNFS00' MEMBER NAME IN ISPSLIB
         SPFTAILR PROC,SUBMIT     CREATE THE JCL
*        LTR   R15,R15
*        BZ    GGG200
*        MVC   SMSG,=CL24'SUBMIT FAILED'
*        MVC   LMSG,=CL72'JOB SUBMISSION FAILURE - CONTACT SOFTWARE'
*        B     HHH200
GGG200   MVC   SMSG,=CL24'SUBMITTED'
         MVC   LMSG,=CL72'JOB TO PRINT THE DATA HAS BEEN SUBMITTED'
HHH200   CLI   TBLSW,C'Y'         IN TABLE PROCESSING
         BE    AAA103             BR TO DISPLAY TABLE
         B     CCC800
EXIT200  EQU   *
         EJECT
*----------------------------------------------------------------------
*        DISPLAY PANEL
*----------------------------------------------------------------------
ENTRY800 DS    0H
         CLI   TBLSW,C'N'        PROCESSING A SPF TABLE
         BE    BBB800            NO BRANCH AROUND TABLE END
         CALL  ISPLINK,(TBEND,FS00TBL1),VL  END TABLE
AAA800   MVC   SMSG,=CL24'JOB LISTING SUCCESSFUL'
         MVC   LMSG,=CL72'LIST OF JOBS COMPLETED SUCCESSFULLY'
         B     CCC800
BBB800   MVC   SMSG,=CL24'BROWSE/PRINT SUCCESSFUL'
         MVC   LMSG,=CL72'BROWSE OR PRINT COMPLETED SUCCESSFULLY'
CCC800   MVC   MSG,=CL8'CMN000'
         SPFSPLAY 'CMNFS00',MSG,'CMNFSJNM',ENTRY050,EOJ
EXIT800  EQU   *
         EJECT
*----------------------------------------------------------------------
*        END OF JOB
*----------------------------------------------------------------------
EOJ      DS    0H
         SPFVPUT (CMNFSJNM,CMNFSOPT),PROFILE
         SPFVPUT (CMNFSDST,CMNFSCLA),PROFILE
         SPFVPUT (CMNBSVA,CMNBSVB,CMNBSVC,CMNBSVD),PROFILE
         SPFALLOC D,DD=CMNFSDD
         SPFALLOC A,DD=CMNFSDD,DSN=INDSN,DISP='(OLD,DELETE,DELETE)'
         SPFALLOC D,DD=CMNFSDD
         LA    R15,0
         SPFEXIT
         EJECT
***********************************************************************
*
*        PROCESS THE LIST/BROWSE REQUEST VIA CALL TO SOUTCALL
*
***********************************************************************
ENTRY900 DS    0H
         ST    R14,ST900
         OI    FLAG,AUTH          INDICATE AUTHORIZED REQUEST
         LA    R1,SUBPARM         SOUTSUB PARAMETER LIST
         LINK  EP=SOUTCALL        CALL THE SOUT SUBROUTINE
         LTR   R2,R15             SOUTSUB RETCODE OK?
         BNZ   ERROR2             RETURN WILL CHECK TO DISPLAY MESSAGE
EXIT900  EQU   *
         L     R14,ST900
         BR    R14
*----------------------------------------------------------------------
*        BROWSE JOB IF REQUESTED
*----------------------------------------------------------------------
ENTRY910 DS    0H
         ST    R14,ST910
         SPFBRWSE INDSN
EXIT910  EQU   *
         L     R14,ST910
         BR    R14
***********************************************************************
*
*        PRINT JOB IF REQUESTED / OR BUILD THE TABLE
*
***********************************************************************
ENTRY920 DS    0H
         ST    R14,ST920
         CLI   PRTSW,C'Y'            PRINT REQUESTED
         BNE   CCC920                NO - MUST BE BUILD TABLE
         MVC   OUTCLASS,=C'A'        DEFAUT ONLINE CLASS A
         CLI   CMNFSCLA,C' '         CLASS SPECIFIED?
         BE    AAA920
         CLI   CMNFSCLA,C'*'         CLASS SPECIFIED AS *
         BE    AAA920                CHANGE TO "A"
         MVC   OUTCLASS,CMNFSCLA     MOVE IN CLASS
AAA920   EQU   *
         MVC   OUTDEST,=CL8'R0'
         CLI   CMNFSDST,C' '         IS DEST SPECIFIED?
         BE    BBB920                NO - SET DEFAULT
         MVC   OUTDEST,CMNFSDST      MOVE IN SPECIFIED DEST
BBB920   SPFALLOC A,DD=OUTDD,SYSOUT=OUTCLASS,DEST=OUTDEST
         B     DDD920
CCC920   EQU   *
         CALL  ISPLINK,(TBCREATE,FS00TBL1,,NAMELST,NOWRITE),VL
         MVI   EMPTY,C'Y'
DDD920   EQU   *
         OPEN  (SOUTDCB,INPUT)
         CLI   PRTSW,C'Y'
         BNE   EEE920
         OPEN  (PRINTDCB,OUTPUT)
EEE920   GET   SOUTDCB            GET INPUT RECORD
         CLI   PRTSW,C'Y'         PRINT ?
         BNE   FFF920             NO - BUILD TABLE
         LR    R0,R1              COPY DATA ADDRESS
         PUT   PRINTDCB           PUT RECORD
         B     EEE920             GET NEXT RECORD
FFF920   EQU   *
         MVI   EMPTY,C'N'
         LR    R7,R1              COPY DATA ADDRESS
         MVI   CMNFSSEL,C' '          SET UP TABLE ENTRIES
         MVC   CMNFSNAM,1(R7)       "
         MVC   CMNFSDAT,10(R7)       "
         MVC   CMNFSTIM,21(R7)      "
         MVC   CMNFSJNU,30(R7)      "
         MVC   CMNFSDAY,41(R7)     "
         MVC   CMNFSLIN,45(R7)     "
         MVC   CMNFSDEV,55(R7)     "
         MVC   CMNFSADR,62(R7)     "
         MVC   CMNFSSTP,64(R7)     "
         CALL  ISPLINK,(TBADD,FS00TBL1),VL  ADD TO TABLE
         B     EEE920             GET NEXT RECORD
GGG920   CLOSE (SOUTDCB)
         CLI   PRTSW,C'Y'
         BNE   EXIT920
         CLOSE (PRINTDCB)
         SPFALLOC D,DD=OUTDD
EXIT920  EQU   *
         L     R14,ST920
         BR    R14
         EJECT
*----------------------------------------------------------------------
*        DYNAMIC ALLOCATION ERROR
*----------------------------------------------------------------------
ERROR#   DS    0H
         CLI   TBLSW,C'Y'
         BE    BBB100
         SPFSPLAY  PANEL,'CMN000',CURSOR,ENTRY050,EOJ
ERROR1   DS    0H
         MVC   PANEL,=CL8'CMNFS00'
         MVC   CURSOR,=CL8'CMNFSJNM'
         MVC   SMSG,=CL24'TEMP DATASET ERROR'
         MVC   LMSG,=CL72'TEMPORARY DATASET ALLOCATION ERROR'
         B     ERROR#
ERROR2   DS    0H
         CL    R2,=F'4'
         BE    ERROR2A
         CL    R2,=F'8'
         BE    ERROR2B
         CL    R2,=F'12'
         BE    ERROR2C
         CL    R2,=F'16'
         BE    ERROR2D
         CL    R2,=F'20'
         BE    ERROR2E
         CL    R2,=F'24'
         BE    ERROR2F
         CL    R2,=F'28'
         BE    ERROR2G
         CL    R2,=F'32'
         BE    ERROR2H
         CL    R2,=F'36'
         BE    ERROR2I
         MVC   SMSG,=CL24'SOUTSUB ERROR'
         MVC   LMSG,=CL72'ERROR IN SOUTSUB SUBROUTINE'
         B     ERROR#
ERROR2A  EQU   *
         MVC   SMSG,=CL24'JOB NOT FOUND'
         MVC   LMSG,=CL72'JOB NOT FOUND WITHIN THE CONTEXT DEFINED '
         B     ERROR#
ERROR2B  EQU   *
         MVC   SMSG,=CL24'OUTPUT OPEN ERROR'
         MVC   LMSG,=CL72'OPEN FAILED FOR OUTPUT TEMPORARY FILE'
         B     ERROR#
ERROR2C  EQU   *
         MVC   SMSG,=CL24'INVALID PARM PASSED'
         MVC   LMSG,=CL72'INVALID PARAMETER PASSED TO SOUTSUB'
         B     ERROR#
ERROR2D  EQU   *
         MVC   SMSG,=CL24'JOURNAL ALLOC. FAILED'
         MVC   LMSG,=CL72'DYNAMIC ALLOC FAILED FOR THE VSAM JOURNAL'
         B     ERROR#
ERROR2E  EQU   *
         MVC   SMSG,=CL24'EXPANSION FAILED'
         MVC   LMSG,=CL72'DE-COMPRESSION SUBROUTINE ERROR'
         B     ERROR#
ERROR2F  EQU   *
         MVC   SMSG,=CL24'JOURNAL READ ERROR'
         MVC   LMSG,=CL72'ERROR READING THE JOURNAL VSAM FILE'
         B     ERROR#
ERROR2G  EQU   *
         MVC   SMSG,=CL24'JOURNAL OPEN ERROR'
         MVC   LMSG,=CL72'OPEN FAILURE FOR THE JOURNAL VSAM FILE'
         B     ERROR#
ERROR2H  EQU   *
         MVC   SMSG,=CL24'TAPE IN USE/NO REPLIED'
         MVC   LMSG,=CL72'OPERATOR REPLIED "NO" TO TAPE MOUNT OR TAPE I*
               S IN USE; TRY LATER'
         B     ERROR#
ERROR2I  EQU   *
         B     ERROR2H
         MVC   SMSG,=CL24'INPUT ALLOC. ERROR'
         MVC   LMSG,=CL72'INPUT SYSOUT FILE ALLOCATION ERROR; DATSET NO*
               T FOUND?'
         B     ERROR#
ERROR3   DS    0H
         MVI   TBLSW,C'Y'
         MVC   PANEL,=CL8'CMNFS01'
         MVC   CURSOR,=CL8'ZCMD'
         MVC   SMSG,=CL24'INVALID OPTION'
         MVC   LMSG,=CL72'OPTION MUST BE "S" OR "P" ONLY'
         B     ERROR#
ERROR4   DS    0H
         MVI   TBLSW,C'N'
         MVC   PANEL,=CL8'CMNFS00'
         MVC   CURSOR,=CL8'CMNFSOPT'
         MVC   SMSG,=CL24'INVALID OPTION'
         MVC   LMSG,=CL72'OPTION MUST BE "P" "S" OR "L" ONLY'
         B     ERROR#
ERROR5   DS    0H
         MVI   TBLSW,C'N'
         MVC   PANEL,=CL8'CMNFS00'
         MVC   CURSOR,=CL8'CMNFSJNM'
         MVC   SMSG,=CL24'INVALID JOBNAME'
         MVC   LMSG,=CL72'JOBNAME MUST BE ENTERED'
         B     ERROR#
ERROR6   DS    0H
         MVI   TBLSW,C'N'
         MVC   PANEL,=CL8'CMNFS00'
         MVC   CURSOR,=CL8'CMNFSJNM'
         MVC   SMSG,=CL24'NO JOBS FOR REQUEST'
         MVC   LMSG,=CL72'NO JOBS FOUND FOR JOBNAME REQUESTED'
         B     ERROR#
ERROR7   DS    0H
         MVI   TBLSW,C'N'
         MVC   PANEL,=CL8'CMNFS00'
         MVC   SMSG,=CL24'DESTINATION REQUIRED'
         MVC   LMSG,=CL72'OUTPUT DESTINATION IS A REQUIRED  FIELD'
         B     ERROR#
ERROR8   DS    0H
         MVI   TBLSW,C'N'
         MVC   PANEL,=CL8'CMNFS00'
         MVC   CURSOR,=CL8'CMNFSJNM'
         MVC   SMSG,=CL24'"*" NOT VALID FOR OPTION'
         MVC   LMSG,=CL72'ASTERISK IS NOT VALID FOR "P" OR "S" OPTION'
         B     ERROR#
         EJECT
* *********************************************************************
*        SPF VARIALBES
* *********************************************************************
SPFMACS  SPFCMDS FILES=YES,TABLES=YES
         EJECT
* *********************************************************************
*        DCB FOR INPUT AND OUTPUT FILES
* *********************************************************************
SOUTDCB  DCB   DSORG=PS,MACRF=(PM,GL),EODAD=GGG920,                    X
               LRECL=133,RECFM=FBA,BLKSIZE=6118
PRINTDCB DCB   DDNAME=PRTOUTDD,DSORG=PS,MACRF=PM,RECFM=FBA,            X
               LRECL=133,BLKSIZE=6118
         EJECT
* *********************************************************************
*        WORK VARIALBES
* *********************************************************************
ST900    DC    F'-1'
ST910    DC    F'-1'
ST920    DC    F'-1'
* PARM PASSED TO SOUTDATE
DATEPARM DS    0F
DP1      DC    A(BACKUP#)         ADDR OF BACK UP TO # DAYS
DP2      DC    A(CMNFSLMT)        ADDR OF LIMIT DATE
*
BACKUP#  DC    CL5'00003'         BACKUP 3 DAYS
* PARM PASSED TO SOUTSUB
SUBPARM  DS    0F
SUBP1    DC    A(JOBNAME)         ADDR OF JOBNAME
SUBP2    DC    A(JOBNUM)          ADDR OF JOB NUMBER
SUBP3    DC    A(DATE)            ADDR OF DATE
SUBP4    DC    A(TIME)            ADDR OF TIME
SUBP5    DC    A(PARMDD)          ADDR OF DDNAME
SUBP6    DC    A(PARMQUAL)        ADDR OF DSN QUALIFIER
SUBP7    DC    A(FLAG)            ADDR OF FLAGS
SUBP8    DC    A(PARMDATL)        ADDR OF SEARCH DATE LIMIT
SUBP9    DC    A(PARMOLDQ)        ADDR OF OLD QUALIFIER
PARMKEY  DS    0CL31
JOBNAME  DC    CL8' '             JOBNAME FOR SEARCH
JOBNUM   DC    CL5' '             JOBNUM FOR SEARCH
DATE     DC    CL10' '            DATE FOR SEARCH
TIME     DC    CL8' '             TIME FOR SEARCH
PARMDD   DC    CL8' '             DD NAME TO USE
PARMQUAL DC    CL21' '            DSNAME QUALIFIER
FLAG     DC    X'01'               OPTION FLAGS
TSO      EQU   X'01'               ENVIRONEMENT (ISSUE TPUTS)
LST      EQU   X'02'               LIST  REQUESTED
AUTH     EQU   X'04'               CALLER IS AUTH'D
PARMDATL DC    CL10' '
PARMOLDQ DC    CL25' '            OLD DSNAME QUALIFIER
*
PRTSW    DC    CL1' '
TBLSW    DC    CL1' '
NAMELST  DS    0CL101
         DC    CL28'(CMNFSSEL CMNFSNAM CMNFSJNU '
         DC    CL27'CMNFSDAT CMNFSTIM CMNFSDAY '
         DC    CL27'CMNFSLIN CMNFSDEV CMNFSADR '
         DC    CL18'CMNFSSTP)'
HLDLIMIT DC    CL10' '
HOLDKEY  DC    CL31' '
S        DC    CL1'S'
P        DC    CL1'P'
L        DC    CL1'L'
Q        DC    CL1'Q'
         SPACE 3
*----------------------------------------------------------------------
*        BATCH JOB SUBMIT AREA
*----------------------------------------------------------------------
         DS    0H
SUBBUF   DC    CL12'CMD(SUBMIT ''' SUBMIT COMMAND
SUBDSN   DC    CL44' ',CL2' '      SUBMIT DSN
SUBBUFLN DC    A(*-SUBBUF)         SIZE OF SUBMIT COMMAND BUFFER
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
FS00TBL1 DC    CL8'FS00TBL1'
EMPTY    DC    CL1' '
INDSN    DC    CL44' '
*----------------------------------------------------------------------
DDNAMES  EQU   *
CMNFSDD  DC    CL8'       ',X'00'
OUTDD    DC    CL8'PRTOUTDD',X'00'
         DC    X'FFFF'            END INDICATOR
*----------------------------------------------------------------------
         END
