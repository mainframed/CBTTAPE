J13X3    TITLE '2ISG  EXIT-3 ACCT EXIT '
*---------------------------------------------------------------------*
*                                                                     *
* COURTESY: JIM MARSHALL, CAPT, USAF (RET)                            *
*                                                                     *
*        JES2 EXIT 3.  JCL AND CONTROL CARD SCAN ROUTINE.             *
*                                                                     *
*        THIS EXIT WILL VERIFY THAT ALL ACCOUNTING CODES ARE          *
*        VALID AT 2ISG. SOME PEOPLE CALL THEM OUR SILLY              *
*        STANDARDS.                                                   *
*                                                                     *
* INSTALLED: 20 OCT 84                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         GBLB  &SP134                                            JDM1
&SP134   SETB  1                   1 - SP1.3.4 / 0 - SP1.3.2     JDM1
         AIF   (NOT &SP134).SKIP10                               JDM1
         COPY  $HASPGBL
ISG3    $MODULE $BUFFER,                                               X
               $JCT,                                                   X
               $JQE,                                                   X
               $HASPEQU,                                               X
               $PCE,                                                   X
               $CAT,                                                   X
               $DCT,                                                   X
               $HCT,                                                   X
               $PDDB,                                                  X
               $MIT,                                                   X
               $JOE,                                                   X
               $PPPWORK,                                               X
               $CMB
.SKIP10  ANOP                                                    JDM1
         AIF   (&SP134).SKIP15                                   JDM1
         PRINT ON,NOGEN
         $SYSPARM (ON,GEN,DATA,NO,NO)
         COPY  $HASPGEN
         PRINT ON,NOGEN
ISG3     $MODULE                  CREATE THE MIT
         SPACE
         PRINT ON,NOGEN           TURN OFF FOR DSECT DEFINITIONS
         $SYSPARM (ON,GEN,DATA,YES,YES)
         $BUFFER DOC=YES
         $JCT    DOC=YES
         $JQE    DOC=YES
         $PCE    DOC=YES
         $CAT    DOC=YES
         $DCT    DOC=YES
         $HCT    DOC=YES
         $PDDB   DOC=YES
         $TAB    DOC=YES
         $IOT    DOC=YES
         $JOE    DOC=YES
         $XIT    DOC=YES
         $EXITPL DOC=YES
         $LRC    DOC=YES
         $MIT    DOC=YES
         PRINT ON,NOGEN
         $RDRWORK DOC=YES
         PRINT ON,GEN
.SKIP15  ANOP                                                    JDM1
         EJECT
         TITLE '2ISG EXIT-3  JECL  CONTROL CARD SCAN ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        J C L  A N D  C O N T R O L  S T A T E M E N T  S C A N      *
*                                                                     *
*        REGISTERS ON ENTRY ARE:                                      *
*                                                                     *
*        R0  -  LENGTH OF ACCT FIELD IN BYTES; IF NO ACCT FIELD       *
*               SPECIFIED, THEN LENGTH IS ZERO.                       *
*        R1  -  ADDR OF 3 FULLWORD PARAMETER LIST.                    *
*               ------   -------                                      *
*               ¦ R1 ¦ ->¦WORD1¦ POINT TO ACCT(JCTWORK IN JCT)        *
*               ------   -------                                      *
*                        ¦WORD2¦ POINT TO XFLAG BYTE, RXITFLAG        *
*                        -------                                      *
*                        ¦WORD3¦ POINT TO JCTXWRK IN JCT              *
*                        -------                                      *
*        R2  -  ADDR OF SMF RECORD TYPE 6                             *
*        R3-R9 - N/A                                                  *
*        R10 -  ADDRESS OF JCT                                        *
*        R11 -  ADDRESS OF HCT                                        *
*        R12 -  N/A                                                   *
*        R13 -  ADDR OF HASPRDR PCE                                   *
*        R14 -  RETURN ADDR                                           *
*        R15 -  ENTRY ADDR                                            *
*                                                                     *
*        REGISTERS WHEN CONTROL IS PASSED BACK TO JES2                *
*                                                                     *
*        R0-R1  - N/A                                                 *
*        R15  -  A RETURN CODE (ONES WE USE)                          *
*                0  - TELLS JES2 IF THERE ARE ANYMORE EXITS. IF NOT   *
*                     USE THE CURRENT SETTING OF &RJOBOPT INIT        *
*                     PARM TO DETERMINE WHETHER OR NOT TO EXEC        *
*                     'HASPRSCN' SUBROUTINE.                          *
*                12 - TELLS JES2 TO CANCEL THE JOB BECAUSE IT HAS     *
*                     ILLEGAL ACCOUNTING FIELD (2ISG).  TELLS JES2    *
*                     TO SUPPRESS EXEC OF HASPRSCN AND QUEUE THE      *
*                     JOB FOR OUTPUT; INCLUDES THE INCOMPLETE JCL     *
*                     IMAGES LISTING.                                 *
*                                                                     *
*        REGISTERS USED BY 2ISG                                       *
*                                                                     *
*        R3  -  ADDR OF ACCT WORK AREA, JCTWORK IN JCT                *
*        R4  -  ADDR OF RXITFLAG BYTE IN PCE                          *
*        R5  -  ADDR OF JCTXWRK IN JCT                                *
*        R6  -  WORK REGISTER                                         *
*        R7  -  WORK REGISTER                                         *
*        R8  -  UNUSED                                                *
*        R9  -  UNUSED                                                *
*        R10 -  ESTABLISH ADDRESSABILITY TO JCT                       *
*        R12 -  BASE REGISTER OF PGM                                  *
*        R13 -  ESTABLISH ADDRESSABILITY TO PCE                       *
*        R14 -  RETURN ADDR                                           *
*        R15 -  ENTRY ADDR/RETURN CODE                                *
*                                                                     *
* CHNGE LOG:                                                          *
*                                                                     *
*    15NOV85 - CONVERT TO JES2/SP1.3.4, USE CONDITIONAL ASSEMBLIES    *
*              TO MAINTAIN EACH VERSION OF THE CODE              JDM1 *
*    12FEB86 - SKIP SOME SILLY CHECKS FOR TSO USERS, NEEDED TO        *
*              DO THIS WHEN WE BACK OUT SOME OLD TSO MODS.       JDM2 *
*---------------------------------------------------------------------*
         SPACE 2
         AIF   (NOT &SP134).SKIP20                               JDM1
J13X3   $ENTRY BASE=R12
.SKIP20  ANOP                                                    JDM1
         AIF   (&SP134).SKIP25                                   JDM1
J13X3   $ENTRY BASE=R12,CSECT=YES
.SKIP25  ANOP                                                    JDM1
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         $SAVE
         SPACE
         LR    R12,R15            R12 WILL BE OUR BASE REGISTER
         USING JCTDSECT,R10
         USING PCEDSECT,R13
         SPACE
*--------------------------------------------------------------------*
*        FIRST CHECK FOR STC, BYPASS ALL CHECKS IF IT IS
*-----------------------------------------------------------*
         SPACE
         CLI   JCTJOBID,C'S'      STC ???
         BE    EX3RET0
         SPACE
*---------------------------------------------------------------------*
*        NEXT WE NEED TO DO SOME HOUSECLEANING
*---------------------------------------------------------------------*
         SPACE
         L     R3,0(R1)           GET WORD-1 ADDR
         L     R4,4(R1)           GET WORD-2 ADDR
         L     R5,8(R1)           GET WORD-3 ADDR
         SPACE
*---------------------------------------------------------------------*
*        WE MUST MAKE SURE THE RUN JOB CLASS IS VALID.
*---------------------------------------------------------------------*
         SPACE
         CLI   JCTJOBID,C'J'      BATCH JOB???
         BNE   CLSOK
         LA    R6,CLSTAB          LOAD THE ADDRESS OF THE TABLE
LOOP     EQU   *
         CLI   0(R6),X'FF'        ARE WE AT END OF TABLE ??
         BE    CLSERR
         CLC   0(1,R6),JCTJCLAS   IS IT A VALID CLASS??
         BE    CLSOK
         A     R6,=F'1'           BUMP THE POINTER
         B     LOOP
         SPACE
CLSERR   EQU   *
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         MVC   0(L'ERRCLS,R5),ERRCLS
         B     EX3RET12
         SPACE
*---------------------------------------------------------------------*
*        BEGIN OUR MISSION OF VALIDATING THE ACCOUNTING FIELDS
*        THAT 2ISG WANTS DONE.
*---------------------------------------------------------------------*
         SPACE
CLSOK    EQU   *
         LTR   R0,R0              WE MUST TEST TO SEE IF ANY PASSED
         BNZ   CONT1
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         MVC   0(L'ERRACT,R5),ERRACT
         B     EX3RET12
         SPACE
*--------------------------------------------------------------------*
*        NOW WE CHECK THE TYPE TIME CODE LOCATED IN PGM NAME FLD
*        AT THE 9-10 POSITION. FOR SOME STUPID REASON IT MUST BE A
*        NUMBER BETWEEN 10-49. DO THIS FOR ALL BUT TSO USERS. SINCE
*        WHEN WE WENT TO MVS/SP1.3.3 WITH TSO/E, A LOCAL MOD WAS
*        TRASHED TO IKJFLEA THAT SET UP THE PGM'ER NAME FIELD FOR
*        A TSO USER. SINCE TECH SUPPORT PUTS IN THE TSOIDS, IT IS
*        ALL ASSUMED TO BE CORRECT.
*--------------------------------------------------------------------*
         SPACE
CONT1    EQU   *                                                 JDM2
         CLI   JCTJOBID,C'T'                                     JDM2
         BE    EX3RET0                           CONT2           JDM2
         CLC   JCTPNAME+8(1),=X'F1'
         BL    CANPGMN
         CLC   JCTPNAME+8(1),=X'F4'
         BH    CANPGMN
         CLC   JCTPNAME+9(1),=X'F0'
         BL    CANPGMN
         CLC   JCTPNAME+9(1),=X'F9'
         BH    CANPGMN
CONT1A   EQU   *                                                 JDM2
         SPACE
*-------------------------------------------------------------------*
*        NOW WE CHECK THE ACCOUNTING FILEDS
*-------------------------------------------------------------------*
         SPACE
         L     R6,PVSTBL
LOOP1    EQU   *
         CLI   0(R6),X'FF'         END OF TABLE ???
         BE    CANCC               IF SO, WE CANCEL
         CLC   0(2,R6),JCTJNAME    COMPARE 1ST 2 CHARS OF JOBNAME
         BE    CONT2               IF OK, CONTINUE
         A     R6,SIX              BUMP POINTER
         B     LOOP1               DO IT AGAIN
         SPACE
*--------------------------------------------------------------------*
*        NOW WE CHECK THE ACCOUNT CODES
*--------------------------------------------------------------------*
         SPACE
CONT2    EQU   *
         MVC   PCEUSER0(4),1(R3)   MOVE 1ST FLD
         MVC   PCEUSER1(4),6(R3)   MOVE 2ND FLD
         L     R7,2(R6)
LOOP2    EQU   *
         CLI   0(R7),X'FF'
         BE    CANPROJ
         CLC   0(8,R7),PCEUSER0
         BE    EX3RET0
         A     R7,EIGHT
         B     LOOP2
         SPACE
CANPGMN  EQU   *
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         MVC   0(L'ERRTYP,R5),ERRTYP
         B     EX3RET12
         SPACE
CANCC    EQU   *
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         MVC   0(L'ERRCC,R5),ERRCC
         B     EX3RET12
         SPACE
CANPROJ  EQU   *
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         MVC   0(L'ERRPROJ,R5),ERRPROJ
         B     EX3RET12
         SPACE
CANACC1  EQU   *
         MVI   0(R5),C' '         BLANK OUT THE AREA
         MVC   1(79,R5),0(R5)     ROLL THE BLANK
         MVC   0(L'ERRACC1,R5),ERRACC1
         B     EX3RET12
         SPACE
CANACC2  EQU   *
         MVI   0(R5),C' '         BLANK OUT THE AREA
         MVC   1(79,R5),0(R5)     ROLL THE BLANK
         MVC   0(L'ERRACC2,R5),ERRACC2
         B     EX3RET12
         SPACE
CANACC3  EQU   *
         MVI   0(R5),C' '         BLANK OUT THE AREA
         MVC   1(79,R5),0(R5)     ROLL THE BLANK
         MVC   0(L'ERRACC3,R5),ERRACC3
         B     EX3RET12
         SPACE
EX3RET0  EQU   *
         SR    R15,R15            SET FOR NORMAL PROCESSING
EX3RET   $RETURN RC=(R15)         RETURN TO MAINLINE PROCESSING
         SPACE
EX3RET12 EQU   *
         L     R15,RC12           SIGNAL WE HAVE ERROR
         MVI   0(R4),X'20'        TELL WE HAVE MSG
         B     EX3RET             GO EXIT
         EJECT
         SPACE
         LTORG
SIX      DC    F'6'
EIGHT    DC    F'8'
PVSTBL   DC    V(J13X3PVS)
RC12     DC    F'12'
ERRACT   DC    C'ISG0124E JOB CARD ACCOUNT FIELDS MISSING'
ERRCC    DC    C'ISG0128E ILLEGAL COSTCODE (1ST TWO CHARS OF JOBNAME)'
ERRPROJ  DC    C'ISG0132E ILLEGAL PROJECT OR SUB-PROJECT CODE'
ERRTYP   DC    C'ISG0136E ILLEGAL TYPE TIME CODE'
ERRACC1  DC    C'ISG0140E PROJECT OR SUB-PROJECT CODE MISSING'
ERRACC2  DC    C'ISG0142E PROJECT CODE IS NOT 4 CHARS IN LENGTH'
ERRACC3  DC    C'ISG0144E SUB-PROJECT CODE IS NOT 4 CHARS IN LENGTH'
ERRCLS   DC    C'ISG0148E ILLEGAL JOB CLASS'
         SPACE
*-------------------------------------------------------------------*
*        THESE ARE THE VALID JOB CLASSES AT 2ISG.                   *
*-------------------------------------------------------------------*
         SPACE
CLSTAB   EQU   *
         DC    C'Q'                VALID RUN CLASS
         DC    C'A'                VALID RUN CLASS
         DC    C'B'                VALID RUN CLASS
         DC    C'C'                VALID RUN CLASS
         DC    C'D'                VALID RUN CLASS
         DC    C'E'                VALID RUN CLASS
         DC    C'F'                VALID RUN CLASS
         DC    C'I'                VALID RUN CLASS
         DC    C'O'                VALID RUN CLASS
         DC    C'P'                VALID RUN CLASS
         DC    C'S'                VALID RUN CLASS
         DC    C'V'                VALID RUN CLASS
         DC    X'FF'               END OF THE TABLE
         SPACE
         $MODEND
         SPACE
         END
