         TITLE 'S M F X T R K  -   VER 12'
*--------------------------------------------------------------------*
*        THIS IS A COMPLETE REWORK AND EXPANSION OF THE ORIGINAL
*        'SMFXTRCT' WHICH CAME WAY OF BOB HANSEN, WHO WORKED AT THE
*        US SECURITIES EXCHANGE COMMISSION, HUD, AND ONTO EDS.
*        HE DID A GREAT JOB ON THE BASIS FOR THE CODE AND THE ONLY
*        THING WHICH I HAVE HAD TO DO IS TO EXPAND THE CODE.
*--------------------------------------------------------------------*
         SPACE
*--------------------------------------------------------------------*
*        THIS PROGRAM EXTRACTS DATA FROM THE SMF RAW TAPE AND WRITES
*        IT TO AN OUTPUT DATASET UNCHANGED IN FORMAT, USING THE SAME
*        DATA SET CHARACTERISTICS AS THE SMF RAW TAPE (VBS/32760/4096
*        OR WHATEVER). THIS IS ACHIEVED BY COPYING THE INPUT DCB ATTR
*        UNLESS THE OUTPUT DCB ATTRIBUTES ARE TO BE DIFFERENT FROM
*        INPUT. THE RDJFCB MACRO IS USED TO DETERMINE IF THE PERT-
*        INENT CHARACTERISTICS SUCH AS RECFM, LRECL AND BLOCKSIZE ARE
*        DIFFERENT.  IF THEY ARE DIFFERENT, THESE VALUES ARE COPIED
*        INTO THE OUTPUT DCB INSTEAD OF USING THE INPUT DCB VALUES.
*
*        EFFECTIVE WITH THIS VERSION ARE SEVERAL PROGRAM IMPROVEMENTS
*        OVER VERSION 3;
*
*        (1) - IMPROVED PARM FIELD INFORMATION DECODE TO HANDLE MORE
*              THAN ONE PARAMETER IN ANY ORDER.
*        (2)   FIX FOR ERROR MESSAGE ISX001 FOR NO CONTROL CARD OR
*              INSUFFICIENT NUMBER OF CONTROL CARDS
*        (3)   A USER OPTION TO LINK TO THE SORT PGM BY SPECIFYING
*              AN OPTION IN THE PARM FIELD. WHEN THE SORT OPTION IS
*              IS SPECIFIED THE USER MUST SUPPLY THE CONTROL CARDS
*              BEHIND THE CONTROL CARD FOR SMFXTRK. AN INSUFFICIENT
*              INSUFFICIENT NUMBER OF SORT CONTROL CARDS WILL CAUSE
*              AN SMFXTRK ERROR, NOT A SORT ERROR (ISX001).
*
*        THIS PROGRAM REQUIRES THE FOLLOWING DD CARDS:
*        NOTE: CHANGED THE DDNAMES AND JFCB REFERENCES SO
*              LATER ON IT WILL BE EASIER TO MAINTAIN AND
*              ENHANCE. PLAN ON PUTTING NEW FUNCTION.
*
*              SMFXPRT  -- PRINTED OUTPUT DATA SET
*              SMFXIN   -- INPUT SMF DATASET
*              SMFXOUT  -- OUTPUT EXTRACTED RECORD DATASET
*              SYSXIN   -- CONTROL CARD INPUT
*
*              IN ADDITION, ALL NECESSARY JCL FOR SORT SHOULD BE
*              INCLUDED IF PARM=SORT WILL BE SPECIFIED.
*
*        MODIFICATION MAKE TO PRINT THE TOTALS OF EACH RECORD     JDM
*        TYPE OUTPUTED.    CAPT JIM MARSHALL                      JDM
*        23 MARCH 1982.    MODIFICATIONS MADE TO PROGRAM FOR THE  RVH
*                          FOLLOWING AREAS:                       RVH
*           1.  ADDED MASK ROUTINE                                RVH
*           2.  ADDED JOBNAME ROUTINE                             RVH
*           3.  CHANGED CONTROL CARD ROUTINE                      RVH
*           4.  FIXED SOME BUGS (BAD BASE REG FOR ONE)            RVH
*           5.  STARTED TO USE REGISTER EQUATES                   RVH
*           6.  FIXED S0C4, RECORD BUFFER WAS TOO SMALL, MADE IT
*               32768 TO TAKE ALL CASES. RUNS IN MVS/XA, 11/25/86JDM1
*--------------------------------------------------------------------*
         SPACE
*--------------------------------------------------------------------*
*        15JUL90  CLEANED UP THE CODE. NO LOGIC CHANGES    J MARSHALL
*        19FEB91  ADDED DSN TO LISTING TO HELP IDENTIFY WHAT THE
*                 DATA IS.                                       JDM2
*         5MAR91  PRT OF RECORD COUNTERS TOO SMALL, GENERATING
*                 ALOT OF SMF RECORDS                            JDM3
*         6MAR91  RECORD SIZES MIN AND MAX TOO SMALL FOR MVS/XA  JDM4
*        25AUG92  CLEANUP CODE PRIOR TO ESA CONVERSION, CHANGES
*                 WILL MAINLY BE COSMETIC AND TOO FEW TO MARK
*        27AUG92  IF BLKSIZE IN SMFXPRT IS SPECIFIED IN JCL, CHANGE
*                 THE DEFAULT OF 133.                            JDM5
*        28DEC92  CHANGE CODE TO SELECT ON JOBNAME OR SOME PART OF
*                 THE NAME, LIKE MASKING                         JDM6
*        21NOV96  JOBNAME AND DATE BUG FIXED TO EXPAND DATE
*                 CONTROL CARD FIXES JOBNAME SELECTION.          JDM7
*        25NOV96  ADDED ACCOUNT CODE EXTRACT ROUTINE FOR US IN
*                 MY CHARGE BACK SYSTEM.                         JDM8
*        24JUL98  UPDATED FOR Y2K.                               JDM9
*
*   CONTROL CARD(S) FORMAT  COL 1-70 WITH AN "*" IN 72 FOR CONT.  RVH*
*                                                                 RVH*
*   1    RECTYPE=ALL                                              RVH*
*               =X                                                RVH*
*               =(X,Y,Z)             MAX OF 20                    RVH*
*                                                                 RVH*
*   2    EXCLUDE=ALL                                              RVH*
*               =X                                                RVH*
*               =(X,Y,Z)             MAX OF 20                    RVH*
*                                                                 RVH*
*   3    DATE=YYYYDDD                                        RVH  JDM9
*            =(YYYYDDD)              OTHER FORMAT FOR DATE   JDM7 JDM9
*            =(YYYYDDD,YYYYDDD)      RANGE OF DATES          RVH  JDM9
*                                                                 RVH*
*   4    NUMBER=XXXXXXX              OUTPUT RECORD COUNT LIMIT    RVH*
*                                                                 RVH*
*   5    SUBCODE=XXXX                                             RVH*
*               =(XXXX,YYYY)         MAX OF 5                     RVH*
*                                                                 RVH*
*   6    TIME=HH.MM.SS                                            RVH*
*            =(HH.MM.SS,HH.MM.SS)    RANGE OF TIMES               RVH*
*                                                                 RVH*
*   7    FIELD=(DISP,LENGTH,VALUE)   DISP=OFFSET+4 FOR RDW        RVH*
*                                                                 RVH*
*   8    MASK=(DISP,OPERATOR,MASK)   OPERATOR = BO,BM,BZ,BNO      RVH*
*                                    SELECT IF OPER IS TRUE       RVH*
*                                                                 JDM8
*   9    ACCT=AAAAAAAAAAAA           ACCOUNT CODE: 12 CHARS MAX   JDM8
*-----------------------------------------------------------------RVH*
         SPACE
*--------------------------------------------------------------------*
*        SECTION ESTABLISHES BASE REGS, LINKAGE CONVENTIONS, AND
*        OPENS THE DCB'S FOR THE DATA SETS. THE PGM HEADER IS PRINTED
*        AND THE JFCB IS READ IN TO DETERMINE ANY IF ANY CHANGES WERE
*        BY THE USER IN THE DCB ATTRIBUTES OF THE OUTPUT FILE. IF
*        THERE WERE, THE DCB ATTRIBUTES SUPPLIED BY THE USER ON THE
*        SMFXOUT CARD ARE SUBSTITUTED INTO THE OUTPUT DCB FOR SMFXOUT.
*        OUTPUT DCB FOR SYSUT2.  IF NO CHANGES ARE INDICATED, DCB
*        ATTRIBUTES ARE COPIED FROM SMFXIN. IF THE DCB ATTRIBUTES ARE
*        FOR OUTPUT, A MSG CONCERNING THE NEW DCB IS PRINTED.
*--------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                  3RD BASE REG
R11      EQU   11                  2ND BASE REG
R12      EQU   12                  BASE REG
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
SMFXTRCT AMODE 24
SMFXTRCT RMODE 24
SMFXTRCT CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING SMFXTRCT,R12,R11,R10
         LA    R11,4095(R12)
         LA    R11,1(R11)
         LA    R10,4095(R11)
         LA    R10,1(R10)          3 BASE REGISTERS - MY, MY...
         ST    R13,SAVE+4
         LA    R9,SAVE
         ST    R9,8(R13)
         LR    R13,R9
         LR    R2,R1               ADDR OF FULLWD PTER TO PARM AREA
         SPACE
*--------------------------------------------------------------------*
*        LOAD R2 WITH ADDRESS OF PARM AREA. THIS IS DONE BECAUSE     *
*        THE CHECK FOR PARM INFORMATION  OCCURS AFTER THE PUT        *
*        MACROS FOR THE TITLE, WHICH USE REGISTER 1.                 *
*                                 WHICH USE REGISTER 1.              *
*--------------------------------------------------------------------*
         SPACE
         L     R2,0(R2)           LOAD R2 WITH ADDRESS OF PARM AREA
         SPACE
*--------------------------------------------------------------------*
*        NOW WE ARE GOING TO BE NICE TO THE USER. IF THE USER DID    *
*        NOT SPECIFY DCB VALUES, COPY THE INPUT ONES.  IF CERTAIN    *
*        ONES ARE CHANGED, TAKE THOSE OVER THE INPUT. AREN'T WE NICE!*
*        ADDED CODE TO USE IBM DSECT'S (INCASE IBM CHANGES THINGS)   *
*--------------------------------------------------------------------*
         SPACE
         OPEN  (SYSXIN,,SMFXIN)
         RDJFCB (SMFXIN)                                         JDM2
         RDJFCB (SMFXOUT)
         LA    R15,SMFXIN          GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         MVC   INRECFM,DCBRECFM    SAVE OFF RECFM
         MVC   INBLKSI,DCBBLKSI    SAVE OFF BLKSIZE
         MVC   INLRECL,DCBLRECL    SAVE OFF LRECL
         DROP  R15
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         LA    R1,JFCBXOUT         GET ADDRESS OF JFCB AREA
         USING INFMJFCB,R1         ESTABILISH ADDRESSABILITY
         CLC   JFCBLKSI,=X'0000'   SEE IF USER OVERRODE BLOCKSIZE
         BNE   CHBLKSIZ            IF NOT, PROCEED
         MVC   DCBBLKSI,INBLKSI    COPY BLKSIZE FROM SMFXIN
         BE    CLRECL
CHBLKSIZ EQU   *
         MVC   DCBBLKSI,JFCBLKSI   COPY BLOCKSIZE FROM JFCB
CLRECL   EQU   *
         CLC   JFCLRECL,=X'0000'   SEE IF USER OVERRODE LRECL
         BNE   CHLRECL             IF NOT PROCEED
         MVC   DCBLRECL,INLRECL    COPY LRECL FROM SMFXIN
         B     CRECFM
         SPACE
CHLRECL  EQU   *
         MVC   DCBLRECL,JFCLRECL   COPY LRECL FROM JFCB
         SPACE
CRECFM   EQU   *
         CLI   JFCRECFM,X'00'      SEE IF USER OVERRODE RECFM
         BNE   CHRECFM             IF NOT, PROCEED
         MVC   DCBRECFM,INRECFM    COPY RECFM FROM SMFXIN
         B     OPENOUT             IF NOT, PROCEED
         SPACE
CHRECFM  EQU   *
         MVC   DCBRECFM,JFCRECFM   COPY RECFM FROM JFCB
         DROP  R1
         DROP  R15
         SPACE
OPENOUT  EQU   *
         RDJFCB (SMFXPRT)          GET INFO FROM JCL CARD        JDM5
         LA    R15,SMFXPRT         GET ADDRESS OF DCB            JDM5
         USING IHADCB,R15          ESTABLISH ADDRESSABILITY      JDM5
         LA    R1,JFCBXPRT         GET ADDRESS                   JDM5
         USING INFMJFCB,R1         ESTABLISH ADDRESSABILITY      JDM5
         CLC   JFCBLKSI,=X'0000'   USER OVERRODE BLOCKSIZE ?     JDM5
         BE    OPT0                                              JDM5
         MVC   DCBBLKSI,JFCBLKSI   COPY JFCB BLOCKSIZE           JDM5
         DROP  R1                                                JDM5
         DROP  R15                                               JDM5
OPT0     EQU   *                                                 JDM5
         OPEN  (SMFXOUT,(OUTPUT),SMFXPRT,(OUTPUT))
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         TM    DCBRECFM,DCBRECU    TEST FOR RECFM=U.
         BM    OPT1                IF NOT U, IT IS EITHER F OR V.
         B     OPT2                IF RECFM=U, TREAT IT AS V FOR NOW...
         SPACE
OPT1     EQU   *
         TM    DCBRECFM,DCBRECF
         DROP  R15
         BZ    OPT2
         LA    R3,WORKA+4
         ST    R3,AWORK
         B     PRNTTITL
         SPACE
OPT2     EQU   *
         LA    R3,WORKA
         ST    R3,AWORK            NECESSARY BECAUSE REG 3 IS USED
*                                  IN THE NEXT SECTION IN A BCT.
PRNTTITL EQU   *
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F1'
         MVC   PRINTOUT+39(54),ALLSTARS
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+40(52),ALLSPACE
         PUT   SMFXPRT,PRINTOUT
         MVC   PRINTOUT+41(50),=C'SMFXTRK  - SMF DATA EXTRACTION PROGRAX
               M - VER  12  '                                    JDM4
         PUT   SMFXPRT,PRINTOUT
         MVC   PRINTOUT+40(52),ALLSPACE
         PUT   SMFXPRT,PRINTOUT
         MVC   PRINTOUT+40(52),ALLSTARS
         PUT   SMFXPRT,PRINTOUT
         SPACE 1                                                 JDM2
*----------------------------------------------------------------JDM2-*
*        ADD INFO LINES ON REPORT FOR INPUT DSN= AND OUTPUT DSN= JDM2 *
*        NOTE: SINCE WE WOULD NEVER PUT SMF DATA AS A MEMBER OF  JDM2 *
*              A PDS (INSANE PEOPLE MIGHT) WE WILL NOT PRINT     JDM2 *
*              MEMBER NAME IF THERE IS ONE.                      JDM2 *
*----------------------------------------------------------------JDM2-*
         SPACE 1                                                 JDM2
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         PUT   SMFXPRT,PRINTOUT                                  JDM2
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+1(11),=CL11'INPUT  DSN='                 JDM2
         MVC   PRINTOUT+12(44),JFCBXIN   MOVE IN DSNIN           JDM2
         PUT   SMFXPRT,PRINTOUT                                  JDM2
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+1(11),=CL11'OUTPUT DSN='                 JDM2
         MVC   PRINTOUT+12(44),JFCBXOUT  MOVE IN DSNOUT          JDM2
         PUT   SMFXPRT,PRINTOUT                                  JDM2
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         PUT   SMFXPRT,PRINTOUT                                  JDM2
         SPACE 1                                                 JDM2
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         CLC   DCBRECFM,INRECFM
         BNE   DIFFDCB
         CLC   DCBBLKSI,INBLKSI
         BNE   DIFFDCB
         CLC   DCBLRECL,INLRECL
         BNE   DIFFDCB
         DROP  R15
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+1(58),=C'OUTPUT DATASET WILL HAVE SAME DATASET X
               ATTRIBUTES AS INPUT.'
         PUT   SMFXPRT,PRINTOUT
         B     SEEPARM
         SPACE
DIFFDCB  EQU   *
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+1(40),=C'OUTPUT DATASET HAS FOLLOWING ATTRIBUTES
               S:'
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'
         MVC   PRINTOUT+10(8),=C'RECFM = '
         LA    R4,PRINTOUT+19
TM1      EQU   *
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         TM    DCBRECFM,DCBRECU    TEST FOR RECFM=U
         BO    *+8
         B     TM2
         MVC   0(1,R4),=C'U'
         B     PRTRECFM
         SPACE
TM2      EQU   *
         TM    DCBRECFM,DCBRECF    TEST FOR RECFM=F
         BZ    TM3                 IF NOT U OR F, IT MUST BE V...
         MVC   0(1,R4),=C'F'       MOVE OUT INDICATOR
         LA    R4,1(R4)            POINT TO NEXT PRINT POSITION
         B     TM4                 NO POINT GOING TO THE TM FOR RECFM=V
*                                  IF YOU ALREADY KNOW IT IS F.
         SPACE
TM3      EQU   *
         TM    DCBRECFM,DCBRECV    TEST FOR RECFM=V..
         BZ    TM4
         MVC   0(1,R4),=C'V'
         LA    R4,1(,R4)
TM4      EQU   *
         TM    DCBRECFM,DCBRECBR   TEST FOR RECFM=.B.
         BZ    TM5
         MVC   0(1,R4),=C'B'
         LA    R4,1(,R4)
TM5      EQU   *
         TM    DCBRECFM,DCBRECSB   TEST FOR RECFM=..S
         DROP  R15
         BZ    PRTRECFM
         MVC   0(1,R4),=C'S'
PRTRECFM EQU   *
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+10(8),=C'LRECL = '
         LA    R5,0
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         LH    R5,DCBLRECL         MOVE IN LRECL
         DROP  R15
         CVD   R5,DBLWORD
         MVC   PRINTOUT+18(6),=X'402020202021'
         ED    PRINTOUT+18(6),DBLWORD+5
         PUT   SMFXPRT,PRINTOUT
         MVC   PRINTOUT+10(8),=C'BLKSZ = '
         LA    R15,SMFXOUT         GET ADDRESS OF DCB
         USING IHADCB,R15          ESTABILISH ADDRESSABILITY
         LH    R5,DCBBLKSI         MOVE IN BLKSIZE
         DROP  R15
         CVD   R5,DBLWORD
         MVC   PRINTOUT+18(6),=X'402020202021'
         ED    PRINTOUT+18(6),DBLWORD+5
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         SPACE
*--------------------------------------------------------------------*
*        DETERMINE EXISTENCE OF PARM INFORMATION ON THE EXEC CARD,   *
*        AND TRANSFER IT TO A PARM WORK AREA.  PARM INFO WILL ALSO   *
*        BE PRINTED OUT IN THIS SECTION, AND ANY ACTION  INDICATED   *
*        BY THE PARM INFO WILL BE TAKEN AT THIS TIME.                *
*--------------------------------------------------------------------*
         SPACE
SEEPARM  EQU   *
         CLC   0(2,R2),=X'0000'    SEE IF PARM COUNT IS ZERO
         BE    NOPARM              IF SO, NO PARM INFO EXISTS
         MVI   PARMAREA,X'40'
         MVC   PARMAREA+1(99),PARMAREA
         LA    R3,0               INITIALIZE REG 3 TO 0.
         LH    R3,0(R2)           LOAD REG 3 WITH NUMBER OF BYTES
*                                 OF PARM INFO.
         LA    R4,PARMAREA        LOAD REG 4 WITH ADDR OF PARMAREA.
         LA    R2,2(R2)           LOAD REG 2 WITH ADDR OF FIRST BYTE
*                                 OF PARM INFO.
         ST    R2,PARMSAVE        STORE ADDRESS FOR USE LATER ON.
PARMA    EQU   *
         CLI   0(R2),C','         IS THIS CHARACTER A DELIMITING COMMA
         BE    ANALYZEP           IF SO GO TO ANALYZE PREVIOUS PARM
         BCT   R3,*+18            IF MORE PARM INFO, KEEP GOING
         MVI   MOREPARM,C'N'      SET SWITCH TO SHOW THIS IS LAST PARM.
         MVC   0(1,R4),0(R2)      MOVE LAST BYTE OF PARM INFO OUT
         B     ANALYZEP           GO ANALYZE THE PARM INFO
         MVC   0(1,R4),0(R2)      MOVE A BYTE OF PARM INFO TO TEST AREA
         LA    R4,1(R4)           POINT TO NEXT SLOT IN TEST AREA.
         LA    R2,1(R2)           POINT TO NEXT CHARACTER IN PARM FIELD
         B     PARMA               GO BACK AND DO IT AGAIN.
         SPACE
ANALYZEP EQU   *
         CLC   PARMAREA(14),=C'ABEND ON ERROR'   WAS THIS PARM SPEC?
         BE    PARMB              IF SO SET APPROPRIATE SWITCH.
         CLC   PARMAREA(14),=C'ABEND ON EOJ  '   WAS THIS PARM SPEC?
         BE    PARMC              SET APPROPRIATE SWITCH
         CLC   PARMAREA(14),=C'SORT          '   WAS THIS PARM SPEC?
         BE    PARMD              SET APPROPRIATE SWITCH.
         B     PARME              OTHERWISE PARM HAS NO SPECIAL MEANING
*                                 AND IS ASSUMED INFORMATIONAL.
         SPACE
PARMB    EQU   *
         MVI   ABENDSW,C'E'
         B     PARME
         SPACE
PARMC    EQU   *
         MVI   ABENDSW,C'J'
         B     PARME
         SPACE
PARMD    EQU   *
         MVI   SORTSW,C'S'
PARME    EQU   *
         LA    R4,PARMAREA        RESET REG 4 TO BEGINNING OF TEST AREA
         MVI   PARMAREA,X'40'
         MVC   PARMAREA+1(99),PARMAREA
         CLI   MOREPARM,C'N'      IS THIS THE LAST PARM?
         BE    ENDPARM
         LA    R2,1(R2)           POINT PAST COMMA
         B     PARMA
         SPACE
ENDPARM  EQU   *
         L     R2,PARMSAVE        LOAD REG 2 WITH ADDR OF PARM INFO
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'     CLEAR OUT PRINT LINE
         MVC   PRINTOUT+1(5),=C'*****' PARM INFO SIGNAL
         LA    R4,PRINTOUT+7      POINT REG 4 TO NEXT FIELD OF PRINTOUT
         LA    R3,0
         S     R2,=F'2'           POINT TO PARM LENGTH FIELD
         LH    R3,0(R2)           LOAD LENGTH OF PARM INFO INTO REG 3.
         EX    R3,MVCPARM         USE 3 AS LENGTH FACTOR FOR MVCPARM
         B     PRNTPARM           GO AND PRINT THE PARM INFO.
         SPACE
MVCPARM  MVC   0(0,R4),2(R2)      VARIABLE-LENGTH MOVE INSTRUCTION
         SPACE
PRNTPARM EQU   *
         PUT   SMFXPRT,PRINTOUT
NOPARM   EQU   *
         SPACE 3
*---------------------------------------------------------------------*
*              ANALYZE CONTROL CARD FOR PARAMETERS                    *
*---------------------------------------------------------------------*
*        THIS SECTION IS RESPONSIBLE OF DECODING ALL CONTROL CARD    *
*        PARAMETERS AND SETTING UP CONTROL DATA WHEN NEEDED.  THE    *
*        MODUS OPERANDI IS SIMPLY THIS:  EACH TIME A PARTICULAR PARA-*
*        METER IS DECODED, THE CORRESPONDING FUNCTION BYTE IS SET TO *
*        A CHARACTER 1 (F1).  OTHERWISE IT REMAINS ZERO.  WHERE TWO  *
*        PARAMETERS ARE MUTUALLY EXCLUSIVE, THE CODING FOR THAT PARA-*
*        METER TURNS ON ITS OWN FUNCTION SWITCH AND ALSO TURNS OFF   *
*        THE FUNCTION BYTES OF THOSE PARAMETERS IT IS MUTUALLY EXCLU-*
*        SIVE TO.  THUS, THE LAST PARAMETER CODED HAS THE FINAL      *
*        INFLUENCE.  LATER ON, IN THE RECORD SELECTION SECTION,      *
*        DIFFERENT SUBROUTINES ARE GONE TO BASED ON THE SETTING      *
*        OF THE FUNCTION BYTES IN THIS SECTION.                      *
*                                                                    *
*        IN THIS SEGMENT OF THE PROGRAM, REGISTER 9 IS USED AS A     *
*        POINTER TO THE CONTROL CARD WORK AREA.  REGISTER 9 ALWAYS   *
*        POINTS TO THE BEGINNING OF THE FIELD UNDER ANALYSIS.  OTHER *
*        REGISTER ASSIGNMENTS ARE MADE WITHIN THE ANALYSIS ROUTINES  *
*        THEMSELVES, AND ARE INDEPENDENT OF EACH OTHER.  THE INTEG-  *
*        RITY OF REGISTER 9, HOWEVER, IS MAINTAINED THROUGHOUT  ALL  *
*        ROUTINES IN THIS SECTION.                                   *
*---------------------------------------------------------------------*
         SPACE
         GET   SYSXIN,CARDIN    READ IN CONTROL CARD
CC1      EQU   *
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'     BLANK OUT PRINT LINE
         MVC   PRINTOUT+1(5),=C'*****'
         MVC   PRINTOUT+6(19),=C'CONTROL CARD IMAGE:'
         MVC   PRINTOUT+26(80),CARDIN                            RVH
         PUT   SMFXPRT,PRINTOUT
         LA    R9,CARDIN          LOAD ADDR FOR SEARCH ROUTINES  RVH
         B     CCCHECK            AND GO TO THE SEARCH ROUTINES  RVH
         SPACE
NXTCCARD EQU   *
         GET   SYSXIN,CARDIN      READ NEXT CONTROL CARD         RVH
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+26(80),CARDIN                            RVH
         PUT   SMFXPRT,PRINTOUT
         LA    R9,CARDIN          LOAD ADDR OF CARD IMAGE INTO REG 9
CCCHECK  EQU   *
         CLC   0(4,R9),=C'RECT'   WAS RECTYPE CODED?
         BE    RECTYPE            IF SO GO TO RECTYPE DECODE ROUTINE
         CLC   0(4,R9),=C'EXCL'   WAS EXCLUDE CODED?
         BE    EXCLUDE            GOTO EXCLUDE DECODE ROUTINE
         CLC   0(4,R9),=C'DATE'   WAS DATE CODED?
         BE    DATE               GOTO DATE DECODE ROUTINE.
         CLC   0(4,R9),=C'SUBC'   WAS SUBCODE CODED?
         BE    SUBCODE            GOTO SUBCODE DECODE ROUTINE
         CLC   0(4,R9),=C'OPRC'   WAS OPRCODE CODED?
         BE    OPRCODE            GOTO OPRCODE DECODE ROUTINE
         CLC   0(4,R9),=C'JOBN'   WAS JOBNAMES CODED?
         BE    JOBNAMES           GOTO JOBNAMES DECODE ROUTINE
         CLC   0(4,R9),=C'NUMB'   WAS NUMBER PARAMETER CODED?
         BE    NUMBER             GOTO NUMBER DECODE RTN
         CLC   0(4,R9),=C'TIME'   TIME PARAMETER CODED?
         BE    TIME               GOTO TIME DECODE RTN
         CLC   0(4,R9),=C'FIEL'   FIELD PARAMETER SPECIFIED?
         BE    FIELD              GOTO FIELD DECODE ROUTINE
         CLC   0(4,R9),=C'ACCT'   ACCOUNT CODE SELECTION?        JDM8
         BE    ACCT               GOTO ACCT ROUTINE              JDM8
         CLC   0(4,R9),=C'MASK'   MASK PARAMETER SPECIFIED?      RVH
         BE    MASK               GOTO MASK DECODE ROUTINE       RVH
         CLI   0(R9),C' '         IS THERE A PARAMETER AT ALL?   RVH
         BNE   ISX004             IF NOT GOTO ERR1 RTN BECAUSE   RVH
*                                 INVALID KEYWORD CONTROL CARD.  RVH
         CLI   CARDIN+71,C' '     ELSE - IS THERE A CONT?        RVH
         BE    GETRECZ            NO   - GO DO THE PROCESSING    RVH
         B     NXTCCARD           YES  - GO GET THE NEXT CARD    RVH
         SPACE
*--------------------------------------------------------------------*
*        THE INDIVIDUAL PARAMETER-ANALYSIS ROUTINES WHICH FOLLOW
*        EACH START ON A NEW PAGE.  EACH ONE HAS THE TASK OF ANALYZING
*        THE PARAMETER FOR CORRECTNESS, AND BRANCHING TO ANY ERROR
*        MESSAGE ROUTINES WHICH ARE NEEDED; THEY SET UP ALL NECESSARY
*        DEFAULTS IF NEEDED, AND THEY CHECK FOR THE EXISTENCE OF THE
*        NEXT PARAMETER (AND POINT TO IT IF IT EXISTS).  CONTROL IS
*        THEN RETURNED TO THE CCCHECK ROUTINE OR TO GETRECZ, AS
*        APPROPRIATE.
*--------------------------------------------------------------------*
         SPACE
*--------------------------------------------------------------------*
*        RECTYPE  -  RECTYPE PARAMETER DECODE ROUTINE
*--------------------------------------------------------------------*
*        RECTYPE (AND EXCLUDE) WORK BY GENERATING A TABLE OF RECORD
*              TYPES WHICH ARE MATCHED AGAINST INCOMING SMF RECORDS
*              LATER ON IN THE PROGRAM.  THIS SECTION GENERATES THAT
*              TABLE.
*
*              REGISTER CONVENTIONS FOR THIS SECTION ARE AS FOLLOWS:
*
*              3:  WORK REGISTER FOR NUMBER CONVERSIONS.
*              4:  ADDRESS REGISTER.  THE LAST INSTRUCTION OF THE
*                  PARAMETER CONVERSION PORTION OF THE
*                  RECTYPE PARAMETER CONTAINS A BR 4 INSTRUCTION.
*                  THIS SECTION STARTS AT STATEMENT R2 AND RUNS
*                  ON DOWN TO THE BR 4 INSTRUCTION.
*                  NORMALLY REGISTER 4 HAS IN IT THE ADDRESS OF CCCHECK
*                  SO THAT AFTER THE RECTYPE PARAMETER HAS BEEN FULLY
*                  CHECKED, RETURN WILL BE MADE TO CCCHECK.  HOWEVER,
*                  IF THE MULTIPLE-RECORD-FORMAT OPTION OF RECTYPE
*                  IS USED THE SERIES OF INSTRUCTIONS FROM R2 ON DOWN
*                  TO THE BR4 WILL HAVE TO BE REPEATEDLY EXECUTED FOR
*                  EACH PARAMETER CODED WITHIN THE PARENTHESIS.
*                  THUS, PART OF THE FUNCTION OF "RMANYREC" IS TO SET
*                  THE ADDRESS IN REGISTER 4 TO CAUSE A RETURN TO AN
*                  INSTRUCTION WHICH CHECKS TO SEE IF THERE ARE ANY
*                  MORE RECORD TYPES.  IF THERE ARE, THE CODING
*                  STARTING AT R2 IS RE-ENTERED, THIS TIME AS A
*                  SUBROUTINE.
*              6:  AS AN ADDRESS POINTER FOR RECTABLE, WHERE ALL THE
*                  RECORD TYPES TO BE SELECTED OR EXCLUDED RESIDE.
*              7:  ADDRESS POINTER TO HOLDREC.   HOLDREC IS A 3-BYTE
*                  FIELD USED TO RIGHT-JUSTUFY THE RECORD TYPES CODED
*                  ON THE RECTYPE/EXCLUDE PARAMETER PRIOR TO THEIR
*                  BEING CONVERTED TO BINARY AND STORED IN RECTABLE.
*              8:  USED AS A COUNTER TO DETERMINE HOW MANY DIGITS EACH
*                  RECTYPE SUBPARAMETER HAS.
*              9:  STANDARD USAGE AS A POINTER TO THE CONTROL CARD
*                  WORK AREA.
*---------------------------------------------------------------------*
         SPACE
RECTYPE  EQU   *
         LA    R9,4(R9)           POINT PAST 1ST FOUR TO SHORTEN SCAN
         LA    R6,XCBRCTBL        LOAD R6 WITH ADDR OF REC TABLE
         LA    R4,CCCHECK         LOAD REG 4 WITH ADDRESS OF CCCHECK
         MVI   XCBIND0,C'1'       ENABLE RECTYPE CHECK
         MVI   XCBIND1,C'0'       DISABLE EXCLUDE CHECK BECAUSE M.E.
REC0     EQU   *
         CLI   0(R9),C'='         CHECK FOR EQUAL SIGN
         BE    REC1               WHEN FOUND GO TO R1
         LA    R9,1(R9)           OTHERWISE INCREMENT ADDR IN R9 BY 1
         B     REC0                  AND GO TRY IT AGAIN.
         SPACE
REC1     EQU   *
         LA    R9,1(R9)           POINT PAST = SIGN.
         CLC   0(3,R9),=C'ALL'    RECTYPE=ALL CODED?
         BNE   *+16+4             IF NOT JUMP AHEAD 20 AND CONTINUE
         MVI   XCBIND9,C'1'       SET FOR RCTYPE=ALL CONDITION...
         MVI   XCBIND0,C'0'       RECTYPE=ALL IMPLIES NO RECTYPE EDIT
         LA    R9,4(R9)           POINT TO NEXT PARM, IF THERE IS ONE
         B     CCCHECK
         LA    R7,HOLDREC         LOAD R7 WITH ADDR OF HOLDREC
         LA    R8,0               INITIALIZE R8 TO ZERO.
         CLI   0(R9),C'('         IS THIS CHARACTER A (?
         BE    RMANYREC           IF SO, INDICATES MORE THAN 1 REC-TYPE
REC2     EQU   *
         MVC   0(1,R7),0(R9)      MOVE FIRST DIGIT TO HOLDREC
         LA    R7,1(R7)           POINT TO NEXT SLOT IN HOLDREC
         LA    R8,1(R8)           COUNT NUMBER OF DIGITS IN HOLDREC
         C     R8,=F'3'           MORE THAN 3 CHARACTERS?
         BH    ISX016             IF SO FLAG THE ERROR
         LA    R9,1(R9)           POINT TO NEXT SLOT IN CARDIN.
         CLI   0(R9),C','         IS THIS CHARACTER A COMMA?
         BE    RCOMMA             IF SO GO TO RCOMMA
         CLI   0(R9),C')'         IS THIS CHARACTER A )?
         BE    RPAREN             IF SO GO TO RPAREN
         CLI   0(R9),C' '         IS THIS CHARACTER A SPACE?
         BE    RCOMMA             IF SO NO MORE PARAMETERS EXIST.
         B     REC2               GO BACK TO MOVETHE OTHER CHARACTERS
         SPACE
RPAREN   EQU   *
         MVI   MULTISW,C'0'       TURN OFF MULTISW
RCOMMA   EQU   *
         C     R8,=F'1'           1 CHARACTER CODED?
         BNE   REC3               IF NOT GO TO R3
         MVC   HOLDREC3,HOLDREC1  RIGHT JUSTIFY THE DIGIT IN HOLDREC
         MVC   HOLDREC(2),=C'00'  ZERO OUT 2 HI-ORDER BYTES.
         B     RPACKIT            GO TO PACK THE NUMBER IN HOLDREC
         SPACE
REC3     EQU   *
         C     R8,=F'2'           IS THE NUMBER IN HOLDREC 2 DIGITS?
         BNE   RPACKIT            IF NOT, HOLDREC MUST BE FULL, SO
*                                 NO RIGHT JUSTIFICATION IS NEEDED.
         MVC   HOLDREC3,HOLDREC2  RIGHT JUSTIFY UNITS PLACE.
         MVC   HOLDREC2,HOLDREC1  RIGHT JUSTIFY TENS PLACE.
         MVI   HOLDREC,C'0'       ZERO OUT HI-ORDER BYTE.
RPACKIT  EQU   *
         PACK  DBLWORD,HOLDREC    PACK HOLDREC INTO DBLWORD SO IT CAN
         OI    DBLWORD+7,X'0F'    OR THE LAST BYTE FOR PROPER SIGN
         UNPK  TESTRECT,DBLWORD+6(2)  UNPACK TO DO NUMERIC TEST
         CLC   TESTRECT,HOLDREC   IF EQUAL, DATA WAS NUMERIC
         BNE   ISX018             IF NOT EQUAL, VALUE IN HOLDREC IS BAD
         CVB   R3,DBLWORD             BE CONVERTED TO BINARY INTO R3
         STC   R3,0(R6)           STORE RIGHTMOST CHARACTER INTO FIRST
*                                 ELEMENT OF RECTABLE .
         LA    R6,1(R6)           POINT REG 6 TO NEXT BYTE OF RECTABLE
         LA    R7,HOLDREC         RESET ADDR IN REG 7 TO BEGINNING.
         LA    R8,0               REINITIALIZE R8 TO ZERO.
         AP    ELEMENTS,=P'1'     COUNT # OF ELEMENTS IN RECTABLE.
         CP    ELEMENTS,=P'20'    ARE THERE 20 RECORD TYPES YET?
         BH    ISX010             MORE THAN 20 WILL OVERFLOW RECTABLE
         LA    R9,1(R9)           POINT PAST COMMA OR PAREN THAT
*                                 SENT YOU INTO THIS ROUTINE.
         BR    R4                 BRANCH TO THE ADDRESS IN REG 4.
         SPACE
RMANYREC EQU   *
         MVI   MULTISW,C'1'       TURN MULTISW ON.
         LA    R9,1(R9)           POINT PAST ( TO FIRST RECORD TYPE
         LA    R7,HOLDREC         LOAD R7 WITH ADDR OF HOLDREC
         LA    R8,0               RESET REG 8 TO 0.
         BAL   R4,REC2            GO AND EXECUTE THE CHARACTER MOVE
*                                 AND TRANSLATION PROCESS.
RRETURN  EQU   *
         CLI   MULTISW,C'1'       IS MULTISW STILL ON?
         BNE   RSPACE             SEE IF NEXT PARAMETER CODED.
         LA    R7,HOLDREC         LOAD R7 WITH ADDR OF HOLDREC
         LA    R8,0               RESET REG 8 TO 0.
         BE    REC2               EXECUTE THE CHAR MOVE/TRANS. ROUTINE
RSPACE   EQU   *
         CLI   0(R9),C' '         IS THIS CHARACTER A SPACE?
         BE    GETRECZ            NO MORE PARMS, THEN.
         CLI   0(R9),C','         IS THIS CHARACTER A COMMA?
         BNE   ISX007             IF NOT ERROR MESSAGE TIME.
         LA    R9,1(R9)           POINT REG 9 PAST COMMA
         B     CCCHECK
         SPACE
*---------------------------------------------------------------------
*        EXCLUDE  -  EXCLUDE DECODE ROUTINE
*---------------------------------------------------------------------
         SPACE
EXCLUDE  EQU   *
         MVI   XCBIND1,C'1'       ENABLE EXCLUDE CHECK
         MVI   XCBIND0,C'0'       DISABLE RECTYPE IF ALREADY ENABLED
         LA    R9,4(R9)           POINT AHEAD A LITTLE
         LA    R6,XCBRCTBL        LOAD R6 WITH ADDR OF REC TABLE
         LA    R4,CCCHECK         LOAD REG 4 WITH ADDR OF CCCHECK
         B     REC0               USE RECTYPE CODING TO BUILD TABLE
         SPACE
*---------------------------------------------------------------------
*        DATE   -   DATE PARAMETER DECODE ROUTINE
*                                                                JDM7
*        ADD A NEW DATE FORMAT DATE=(YYYYDDD)  WHICH WILL   JDM7 JDM9
*        CURE ANOTHER PROBLEM I DISCOVERED TRYING TO GO AFTER    JDM7
*        A CERTAIN JOBNAME.  CODED THE ABOVE AND IT APPEARED     JDM7
*        TO WORK BUT TOLD ME NO RECORDS WERE SELECTED FOR DATE.  JDM7
*---------------------------------------------------------------------
         SPACE
DATE     EQU   *
         MVI   XCBIND2,C'1'       TURN ON DATE FUNCTION BYTE
         LA    R9,4(R9)           POINT PAST "DATE" TO NEXT CHARACTER
D0       EQU   *
         CLI   0(R9),C'='         IS THIS THE EQUAL SIGN?
         BE    D1                 IF SO GO TO D1
         LA    R9,1(R9)           POINT TO NEXT CHARACTER
         B     D0
         SPACE
D1       EQU   *
         LA    R9,1(R9)           POINT PAST =
         LA    R4,XCBDATE1        ADDR OF XCBDATE1 IN R4 FOR DATECHK
         CLI   0(R9),C'('         PAREN HERE? (MORETHAN 1 DATE?)
         BE    D3                 YES, GO TO TWO-DATE ROUTINE
         BAL   R5,DATECHK         OTHERWISE "DO" FIRST DATE
         MVC   XCBDATE2,XCBDATE1  IF ONLY 1 DATE CODED,SET BOTH EQU RVH
DNEXT    EQU   *
         CLI   0(R9),C','         IS THIS CHARACTER A COMMA?
         BE    D2                 YES, GO INCREMENT 9
         CLI   0(R9),C' '         IF NOT COMMA, IS IT A SPACE?
         BE    GETRECZ            YES, NO MORE PARAMS TO DECODE
         B     ISX007             OTHERWISE INVALID CHARACTER
         SPACE
D2       EQU   *
         LA    R9,1(R9)           POINT TO START OF NEXT PARAMETER
         B     CCCHECK            GO DECODE THIS PARAMETER
         SPACE
D3       EQU   *
         LA    R9,1(R9)           POINT PAST ( THAT SENT YOU HERE
         BAL   R5,DATECHK         GO "DO" FIRST DATE
         CLI   0(R9),C','         COMMA HERE?
         BE    D4                 YES, SECOND DATE FOLLOWS
         CLI   0(R9),C')'         PAREN HERE? (NO MORE DATES)
         BE    D3A                YES GO POINT PAST IT           JDM7
         B     ISX027             OTHERWISE INVALID CHARACTER
         SPACE
D3A      EQU   *                                                 JDM7
         MVC   XCBDATE2,XCBDATE1  ONLY 1 DATE CODED, BOTH EQU    JDM7
         B     D5                                                JDM7
         SPACE 1                                                 JDM7
D4       EQU   *
         LA    R9,1(R9)           POINT PAST MIDDLE COMMA
         LA    R4,XCBDATE2        ADDR OF XCBDATE2 IN R4 FOR DATECHK
         BAL   R5,DATECHK         GO "DO" SECOND DATE
         CLI   0(R9),C')'         PAREN HERE?
         BNE   ISX027             IF NOT, ERROR TIME...
D5       EQU   *
         LA    R9,1(R9)           POINT PAST )
         B     DNEXT              GO CHECK FOR DELIMITERS
         SPACE
*----------------------------------------------------------------JDM9*
*        REMEMBER, WE INPUT CONTROL CARDS WITH DATES OF YYYYDDD  JDM9
*        THAT TRANSLATES TO X"YYYYDDDF". BUT WHAT IS ACTUALLY    JDM9
*        COMING IN THE SMF RECORD IS X"0CYYDDDF".  SO ADJUST THE JDM9
*        COMPARED DATE HERE IN THE SUBROUTINE.                   JDM9
*----------------------------------------------------------------JDM9*
DATECHK  EQU   *
         PACK  WORK5A,0(7,R9)     |                              JDM9
         OI    WORK5A+6,X'0F'     | SUBROUTINE USED ONLY IN THIS JDM9
         UNPK  WORK5B,WORK5A      | SECTION; IT CHECKS A GIVEN   JDM9
         CLC   WORK5B,0(R9)       | GIVEN DATE PARAMETER FOR     JDM9
         BNE   ISX023             | NUMERIC. IF IT IS NUMERIC,   JDM9
         SP    WORK5A,=P'1900000'                                JDM9
         OI    WORK5A+6,X'0F'                                    JDM9
         MVC   0(4,R4),WORK5A+3   | THE DATE IS MOVED TO THE XCB.JDM9
         LA    R9,7(,R9)          | IF NON-NUMERIC, CONTROL IS   JDM9
         BR    R5                 | TRANSFERRED TO ISX023.  R9   JDM9
*                                 | IS ALSO UPDATED TO LOCATION  JDM9
*                                 | OF NEXT (ASSUMED) DELIMITER. JDM9
         SPACE
*---------------------------------------------------------------------
*        NUMBER   -  NUMBER PARAMETER DECODE ROUTINE
*---------------------------------------------------------------------
         SPACE
NUMBER   EQU   *
         MVI   XCBIND6,C'1'       ENABLE NUMBER FUNCTION
         LA    R9,4(R9)           ADVANCE POINTER 4 POSITIONS
N0       EQU   *
         CLI   0(R9),C'='         IS THIS AN EQUAL SIGN?
         BE    N1                 IF SO GO TO N1
         LA    R9,1(R9)           ELSE POINT TO NEXT POSITION OF PARAM
         B     N0                 ...AND GO BACK AND TRY AGAIN.
         SPACE
N1       EQU   *
         LA    R9,1(R9)           POINT PAST = SIGN.
         LR    R8,R9              LOAD R8 WITH ADDR IN R9
*                                 R8 WILL BE USED AS A SCAN POINTER
*                                 WITHIN THIS ROUTINE ONLY.
         LA    R7,R7              LOAD R7 WITH 7
N2       EQU   *
         CLI   0(R8),C' '         IS THIS CHARACTER A SPACE?
         BE    NMOVE              YES, GO PROCESS PARAMETER
         CLI   0(R8),C','         IF NOT SPACE, IS IT A COMMA?
         BE    NMOVE              YES, GO PROCESS PARAMETER
         LA    R8,1(R8)           OTHERWISE ASSUME VALID CHAR FOR NOW
         B     N2                 GO CHECK NEXT CHARACTER
         SPACE
NMOVE    EQU   *
         SR    R8,R9              SUBTRACT TO FIND LENGTH OF PARAMETER
         C     R8,=F'7'           CANNOT EXCEED 7 DIGITS
         BH    ISX031             IF IT DOES, ISSUE ERROR MESSAGE
         SR    R7,R8              SUBTRACT TO FIND OFFSET (USED LATER)
         BCTR  R8,0               SUBTRACT 1 TO USE R8 AS LEN IN PACK
         EX    R8,NPACKER         PACK PARAMETER INTO XCBNUMBR
         OI    XCBNUMBR+3,X'0F'   ENSURE VALID SIGN
         UNPK  NUMWORK,XCBNUMBR   UNPACK INTO NUMBER WORK AREA
         LA    R3,NUMWORK         GET ADDR OF WORK AREA INTO R3
         AR    R3,R7              INCREMENT R3 BY OFFSET IN R7
         EX    R8,NCHK            IS THIS SAME AS ON THE CARD?
         BNE   ISX035             IF NOT, DATA WAS NOT NUMERIC
         LA    R8,1(R8)           ADD 1 TO R8, THEN ADD...
         AR    R9,R8              ...R8 TO R9 TO POINT R9 TO DELIMITER
         B     N3                 GO CHECK DELIMITER
         SPACE
NPACKER  EQU   *
         PACK  XCBNUMBR,0(0,9)    PLACES VALUE INTO XCBNUMBR
NCHK     EQU   *
         CLC   0(0,R3),0(R9)      PERFORMS THE NUMERIC CHECK
         SPACE
N3       EQU   *
         CLI   0(R9),C' '         IS NEXT CHARACTER A SPACE?
         BE    GETRECZ            IF SO THERE ARE NO MORE PARAMS
         CLI   0(R9),C','         IS THIS CHARACTER A COMMA?
         BNE   ISX007             IF NOT, GO TO ERROR MESSAGE
         LA    R9,1(R9)           POINT PAST COMMA
         B     CCCHECK            GO BACK AND ANALYZE NEXT PARAM
         SPACE
*---------------------------------------------------------------------
*        SUBCODE  -  SUBCODE PARAMETER DECODE ROUTINE
*---------------------------------------------------------------------
         SPACE
SUBCODE  EQU   *
         MVI   XCBIND4,C'1'       ENABLE SUBCODE CHECK
         MVI   XCBIND5,C'0'       DISABLE JOBNAME IF SUBCODE CODED
         LA    R9,4(R9)           POINT AHEAD A LITTLE BIT
         LA    R6,R5              LOAD R6 WITH MAX # OF ENTRIES ALLOWED
S0       EQU   *
         CLI   0(R9),C'='         EQUAL SIGN HERE?
         BE    S1                 YES, GO START DECODE
         LA    R9,1(R9)           ELSE POINT TO NEXT CHARACTER...
         B     S0                 ...AND CHECK AGAIN
         SPACE
TBLFILL  EQU   *
         MVC   0(4,R7),0(R9)      THIS IS A LOCAL SUBROUTINE USED ONLY
         LA    R7,4(R7)           BY SUBCODE AND OPRCODE.  IT FILLS
         LA    R9,4(R9)           XCBSTBL OR XCBOTBL AND INCREMENTS
         BR    R5                 CONTROL CARD AND TABLE POINTERS.
         SPACE
S1       EQU   *
         LA    R9,1(R9)           POINT PAST =
         LA    R7,XCBSTBL         GET ADDR OF SUBCODE TABLE IN R7
         CLI   0(R9),C'('         MORE THAN 1 SUBCODE?
         BE    S3                 YES, GO TO S3
         BAL   R5,TBLFILL         ELSE GO PLACE ENTRY IN XCBSTBL
         MVI   XCBSCNT,X'01'      SET XCBSCNT TO 1
S2       EQU   *
         CLI   0(R9),C' '         SPACE HERE (NO MORE PARAMETERS) ?
         BE    GETRECZ            YES, GET READY TO READ DATA
         CLI   0(R9),C','         COMMA HERE IF NOT SPACE?
         BNE   ISX007             IF NOT COMMA OR SPACE, INV DELIMITER
         LA    R9,1(R9)           POINT PAST COMMA
         B     CCCHECK            GO ANALYZE NEXT PARM
         SPACE
S3       EQU   *
         LA    R9,1(R9)           POINT PAST LEFT PAREN
         BAL   R5,TBLFILL         GO PLACE ENTRY IN SUBCODE TABLE
         CLI   0(R9),C','         NEXT CHARACTER AFTER ENTRY A COMMA?
         BNE   S4                 NO, GO CHECK FOR RIGHT PAREN
         BCT   R6,S3              YES, LOOP BACK TO S3 AND CONTINUE
         B     ISX051             ELSE TOO MANY PARAMETERS: ERROR
         SPACE
S4       EQU   *
         CLI   0(R9),C')'         BETTER BE A RIGHT PAREN HERE...
         BNE   ISX054             IF NOT, ERROR TIME
         LA    R7,5               LOAD R7 WITH VALUE OF 5.
         SR    R7,R6              SUBTRACT QUANTITY IN R6 FROM 5
         STC   R7,XCBSCNT         THIS GIVES NUMBER OF SUBCODES PRESENT
         LA    R9,1(R9)           POINT PAST RIGHT PAREN
         B     S2                 NOW GO CHECK FOR COMMA OR SPACE
         SPACE
*---------------------------------------------------------------------
*        OPRCODE  -   OPRCODE PARAMETER DECODE ROUTINE
*---------------------------------------------------------------------
         SPACE
OPRCODE  EQU   *                  GO TO INVALID KEYWORD ERROR MESSAGE
         B     ISX004             GO TO INVALID KEYWORD ERROR MESSAGE
         SPACE
*----------------------------------------------------------------JDM6*
*        JOBNAMES  -  JOBNAMES PARAMETER DECODE ROUTINE          JDM6*
*                                                                JDM6*
*        ADD CODE TO PERMIT MATCHES ON OTHER THAN 8 CHARACTER    JDM6*
*        JOBNAMES. ALSO LIMIT THE COMPARE TO ONLY SMF RECORDS    JDM6*
*        WHICH DO HAVE JOBNAMES IN THEM (FOR OBVIOUS REASONS).   JDM6*
*----------------------------------------------------------------JDM6*
         SPACE
JOBNAMES EQU   *                                                 RVH
         MVI   XCBIND5,C'1'       SET INDICATOR FOR JOBNAMES     RVH
         LA    R9,4(,R9)          PAST FIRST 4 CHAR              RVH
J0       EQU   *                                                 RVH
         CLI   0(R9),C'='         THIS = FOR START OF NAME?      RVH
         BE    J1                 YES  - GOT START OF JOBNAMES   RVH
         B     ISX073             DO NOT HAVE A JOBNAME          JDM8
         SPACE
J1       EQU   *                                                 RVH
         LA    R9,1(,R9)          PAST "=" CHARACTER             RVH
         LA    R15,0              ZERO OUR COUNTER               JDM6
         LR    R1,R9              SAVE THE BEGINNING ADDR        JDM6
J2       EQU   *                                                 JDM6
         CLI   0(R9),C' '         AT THE END ??                  JDM6
         BE    J3                                                JDM6
         CLI   0(R9),C','         AT THE END ??                  JDM6
         BE    J3                                                JDM6
         LA    R15,1(,R15)        BUMP THE COUNTER               JDM6
         LA    R9,1(,R9)          MOVE ALONG THE ADRESS          JDM6
         B     J2                                                JDM6
         SPACE
J3       EQU   *                                                 JDM6
         LTR   R15,R15            LEAVE OUT JOBNAME ?            JDM6
         BZ    ISX072             FORGOT TO CODE ONE             JDM6
         STC   R15,XCBJOBNL       SAVE THE LENGTH WANTED         JDM6
         MVC   XCBJOBN(8),=CL8' ' PREP IT.                       JDM6
         BCTR  R15,0              SUBTRACT 1 FOR EXECUTE         JDM6
         EX    R15,MVCJOBN        SAVE THE JOBNAME               JDM6
         B     CCCHECK            RETURN TO CHECK MORE FILEDS    RVH
         SPACE
MVCJOBN  MVC   XCBJOBN(*-*),0(R1)                                JDM6
         DS    0H                                                JDM6
         SPACE 1
*---------------------------------------------------------------------
*        TIME  -   TIME PARAMETER DECODE ROUTINE
*---------------------------------------------------------------------
         SPACE
TIME     EQU   *
         MVI   XCBIND7,C'1'       ENABLE TIME FUNCTION BYTE
         LA    R7,TIMEFLD         GET ADDR OF BEGINNING OF TIMEFLD
         LA    R8,XCBTIME1        GET ADDR OF START TIME FIELD
T0       EQU   *
         CLI   0(R9),C'='         SCAN FOR EQUAL SIGN
         BE    T1                 WHEN FOUND GO TO T1
         LA    R9,1(R9)           OTHERWISE POINT TO NEXT CHARACTER
         B     T0                 GO CHECK FOR = AGAIN.
         SPACE
T1       EQU   *
         LA    R9,1(R9)           POINT PAST EQUAL SIGN
         CLI   0(R9),C'('         ( INDICATES START AND END TIMED CODED
         BE    TWOTIMES           IF GO GO TO APPROPRIATE ROUTINE
         BAL   R5,TIMESCAN        OTHERWISE JUST DO TIMESCAN ONCE,...
         MVC   XCBTIME2,XCBTIME1  IF ONLY 1 TIME CODED, SET BOTH SAME
         LA    R9,1(R9)           ...POINT PAST ASSUMED COMMA,...
         B     CCCHECK            ...AND GO TO CHECK FOR MORE PARAMETER
         SPACE
TWOTIMES EQU   *
         LA    R9,1(R9)           POINT PAST LEFT PAREN
         BAL   R5,TIMESCAN        GO FIGURE OUT START TIME
         LA    R9,1(R9)           WHEN YOU GET BACK SKIP OVER COMMA,...
         LA    R7,TIMEFLD         RESET POINTER TO TIMEFLD
         BAL   R5,TIMESCAN        ...AND GO FIGURE OUT END TIME.
         LA    R9,2(R9)           WHEN BACK, POINT PAST ASSUMED ),
         B     CCCHECK            ...AND RETURN TO PARAMETER CHECK RNT.
         SPACE
TIMESCAN EQU   *
         MVC   0(2,R7),0(R9)      MOVE HRS FIELD TO FIRST 2 OF TIMEFLD
         LA    R9,2(R9)           POINT WHERE NEXT DELIMITER SHOULD BE
         CLI   0(R9),C','         |
         BE    TCONVERT           |
         CLI   0(R9),C' '         |  A COMMA, SPACE OR A ) WILL TERMIN-
         BE    TCONVERT           | ATE THE SCAN, WHILE A PERIOD INDI-
         CLI   0(R9),C')'         | CATES  MORE TIME FIELDS.  ANY OTHER
         BE    TCONVERT           | CHARACTER WILL CAUSE AN ERROR.
         CLI   0(R9),C'.'         |
         BNE   ISX040             |
         LA    R7,2(R7)           POINT TO NEXT SLOTS IN TIMEFLD
         LA    R9,1(R9)           POINT PAST PERIOD
         B     TIMESCAN           GO MOVE NEXT TIME FIELD INTO TIMEFLD
         SPACE
TCONVERT EQU   *
         PACK  TIMETEST,TIMEFLD   |
         OI    TIMETEST+4,X'0F'   |
         UNPK  TIMETST2,TIMETEST  |  IS TIME PARAMETER NUMERIC?
         CLC   TIMETST2,TIMEFLD   |
         BNE   ISX041             |
         PACK  ANSW,TIMEFLD(2)    PACK HOURS INTO ANSW.
         MP    ANSW,=P'360000'    MULT TO CNVRT TO BINARY HUND. OF SEC.
         ZAP   TOTLTIME,ANSW      ACCUMULATE BINARY TIME
         PACK  ANSW,TIMEFLD+2(2)  PACK MINUTES INTO ANSW.
         MP    ANSW,=P'6000'      CONVERT TO BINARY HUND. OF SEC.
         AP    TOTLTIME,ANSW      ACCUMULATE TOTAL HUND. SECS.
         PACK  ANSW,TIMEFLD+4(2)  PACK SECONDS INTO ANSW
         MP    ANSW,=P'100'       CONVERT TO BINARY HUND. OF SEC.
         AP    TOTLTIME,ANSW      ACCUMULATE TOTAL HUND. OF SEC.
         PACK  ANSW,TIMEFLD+6(2)  PACK BINARY HUND.OF SEC. FIELD
         AP    TOTLTIME,ANSW      ACCUMULATE INTO TOTLTIME
         CVB   R1,TOTLTIME        CONVERT DECIMAL QUANTITY TO BINARY
         ST    R1,0(R8)           R8 POINTS TO XCBTIME1; NEXT TIME...
         LA    R8,8(R8)           ...R8 WILL POINT TO XCBTIME2
         ZAP   TOTLTIME,=P'0'     CLEAR TOTLTIME
         ZAP   ANSW,=P'0'         CLEAR ANSW
         MVC   TIMEFLD,=C'00000000'    REINITIALIZE TIMEFLD
*                                 ROUTINE IS REENTERED.
         BR    R5                 GO BACK WHERE YOU CAME FROM
         SPACE
*---------------------------------------------------------------------
*        FIELD  -  FIELD ROUTINE
*---------------------------------------------------------------------
         SPACE
FIELD    EQU   *
         MVI   XCBIND8,C'1'       ENABLE FIELD CHECK
         LA    R9,4(R9)           POINT AHEAD A BIT
F0       EQU   *
         CLI   0(R9),C'='         FOUND EQUAL SIGN YET?
         BE    F1                 YES, GO START DECODE
         LA    R9,1(R9)           ELSE POINT TO NEXT BYTE...
         B     F0                 ...AND GO CHECK FOR = AGAIN.
         SPACE
F1       EQU   *
         CLI   1(R9),C'('         MAKE SURE HE CODED LEFT PAREN.
         BNE   ISX062             IF NOT, ERROR MESSAGE TIME
         LA    R9,2(R9)           POINT PAST =(
         MVC   NUMWORK,=C'0000000'     INITIALIZE NUMWORK TO ZERO
*** THIS CODE DECODES THE DISPLACEMENT PORTION OF FIELD PARAMETER *****
F2       EQU   *
         CLI   0(R9),C','         FOUND COMMA YET?
         BE    F3                 YES, GO TO F3
         CLI   0(R9),X'F0'        IS DIGIT LOWER THAN EBCDIC 0?
         BL    ISX066             YES, ERROR MESSAGE TIME
         MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK CONTENTS LEFT 1
         MVC   NUMWORK+6(1),0(R9)  MOVE DIGIT INTO LOW ORDER POSITION
         LA    R9,1(R9)            POINT TO NEXT BYTE
         B     F2                 GO DO IT AGAIN
         SPACE
F3       EQU   *
         PACK  DBLWORD,NUMWORK    PACK DISPLACEMENT INTO DBLWORD
         CVB   R8,DBLWORD         CONVERT TO BINARY IN R8
         STH   R8,XCBFDISP        STORE DISPLACEMENT IN XCB
*** THIS CODE DECODES THE LENGTH PORTION OF THE PARAMETER  ************
         MVC   NUMWORK,=C'0000000'     RESET NUMWORK TO ZERO
         LA    R9,1(R9)           POINT AHEAD 1 BYTE PAST COMMA
F4       EQU   *
         MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK 1 BYTE LEFT
         CLI   0(R9),X'F0'        IS DIGIT NUMERIC?
         BL    ISX069             NO, ERROR MESSAGE TIME
         MVC   NUMWORK+6(1),0(9)  MOVE DIGIT TO NUMWORK LOW ORDER BYTE
         LA    R9,1(R9)           POINT TO NEXT BYTE
         CLI   0(R9),C','         COMMA HERE?
         BNE   F4                 IF NOT ASSUME DIGIT AND GO BACK TO F4
         LA    R9,1(R9)           POINT PAST COMMA
         PACK  DBLWORD,NUMWORK    PACK LENGTH INTO DBLWORD
         CVB   R8,DBLWORD         CONVERT LENGTH TO BINARY
         C     R8,=F'21'          IS FIELD LEN > 21?
         BH    ISX064             IF SO, ERRORMESSAGE TIME
         BCTR  R8,0               DECREMENT LEN BY 1
         STC   R8,XCBFLDLN        STORE LEN OF FIELD - 1 IN XCB
         STC   R8,FMOVE+1         STORE RESULT AS LEN FACTOR IN FMOVE
FMOVE    EQU   *
         MVC   XCBFIELD,0(R9)     MOVE FIELD DATA TO XCB
         LA    R8,1(R8)           RE-INCREMENT R8 SO RESULT IS ACT. LEN
         AR    R9,R8              ADD R8 TO R8 TO FIND RIGHT PAREN
         CLI   0(R9),C')'         RIGHT PAREN HERE?
         BNE   ISX063             NO, ERROR MESSAGE TIME
         LA    R9,1(R9)           POINT PAST LEFT PAREN
         CLI   0(R9),C','         COMMA HERE?
         BE    F5                 YES, GO TO F5
         CLI   0(R9),C' '         SPACE HERE INSTEAD?
         BNE   ISX007             IF NOT, DELIMITER ERROR EXISTS
         B     GETRECZ            ELSE GO SELECT SMF DATA
         SPACE
F5       EQU   *
         LA    R9,1(R9)           POINT TO NEXT PARAMETER LOCATION
         B     CCCHECK            GO CHECK CONTROL CARD
         SPACE 1                                                    RVH
*-------------------------------------------------------------------RVH
*        MASK ROUTINE                                               RVH
*-------------------------------------------------------------------RVH
         SPACE 1                                                    RVH
MASK     EQU   *                                                    RVH
         MVI   XCBIND8A,C'1'      ENABLE MASK CHECK                 RVH
         LA    9,4(9)             POINT AHEAD A BIT                 RVH
M0       EQU   *                                                    RVH
         CLI   0(9),C'='          FOUND EQUAL SIGN YET?             RVH
         BE    M1                 YES, GO START DECODE              RVH
         LA    R9,1(R9)           ELSE POINT TO NEXT BYTE...        RVH
         B     M0                 ...AND GO CHECK FOR = AGAIN.      RVH
         SPACE 1                                                    RVH
M1       EQU   *                                                    RVH
         CLI   1(R9),C'('         MAKE SURE HE CODED LEFT PAREN.    RVH
         BNE   ISX062             IF NOT, ERROR MESSAGE TIME        RVH
         LA    R9,2(R9)           POINT PAST =(                     RVH
         MVC   NUMWORK,=C'0000000'     INITIALIZE NUMWORK TO ZERO   RVH
*** THIS CODE DECODES THE DISPLACEMENT PORTION OF FIELD PARAMETER **RVH
M2       EQU   *                                                    RVH
         CLI   0(R9),C','         FOUND COMMA YET?                  RVH
         BE    M3                 YES, GO TO M3                     RVH
         CLI   0(R9),X'F0'        IS DIGIT LOWER THAN EBCDIC 0?     RVH
         BL    ISX066             YES, ERROR MESSAGE TIME           RVH
         MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK CONTENTS LEFT 1RVH
         MVC   NUMWORK+6(1),0(R9) MOVE DIGIT INTO LOW ORDER POSITIONRVH
         LA    R9,1(R9)           POINT TO NEXT BYTE                RVH
         B     M2                 GO DO IT AGAIN                    RVH
         SPACE 1                                                    RVH
M3       EQU   *                                                    RVH
         PACK  DBLWORD,NUMWORK    PACK DISPLACEMENT INTO DBLWORD    RVH
         CVB   R8,DBLWORD         CONVERT TO BINARY IN R8           RVH
         STH   R8,XCBMDISP        STORE DISPLACEMENT IN XCB         RVH
*** THIS CODE DECODES THE OPERATOR DESCRIPTION PARAMETER   *********RVH
         LA    R9,1(R9)           POINT AHEAD 1 BYTE PAST COMMA     RVH
         CLC   0(2,R9),=C'BO'     IS IT "BO"                        RVH
         BNE   M4CK4              NO  - CHECK NEXT TYPE             RVH
         LA    R9,3(R9)           YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'10'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE.                     RVH
         SPACE 1                                                    RVH
M4CK4    EQU   *                                                    RVH
         CLC   0(2,R9),=C'BM'     IS IT "BM"                        RVH
         BNE   M4CK8              NO  - CHECK NEXT TYPE             RVH
         LA    R9,3(R9)           YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'40'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE                      RVH
         SPACE 1                                                    RVH
M4CK8    EQU   *                                                    RVH
         CLC   0(2,R9),=C'BZ'     IS IT "BZ"                        RVH
         BNE   M4CKE              NO  - CHECK NEXT TYPE             RVH
         LA    R9,3(R9)           YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'80'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE                      RVH
         SPACE 1                                                    RVH
M4CKE    EQU   *                                                    RVH
         CLC   0(3,R9),=C'BNO'    IS IT "BNO"                       RVH
         BNE   ISX4               NO  - GIVE AN ERROR               RVH
         LA    R9,4(R9)           YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'E0'     AND SET BC INSTR. TYPE            RVH
MMOVE    EQU   *                                                    RVH
         L     R8,TOHEXADD        GET ADDR OF TR TABLE              RVH
         MVC   NUMWORK+4(2),0(R9) PUT CHAR(S) IN WORK AREA          RVH
         TR    NUMWORK+4(2),0(R8) TRANSLATE FOR CONVERT TO HEX      RVH
         PACK  NUMWORK(2),NUMWORK+4(3) CONVERT TO HEX               RVH
         MVC   XCBMASK(1),NUMWORK SAVE HEX CHARACTER                RVH
         LA    R9,2(,R9)          ADD 2 TO R9 TO FIND RIGHT PAREN   RVH
         CLI   0(9),C')'          RIGHT PAREN HERE?                 RVH
         BNE   ISX063             NO, ERROR MESSAGE TIME            RVH
         LA    R9,1(R9)           POINT PAST LEFT PAREN             RVH
         CLI   0(R9),C','         COMMA HERE?                       RVH
         BE    M5                 YES, GO TO M5                     RVH
         CLI   0(R9),C' '         SPACE HERE INSTEAD?               RVH
         BNE   ISX007             IF NOT, DELIMITER ERROR EXISTS    RVH
         B     GETRECZ            ELSE GO SELECT SMF DATA           RVH
         SPACE 1                                                    RVH
M5       EQU   *                                                    RVH
         LA    R9,1(R9)           POINT TO NEXT PARAMETER LOCATION  RVH
         B     CCCHECK            GO CHECK CONTROL CARD             RVH
         SPACE
*----------------------------------------------------------------JDM8-
*        ACCT - ACCOUNT CODE ROUTINE                             JDM8
*----------------------------------------------------------------JDM8-
         SPACE 1                                                 JDM8
ACCT     EQU   *
         MVI   XCBIND10,C'1'      SET INDICATOR FOR ACCOUNT      JDM8
         LA    R9,4(,R9)          PAST FIRST 4 CHAR              JDM8
A0       EQU   *                                                 JDM8
         CLI   0(R9),C'='         THIS = FOR START OF NAME?      JDM8
         BE    J1                 YES  - GOT START OF JOBNAMES   JDM8
         B     ISX075             DO NOT HAVE A ACCOUNT CODE     JDM8
         SPACE 1                                                 JDM8
A1       EQU   *                                                 JDM8
         LA    R9,1(,R9)          PAST "=" CHARACTER             JDM8
         LA    R15,0              ZERO OUR COUNTER               JDM8
         C     R15,=F'12'         12 IS MAX                      JDM8
         BH    ISX076             WE HAVE AN ERROR               JDM8
         LR    R1,R9              SAVE THE BEGINNING ADDR        JDM8
A2       EQU   *                                                 JDM8
         CLI   0(R9),C' '         AT THE END ??                  JDM8
         BE    A3                                                JDM8
         CLI   0(R9),C','         AT THE END ??                  JDM8
         BE    A3                                                JDM8
         LA    R15,1(,R15)        BUMP THE COUNTER               JDM8
         LA    R9,1(,R9)          MOVE ALONG THE ADRESS          JDM8
         B     A2                                                JDM8
         SPACE 1                                                 JDM8
A3       EQU   *                                                 JDM8
         LTR   R15,R15            LEAVE OUT JOBNAME ?            JDM8
         BZ    ISX077             FORGOT TO CODE ONE             JDM8
         STC   R15,XCBACCTL       SAVE THE LENGTH WANTED         JDM8
         MVC   XCBACCT(12),=CL12' '  PREP IT.                    JDM8
         BCTR  R15,0              SUBTRACT 1 FOR EXECUTE         JDM8
         EX    R15,ACCTMVC1       SAVE THE ACCOUNT CODE          JDM8
         B     CCCHECK            RETURN TO CHECK MORE FILEDS    JDM8
         SPACE 1                                                 JDM8
ACCTMVC1 MVC   XCBACCT(*-*),0(R1)                                JDM8
         DS    0H                                                JDM8
         SPACE 1                                                 JDM8
*--------------------------------------------------------------------*
*        ERROR MESSAGES
*--------------------------------------------------------------------*
         SPACE
ISX001   EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE SPACE
         MVC   PRINTOUT+1(132),MISX001
         DROP  R15
         B     ISXERPRT            PRINT ERROR MESSAGE
         SPACE
ISX002   EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX002
         DROP  R15
         B     ISXERPRT            PRINT ERROR MESSAGE
         SPACE
ISX004   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX4                IF NOT GO PRODUCE ERROR MESSAGE
         ABEND 4,DUMP              OTHERWISE ABEND WITH U0004.
ISX4     EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE SPACE
         MVC   PRINTOUT+1(132),MISX004
         DROP  R15
         LA    R8,PRINTOUT+44     |
ISX4L    EQU   *                  |
         CLI   0(R9),C'='         |
         BE    ISX4E              |
         CLI   0(R9),C' '         |
         BE    ISX4E              |    LOCATE AND MOVE OUT
         MVC   0(1,R8),0(R9)      |    ENTIRE INVALID KEYWORD
         LA    R9,1(R9)           |
         LA    R8,1(R8)           |
         B     ISX4L              |
         SPACE
ISX4E    EQU   *
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE
ISX007   EQU   *
         CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX7               IF NOT GO PROCESS ERROR MESSAGE
         ABEND 7,DUMP             ABEND WITH U0007.
ISX7     EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE.
         MVC   PRINTOUT+1(132),MISX007
         DROP  R15
         MVC   PRINTOUT+44(1),0(9) MOVE OUT INVALID CHARACTER
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE
         B     GETRECZ             ASSUME NO MORE PARAMETERS; GO TRY
*                                  TO WORK WITH DATA YOU HAVE.
         SPACE
ISX009   EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX009
         DROP  R15
         PUT   SMFXPRT,PRINTOUT   WRITE ERROR MESSAGE
         BR    R5                 GO BACK TO WHERE YOU CAME FROM
         SPACE
ISX010   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX10               IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 10,DUMP             ABEND WITH U0010
ISX10    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX010
         DROP  R15
         PUT   SMFXPRT,PRINTOUT
         ZAP   ELEMENTS,=P'20'     SET COUNTER TO 20
         LA    1,CARDIN+159        GET ADDR OF END OF CONTR CARD FIELD
ISX10LP  EQU   *
         LA    R9,1(R9)            INCREMENT REGISTER 9
         CLI   0(R9),C')'          HAVE WE REACHED A RIGHT PAREN YET?
         BE    ISX10X              IF SO GO CHECK FOR DELIMITERS
         CR    R9,R1               HAVE WE REACHED END OF CONTROL CARD?
         BE    ISX014              IF SO, USER LEFT OFF PAREN - GO TELL
         B     ISX10LP             ELSE GO BACK THROUGH LOOP AGAIN.
         LA    R9,1(R9)           |  THE START OF THE NEXT PARAMETER.
ISX10X   EQU   *                  |
         CLI   0(R9),C','         | IF NOT COMMA OR SPACE,
         BNE   *+12               | ISSUE ISX007 ERROR MESSAGE
         LA    R9,1(R9)           |
         B     CCCHECK            |
         CLI   0(R9),C' '         |
         BE    GETRECZ            |
         B     ISX007             |
         SPACE
ISX014   EQU   *
         CLI   ABENDSW,C'E'        IS ABENDSW SET?
         BNE   ISX14               IF NOT GO PROCESS ERRMSG NORMALLY
         ABEND 14,DUMP             ELSE ABEND WITH U0014.
ISX14    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX014
         DROP  R15
         PUT   SMFXPRT,PRINTOUT
         B     GETRECZ
         SPACE
ISX016   EQU   *
         CLI   ABENDSW,C'E'        IS ABENDSW ON?
         BNE   ISX16               IF NOT GO WRITE ERROR MESSAGE.
         ABEND 16,DUMP             ELSE ABEND WITH U0016.
ISX16    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE.
         MVC   PRINTOUT+1(132),MISX016
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX018   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX18               IF NOT GO PROCESS ERROR MESSAGE.
         ABEND 18,DUMP             ELSE ABEND WITH U0018.
ISX18    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE SPACE.
         MVC   PRINTOUT+1(132),MISX018
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX023   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX23               IF NOT PROCESS ERROR MESSAGE
         ABEND 23,DUMP             OTHERWISE ABEND WITH U0023
ISX23    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX023
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX027   EQU   *
         CLI   ABENDSW,C'E'        ABEND SWITCH SET?
         BNE   ISX27               IF NOT GO PROCESS ERROR MESSAGE
         ABEND 27,DUMP             ELSE ABEND WITH U0027
ISX27    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX027
         DROP  R15
         PUT   SMFXPRT,PRINTOUT
         B     GETRECZ             GO GET SET TO PROCESS SMF DATA
         SPACE
ISX031   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX31               IF NOT GO PROCESS ERROR MESSAGE
         ABEND 31,DUMP             OTHERWISE ABEND WITH U0031
ISX31    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX031
         DROP  R15
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+9(47),=C'DELETING CHARACTERS UNTIL COMMA OR SPAC
               CE FOUND.'
         PUT   SMFXPRT,PRINTOUT
I31L     EQU   *
         CLI   0(R9),C' '          IS THIS CHARACTER A SPACE?
         BE    GETRECZ             IF SO THERE ARE NO MORE PARAMS.
         CLI   0(R9),C','          IS THIS CHARACTER A COMMA?
         BE    I31L1               IF SO GO TO I31L
         LA    R9,1(R9)            POINT TO NEXT CHARACTER
         B     I31L                GO CHECK THIS BYTE
         SPACE
I31L1    EQU   *
         LA    R9,1(R9)            POINT PAST COMMA
         B     CCCHECK             GO CHECK FOR MORE PARAMS
         SPACE
ISX035   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX35               IF NOT GO PROCESS ERROR MESSAGE
         ABEND 35,DUMP             ELSE ABEND WITH U0035
ISX35    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX035
         DROP  R15
         PUT   SMFXPRT,PRINTOUT
         ZAP   XCBNUMBR,=P'100'    DEFAULT TO 100 RECORDS FOR NUMBER
         LA    R9,1(R9)            POINT TO NEXT PARAM
         B     CCCHECK             GO ANALYZE IT.
         SPACE
ISX040   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX40               IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 40,DUMP             OTHERWISE ABEND WITH U0040
ISX40    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX040
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX041   EQU   *
         CLI   ABENDSW,C'E'        IS ABEND SWITCH SET?
         BNE   ISX41               IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 41,DUMP             ELSE ABEND WITH U0041
ISX41    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX041
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX051   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX51               IF NOT, GO PROCESS ERROR MESSAGE
         ABEND 51,DUMP             ELSE ABEND WITH U0051
ISX51    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX051
         DROP  R15
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+10(44),=C'FIRST FIVE USED;  REMAINDER OF CARD IG
               GNORED.'
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE SECOND LINE
         MVI   XCBSCNT,X'05'       SET COUNT FOR XCBSTBL TO 5
         B     GETRECZ             GET READY TO PROCESS SMF DATA
         SPACE
ISX054   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX54               IF NOT, GO PROCESS ERROR MESSAGE
         ABEND 54,DUMP             ELSE ABEND WITH U0054
ISX54    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SINGLE-SPACE
         MVC   PRINTOUT+1(132),MISX054
         DROP  R15
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVC   PRINTOUT+1(29),=C'REMAINING PARAMETERS IGNORED.'
         PUT   SMFXPRT,PRINTOUT    WRITE ERROR MESSAGE SECOND LINE
         LA    R7,5                PUT A VALUE OF 5 IN R7
         SR    R7,R6               SUBTRACT CONTENTS OF R6 FROM 5
         STC   R7,XCBSCNT          RESULT IS NUMBER OF SUBCODES CODED
         B     GETRECZ             GO PROCESS SMF DATA
         SPACE
ISX062   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX62               NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 62,DUMP             ELSE ABEND WITH U0062
ISX62    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER
         MVC   PRINTOUT+1(132),MISX062
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX063   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX63               NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 63,DUMP             ELSE ABEND WITH U0063
ISX63    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER
         MVC   PRINTOUT+1(132),MISX063
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX064   EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER
         MVC   PRINTOUT+1(132),MISX064
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
ISX066   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX66               NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 66,DUMP             ELSE ABEND WITH U0066
ISX66    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER
         MVC   PRINTOUT+1(132),MISX066
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE
ISX069   EQU   *
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?
         BNE   ISX69               NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 69,DUMP             ELSE ABEND WITH U0069
ISX69    EQU   *
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER
         MVC   PRINTOUT+1(132),MISX069
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE
         SPACE 1                                                 JDM6
ISX072   EQU   *                                                 JDM6
         CLI   ABENDSW,C'Y'        ABEND SWITCH SET?             JDM6
         BNE   ISX72               NO, PROCESS ERR MSG NORMALLY  JDM6
         ABEND 72,DUMP             ELSE ABEND WITH U0072         JDM6
ISX72    EQU   *                                                 JDM6
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'      SET CONTROL CHARACTER         JDM6
         MVC   PRINTOUT+1(132),MISX072
         DROP  R15
         B     ISXERPRT            GO PRINT ERROR MESSAGE        JDM6
         SPACE
ISX073   EQU   *                                                 JDM7
         CLI   ABENDSW,C'Y'       ABEND SWITCH SET?              JDM7
         BNE   ISX73              NO, PROCESS ERROR MSG NORMALLY JDM7
         ABEND 73,DUMP            ELSE ABEND WITH U0073          JDM7
ISX73    EQU   *                                                 JDM7
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER          JDM7
         MVC   PRINTOUT+1(132),MISX073                           JDM7
         DROP  R15
         B     ISXERPRT           GO PRINT ERROR MESSAGE         JDM7
         SPACE 1                                                 JDM8
ISX075   EQU   *                                                 JDM8
         CLI   ABENDSW,C'Y'       ABEND SWITCH SET?              JDM8
         BNE   ISX75              NO, PROCESS ERROR MSG NORMALLY JDM8
         ABEND 75,DUMP            ELSE ABEND WITH U0073          JDM8
ISX75    EQU   *                                                 JDM8
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER          JDM8
         MVC   PRINTOUT+1(132),MISX075                           JDM8
         DROP  R15
         B     ISXERPRT           GO PRINT ERROR MESSAGE         JDM8
         SPACE 1                                                 JDM8
ISX076   EQU   *                                                 JDM8
         CLI   ABENDSW,C'Y'       ABEND SWITCH SET?              JDM8
         BNE   ISX73              NO, PROCESS ERROR MSG NORMALLY JDM8
         ABEND 76,DUMP            ELSE ABEND WITH U0073          JDM8
ISX76    EQU   *                                                 JDM8
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER          JDM8
         MVC   PRINTOUT+1(132),MISX076                           JDM8
         DROP  R15
         B     ISXERPRT           GO PRINT ERROR MESSAGE         JDM8
         SPACE 1                                                 JDM8
ISX077   EQU   *                                                 JDM8
         CLI   ABENDSW,C'Y'       ABEND SWITCH SET?              JDM8
         BNE   ISX73              NO, PROCESS ERROR MSG NORMALLY JDM8
         ABEND 77,DUMP            ELSE ABEND WITH U0073          JDM8
ISX77    EQU   *                                                 JDM8
         L     R15,=V(MSGS)        GET ADDR OF MSG CSECT
         USING MSGDSECT,R15        GAIN ADDRESSABILITY
         MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER          JDM8
         MVC   PRINTOUT+1(132),MISX077                           JDM8
         DROP  R15
         B     ISXERPRT           GO PRINT ERROR MESSAGE         JDM8
         SPACE 1                                                 JDM8
ISXERPRT EQU   *
         PUT   SMFXPRT,PRINTOUT   WRITE OUT ERROR MESSAGE
         CLOSE (SYSXIN,,SMFXIN,,SMFXOUT,,SMFXPRT)
         LA    R15,12             CONTROL CARD ERRORS CAUSE RC=12
         B     LOAD13             GO PREPARE FOR RETURN
         SPACE
*---------------------------------------------------------------------*
*        GETRECZ
*---------------------------------------------------------------------*
         SPACE
GETRECZ  EQU   *
         L     R3,AWORK           GET ADDRESS OF WORKA FOR PUT-
*                                 AWORK WILL CONTAIN THE ADDRESS OF
*                                 WORKA+4 IF FIXED OUTPUT WAS SPECIFIED
*                                 IN JCL; OTHERWISE AWORK WILL CONTAIN
*                                 THE ADDRESS OF WORKA.
         CLC   XCBINDIC,=12C'0'   MAKE SURE WE HAVE WORK TO DO
         BE    ISX002             IF NOT ISSUE ERROR MESSAGE
         SPACE
*--------------------------------------------------------------------*
*        THIS SECTION HANDLES ALL RECORD SELECTION, AND ACCUMULATES  *
*        JOB STATISTICS SUCH AS NUMBER OF RECORDS READ, AND THE      *
*        LARGEST AND SMALLEST RECORDS READ.                          *
*                                                                    *
*        THE RECORD SELECTION IS ACCOMPLISHED VIA BRANCH-AND-LINKS   *
*        TO VARIOUS EDIT ROUTINES BASED ON THE SETTING OF THE        *
*        APPROPRIATE FUNCTION BYTES.  IF A RECORD PASSES THE         *
*        EDIT OF A PARTICULAR ROUTINE, CONTROL IS RETURNED TO THE    *
*        NEXT EDIT CHECK AND ULTIMATELY TO THE "CHECKRDW" ROUTINE.   *
*        IF A RECORD FAILS THE PARTICULAR EDIT, CONTROL IS RETURNED  *
*        TO THE GETREC INSTRUCTIONS TO READ IN ANOTHER RECORD        *
*                                                                    *
*                                                                    *
*        EACH DECODE ROUTINE LOADS THE REGISTERS WITH THE VALUES IT  *
*        NEEDS FOR EXECUTION;  THUS THE CONTENTS OF REGISTERS WILL   *
*        CHANGE FROM ROUTINE TO ROUTINE.    THE FOLLOWING CON-       *
*        VENTIONS ARE ADHERED TO, HOWEVER, IN ALL ROUTINES:          *
*                                                                    *
*        5:  COMMUNICATION REGISTER FOR USE WITH BAL COMMANDS.       *
*        6:    ADDRESS POINTER TO TABLE ELEMENTS.                    *
*        8:  LOOP COUNTER.                                           *
*                                                                    *
*        THE FOLLOWING REGISTERS ARE UNUSED IN THIS SECTION:         *
*                                                                    *
*              2     3     4     7     9                             *
*--------------------------------------------------------------------*
         SPACE
*--------------------------------------------------------------------*
*        GETREC
*--------------------------------------------------------------------*
         SPACE
GETREC   EQU   *
         GET   SMFXIN,WORKA      READ SMF RECORD
*---------------------------------------------------------------------
CHKF0    EQU   *
         CLI   XCBIND0,C'0'       IS RECTYPE DISABLED?
         BE    CHKF1              IF SO,  TRY NEXT EDIT.
         BAL   R5,EDITRECT        OTHERWISE BRANCH TO  RECORD SELECTION
*                                 BY RECORD-TYPE ROUTINE.
*---------------------------------------------------------------------
CHKF1    EQU   *
         CLI   XCBIND1,C'0'       IS EXCLUDE DISABLED?
         BE    CHKF2              IF SO GO TRY NEXT EDIT
         BAL   R5,EDITEXCL        OTHERWISE GO TO RECORD-SELECTION
*                                 BY EXCLUSION ROUTINE.
*---------------------------------------------------------------------
CHKF2    EQU   *
         CLI   XCBIND2,C'0'       IS DATE DISABLED?
         BE    CHKF3              IF SO GO TRY NEXT EDIT.
         BAL   R5,EDITDATE        OTHERWISE GO TO RECORD-SELECTION BY
*                                 DATE ROUTINE.
*---------------------------------------------------------------------
CHKF3    EQU   *
         CLI   XCBIND3,C'0'       IS OPR-CODE DISABLED?
         BE    CHKF4              IF SO GO CHECK NEXT FUNCTION-BYTE
         B     CHKF4              (WILL BE REPLACED WHEN OPR ACTIVATED)
*---------------------------------------------------------------------
CHKF4    EQU   *
         CLI   XCBIND4,C'0'       IS SUB-CODE DISABLED?
         BE    CHKF5              IF SO, GO CHECK NEXT FUNCTION-BYTE.
         BAL   R5,EDITSUBC        OTHERWISE GO TO RECORD-SELECTION
*                                 BY SUBCODE ROUTINE
*---------------------------------------------------------------------
CHKF5    EQU   *
         CLI   XCBIND5,C'0'       IS JOBNAMES DISABLED?
         BE    CHKF7              IF SO GO CHECK NEXT FUNCTION BYTE.
         BAL   R5,EDITJOBN        OTHERWISE GO TO RECORD SELETEION  RVH
*                                 BY JOBNAME ROUTINE.
*---------------------------------------------------------------------
CHKF7    EQU   *
         CLI   XCBIND7,C'0'       IS TIME DISABLED?
         BE    CHKF8              IF SO GO CHECK NEXT FUNCTION BYTE
         BAL   R5,EDITTIME        OTHERWISE GO TO RECORD SELECTION BY
*                                 TIME ROUTINE.
*---------------------------------------------------------------------
CHKF8    EQU   *
         CLI   XCBIND8,C'0'       IS FIELD PARAMETER DISABLED?
         BE    CHKF8A             IF SO GO CHECK NEXT FUNCTION BYTE RVH
         BAL   R5,EDITFLD         OTHERWISE GO TO RECORD SELECTION BY
*                                 FIELD ROUTINE.
*---------------------------------------------------------------------
CHKF8A   EQU   *                                                    RVH
         CLI   XCBIND8A,C'0'      IS MASK PARAMETER DISABLED?       RVH
         BE    CHKF10             IF SO THERE ARE NO MORE EDITS     RVH
         BAL   R5,EDITMASK        OTHERWISE GO TO RECORD SELECTION  RVH
*                                 BY MASK ROUTINE.                  RVH
*---------------------------------------------------------------------
CHKF10   EQU   *
         CLI   XCBIND10,C'0'      CHECK SELECT BY ACCT CODE
         BE    CHECKRDW           IF SO GO CHECK NEXT FUNCTION BYTE
         BAL   R5,ACCTEDIT        OTHERWISE GO TO RECORD SELECTION BY
*                                 ACCT ROUTINE.
*---------------------------------------------------------------------
         B     CHECKRDW           NOW GO TO OUTPUT WRITING ROUTINE
         SPACE
*--------------------------------------------------------------------*
*        RECORD SELECTION SUBROUTINES.
*--------------------------------------------------------------------*
         SPACE
EDITRECT EQU   *
         LA    R6,XCBRCTBL        LOAD 6 WITH ADDR OF RECTABLE
         LA    R8,0               ENSURE REG 8 IS 0.
         CVB   R8,ELEMENTS        PUT # OF ELEMENTS IN RECTABLE INTO 8.
LOOP1    EQU   *
         CLC   SMFRECTP,0(6)      DOES RECORD TYPE MATCH TABLE ELEMENT?
         BE    GOBACK1            IF THEY MATCH, RETURN TO NEXT EDIT.
         LA    R6,1(R6)           POINT TO NEXT ELEMENT OF RECTABLE.
         BCT   R8,LOOP1           LOOP BACK FOR AS MANY TIMES AS THERE
*                                 ARE ELEMENTS IN RECTABLE.
         LA    R5,GETREC          IF FALL-THROUGH OCCURS, THE CURRENT
*                                 RECORD IS NOT ONE TO BE SELECTED.
GOBACK1  EQU   *
         BR    R5                 RETURN TO ADDRESS IN REGISTER 5
         SPACE
*------- EDITEXCL ROUTINE -------------------------------------------*
         SPACE
EDITEXCL EQU   *
         LA    R6,XCBRCTBL        LOAD 6 WITH ADDR OF RECTABLE
         LA    R8,0               ENSURE REG 8 IS 0.
         CVB   R8,ELEMENTS        PUT # OF ELEMENTS IN RECTABLE INTO 8.
LOOP2    EQU   *
         CLC   SMFRECTP,0(R6)     DOES RECORD TYPE MATCH TABLE ELEMENT?
         BNE   *+12               IF NOT JUMP AHEAD 3 INSTRUCTIONS.
         LA    R5,GETREC          A MATCH INDICATES THAT THIS RECORD
*                                 IS TO BE EXCLUDED.
         B     GOBACK2
         LA    R6,1(R6)           POINT TO NEXT ELEMENT IN RECTABLE.
         BCT   R8,LOOP2           REPEAT FOR AS MANY TIMES AS THERE ARE
*                                 ELEMENTS IN RECTABLE.
GOBACK2  EQU   *
         BR    R5                 RETURN TO ADDRESS IN REGISTER 5
         SPACE
*------- EDITDATE ROUTINE -------------------------------------------*
         SPACE
EDITDATE EQU   *
         CLC   SMFDATE,XCBDATE1   CHECK SMFDATE AGAINST LOWER-BOUND
         BL    DZ                 IF SMFDATE LOWER STILL WE DONT WANTIT
         CLC   SMFDATE,XCBDATE2   CHECK SMFDATE AGAINST UPPER-BOUND
         BNH   GOBACK3            IF LOW OR EQUAL, WE WANT IT.
DZ       EQU   *
         LA    R5,GETREC          OTHERWISE PLAN TO RETURN TO GETREC
GOBACK3  EQU   *
         BR    R5
         SPACE
*------- EDITTIME ROUTINE -------------------------------------------*
         SPACE
EDITTIME EQU   *
         CLC   SMFTIME(4),XCBTIME1     CHECK SMF TIME IN RECORD
         BL    TOOEARLY           IF LOW, RECORD TIME IS TOO EARLY
         CLC   SMFTIME(4),XCBTIME2     CHECK TIME AGAINST UPPER BOUND
         BNH   GOBACK4            IF NOT HIGH, RECORD FALLS WITHIN LMTS
TOOEARLY EQU   *
         LA    R5,GETREC          CHANGE RETURN ADDRESS TO READ ANOTHER
GOBACK4  EQU   *
         BR    R5                 SMF RECORD
         SPACE
*------- EDITSUBC ROUTINE -------------------------------------------*
         SPACE
EDITSUBC EQU   *
         LA    R6,XCBSTBL         ADDR OF SUBCODE TABLE IN R6
         LA    R8,0               SET R8 TO 0
         IC    R8,XCBSCNT         INSERT # OF SUBCODES INTO R8
         CLI   SMFRECTP,X'04'     |    CHECK TO MAKE SURE THAT
         BE    LOOP3              |    EACH RECORD HAS A VALID SUBCODE
         CLI   SMFRECTP,X'05'     |    FIELD; THIS WOULD INCLUDE ONLY
         BE    LOOP3              |    TYPES 4, 5, 14, AND 15 RECORDS.
         CLI   SMFRECTP,X'0E'     |    IF THE CURRENT RECORD IS NOT
         BE    LOOP3              |    ONE OF THESE, NO EDITTING OCCURS
         CLI   SMFRECTP,X'0F'     |    AND CONTROL IS RETURNED TO THE
         BE    LOOP3              |    CALLING ROUTINE TO PERFORM
         BR    R5                 |    OTHER EDITS.
         SPACE
LOOP3    EQU   *
         CLC   SMFJOBNM(4),0(R6)  COMPARE FIRST 4 OF JOBNAME WITH
*                                 A SUBCODE TABLE ELEMENT.
         BNE   *+6                IF NOT EQUAL, TRY NEXT ELEMENT
         BR    R5                 IF EQUAL, RETURN TO CALLING ROUTINE
         LA    R6,4(R6)           POINT R6 TO NEXT SUBCODE TABLE ELEMNT
         BCT   R8,LOOP3           GO BACK AND CHECK XCBSTBL AGAIN...
         LA    R5,GETREC          ...UNLESS ALL HAVE BEEN CHECKED.
         BR    R5                 IN WHICH CASE RETURN TO GETREC
         SPACE
*------- EDITFLD  ROUTINE -------------------------------------------*
         SPACE
EDITFLD  EQU   *
         LA    R6,WORKA           ADDR OF WORKA IN R6
         AH    R6,XCBFDISP        FIND LOCATION OF ITEM TO BE CHECKED
         IC    R7,XCBFLDLN        INSERT LEN OF FIELD DATA INTO R7
         EX    R7,COMPARE         EXECUTE CLC INSTRUCTION FOR FIELD
         BNE   GOBACK6            NOT EQUAL GET ANOTHER SMF RECORD
         BR    R5                 ELSE RETURN TO DO MORE EDITS
         SPACE
GOBACK6  EQU   *
         LA    R5,GETREC          ADDR OF GETREC ROUTINE IN R5
         BR    R5                 BRANCH TO ADDR IN R5
         SPACE
COMPARE  EQU   *
         CLC   0(0,R6),XCBFIELD   COMPARE SMF DATA AGAINST FIELD DATA
         SPACE 1                                                    RVH
EDITMASK EQU   *                                                    RVH
         LA    R6,WORKA           ADDR OF WORKA IN R6               RVH
         AH    R6,XCBMDISP        FIND LOC. OF ITME TO BE CHECKED   RVH
         OI    XCBMOPRN,X'05'     PUT IN THE REGISTER FOR EX INSTR  RVH
         IC    R7,XCBMASK         INSERT MASK OF DATA INTO R7       RVH
         EX    R7,TMINSTR         EXECUTE TM  INSTRUCTION FOR DATA  RVH
         IC    R7,XCBMOPRN        GET TYPE OF OPERAND TO USE        RVH
         EX    R7,BCINSTR         AND EXECUTE THE BC INSTRUCTION    RVH
GOBACK7  EQU   *                                                    RVH
         LA    R5,GETREC          ADDR OF GETREC ROUTINE IN R5      RVH
         BR    R5                 BRANCH TO ADDR IN R5              RVH
TMINSTR  EQU   *                                                    RVH
         TM    0(R6),X'00'        USE TM INSTRUCTION FOR DATA       RVH
BCINSTR  EQU   *                                                    RVH
         BCR   0,R5               IF TRUE SELECT THIS RECORD        RVH
         SPACE 1                                                    RVH
*----------------------------------------------------------------JDM6-*
*        CHANGE LOGIC TO SELECT ON LESS THAN 8 CHAR JOBNAME, BUT JDM6 *
*        FIRST CHECK ON RECTYPE TO VALIDATE                      JDM6 *
*----------------------------------------------------------------JDM6-*
         SPACE 1                                                 JDM6
EDITJOBN EQU   *                                                    RVH
         CLC   SMFRECTP(1),=X'1E' SMFTYPE 30?                    JDM6
         BE    EDIT30M                                           JDM6
         CLC   SMFRECTP(1),=X'20' SMFTYPE 32?                    JDM6
         BE    EDIT32M                                           JDM6
         LA    R15,JTABLE         GET ADDRESS OF TABLE           JDM6
EDITLOOP EQU   *                                                 JDM6
         CLI   0(R15),X'00'       END OF TABLE?                  JDM6
         BE    EDITSKP            CAN NOT COMPARE                JDM6
         CLC   SMFRECTP(1),0(R15) DO WE MATCH                    JDM6
         BE    EDITMVC                                           JDM6
         LA    R15,1(R15)         MOVE ALONG IN TABLE            JDM6
         B     EDITLOOP                                          JDM6
         SPACE 1                                                 JDM6
EDIT30M  EQU   *                  GET JOBNAME FROM TYPE 30       JDM6
         L     R15,AWORK          GET WORK AREA ADDR             JDM8
         USING SMFRCD30,R15                                      JDM6
         A     R15,SMF30IOF       OFFSET TO ID SECTION           JDM6
         DROP  R15                                               JDM6
         USING SMF30ID,R15                                       JDM6
         MVC   SMFJOBNS(8),SMF30JBN   SAVE OFF JOBNAME           JDM6
         DROP  R15                                               JDM6
         B     EDITMVC                                           JDM6
EDIT32M  EQU   *                  GET JOBNAME FROM TYPE 32       JDM6
         L     R15,AWORK          GET WORK AREA ADDR             JDM8
         USING SMFRCD32,R15                                      JDM6
         A     R15,SMF32IOF       OFFSET TO ID SECTION           JDM6
         DROP  R15                                               JDM6
         USING SMF32ID,R15                                       JDM6
         MVC   SMFJOBNS(8),SMF32JBN   SAVE OFF JOBNAME           JDM6
         DROP  R15                                               JDM6
         B     EDITMVC                                           JDM6
EDITMVC  EQU   *                                                 JDM6
         XR    R15,R15            CLEAR R15                      JDM6
         IC    R15,XCBJOBNL       GET THE LENGTH                 JDM6
         S     R15,=F'1'          SUBTRACT ONE FOR EXECUTE       JDM6
         EX    R15,EDITCLC        IS IT THE ONE ???              RVH
         BER   R5                 YES SELECT THE RECORD          RVH
EDITSKP  EQU   *                                                 JDM6
         LA    R5,GETREC          OTHERWISE PLAN TO RETURN TO    RVH
         BR    R5                 GET THE NEXT RECORD            RVH
         SPACE 1                                                 JDM6
EDITCLC  CLC   XCBJOBN(*-*),SMFJOBNS                             JDM6
SMFJOBNS DC    CL8' '             TEMPORARY HOLD AREA            JDM6
JTABLE   DC    AL1(04),AL1(05),AL1(06),AL1(10)                   JDM6
         DC    AL1(14),AL1(15),AL1(16),AL1(17)                   JDM6
         DC    AL1(18),AL1(20),AL1(25),AL1(26),AL1(34)           JDM6
         DC    AL1(35),AL1(40),AL1(62),AL1(63)                   JDM6
         DC    AL1(64),AL1(67),AL1(68),AL1(69)                   JDM6
         DC    AL1(00),AL1(00)    THE FENCE OF ZEROS             JDM6
         DS    0H
         SPACE
*----------------------------------------------------------------JDM6-*
*        CHANGE LOGIC TO SELECT ON LESS THAN 8 CHAR JOBNAME, BUT JDM6 *
*        FIRST CHECK ON RECTYPE TO VALIDATE                      JDM6 *
*----------------------------------------------------------------JDM6-*
         SPACE 1                                                 JDM6
ACCTEDIT EQU   *                                                    RVH
         CLC   SMFRECTP(1),=X'04' SMFTYPE 04?
         BE    ACCT0405
         CLC   SMFRECTP(1),=X'05' SMFTYPE 05?
         BE    ACCT0405
         CLC   SMFRECTP(1),=X'14' SMFTYPE 20?
         BE    ACCT20
         CLC   SMFRECTP(1),=X'1A' SMFTYPE 26?
         BE    ACCT26
         CLC   SMFRECTP(1),=X'1E' SMFTYPE 30?
         BE    ACCT30M
         CLC   SMFRECTP(1),=X'21' SMFTYPE 33?
         BE    ACCT30M
         CLC   SMFRECTP(1),=X'23' SMFTYPE 35?
         BE    ACCT35M
         B     ACCTSKP
ACCT0405 EQU   *                  GET ACCT FROM TYPE 04/05
         L     R15,AWORK          GET WORK AREA ADDR
         USING SMFRCD4,R15
         LH    R1,SMF4LENN        GET 2 BTYE LENGTH OF DEV ENTRY
         DROP  R15
         AR    R1,R15             POINT TO ACCT SECTION
         LA    R1,1(,R1)          GO PASSED LENGTH FIELD
         B     ACCTMVC
ACCT20   EQU   *                  GET ACCT FROM TYPE 20
         L     R15,AWORK          GET WORK AREA ADDR
         USING SMFRCD20,R15
         LA    R1,SMF20ACT        POINT RIGHT TO DATA
         DROP  R15
         B     ACCTMVC
ACCT26   EQU   *                  GET ACCT FROM TYPE 26
         B     ACCTSKP
ACCT30M  EQU   *                  GET ACCT FROM TYPE 30
         L     R15,AWORK          GET WORK AREA ADDR
         USING SMFRCD30,R15
         A     R15,SMF30AOF       OFFSET TO ACCT SECTION
         DROP  R15
         LR    R1,R15             GET ADDRESS
         LA    R1,1(,R1)          MOVE BEYOND LENGTH
         B     ACCTMVC
ACCT33M  EQU   *                  GET ACCT FROM TYPE 32
         L     R15,AWORK          GET WORK AREA ADDR
         USING SMFRCD33,R15
         A     R15,SMF33UOF       OFFSET TO ID SECTION
         LR    R1,R15             SAVE THE ADDRESS
         LA    R1,1(,R1)          POINT TO ACCT FIELD
         DROP  R15
         B     ACCTMVC
ACCT35M  EQU   *                  GET ACCT FROM TYPE 32
         L     R15,AWORK          GET WORK AREA ADDR
         USING TLGSMF,R15
         LA    R1,TLGACFLD        POINT TO ACCT FIELD
         DROP  R15
         B     ACCTMVC
ACCTMVC  EQU   *
         XR    R15,R15            CLEAR R15
         IC    R15,XCBACCTL       GET THE LENGTH
         S     R15,=F'1'          SUBTRACT ONE FOR EXECUTE
         EX    R15,ACCTCLC        IS IT THE ONE ???
         BER   R5                 YES SELECT THE RECORD
ACCTSKP  EQU   *
         LA    R5,GETREC          OTHERWISE PLAN TO RETURN TO
         BR    R5                 GET THE NEXT RECORD
         SPACE 1
ACCTCLC  CLC   XCBACCT(*-*),0(R1)
ACCTNUMB DC    CL12' '            TEMPORARY HOLD AREA
         SPACE
*--------------------------------------------------------------------*
*        CHECKRDW
*--------------------------------------------------------------------*
         SPACE
CHECKRDW EQU   *
         MVC   FSIZE+2(2),RDW
         L     R5,FSIZE           LOAD RDW INTO R5
         CVD   R5,DSIZE           CONVERT RDW TO DECIMAL
         MVC   FSIZE,=X'00000000' RESET FSIZE TO BINARY 0 SO THAT
*                                 NEXT TIME THRU FSIZE WILL NOT
*                                 CONTAIN OLD VALUE OF RDW.
         UNPK  FTEST,DSIZE+4(R4)  UNPACK RDW
         OI    FTEST+4,X'F0'      OR THE SIGN                    JDM4
         CLC   FTEST,MAXREC       COMPARE CURRENT RDW AGAINST
*                                 LARGEST VALUE SO FAR.
         BNH   CLCLOW             IF LOW OR EQUAL GO CHECK FOR LOW
         MVC   MAXREC,FTEST       OTHERWISE REPLACE MAXREC WITH NEW
*                                 HIGHER VALUE
CLCLOW   EQU   *
         CLC   FTEST,MINREC       SEE IF CURRENT RDW LESS THAN LOWEST
*                                 VALUE STORED SO FAR
         BNL   OUTPUT             IF RDW NOT SMALLEST GO TO OUTPUT
         MVC   MINREC,FTEST       OTHERWISE REPLACE MINREC WITH NEW
*                                 LOWEST VALUE.
OUTPUT   EQU   *
         PUT   SMFXOUT,(R3)        WRITE THE SELECTED RECORD
*                                 TO THE OUTPUT DATASET.
         AP    CTR,=P'1'          COUNT EACH RCD AS IT IS WRITTEN
         LA    R7,COUNTR          GET ADDR FOR RECORD CNTR       JDM
         SR    R9,R9              CLEAR REG 9                    JDM
         IC    R9,SMFRECTP        PICK UP RECORD TYPE            JDM
         SLL   R9,2               MULTIPLY BY 4                  JDM
         L     R8,0(R9,R7)        GET PROPER COUNTER             JDM
         LA    R8,1(R8)           UP CT BY ONE                   JDM
         ST    R8,0(R9,R7)        SAVE NEW COUNT                 JDM
         CLI   XCBIND6,C'1'       IS NUMBER ENABLED?
         BNE   GETREC             IF NOT GO READ SOME MORE RECORDS.
         CP    XCBNUMBR,CTR       SEE IF NUMBER LIMIT REACHED
         BNE   GETREC             IF NOT GO PROCESS MORE DATA.
*                                 FALL THROUGH OTHERWISE TO EOJ.
         SPACE
*--------------------------------------------------------------------*
*        END-OF-JOB PROCESSING                                       *
*--------------------------------------------------------------------*
         SPACE
EOJ      EQU   *
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'     CLEAR PRINTOUT, DOUBLE-SPACE
         MVC   PRINTOUT+1(18),=C'RECORDS SELECTED: '
         MVC   PRINTOUT+19(12),=XL12'402020206B2020206B202120'   JDM9
         ED    PRINTOUT+19(12),CTR                               JDM9
         PUT   SMFXPRT,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         PUT   SMFXPRT,PRINTOUT
         LA    R7,COUNTR          GET ADDRESS FOR RECORD COUNTS  JDM
MORE     EQU   *                                                 JDM
         CLC   0(4,R7),=F'0'      ZERO ENTRY ?                   JDM
         BE    NEXTONE            YES                            JDM
         L     R9,0(R7)           PICK UP THE COUNT              JDM
         CVD   R9,DBLWORD         MAKE IT DECIMAL                JDM
         MVC   PRINTOUT+1(L'LINE7),LINE7                         JDM
         MVC   PRINTOUT+12(4),=X'40202120'                       JDM
         ED    PRINTOUT+12(4),RECTYPX                            JDM
         MVC   PRINTOUT+19(12),=XL12'402020206B2020206B202120'   JDM5
         ED    PRINTOUT+19(12),DBLWORD+3                    JDM3 JDM
         PUT   SMFXPRT,PRINTOUT                                  JDM
NEXTONE  EQU   *                                                 JDM
         AP    RECTYPX,=P'1'      SET LOW                        JDM
         CP    RECTYPX,=P'256'    ARE WE DONE                    JDM
         BE    FINI                                              JDM
         LA    R7,4(R7)           BUMP COUNTER                   JDM
         B     MORE                                              JDM
         SPACE 1                                                 JDM
FINI     EQU   *                                                 JDM
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'60'     CLEAR PRINTOUT, TRIPLE-SPACE   JDM
*                                                                JDM
         MVC   PRINTOUT+1(21),=C'RECORD SIZES -- MAX: '
         MVC   PRINTOUT+22(5),MAXREC                             JDM4
         MVC   PRINTOUT+27(15),=C'          MIN: '               JDM4
         MVC   PRINTOUT+41(5),MINREC                             JDM4
         PUT   SMFXPRT,PRINTOUT
         STM   0,15,MYREGS
         CLOSE (SYSXIN,,SMFXIN,,SMFXOUT)
         CLI   ABENDSW,C'J'       DOES ABENDSW=J?
         BNE   LOAD13             IF NOT, TERMINATE NORMALLY
         BAL   5,ISX009           GO PRINT ISX009 MESSAGE
         CLOSE (SMFXPRT)
         ABEND 9,DUMP             ABEND WITH U0009
         SPACE
LOAD13   EQU   *
         L     R13,SAVE+4         GET REGISTER 13 BACK
         C     R15,=F'12'         CONTROL CARD ERROR DETECTED?
         BE    RETURN12           IF SO, TERMINATE WITH RC = 12
         CP    CTR,=P'0'          NO DATA FOUND?
         BE    RETURN8            IF NOT, TERMINATE WITH RC = 8
         RETURN (14,12),RC=0
RETURN8  EQU   *
         RETURN (14,12),RC=8      INDICATES NO DATA FOUND
RETURN12 EQU   *
         RETURN (14,12),RC=12
         SPACE
*--------------------------------------------------------------------*
*              DATA CONTROL BLOCK DEFINITIONS                        *
*--------------------------------------------------------------------*
         PRINT ON,NOGEN
         SPACE
SYSXIN   DCB   DSORG=PS,MACRF=(GM),DDNAME=SYSXIN,EODAD=ISX001
         SPACE
SMFXIN   DCB   DSORG=PS,MACRF=(GM),DDNAME=SMFXIN,EODAD=EOJ,      JDM2  X
               EXLST=JFCXIN                                      JDM2
         SPACE
SMFXOUT  DCB   DSORG=PS,MACRF=(PM),DDNAME=SMFXOUT,EXLST=JFCXOUT
         SPACE
SMFXPRT  DCB   DSORG=PS,MACRF=(PM),DDNAME=SMFXPRT,RECFM=FBA,           X
               LRECL=133,BLKSIZE=133,EXLST=JFCXPRT               JDM5
         PRINT ON,GEN
         SPACE
*--------------------------------------------------------------------*
*              DATA NAME DEFINITIONS AND ALIGNMENT                   *
*--------------------------------------------------------------------*
         SPACE
*******************   DOUBLE WORD ALIGNMENT    ***********************
         SPACE
         DS    0F
         DS    0D
         DC    C'    DATANAME DEFINITIONS'       DUMP HEADER
SAVE     DS    9D
CARDIN   DC    CL160' '           CONTROL CARD SCAN AREA
DSIZE    DS    CL8
DBLWORD  DS    CL8
ELEMENTS DC    PL8'0'
TIMEFLD  DC    CL8'00000000'
TIMETST2 DS    CL8
TOTLTIME DC    PL8'0'
         SPACE
XCB      DS    0D  *****  EXTRACT CONTROL BLOCK  *****
         SPACE
XCBINDIC DS    0CL12 IF PARAMETER CODED, BYTE IS 1, ELSE 0
XCBIND0  DC    C'0'               RECTYPE
XCBIND1  DC    C'0'               EXCLUDE
XCBIND2  DC    C'0'               DATE
XCBIND3  DC    C'0'               OPRCODE
XCBIND4  DC    C'0'               SUBCODE
XCBIND5  DC    C'0'               JOBNAME
XCBIND6  DC    C'0'               NUMBER
XCBIND7  DC    C'0'               TIME
XCBIND8  DC    C'0'               FIELD
XCBIND8A DC    C'0'               MASK
XCBIND9  DC    C'0'               "RECTYPE=ALL" FUNCTION REQUESTED
XCBIND10 DC    C'0'               ACCOUNT                        JDM8
XCBIND11 DC    C'0'               RESERVED FOR LATER FUNCTIONS
XCBABSW  DC    C'N'               ABEND ON CONTROL CARD ERROR OR NOT
XCBSRTSW DC    C'N'               SORT SPECIFIED IN PARM FIELD
XCBMSW   DC    C'0'               SHOWS MORE THAN 1 RECTYPE SUBPARAM.
XCBRECNT DC    X'00'              NO. OF RECTYPE/EXCLUDE SUBPARAMERTRS
XCBRCTBL DS    CL20               TABLE OF RECORD TYPES (MAX OF 20)
XCBDT1   DS    0CL8               LOWER-BOUND TIMESTAMP
XCBDATE1 DS    CL4                LOWER-BOUND DATE
XCBTIME1 DS    CL4                LOWER-BOUND DATE
XCBDT2   DS    0CL8               UPPER-BOUND TIMESTAMP
XCBDATE2 DS    CL4                UPPER-BOUND DATE
XCBTIME2 DS    CL4                UPPER-BOUND TIME
XCBNUMBR DS    CL4                VALUE CODED FOR NUMBER PARAMETER
         DC    X'00'              (ALIGNMENT)
XCBSCNT  DC    X'00'              COUNT FOR # OF CODES IN XCBSTBL
XCBSTBL  DS    CL20               TABLE OF SUBCODES (MAX OF 5)
XCBFDISP DC    H'0'               FIELD: DISPLACEMENT FROM BEG. OF REC
XCBFLDLN DC    X'00'              FIELD: LENGTH OF FIELD DATA
XCBFIELD DC    21C' '             FIELD: FIELD DATA
         SPACE
XCBMDISP DC    H'0'               MASK: DISP. FROM BEG. OF REC   RVH
XCBMOPRN DC    X'00'              MASK: BC INSTRUCTION OPRN FIELDRVH
XCBMASK  DC    X'00'              MASK: MASK DATA                RVH
         SPACE 1                                                 RVH
XCBJOBN  DC    CL8' '             JOBNAME: NAME OF JOB TO SELECT RVH
XCBJOBNL DC    XL1'00'            LENGTH OF JOBNAME FIELD        JDM6
         SPACE
XCBACCT  DC    CL12' '            ACCOUNT NUMBER                 JDM8
XCBACCTL DC    XL1'00'            LENGTH OF JOBNAME FIELD        JDM8
         SPACE 1                                                 JDM8
********************   FULLWORD ALIGNMENT   **************************
         SPACE
FSIZE    DC    F'0'
         SPACE
* -----  JFCB FOR DDNAME=SMFXPRT
JFCXPRT  DS    0F                                                JDM5
         DC    X'87'                                             JDM5
         DC    AL3(JFCBXPRT)                                     JDM5
         DS    0D                                                JDM5
JFCBXPRT DC    XL176'00'                                         JDM5
         SPACE
* -----  JFCB FOR DDNAME=SMFXIN
JFCXIN   DS    0F                                                JDM2
         DC    X'87'                                             JDM2
         DC    AL3(JFCBXIN)                                      JDM2
         DS    0D                                                JDM2
JFCBXIN  DC    XL176'00'                                         JDM2
         SPACE
* -----  JFCB FOR DDNAME=SMFXOUT
JFCXOUT  DS    0F
         DC    X'87'
         DC    AL3(JFCBXOUT)
         DS    0D
JFCBXOUT DC    CL176'00'
         SPACE
AWORK    DS    F                  ADDRESS OF BEGINNING OF WORKA
*                                 DEPENDING ON WHETHER RECFM=F OR V.
         DC    C'  MYREGS'
MYREGS   DS    16F
         DC    C'CODETABL'
PARMSAVE DC    F'0'               SAVE LOC FOR ADDR OF PARM INFO.
         SPACE
********************     HALF-WORD ALIGNMENT    **********************
         SPACE
ANSW     DC    PL6'0'
         SPACE
********************    BYTE  ALIGNMENT    ***************************
         SPACE
PRINTOUT DS    CL133
CTR      DC    PL5'0'                                            JDM3
COUNT    DS    CL9                                               JDM3
HOLDREC  DS    0CL3
HOLDREC1 DS    CL1
HOLDREC2 DS    CL1
HOLDREC3 DS    CL1
TESTRECT DS    CL3                USED FOR NUMERIC TEST IN "RECTYPE"
TIMETEST DS    CL5
HOLDER   DC    C'     '
DATE1    DS    CL4
DATE2    DS    CL4
MAXREC   DC    CL5'00000'                                        JDM4
MINREC   DC    CL5'99999'                                        JDM4
FTEST    DC    CL5'     '                                        JDM4
ABENDSW  DC    C'N'               N (DEFAULT) MEANS NO ABEND;
*                                 E MEANS ABEND ON ERROR (CONTROL CARD)
*                                 J MEANS ABEND ON EOJ (AFTER CLOSE)
SORTSW   DC    C'N'               N INDICATES PARM=SORT NOT CODED;
*                                 S INDICATES PARM=SORT WAS CODED
I        DC    PL2'0'             A COUNTER USED WITH RECTYPE/EXCLUDE
MULTISW  DC    C'0'               FOR RECTYPE=(.......)
ALLSTARS DC    54C'*'
ALLSPACE DC    54C' '
LPCNTR   DC    PL1'3'
INRECFM  DC    X'00'              SAVE INPUT RECFM
INLRECL  DC    H'00'              SAVE INPUT LRECL
INBLKSI  DC    H'00'              SAVE INPUT BLKSIZE
         DS    0F
WORK5A   DS    CL7                                               JDM9
         DC    X'00'
WORK5B   DS    CL7                                               JDM9
         DC    X'00'
NUMWORK  DS    CL7                WORK AREA FOR NUMBER
MOREPARM DC    C'Y'               ADDITIONAL PARM PARAMETER SWITCH
PARMAREA DC    CL100' '
         LTORG
RECTYPX  DC    PL2'0'                                              JDM
LINE7    DC    C'RECORD TYPE     :          '                      JDM
COUNTR   DC    256F'0'            RECORD TYPE COUNTERS             JDM
*                                                                  JDM
TOHEXADD DC    A(TOHEX)           ADDR OF TR TABLE                 RVH
TOHEX    DC    256X'00'           TRANSLATE TABLE FOR HEX CONV.     RVH
         ORG   TOHEX+C'A'                                           RVH
         DC    X'FAFBFCFDFEFF'                                      RVH
         ORG   TOHEX+C'0'                                           RVH
         DC    X'F0F1F2F3F4F5F6F7F8F9'                              RVH
         ORG   ,                                                    RVH
WORKA    DS    0CL32768           SMF RECORD WORKA AREA            JDM1
RDW      DS    CL4
         DS    CL1
SMFRECTP DS    CL1
         DS    CL4
SMFDATE  DS    CL4
         DS    CL32754                                             JDM1
SMFTIME  EQU   WORKA+6
SMFJOBNM EQU   WORKA+18
SMFOPRCC EQU   WORKA+112
         SPACE
         ORG   WORKA
         IFASMFR 04                                              JDM6
         IFASMFR 14
         IFASMFR 20                                              JDM6
         IFASMFR 30                                              JDM6
         IFASMFR 32                                              JDM6
         IFASMFR 33                                              JDM6
         IFASMFR 35                                              JDM6
         SPACE
MSGDSECT DSECT
         SPACE
* ----------------------------------------------------------- *
*        THESE MUST MATCH THE ORDER IN THE'MSGS' CSECT !!!
* ----------------------------------------------------------- *
         SPACE
MISX001  DS    CL132
MISX002  DS    CL132
MISX004  DS    CL132
MISX007  DS    CL132
MISX009  DS    CL132
MISX010  DS    CL132
MISX014  DS    CL132
MISX016  DS    CL132
MISX018  DS    CL132
MISX023  DS    CL132
MISX027  DS    CL132
MISX031  DS    CL132
MISX035  DS    CL132
MISX040  DS    CL132
MISX041  DS    CL132
MISX051  DS    CL132
MISX054  DS    CL132
MISX062  DS    CL132
MISX063  DS    CL132
MISX064  DS    CL132
MISX066  DS    CL132
MISX069  DS    CL132
MISX072  DS    CL132
MISX073  DS    CL132
MISX075  DS    CL132
MISX076  DS    CL132
MISX077  DS    CL132
MSGL     EQU   *-MISX001
         DSECT
         IEFJFCBN LIST=YES
         SPACE
         DCBD  DSORG=(PS)
         SPACE
         END
* ----------------------------------------------------------------- *
*        PUT ALL MESSAGES HERE TO REDUCE ADDRESSABILITY PROBLEMS
*        IN THE MAIN MODULE.
* ----------------------------------------------------------------- *
         SPACE
MSGS     AMODE 24
MSGS     RMODE 24
MSGS     CSECT
         SPACE
MISX001  DC    CL132'ISX001:  INSUFFICIENT OR NON-EXISTENT CONTROL CONT*
               ROL CARD(S).  JOB TERMINATED.'
         SPACE
MISX002  DC    CL132'ISX002:  BLANK CONTROL CARD.  JOB TERMINATED.'
         SPACE
MISX004  DC    CL132'ISX004:  INVALID KEYWORD ON CONTROL CARD.'
         SPACE
MISX007  DC    CL132'ISX007:  EXPECTING COMMA OR SPACE. FOUND " ", ASSU*
               ED SPACE.'
         SPACE
MISX009  DC    CL132'ISX009:  ABEND ON EOJ REQUESTED; ABNORMAL TERMINAT*
               ION FOLLOWS IMMEDIATELY.'
         SPACE
MISX010  DC    CL132'ISX010:  MORE THAN 20 RECTYPES SPECIFIED FOR RECTY*
               PE/EXCLUDE.  20 ASSUMED.'
         SPACE
MISX014  DC    CL132'ISX014:  MISSING RIGHT PAREN FOR RECTYPE/EXCLUDE. *
                ANY REMAINING PARAMETERS IGNORED.'
         SPACE
MISX016  DC    CL132'ISX016:  A RECTYPE/EXCLUDE SUBPARAMETER IS GREATER*
                THAN 3 DIGITS.  JOB TERMINATED.'
         SPACE
MISX018  DC    CL132'ISX018:  A RECTYPE/EXCLUDE SUBPARAMETER CONTAINS N*
               ON-NUMERIC DATA.  JOB TERMINATED.'
         SPACE
MISX023  DC    CL132'ISX023:  A DATE SUBPARAMETER CONTAINS NON-NUMERIC *
               DATA OR IS NOT USING YYYYDDD. JOB TERMINATED'     JDM9
         SPACE
MISX027  DC    CL132'ISX027:  EXPECTED RIGHT PAREN NOT FOUND IN DATE PA*
               RAMETER; REMAINING PARAMETERS IGNORED.'
         SPACE
MISX031  DC    CL132'ISX031:  THE NUMBER PARAMETER HAS A MAX LENGTH OF *
               7 CHARACTERS.'
         SPACE
MISX035  DC    CL132'ISX035:  NON-NUMERIC DATE IN NUMBER PARAMETER, 100*
                ASSUMED.'
         SPACE
MISX040  DC    CL132'ISX040:  INVALID DELIMITER IN THE TIME FIELD.  JOB*
                TERMINATED.'
         SPACE
MISX041  DC    CL132'ISX041:  A TIME SUB-PARAMETER CONTAINS NON-NUMERIC*
                DATA.'
         SPACE
MISX051  DC    CL132'ISX051:  MORE THAN 5 PARAMTERS CODE FOR SUBCODE.'
         SPACE
MISX054  DC    CL132'ISX054:  EXPECTED RIGHT PAREN OR COMMA NOT FOUND I*
               N SUBCODE PARAMETER.'
         SPACE
MISX062  DC    CL132'ISX062:  FIELD PARAMETER MISSING LEFT PAREN.'
         SPACE
MISX063  DC    CL132'ISX063:  FIELD PARAMETER MISSING RIGHT PAREN.'
         SPACE
MISX064  DC    CL132'ISX064:  FIELD PARAMETER LENGTH CAN NOT EXCEED 21.*
               '
         SPACE
MISX066  DC    CL132'ISX066:  FIELD PARAMETER DISPLACEMENT NOT NUMERIC.*
               '
         SPACE
MISX069  DC    CL132'ISX069:  FIELD PARAMETER LENGTH NOT NUMERIC.'
         SPACE
MISX072  DC    CL132'ISX072:  JOBNAME LENGTH IS 0 FOR SELECT.'   JDM7
         SPACE
MISX073  DC    CL132'ISX073:  JOBN SELECTION PARAMETER INVALID.' JDM7
         SPACE
MISX075  DC    CL132'ISX075:  ACCT SELECTION PARAMETER INVALID.' JDM8
         SPACE
MISX076  DC    CL132'ISX076:  ACCOUNT NUMBER EXCEEDS 12 CHAR.'   JDM8
         SPACE
MISX077  DC    CL132'ISX077:  ACCOUNT NUMBER MISSING.'           JDM8
         SPACE
MSGL     EQU   *-MISX001
         END
