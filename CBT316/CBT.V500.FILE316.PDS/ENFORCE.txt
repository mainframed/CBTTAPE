         TITLE 'MPF EXIT TO ENFORCE NO TAPE MOUNTS IN CLASS=A'
*--------------------------------------------------------------------*
*        THIS EXIT WAS CODED TO BE SCHEDULED BY THE  MESSAGE         *
*        PROCESSING FACILITY (MPF) WHEN A JOB CALLS FOR A TAPE       *
*        OR CARTRIDGE (3480) MOUNT.  THE CODE CAN PICK UP THE        *
*        JOBNAME FROM THE MESSAGE AND CHAIN DOWN THROUGH THE         *
*        RIGHT CONTROL BLOCKS TO FIND THE PROCESSING JOBCLASS.       *
*        THERE IS A TSO COMMAND CALLED '$DI' WHICH DOES THE SAME     *
*        THING AND WAS USED AS A MODEL.                              *
*                                                                    *
*        THE POLITICAL QUESTION TO BE ANSWERED THEN IS WHAT WILL     *
*        BE DONE TO THE PROCESSING JOB.  YOU COULD WARN THE USER     *
*        BY ISSUING A JES2 COMMAND TO INSERT A MESSAGE IN THE        *
*        JOBLOG.  OR THE 'HARD BALL' APPROACH OF CANCELLING THE      *
*        JOB IS ANOTHER.  OR BOTH  "DIE YOU SLIM BALL".              *
*                                                                    *
*        TRAP MSGS:  TMS001, TMS002, IEF233A, IEC501A                *
*                                                                    *
*        NOTE: IGNORE IEF233D MOUNT MSG, ALL I HAVE EVER SEEN        *
*              USING THIS IS STC'S DOING SYSTEM RELATED THINGS.      *
*                                                                    *
*        IF YOU ARE DOING SAY 'DFHSM' OR ANY OTHER SOFTWARE WHICH    *
*        DOES TAPE MOUNTS RUNNING IN AN INITIATOR, DO NOT RUN THEM   *
*        CLASS=A FOR THEY MAY GET KILLED !!!!!                       *
*                                                                    *
*        TRAP MOUNT MSGS. I KNOW THE LOCATION OF THE JOBNAME IN      *
*        EACH OF THOSE MESSAGES. PLUS WE CAN GO DOWN CONTROL BLOCK   *
*        CHAINS TO DISCOVER THE JOBCLASS. THEN IT IS ALL POLITICAL   *
*        AS TO WHAT YOU WANT TO DO !!!                               *
*                                                                    *
*        JIM MARSHALL, GRUMMAN DATA SYSTEMS                          *
*                                                                    *
*        REGISTER USAGE:                                             *
*        R0    -                                                     *
*        R1    -                                                     *
*        R2    -   ADDRESSABILITY TO JESCT  (IEFJESCT)               *
*        R3    -   ADDRESSABILITY TO SSCT   (IEFJESCT)               *
*        R4    -   ADDRESSABILITY TO SSVT                            *
*        R5    -   ADDRESSABILITY TO PIT ($PIT)                      *
*        R6    -   ADDRESSABILITY TO HCT ($HCT)                      *
*        R7    -                                                     *
*        R8    -                                                     *
*        R9    -   ADDRESSABILITY TO CTXT (IEEZVX100)                *
*        R10   -   ADDRESSABILITY TO MSGTEXT (IEEZVX100)             *
*        R11   -   ADDRESSABILITY TO SJB ($SJB)                      *
*        R12   -   BASE REG                                          *
*        R13   -   ADDRESSABILITY TO GETMAINED AREA                  *
*        R14   -   WORK REG IF YOU KNOW WHAT YOU ARE DOING           *
*                                                                    *
* DATE INSTALLED: IN TESTING                                         *
*                                                                    *
* CHNGE LOG: NONE                                                    *
*--------------------------------------------------------------------*
         SPACE
         PRINT ON,NOGEN
SP230    EQU   230                 SUBPOOL = 230
ENFORCE  AMODE 31
ENFORCE  RMODE 24
ENFORCE  CSECT
         SAVE  (14,12),,ENFORCE_&SYSDATE
         LR    R12,R15
         USING ENFORCE,R12
         SPACE
         L     R9,0(R1)            GET ADDRESS OF CTXTPTR
         USING CTXT,R9             ESTABLISH ADDRESSABILITY
         SPACE
         GETMAIN RU,LV=WORKSIZE,SP=SP230,LOC=BELOW
         USING WORKAREA,R1         ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4          SAVE
         ST    R1,8(R13)             AREA
         LR    R13,R1                  CHAINING
         DROP  R1
         USING WORKAREA,R13        ESTABLISH ADDRESSABILITY
         SPACE
*--------------------------------------------------------------------*
*        HOUSEKEEPING IS OVER, SET UP ADDRESSABILITY TO MSG          *
*--------------------------------------------------------------------*
         SPACE
         L     R15,CTXTTXPJ        GET ADDRESS OF MSG ATTRIBUTE
         USING CTXTATTR,R15        ESTABLISH ADDRESSABILITY
         LA    R10,CTXTTMSG        GET ADDRESS OF MESSAGE
         USING MSGTEXT,R10         ESTABLISH ADDRESSABILITY
         DROP  R15
         SPACE
*--------------------------------------------------------------------*
*        DETERMINE WHAT MSG NUMBER IT IS AND WHERE THE JOBNAME IS.   *
*                                                                    *
*        JOBNAMES HERE ARE GENERALLY 8 CHARACTERS, SOME MAY BE 7     *
*        CHARACTERS. YOU WILL SEE SOME CODE TO TRY TO COMPENSATE     *
*        FOR THAT LATER.                                             *
*--------------------------------------------------------------------*
         SPACE
*        LA    R0,L'IMSG           DEBUG CODE
*        LA    R1,IMSG             DEBUG CODE
*        TPUT  (R1),(R0),R         DEBUG CODE
*        LA    R0,45               DEBUG CODE
*        LA    R1,MSGID            DEBUG CODE
*        TPUT  (R1),(R0),R         DEBUG CODE
*        MVC   JMSGM(7),MSGID      DEBUG CODE
         CLC   MSGID(6),TMS001
         BE    $TMS001
         CLC   MSGID(6),TMS002
         BE    $TMS002
         CLC   MSGID(7),IEF233A
         BE    $IEF233A
         CLC   MSGID(7),IEC501A
         BE    $IEC501A
         B     RETURN              IGNORE OTHER MSGIDS
         SPACE
*--------------------------------------------------------------------*
*        TMS001 IS EASY TO GET THE JOBNAME. MSG IS AS FOLLOWS        *
*  FORM  0         1         2         3         4                   *
*        01234567890123456789012345678901234567890                   *
*  (A)   TMS001  IEC501A M NNN,PRIVAT,SL,,JOBNAME , ....             *
*  (B)   TMS001  IEF233A M NNN,PRIVAT,SL,JOBNAME , .....             *
*  (C)   TMS001  IEF233D M NNN,SCRTCH,,JOBNAME , .....               *
*  (C)   TMS001  IEF233D M NNN,PRIVAT,SL,JOBNAME , ....              *
*--------------------------------------------------------------------*
         SPACE
$TMS001  EQU   *
         CLC   MSGID+8(7),IEC501A  IS IT THIS FORM ?
         BE    $TMS001A
         CLC   MSGID+8(7),IEF233A  IS IT THIS FORM ?
         BE    $TMS001B
         CLC   MSGID+8(7),IEF233D  IS IT THIS FORM ?
         BE    $TMS001C
         B     RETURN              IGNORE OTHER MSGS
$TMS001A EQU   *
         MVC   JOBNAME(8),MSGID+33 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS001B EQU   *
         MVC   JOBNAME(8),MSGID+32 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS001C EQU   *
         CLC   MSGID+28(2),=C',,'
         BE    $TMS011C
         MVC   JOBNAME(8),MSGID+32 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS011C EQU   *
         MVC   JOBNAME(8),MSGID+30 SAVE THE JOBNAME
         B     RUNBLOKS
         SPACE
*--------------------------------------------------------------------*
*        TMS002 IS EASY BUT NOT TOO EASY TO GET THE JOBNAME          *
*  FORM  0         1         2         3         4         5         *
*        012345678901234567890123456789012345678901234567890         *
*  (A)   TMS002  IEC501A M NNN,TRANSIT-TAPE,SL,,JOBNAME , .....      *
*        TMS002  IEC501A M NNN,TRANSIT-TAPE,NL,6250 BPI,JOBNAME ,... *
*  (B)   TMS002  IEF233A M NNN,TRANSIT-TAPE,SL,,JOBNAME , .....      *
*        TMS002  IEF233A M NNN,TRANSIT-TAPE,NL,,JOBNAME , .....      *
*  (C)   TMS002  IEF233D M NNN,TRANSIT-TAPE,SL,JOBNAME , .....       *
*--------------------------------------------------------------------*
         SPACE
$TMS002  EQU   *
         CLC   MSGID+8(7),IEC501A  IS IT THIS FORM ?
         BE    $TMS002A
         CLC   MSGID+8(7),IEF233A  IS IT THIS FORM ?
         BE    $TMS002B
         CLC   MSGID+8(7),IEF233D  IS IT THIS FORM ?
         BE    $TMS002C
         B     RETURN              IGNORE OTHER FORMS !!!!
$TMS002A EQU   *
         CLC   MSGID+38(8),=C'6250 BPI'
         BE    $TMS012A
         MVC   JOBNAME(8),MSGID+39 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS012A EQU   *
         MVC   JOBNAME(8),MSGID+47 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS002B EQU   *
         MVC   JOBNAME(8),MSGID+39 SAVE THE JOBNAME
         B     RUNBLOKS
$TMS002C EQU   *
         MVC   JOBNAME(8),MSGID+38 SAVE THE JOBNAME
         B     RUNBLOKS
         SPACE
*--------------------------------------------------------------------*
*        IEF233A IS ALSO EASY TO GET JOBNAME.  THUS ...              *
*        0         1         2         3         4                   *
*        01234567890123456789012345678901234567890                   *
*        IEF233A M NNN,VVVVVV,,JOBNAME, ....                         *
*                                                                    *
*        NOTE: IF AN OPERATOR DOES A MOUNT WE WILL IGNORE IT         *
*--------------------------------------------------------------------*
         SPACE
$IEF233A EQU   *
         CLC   MSGID+22(6),=C'MOUNT,'    IS THIS AN OPER MOUNT ?
         BE    RETURN
         MVC   JOBNAME(8),MSGID+22 SAVE THE JOBNAME
         B     RUNBLOKS
         SPACE
*--------------------------------------------------------------------*
*        IEC501A IS NO SO EASY TO GET THE JOBNAME !!!!! M            *
* FORM   0         1         2         3         4                   *
*        01234567890123456789012345678901234567890                   *
* A)     IEC501A M NNN,VVVVVV,SL,,JOBNAME ,......     MOST WILL BE   *
* A)     IEC501A M NNN,VVVVVV,NL,,JOBNAME ,......       OR THESE     *
* B)     IEC501A M NNN,VVVVVV,BLP,DDDD BPI,JOBNAME , ...             *
* C)     IEC501A M NNN,VVVVVV,NL,DDDD BPI,JOBNAME ,   NEXT MOST      *
* C)     IEC501A M NNN,VVVVVV,SL,DDDD BPI,JOBNAME ,   ETC            *
* C)     IEC501A M NNN,TVVVVV,NL,DDDD BPI,JOBNAME , ....             *
* C)     IEC501A M NNN,VVVVVV,NL,DDDD BPI,JOBNAME , ...              *
* C)     IEC501A M NNN,VVVVV,BLP,DDDD BPI,JOBNAME , ...              *
* D)     IEC501A M NNN,VVVV,BLP,DDDD BPI,JOBNAME , ....              *
* D)     IEC501A M NNN,VVVVV,SL,DDDD BPI,JOBNAME , ...               *
* D)     IEC501A M NNN,VVVVV,NL,DDDD BPI,JOBNAME , ...               *
* E)     IEC501A M NNN,VVV,BLP,DDDD BPI,JOBNAME , ....               *
* E)     IEC501A M NNN,VVVV,NL,DDDD BPI,JOBNAME , ....               *
* E)     IEC501A M NNN,VVVV,SL,DDDD BPI,JOBNAME , ....               *
* F)     IEC501A M NNN,VVV,NL,DDDD BPI,JOBNAME , ....                *
* G)     IEC501A M NNN,VVVVVV,BLP,,JOBNAME , ........                *
* H)     IEC501A M NNN,VVVVV,NL,,JOBNAME ,......                     *
* H)     IEC501A M NNN,VVVVV,SL,,JOBNAME ,......                     *
* H)     IEC501A M NNN,VVVV,BLP,,JOBNAME ,......                     *
* I)     IEC501A M NNN,VVVV,SL,,JOBNAME ,......                      *
* I)     IEC501A M NNN,VVVV,NL,,JOBNAME ,......                      *
* I)     IEC501A M NNN,VVV,BLP,,JOBNAME ,......                      *
* J)     IEC501A M NNN,VVV,SL,,JOBNAME ,......                       *
* J)     IEC501A M NNN,VVV,NL,,JOBNAME ,......                       *
*   NOTE: YOU CAN CARRY THIS CHECKING ONLY SO FAR! IF SOME IDIOT     *
*         CALLS FOR A 2CHAR VOLSER IN CLASS=A, GIVE IT TO HIM/HER.   *
*--------------------------------------------------------------------*
         SPACE
$IEC501A EQU   *
         CLC   MSGID+23(2),=C',,'  IS IT FORM - A
         BE    FORMA
         CLC   MSGID+30(3),=C'BPI' IS IT FORM - B
         BE    FORMB
         CLC   MSGID+29(3),=C'BPI' IS IT FORM - C
         BE    FORMC
         CLC   MSGID+28(3),=C'BPI' IS IT FORM - D
         BE    FORMD
         CLC   MSGID+27(3),=C'BPI' IS IT FORM - E
         BE    FORME
         CLC   MSGID+26(3),=C'BPI' IS IT FORM - F
         BE    FORMF
         CLC   MSGID+24(2),=C',,'  IS IT FORM - G
         BE    FORMG
         CLC   MSGID+22(2),=C',,'  IS IT FORM - H
         BE    FORMH
         CLC   MSGID+21(2),=C',,'  IS IT FORM - I
         BE    FORMI
         CLC   MSGID+20(2),=C',,'  IS IT FORM - J
         BE    FORMJ
         B     RETURN              IGNORE OTHER FORMS
FORMA    EQU   *
         MVC   JOBNAME(8),MSGID+25 SAVE THE JOBNAME
         B     RUNBLOKS
FORMB    EQU   *
         MVC   JOBNAME(8),MSGID+34 SAVE THE JOBNAME
         B     RUNBLOKS
FORMC    EQU   *
         MVC   JOBNAME(8),MSGID+33 SAVE THE JOBNAME
         B     RUNBLOKS
FORMD    EQU   *
         MVC   JOBNAME(8),MSGID+32 SAVE THE JOBNAME
         B     RUNBLOKS
FORME    EQU   *
         MVC   JOBNAME(8),MSGID+31 SAVE THE JOBNAME
         B     RUNBLOKS
FORMF    EQU   *
         MVC   JOBNAME(8),MSGID+30 SAVE THE JOBNAME
         B     RUNBLOKS
FORMG    EQU   *
         MVC   JOBNAME(8),MSGID+26 SAVE THE JOBNAME
         B     RUNBLOKS
FORMH    EQU   *
         MVC   JOBNAME(8),MSGID+24 SAVE THE JOBNAME
         B     RUNBLOKS
FORMI    EQU   *
         MVC   JOBNAME(8),MSGID+23 SAVE THE JOBNAME
         B     RUNBLOKS
FORMJ    EQU   *
         MVC   JOBNAME(8),MSGID+22 SAVE THE JOBNAME
         B     RUNBLOKS
         SPACE
*--------------------------------------------------------------------*
*        TIME TO  GO RUN THE CONTROL BLOCKS                          *
*--------------------------------------------------------------------*
         SPACE
RUNBLOKS EQU   *
         CLI   JOBNAME+4,C','      IS THERE A COMMA
         BE    RUNBLOK1
         CLI   JOBNAME+5,C','      IS THERE A COMMA
         BE    RUNBLOK2
         CLI   JOBNAME+6,C','      IS THERE A COMMA
         BE    RUNBLOK3
         CLI   JOBNAME+7,C','      IS THERE A COMMA
         BE    RUNBLOK4
         B     RUNBLOK
RUNBLOK1 EQU   *
         MVC   JOBNAME+4(4),=CL4' ' SANITIZE JOBNAME
         B     RUNBLOK
RUNBLOK2 EQU   *
         MVC   JOBNAME+5(3),=CL3' ' SANITIZE JOBNAME
         B     RUNBLOK
RUNBLOK3 EQU   *
         MVC   JOBNAME+6(2),=CL2' ' SANITIZE JOBNAME
         B     RUNBLOK
RUNBLOK4 EQU   *
         MVI   JOBNAME+7,C' '      SANITIZE JOBNAME
RUNBLOK  EQU   *
         L     R1,CVTPTR           GET POINTER TO CVT
         USING CVTMAP,R1           ESTABLISH ADDRESSABILITY
         L     R2,CVTJESCT         GET POINTER TO JESCT
         DROP  R1                  NO LONGER NEED IT
         USING JESCT,R2            ESTABLISH ADDRESSABILITY
         L     R3,JESSSCT          GET POINTER 1ST JESCT SSCT (SSCVT)
         USING SSCT,R3             ESTABLISH ADDRESSABILITY
         L     R4,SSCTSSVT         GET JES SSVT
         USING SSVT,R4             ESTABLISH ADDRESSABILITY
         TM    SSCTSSID,SSCTJES2   IS IT JES2 ?
         BO    ISJES2              THANK HEAVENS
         DC    XL4'0'              BLOW UP
         DS    0H                  MAKE SURE WE ALIGN UP
ISJES2   EQU   *
         L     R5,SVTPIT           GET PIT ADDRESS
         USING PIT,R5
         L     R6,SVTHCT           GET HCT ADDRESS
         USING HCT,R6
         SPACE
*--------------------------------------------------------------------*
*        NOW START THE HUNT DOWN THE INITS.  AN STC LIKE DFHSM WILL  *
*        NOT BE AFFECTED, UNLESS YOU RUN IT AS A BATCH JOB.          *
*--------------------------------------------------------------------*
         SPACE
LOOP     EQU   *
         TM    PITSTAT,PITBUSY     IS INIT BUSY    ?
         BNO   NEXTPIT             IF NOT THEN KEEP SEARCHING
         L     R11,PITSJB          GET SJB ADDRESS
         USING SJB,R11
*        MVC   JMSG(8),JOBNAME     DEBUG CODE
*        MVC   JMSG+8(3),=CL3' = ' DEBUG CODE
*        MVC   JMSG+11(8),SJBJOBNM DEBUG CODE
*        MVC   JMSG+21(1),SJBJCLAS DEBUG CODE
*        LA    R0,L'JMSG           DEBUG CODE
*        LA    R1,JMSG             DEBUG CODE
*        TPUT  (R1),(R0),R         DEBUG CODE
         CLC   JOBNAME(8),SJBJOBNM IS IT THIS JOB ?
         BNE   NEXTPIT             KEEP ON SEARCHING
         CLI   SJBJCLAS,C'A'       IS IT A CLASS=A JOB ?
         BE    VIOLATED
         B     RETURN              CONGRATS - GOOD USER !!!!!!
         SPACE
NEXTPIT  EQU   *
         L     R5,PITNEXT          GET NEXT PIT
         LTR   R5,R5               ARE WE THROUGH
         BZ    RETURN              DID NOT MATCH ANY ???
         B     LOOP
         SPACE
VIOLATED EQU   *
*        MVI   THEMSG,C' '         BLANK OUT MSG
*        MVC   THEMSG+1(79),THEMSG BLANK OUT REST
*        MVC   THEMSG(L'ERRMSG),ERRMSG
*        LA    R1,THEMSG           DEBUG
*        LA    R0,L'ERRMSG         DEBUG
*        TPUT  (R1),(R0),R         DEBUG MSG
*        B     EOD
*--------------------------------------------------------------------*
*        TIME TO "BLAST'EM OUT OF THE SYSTEM"                        *
*--------------------------------------------------------------------*
         SPACE
         XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR PARAMETER LIST
         MVI   MGCRTEXT,C' '                  MOVE IN BLANK
         MVC   MGCRTEXT+1(125),MGCRTEXT       ROLL IT
         MVC   MGCRTEXT(CANCMDL),CANCMD       MOVE IN THE COMMAND
         MVC   MGCRTEXT+L'$DMMSG(8),JOBNAME   MOVE IN JOBNAME
         LA    R1,(MGCRTEXT-MGCRPL)+$DMMSGL   GET LENGTH OF MGCR
         STC   R1,MGCRLGTH                    STORE THE LENGTH
         SLR   R0,R0                          CLEAR R0
         MGCR  MGCRPL                         HEY YOU !!!!!
         SPACE
*--------------------------------------------------------------------*
*        NEED TO ALLOW FOR TIME TO GET MSG INTO THE JOBLOG BEFORE    *
*        WE DO THE "DIRTY HARRY" ROUTINE.                            *
*--------------------------------------------------------------------*
         SPACE
         STIMER WAIT,BINTVL=SNOOZE            STOP ! POLICE
         SPACE
         XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR PARAMETER LIST
         MVI   MGCRTEXT,C' '                  MOVE IN BLANK
         MVC   MGCRTEXT+1(125),MGCRTEXT       ROLL IT
         MVC   MGCRTEXT(CANCMDL),CANCMD       MOVE IN THE COMMAND
         MVC   MGCRTEXT+L'CANCMD(8),JOBNAME   MOVE IN JOBNAME
         LA    R1,(MGCRTEXT-MGCRPL)+CANCMDL   GET LENGTH OF MGCR
         STC   R1,MGCRLGTH                    STORE THE LENGTH
         SLR   R0,R0                          CLEAR R0
         MGCR  MGCRPL                         GOTCHA  !!!!!
         SPACE
RETURN   EQU   *
*        LA    R1,THEMSG+4         DEBUG
*        LA    R0,L'DMSG1          DEBUG
*        TPUT  (R1),(R0),R         DEBUG MSG
*EOD     EQU   *
         LR    R1,R13              PT TO GETMAINED AREA
         L     R13,SAVE+4
         FREEMAIN RU,LV=WORKSIZE,A=(R1),SP=SP230
         RETURN (14,12),RC=0
         SPACE
*--------------------------------------------------------------------*
*        DATA AREAS BELOW                                            *
*--------------------------------------------------------------------*
         SPACE
TMS001   DC    CL6'TMS001'
TMS002   DC    CL6'TMS002'
IEC501A  DC    CL7'IEC501A'
IEF233A  DC    CL7'IEF233A'
IEF233D  DC    CL7'IEF233D'
         SPACE
*ERRMSG  DC    C'7CG0079E  MOUNTING TAPES IN CLASS=A IS PROHIBITED'''
*DMSG1   DC    C'CORRECT CLASS USED'
*IMSG    DC    C'BEGINNING ENFORCEMENT'
*JMSG    DS    0CL40
*        DC    CL19' '
*        DC    C'='
*        DC    C' '
*        DC    C' '
*        DC    C' '
*JMSGM   DC    CL18' '
         SPACE
$DMMSG   DC    CL5'Â¢DM,'''         OUR JES PREFIX IS CENT SIGN ????
$DMJOBN  DC    CL8' '              JOBNAME
$DMFILL  DC    CL3''','''          CONTINUE WITH CMD
$DMUDIE  DC    C'7CG0079E  MOUNTING TAPES IN CLASS=A IS PROHIBITED'''
$DMMSGL  EQU   *-$DMMSG
CANCMD   DC    CL7'CANCEL '        KILL
CANJOBN  DC    CL8'XXXXXXX'        SLIMBALL
         DC    CL9' '
         DC    C'CANCELLATION DONE BY SYSTEM EXIT '
CANCMDL  EQU   *-CANCMD
         SPACE
         LTORG
         DS    0F
WORKSIZE DC    AL1(000),AL3(WORKEND-WORKAREA)
SNOOZE   DC    A(1*100)            ONE SECOND DELAY
         SPACE
WORKAREA DSECT
SAVE     DS    18F
         SPACE
*--------------------------------------------------------------------*
*        126 BYTES (MGCRTEXT) IS THERE FOR COMMAND                   *
*--------------------------------------------------------------------*
MGCR     IEZMGCR DSECT=NO
         SPACE
JOBNAME  DS    CL8
         DS    0F
THEMSG   DS    CL80
WORKEND  EQU   *
         SPACE
MSGTEXT  DSECT
MSGID    DS    CL8                 MSGID HERE
MSGDATA  DS    CL118               ESTIMATE OF REST OF DATA
         SPACE
         PRINT ON,NOGEN
         CVT   DSECT=YES,LIST=NO
         SPACE
         IEFJESCT
         SPACE
         IEFJSCVT
         SPACE
         GBLC  &VERSION
&VERSION SETC  'SP 2.1.5'          ALSO MEANS HIGHER TO SP 2.2.0
         SPACE
         GBLC  &MACSPLV
&MACSPLV SETC  '2'
         SPACE
         $HASPEQU
         SPACE
         $HCT
         SPACE
         $HFAME
         SPACE
         $MIT
         SPACE
         $PCE
         SPACE
         $PIT
         SPACE
         $SCAT
         SPACE
         $SJB
         SPACE
         $SVT
         SPACE
         $TQE
         SPACE
         $XECB
         SPACE
         PRINT ON,GEN
         IEZVX100
         END
