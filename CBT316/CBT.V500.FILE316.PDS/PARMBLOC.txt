         TITLE '   P A R M B L O C   '
*$DOC@*****************************************************************
*                                                                     *
*   PARMBLOC - WRITTEN BY BILL GODFREY,                               *
*              PLANNING RESEARCH CORPORATION, MCLEAN, VA.             *
*                                                                     *
*   UPDATED AT AFDSC/PENTAGON BY BILL GODFREY                         *
*                                                                     *
*   THIS PROGRAM PRINTS A PAGE CONTAINING A ROW OF BLOCK              *
*   LETTERS.  THE PARM FIELD SPECIFIES THE LETTERS TO BE              *
*   PRINTED.  UP TO 8 CHARACTERS ARE ALLOWED.                         *
*                                                                     *
*   SAMPLE JCL                                                        *
*     //STEP EXEC PGM=PARMBLOC,PARM='STRING'                          *
*     //SYSBLOC  DD SYSOUT=A                                          *
*                                                                     *
*   IF THERE IS NO SYSBLOC DD PRESENT, IT WILL TRY SYSPRINT.          *
*                                                                     *
*   THE STRING IN THE PARM FIELD MAY BE FOLLOWED BY A COMMA AND       *
*   THEN SOME OPTION CODES.  THESE CODES ARE                          *
*     C - CENTER THE BLOCK LETTERS ON THE PAGE,                       *
*     L - LEAVE LETTERS UNCENTERED ON THE PAGE,                       *
*     S - MAKE THE BLOCK LETTERS SLANTED,                             *
*     V - MAKE VERTICAL (UNSLANTED) LETTERS,                          *
*     M - IF OUTPUT RECFM IS NOT SET YET, MAKE IT FBM,                *
*     A - IF OUTPUT RECFM IS NOT SET YET, MAKE IT FBA                 *
*                                                                     *
*   THE OPTION DEFAULTS ARE CSA.                                      *
*   THE DEFAULT ATTRIBUTES OF THE OUTPUT FILE ARE                     *
*   RECFM=FBA,LRECL=121,BLKSIZE=121.                                  *
*   IF DIFFERENT ATTRIBUTES ARE PRESENT IN THE JCL OR                 *
*   DATA SET LABEL, THEY WILL BE RECOGNIZED AND USED.                 *
*   THE LRECL SHOULD NOT EXCEED 133 FOR FIXED LENGTH RECORDS          *
*   AND 137 FOR VARIABLE LENGTH RECORDS.                              *
*                                                                     *
*   AN OVERRIDING DDNAME MAY BE SPECIFIED AFTER THE SECOND COMMA      *
*   IN THE PARM FIELD.   PARM='STRING,,SYSUT2'                        *
*                                                                     *
*$DOC$*****************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        'PARMBLOC'                                                   *
*                                                                     *
***********************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. PRC, MCLEAN VA.
*        DATE WRITTEN. NOV 28 1979.
*        DATE UPDATED. OCTOBER 30 1981.
*        ATTRIBUTES. RE-ENTRANT.
*        DESCRIPTION.
*         THIS PROGRAM PRINTS A PAGE CONTAINING AN 8-CHARACTER
*         NAME IN BLOCK LETTERS (12 BY 12).  THE NAME IS
*         SPECIFIED IN THE PARM FIELD.
*
*         THE PARM FIELD HAS THE FOLLWING FORMAT:
*         PARM=NAME OR PARM=(NAME,OPTIONS)
*         OR PARM=(NAME,OPTIONS,DDNAME)
*         WHERE 'NAME' IS ONE TO 8 CHARACTERS
*         AND 'OPTIONS' ARE AS FOLLOWS:
*         'C' MEANS CENTER THE BLOCK LETTERS ON THE LINE,
*         'S' MEANS SLANT THE BLOCK LETTERS.
*         'M' MEANS MACHINE CARRIAGE CONTROLS WILL BE USED
*             IF RECFM IS NOT OTHERWISE DEFINED.
*         EXAMPLE: PARM=(ABC,SC) WILL PRINT 'ABC' IN
*         SLANTED LETTERS CENTERED ON THE LINE.
*         IF 'NAME' IS LONGER THAN 8, ONLY THE FIRST 8
*         WILL BE USED.
*
*
*         THE SYSBLOC FILE CAN HAVE ANY OF SEVERAL RECORD
*         FORMATS. THE DEFAULT IS:
*         RECFM = FBA   LRECL = 121   BLKSIZE = 121
*         THE RECFM MAY BE FA, FBA, VA, VBA, FM, FBM, VM, OR VBM.
*         IF 'A' OR 'M' IS NOT SPECIFIED, THE FIRST BYTE OF
*         EACH LINE WILL STILL CONTAIN ASA CARRIAGE CONTROLS,
*         BUT THEY WILL NOT BE TREATED AS SUCH AND WILL APPEAR
*         ON THE PRINTED OUTPUT.
*
*         THE IBM VERSION OF 'IEFSD095' DOES A GETMAIN AND FREEMAIN
*         EACH TIME IT IS CALLED.  THIS PROGRAM CALLS A MODIFIED
*         VERSION OF IEFSD095 WHICH ACCEPTS A 5TH PARAMETER
*         AS A WORKAREA SO IT DOESNT DO THE GETMAIN/FREEMAIN.
*
*         10APR80 - PARMBL12 CHANGED FROM LOAD MACRO TO VCON.
*         10APR80 - MVCL CHANGED TO 2 XC'S.
*         25APR80 - SUPPORT FOR MACHINE CARRIAGE CONTROL ADDED.
*         28APR80 - SLANT INCREMENT CH FROM 11 TO 11-OFFSET.
*         30MAY80 - OPTIONS L,V,A ADDED. DEFAULT CSA. $DOC.
*         07OCT81 - CALL EALSDW95 INSTEAD OF IEFSD095
*                   TO SUPPORT SPECIAL CHARACTERS.
*                   TRY SYSPRINT IF SYSBLOC DD NOT PRESENT.
*         30OCT81 - ALLOW DDNAME TO BE SPECIFIED IN PARM. REMOVE WTO.
         SPACE
PARMBLOC START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'PARMBLOC '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(R13)       SAVE REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R2,R1               PARM POINTER
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R9,R1               INITIALIZE WORKAREA POINTER
         USING @DATA,R9
         SPACE 1
         XC    0(256,R1),0(R1)
         XC    256(@DATAL-256,R1),256(R1)
         SPACE 1
         ST    R13,4(,R9)          CHAIN SAVEAREA
         ST    R9,8(,R13)          CHAIN SAVEAREA
         LR    R13,R9              UPDATE SAVEAREA POINTER
         SPACE
************************************************************
*                                                          *
*         SCAN THE PARM FIELD                              *
*                                                          *
************************************************************
         SPACE 1
         LA    R3,PRTDCBW          POINT R3 TO DCB IN WORK AREA
         MVC   0(PRTDCBL,R3),PRTDCB
         OI    OPTION,CENTER+SLANT DEFAULT OPTIONS
         L     R2,0(,R2)           POINT TO PARM
         LA    R5,2(,R2)           POINT TO PARM DATA
         LH    R4,0(,R2)           GET LENGTH OF PARM
         MVC   NAME(8),=CL8' '
         SLR   R6,R6               COUNTER
         LTR   R4,R4               IS THERE A PARM
         BZ    PARMX               NO, BRANCH
         LA    R1,NAME
         LA    R0,8                LENGTH OF NAME
         CR    R4,R0
         BH    *+6
         LR    R0,R4               IF LEN LESS THAN 8, USE LEN
PARMNAME CLI   0(R5),C','
         BE    PARMNAMX
         MVC   0(0,R1),0(R5)
         LA    R6,1(,R6)           COUNT BYTES MOVED
         LA    R1,1(,R1)
         LA    R5,1(,R5)
         BCTR  R4,0
         BCT   R0,PARMNAME
         LTR   R4,R4
         BZ    PARMX
PARMNAMI CLI   0(R5),C','
         BE    PARMNAMX
         LA    R5,1(,R5)
         BCT   R4,PARMNAMI
         B     PARMX
PARMNAMX EQU   *
         LA    R5,1(,R5)           POINT PAST COMMA
         BCTR  R4,0
         LTR   R4,R4
         BNP   PARMX
PARMOPT  CLI   0(R5),C','
         BE    PARMDD
         CLI   0(R5),C'S'
         BNE   *+8
         OI    OPTION,SLANT
         CLI   0(R5),C'V'
         BNE   *+8
         NI    OPTION,255-SLANT
         CLI   0(R5),C'C'
         BNE   *+8
         OI    OPTION,CENTER
         CLI   0(R5),C'L'
         BNE   *+8
         NI    OPTION,255-CENTER
         CLI   0(R5),C'M'
         BNE   *+8
         OI    OPTION,MACHINE
         CLI   0(R5),C'A'
         BNE   *+8
         NI    OPTION,255-MACHINE
         CLI   0(R5),C'N'
         BNE   *+8
         OI    OPTION,NOH
         LA    R5,1(,R5)
         BCT   R4,PARMOPT
         B     PARMX
PARMDD   EQU   *
         LA    R5,1(,R5)
         BCTR  R4,0
         LTR   R4,R4
         BNP   PARMX
         MVC   DDNAM(8,R3),=CL8' '
         BCTR  R4,0
         EX    R4,PARMDDM
         B     PARMX
PARMDDM  MVC   DDNAM(0,R3),0(R5)
PARMX    EQU   *
         LTR   R6,R6
         BNZ   *+8
         LA    R6,1
         ST    R6,LENGTH
         SPACE
************************************************************
*                                                          *
*         OPEN THE PRINT FILE                              *
*                                                          *
************************************************************
         SPACE
         MVC   DDNAME,DDNAM(R3)
         DEVTYPE DDNAME,DEVAREA
         LTR   R15,R15             IS SYSBLOC ALLOCATED
         BZ    *+10                YES, USE IT
         MVC   DDNAM(8,R3),=CL8'SYSPRINT'  NO, USE SYSPRINT
         LA    R15,PRTEXL          GET EXIT LIST ADDRESS
         IC    R14,EXLST(,R3)      EXLST EQU 36
         ST    R15,EXLST(,R3)      STORE ADDRESS IN DCB
         STC   R14,EXLST(,R3)
         LA    R14,PRTEXO          GET OPEN EXIT ADDRESS
         ST    R14,0(,R15)         STORE IN EXIT LIST
         MVI   0(R15),X'85'        SET EXIT CODE
         LA    R1,OPEN
         MVI   0(R1),X'80'
         OPEN  ((R3),OUTPUT),MF=(E,(1))
         TM    OFLGS(R3),X'10'     WAS OPEN SUCCESSFUL?
         BO    OKPRT               YES
*        LA    R1,MSG01
*        WTO   MF=(E,(1))
         B     EXIT12
PRTEXO   CLI   RECFM(R1),0         IS RECFM OK
         BNE   PRTEXOR             YES, BRANCH
         MVI   RECFM(R1),X'94'     FBA
         TM    OPTION,MACHINE
         BNO   *+8
         MVI   RECFM(R1),X'92'     FBM
PRTEXOR  TM    RECFM(R1),X'40'     VARIABLE LENGTH
         BO    PRTEXOV             YES, BRANCH
         TM    RECFM(R1),X'10'     BLOCKED?
         BO    PRTEXOFB            YES, BRANCH
         CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),=H'121' DEFAULT BLKSI RECFM FA
         BR    R14
PRTEXOFB CLC   LRECL(2,R1),=H'0'
         BNE   *+10
         MVC   LRECL(2,R1),=H'121' DEFAULT LRECL RECFM FBA
         CLC   BLKSI(2,R1),=H'0'
         BNE   *+10
         MVC   BLKSI(2,R1),LRECL(R1) FBA BLKSI = LRECL
         BR    R14
PRTEXOV  TM    RECFM(R1),X'10'     BLOCKED?
         BO    PRTEXOVB
         CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),=H'125' DEFAULT BLKSI RECFM VA
         BR    R14
PRTEXOVB CLC   LRECL(2,R1),=H'0'
         BNE   *+10
         MVC   LRECL(2,R1),=H'125' DEFAULT LRECL RECFM VBA
         CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         LH    R8,LRECL(,R1)
         LA    R8,4(,R8)
         STH   R8,BLKSI(,R1)       VBA BLKSI = LRECL+4
         BR    R14
OKPRT    OI    STATUS,X'80'        FILE IS OPEN
         SPACE 1
************************************************************
*                                                          *
*         LOAD THE BLOCK LETTER GENERATOR                  *
*                                                          *
************************************************************
         SPACE
*        LA    R0,=CL8'EALSDW95'
*        LOAD  EPLOC=(0)
         L    R0,=V(EALSDW95)
         ST    R0,LOADPTR
         SPACE
************************************************************
*                                                          *
*         PRINT SOME BLANK LINES                           *
*                                                          *
************************************************************
         SPACE
         SLR   R0,R0
         ST    R0,LINEV
         MVI   LINE-1,C' '
         MVC   LINE,LINE-1
         MVI   CCS,C' '            SET SINGLE SPACE
         TM    RECFM(R3),X'04'     ASA CARRIAGE CONTROL?
         BO    CCA                 BRANCH IF ASA
         TM    RECFM(R3),X'02'     MACHINE CARRIAGE CONTROL?
         BNO   NOCC                BRANCH IF NO CARRIAGE CONTROL
CCM      MVI   CCS,X'09'           SET SINGLE SPACE
         TM    OPTION,NOH          REQUEST ALL SINGLE SPACE
         BO    NOCCM1              YES, BRANCH
         MVI   LINE,X'8B'          SKIP TO CHANNEL 1 IMMEDIATE
         BAL   R8,PUTLINE          SKIP TO CHANNEL 1
NOCCM1   MVI   LINE,X'09'          SINGLE SPACE
         B     NOCC
CCA      TM    OPTION,NOH          REQUEST ALL SINGLE SPACE
         BO    NOCC                YES, BRANCH
         MVI   LINE,C'1'           YES, SKIP TO CHANNEL 1
NOCC     EQU   *
         BAL   R8,PUTLINE          FIRST BLANK LINE
         MVC   LINE(1),CCS         SINGLE SPACE
         BAL   R8,PUTLINE          SECOND BLANK LINE
         BAL   R8,PUTLINE          THIRD BLANK LINE
         SPACE
************************************************************
*                                                          *
*         INITIALIZE THE PARAMETER LIST                    *
*                                                          *
************************************************************
         SPACE
         LA    R1,PARAMS
         LA    R0,NAME
         ST    R0,0(,R1)
         LA    R0,SUBSCRIP
         ST    R0,4(,R1)
         LA    R7,LINE+1+OFFSET
         TM    OPTION,CENTER       TO BE CENTERED?
         BZ    NOCENT              NO, BRANCH
         LA    R15,8
         SR    R15,R6              GET 8-LENGTH
         MH    R15,=H'7'           (1+12+1)/2
         AR    R7,R15              ADD (8-LENGTH)*7 TO CENTER IT
NOCENT   EQU   *
         TM    OPTION,SLANT        TO BE SLANTED?
         BZ    NOSLANT             NO
         LA    R7,11-OFFSET(,R7)   ADD 11 FOR SLANTING
NOSLANT  ST    R7,8(,R1)
         LA    R0,LENGTH
         ST    R0,12(,R1)
         LA    R0,WORK
         ST    R0,16(,R1)
         SPACE
************************************************************
*                                                          *
*         PRINT THE BLOCK LETTERS                          *
*                                                          *
************************************************************
         SPACE
         SLR   R4,R4
         LA    R5,12               VERTICAL SIZE OF BLOCK
         L     R6,LOADPTR
CALLOOP  LA    R4,1(,R4)           INCREMENT SUBSCRIPT
         ST    R4,SUBSCRIP
         MVC   LINE,LINE-1
         LA    R1,PARAMS
         LR    R15,R6
         BALR  R14,R15             CALL THE BLOCK LETTER GENERATOR
         MVC   LINE(1),CCS         SINGLE SPACE
         BAL   R8,PUTLINE
         TM    OPTION,SLANT        SLANTING?
         BZ    NOSLAND             NO, BYPASS DECREMENT
         L     R7,PARAMS+8         GET ADDRESS OF OUTPUT
         BCTR  R7,0                SHIFT LEFT ONE BYTE
         ST    R7,PARAMS+8         STORE SHIFTED ADDRESS
NOSLAND  EQU   *
         BCT   R5,CALLOOP
         SPACE
************************************************************
*                                                          *
*         TERMINATION                                      *
*                                                          *
************************************************************
         SPACE
EXIT0    SR    R15,R15             RETURN CODE ZERO
         B     EXIT
EXIT12   LA    R15,12              RETURN CODE 12
EXIT     LR    R2,R15              SAVE RETURN CODE
         TM    STATUS,X'80'        IS FILE OPEN?
         BZ    EXITCPRT            NO
         LA    R3,PRTDCBW
         LA    R1,CLOSE
         MVI   0(R1),X'80'
         CLOSE ((R3)),MF=(E,(1))
EXITCPRT EQU   *
EXITX    LR    R15,R2              RESTORE RETURN CODE
         LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         ST    R15,16(,R13)        STORE RETURN CODE FOR LM
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(R13)       LOAD RETURN ADDRESS AND RC
         BR    14                  RETURN
         SPACE
************************************************************
*                                                          *
*         PRINT A LINE                                     *
*                                                          *
************************************************************
         SPACE
PUTLINE  TM    RECFM(R3),X'40'     RECFM V
         BO    PUTV
         PUT   (R3),LINE
         BR    R8
PUTV     LH    R15,LRECL(R15)
         LA    R1,LINEV(R15)
PUTVLOOP BCTR  R15,0
         CLI   0(R15),C' '
         BNE   PUTVPUT
         BCTR  R1,0
         B     PUTVLOOP
PUTVPUT  CH    R15,=H'6'
         BNL   *+8
         LA    R15,6
         SLL   R15,16
         ST    R15,LINEV
         PUT   (R3),LINEV
         MVI   LINE-1,C' '
         BR    R8
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         PRINT NOGEN
         SPACE 1
PRTDCB   DCB   DDNAME=SYSBLOC,DSORG=PS,MACRF=PM
PRTDCBL  EQU   *-PRTDCB
         SPACE 1
         PRINT GEN
         SPACE 1
         LTORG
         DC    0D'0'
RECFM    EQU   36
EXLST    EQU   36
DDNAM    EQU   40
OFLGS    EQU   48                  DCB OFFSET
BLKSI    EQU   62
LRECL    EQU   82
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D                   DOUBLEWORD WORK AREA
OPEN     EQU   DOUBLE,4            OPEN LIST
CLOSE    EQU   DOUBLE,4            CLOSE LIST
DEVAREA  EQU   DOUBLE
STATUS   DS    C                   STATUS BITS
OPTION   DS    C                   OPTION BITS
SLANT    EQU   X'80'
CENTER   EQU   X'40'
MACHINE  EQU   X'20'
NOH      EQU   X'10'
CCS      DS    C                   BLANK IF ASA, X'09' IF MACHINE CC
DDNAME   DS    D
PRTDCBW  DS    0D,(PRTDCBL)X       DCB
PRTEXL   DS    F
LOADPTR  DS    F
PARAMS   DS    5F
NAME     DS    CL8
SUBSCRIP DS    F
LENGTH   DS    F
LINEV    DS    F
LINE     DS    CL133
OFFSET   EQU   6
WORK     DS    4D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
         TITLE '   E A L S D W 9 5  '
***********************************************************************
*                                                                     *
*                  I  E  F  S  D  0  9  5                             *
*                                                                     *
*   THIS MODULE HAS BEEN MODIFIED BY EASTERN AIR LINES.               *
*                                                                     *
*   UPDATES HAVE BEEN APPLIED SO THAT:                                *
*                                                                     *
*   1. THE SPECIAL CHARACTERS FOUND ON MOST PRINTER UNITS WILL        *
*      PRINT IN BLOCK FORMAT.                                         *
*                                                                     *
*      THE SPECIAL CHARACTERS ARE:                                    *
*                                                                     *
*      . < ( + | & $ * ) ; Â¬ - / , % _ > ? : # @ ' = ''               *
*                                                                     *
*   THIS MODULE HAS BEEN MODIFIED BY BILL GODFREY AT AFDSC/PENTAGON.  *
*                                                                     *
*   UPDATES HAVE BEEN APPLIED SO THAT:                                *
*                                                                     *
*   1. A 5TH PARAMETER IS PASSED, TO BE USED AS A WORKAREA.           *
*                                                                     *
*   2. THE GETMAIN AND FREEMAIN ARE REMOVED.                          *
*                                                                     *
*                                                                     *
***********************************************************************
*INITIALIZE
EALSDW95 CSECT
         USING *,R9
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'EALSDW95'
         DC    CL16' &SYSDATE &SYSTIME '
@PROLOG  STM   14,12,12(R13)
         LR    R9,R15                  SET BASE REGISTER
         L     R3,4(R1)                PARM 2, LINE NUMBER
         L     R3,0(R3)                LINE NUMBER
         L     R4,8(R1)                PARM 3, PRINT LINE AREA
         L     R11,12(R1)              PARM 4, LENGTH OF STRING
         L     R11,0(R11)              LENGTH OF STRING
         L     R12,16(,R1)             PARM 5, WORK AREA
         L     R1,0(R1)                PARM 1, STRING
         USING WORKAREA,R12
         ST    R13,SAVEREGR            SAVE R13
         XC    SAVECHAR(4),SAVECHAR    ZERO SAVECHAR
LOOP     MVC   SAVECHAR+3(1),0(R1)     GET CHARACTER
         L     R2,SAVECHAR             PUT IT IN R2
         LR    R10,R2                  CHAR TO BE USED TO FILL IN BLOCK
         C     R2,BNK                  IS IT A BLANK, IF SO
         BE    FBL                     BRANCH TO FBL
         C     R2,C1                   IS IT LESS THAN C1 IF SO
         BL    TROUBLE                 BRANCH TO TROUBLE
         C     R2,CA
         BL    TEST2
         C     R2,D1
         BL    TROUBLE
         C     R2,DA
         BL    TEST3
         C     R2,E2
         BL    TROUBLE
         C     R2,EA
         BL    TEST4
         C     R2,F0
         BL    TROUBLE
         C     R2,FA
         BL    TEST5
         B     FBL                                               *EAL*1
TROUBLE  C     R2,FOURB                                          *EAL*1
         BL    FBL                                               *EAL*1
         C     R2,FIVE1                                          *EAL*1
         BL    TEST6                                             *EAL*1
         C     R2,FIVEB                                          *EAL*1
         BL    FBL                                               *EAL*1
         C     R2,SIX2                                           *EAL*1
         BL    TEST7                                             *EAL*1
         C     R2,SIXB                                           *EAL*1
         BL    FBL                                               *EAL*1
         C     R2,SEVEN0                                         *EAL*1
         BL    TEST8                                             *EAL*1
         C     R2,SEVENA                                         *EAL*1
         BL    FBL                                               *EAL*1
         C     R2,EIGHT0                                         *EAL*1
         BL    TEST9                                             *EAL*1
FBL      LA    R6,CHARACTR-1          SET R6 TO ADDR OF BLANK
         B     EXIT1
TEST2    S     R2,C0                   GET OFF SET
         MH    R2,TWLV+2
         LA    R6,CHARACTR-1          SET  R6 TO ADDR OF LETTER
         AR    R6,R2
         B     EXIT1
TEST3    S     R2,D0
         MH    R2,TWLV+2
         A     R2,ONZET
         LA    R6,CHARACTR-1          SET  R6 TO ADDR OF LETTER
         AR    R6,R2
         B     EXIT1                                             *EAL*1
TEST6    S     R2,FOURA                                          *EAL*1
         B     TEST12                                            *EAL*1
TEST7    S     R2,FIVE4                                          *EAL*1
         B     TEST12                                            *EAL*1
TEST8    S     R2,FIVED                                          *EAL*1
         B     TEST12                                            *EAL*1
TEST9    S     R2,SIX7                                           *EAL*1
TEST12   MH    R2,TWLV+2                                         *EAL*1
         A     R2,SPECIAL                                        *EAL*1
         LA    R6,CHARACTR-1                                     *EAL*1
         AR    R6,R2                                             *EAL*1
         B     EXIT1
TEST4    S     R2,E1
         MH    R2,TWLV+2
         A     R2,TWOSX
         LA    R6,CHARACTR-1          SET  R6 TO ADDR OF LETTER
         AR    R6,R2
         B     EXIT1
TEST5    S     R2,EF
         MH    R2,TWLV+2
         A     R2,TROTW
         LA    R6,CHARACTR-1          SET  R6 TO ADDR OF LETTER
         AR    R6,R2
*   GET POINTER TO LINE PATTERN
EXIT1    XR    R7,R7
         AR    R6,R3
         IC    R7,0(R6)
*                                      LINE PATTERN FROM LINEPATH
         LA    R8,LINEPATN
         AR    R7,R8
         LH    R5,0(R7)
         SLL   R5,19
         O     R5,ONEX
         BXH   R5,R5,LP1
         STC   R10,0(R4)
LP1      BXH   R5,R5,LP2
         STC   R10,1(R4)
LP2      BXH   R5,R5,LP3
         STC   R10,2(R4)
LP3      BXH   R5,R5,LP4
         STC   R10,3(R4)
LP4      BXH   R5,R5,LP5
         STC   R10,4(R4)
LP5      BXH   R5,R5,LP6
         STC   R10,5(R4)
LP6      BXH   R5,R5,LP7
         STC   R10,6(R4)
LP7      BXH   R5,R5,LP8
         STC   R10,7(R4)
LP8      BXH   R5,R5,LP9
         STC   R10,8(R4)
LP9      BXH   R5,R5,LP10
         STC   R10,9(R4)
LP10     BXH   R5,R5,LP11
         STC   R10,10(R4)
LP11     BXH   R5,R5,LP12
         STC   R10,11(R4)
LP12     A     R4,FORT
         A     R1,ONEX
         S     R11,ONEX
         XR    R8,R8
         CR    R11,R8
         BH    LOOP
         L     R13,SAVEREGR
         RETURN (14,12),RC=0
*   CONSTANTS
         DS    0F
BNK      DC    XL4'40'
C1       DC    XL4'C1'
CA       DC    XL4'CA'
D1       DC    XL4'D1'
DA       DC    XL4'DA'
E2       DC    XL4'E2'
EA       DC    XL4'EA'
F0       DC    XL4'F0'
FA       DC    XL4'FA'
C0       DC    XL4'C0'
D0       DC    XL4'D0'
E1       DC    XL4'E1'
EF       DC    XL4'EF'
FOURA    DC    XL4'4A'                                           *EAL*1
FOURB    DC    XL4'4B'                                           *EAL*1
FIVE1    DC    XL4'51'                                           *EAL*1
FIVE4    DC    XL4'54'                                           *EAL*1
FIVEB    DC    XL4'5B'                                           *EAL*1
FIVED    DC    XL4'5D'                                           *EAL*1
SIX2     DC    XL4'62'                                           *EAL*1
SIX7     DC    XL4'67'                                           *EAL*1
SIXB     DC    XL4'6B'                                           *EAL*1
SEVEN0   DC    XL4'70'                                           *EAL*1
SEVENA   DC    XL4'7A'                                           *EAL*1
EIGHT0   DC    XL4'80'                                           *EAL*1
SPECIAL  DC    XL4'01B0'                                         *EAL*1
TWLV     DC    XL4'0C'
ONZET    DC    XL4'6C'
TWOSX    DC    XL4'D8'
TROTW    DC    XL4'0138'
FORT     DC    XL4'0E'
ONEX     DC    XL4'01'
CHARACTR DS    0F
BLANK    DC    12X'4C'
A        DC    X'020008080800000808080808'
B        DC    X'2C000808322A2A320808002C'
C        DC    X'020008262626262626080002'
D        DC    X'282A32080808080808322A28'
E        DC    X'000026262668682626260000'
F        DC    X'000026262668682626262626'
G        DC    X'020008262626343408080002'
H        DC    X'080808080800000808080808'
I        DC    X'020214141414141414140202'
J        DC    X'66660A0A0A0A0A0A3A3A686A'
K        DC    X'08322E3C3A36363A3C2E3208'
L        DC    X'262626262626262626260000'
M        DC    X'083E40424446080808080808'
N        DC    X'086C6E484A464E5052540876'
O        DC    X'000008080808080808080000'
P        DC    X'2C00080808002C2626262626'
Q        DC    X'020008080808084E50522C56'
R        DC    X'2C00080808002C3A3C2E3208'
S        DC    X'02000826585A5C5E64080002'
T        DC    X'000014141414141414141414'
U        DC    X'080808080808080808080002'
V        DC    X'080808080808083062206014'
W        DC    X'080808080808464442403E08'
X        DC    X'080830622060602062300808'
Y        DC    X'080830622060141414141414'
Z        DC    X'00001070120A147216742400'
ZERO     DC    X'040208080808080808080204'
ONE      DC    X'140C0E141414141414140202'
TWO      DC    X'020008646410121416180000'
THREE    DC    X'02000864641A1A6464080002'
FOUR     DC    X'061C1E202224001212121212'
FIVE     DC    X'0000262626282A106464002C'
SIX      DC    X'02000826262C000808080002'
SEVEN    DC    X'002C2E120A14141414141414'
EIGHT    DC    X'020008083004043008080002'
NINE     DC    X'020008080800006464080002'
PERIOD   DC    X'4C4C4C4C4C4C4C4C4C146014'                       *EAL*1
LESSTHAN DC    X'64700A7274262674720A7064'                       *EAL*1
LPAREN   DC    X'120A14147272727214140A12'                       *EAL*1
PLUS     DC    X'4C4C14141400001414144C4C'                       *EAL*1
BAR      DC    X'141414141414141414141414'                       *EAL*1
AMPRSAND DC    X'0CAC2222AC0CAEB0B2B4B6B8'                       *EAL*1
DOLLARS  DC    X'14040246785A5C7A46020414'                       *EAL*1
ASTERISK DC    X'4C4C14860288880286144C4C'                       *EAL*1
RPAREN   DC    X'167214140A0A0A0A14147216'                       *EAL*1
SEMICOLN DC    X'4C4C4C1460144C1460820A80'                       *EAL*1
NOT      DC    X'000064644C4C4C4C4C4C4C4C'                       *EAL*1
DASH     DC    X'4C4C4C4C4C00004C4C4C4C4C'                       *EAL*1
SLASH    DC    X'7C641070120A14721674187E'                       *EAL*1
COMMA    DC    X'4C4C4C4C4C4C4C1460820A80'                       *EAL*1
PERCENT  DC    X'66929496980A149A9C9EA0A2'                       *EAL*1
UNDSCORE DC    X'4C4C4C4C4C4C4C4C4C4C0000'                       *EAL*1
GRTHAN   DC    X'2674720A706464700A727426'                       *EAL*1
QUESTION DC    X'60886262120A14144C146014'                       *EAL*1
COLON    DC    X'4C4C4C4C1460144C4C146014'                       *EAL*1
POUND    DC    X'4C202000002020000020204C'                       *EAL*1
AT       DC    X'88043008A4A6A84E4EAA0488'                       *EAL*1
APOSTRFY DC    X'1460820A804C4C4C4C4C4C4C'                       *EAL*1
EQUAL    DC    X'4C4C4C00004C4C00004C4C4C'                       *EAL*1
QUOTES   DC    X'228A8C8E904C4C4C4C4C4C4C'                       *EAL*1
LINEPATN DS    0F
L1       DC    BL2'111111111111'
L2       DC    BL2'011111111110'
L3       DC    BL2'001111111100'
L4       DC    BL2'000000111000'
L5       DC    BL2'110000000011'
L6       DC    BL2'000000110000'
L7       DC    BL2'000011100000'
L8       DC    BL2'000111100000'
L9       DC    BL2'000000000110'
L10      DC    BL2'000000011000'
L11      DC    BL2'000001100000'
L12      DC    BL2'000110000000'
L13      DC    BL2'011000000000'
L14      DC    BL2'000000011110'
L15      DC    BL2'000001111000'
L16      DC    BL2'000011011000'
L17      DC    BL2'000110011000'
L18      DC    BL2'001100011000'
L19      DC    BL2'011111111111'
L20      DC    BL2'110000000000'
L21      DC    BL2'111111111000'
L22      DC    BL2'111111111100'
L23      DC    BL2'111111111110'
L24      DC    BL2'110000001100'
L25      DC    BL2'011000000110'
L26      DC    BL2'110000000110'
L27      DC    BL2'110000011111'
L28      DC    BL2'111111100000'
L29      DC    BL2'110001100000'
L30      DC    BL2'110000110000'
L31      DC    BL2'110000011000'
L32      DC    BL2'111000000111'
L33      DC    BL2'111100001111'
L34      DC    BL2'110110011011'
L35      DC    BL2'110011110011'
L36      DC    BL2'110001100011'
L37      DC    BL2'110110000011'
L38      DC    BL2'110011000011'
L39      DC    BL2'000000000000'
L40      DC    BL2'110000110011'
L41      DC    BL2'110000011011'
L42      DC    BL2'110000001111'
L43      DC    BL2'110000000111'
L44      DC    BL2'011111111011'
L45      DC    BL2'111000000000'
L46      DC    BL2'011111111100'
L47      DC    BL2'001111111110'
L48      DC    BL2'000000000111'
L49      DC    BL2'000011110000'
L50      DC    BL2'001100001100'
L51      DC    BL2'000000000011'
L52      DC    BL2'001111111111'
L53      DC    BL2'111111110000'
L54      DC    BL2'011111100000'
L55      DC    BL2'111000000011'
L56      DC    BL2'111100000011'
L57      DC    BL2'000000001100'
L58      DC    BL2'000011000000'
L59      DC    BL2'001100000000'
L60      DC    BL2'110000000001'
L61      DC    BL2'110001100001'                                 *EAL*1
L62      DC    BL2'100001100011'                                 *EAL*1
L63      DC    BL2'000000000001'                                 *EAL*1
L64      DC    BL2'010000000000'                                 *EAL*1
L65      DC    BL2'000000100000'                                 *EAL*1
L66      DC    BL2'000001110000'                                 *EAL*1
L67      DC    BL2'010001100010'                                 *EAL*1
L68      DC    BL2'011001100110'                                 *EAL*1
L69      DC    BL2'000111111000'                                 *EAL*1
L70      DC    BL2'011110111100'                                 *EAL*1
L71      DC    BL2'001110011100'                                 *EAL*1
L72      DC    BL2'000110001100'                                 *EAL*1
L73      DC    BL2'000100001000'                                 *EAL*1
L74      DC    BL2'011100000011'                                 *EAL*1
L75      DC    BL2'110110000110'                                 *EAL*1
L76      DC    BL2'011100001100'                                 *EAL*1
L77      DC    BL2'001000011000'                                 *EAL*1
L78      DC    BL2'000011000100'                                 *EAL*1
L79      DC    BL2'000110001110'                                 *EAL*1
L80      DC    BL2'001100011011'                                 *EAL*1
L81      DC    BL2'011000001110'                                 *EAL*1
L82      DC    BL2'110000000100'                                 *EAL*1
L83      DC    BL2'100111000011'                                 *EAL*1
L84      DC    BL2'001111110011'                                 *EAL*1
L85      DC    BL2'011000110011'                                 *EAL*1
L86      DC    BL2'011000110110'                                 *EAL*1
L87      DC    BL2'000110110000'                                 *EAL*1
L88      DC    BL2'000111100110'                                 *EAL*1
L89      DC    BL2'001100111100'                                 *EAL*1
L90      DC    BL2'011000011000'                                 *EAL*1
L91      DC    BL2'011000111100'                                 *EAL*1
L92      DC    BL2'001111100110'                                 *EAL*1
L93      DC    BL2'000111000010'                                 *EAL*1
         DC    0D'0'
         SPACE 1
WORKAREA DSECT
SAVECHAR DS    F
SAVEREGR DS    F
UNUSEDPT DS    6F
WORKEND  EQU   *
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
