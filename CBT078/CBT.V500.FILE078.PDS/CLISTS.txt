./ ADD NAME=BROWSE4  0137-89305-94340-0940-00048-00003-00000-CLIST
ISREDIT MACRO (DSNAME)
CONTROL NOMSG MAIN NOLIST NOCONLIST NOSYMLIST
IF &STR(&DSNAME) = ? THEN +
  DO
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(#BROWSE4)
    EXIT
  END
IF &STR(&DSNAME) ¬= &STR() THEN -
  DO
    ISREDIT (EDITDSN) = DATASET
    SET &RPAREN = &NRSTR())
    /*  DATASET NAME? */
    IF &SYSINDEX(&RPAREN,&STR(&DSNAME),1) < 1 AND -
       &SYSINDEX(&STR('),&STR(&DSNAME),1) < 1 AND -
       &SYSINDEX(&STR(.),&STR(&DSNAME),1) < 1 THEN -
      DO
        /*  MEMBER NAME OR PATTERN? */
        IF &STR(&SYSDSN('&EDITDSN(&DSNAME)')) = &STR(OK) OR -
           &SYSINDEX(&STR(*),&STR(&DSNAME),1) > 0 THEN -
          SET DSNAME = &STR('&EDITDSN(&DSNAME)')
      END
    IF &SUBSTR(1,&STR(&DSNAME)) = ' -
      THEN -
        SET DSNS = &SUBSTR(2:&LENGTH(&STR(&DSNAME))-1,&STR(&DSNAME))
      ELSE -
        SET DSNS = &STR(&SYSUID..&DSNAME)
    ISPEXEC VGET (ZVIEW) PROFILE
    IF &ZVIEW = &STR(YES) THEN -
      SET &VIEWM = &STR(VIEW)
    ELSE -
      SET &VIEWM = &STR(BROWSE)
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC &VIEWM DATASET('&DSNS')
    SET &BROWCC = &LASTCC
    IF &BROWCC > 4 THEN -
      DO
        SET &ZEDSMSG = &STR(DSN OR MEMBER NOT FOUND)
        SET &ZEDLMSG = &STR(DATASET '&DSNS' NOT FOUND)
        ISPEXEC SETMSG MSG(ISRZ001W)
      END
    ISPEXEC CONTROL ERRORS CANCEL
  END
ELSE -
  DO
    ISPEXEC SELECT PGM(ISRBRO) PARM(ISRBRO01)
  END
/* J. KALINICH, X4521 */
/* EDIT MACRO TO INVOKE BROWSE WHILST IN EDIT           */
./ ADD NAME=BROWZE   0110-91365-97122-0814-00014-00009-00000-CLIST
PROC 0 DEBUG MODE(BROWSE) DATASET() PANEL(ISRBROBA)
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
IF &MODE = &STR(VIEW) && -
  &PANEL = &STR(ISRBROBA) THEN -
  SET &PANEL = &STR(ISREDDE2)
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC &MODE DATASET('&DATASET') PANEL(&PANEL)
ISPEXEC CONTROL ERRORS CANCEL
END
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE VIEW/BROWSE WITH OPTIONAL CUSTOM PANEL      */
./ ADD NAME=DSK33XX  0112-90267-93008-0910-00236-00107-00000-CLIST
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
PANEL: +
ISPEXEC DISPLAY PANEL(DSK33XX)
IF &LASTCC = 8 THEN EXIT
CALC3350: +
IF &DLEN > 19069 THEN -
  DO
    SET &UTRK5 = 0
    SET &PTRK5 = 0
    SET &WTRK5 = 0
    SET &BTRK5 = 0
    SET &BCYL5 = 0
    SET &TTRK5 = 0
    SET &TCYL5 = 0
    SET &TVOL5 = 0
    GOTO CALC3380
  END
IF &KLEN = 0 OR &LENGTH(&KLEN) = 0 -
  THEN -
    DO
      SET C = 185
    END
  ELSE -
    DO
      SET C = 267
    END
SET BTRK5 = 19254 / (&C + &KLEN + &DLEN)
IF &NUMREC < &BTRK5 -
  THEN -
    SET UTRK5 = &DLEN * &NUMREC
  ELSE -
    SET UTRK5 = &DLEN * &BTRK5
SET WTRK5 = 19069 - &UTRK5
SET BCYL5 = 30 * &BTRK5
SET TTRK5 = &NUMREC // &BTRK5
IF &TTRK5 = 0 -
  THEN -
    SET TTRK5 = &NUMREC / &BTRK5
  ELSE -
    SET TTRK5 = (&NUMREC / &BTRK5) + 1
SET TCYL5 = &TTRK5 // 30
IF &TCYL5 = 0 -
  THEN -
    SET TCYL5 = &TTRK5 / 30
  ELSE -
    SET TCYL5 = (&TTRK5 / 30) + 1
/* SINGLE DENSITY */
SET TVOL5 = &TCYL5 // 555
IF &TVOL5 = 0 -
  THEN -
    SET TVOL5 = &TCYL5 / 555
  ELSE -
    SET TVOL5 = (&TCYL5 / 555) + 1
SET PTRK5 = (&UTRK5 * 1000) / 19069
IF &PTRK5 > 999 THEN -
  DO
    SET PTRK5 = &SUBSTR(1:3,&PTRK5)&STR(.)&SUBSTR(4:4,&PTRK5)
    GOTO CALC3380
  END
IF &PTRK5 > 99 THEN -
  DO
    SET PTRK5 = &SUBSTR(1:2,&PTRK5)&STR(.)&SUBSTR(3:3,&PTRK5)
    GOTO CALC3380
  END
IF &PTRK5 > 9 THEN -
  DO
    SET PTRK5 = &SUBSTR(1:1,&PTRK5)&STR(.)&SUBSTR(2:2,&PTRK5)
    GOTO CALC3380
  END
SET PTRK5 = &STR(.)&SUBSTR(1:1,&PTRK5)
CALC3380: +
IF &DLEN > 47476 THEN -
  DO
    SET &UTRK8 = 0
    SET &PTRK8 = 0
    SET &WTRK8 = 0
    SET &BTRK8 = 0
    SET &BCYL8 = 0
    SET &TTRK8 = 0
    SET &TCYL8 = 0
    SET &TVOL8 = 0
    GOTO CALC3390
  END
IF &KLEN = 0 OR &LENGTH(&KLEN) = 0 -
  THEN -
    DO
      SET C = 15
      SET K = 0
    END
  ELSE -
    DO
      SET C = 22
      SET K = (&KLEN + 12) // 32
      IF &K = 0 -
        THEN -
          SET K = (&KLEN + 12) / 32
        ELSE -
          SET K = ((&KLEN + 12) / 32) + 1
    END
SET D = (&DLEN + 12) // 32
IF &D = 0 -
  THEN -
    SET D = (&DLEN + 12) / 32
  ELSE -
    SET D = ((&DLEN + 12) / 32) + 1
SET BTRK8 = 1499 / (&C + &K + &D)
IF &NUMREC < &BTRK8 -
  THEN -
    SET UTRK8 = &DLEN * &NUMREC
  ELSE -
    SET UTRK8 = &DLEN * &BTRK8
SET WTRK8 = 47476 - &UTRK8
SET BCYL8 = 15 * &BTRK8
SET TTRK8 = &NUMREC // &BTRK8
IF &TTRK8 = 0 -
  THEN -
    SET TTRK8 = &NUMREC / &BTRK8
  ELSE -
    SET TTRK8 = (&NUMREC / &BTRK8) + 1
SET TCYL8 = &TTRK8 // 15
IF &TCYL8 = 0 -
  THEN -
    SET TCYL8 = &TTRK8 / 15
  ELSE -
    SET TCYL8 = (&TTRK8 / 15) + 1
/* DOUBLE DENSITY */
SET TVOL8 = &TCYL8 // 1770
IF &TVOL8 = 0 -
  THEN -
    SET TVOL8 = &TCYL8 / 1770
  ELSE -
    SET TVOL8 = (&TCYL8 / 1770) + 1
SET PTRK8 = (&UTRK8 * 1000) / 47476
IF &PTRK8 > 999 THEN -
  DO
    SET PTRK8 = &SUBSTR(1:3,&PTRK8)&STR(.)&SUBSTR(4:4,&PTRK8)
    GOTO CALC3390
  END
IF &PTRK8 > 99 THEN -
  DO
    SET PTRK8 = &SUBSTR(1:2,&PTRK8)&STR(.)&SUBSTR(3:3,&PTRK8)
    GOTO CALC3390
  END
IF &PTRK8 > 9 THEN -
  DO
    SET PTRK8 = &SUBSTR(1:1,&PTRK8)&STR(.)&SUBSTR(2:2,&PTRK8)
    GOTO CALC3390
  END
SET PTRK8 = &STR(.)&SUBSTR(1:1,&PTRK8)
CALC3390: +
IF &KLEN = 0 OR &LENGTH(&KLEN) = 0 -
  THEN -
    DO
      SET C = 19
      SET K = 0
    END
  ELSE -
    DO
      SET C = 28
      SET KN = (&KLEN + 6) // 232
      IF &KN = 0 -
        THEN -
          SET KN = (&KLEN + 6) / 232
        ELSE -
          SET KN = ((&KLEN + 6) / 232) + 1
      SET K = (&KLEN + (&KN * 6) + 6) // 34
      IF &K = 0 -
        THEN -
          SET K = (&KLEN + (&KN * 6) + 6) / 34
        ELSE -
          SET K = ((&KLEN + (&KN * 6) + 6) / 34) + 1
    END
SET DN = (&DLEN + 6) // 232
IF &DN = 0 -
  THEN -
    SET DN = (&DLEN + 6) / 232
  ELSE -
    SET DN = ((&DLEN + 6) / 232) + 1
SET D = (&DLEN + (&DN * 6) + 6) // 34
IF &D = 0 -
  THEN -
    SET D = (&DLEN + (&DN * 6) + 6) / 34
  ELSE -
    SET D = ((&DLEN + (&DN * 6) + 6) / 34) + 1
SET BTRK9 = 1729 / (&C + &K + &D)
IF &NUMREC < &BTRK9 -
  THEN -
    SET UTRK9 = &DLEN * &NUMREC
  ELSE -
    SET UTRK9 = &DLEN * &BTRK9
SET WTRK9 = 56664 - &UTRK9
SET BCYL9 = 15 * &BTRK9
SET TTRK9 = &NUMREC // &BTRK9
IF &TTRK9 = 0 -
  THEN -
    SET TTRK9 = &NUMREC / &BTRK9
  ELSE -
    SET TTRK9 = (&NUMREC / &BTRK9) + 1
SET TCYL9 = &TTRK9 // 15
IF &TCYL9 = 0 -
  THEN -
    SET TCYL9 = &TTRK9 / 15
  ELSE -
    SET TCYL9 = (&TTRK9 / 15) + 1
/* DOUBLE DENSITY */
SET TVOL9 = &TCYL9 // 2226
IF &TVOL9 = 0 -
  THEN -
    SET TVOL9 = &TCYL9 / 2226
  ELSE -
    SET TVOL9 = (&TCYL9 / 2226) + 1
SET PTRK9 = (&UTRK9 * 1000) / 56664
IF &PTRK9 > 999 THEN -
  DO
    SET PTRK9 = &SUBSTR(1:3,&PTRK9)&STR(.)&SUBSTR(4:4,&PTRK9)
    GOTO PANEL
  END
IF &PTRK9 > 99 THEN -
  DO
    SET PTRK9 = &SUBSTR(1:2,&PTRK9)&STR(.)&SUBSTR(3:3,&PTRK9)
    GOTO PANEL
  END
IF &PTRK9 > 9 THEN -
  DO
    SET PTRK9 = &SUBSTR(1:1,&PTRK9)&STR(.)&SUBSTR(2:2,&PTRK9)
    GOTO PANEL
  END
SET PTRK9 = &STR(.)&SUBSTR(1:1,&PTRK9)
GOTO PANEL
END
/* J.KALINICH/M. RESCHKE, X4521 */
/* CLIST TO COMPUTE 3350/3380/3390 SPACE CALCULATIONS            */
./ ADD NAME=DVOL     0160-88230-98197-0734-00132-00111-00000-CLIST
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL MSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
PANEL: +
ISPEXEC DISPLAY PANEL(DVOL)
IF &LASTCC = 8 THEN EXIT
CREATE: +
ISPEXEC TBCREATE DVOL NAMES(DVOLSER DVOLF1 DVOLUNIT DVOLF2 +
  DVOLTOT DVOLF3 DVOLLINE) NOWRITE REPLACE
IF &LASTCC > 4 THEN -
  DO
    WRITE TBCREATE FAILED
    EXIT
  END

SET LINELIM = 2000
SET SYSOUTTRAP = &LINELIM
CONTROL NOPROMPT
DVOL (&DSER1 &DSER2 &DSER3 &DSER4 &DSER5 +
      &DSER6 &DSER7 &DSER8 &DSER9 &DSER10) &DTYPE

SET MAXLINE = &SYSOUTLINE
IF &DTYPE = &STR(SHORT) THEN -
  SET NUM = 3
ELSE -
  SET NUM = 1
DO WHILE &NUM ¬> &MAXLINE AND &NUM ¬> &LINELIM
  SET SYSSCAN = 0
  SET HVAL1 = &SYSOUTLINE
  SET SYSSCAN = 1
  SET HVAL2 = &HVAL1&NUM
  SET SYSSCAN = 16
  SET &DVOLSER  = &SUBSTR(1:6,&STR(&HVAL2))
  SET &DVOLF1   = &SUBSTR(8:11,&STR(&HVAL2))
  SET &DVOLUNIT = &SUBSTR(13:18,&STR(&HVAL2))
  SET &DVOLF2   = &SUBSTR(20:39,&STR(&HVAL2))
  SET &DVOLTOT  = &SUBSTR(41:46,&STR(&HVAL2))
  SET &DVOLF3   = &SUBSTR(48:80,&STR(&HVAL2))
  SET &DVOLLINE = &SUBSTR(1:80,&STR(&HVAL2))
  IF &SUBSTR(1:16,&STR(&HVAL2)) = &STR(SERIAL  --UNIT--) OR -
     &SUBSTR(1:16,&STR(&HVAL2)) = &STR(       ADDR TYPE) OR -
     &SUBSTR(1:25,&STR(&HVAL2)) = &STR( ) THEN -
    GOTO SKIPHDRS
  ISPEXEC TBADD DVOL
  IF &LASTCC NE 0 THEN -
    DO
      WRITE TBADD FAILED
    END
  SKIPHDRS: +
  SET NUM = &NUM + 1
END

SORT: +
IF &DTYPE = &STR(SHORT) THEN -
  DO
    ISPEXEC TBSORT DVOL FIELDS(DVOLUNIT,C,D,DVOLSER,C,A)
    SET &KEY2 = &STR(SortK1) /* PRIMARY SORT KEY */
    SET &KEY1 = &STR(SortK2) /* SECONDARY SORT KEY */
    SET &KEY3 = &STR( )
  END
ISPEXEC TBTOP  DVOL
DISPLAY: +
SET CSSR = 1
SET RC   = 0
DO WHILE &RC < 8
  ISPEXEC TBDISPL DVOL  PANEL(DVOLTBL&SUBSTR(1:1,&DTYPE)) +
    CURSOR(ZCMD) CSRROW(&CSSR)
  SET RC = &LASTCC
  IF &LENGTH(&STR(&ZCMD)) > 2 THEN -
    IF &SUBSTR(1:2,&ZCMD) = &STR(F ) OR -
       &SUBSTR(1:2,&ZCMD) = &STR(L ) THEN -
      IF &DTYPE = &STR(SHORT) THEN -
        DO
            DO
              ISPEXEC TBVCLEAR DVOL
              SET DVOLSER = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&STR(&ZCMD))+
                            &STR(*)
              ISPEXEC TBTOP  DVOL
              ISPEXEC TBSARG DVOL
              ISPEXEC TBSCAN DVOL
              ISPEXEC TBQUERY DVOL POSITION(CSSR)
            END
        END
  IF &LENGTH(&STR(&ZCMD)) > 5 THEN -
    IF &SUBSTR(1:5,&ZCMD) = &STR(SORT) THEN -
      IF &DTYPE = &STR(SHORT) THEN -
        DO
          SET &SORTKEY = DVOLSER
          SET &SORTSEQ = A
          IF &SUBSTR(6:6,&ZCMD) = &STR(S) THEN -
            DO
              SET &SORTKEY = DVOLSER
              SET &SORTSEQ = A
              SET &KEY2 = &STR(SortK1) /* PRIMARY SORT KEY */
              SET &KEY1 = &STR(SortK2) /* SECONDARY SORT KEY */
              SET &KEY3 = &STR( )
            END
          IF &SUBSTR(6:6,&ZCMD) = &STR(T) THEN -
            DO
              SET &SORTKEY = DVOLTOT
              SET &SORTSEQ = D
              SET &KEY2 = &STR(SortK1) /* PRIMARY SORT KEY */
              SET &KEY1 = &STR( )
              SET &KEY3 = &STR(SortK2) /* SECONDARY SORT KEY */
            END
          ISPEXEC TBSORT DVOL +
            FIELDS(DVOLUNIT,C,D,&SORTKEY,C,&SORTSEQ)
          ISPEXEC TBTOP  DVOL
        END
  IF &LENGTH(&STR(&ZCMD)) = 4 THEN -
    IF &SUBSTR(1:4,&ZCMD) = &STR(SORT) THEN -
      IF &DTYPE = &STR(SHORT) THEN -
        DO
          SET &KEY2 = &STR(SortK1) /* PRIMARY SORT KEY */
          SET &KEY1 = &STR(SortK2) /* SECONDARY SORT KEY */
          SET &KEY3 = &STR( )
          ISPEXEC TBSORT DVOL +
            FIELDS(DVOLUNIT,C,D,DVOLSER,C,A)
          ISPEXEC TBTOP  DVOL
        END
END
IF &RC = 8 THEN -
  DO
    ISPEXEC TBCLOSE DVOL
    GOTO PANEL
  END
GOTO DISPLAY
/* J. KALINICH, X4521 */
/* CLIST TO DISPLAY DISK FREE SPACE USING DVOL (CBT FILE 296) */
/* REQUIRES AUG. 1997 VERSION OF DVOL COMMAND FROM BRUCE LELAND */
./ ADD NAME=EB       0124-93174-99090-0722-00173-00168-00000-CLIST
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC VGET (EB1  EB2  EB3  EB4  EB5  EB6  EB7  EB8  EB9  EB10 +
              EB11 EB12 EB13 EB14 EB15 EB16 EB17 EB18 EB19 +
              EM1  EM2  EM3  EM4  EM5  EM6  EM7  EM8  EM9  EM10 +
              EM11 EM12 EM13 EM14 EM15 EM16 EM17 EM18 EM19) PROFILE
ISPEXEC VGET ZTRAIL
IF &LENGTH(&STR(&ZTRAIL)) > 0 THEN -
  DO
    ISPEXEC  CONTROL NONDISPL ENTER
    SET PDS = &STR(&ZTRAIL)
  END
PANEL: +
ISPEXEC DISPLAY PANEL(EB01A) MSG(&MSGID)
IF &LASTCC > 0 THEN EXIT
SET MSGID = &STR()
IF &PDS = 0 THEN -
  DO
    SET INDX = 1
    DO WHILE &INDX < 20
      SET HOLD = &&EB&INDX
      SET EO&INDX = &HOLD
      SET INDX = &INDX + 1
    END
    ISPEXEC DISPLAY PANEL(EB01B)
    SET HOLDCC = &LASTCC
    IF &HOLDCC = 0 THEN -
      DO
        SET INDX = 1
        DO WHILE &INDX < 20
          SET OLD = &&EO&INDX
          SET NEW = &&EB&INDX
          IF &LENGTH(&NEW) > 0 AND &OLD ¬= &NEW THEN -
            DO
              ALLOC DA(&NEW) SHR
              IF &LASTCC = 0 THEN -
                DO
                  FREE DA(&NEW)
                  SET EO&INDX = &NEW
                END
              ELSE -
                DO
                  ISPEXEC DISPLAY PANEL(EB01B) MSG(EB005) +
                     CURSOR(EB&INDX)
                  SET HOLDCC = &LASTCC
                  IF &HOLDCC > 0 THEN -
                    DO
                      SET INDXB = 1
                      DO WHILE &INDXB < 20
                        SET HOLD = &&EO&INDXB
                        SET EB&INDXB = &HOLD
                        SET INDXB = &INDXB + 1
                      END
                      SET INDX = 20
                    END
                  ELSE -
                    SET INDX = 0
                END
            END
          SET INDX = &INDX + 1
        END
        IF &HOLDCC = 0 THEN -
          DO
            ISPEXEC VPUT (EB1 EB2 EB3 EB4 EB5 EB6 EB7 EB8 EB9 EB10 +
                EB11 EB12 EB13 EB14 EB15 EB16 EB17 EB18 EB19 +
                EM1  EM2  EM3  EM4  EM5  EM6  EM7  EM8  EM9  EM10 +
                EM11 EM12 EM13 EM14 EM15 EM16 EM17 EM18 EM19) PROFILE
            SET MSGID = &STR(EB004)
          END
      END
  END
ELSE -
  IF &PDS = 20 OR &LENGTH(&DSNAME) = 0 THEN -
    ISPEXEC SELECT PGM(ISREDIT)
  ELSE -
    DO
      IF &STR(&MODE) = E OR -
         &STR(&MODE) = B OR -
         &STR(&MODE) = V THEN -
        DO
          IF &STR(&MODE) = B THEN -
            SET MODE = BROWSE
          IF &STR(&MODE) = E THEN -
            SET MODE = EDIT
          IF &STR(&MODE) = V THEN -
            SET MODE = VIEW
          ISPEXEC CONTROL ERRORS RETURN
          IF &STR(&MEMBER) = &STR() THEN -
            ISPEXEC &MODE DATASET(&DSNAME)
          ELSE -
            DO
              IF &SUBSTR(1:1,&DSNAME) = &STR(') THEN -
                DO
                  SET DSNAME = &SUBSTR(2:&LENGTH(&DSNAME)-1,&DSNAME)
                  ISPEXEC &MODE DATASET('&DSNAME(&STR(&MEMBER))')
                END
              ELSE -
                ISPEXEC &MODE DATASET(&DSNAME(&STR(&MEMBER)))
            END
          SET HOLDCC = &LASTCC
          ISPEXEC CONTROL ERRORS CANCEL
          IF &HOLDCC = 14 AND &MODE = &STR(BROWSE) THEN -
            SET MSGID = &STR(EB001)
          IF &HOLDCC = 14 AND &MODE = &STR(VIEW) THEN -
            SET MSGID = &STR(EB001)
          IF &HOLDCC = 14 AND &MODE = &STR(EDIT) THEN -
            SET MSGID = &STR(EB006)
          IF &HOLDCC = 16 THEN -
            SET MSGID = &STR(EB007)
          IF &HOLDCC = 20 THEN -
            SET MSGID = &STR(EB008)
        END
      ELSE -
        DO
          IF &STR(&MODE) = &STR(LE) THEN -
            DO
              IF &SYSINDEX(&STR(*),&STR(&MEMBER),1) > 0 OR +
                &STR(&MEMBER) = &STR() THEN -
                SET &CMD = &STR(LIST)
              ELSE -
                SET &CMD = &STR(EDIT)
              IF &STR(&MEMBER) = &STR() THEN-
                SET &MEMBER = &STR(*)
              ISPEXEC SELECT PGM(ELIPS) +
                NEWAPPL(LIB@) +
                NEWPOOL +
                PARM( +
                     CMD('&CMD &MEMBER') +
                     DSN(&DSNAME) +
                     )
            END
          IF &STR(&MODE) = &STR(LB) THEN -
            DO
              IF &SYSINDEX(&STR(*),&STR(&MEMBER),1) > 0 OR +
                &STR(&MEMBER) = &STR() THEN -
                SET &CMD = &STR(LIST)
              ELSE -
                SET &CMD = &STR(BROWSE)
              IF &STR(&MEMBER) = &STR() THEN-
                SET &MEMBER = &STR(*)
              ISPEXEC SELECT PGM(ELIPS) +
                NEWAPPL(LIB@) +
                NEWPOOL +
                PARM( +
                     CMD('&CMD &MEMBER') +
                     DSN(&DSNAME) +
                     )
            END
          IF &STR(&MODE) = &STR(PDS) THEN -
            DO
              IF &STR(&MEMBER) = &STR() THEN -
                ISPEXEC SELECT PGM(PDS85) PARM(PDS85 &DSNAME +
                                               MEMLIST :)
              ELSE -
                ISPEXEC SELECT PGM(PDS85) PARM(PDS85 &DSNAME +
                                               MEMLIST &MEMBER)
            END
        END
    END
GOTO PANEL
EXIT CODE(0)
/* J. KALINICH/M. RESCHKE, X4521                                     */
/* CLIST TO EDIT/BROWSE A MENU OF LIBRARIES                          */
/*                                                                   */
/* CLIST FUNCTIONS:                                                  */
/*                                                                   */
/* 1.  ISPF EDIT/BROWSE/VIEW FOR PDS AND SEQUENTIAL DATASETS         */
/* 2.  LIBRARIAN ELIPS EDIT/BROWSE FOR LIBRARIAN DATASETS            */
/* 3.  PDS 8.5 FUNCTIONS FOR PDS AND SEQUENTIAL DATASETS             */
/*                                                                   */
./ ADD NAME=EDET     0106-92091-95019-1234-00011-00009-00000-CLIST
PROC 0 DEBUG DATASET() PANEL(ISREDDE2) MACRO()
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC EDIT DATASET('&DATASET') PANEL(&PANEL) MACRO(&MACRO)
ISPEXEC CONTROL ERRORS CANCEL
END
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE EDIT WITH OPTIONAL CUSTOM EDIT PANEL AND MACRO */
./ ADD NAME=FX       0104-94279-97216-1255-00115-00111-00000-CLIST
ISREDIT MACRO (FXSTRING COL1 COL2 OTHER)
/*--------------------------------------------------------------------*/
/*     FX EDIT MACRO - FIND ALL 'STRING' (AFTER EXCLUDE ALL LINES)    */
/*--------------------------------------------------------------------*/
/*    5 OCT 94 - SUPPORT ' AND " AS STRING DELIMITERS                 */
/*               SUPPORT OTHER FIND PARAMETERS:                       */
/*                 1.  LABEL-RANGE                                    */
/*                 2.  PREFIX, SUFFIX, AND WORD                       */
/*               ISSUE MESSAGE ON SYNTAX/SEVERE ERROR                 */
/*   20 MAY 93 - ZERO SUPPRESS &FINDS, &LEFT, AND &RIGHT VARIABLES    */
/*   21 APR 92 - SUPPORT (P)ICTURE, HE(X), (T)EXT, AND (C)HARACTER    */
/*               STRING ARGUMENTS                                     */
/*    9 MAR 92 - SUPPORT CLIST &VARIABLES AS STRING ARGUMENTS         */
/*    1 OCT 91 - SUPPORT FIRST/LAST COLUMNS AS PARAMETERS             */
/*               SUPPORT SEQUENCE NUMBERS AS SEARCHABLE DATA          */
/*--------------------------------------------------------------------*/
CONTROL NOFLUSH NOMSG NOLIST NOCONLIST NOSYMLIST
IF &NRSTR(&FXSTRING) = &STR() THEN -
  DO
    SET &ZEDSMSG = &STR(ENTER FIND STRING)
    SET &ZEDLMSG = &STR(FX REQUIRES A STRING AS AN ARGUMENT)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
  END
IF &LENGTH(&NRSTR(&FXSTRING)) = 1 THEN -
  DO
    /* WRAP IN QUOTES SO &SUBSTR(1:2,... DOES NOT BOMB */
    SET &FXSTRING = &NRSTR('&FXSTRING')
  END
IF &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(C') OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(P') OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(T') OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(X') OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(C") OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(P") OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(T") OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR(X") OR -
   &SUBSTR(1:1,&NRSTR(&FXSTRING)) = &STR(')  OR -
   &SUBSTR(1:1,&NRSTR(&FXSTRING)) = &STR(")  THEN -
  SET &QUOTE = &STR()
ELSE -
  SET &QUOTE = &STR(')
ISREDIT (STAT,VALUE) = NUMBER  /* SAVE NUMBER SETTING */
ISREDIT NUMBER OFF             /* SEARCH SEQUENCE NUMBERS ON 'FIND' */
SET SCAN = ON
IF &SUBSTR(1:1,&NRSTR(&FXSTRING)) = &STR(&&) OR -
   &SUBSTR(1:2,&NRSTR(&FXSTRING)) = &STR('&&) THEN -
  DO
    SET &SCAN = OFF
    SET &FXSTRING = &NRSTR(&SUBSTR(1:&LENGTH(&NRSTR(&FXSTRING)),+
                    &NRSTR(&FXSTRING)))
  END
ISREDIT EXCLUDE ALL
ISPEXEC CONTROL ERRORS RETURN
ISREDIT SCAN &SCAN
/*--------------------------------------------------------------------*/
ISREDIT FIND ALL &FXSTRING &COL1 &COL2 &OTHER
/*--------------------------------------------------------------------*/
SET &FINDCC = &LASTCC
IF &FINDCC > 4 THEN -
  DO
    SET &ZEDSMSG = &STR(PARAMETER NOT RECOGNIZED)
    SET &ZEDLMSG = &STR(CHECK FOR MISSPELLED KEYWORDS OR +
                        TOO MANY BOUNDS OR RANGE PARAMETERS.)
    ISPEXEC SETMSG MSG(ISRZ000)
    ISREDIT RESET EXCLUDED
    ISREDIT NUMBER = (STAT,VALUE)  /* RESTORE NUMBER SETTING */
    EXIT CODE(1)                   /* RETURN CURSOR TO COMMAND LINE */
  END
ISREDIT (FINDS) = FIND_COUNTS
ISREDIT (LEFT,RIGHT) = BOUNDS
IF &COL1 = &STR() AND -
   &COL2 = &STR() THEN GOTO NOCOLS
IF &COL1 ¬= &STR() AND -
   &DATATYPE(&COL1) = &STR(NUM) THEN -
  SET &LEFT = &COL1
IF &COL2 ¬= &STR() AND -
   &DATATYPE(&COL2) = &STR(NUM) THEN -
  SET &RIGHT = &COL2
ELSE -
  DO
    IF &DATATYPE(&COL1) = &STR(NUM) AND -
       &DATATYPE(&COL2) ¬= &STR(NUM) THEN -
      DO
        SET &RIGHT = &LEFT + &LENGTH(&NRSTR(&FXSTRING)) - 1
        IF &SUBSTR(1:1,&NRSTR(&FXSTRING)) = &STR(')  OR -
           &SUBSTR(1:1,&NRSTR(&FXSTRING)) = &STR(")  THEN -
          SET &RIGHT = &RIGHT - 2
      END
  END
NOCOLS: +
SET &FINDS = &FINDS
SET &LEFT  = &LEFT
SET &RIGHT = &RIGHT
IF &FINDCC > 0 THEN -
  DO
    SET &ZEDSMSG = &STR(NO CHARS &QUOTE&FXSTRING&QUOTE FOUND)
    SET &ZEDLMSG = &STR(CHARS &QUOTE&FXSTRING&QUOTE - NOT FOUND +
                        ON ANY LINES +
                        (COLS &LEFT TO &RIGHT).)
    ISPEXEC SETMSG MSG(ISRZ001)
    ISREDIT RESET EXCLUDED
  END
ELSE -
  DO
    SET &ZEDSMSG = &STR(&FINDS CHARS &QUOTE&FXSTRING&QUOTE)
    SET &ZEDLMSG = &STR(CHARS &QUOTE&FXSTRING&QUOTE FOUND +
                        &FINDS TIMES +
                        WITHIN COLUMNS &LEFT TO &RIGHT.)
    ISPEXEC SETMSG MSG(ISRZ000)
  END
ISREDIT NUMBER = (STAT,VALUE)  /* RESTORE NUMBER SETTING */
EXIT CODE(1)                   /* RETURN CURSOR TO COMMAND LINE */
/* J. KALINICH, X4521 */
/* EDIT MACRO TO DO FIND ALL AFTER EXCLUDE ALL */
./ ADD NAME=FXC      0136-90332-93313-0804-00088-00065-00000-CLIST
ISREDIT MACRO
/*                                                                 */
/* FXC EDIT MACRO - FIND ALL IN EXCLUDED LINES (INCL SEQ NUMBERS)  */
/*                  USING THE 'STRING' THE CURSOR IS SITTING ON    */
/*                                                                 */
/*   20 MAY 93 - ZERO SUPPRESS &FINDS, &LEFT, AND &RIGHT VARIABLES */
/*                                                                 */
CONTROL NOFLUSH NOMSG NOLIST NOCONLIST NOSYMLIST
ISREDIT (LINE) = LINE .ZCSR
ISREDIT (END)  = LRECL
ISREDIT (CL,CC) = CURSOR
IF &CC = 0 OR -
   &CC > &END THEN -
  DO
    SET &ZEDSMSG = &STR(PUT CURSOR ON A STRING)
    SET &ZEDLMSG = &STR(FXC NEEDS THE CURSOR IN THE DATA AREA)
    ISPEXEC SETMSG MSG(ISRZ001W)
    IF &CC > &END THEN -
      SET &CC = 0
    ISREDIT CURSOR = (CL,CC)
    EXIT CODE(0)
  END
/* STRING DELIMITERS BELOW - MODIFY AS APPROPRIATE  */
SET &DELIM = &NRSTR(~`!@#$%¬()-_=+|¢{Ý¨};:"'<,>?/ )
/* EBCDIC HEX ----> A757756545667444CABD5777466664  */
/*  VALUES          19ACBBCFDD0DEEFA0DD0EAFDCBEF10  */
/*                                                  */
/* IN ORDER TO RECONSTRUCT AFTER AN ASCII -> EBCDIC */
/* CODE TRANSLATION DURING FILE TRANSFER.           */

SET &START = 1
DO &I = &CC TO &START BY -1
  IF &SYSINDEX(&SUBSTR(&I,&NRSTR(&LINE)),&NRSTR(&DELIM),1) > 0 THEN -
    SET &START = &I + 1
END

DO &I = &CC TO &END
  IF &SYSINDEX(&SUBSTR(&I,&NRSTR(&LINE)),&NRSTR(&DELIM),1) > 0 THEN -
    SET &END = &I - 1
END

IF &START > &END THEN -
  DO
    SET &ZEDSMSG = &STR(PUT CURSOR ON A STRING)
    SET &ZEDLMSG = &STR(FXC NEEDS THE CURSOR ON A NON-DELIMITER)
    ISPEXEC SETMSG MSG(ISRZ001W)
    ISREDIT CURSOR = (CL,CC)
    EXIT CODE(0)
  END

SET &FXSTRING = &NRSTR(&SUBSTR(&START:&END,&NRSTR(&LINE)))

ISREDIT (STAT,VALUE) = NUMBER  /* SAVE NUMBER SETTING */
ISREDIT NUMBER OFF             /* SEARCH SEQUENCE NUMBERS ON 'FIND' */
SET &SCAN = ON
IF &STR(&SUBSTR(1:1,&STR(&FXSTRING))) = &STR(&&) THEN -
  SET &SCAN = OFF
ISREDIT EXCLUDE ALL
ISPEXEC CONTROL ERRORS RETURN
ISREDIT SCAN &SCAN
ISREDIT FIND ALL '&FXSTRING'
SET &FINDCC = &LASTCC
ISREDIT (FINDS) = FIND_COUNTS
ISREDIT (LEFT,RIGHT) = BOUNDS
SET &FINDS = &FINDS
SET &LEFT  = &LEFT
SET &RIGHT = &RIGHT
IF &FINDCC > 0 -
  THEN -
    DO
      SET &ZEDSMSG = &STR(NO CHARS '&FXSTRING' FOUND)
      SET &ZEDLMSG = &STR(CHARS '&FXSTRING' - NOT FOUND ON ANY LINES +
                          (COLS &LEFT TO &RIGHT).)
      ISPEXEC SETMSG MSG(ISRZ001W)
      ISREDIT RESET EXCLUDED
    END
  ELSE -
    DO
      SET &ZEDSMSG = &STR(&FINDS CHARS '&FXSTRING')
      SET &ZEDLMSG = &STR(CHARS '&FXSTRING' FOUND &FINDS TIMES +
                          WITHIN COLUMNS &LEFT TO &RIGHT.)
      ISPEXEC SETMSG MSG(ISRZ000W)
    END
ISREDIT NUMBER = (STAT,VALUE)  /* RESTORE NUMBER SETTING */
EXIT CODE(1)                   /* RETURN CURSOR TO COMMAND LINE */
END
/* J. KALINICH, X4521 */
/* EDIT MACRO TO DO FIND ALL FROM EXCLUDED LINES ON WORD AT CURSOR */
./ ADD NAME=GO       0125-90310-97072-1004-00005-00008-00000-CLIST
ISREDIT MACRO
ISREDIT SUBMIT
ISPEXEC SELECT PGM(IOFSPF) NEWAPPL(IOF) PASSLIB
/* J. KALINICH, X4521 */
/* EDIT MACRO TO SUBMIT JOB AND INVOKE IOF */
./ ADD NAME=ISFP     0110-89241-99007-1339-00003-00001-00000-CLIST
PDF
/* J. KALINICH, X4521 */
/* FOR THOSE OF USE WHO CAN'T SPELL */
./ ADD NAME=JC       0131-89275-94012-1359-00139-00086-00000-CLIST
ISREDIT MACRO (JCCLASS)
/**********************************************************************/
/*    JOB CARD MACRO:                                                 */
/*                                                                    */
/*    IF INVOKED WITH A JOB CLASS PARAMETER (E.G., "JC B"), THEN      */
/*    THE PANEL WILL NOT BE DISPLAYED AND THE JOB CARD WILL BE BUILT  */
/*    WITH THE PROFILE VARIABLES THAT HAVE BEEN SAVED.                */
/*                                                                    */
/**********************************************************************/
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
IF &STR(&JCCLASS) ¬= &Z THEN -
  DO
    ISPEXEC CONTROL NONDISPL ENTER
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
ELSE -
  DO
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
IF &STR(&JCPROC) ¬= &Z THEN -
  DO
    IF &SUBSTR(1,&STR(&JCPROC)) = ' THEN -
      SET JCPROC = &SUBSTR(2:&LENGTH(&STR(&JCPROC))-1,&STR(&JCPROC))
    ELSE -
      SET JCPROC = &STR(&SYSUID..&STR(&JCPROC))
  END
IF &STR(&JCOTHER1) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER1)),&STR(&JCOTHER1)) ¬= &STR(,) -
      THEN -
        SET JCOTHER1 = &STR(&JCOTHER1)&STR(,)
  END
IF &STR(&JCOTHER2) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER2)),&STR(&JCOTHER2)) ¬= &STR(,) -
      THEN -
        SET JCOTHER2 = &STR(&JCOTHER2)&STR(,)
  END
IF &STR(&JCOTHER3) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER3)),&STR(&JCOTHER3)) ¬= &STR(,) -
      THEN -
        SET JCOTHER3 = &STR(&JCOTHER3)&STR(,)
  END
SET JOB1  = &STR(//&JCPRE.&JCSU JOB (&JCACC),'&JCNAME',)
SET JOBO1 = &STR(//             &JCOTHER1)
SET JOBO2 = &STR(//             &JCOTHER2)
SET JOBO3 = &STR(//             &JCOTHER3)
SET JOB2  = &STR(//             CLASS=&JCCLASS,)
SET JOB3  = &STR(//             TIME=(&JCTIME),)
SET JOB4  = &STR(//             NOTIFY=&JCNOTIFY,)
SET JOB5  = &STR(//             MSGCLASS=&JCMC)
SET JOB6  = &STR(&STR(/*)JOBPARM R=&JCRM,T=9999,L=9999)
SET JOB7  = &STR(&STR(/*)ROUTE XEQ   &JCJXEQ)
SET JOB8  = &STR(&STR(/*)ROUTE PRINT &JCJPRT)
SET JOB9  = &STR(//JCL      JCLLIB  ORDER=&JCPROC)
SET JOB10 = &STR(//JOB      OUTPUT  NAME='&JCNAME',)
SET JOB11 = &STR(//             TITLE='&JCTITLE',)
SET JOB12 = &STR(//             DEPT='&JCDEPT',)
SET JOB13 = &STR(//             BUILDING='&JCBLDG',)
SET JOB14 = &STR(//             ADDRESS=('&JCADDR1',
SET JOB15 = &STR(//             '&JCADDR2',)
SET JOB16 = &STR(//             '&JCADDR3',)
SET JOB17 = &STR(//             '&JCADDR4'))&STR(,)
SET JOB18 = &STR(//             JESDS=ALL,)
SET JOB19 = &STR(//             DEFAULT=YES)
ISREDIT (TOP,BOT) = DISPLAY_LINES
IF &TOP = 1 THEN                           /* IS THERE A LINE 1? */ -
  DO                                       /* YES */
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB19"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB18"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB17"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB16"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB15"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB14"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB13"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB12"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB11"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB10"
    IF &STR(&JCPROC) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOB9"
    IF &STR(&JCJPRT) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOB8"
    IF &STR(&JCJXEQ) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOB7"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB6"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB5"
    IF &STR(&JCNOTIFY) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOB4"
    IF &STR(&JCTIME) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOB3"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB2"
    IF &STR(&JCOTHER3) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOBO3"
    IF &STR(&JCOTHER2) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOBO2"
    IF &STR(&JCOTHER1) ¬= &Z THEN -
      ISREDIT LINE_BEFORE .ZFIRST = "&JOBO1"
    ISREDIT LINE_BEFORE .ZFIRST = "&JOB1"
  END
ELSE                                       /* NO, THEN NEW MEMBER */ -
  DO
    ISREDIT LINE_AFTER .ZFIRST = "&JOB1"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB19"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB18"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB17"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB16"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB15"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB14"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB13"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB12"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB11"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB10"
    IF &STR(&JCPROC) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOB9"
    IF &STR(&JCJPRT) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOB8"
    IF &STR(&JCJXEQ) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOB7"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB6"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB5"
    IF &STR(&JCNOTIFY) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOB4"
    IF &STR(&JCTIME) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOB3"
    ISREDIT LINE_AFTER .ZFIRST = "&JOB2"
    IF &STR(&JCOTHER3) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOBO3"
    IF &STR(&JCOTHER2) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOBO2"
    IF &STR(&JCOTHER1) ¬= &Z THEN -
      ISREDIT LINE_AFTER .ZFIRST = "&JOBO1"
  END
/* J. KALINICH, X4521 */
/* EDIT MACRO TO BUILD JOBCARD SEQUENCE */
./ ADD NAME=JCI      0122-89275-94012-1359-00110-00072-00000-CLIST
ISREDIT MACRO (JCCLASS)
/**********************************************************************/
/*    JOB CARD INITIAL MACRO:                                         */
/*                                                                    */
/*    USE AS AN INITIAL MACRO TO BUILD A JOB CARD AFTER FILE          */
/*    TAILORING HAS CREATED ALL THE OTHER JCL.                        */
/*                                                                    */
/*    THE PANEL WILL BE DISPLAYED AND THE JOB CARD WILL BE BUILT      */
/*    WITH THE PROFILE VARIABLES THAT HAVE BEEN SAVED.                */
/*                                                                    */
/**********************************************************************/
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
IF &STR(&JCCLASS) ¬= &Z THEN -
  DO
    ISPEXEC CONTROL NONDISPL ENTER
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
ELSE -
  DO
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
IF &STR(&JCPROC) ¬= &Z -
  THEN -
    DO
      IF &SUBSTR(1,&STR(&JCPROC)) = ' THEN -
        SET JCPROC = &SUBSTR(2:&LENGTH(&STR(&JCPROC))-1,&STR(&JCPROC))
      ELSE -
        SET JCPROC = &STR(&SYSUID..&STR(&JCPROC))
    END
IF &STR(&JCOTHER1) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER1)),&STR(&JCOTHER1)) ¬= &STR(,) -
      THEN -
        SET JCOTHER1 = &STR(&JCOTHER1)&STR(,)
  END
IF &STR(&JCOTHER2) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER2)),&STR(&JCOTHER2)) ¬= &STR(,) -
      THEN -
        SET JCOTHER2 = &STR(&JCOTHER2)&STR(,)
  END
IF &STR(&JCOTHER3) ¬= &Z  THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER3)),&STR(&JCOTHER3)) ¬= &STR(,) -
      THEN -
        SET JCOTHER3 = &STR(&JCOTHER3)&STR(,)
  END
SET JOB1  = &STR(//&JCPRE.&JCSU JOB (&JCACC),'&JCNAME',)
SET JOBO1 = &STR(//             &JCOTHER1)
SET JOBO2 = &STR(//             &JCOTHER2)
SET JOBO3 = &STR(//             &JCOTHER3)
SET JOB2  = &STR(//             CLASS=&JCCLASS,)
SET JOB3  = &STR(//             TIME=(&JCTIME),)
SET JOB4  = &STR(//             NOTIFY=&JCNOTIFY,)
SET JOB5  = &STR(//             MSGCLASS=&JCMC)
SET JOB6  = &STR(&STR(/*)JOBPARM R=&JCRM,T=9999,L=9999)
SET JOB7  = &STR(&STR(/*)ROUTE XEQ   &JCJXEQ)
SET JOB8  = &STR(&STR(/*)ROUTE PRINT &JCJPRT)
SET JOB9  = &STR(//JCL      JCLLIB  ORDER=&JCPROC)
SET JOB10 = &STR(//JOB      OUTPUT  NAME='&JCNAME',)
SET JOB11 = &STR(//             TITLE='&JCTITLE',)
SET JOB12 = &STR(//             DEPT='&JCDEPT',)
SET JOB13 = &STR(//             BUILDING='&JCBLDG',)
SET JOB14 = &STR(//             ADDRESS=('&JCADDR1',
SET JOB15 = &STR(//             '&JCADDR2',)
SET JOB16 = &STR(//             '&JCADDR3',)
SET JOB17 = &STR(//             '&JCADDR4'))&STR(,)
SET JOB18 = &STR(//             JESDS=ALL,)
SET JOB19 = &STR(//             DEFAULT=YES)
ISREDIT LINE_BEFORE .ZFIRST = "&JOB19"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB18"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB17"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB16"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB15"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB14"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB13"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB12"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB11"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB10"
/* CHECK FOR EXISTING JCLLIB IN SKELETON JCL */
ISREDIT FIND FIRST " JCLLIB "
IF &LASTCC ¬= 0 && &STR(&JCPROC) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB9"
ISREDIT CURSOR = 1 1
IF &STR(&JCJPRT) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB8"
IF &STR(&JCJXEQ) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB7"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB6"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB5"
IF &STR(&JCNOTIFY) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB4"
IF &STR(&JCTIME) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB3"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB2"
IF &STR(&JCOTHER3) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO3"
IF &STR(&JCOTHER2) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO2"
IF &STR(&JCOTHER1) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO1"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB1"
/* RETURN CURSOR TO COMMAND LINE */
EXIT CODE(1)
/* J. KALINICH, X4521 */
/* INITIAL EDIT MACRO TO BUILD JOBCARD SEQUENCE FOR JCL SKELETONS */
./ ADD NAME=LISTDSI  0119-89306-98188-1424-00051-00031-00000-CLIST
ISREDIT MACRO (DSNAME)
CONTROL NOMSG MAIN NOLIST NOCONLIST NOSYMLIST ASIS
IF &DSNAME ¬= &STR() THEN -
  DO
    IF &SUBSTR(1,&DSNAME) = ' THEN -
      SET DSNS = &SUBSTR(2:&LENGTH(&DSNAME)-1,&DSNAME)
    ELSE -
      SET DSNS = &STR(&SYSUID..&DSNAME)
  END
ELSE -
  DO
    ISREDIT (DSNS) = DATASET
  END
LISTDSI '&DSNS' DIRECTORY NORECALL
SET &LISTCC = &LASTCC
IF &LISTCC > 0 THEN -
  DO
      SET &SYSREASON= &SYSREASON
      SET &ZEDSMSG = &STR(RC = &LISTCC-&SYSREASON)
      SET &ZEDLMSG = &STR(&SYSMSGLVL2)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(&LISTCC)
  END
SET &DSNS    = &STR(&SYSDSNAME)
SET &ZALVOL  = &STR(&SYSVOLUME)
SET &DEVT    = &STR(&SYSUNIT)
SET &DSORG   = &STR(&SYSDSORG)
SET &ZALRF   = &STR(&SYSRECFM)
SET &ZALLREC = &STR(&SYSLRECL)
SET &ZALBLK  = &STR(&SYSBLKSIZE)
SET &TOTA    = &STR(&SYSALLOC)
SET &TOTU    = &STR(&SYSUSED)
SET &ZAL1EX  = &STR(&SYSPRIMARY)
SET &ZAL2EX  = &STR(&SYSSECONDS)
SET &ZALSPC  = &STR(&SYSUNITS)
SET &EXTA    = &STR(&SYSEXTENTS)
SET &CRDT    = &STR(&SYSCREATE)
IF &SYSEXDATE = 0 THEN -
  SET &EXDATE  = &STR(***None***)
ELSE -
  SET &EXDATE  = &STR(&SYSEXDATE)
SET &ZALDIR  = &STR(&SYSADIRBLK)
SET &DIRU    = &STR(&SYSUDIRBLK)
SET &NRMEM   = &STR(&SYSMEMBERS)
SET &EXTU    = &STR(N/A)
ISPEXEC ADDPOP
ISPEXEC DISPLAY PANEL(ISRUAIPO)
ISPEXEC REMPOP
EXIT
/* J. KALINICH, X4521 */
/* EDIT MACRO TO LISTDSI AND DISPLAY INFO WITH =3.2 PANEL */
./ ADD NAME=LOGLIST  0102-96026-96029-1012-00029-00027-00000-CLIST
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
IF &SYSISPF = &STR(NOT ACTIVE) THEN -
  DO
    ISPSTART CMD(%LOGLIST) NEWAPPL(ISR)
    EXIT
  END
DO WHILE &DISPCC < 8
  ISPEXEC DISPLAY PANEL(LOGLIST)
  SET &DISPCC = &LASTCC
  IF &SYSINDEX(&STR(can),&ZCMD,1) > 0 OR -
     &SYSINDEX(&STR(CAN),&ZCMD,1) > 0 THEN -
    EXIT CODE(12)
END
ISPEXEC FTOPEN
ISPEXEC FTINCL LOGLIST
ISPEXEC FTCLOSE NAME(@LOGLIST)
IF &LASTCC = 0 THEN -
  DO
    SET ZEDSMSG = &STR(@LOGLIST UPDATED)
    SET ZEDLMSG = &STR(REXX EXEC "@LOGLIST" UPDATED IN ISPFILE)
    ISPEXEC SETMSG MSG(ISRZ001)
  END
EXIT CODE(0)
/* J.KALINICH, X4521 */
/* CLIST TO UPDATE "@LOGLIST" REXX EXEC IN THE ISPFILE DATASET    */
./ ADD NAME=MEMLIST  0107-91301-95278-0659-00087-00103-00000-CLIST
ISREDIT MACRO (FROM,TO)
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISREDIT (WORKDSN) = DATASET
ISREDIT (XLINE) = DISPLAY_LINES
IF &STR(&XLINE) = &STR() THEN -
  DO
    SET ZEDSMSG = &STR(EMPTY DATASET)
    SET ZEDLMSG = &STR(ML COMMAND NOT VALID FOR EMPTY DATASET)
    ISPEXEC SETMSG MSG(ISRZ001W)
    EXIT CODE(4)
  END
SET &MAX = 1
SET &Z9 = Z9999999
IF &FROM = ? THEN DO
  ISPEXEC DISPLAY PANEL(#MEMLIST)
  EXIT
END
IF &TO = THEN SET &TO = &Z9
IF &FROM = THEN -
  SET &RT = &STR()
ELSE -
  SET &RT = &STR(FROM &FROM TO &TO)
SET &TARGET1 = &FROM
SET T1 = &LENGTH(&TARGET1)
IF &FROM =  THEN SET &T1 = 8
SET &TARGET2 = &TO
SET T2 = &LENGTH(&TARGET2)
SET PDSDSN = &WORKDSN
/* INITIALIZE DATASET */
ISPEXEC  LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                ENQ(SHRW)                        +
                ORG(PDSORG)
SET LMRC = &LASTCC
IF &LMRC ¬= 0 THEN +
   DO
     SET ZEDSMSG = &STR(LMINIT FAILED - RETURN CODE &LMRC)
     SET ZEDLMSG = &STR(DATA SET IS NOT ALLOCATED OR NOT PARTITIONED)
     ISPEXEC SETMSG MSG(ISRZ001W)
     EXIT CODE(8)
   END
/* OPEN DATA SET */
ISPEXEC LMOPEN DATAID(&PDSID) OPTION(INPUT)
SET LMRC = &LASTCC
DO WHILE (&LMRC = 0)
  /* BUILD MEMBER LIST AND RETURN NEXT MEMBER */
  ISPEXEC LMMLIST DATAID(&PDSID) -
    OPTION(LIST) MEMBER(MEMBER) -
    STATS(NO)
  SET LMRC = &LASTCC
  IF &LMRC = 0 THEN /* NOT END OF MEMBER LIST */ +
    DO
      IF &SUBSTR(1:&T1,&MEMBER) GE &TARGET1 AND +
         &SUBSTR(1:&T2,&MEMBER) LE &TARGET2  THEN -
        DO
          SET &X = &STR(&&L&MAX)
          IF &LENGTH(&STR(&X)) > 60 THEN -
            DO
              SET &MAX = &MAX + 1
              SET &L&MAX = &STR()
              SET &X = &STR(&&L&MAX)
            END
          SET &X = &STR(&X)&STR(&MEMBER  )
          SET &L&MAX = &STR(&X)
        END
    END
END
ISREDIT LINE_AFTER &XLINE = NOTELINE ' '
/* SEND OUT IN REVERSE ORDER */
SET I = &MAX
DO WHILE (&I > 0)
  SET X = &STR(&&L&I)
  ISREDIT LINE_AFTER &XLINE = NOTELINE '&X'
  SET I = &I - 1
END
ISREDIT LINE_AFTER &XLINE = +
    MSGLINE 'MEMLIST:  &RT'
ISREDIT LINE_AFTER &XLINE = NOTELINE ' '
/* FREE MEMBER LIST */
ISPEXEC LMMLIST DATAID(&PDSID) OPTION(FREE)
/* CLOSE DATA SET */
ISPEXEC LMCLOSE DATAID(&PDSID)
/* FREE DATA ID */
ISPEXEC LMFREE  DATAID(&PDSID)
EXIT CODE(0)
/* J. KALINICH, X4521 */
/* EDIT MACRO TO DISPLAY A PDS MEMBER LIST                          */
/* LSSC MODIFICATION TO CBT FILE 095 - USE =NOTE= INSTEAD OF WRITE  */
./ ADD NAME=OPER     0128-91087-94062-1314-00044-00038-00000-CLIST
ISREDIT MACRO (PARM) NOPROCESS
/* SDSF OPERATOR COMMANDS ARE LIMITED TO 42 BYTES */
/* (ISFPANEL INPUT SIZE)                          */
CONTROL NOFLUSH NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
IF &PARM = HELP OR   +
   &PARM = ?    THEN +
  DO
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(#OPER)
    EXIT
  END
/* PROCESS LINE COMMANDS, CHECK IF "C" WAS SPECIFIED */
ISPEXEC CONTROL ERRORS RETURN
ISREDIT PROCESS RANGE C
IF &LASTCC > 0 THEN +
  DO
    SET ZEDSMSG = &STR(ENTER "C" LINE CMD)
    SET ZEDLMSG = &STR(SDSF OPER REQUIRES A "Cn" OR "CC" LINE COMMAND)
    ISPEXEC SETMSG MSG(ISRZ001W)
    EXIT CODE(12)
  END
ISREDIT (LINE1) = LINENUM .ZFRANGE   /* FIRST LINE IN RANGE */
ISREDIT (LINE2) = LINENUM .ZLRANGE   /* LAST LINE IN RANGE  */
/*                                                          */
/*  )INIT SECTION OF PANEL ISFPANEL HAS BEEN MODIFIED TO    */
/*  ISSUE .RESP=END IF &SDSFOPER HAS BEEN SET TO 'END'      */
/*  BY THIS EDIT MACRO.  CAUSES SDSF PANEL NOT TO DISPLAY   */
/*  AFTER EACH OPERATOR COMMAND HAS BEEN ISSUED.            */
/*                                                          */
SET &SDSFOPER = &STR(END)
ISPEXEC VPUT (SDSFOPER) SHARED
/* PASS THE COMMAND LINES TO SDSF */
SET I = &LINE1
DO WHILE &I <= &LINE2
  ISREDIT (SDSFCMD) = LINE &I
  /* IF NO '/' IN COLUMN 1, THEN ADD ONE FOR SDSF */
  IF &SUBSTR(1:1,&SDSFCMD) ¬= &STR(/) THEN -
    SET &SDSFCMD = &STR(/)&STR(&SDSFCMD)
  ISPEXEC SELECT PGM(ISFISP) PARM(&STR(&SDSFCMD))
  /* LEAVE OUT NEWAPPL(ISF) OR SDSFOPER VARIABLE NOT PASSED */
  SET I = &I + 1
END
EXIT CODE(1)  /* RETURN CURSOR TO COMMAND LINE */
/* J. KALINICH, X4521 */
/* EDIT MACRO TO PASS MVS/JES2 OPERATOR COMMANDS TO SDSF */
./ ADD NAME=RUN      0168-90310-96047-0737-00078-00008-00000-CLIST
ISREDIT MACRO (PARM) NOPROCESS
CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
ISPEXEC CONTROL DISPLAY LINE START(23)
ISREDIT (DSN) =  DATASET

/* CHECK DSNAME EXTENSION FOR CLIST/EXEC TYPE        */
IF &SUBSTR(&LENGTH(&STR(&DSN))-4:+
  &LENGTH(&STR(&DSN)),&STR(&DSN)) = &STR(.EXEC) THEN -
    SET &TYPE = &STR(EXEC)
IF &SUBSTR(&LENGTH(&STR(&DSN))-4:+
  &LENGTH(&STR(&DSN)),&STR(&DSN)) = &STR(.REXX) THEN -
    SET &TYPE = &STR(EXEC)
IF &SUBSTR(&LENGTH(&STR(&DSN))-5:+
  &LENGTH(&STR(&DSN)),&STR(&DSN)) = &STR(.CLIST) THEN -
    SET &TYPE = &STR(CLIST)

ISREDIT (LINE1) = LINE 1

/* CHECK FOR "//" TO SEE IF THEY MEANT "GO"          */
IF &SUBSTR(1:2,&STR(&LINE1)) = &STR(//) THEN -
  DO
    ISREDIT SUBMIT
    ISPEXEC SELECT PGM(IOFSPF) NEWAPPL(IOF) PASSLIB
    EXIT CODE(4)
  END

/* CHECK LINE 1 FOR REXX IDENTIFIER                  */
IF &SYSINDEX(&STR(REXX),&STR(&LINE1),3) > 0 OR -
   &SYSINDEX(&STR(Rexx),&STR(&LINE1),3) > 0 OR -
   &SYSINDEX(&STR(rexx),&STR(&LINE1),3) > 0 THEN -
  SET &TYPE = &STR(EXEC)

/* PROCESS LINE COMMANDS, CHECK IF "C" WAS SPECIFIED */
ISPEXEC CONTROL ERRORS RETURN
ISREDIT PROCESS RANGE C
IF &LASTCC > 0 THEN -
  ISREDIT (MEM) =  MEMBER
ELSE -
  DO
    ISREDIT (MEM) =  MEMBER
    IF &STR(&MEM) = &STR() THEN -
      DO
        SET ZEDSMSG = &STR(DSORG=PS)
        SET ZEDLMSG = &STR(CAN NOT BUILD A PDS MEMBER)
        ISPEXEC SETMSG MSG(ISRZ001W)
        EXIT CODE(12)
      END
    IF &LENGTH(&SYSUID) > 6 THEN -
      SET &UID = &SUBSTR(1:6,&SYSUID)
    ELSE -
      SET &UID = &STR(&SYSUID)
    ISREDIT REPLACE @&UID.@ .ZFRANGE .ZLRANGE
    SET MEM = &STR(@&UID.@)
  END

IF &STR(&PARM) = &STR() THEN -
  DO
    IF &STR(&MEM) = &STR() THEN -
      EXEC '&DSN' &TYPE
    ELSE -
      EXEC '&DSN.(&MEM)' &TYPE
  END
ELSE -
  DO
    IF &STR(&MEM) = &STR() THEN -
      EXEC '&DSN' '&PARM' &TYPE
    ELSE -
      EXEC '&DSN.(&MEM)' '&PARM' &TYPE
  END

SET &RC = &LASTCC
SET ZEDSMSG = &STR(&TYPE ENDED)
SET ZEDLMSG = &STR(&TYPE '&MEM' ENDED WITH RETURN CODE &RC)
ISPEXEC SETMSG MSG(ISRZ001W)

EXIT CODE(1)                   /* RETURN CURSOR TO COMMAND LINE */
/* J. KALINICH, X4521 */
/* EDIT MACRO TO 'EXECute' THE CLIST CURRENTLY BEING EDITED  */
./ ADD NAME=SHOWCUT  0120-90282-98168-0749-00023-00015-00000-CLIST
ISREDIT MACRO (PARM) NOPROCESS
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC VGET (CUTNAME) PROFILE
SET CT = &CUTNAME
IF &LENGTH(&STR(&PARM)) > 0 THEN -
  SET CT = &PARM
SET CUTPST = CUTPST&CT
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC TBOPEN &CUTPST NOWRITE SHARE
IF &LASTCC ¬= 0 THEN -
  DO
    SET ZEDSMSG = &STR(CUT TABLE NOT FOUND)
    SET ZEDLMSG = &STR(CUT TABLE '&CT' DOES NOT EXIST)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
  END
ISPEXEC TBTOP  &CUTPST
DO WHILE &RC < 8
  ISPEXEC TBDISPL &CUTPST PANEL(SHOWCUTP)
  SET RC = &LASTCC
END
/* J. KALINICH, X4521 */
/* MACRO TO DISPLAY THE PDS 8.5 CUT (EDIT) TABLES       */
./ ADD NAME=STARTUP  0102-95195-95212-0957-00033-00028-00000-CLIST
PROC  0
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST

IF &STR(&SYSDSN(ISPPROF)) = &STR(OK) THEN -
  ALLOC F(ISPPROF) DA(ISPPROF) SHR REUSE
ELSE -
  DO
    ALLOC F(ISPPROF) DA(ISPPROF) TRACKS SP(5 5) -
      DIR(5) NEW CATALOG UNIT(PERM) -
      RECFM(F B) LRECL(80) BLKSIZE(6240)
  END

IF &STR(&SYSDSN(GEN.CNTL)) = &STR(OK) THEN -
  ALLOC F(ISPFILE) DA(GEN.CNTL) SHR REUSE
ELSE -
  DO
    ALLOC F(ISPFILE) DA(GEN.CNTL) TRACKS SP(5 5) -
      DIR(5) NEW CATALOG UNIT(PERM) -
      RECFM(F B) LRECL(80) BLKSIZE(6240)
  END

IF &STR(&SYSDSN(GEN.CNTL(@LOGLIST))) = &STR(OK) THEN -
  EXEC '&SYSUID..GEN.CNTL(@LOGLIST)' EXEC
ELSE -
  DO
    ALLOC F(ISPLIST) SYSOUT(R) HOLD -
      LRECL(121) BLKSIZE(1210) RECFM(F B A)
    ALLOC F(ISPLOG)  SYSOUT(R) HOLD -
      LRECL(125) BLKSIZE(129) RECFM(V A)
  END

/* ISPF PROFILE, FILE TAILORING, LOG, LIST ALLOCATION     */
/* OPERATING SYSTEMS BRANCH 7/14/95                       */
./ ADD NAME=SUPERC   0105-91143-92358-0831-00096-00089-00000-CLIST
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
PANEL: +
ISPEXEC DISPLAY PANEL(ISRSFSPR)
IF &LASTCC = 8 THEN EXIT
IF &STR(&ZSSFNDSN) = &STR() THEN -
  SET &NEWDD = &ZSSFNPRJ..&ZSSFNGR1..&ZSSFNTYP
ELSE -
  DO
    IF &SUBSTR(1,&ZSSFNDSN) = &STR(') THEN -
      SET &NEWDD = &STR(&ZSSFNDSN)
    ELSE -
      SET &NEWDD = &STR(&SYSUID..&ZSSFNDSN)
    /* STRIP OUT (*) IF ENTERED WITH OTHER DSNAME               */
    IF &SUBSTR(&LENGTH(&STR(&NEWDD))-3:+
       &LENGTH(&STR(&NEWDD)),&STR(&NEWDD)) = &STR((*)') THEN -
      SET &NEWDD = &SUBSTR(2:&LENGTH(&STR(&NEWDD))-4,&STR(&NEWDD))
    IF &SUBSTR(&LENGTH(&STR(&NEWDD))-2:+
       &LENGTH(&STR(&NEWDD)),&STR(&NEWDD)) = &STR((*)) THEN -
      SET &NEWDD = &SUBSTR(1:&LENGTH(&STR(&NEWDD))-3,&STR(&NEWDD))
  END
IF &ZSSFNVOL = &STR() THEN -
  SET &NEWVOL = &STR()
ELSE -
  SET &NEWVOL = &STR(,VOL=SER=&ZSSFNVOL,UNIT=SYSALLDA)
IF &ZSSFMULT = &STR(YES) THEN -
  DO
    ISPEXEC DISPLAY PANEL(ISRSFSST)
    IF &LASTCC = 8 THEN GOTO PANEL
  END
SET I = 1
DO WHILE &I <= 10
  SET &SRCH&I = &STR()
  SET &SFS = &&ZSSFS&I
  IF &STR(&SFS) ¬= &STR() THEN -
    DO
      IF &SUBSTR(1:1,&STR(&SFS)) ¬= &STR(') THEN -
        DO
          IF &LENGTH(&STR(&SFS)) > 5 THEN -
            DO
              IF &SUBSTR(&LENGTH(&STR(&SFS))-4:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( WORD) THEN -
                SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-3,&STR(&SFS))
            END
          IF &LENGTH(&STR(&SFS)) > 7 THEN -
            DO
              IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( PREFIX) THEN -
                SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
            END
          IF &LENGTH(&STR(&SFS)) > 7 THEN -
            DO
              IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( SUFFIX) THEN -
                SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
            END
          IF &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                     &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( C) OR -
             &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                     &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( P) OR -
             &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                     &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( S) OR -
             &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                     &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( W) THEN -
            DO
              SET &SFS = &STR(')+
                         &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))+
                         &STR(',)+
                         &SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS))
            END
          ELSE -
            SET &SFS = &STR('&STR(&SFS)')
        END
      IF &SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR(C) THEN -
        DO
          SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))
          SET &SRCH&I = &STR(SRCHFORC)
        END
      ELSE -
        DO
          SET &SRCH&I = &STR(SRCHFOR)
        END
      SET &SFS&I = &STR(&SFS)
    END
  SET I = &I + 1
END
ISPEXEC FTOPEN
ISPEXEC FTINCL SUPERC
ISPEXEC FTCLOSE NAME(SUPERC)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(SUPERC)) MACRO(JCI)
/* J. KALINICH, X4521 */
/* CLIST TO CREATE JCL TO RUN OPTION 3.14 SEARCH-FOR IN BATCH     */
./ ADD NAME=SYSLOG   0130-90291-99067-0829-00074-00012-00000-CLIST
PROC 0 DEBUG PARM()
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
/* DAY OF WEEK:  "ALL" OR "MON-SUN" ENTERED ON SYSLOG COMMAND ? */
IF &STR(&PARM) = &STR(ALL) THEN -
  DO
    ISPEXEC SELECT PGM(IOFSPF) +
      PARM(.SLAMMEMO GROUP .EXCLUDE WTRID NC 0000 +
           .RIGHT 90 .SORT PRMODE) +
      NEWAPPL(IOF) PASSLIB
      EXIT
  END
IF &STR(&PARM) = &STR(MON) OR -
   &STR(&PARM) = &STR(TUE) OR -
   &STR(&PARM) = &STR(WED) OR -
   &STR(&PARM) = &STR(THU) OR -
   &STR(&PARM) = &STR(FRI) OR -
   &STR(&PARM) = &STR(SAT) OR -
   &STR(&PARM) = &STR(SUN) THEN -
  DO
    /* WE ARE SAVING 2 WEEKS WORTH OF SLAMMEMOS,     */
    /* SO "2 VIEW" SELECTS THE DAY FROM THE 2ND WEEK */
    ISPEXEC SELECT PGM(IOFSPF) +
      PARM(.SLAMMEMO GROUP .EXCLUDE UCS NC &PARM +
           .RIGHT 90 .2 VIEW .TOP) +
      NEWAPPL(IOF) PASSLIB
      EXIT
  END
DO WHILE &LOGCC < 8
  ISPEXEC DISPLAY PANEL(SYSLOG)
  SET &LOGCC = &LASTCC
  IF &LOGCC = 8 THEN EXIT
  IF &STR(&LOGDAY) = &STR(*) THEN -
    DO
      IF &STR(&LOGPGM) = &STR(IOF) THEN -
        DO
          ISPEXEC CONTROL DISPLAY LOCK
          SET ZEDSMSG = &STR(USE "#" FOR MVS COMMANDS)
          ISPEXEC DISPLAY MSG(ISRZ001W)
          ISPEXEC SELECT PGM(IOFSPF) +
            PARM(.L) NEWAPPL(IOF) PASSLIB
        END
      ELSE -
        DO
          ISPEXEC CONTROL DISPLAY LOCK
          SET ZEDSMSG = &STR(USE "/" FOR MVS COMMANDS)
          ISPEXEC DISPLAY MSG(ISRZ001W)
          ISPEXEC SELECT PGM(ISFISP) PARM('LOG') NEWAPPL(ISF) PASSLIB
        END
    END
  ELSE -
    DO
      IF &LOGDAY = &STR(ALL) THEN -
        DO
          ISPEXEC SELECT PGM(IOFSPF) +
            PARM(.SLAMMEMO GROUP .EXCLUDE WTRID NC 0000 +
                 .RIGHT 90 .SORT PRMODE) +
            NEWAPPL(IOF) PASSLIB
        END
      ELSE -
        DO
          /* WE ARE SAVING 2 WEEKS WORTH OF SLAMMEMOS,     */
          /* SO "2 VIEW" SELECTS THE DAY FROM THE 2ND WEEK */
          ISPEXEC SELECT PGM(IOFSPF) +
            PARM(.SLAMMEMO GROUP .EXCLUDE UCS NC &LOGDAY +
                 .RIGHT 90 .2 VIEW .TOP) +
            NEWAPPL(IOF) PASSLIB
        END
    END
END
/* J.KALINICH, X4521 */
/* CLIST TO VIEW IOF SLAM MEMOS OR CURRENT SYSLOG VIA SDSF OR IOF */
./ ADD NAME=UNX      0106-92349-93007-1343-00054-00029-00000-CLIST
ISREDIT MACRO (NLINES)
/*
/* UNX EDIT MACRO - SHOW FIRST N LINES OF EACH 'X-CLUDED' BLOCK
/*                  IN THE MEMBER
/*                  DEFAULT = 1 LINE
/*
CONTROL NOFLUSH NOMSG NOLIST NOCONLIST NOSYMLIST
IF &NLINES = HELP OR -
   &NLINES = ?  THEN -
  DO
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(#UNX)
    EXIT
  END
IF &NLINES = &STR() THEN -
  SET &NLINES = 1
ISPEXEC CONTROL ERRORS RETURN
SET &RC  = 0
SET &LINE = 1
DO WHILE &RC < 12
  ISREDIT (XSTATUS) = XSTATUS &LINE
  SET &RC = &LASTCC
  IF &RC = 12 THEN -
    EXIT
  IF &XSTATUS = &STR(X) THEN -
    DO
      SET &XLINES = 0
      DO WHILE &XLINES < &NLINES
        ISREDIT (XSTATUS) = XSTATUS &EVAL(&LINE+&XLINES)
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        ISREDIT XSTATUS &EVAL(&LINE+&XLINES) = NX
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        SET &XLINES = &XLINES + 1
      END
      SET &LINE = &LINE + &EVAL(&NLINES)
      DO WHILE &XSTATUS = &STR(X)
        ISREDIT (XSTATUS) = XSTATUS &LINE
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        SET &LINE = &LINE + 1
      END
    END
  ELSE -
    SET &LINE = &LINE + 1
END
ISREDIT LOCATE 0
EXIT CODE(1)  /* RETURN CURSOR TO COMMAND LINE */
END
/* J. KALINICH, X4521 */
/* EDIT MACRO TO SHOW FIRST N LINES IN EACH 'X-CLUDED' BLOCK    */
