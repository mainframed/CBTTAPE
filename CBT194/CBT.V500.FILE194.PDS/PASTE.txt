/* REXX ***************************************************************/
/***                                                                  */
/*** ISPF/PDF edit macro to write lines from the user PROFILE pool    */
/*** into the current file.  This macro is used in conjunction with   */
/*** the CUT macro.                                                   */
/***                                                                  */
/*** To run:                                                          */
/***   Enter PASTE on the COMMAND line and use the A or B line        */
/***   command to specify where the lines are to be pasted.           */
/***                                                                  */
/***   A parameter of K or KEEP can be specified to prevent the macro */
/***   from setting the profile variables to null after the lines     */
/***   have been pasted into the file.                                */
/***                                                                  */
/***   If line truncation occurs, the profile variables will not be   */
/***   set to null.  When this occurs, edit a data set with a large   */
/***   enough record length or use CUT REPLACE to replace the lines   */
/***   in the PROFILE pool.                                           */
/**********************************************************************/
Trace
Address ISPEXEC

'ISREDIT MACRO (PARM1) NOPROCESS'
parm1 = translate(parm1)
If parm1 = '?' Then
  Do
    Call help
    Exit
  End

If parm1 ¬= ' ' & parm1 ¬= 'K' & parm1 ¬= 'KEEP' Then
  Do
    zedsmsg = 'Invalid parameter'
    zedlmsg = 'Use ? for help, K or KEEP to prevent variable deletion'
    'SETMSG MSG(ISRZ001)'
    Exit 12
  End

/* Process line commands, check if A or B was specified              */
'CONTROL ERRORS RETURN'
'ISREDIT PROCESS DEST'
Select
  When rc = 0 Then
    'ISREDIT (ZDEST) = LINENUM .ZDEST'
  When rc <= 8 Then /* No A or B entered */
    Do
       zedsmsg = 'Enter "A"/"B" line cmd'
       zedlmsg = 'PASTE requires an "A" or "B" line command'
       'SETMSG MSG(ISRZ001)'
       Exit 12
    End
  When rc < 20 Then /* Conflicting line commands - edit sets message */
    Exit 12
  When rc = 20 Then
    zdest = 0
  Otherwise
    Exit 12
End
'CONTROL ERRORS CANCEL'

/* Get the number of lines variable                                  */
'VGET (CUTCNT,CUTCNTMX) PROFILE'
If rc ¬= 0 Then
  Do
    zedsmsg = 'Use CUT before PASTE'
    zedlmsg = 'The CUTCNT profile variable was not found-use CUT first'
    'SETMSG MSG(ISRZ001)'
    Exit 12
  End
If cutcnt <= 0 Then
  Do
    zedsmsg = 'Use CUT before PASTE'
    zedlmsg = 'No data has been stored via the CUT macro'
    'SETMSG MSG(ISRZ001)'
    Exit 12
  End

trunccnt = 0
cutcount = cutcnt
/* get each cut line from the profile and add it to the file         */
Do i = cutcnt to 1 by -1
  'VGET (CL'i') PROFILE'
  'ISREDIT LINE_AFTER 'zdest' = DATALINE (CL'i')'
  If rc = 4 Then trunccnt = trunccnt + 1
End


/* If KEEP parameter not given and no lines were truncated, set to    */
/* null all variables stored in the profile.  Variable cutcntmx       */
/* contains the number of lines cut but not deleted.                  */
If parm1 ¬= 'K' & parm1 ¬= 'KEEP' & trunccnt = 0 Then
  Do
    'VGET (ZENVIR) SHARED'
    Do i = 1 to cutcntmx by 1
      'VERASE (CL'i') PROFILE'
    End
    cutcnt   = 0
    cutcntmx = 0
    'VPUT (CUTCNT,CUTCNTMX) PROFILE'
  End

/* Check if truncation occurred, if so display message                */
If trunccnt > 0 Then
  Do
    zedsmsg = trunccnt 'lines truncated'
    msg     = 'Current record length shorter than origin'
    zedlmsg = msg '-' trunccnt 'of' cutcount 'records were truncated'
    'SETMSG MSG(ISRZ001)'
  End
Else
  Do
    zedsmsg = cutcount 'lines pasted'
    zedlmsg = cutcount 'lines were pasted into the current file'
    'SETMSG MSG(ISRZ000)'
  End

line1 = zdest + 1
'ISREDIT CURSOR = 'line1 0
Exit

help:
Say '                                                                '
Say ' ISPF/PDF edit macro to write lines from the user PROFILE pool  '
Say ' into the current file. This macro is used in conjunction with  '
Say ' the CUT macro.                                                 '
Say '                                                                '
Say ' To run:                                                        '
Say '  Enter PASTE on the COMMAND line and use the A or B line       '
Say '  command to specify where the lines are to be pasted.          '
Say '                                                                '
Say '  A parameter of K or KEEP can be specified to prevent the macro'
Say '  from setting the profile variables to null after the lines    '
Say '  have been pasted into the file.  If you want to paste the same'
Say '  set of lines many places, specify the K or KEEP parameter.    '
Say '                                                                '
Say '  If line truncation occurs, the profile variables will not be  '
Say '  set to null.  If this occurs, edit a data set with a large    '
Say '  enough record length or use CUT REPLACE to replace the lines  '
Say '  in the PROFILE pool.                                          '
Say '                                                                '
Return
