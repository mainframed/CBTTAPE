/**************************** REXX **********************************/
/*                                                                  */
/*      This REXX EXEC will run IPCS in either INTERACTIVE          */
/*      or BATCH mode using an IPCS VSAM DUMP DIRECTORY created     */
/*      by the IPCSDDR batch job.                                   */
/*                                                                  */
/*      NOTE: You must be outside of ISPF/PDF to run this EXEC!     */
/*                                                                  */
/*                                                                  */
/*      Keyword operands                                            */
/*                                                                  */
/*      SUFX  ==>  last index name of your sequential dump          */
/*                 dataset for the stand-alone dump                 */
/*                                                                  */
/*      BATCH  ==>  specify this operand if you want IPCS to        */
/*                  be run in batch (interactive is default)        */
/*                                                                  */
/********************************************************************/

arg sufx batch

status = MSG('OFF')

subcom ispexec
if rc=0 then
   do
     say '@IPCSSAD rexx exec cannot be executed under ISPF'
     exit
   end
else NOP

USERID = SYSVAR(syspref)

"DELETE '"USERID".IPCSPRNT'"
if rc¬=0 then
   do
     say 'DELETE TSO command failed'
     say 'for IPCS print dataset - RC is' rc
   end
else NOP

"ALLOCATE F(IPCSPRNT) DA('"USERID".IPCSPRNT')",
   "NEW SPA(5 1) CYL REL REUSE"
if rc¬=0 then
   do
     say 'ALLOCATE TSO command failed'
     say 'for IPCS print dataset - RC is' rc
     exit
   end
else NOP

ddir = sysdsn("'"USERID".DDIR.SADUMP."sufx"'")
if ddir = 'DATASET NOT FOUND' then
   do
      SAY USERID'.DDIR.SADUMP.'sufx NOT FOUND
      "%@IPCSTRM"
      exit
   end

"ALLOCATE F(IPCSDDIR) DA('"USERID".DDIR.SADUMP."sufx"') SHR REUSE"
if rc¬=0 then
   do
     say 'ALLOCATE TSO command failed'
     say 'for IPCS dump directory - RC is' rc
     exit
   end
else NOP


if batch ¬= 'BATCH' then
 do
   say '@IPCSSAD rexx exec online execution'
   say 'existing' userid'.DDIR.SADUMP.'sufx,
       'vsam dump directory allocated'
   say 'dataset' userid'.IPCSPRNT will be created'
   queue "SETDEF LENGTH(200) FLAG(ERROR) LIST",
               "DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL)",
               "NOCONFIRM NOVERIFY TERMINAL NOPRINT"
   queue "ISPF PGM(BLSGLIBD) PARM(PANEL(BLSPPRIM))",
              "PARM(PANEL(BLSPPRIM)) NEWAPPL(BLSG) PASSLIB"
   "IPCS NOPARM"
   exit
  end

if batch = 'BATCH' then
 do
   say '@IPCSSAD rexx exec batch execution'
   say 'existing' userid'.DDIR.SADUMP.'sufx,
       'vsam dump directory allocated'
   say 'dataset' userid'.IPCSPRNT will be created'
   queue "SETDEF LENGTH(200) FLAG(ERROR) LIST",
               "DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL)",
               "NOCONFIRM NOVERIFY TERMINAL NOPRINT"
   "IPCS NOPARM"
    exit
 end
