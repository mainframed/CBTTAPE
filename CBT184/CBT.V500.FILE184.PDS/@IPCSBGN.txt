/**************************** REXX **********************************/
/*                                                                  */
/*      This REXX EXEC will run IPCS in either INTERACTIVE          */
/*      or BATCH mode and will allocate a new IPCS VSAM DUMP        */
/*      DIRECTORY.                                                  */
/*                                                                  */
/*      NOTE: You must be outside of ISPF/PDF to run this EXEC!     */
/*                                                                  */
/*                                                                  */
/*      Keyword operands                                            */
/*                                                                  */
/*      BATCH  ==>  specify this operand if you want IPCS to        */
/*                  be run in batch (interactive is default)        */
/*                                                                  */
/*      VOLUME ==>  specify volume where IPCS VSAM DUMP DIRECTORY   */
/*                  is to be allocated                              */
/*                                                                  */
/********************************************************************/
arg batch volume

status = MSG('OFF')

subcom ispexec
if rc=0 then
   do
     say '@IPCSBGN rexx exec cannot be executed under ISPF'
     exit
   end
else NOP

If volume = '' then volume = 'HB8116'

USERID = SYSVAR(syspref)

"DELETE '"USERID".DDIR.SVCDUMP' CLUSTER"
if rc¬=0 then
   do
     say 'DELETE TSO command failed'
     say 'for IPCS dump directory - RC is' rc
   end
else NOP

"DELETE '"USERID".IPCSPRNT'"
if rc¬=0 then
   do
     say 'DELETE TSO command failed'
     say 'for IPCS print dataset - RC is' rc
   end
else NOP

"ALLOCATE F(IPCSPRNT) DA('"USERID".IPCSPRNT')",
   "NEW SPA(5 1) CYL REL REUSE"
if rc¬=0 then
   do
     say 'ALLOCATE TSO command failed'
     say 'for IPCS print dataset - RC is' rc
     exit
   end
else NOP

if batch ¬= 'BATCH' then
   do
     say '@IPCSBGN rexx exec online execution'
     say userid'.DDIR.SVCDUMP vsam dump directory will be created'
     say 'dataset' userid'.IPCSPRNT will be created'
     CALL IPCSDDR
     if result > 0 then exit
     queue "SETDEF LENGTH(200) FLAG(ERROR) LIST",
                 "DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL)",
                 "NOCONFIRM NOVERIFY TERMINAL NOPRINT"
     queue "ISPF PGM(BLSGLIBD) PARM(PANEL(BLSPPRIM))",
                "PARM(PANEL(BLSPPRIM)) NEWAPPL(BLSG) PASSLIB"
     "IPCS NOPARM"
     exit
    end

if batch = 'BATCH' then
   do
     say '@IPCSBGN rexx exec batch execution'
     say userid'.DDIR.SVCDUMP vsam dump directory will be created'
     say 'dataset' userid'.IPCSPRNT will be created'
     CALL IPCSDDR
     if result > 0 then exit
     queue "SETDEF LENGTH(200) FLAG(ERROR) LIST",
                 "DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL)",
                 "NOCONFIRM NOVERIFY TERMINAL NOPRINT"
     "IPCS NOPARM"
      exit
   end


IPCSDDR:

" DEFINE CLUSTER(NAME('"USERID".DDIR.SVCDUMP')        ",
" VOLUME("volume") INDEXED FREESPACE(10,10) REUSE     ",
" IMBED REPLICATE SHAREOPTIONS(1,3))                  ",
" INDEX(NAME('"USERID".DDIR.SVCDUMP.INDEX')           ",
" CYLINDERS(1,1) CISZ(2048))                          ",
" DATA(NAME('"USERID".DDIR.SVCDUMP.DATA')             ",
" CYLINDERS(70,30) CISZ(4096) KEYS(128,0) RECORDSIZE(384 3072))"
if rc¬=0 then
   do
     say 'DEFINE CLUSTER TSO command failed'
     say 'for IPCS dump directory - RC is' rc
     RETURN RC
     exit
   end
else NOP

" IPCSDDIR '"USERID".DDIR.SVCDUMP'                    "
if rc¬=0 then
   do
     say 'IPCSDDIR TSO command failed'
     say 'for IPCS dump directory - RC is' rc
     RETURN RC
     exit
   end
else NOP

" ALLOCATE FILE(IPCSDDIR)                             ",
"          DSNAME('"USERID".DDIR.SVCDUMP')            ",
"          SHR REUSE                                  "
if rc¬=0 then
   do
     say 'ALLOCATE TSO command failed'
     say 'for IPCS dump directory - RC is' rc
     RETURN RC
     exit
   end
else NOP
