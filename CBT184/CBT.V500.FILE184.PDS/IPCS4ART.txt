1

         IPCS/ESA Customization Using REXX                   Page #1


    In a previous series of articles, I discussed general usage
    of IPCS under MVS/ESA.  Once you have spent some time using
    IPCS many people will want to develop their own specialized
    procedures Using REXX execs under IPCS is the quickest way
    to do exactly this.


    This article will cover the basics of using REXX EXECS to
    develop specialized IPCS tools for dump analysis.
    Once you know how to develop these tools, you can save yourself
    a great deal of time in problem determination and begin to
    gather in-depth knowledge how MVS/ESA works.


    The REXX execs I will present were written in less that one
    week.  I ensure you these REXX execs are at the novice level.
    I would bet there are many more efficient ways to do
    some of the data manipulation I do.  I welcome the more
    experienced REXX practicioners to streamline these procedures
    and let NASPA readership know what they have done.


    A picture is worth 1000 words (depending on your computer
    architecture).  I will cover each fully-documented REXX
    exec with comments on the joys (and pains) of its development.


    IPCS/ESA has given us a number of commands specifically for
    interactive procedure (CLIST and REXX exec) usage.  Please
    refer to Figure #1 on IPCS COMMANDS FOR REXX EXECS.


    We can eliminate the usage of the IPCS INTEGER command when
    using REXX execs due to the superior data manipulation of REXX
    over TSO CLIST.  REXX has the hex-to-decimal (X2D)
    and decimal-to-hex (D2X) commands to do this type of data
    manipulation.


    Another reason for not using the IPCS INTEGER command in a REXX
    exec is because you are running in a different REXX HOST COMMAND
    ENVIRONMENT (HCE).  There is a certain amount of overhead going
    from one HCE to another.  It would be best to do all the
    character manipulation in REXX and then place it into IPCS
    variables.

1

         IPCS/ESA Customization Using REXX                   Page #2


    The EVALDEF, EVALDUMP and EVALMAP commands deal primarily with
    the IPCS environment and not with dump data manipulation.
    Our emphasis on using REXX execs in IPCS will be to create
    customized reports and not deal with the IPCS environment.
    We will not cover these IPCS commands in this article.
    If you have need to manipulate the IPCS environment the I
    would suggest you look at the IBM MVS/ESA clist library
    SYS1.SBLSCLI0 which has many good working examples of these
    commands.


    We are left with only two IPCS commands: EVALUATE and EVALSYM.
    I have found the IPCS EVALUATE and EVALSYM commands the most
    helpful in creating customized IPCS REXX procedures.
    The EVALUATE command will allow us to transfer dump data
    to and from REXX variables and the EVALSYM command will
    allow us to dynamically reference IPCS symbols in REXX
    procedures.


    To use the REXX keyword in the EVALUATE command you must have
    the maintenance for APAR OY46507 applied to your system.


    My first IPCS REXX exec was a small exec to display the dump
    type.  You can get this same information from the IPCS
    STATUS SYSTEM command.  This REXX exec is a simple,
    straightforward example for a novice REXX exec writer
    and shows you what you can immediately do using IPCS REXX
    execs:


    PLEASE REFER TO FIGURE #2 @DUMPTYP REXX EXEC.



    In our shop, I run my REXX execs out of the CLIST library
    SYSPROC concatenation, hence I use the specialized REXX
    comment at the top of the IPCS REXX exec.

1

         IPCS/ESA Customization Using REXX                   Page #3


    Here is an overview of what the @DUMPTYP IPCS REXX exec does:


    1) The first thing I do in any REXX exec is to ensure that we
       have an environment that could establish an ipcs session.
+                               ________________________________
       What this means is that the control block structure has
       been created that would allow passage into the IPCS enviroment.

       The REXX SUBCOM command will check to see if the IPCS task
       structure is available.  If the return code (rc) is not equal
       to 0 then I issue a message stating the @DUMPTYP REXX exec
       does not have a valid IPCS environment and then exit the
       exec.  In most cases (if not all), I would recommend you
       inspect return codes issued by REXX and/or IPCS commands
       to make sure you are executing in a clean error-free environment.


    2) Now we must  set the IPCS host command environment to
       send commands in double quotes (") to IPCS with the ADDRESS
       IPCS command.  This will direct all "COMMANDS" issued to the
       IPCS environment for execution.  I prefer to use double quotes
       (") around the IPCS commands I issue although you could also
       use single quotes (').


    3) Our third step is to issue the IPCS NOTE command that the IPCS
       REXX exec is called @DUMPTYP and that its function is to
       display current active dump type information.  The first IPCS
       NOTE command uses the PAGE keyword option that indicates this
       IPCS output should start at the top of a page.  I could have
       issued the REXX SAY command to display this information but
       chose not to.  Why?  The REXX SAY command will issue line mode
       messages while we are in fullscreen IPCS.  I would use REXX SAY
       messages for error messages since I will see them between IPCS
       fullscreen displays.  I use the IPCS NOTE commands for normal
       output messages.


    4) We use the IPCS EQUATE command with the HEADER
       keyword option to get the dump header information into a
       variable I also call HEADER.  If the dump header information
       is damaged in any way,  the dump is no good.


    5) The IPCS EVALUATE command will retrieve 8 bytes (or less if
       trailing blanks are found) at the HEADER + 40 and place
       this information in the REXX variable called DUMPTYP.

1

         IPCS/ESA Customization Using REXX                   Page #4


    6) The last step of this IPCS REXX exec is using REXX select
       command logic to issue the proper dump type message based
       on the REXX DUMPTYP variable.


    Now you are ready for a more powerful example of IPCS REXX
    exec usage.  The @DPRTMAJ and @DPRTMIN IPCS REXX execs will
    allow you to inspect ASID and DISPATCHING PRIORITY information
    for one address space or all address spaces.  The @DPRTMAJ
    IPCS REXX exec calls @DPRTMIN as an external subroutine although
    @DPRTMIN can run by itself if it is passed a valid ASCB address.



    PLEASE REFER TO FIGURE #3 @DPRTMAJ REXX EXEC.



    Here is an overview of what the @DPRTMAJ IPCS REXX exec does:


    1) I perform many of the same functions as discussed in the
       @DUMPTYP IPCS REXX exec such as:

       - check for a valid IPCS environment
       - check for change of default REXX HCE
       - issue note commands to allow easier IPCS output reading


    2) I issue IPCS EQUATE commands for the MVS/ESA Prefixed Save
       Area (PSA) in order to get to the Communications Vector
       Table (CVT).  Since any valid dump must contain the CVT
       pointer, these steps are to make this IPCS REXX exec
       self-documenting as to what and how it is doing.
       We equate the highest Address Space Control Block (ASCB)
       address (CVTASCBH) to the IPCS variable name of CVTASCBH
       for later use.


    3) We issue the IPCS DROPSYM command to ensure that we have
       no existing symbols beginning with the letters ASCB.
       The often-used IPCS SELECT command will create equates
       beginning with ASCB which may cause inconsistent
       results if we do not drop them.  We specify a range for
       the IPCS DROPSYM command to drop all symbols beginning
       with the letters ASCB.

1

         IPCS/ESA Customization Using REXX                   Page #5


    4) The IPCS RUNCHAIN command will allow us to run the entire
       ASCB control block chain from highest ASCB to lowest ASCB
       and create IPCS symbols for each (ASCB001,ASCB002, etc.)
       which will be used by the @DPRTMIN IPCS REXX exec.
       The IPCS RUNCHAIN command has a parameter to allow you
       to execute a IPCS REXX exec at the time each ASCB symbol
       has been created.  As we will see later, this facility
       will allow the @DPRTMIN IPCS REXX exec to process the
       correct information for each invocation of it.


    6) IPCS has a nasty habit of returning the first output screen
       to the user before the entire output has been written to
       the output buffer.  Should a user decide to end the terminal
       display via the END command (PF3), the IPCS REXX exec will
       display many nasty return code -3 values indicating that
       there was an invalid HCE for IPCS.

       I have elected to turn REXX tracing off (trace off) so I
       can detect the non-zero return code from a user pressing
       END (PF3) and issue a REXX SAY message telling the user
       the remaining output was cancelled.


    Besides being a REXX exec invoked by @DPRTMAJ, the @DPRTMIN
    REXX exec can also be invoked as a stand-alone REXX exec.



    PLEASE REFER TO FIGURE #4 @DPRTMIN REXX EXEC.



    Here is an overview of what the @DPRTMIN IPCS REXX exec does:


    1) I perform many of the same functions as discussed in the
       @DUMPTYP and @DPRTMAJ IPCS REXX execs such as:

       - check for a valid IPCS environment
       - check for change of default REXX HCE
       - issue note commands to allow easier IPCS output reading


    2) The @DPRTMIN REXX exec has a positional keyword called
       ONEASCB which indicates this REXX exec was used by itself.

1

         IPCS/ESA Customization Using REXX                   Page #6


       If @DPRTMIN is used by itself, I invoke a small loop that
       checks to see if the user entered that address with or
       without the ending period.  If the user did not enter the
       ending period, I place it in the effective address variable
       ONEASCB for them.


       When @DPRTMIN is executed by itself (not called from @DPRTMAJ)
       I will issue the IPCS NOTE messages for @DPRTMIN to indicate
       it is being executed by itself.


    3) The essence of the @DPRTMAJ/@DPRTMIN REXX execs relationship
       is defined by the use of the IPCS EVALSYM command.  When
       @DPRTMIN is executed from @DPRTMAJ (the REXX ONEASCB value
       is null), the IPCS EVALSYM command will be executed for
       each ASCB passed from the @DPRTMAJ REXX exec.  The ASCBS
       will look like ASCB001, ASCB002, ... etc. to ASCB999.
       We will never exceed 999 ASCBS since we take the RUNCHAIN
       CHAIN(999) default in @DPRTMAJ.

       We take this IPCS variable ASCB??? (where ??? is the equated
       number) and place it in a REXX variable called ASCB.  This
       transferring variables from IPCS to REXX can easily occur since
       REXX is a typeless language where everything is defined as a
       character string (character, hexadecimal and even binary).


    4) The REXX ASCB variable has to have the ending period '.'
       removed if it is there at the end of the REXX variable.
       We strip it off using the REXX RIGHT built-in function.


    5) The IPCS EVALUATE command is issued to get the hexadecimal
       ASID and DPRTY values from the ASCB.  To make the report
       neater, I have 2 loops (one for the ASID and another for
       the DPRTY) to right-align the decimal and hexadecimal
       variables for the report using the IPCS NOTE command.


    6) Like @DPRTMAJ, I turn off REXX tracing for the @DPRTMIN
       REXX exec so a user pressing END (PF3) will not get many
       nasty error messages due to the way IPCS fills the
       output buffer.

1

         IPCS/ESA Customization Using REXX                   Page #7


    PLEASE REFER TO FIGURE 5 for the output of @DPRTMAJ and @DPRTMIN



    These small working examples of using IPCS REXX execs may be
    expanded to allow an installation to develop a customized set
    of tools to isolate software failures and keep you system
    running.

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #8



    FIGURE #1   IPCS COMMANDS FOR REXX EXECS


    IPCS
    COMMAND      DESCRIPTION
+   ________     ______________________________________________

    EVALDEF      RETRIEVE SESSION ATTRIBUTES
    EVALDUMP     FORMAT DUMP ATTRIBUTES
    EVALMAP      FORMAT A STORAGE MAP ENTRY
    EVALSYM      FORMAT THE DEFINITION OF A SYMBOL
    EVALUATE     RETRIEVE DUMP DATA FOR REXX EXEC
    INTEGER      CONVERT A NUMBER FROM HEX TO DEC (VICE-VERSA)

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #9



    FIGURE #2   @DUMPTYP    IPCS/REXX EXEC

    /**************************** REXX *********************************
    /*
    /*
    /*      This IPCS REXX exec will display current active
    /*      dump information.
    /*
    /*
    /*      In the MVS/XA/ESA dump header + X'40' the following
    /*      dump header names will appear:
    /*
    /*
    /*      DUMP HEADER NAME    DUMP TYPE
    /*      ----------------    ----------------
    /*
    /*      AMDSADMP            STAND-ALONE DUMP
    /*
    /*      SVCDUMP             SYNCHRONOUS SVCDUMP
    /*
    /*      IEAVTSDT            SCHEDULED SVCDUMP
    /*
    /*      SYSMDUMP            UNFORMATTED APPLICATION DUMP
    /*
    /*      ACTIVE              ACTIVE MAIN STORAGE
    /*      MAIN
    /*      STORAGE
    /*
    /*******************************************************************


    /*******************************************************************
    /*    Check to see if we have a valid IPCS environment.            *
    /*    Use REXX ADDRESS keyword to ensure setting of a valid        *
    /*    IPCS environment.                                            *
    /*******************************************************************

    subcom ipcs
    if rc \= 0 then
       do
         say '@DUMPTYP does not have a valid IPCS environment'
         exit
       end
    else NOP

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #10



    FIGURE #2   @DUMPTYP    IPCS/REXX EXEC       (CONTINUED)



    /*******************************************************************
    /*    Change our default REXX host command environment (HCE)       *
    /*    from TSO to IPCS.                                            *
    /*******************************************************************


    address ipcs


     " NOTE '**************************************************' PAGE "
     " NOTE '*    @DUMPTYP:  STATE MVS/ESA DUMP TYPE          *'      "
     " NOTE '**************************************************'      "


    /*******************************************************************
    /*    Retrieve the IPCS header information and place the 8         *
    /*    bytes at HEADER + 40 into a REXX variable.                   *
    /*******************************************************************


     " EQUATE HEADER 0. HEADER  REMARK('IPCS DUMP HEADER AREA') "
       if rc \= 0 then
          do
            say '@DUMPTYP header invalid - dump is unusable'
            say '@DUMPTYP IPCS EQUATE return code is ' rc
            exit
          end
       else NOP

     " EVALUATE HEADER+40 LE(8) CHARACTER REXX(STORAGE(DUMPTYP)) "
       if rc \= 0 then
          do
            say '@DUMPTYP could not retrieve header+40'
            say '@DUMPTYP IPCS EVALUATE return code is ' rc
            exit
          end
       else NOP


    /*******************************************************************
    /*    Use REXX select logic to issue the appropriate message       *
    /*    based on the REXX dumptyp value.                             *
    /*    bytes at HEADER + 40 into a REXX variable.                   *
    /*******************************************************************

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #11



    FIGURE #2   @DUMPTYP    IPCS/REXX EXEC       (CONTINUED)


       select
       when dumptyp = 'AMDSADMP' then
            DO
             " NOTE '                                       ' "
             " NOTE ' DUMP TYPE IS MVS/ESA STAND-ALONE DUMP ' "
             " NOTE '                                       ' "
            END

       when dumptyp = 'SVCDUMP' then
         DO
          " NOTE '                                  ' "
          " NOTE ' DUMP TYPE IS SYNCHRONOUS SVCDUMP ' "
          " NOTE '                                  ' "
         END

       when dumptyp = 'IEAVTSDT' then
         DO
          " NOTE '                                ' "
          " NOTE ' DUMP TYPE IS SCHEDULED SVCDUMP ' "
          " NOTE '                                ' "
         END

       when dumptyp = 'SYSMDUMP' then
         DO
          " NOTE '                                     ' "
          " NOTE ' DUMP TYPE IS UNFORMATTED ABEND DUMP ' "
          " NOTE '                                     ' "
         END

       when dumptyp = 'ACTIVE MAIN STORAGE' then
         DO
          " NOTE '                                             ' "
          " NOTE ' NO DUMP:  THIS IS ACTIVE MAIN STORAGE       ' "
          " NOTE '                                             ' "
         END
       END

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #12



    FIGURE #3   @DPRTMAJ    IPCS/REXX EXEC

    /**************************** REXX *********************************
    /*
    /*      This REXX EXEC will list each address space JOBNAME/STCNAME
    /*      and its addess space dispatching priority from
    /*      highest ASCB to lowest ASCB.
    /*
    /*      Keyword operands     none
    /*
    /*******************************************************************

    /*******************************************************************
    /*    Check to see if we have a valid environment to run IPCS.     *
    /*    Use REXX ADDRESS keyword to set a valid IPCS environment.    *
    /*******************************************************************

    subcom ipcs
    if rc \= 0 then
       do
         say '@DPRTMAJ does not have a valid IPCS environment'
         exit
       end
    else NOP


    ADDRESS IPCS


    " NOTE '***************************************************' PAGE "
    " NOTE '*   @DPRTMAJ: LIST EACH ADDRESS SPACE DISPATCHING *'      "
    " NOTE '*             PRIORITY FROM HIGHEST ASCB TO       *'      "
    " NOTE '*             LOWEST ASCB.                        *'      "
    " NOTE '***************************************************'      "


    /*******************************************************************
    /*    Perform IPCS EQUATES for our PSA, CVT and the HIGHEST ASCB   *
    /*    (LOWEST ASID) in the system.                                 *
    /*******************************************************************

    " EQUATE  PSA  0.  STRUCTURE(PSA)  REMARK('PSA ADDRESS')  "
      if rc \= 0 then
         do
           say 'EQUATE for PSA return code is' rc
           exit
         end
      else NOP

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #13



    FIGURE #3   @DPRTMAJ    IPCS/REXX EXEC       (CONTINUED)


    " EQUATE  CVT  PSA+10?  REMARK('CVT ADDRESS')  "
      if rc \= 0 then
         do
           say 'EQUATE for CVT return code is' rc
           exit
         end
      else NOP

    " EQUATE  CVTASCBH  CVT+234? REMARK('HIGHEST ASCB ADDRESS') "
      if rc \= 0 then
         do
           say 'EQUATE for CVTASCBH return code is' rc
           exit
         end
      else NOP


    /*******************************************************************
    /*    Issue RUNCHAIN command to go from HIGHEST ASCB to LOWEST     *
    /*    ASCB on ASCB QUEUE and execute ASCB JOBNAME/DISPATCHING      *
    /*    PRIORITY LISTING called external subroutine rexx exec        *
    /*    (@DPRTMIN) to perform the actual processing.                 *
    /*                                                                 *
    /*    ATTENTION: I TURN OFF TRACE DUE TO THE FACT THAT IPCS        *
    /*               PERFORMS THE SCREEN DISPLAY BEFORE THE @DPRTMAJ   *
    /*               RUNCHAIN HAS COMPLETED.  THIS WILL AVOID          *
    /*               MANY NASTY REXX MESSAGES AND NEGATIVE RETURN      *
    /*               CODES.                                            *
    /*******************************************************************

    " DROPSYM ASCB:ASCC PURGE "

    trace off

    "  RUNCHAIN ADDRESS(CVTASCBH) NAME(ASCB) LINK(4) ",
    "           CHAIN(999) LENGTH(416) EXEC((@DPRTMIN)) "
       if rc \= 0 then
          do
            say 'user entered END (PF3) before display complete'
            say 'RUNCHAIN return code is' rc
            exit
          end
       else NOP

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #14



    FIGURE #4   @DPRTMIN    IPCS/REXX EXEC


    /**************************** REXX *********************************
    /*
    /*      This REXX EXEC will retrieve address space JOBNAME/STCNAME
    /*      ASID and DISPATCHING PRIORITY by specifying an ASCB or
    /*      being invoked from @DPRTMAJ.
    /*
    /*      Keyword operands     ASCB address
    /*
    /*******************************************************************

    arg oneascb

    /*******************************************************************
    /*    Check to see if we have a valid environment to run IPCS.     *
    /*    Use REXX ADDRESS keyword to set a valid IPCS environment.    *
    /*******************************************************************

    subcom ipcs
    if rc \= 0 then
       do
         say '@DPRTMIN does not have a valid IPCS environment'
         exit
       end
    else NOP


    ADDRESS IPCS


    /*******************************************************************
    /*    When executing @DPRTMIN by itself using an ASCB address,     *
    /*    this block of code will allow us to enter our ASCB address   *
    /*    as ?????? or ??????. and allow it to work correctly.         *
    /*******************************************************************

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #15



    FIGURE #4   @DPRTMIN    IPCS/REXX EXEC       (CONTINUED)



    if oneascb \= '' then
       do
        if right(oneascb,1) /= '.' then
                 oneascb = oneascbââ'.'
        " EQUATE X " oneascb
        if rc = 12 then
           do
               say  'you entered an invalid ascb address of' oneascb
               exit
           end
       end


    /*******************************************************************
    /*    When executing @DPRTMIN by itself using an ASCB address,     *
    /*    this block of code will issue the message that we are        *
    /*    running the @DPRTMIN rexx exec by itself.                    *
    /*******************************************************************


    if oneascb \= '' then
       do
        " NOTE '***************************************************' PAG
        " NOTE '*   @DPRTMIN: PERFORM ADDRESS SPACE CONTROL BLOCK *'
        " NOTE '*             LIST OF JOBNAME/STCNAME, ASID AND   *'
        " NOTE '*             DISPATCHING PRIORITY FOR A GIVEN    *'
        " NOTE '*             ASCB.                               *'
        " NOTE '***************************************************'
       end

    /*******************************************************************
    /*    The @DPRTMAJ RUNCHAIN command passes each ASCB address to    *
    /*    the @DPRTMIN rexx exec as IPCS symbols ASCB1, ASCB2, ASCB3,  *
    /*    etc.                                                         *
    /*                                                                 *
    /*    The EVALSYM command will check for the RUNCHAIN CHAIN(999)   *
    /*    default limit and place the passed IPCS address symbol       *
    /*    into a rexx symbol.                                          *
    /*                                                                 *
    /*    If @DPRTMIN was executed alone, then the rexx ascb value     *
    /*    is set via the oneascb rexx argument.                        *
    /*******************************************************************

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #16



    FIGURE #4   @DPRTMIN    IPCS/REXX EXEC       (CONTINUED)



    if oneascb = '' then
       do
          " EVALSYM LE ASCB999 REXX(ADDRESS(ASCB))"
       end
    else ascb = oneascb


    /*******************************************************************
    /*    Determine if supplied ASCB or current ASCB is to be used.    *
    /*                                                                 *
    /*    Perform IPCS EQUATE to get our rexx variable back into the   *
    /*    IPCS symbol.                                                 *
    /*                                                                 *
    /*    Make sure we properly equate the ascb value XXXXXX. and      *
    /*    allow the user to specify the ending period (or not).        *
    /*******************************************************************



        if right(ascb,1) /= '.' then
                 ascb = ascbââ'.'
        " EQUATE ASCB " ascb


    /*******************************************************************
    /*    Save the current ASCB information in rexx variables for      *
    /*    a later note command.                                        *
    /*                                                                 *
    /*    For our asid and dispatching priority values, get the        *
    /*    decimal and hexadecimal values of each for display.          *
    /*                                                                 *
    /*    Generate the report.                                         *
    /*******************************************************************


     " EVALUATE ASCB+25 LENGTH(1) REXX(STORAGE(ASIDX)) "
       if rc \= 0 then
          do
            say 'EVALUATE for ASID return code is' rc
            exit
          end
       else NOP

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #17



    FIGURE #4   @DPRTMIN    IPCS/REXX EXEC       (CONTINUED)


    /*******************************************************************
    /*    Right-align the decimal and hexadecimal asid value           *
    /*    for the report.                                              *
    /*******************************************************************

     asid = x2d(asidx)
     do while length(asid) < 3
        asid = ' 'ââasid
        end
     do while length(asidx) < 2
        asidx = ' 'ââasidx
        end
     asidx = '('ââasidxââ')'


     " EVALUATE ASCB+2B LENGTH(1) REXX(STORAGE(DPRTYX)) "
       if rc \= 0 then
          do
            say 'EVALUATE for DPRTY return code is' rc
            exit
          end
       else NOP

    /*******************************************************************
    /*    Right-align the decimal and hexadecimal dprty value          *
    /*    for the report.                                              *
    /*******************************************************************

     dprty = x2d(dprtyx)
     do while length(dprty) < 3
        dprty = ' 'ââdprty
        end
     do while length(dprtyx) < 2
        dprtyx = ' 'ââdprtyx
        end
     dprtyx = '('ââdprtyxââ')'

    /*******************************************************************
    /*    Check each ASCB for a jobname field (if present) and         *
    /*    a started-task name field (if present).                      *
    /*******************************************************************

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #18



    FIGURE #4   @DPRTMIN    IPCS/REXX EXEC       (CONTINUED)


     " EVALUATE ASCB+AC? LENGTH(8) CHARACTER REXX(STORAGE(JOBNAME)) "
       if rc \= 0 then
          do
            say 'EVALUATE for ASCB JOBNAME return code is' rc
            exit
          end
       else NOP

     " COMPARE ADDRESS(ASCB+AC) LE(4) WITH(VALUE(X'00000000')) "
       if rc = 0 then
       do
           jobname = 'N/A'
       end


     " EVALUATE ASCB+B0? LENGTH(8) CHARACTER REXX(STORAGE(STCNAME)) "
       if rc \= 0 then
          do
            say 'EVALUATE for ASCB STCNAME return code is' rc
            exit
          end
       else NOP

     " COMPARE ADDRESS(ASCB+B0) LE(4) WITH(VALUE(X'00000000')) "
       if rc = 0 then
       do
           stcname = 'N/A'
       end

    /*******************************************************************
    /*    ATTENTION: I TURN OFF TRACE DUE TO THE FACT THAT IPCS        *
    /*               PERFORMS THE SCREEN DISPLAY BEFORE THE @DPRTMAJ   *
    /*               RUNCHAIN HAS COMPLETED.  THIS WILL AVOID          *
    /*               MANY NASTY REXX MESSAGES AND NEGATIVE RETURN      *
    /*               CODES.                                            *
    /*******************************************************************

     trace off

     " NOTE '                                                        ' "
     " NOTE '  STARTED TASK/TSO LOGON          ===> "STCNAME"        ' "
     " NOTE '  BATCH INITIATED PROGRAM         ===> "JOBNAME"        ' "
     " NOTE '  ASID VALUE (DEC/HEX)            ===> "ASID" "ASIDX"   ' "
     " NOTE '  DISPATCHING PRIORITY (DEC/HEX)  ===> "DPRTY" "DPRTYX" ' "
     " NOTE '                                                        ' "

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #19



    FIGURE #5   OUTPUT FROM EXECUTION OF @DPRTMAJ


    %@DPRTMAJ


    ***************************************************
    *   @DPRTMAJ: LIST EACH ADDRESS SPACE DISPATCHING *
    *             PRIORITY FROM HIGHEST ASCB TO       *
    *             LOWEST ASCB.                        *
    ***************************************************
    XXX SYMBOLS DROPPED

     STARTED TASK/TSO LOGON          ===> *MASTER*
     BATCH INITIATED PROGRAM         ===> N/A
     ASID VALUE (DEC/HEX)            ===>   1 (01)
     DISPATCHING PRIORITY (DEC/HEX)  ===> 255 (FF)

     STARTED TASK/TSO LOGON          ===> PCAUTH
     BATCH INITIATED PROGRAM         ===> N/A
     ASID VALUE (DEC/HEX)            ===>   2 (02)
     DISPATCHING PRIORITY (DEC/HEX)  ===> 119 (77)

     STARTED TASK/TSO LOGON          ===> RASP
     BATCH INITIATED PROGRAM         ===> N/A
     ASID VALUE (DEC/HEX)            ===>   3 (03)
     DISPATCHING PRIORITY (DEC/HEX)  ===> 255 (FF)

     etc.
     etc.
     etc.

    125 BLOCKS PROCESSED



    FIGURE #6   OUTPUT FROM EXECUTION OF @DPRTMIN    (CONTINUED)



    %@DPRTMIN ??????.

    (where ??????. is the address of the RASP ASCB)

1

         IPCS/ESA Customization Using REXX  (DIAGRAMS)       Page #20



    FIGURE #6   OUTPUT FROM EXECUTION OF @DPRTMIN    (CONTINUED)


    ***************************************************
    *   @DPRTMIN: PERFORM ADDRESS SPACE CONTROL BLOCK *
    *             LIST OF JOBNAME/STCNAME, ASID AND   *
    *             DISPATCHING PRIORITY FOR A GIVEN    *
    *             ASCB.                               *
    ***************************************************

     STARTED TASK/TSO LOGON          ===> RASP
     BATCH INITIATED PROGRAM         ===> N/A
     ASID VALUE (DEC/HEX)            ===>   3 (03)
     DISPATCHING PRIORITY (DEC/HEX)  ===> 255 (FF)



