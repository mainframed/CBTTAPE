/* EDF   (Edit Find) ***************************************** Rexx */
/*   Created By: TRAN,C.T.                                          */
/*   When: 02/14/92                                                 */
/*                                                                  */
/*   What: EDF   is a Rexx EXEC dialog To browse/edit/LV/Delete     */
/*   the next occurence of dataset at cursor line                   */
/*   (or bring up a whole list of dsn for further processing).      */
/*   Enter EDF   at command line                                    */
/*   Then move cursor To the string To be found.                    */
/*   Helpful in browse/edit imbeded dsn without typing name         */
/*                                                                  */
/*   Option: B: Browse (default)                                    */
/*           E: Edit                                                */
/*           L: LV status of dataset instead of browse/edit         */
/*           C: Full screen display of dsnames By CATALYST          */
/*      blank : Full screen display of dsnames By Popup panel       */
/*     NOCHECK: Datasets will not be checked for existance          */
/*   By CT Tran program Products        2/92                        */
/*                                                                  */
/********************************************************************/
Address ISREDIT
"MACRO (OPT nocheck) NOPROCESS"
Trace "OFF"
Call Initialize
Call Process_main
/*Call Endpgm*/
Exit

Process_main:
   edfcmd = Left(opt,2)
   Select
      When firstlin = 1 & ccrs = 0 Then Do
         "(LASTLINE) = LINENUM .ZL"
/*       "(firstlin) = LINENUM .ZF"*/
         If ¬Datatype(firstlin,"N") Then firstlin = 1
         Call Setup_display
         Select
            When totaldsn = 0 Then Do
               zedsmsg = "No dsname(s) found"
               Address "ISPEXEC" "setmsg Msg(ISRZ000)"
               End
            When catalyst Then Do
               Call Do_catalyst
               End;
            Otherwise Call Displ_it
            End /* End of Select ... */
         End
      Otherwise Do
         If ¬Datatype(lastline,"N") Then lastline = firstlin
         Call Setup_display
         Select
            When catalyst Then Do
               Call Do_catalyst
               End;
            Otherwise Do
               zcmd = 1
               If totaldsn = 1 Then Call Browse_them
               Else Call Displ_it
               End
            End /* End of Select ... */
         End
      End  /* End of Select ... */
   Return;

Catalyst_msgs:
   dsn.0 = 7
   dsn.1 = "This list contains all the dataset names pulled from "
   dsn.2 = "dataset('"Strip(dsname,"B")"')"
   dsn.3 = "You can perform all Catalyst functions"
   dsn.4 = "against these datasets. To Exit press <Enter>"
   dsn.5 = "twice To get back To the editing session."
   dsn.6 = Copies(" ",20)"Thanks for using EDF C function"
   dsn.7 = Copies(" ",20)"A service of Program Products group"
   Do i = 1 By 1 To dsn.0
      dsn.i = "/*"Substr(dsn.i,1,60," ")"*/"
      End i;
   Return;

Add_dsn_to_table:
   If dsn = "" Then Return;
   Address "ISPEXEC" "TBADD @EDF "
   If rc = 0 Then Do
      i = i + 1
      If ¬nocheck Then Do
         If Sysdsn("'"dsn"'") ¬= "OK"  Then Do
            x = 41-Length(dsn)
            If x > 0 Then,
               dsn = dsn"<-"Substr(dash,1,x,"-")"-UNCATALOGED"
            End
         totaldsn = totaldsn+1
         dsn.i = dsn
         copyf = copyf dsn.i
         End
      End
   Return;

Setup_display:
   Address "ISPEXEC" "TBCREATE @EDF   KEYS(DSN) NOWRITE REPLACE"
   i = dsn.0
   copyf = ""
   totaldsn=0
   Do firstlin = firstlin By 1 To lastline
      "(NAME) = LINE" firstlin
      If Get_line() ¬= 0 Then Iterate
      Call Add_dsn_to_table
      End firstlin /* finished parsing the data lines */
   Return;

Get_line:
   Select
      When Left(name,2) = "/*" Then Return (4)
      When Left(name,3) = "//*" Then Return (4)
      When Pos("DSN=",name) > 0 Then Do
         Parse Var name "DSN=" dsn ","
         End
      When Pos("DSNAME=",name) > 0 Then Do
         Parse Var name "DSNAME=" dsn  ","
         End
      When clist_rexx Then Do
         Call Process_clist_rexx
         Return (4)
         End
      Otherwise Return (4)
      End /* End of Select ... */
   If Left(dsn,1) = "'" & Right(dsn,1) ¬= "'" Then,
      dsn = dsn"'" /* add ending quote */
   Return (0);

Process_clist_rexx:
   Do i = 1 By 1 To Words(name)
      dsn = Word(name,i)
      Select
         When Pos(".",dsn) > 0 Then dsn = Valid_dsname(dsn)
         When Pos("(",dsn) > 0 Then dsn = Valid_dsname(dsn)
         When Pos("'",dsn) > 0 Then dsn = Valid_dsname(dsn)
         Otherwise dsn = ""
         End /* End of Select ... */
      If dsn ¬= "" Then Do
         dsn = Strip(Translate(dsn,"","'"),"B")
         Call Add_dsn_to_table
         End
      End i;
   Return ("")

Valid_dsname:
   Arg vdsn
   Upper vdsn
   Select
      When Pos("'",vdsn) > 0 Then Do
         Parse Var vdsn "'" vdsn "'"
         vdsn = "'"vdsn"'"
         End
      When Lastpos("(",vdsn) > 0 Then Do
         Parse Var vdsn "(" vdsn ")"
         End
      Otherwise Return ("")
      End /* End of Select ... */
  vdsn = Space(vdsn,
     Substr(" )",1+(Pos("(",vdsn)>0 &,
     (Left(vdsn,1)¬="'")),1),0)
   Address "TSO"
   If Listdsi(vdsn "RECALL") ¬= 0 Then,
      If Listdsi("'"vdsn"'" "RECALL") ¬= 0 Then Return ("")
   If Pos("(",vdsn) > 0 Then,
      Parse Var vdsn "(" mem ")"
   Else mem = ""
   Return (Space("'"Sysdsname || Substr(" (",1+(Length(mem)>0),1) ||,
      mem || Substr(" )",1+(Length(mem)>0),1)"'",0))
/********************************************************************/
/*  Common routine for display a popup panel with no update.        */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/********************************************************************/
Displ_it:
/* totaldsn = Right(totaldsn,2,"0")*/
   Address "ISPEXEC"
   parms = ""
   Call ParseFl;
   cc = 0
   Do While cc = 0
      If parms = "" Then Do
         "addpop column(20) row(4)"
         "DISPLAY PANEL(EDFind)"
         cc = rc
         "rempop"
         If cc > 4 Then Return
         End
      Else Parse Upper var parms zcmd
      opt = Wordpos(edfcmd,"BR ED DE LV")
      opt = Word("BROWSE EDIT DELETE LV",opt)
      Select
         When DATATYPE(ZCMD) = "NUM" Then Do
            Call Browse_them
            End
         When zcmd = "" Then Nop
         When  Sysdsn("'"zcmd"'")  = "OK" Then Do
            dsname = zcmd
            Call Search_Browse_them
            End
         Otherwise Do  /* string To scan */
            dsname = zcmd
/*          Call ParseFl;*/
            Call Search_Browse_them
            End
         End /* End of Select ... */
      End
   Return;

ParseFl:
   Parse Upper var CopyF dsn01   dsn02  dsn03  dsn04  ,
      dsn05  dsn06  dsn07  dsn08  dsn09  dsn10 ,
      dsn11  dsn12  dsn13  dsn14 dsn15,
      dsn16  dsn17  dsn18  dsn19  dsn20 ,
      dsn21  dsn22  dsn23  dsn24 dsn25 ,
      dsn26  dsn27  dsn28  dsn29  dsn30 ,
      dsn31  dsn32  dsn33  dsn34 dsn35 ,
      dsn36  dsn37  dsn38  dsn39  dsn40,
      dsn41  dsn42  dsn43  dsn44 dsn45 dsn46 dsn47 dsn48 dsn49 dsn50,
      dsn51  dsn52  dsn53  dsn54 dsn55 dsn56 dsn57 dsn58 dsn59 dsn60,
      dsn61  dsn62  dsn63  dsn64 dsn65 dsn66 dsn67 dsn68 dsn69 dsn70,
      dsn71  dsn72  dsn73  dsn74 dsn75 dsn76 dsn77 dsn78 dsn79 dsn80
   Return

Search_browse_them:
   If Search_word() = 0 Then Call Browse_them
   Else Do
      zedsmsg = "Bad dsn selection.."
      zedlmsg = dsname "is NOT in the LIST displayed below"
      Address "ISPEXEC" "SETMSG MSG(ISRZ000)"
      End
   Return;

Endpgm:
   If subrou = "YES" Then Address ISREDIT "End "
   Return

SEARCH_Word:
  x = Pos(dsname,CopyF)
  If x > 0 Then Do
     x1 = Word(Substr(CopyF,x),1)
     zcmd = Wordpos(x1,copyf)
/*   Call browse_them */
     Return (0)
     End
   Return (4);

Browse_them:
/*
   zcmd = Right(zcmd,2,"0")
   Interpret "dsname = dsn"zcmd
*/
   dsname = Word(copyf,zcmd)
   zedsmsg = "Dataset not found"
   If ¬nocheck Then Do
      If Sysdsn("'"dsname"'")  ¬= "OK" Then Do;
         "setmsg Msg(ISRZ000)";
         Return ;
         End
      End
   Else If Pos("<-",dsname) > 0 Then Do;
      "setmsg Msg(ISRZ000)";
      Return ;
      End
   zedsmsg = edfcmd "Done ..."
   Select
      When edfcmd = "LV" Then Do
         Address "ISPEXEC" "CONTROL DISPLAY SAVE"
         Address "TSO"
/*       "CLS"*/
         "LV  '"dsname"' ALL "
         Address "ISPEXEC" "CONTROL DISPLAY RESTORE"
         End
      When edfcmd = "DE" Then Do
         Address "ISPEXEC" "CONTROL DISPLAY SAVE"
         Address "TSO"
         "DELETE  '"dsname"' ALL "
         Address "ISPEXEC" "CONTROL DISPLAY RESTORE"
         End
      When edfcmd = "BR" | edfcmd = "ED" Then Do
         Address "ISPEXEC" opt "DATASET('"dsname"')"
         If rc ¬= 0 Then Do
            zedsmsg = opt "Not done.."
            zedlmsg = opt "for "dsname "had a rc("rc").."
            End
         Else Call QF_it dsname
         End
      Otherwise Nop
      End /* End of Select ... */
   Address "ISPEXEC" "SETMSG MSG(ISRZ000)"
   Return

QF_it:
   Arg dsname;
   Address "ISPEXEC" "vget qfedit profile"
   x =  "'"dsname"'"
   If Wordpos(x,qfedit) = 0 Then Do
      qfedit = "'"dsname"'" qfedit
      Address "ISPEXEC" "vput qfedit profile"
      End
   Return

Do_catalyst:
   Address "TSO"
   "ALLOC FI(DD1) DA(CNTL(@DIR)) SHR REUSE "
   "EXECIO * DISKW DD1   (STEM DSN. FINIS  "
   "FREE FI(DD1) "
   ppdsn2 = "Y";
   lpt3 = "Y";
   ftm = "";
   pdsn2="CNTL(@DIR)"
   Address "ISPEXEC"
   "VPUT (PPDSN2 LPT3 FTM pdsn2) PROFILE "
   Call  Catalyst
   Return

Initialize:
   "PROCESS RANGE X"
   "(rcmd) = RANGE_CMD"
   Select
      When rcmd = "X" Then Do
         "(firstlin) = Linenum .ZFRANGE"
         "(lastline)  = Linenum .ZLRANGE"
         End
      Otherwise Do
         "(LCRS CCRS) = CURSOR"
         "(NAME) = LINE .ZCSR"
         firstlin = lcrs
         End
       End /* End of Select ... */
   If opt = ""  & nocheck = "" Then Do
      subrou  =  "YES"
      Address "ISPEXEC" "VGET (opt nocheck ) SHARED  "
      End
   Address "ISPEXEC" "CONTROL ERRORS Return"
   nocheck = 0
   Upper   opt
   Upper   nocheck
   If Abbrev(nocheck,"NO",2) > 0 Then nocheck = 1
   opt = Left(Opt,1)
   opt# = Wordpos(opt,"B C E L") + (Wordpos(opt,"B C E L") = 0)
   opt  = Word("BROWSE C EDIT LV",opt#)
   "(DSNAME) = DATASET"
   "(MEM)    = MEMBER "
   dsname_w = Translate(dsname,"",".")
   crnames = "CLIST REXX SYSPROC SYSEXEC PDFCLIB ISPCLIB"
   clist_rexx = 0+(Wordpos(Word(dsname_w,Words(dsname_w)),crnames)>0)
   If mem ¬= "" & mem ¬= "MEMBER" Then
      dsname = dsname"("Space(mem,0)")"
   If opt = "C" Then Do
      catalyst = 1
      Call Catalyst_msgs
      End
   Else Do
      dsn. = ""
      dsn.0 = 0
      catalyst = 0
      End
/*
   If  opt = "L"            Then opt = "LV "
   Else If  opt =  "C" Then opt = "C"
   Else If opt = "E"  Then opt = "EDIT "
   Else opt = "BROWSE "
*/
   x = Msg(OFF)
   dash = "----"
   Return;
