/* G2JDate date ********************************************** REXX */
/* Convert Gregorian date To Julian date                            */
/* Program By CT Tran Program Products    X4506          4/91       */
/* This is a Rexx function executable as                            */
/*  Jdate = g2jdate(12/12/91)   The julian YYDDD is   in Jdate      */
/* Similar function: J2Gdate                                        */
/********************************************************************/
/*   Modified By: IAPPINI,R.                                        */
/*   When: 09/23/92                                                 */
/*   What: Added Return(0) if an invalid date is specified.         */
/*                                                                  */
G2JDate:
Parse Arg edate
date = Translate(edate," ","/.-"," ")
Parse Var date mo da yr

If mo = '' | da = '' | yr = '' |,
   mo = 0  | da = 0            Then Do
   Say "date("edate") must be in form MM/DD/YY",
       "and must contain non-zero values for mm and dd."
   Return(0);
   End

x = "0 31 59 90 120 151 181 212 243 273 304 334 365"
xyr = Right(yr,2)
days_in_month = Word(x,mo+1) - Word(x,mo) + (xyr-xyr%4*4 = 0 & mo = 2)
If da > days_in_month Then Do
   Say edate "Invalid; the day, "da", is greater than the month's,",
       days_in_month'.'
   Return (0);
   End;
day = da  + Word(x,mo) + (xyr-xyr%4*4 = 0 & mo > 2)

Return (yr || Right(day,3,'0'))
