/* Center **************************************************** Rexx */
/*   Revised By: IAPPINI,R. (from Share I184)                       */
/*   When: 07/08/93                                                 */
/*   What: Center, an Edit macro, CENTERs a specified range of text */
/*         within a text dataset.                                   */
/********************************************************************/
Address "TSO"
Trace "OFF"
Address "ISREDIT"
"MACRO (HELP) NOPROCESS"
Address "ISPEXEC"
"CONTROL ERRORS RETURN"
Address "ISREDIT"
/* Process line commands, check if C was specfied */
"PROCESS RANGE C"
lcc = rc
"(rch) = Range_cmd"
If lcc > 0 | rch ¬= "C" Then Do
   If lcc = 16 Then zedsmsg = "Conflicting line commands"
   Else zedsmsg = "No range specified"
   zedlmsg = "You must specify a range of lines",
             "to be centered; Use 'Cn' or 'CC'"
   Address "ISPEXEC"
   "SETMSG MSG(ISRZ001)"
   Exit 12
   End;
/* Centering program logic       */
Address "ISREDIT"
"(fr) = Linenum .zfrange"
"(lr) = linenum .zlrange"
"(lb,rb) = Bounds"
/* ***********************************************************
=  any character                   .  invalid characters
@  alphabetic characters           -  non-numeric characters
#  numeric characters              <  lower case alphabetics
$  special characters              >  upper case alphabetics
¬  non-blank characters
   *********************************************************** */
Do i = fr By 1 To lr
   "Label "i "= .LJ"
   "SEEK P'¬' .lj .lj" lb rb "FIRST"
   "(lline,lcol) = Cursor"
   "SEEK P'¬' .lj .lj" lb rb "LAST"
   "(rline,rcol) = Cursor"
   midpt = (rb-lb)%2+lb
   left = midpt-lcol
   right = rcol - midpt
   count = (left-right+1)%2
   If count > 0 Then "SHIFT ) "i count
   Else              "SHIFT ( "i Abs(count)
   If i = fr Then "Label "i" = .LJX"
   End i;
"SEEK P'=' .ljx .ljx FIRST"
Exit 0
