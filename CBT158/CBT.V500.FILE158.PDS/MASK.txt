/* MATCH ***************************************************** Rexx */
/*   Created By: IAPPINI,R.                                         */
/*   When: 02/17/93                                                 */
/*   What: MATCH, a REXX Exec, is an EDIT macro copied from ESJ,    */
/*         February 1993, that matches braces and parenthesises     */
/********************************************************************/
Address "TSO"
Trace "OFF"
Arg p1
Parse Source environment function exec_name whatdd whatdsn exec_cur,
   env_default env_type supplied_name
"SUBCOM ISPEXEC" /* Is ISPF active? */
If rc ¬= 0 Then Do
   Say exec_name "is an ISPF edit macro --- ISPF is not active"
   Exit 1
   End
Address "ISPEXEC"
"CONTROL ERRORS RETURN"
Address "ISREDIT"
"MACRO"
If rc ¬= 0 Then Do
   Say exec_name "is an ISPF edit macro --- edit is not active"
   Exit 1
   End
"(crow,ccol) = CURSOR"      /* Get current cursor position */
If ccol = 0 Then Exit       /* in command or prefix area */
"CURSOR = "crow ccol        /* Set default cursor position */
"(CLINE) = LINE" crow       /* Get current line         */
char = Substr(cline,ccol,1) /* get character under cursor */
nest = 0                    /* nesting level               */
inc  = 0                    /* search direction/increment  */
/* Set the target character and search direction           */
sources  = "{(})"
targets  = "}){("
char_ndx = Pos(char,sources)
If char_ndx = 0 Then Do
   Say char "is not a '"sources"'"
   Exit
   End
target   = Substr(targets,char_ndx,1)
If char_ndx < 3 Then inc =  1 /* "NEXT" */
Else                 inc = -1 /* "PREV" */
/* Scan until end of file reached            */
Do i_row = 1 By 1 Until rc ¬= 0
   /* Scan from current column to ending column */
   If inc = 1 Then ecol = length(cline)
   Else ecol = 1
   Do i = ccol + 1 By inc to ecol
      char2 = Substr(cline,i,1)
      Select
         When char2 = char Then nest = nest + 1 /* nested chars */
         When char2 = target Then Do
            If nest = 0 Then Do                 /* nested?      */
               "CURSOR = "crow i                /* No!          */
               "XSTATUS "crow "= NX"
               Exit
               End
            Else nest = nest - 1         /* Yes, decrement nest */
            End
         Otherwise Nop
         End /* End of Select ... */
      End i
   crow = crow + inc                     /* Adjust row number   */
   "(CLINE) = LINE" crow                 /* get next line       */
   If rc ¬= 0 Then Leave
   /* Set new "current" column           */
   If inc = 1 Then ccol = 1       /* Forward scan */
   Else ccol = Length(cline)      /* Backward scan*/
   End i_row
Exit 0
