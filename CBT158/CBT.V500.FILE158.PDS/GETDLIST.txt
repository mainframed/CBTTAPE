/* Getdlist ************************************************** Rexx */
/*   Created By: IAPPINI,R.                                         */
/*   When: 02/22/93                                                 */
/*   What: Getdlist, a REXX exec, returns via the RESULT variable   */
/*         datasets cataloged by a generic qualifier.               */
/*  Input: stem_name and a dataset qualifier (q1.q2*.g3*)           */
/*                                                                  */
/*                                                                  */
/********************************************************************/
Getdlist:
Address "TSO"
Trace "OFF"
If Arg() > 1 Then Do
   p1 = ""
   Do i = 1 By 1 To Arg()
      p1 = p1 Arg(i)
      End i
   End
Else Arg p1
proc_stat = "PROC 2 STEM_NAME QUALIFIER"
options = "FULLSCREEN"
Call Proctso p1,proc_stat options
Interpret result
Call Process_main
If fullscreen ¬= "" Then Do
   Interpret get_list
   Call Stemdisp "BROWSE",stem_name,,,"Dataset list for",
      "Qualifier("qualifier")" Date() Time()
   Exit
   End
Else Return (get_list)

Process_main:
   Parse Source environment function exec_name whatdd whatdsn exec_cur,
      env_default env_type supplied_name
   If Sysvar(Sysispf) ¬= "ACTIVE" Then Do
      "ISPF CMD(%"exec_name p1")"
      Exit
      End

   Address ISPEXEC
   Call Get_member_list
   Return;

Get_member_list:
   If Right(qualifier,1) ¬= "*" Then qualifier = qualifier"*"
   stem_name = Strip(Translate(stem_name,"","'"""),"B")
   If Right(stem_name,1) ¬= "." Then stem_name = stem_name"."
   "LMDINIT LISTID(DD1ID) LEVEL("qualifier") "
   lastcc = rc
   dsn=""
   ndsns = 0
   get_list = ""
   "LMDLIST LISTID("DD1ID") STATS(NO) OPTION(LIST) DATASET(DSN)"
    lastcc = rc
   Do i = 1 By 1 While lastcc = 0
       get_list = get_list stem_name || i" = '"dsn"';"
      "LMDLIST LISTID("DD1ID") STATS(NO) OPTION(LIST) DATASET(DSN)"
       lastcc = rc
       End i /* end rc  */
   get_list = get_list stem_name || "0 = "i-1";"
   "LMDFREE LISTID("DD1ID") "
   Return
