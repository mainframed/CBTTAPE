/* DSNBYDD *************************************************** Rexx */
/*   Created by: IAPPINI,R.                                         */
/*   When: 11/20/92                                                 */
/*   What: DSNBYDD is a Rexx EXEC dialog that creates a PS dataset  */
/*         containing a list of datasets concatenated to a ddname.  */
/*         The PS dataset is named with the ddname name.            */
/*                                                                  */
/********************************************************************/
Address 'TSO'
Arg p1
Call Initialize
If debug ¬= "" Then Trace ?i
Call Process_dds
Call Eoj
Exit

Process_dds:
   Do i_dd = 1 By 1 To Words(ddnames)
      v_ddname = Word(ddnames,i_dd)
      "DDCURALT "v_ddname "DSNLIST"
      If rc = 0 Then Call Create_dd_dataset_list
      Else Call Say_msg v_ddname "not allocated."
      Call Say_msg " "
      End i_dd;
   Return;

Create_dd_dataset_list:
   dsnlist = Space(Translate(dsnlist,"","'"))
   xx = Listdsi(v_ddname)
   If xx ¬= 0 Then,
      "ALLOC DA("v_ddname") LRECL(80) RECFM(F B)",
         "BLKSIZE(6160) DSORG(PS)"
   "ALLOC FI(VDDNAME) DA("v_ddname") OUTPUT REUSE"
   dsn. = ""
   Call Say_msg "Dataset("v_ddname") updated, "Date()"," Time()"."
   Do i_vdd = 1 By 1 To Words(dsnlist)
      dsn.i_vdd = Strip(Word(dsnlist,i_vdd),"B")
      Call Say_msg "   "dsn.i_vdd
      End i_vdd
   "EXECIO * DISKW VDDNAME (STEM DSN. FINIS)"
   "FREE FI(VDDNAME)"
   Return;

Eoj:
   If fullscreen Then Call Stemdisp "BROWSE","say.",,,"DD datasets list"
   Return;

Say_msg:
   Parse Arg msg
   If ¬fullscreen Then Say msg
   say_cnt = say_cnt + 1
   say.say_cnt = msg
   Return

Initialize:
   Parse Source environment . exec_name .
   proc_stmt = "PROC 1 DDNAMES DEBUG FULLSCREEN"
   Call Proctso p1,proc_stmt
   Interpret result
   If rc ¬= 0 Then Do
      Say proctso_msg
      Say "Valid options are: "Subword(proc_stmt,3)
      Say "Enter the correct options for "exec_name"."
      Exit
      End
   say_cnt = 0
   If fullscreen = "" Then fullscreen = 0
   Else                    fullscreen = 1
   ddnames = Strip(Translate(ddnames,"","()"),"B")
   Return
