/* REXX ** REXX ** REXX ** REXX ** REXX ** REXX ** REXX ** REXX */
/**************************************************************/
/*                                                            */
/*   CREATED BY: IAPPINI,R.                                   */
/*         WHEN: 08/08/90                                     */
/*         WHAT: UTILITY TO EMPTY AN ISPF TABLE               */
/*                                                            */
/*                                                            */
/**************************************************************/
ADDRESS 'TSO'
ARG PARMS
XPROC = 'PROC 2 TABLE_DATASET_NAME TABLE_NAME'
CALL PROCTSO PARMS,XPROC
INTERPRET RESULT
/**/
if rc ¬= 0 then do
   say proctso_msg
   say cmd_area', terminating SORTD.'
   exit
   end
TDSN = TABLE_DATASET_NAME
TNAM = TABLE_NAME
select
   when length(tdsn) ¬= 0 & length(tnam) ¬= 0 then nop
   otherwise do
      say 'A table_dataset_name AND table_name must be entered.'
      say 'Please re-enter:',
          'TABEMPTY table_dataset_name table_name'
      exit
      end
   end /* End of SELECT ... */
call validate_dataset STRIP(TRANSLATE(TDSN,' ',"'"))"("TNAM")"
if result = '' then exit
parse var result tdsn tnam
"ALLOC FI(SB1) DA("TDSN") SH REUSE"
ADDRESS 'ISPEXEC'
"LIBDEF SB1 LIBRARY ID(SB1)"
"TBOPEN "TNAM "LIBRARY(SB1) WRITE"
"TBSTATS "TNAM" ROWCURR(QROWS)"
"TBTOP "TNAM
DO I = 1 BY 1 TO QROWS
   "TBSKIP "TNAM
   "TBDELETE "TNAM
   END I
"TBCLOSE "TNAM "LIBRARY(SB1)"
"LIBDEF SB1"
ADDRESS "TSO" "FREE FI(SB1)"
EXIT
/**************************************************************/
/*       Subroutines                                          */
/**************************************************************/
validate_dataset:
ARG vdataset
parse var vdataset dsn '(' member ')'
SELECT
   WHEN vdataset = '*' THEN return ""
   WHEN SYSDSN(vdataset) = 'OK' THEN NOP
   OTHERWISE do
      vdataset = "'"vdataset"'"
      Xs = SYSDSN(vdataset)
      IF xs ¬= 'OK' THEN DO
         SAY "Invalid Table specified("vdataset");"xs,
             ';terminating TABEMPTY.'
         EXIT
         END
      END
   END /* END OF SELECT ... */
if substr(vdataset,1,1) = "'" then dsn = "'"dsn"'"
x = listdsi(dsn)
if member = '' & sysdsorg = 'PO' then do
   say 'The Table dataset('vdataset')',
       'is a PDS, please specify a member; terminating TABEMPTY.'
    exit
   end
return (dsn member)
