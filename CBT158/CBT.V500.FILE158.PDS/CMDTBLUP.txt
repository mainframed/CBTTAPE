/* CMDTBLUP*************************************************** Rexx */
/*   Created by: IAPPINI,R.                                         */
/*   When: 12/09/92                                                 */
/*   What: CMDTBLUP is a Rexx EXEC dialog process ISPF command      */
/*         tables.                                                  */
/********************************************************************/
Trace OFF
Address "TSO"
Arg p1
Parse Source environment . exec_name .

If Sysvar(Sysispf) ¬= "ACTIVE" Then Do
   "ISPF CMD(%"exec_name p1")"
   Exit
   End

If Words(p1) = 0 Then Do
   Say "You must enter the NAME of the table to be processed."
   Exit
   End

Call Get_tbl_data
If rc = 0 Then Do
   Call Process_tbl_data
   Call Rebuild_tbl
   End;
Exit

Process_tbl_data:
   Call Build_temp_tbl
   spannam = pannam
   pannam  = "PSICMDIS"
   Address "ISPEXEC"
   zscan = "ALL"
   "VPUT (ZSCAN)"
   "TBTOP   " tblnam
   "TBDISPL " tblnam "PANEL("pannam")"
   tbdcc = rc
   pannamd = "PSICMIN1"
   Call Make_selection
   pannam = spannam
   names  = snames
   tblnam = stblnam
   keys   = ""
   Return

Make_selection:
   Do i_tbdisp = 1 By 1 While tbdcc < 8
      Call Process_options
      Select;
         When ztdsels > 1 Then Do
            "TBDISPL " tblnam
            End
         Otherwise Do
            "TBDISPL " tblnam "PANEL("pannam")"
            tbdcc = rc
            End
         End /* End of Select ... */
      End i_tbdisp
   Return

Process_options:
   se_cols = Space(zfd1 zfd2 zfd3)
   If zsz = "" Then zsz = Left(zcmd,1)
   Select;
      When se_cols ¬= "" Then Do
         If zcmd = "" Then zcmd = "SEARCH "
         /* ZCTVERB   +ZCTTRUNC  +ZCTACT  */
         If zfd1 ¬= "" Then zcmd = Word(zcmd,1) "ZCTVERB("zfd1")"
         If zfd2 ¬= "" Then zcmd = Word(zcmd,1) "ZCTTRUNC("zfd2")"
         If zfd3 ¬= "" Then zcmd = Word(zcmd,1) "ZCTACT("zfd3")"
         Call Position_table
         End
      When Pos(zsz,"BIS") \= 0 Then Do
         "DISPLAY PANEL("pannamd")"
         End
      When Pos(zsz,"CM") \= 0 Then Do
         "DISPLAY PANEL("pannamd")"
         sortf = Word(zctact,1)
         "TBMOD "tblnam "ORDER"
         zedsmsg = zctverb "modified"
         zedlmsg = ""
         "SETMSG MSG(ISRZ000)"
         End
      When Pos(zsz,"D") \= 0 Then Do
         "TBDELETE "tblnam
         zedsmsg = zctverb "deleted"
         zedlmsg = ""
         "SETMSG MSG(ISRZ000)"
         End
      When Pos(zsz,"R") \= 0 Then Call Position_table
      When Pos(zsz,"A") \= 0 Then Do
         "DISPLAY PANEL("pannamd")"
         sortf = Word(zctact,1)
         "TBADD "tblnam "ORDER"
         zedsmsg = zctverb "added"
         zedlmsg = ""
         "SETMSG MSG(ISRZ000)"
         End
      Otherwise Do
         zedsmsg = zsz "invalid"
         zedlmsg = "Valid options are Browse, Inquiry, Select,",
                   "Change, Modify, Delete, Add."
         "SETMSG MSG(ISRZ000)"
         End
      End; /* End of Select ... */
   Return

Position_table:
   Parse Var zcmd function value
   Address "ISPEXEC"
   zscan  = "SCAN"
   If Right(value,1) = ")" Then,
      Parse Var value column  "(" value ")" .
   Select
      When Pos(Left(function,1),"FL") > 0 Then Do
         zctverb = value"*"
         "TBSCAN "tblnam "ARGLIST("column") CONDLIST(GE)"
         End
      When Abbrev("SEARCH",function) Then Do
         "TBVCLEAR "tblnam
         namecond = ""
         Do i_v = 1 By 1 To Words(value)
            w_value = Strip(Word(value,i_v),"B")
            If Right(w_value,1) = ")" Then,
               Parse Upper Var w_value varname "(" varvalue ")" .
            Else varvalue = ""
            Select
               When varvalue ¬= "" Then Do
                  namecond = namecond","varname",EQ"
                  xx = varname" = '"varvalue"*'"
                  Interpret xx
                  End
               Otherwise Do
                  namecond = ",ZCTVERB,EQ"
                  zctverb = value"*"
                  End
               End /* End of Select ... */
            End i_v
         namecond = Right(namecond,Length(namecond)-1)
         "TBSARG "tblnam "NAMECOND("namecond")"
         "TBTOP "tblnam
         End
      When Abbrev("RESET",function) Then Do
         "TBVCLEAR "tblnam
         zctverb = value"*"
         "TBSARG "tblnam "NAMECOND(ZCTVERB,EQ)"
         "TBTOP "tblnam
         End
      Otherwise Do
         zedsmsg = "Invalid command"
         zedlmsg = function "is invalid; valid commands are:",
            valid_primary_commands"."
         "SETMSG MSG(ISRZ000)"
         End
      End /* End of Select ... */
   Return;

Rebuild_tbl:
   Call Alloc_isptabl
   Address "ISPEXEC"
/*
   "TBSTATS "tblnam "CDATE(CDATNAME)    CTIME(CTIMNAME)    ",
            "UDATE(UDATNAME)    UTIME(UTIMNAME)    ",
            "USER(USERNAME)     ROWCREAT(RCRTNAME) ",
            "ROWCURR(RCURNAME)  ROWUPD(RUPDNAME)   ",
            "TABLEUPD(TUPDNAME) SERVICE(SERVNAME)  ",
            "RETCODE(RETCNAME)  STATUS1(STA1NAME)  ",
            "STATUS2(STA2NAME)  STATUS3(STA3NAME)  "
   Say       tblnam "CDATE("CDATNAME") CTIME("CTIMNAME")  ",
            "UDATE("UDATNAME")  UTIME("UTIMNAME")  ",
            "USER("USERNAME")   ROWCREAT("RCRTNAME") ",
            "ROWCURR("RCURNAME") ROWUPD("RUPDNAME") ",
            "TABLEUPD("TUPDNAME") SERVICE("SERVNAME") ",
            "RETCODE("RETCNAME") STATUS1("STA1NAME") ",
            "STATUS2("STA2NAME") STATUS3("STA3NAME") "
  Say "TBSTATS rc("rc")"
*/
   "TBERASE " tblnam           /* Delelet original table */
   "TBCREATE" tblnam "KEYS("keys") NAMES"names
   If rc \= 0 Then Do
/*    Say "TBOPEN rc("rc") *** Error for table("tblnam")."
*/    zedsmsg = "Table error";
      zedlmsg = "Table("tblnam") can not be created."
      "SETMSG MSG(ISRZ001)"
      rc = 4;
      Return;
      End
/* "TBSORT  " tblnam "FIELDS(ZCTACT,C,A ZCTVERB,C,A)"
*/
   "TBTOP  TEMPTBL" /* Position CRP to 1st row */
   "TBSKIP TEMPTBL"         /*                         */
   tbgrc = rc

   Do While tbgrc = 0;
      "TBADD "tblnam "MULT("rownum") ORDER"
      "TBSKIP TEMPTBL"
      tbgrc =rc;
      End;
   "TBCLOSE "tblnam
   "TBCLOSE  TEMPTBL"
   Address "TSO" "FREE FI(ISPTABL)"
   Return

Alloc_isptabl:
   /* Determine if the member exists in the ISPTLIB allocation */
   zz = Outtrap(cmdt.)
   xx = Membydd(tblnam "ISPTLIB")
   zz = Outtrap("OFF")
   If xx ¬= 0 Then Do
      Say "Target table not pre-allocated."
      Say "Terminating "exec_name"."
      Exit
      End
   Parse Var cmdt.1 "(" atblnam ")" . "(" adsnam ")" .
   Address "TSO" "ALLOC FI(ISPTABL) DA('"adsnam"') SH REUSE"
   Return

Get_tbl_data:
   tblnam = Word(p1,1)  /* First entry is the TABLE name*/
   pannam = cmdtbldi
   appldesc = "Table "tblnam
   entries = "Add Browse Change Delete"
   Address "ISPEXEC"
   "TBOPEN " tblnam  "NOWRITE"
   If rc \= 0 Then Do
/*    Say "TBOPEN rc("rc") *** Error for table("tblnam")."
*/    zedsmsg = "Table error";
      zedlmsg = "Table("tblnam") can not be opened."
      "SETMSG MSG(ISRZ001)"
      rc = 4;
      Return
      End
   "TBQUERY" tblnam "KEYS(KEYS) NAMES(NAMES) ROWNUM(ROWNUM)"
   If rc \= 0 Then Do
/*    Say "TBQUERY rc("rc") **** Error for table("tblnam")."
*/    zedsmsg = "Table error";
      zedlmsg = "TBQUERY rc("rc") table("tblnam")."
      "SETMSG MSG(ISRZ001)"
      rc = 4;
      Return;
      End
   xnames = Substr(names,2,Length(names)-1)
/* Say "KEYS("keys") NAMES("xnames") ROWNUM("rownum")"
*/
   "TBTOP " tblnam
   "TBSKIP" tblnam
   "TBGET " tblnam /*"SAVENAME(XNAMES)"*/
   tbgcc = rc
   Do i_tbget = 1 By 1 While tbgcc = 0
      sort_fld = Word(zctact,1)
      Select
         When sort_fld = "ALIAS" Then sort_fld = ""
         Otherwise Nop
         End /* End of Select ... */
      tbget.i_tbget = Left(sort_fld,7)Left(zctverb,9),
                      Left(zcttrunc,3)Left(zctact,50)Left(zctdesc,60)
      "TBSKIP" tblnam
      "TBGET "tblnam /*"SAVENAME(XNAMES)" */
      tbgcc = rc
      End i_tbget;
   tbget.0 = rownum
   "TBCLOSE " tblnam  /* Close input table */
   Return

Build_temp_tbl:
   Address "ISPEXEC"
   stblnam = tblnam  /* Save this table name */
   tblnam  = "TEMPTBL"
   snames  = names   /* Save Names of variables */
   keys    = "(SORTF ZCTVERB)"
   names   = "(ZCTTRUNC ZCTACT ZCTDESC)"
   "TBCREATE "tblnam "NOWRITE KEYS"keys "NAMES"names
   "TBSORT "tblnam "FIELDS(SORTF,C,A ZCTVERB,C,A)"
   If rownum = 0 Then rownum = 1
   mult = "MULT("rownum")"
   Do i_tbadd = 1 By 1 While i_tbadd <= rownum
      Parse Var tbget.i_tbadd sortf +7 zctverb +9 zcttrunc +3 zctact,
                +50 zctdesc +60 .
     sortf    = Space(sortf)
     zctverb  = Space(zctverb)
     zcttrunc = Space(zcttrunc)
     zctact   = Space(zctact)
     zctdesc  = Space(zctdesc)
     "TBADD "tblnam mult "ORDER"
     mult = ""
     End i_tbadd
   Return
