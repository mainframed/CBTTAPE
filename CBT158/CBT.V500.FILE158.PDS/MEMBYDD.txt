/* MEMBYDD *************************************************** Rexx */
/*   Modified by: IAPPINI,R.                                        */
/*   When: 01/28/93                                                 */
/*   What: Added DATA option to return a string of DSN(member)      */
/*         Changed list of member ddname processing                 */
/********************************************************************/
/* MEMBYDD *************************************************** Rexx */
/*   Created by: IAPPINI,R.                                         */
/*   When: 12/09/92                                                 */
/*   What: MEMBYDD, a REXX exec, will determine if a member is      */
/*         in a specified ddname.                                   */
/*                                                                  */
/* Input is entered in pairs, ie., member ddname |                  */
/*   (member member ... member) ddname                              */
/********************************************************************/
Membydd:
Address 'TSO'
Trace "OFF"
Arg p1
Call Initialize
Call Process_main
If data Then,
   Return ("rc="result";ret_data='"ret_data"';")
Else Return (result)

Process_main:
   xx = Get_ddname_list()
   If xx ¬= 0 Then Do
      If data Then Return("rc = 24;") /* DDname not allocated */
      Else Return(xx)
      End
   Call Process_member_list
   Return (at_least_one_member_found)

Get_ddname_list:
   Do i_ddndx = 1 To Words(ddname)
      ddn = Word(ddname,i_ddndx)
      Call Listdd ddn "DSNS"
      Interpret result
      If rc ¬= 0 Then Do
         If ¬quiet Then Say "DDname("ddn") is not allocated"
         Return (4)
         End
      End i_ddndx
   Return (0);

Process_member_list:
   at_least_one_member_found = 4
   Do i_memndx = 1 To Words(member_list) /* loop thru member_list */
      found_member = 0
      Call Process_sysdsn_output Word(member_list,i_memndx)
      If ¬found_member Then,
         If ¬quiet Then,
            Say "Member("member") not in DDname("ddn")."
      End i_memndx
   Return;

Process_sysdsn_output:
   Arg pso_member
   Do i_dsndx = 1 To Words(dsns)
      dsn = Word(dsns,i_dsndx)
      zz = Sysdsn("'"dsn"("pso_member")'")
      If zz = "OK" Then Call Display_member_msg
      End i_dsndx
   Return;

Display_member_msg:
   If data Then,
      ret_data = Space(ret_data,
         Substr(dsn,1,Length(dsn))"("pso_member")")
   If ¬quiet Then,
      Say "Member("pso_member") is in DSname("dsn"),",
          "of DDname("ddn"("i_dsndx"))."
   found_member = 1
   at_least_one_member_found = 0
   Return;

Initialize:
   p1 = Space(p1)
   proc_stmt = "PROC 2 MEMBER DDNAME QUIET DATA"
   Call Proctso p1,proc_stmt
   Interpret result
   member_list = Strip(Translate(member,"","(',)"),"B")
   If ddname = "" Then Do
      Say "Invalid input was entered."
      Say "You entered "p1"."
      Say "You must enter a member ddname or member list ddname",
          "combination for this procedure."
      Exit
      End
   If quiet = "" Then quiet = 0
   Else quiet = 1
   If data = "" Then data = 0
   Else data = 1
   ret_data = ""
   Return;

/*  SYSDSN return values
 OK                            /* data set exists */
 MEMBER NOT FOUND
 MEMBER SPECIFIED, BUT DATA SET IS NOT PARTITIONED
 DATA SET NOT FOUND
 ERROR PROCESSING REQUESTED DATA SET
 PROTECTED DATA SET            /* RACF denied access to data set */
 VOLUME NOT ON SYSTEM
 INVALID DATA SET NAME
 MISSING DATA SET NAME
 UNAVAILABLE DATA SET
*/

/* messages from WHATLIB used to recreate MEMBYDD */
 Member(CMAPRTRS) is in DSname('BI21RI.TABLES'), of DDname(ISPTLIB(0)).
 Member(CMAPRTRS) is in DSname('BI21RI.TABLES'), of DDname(ISPTLIB(4)).
 *** Member(PSFPRTRS) not found in DDname(ISPTLIB).
 Member(ISPCMDS) is in DSname('BI21RI.TABLES'), of DDname(ISPTLIB(0)).
 Member(ISPCMDS) is in DSname('SPF22.SYS.PDFTLIB'), of DDname(ISPTLIB(2)
 Member(ISPCMDS) is in DSname('BI21RI.TABLES'), of DDname(ISPTLIB(4)).
 Member(ISPCMDS) is in DSname('SPF22.PDFTLIB'), of DDname(ISPTLIB(8)).
*/

/*  LISTD output
 BI21RI.TABLES
 --RECFM-LRECL-BLKSIZE-DSORG
   FB    80    6160    PO
 --VOLUMES--
   780018
 --MEMBER---TTR----ALIAS-TTRN-CNT-DATA
   CMAPRTRS 009405 NO    0    00
*/

