/* REXX * REXX * REXX * REXX * REXX * REXX */
Address TSO
arg p1
cmd_area = ,
"PROC 2 FROMQUALIFIER TOQUALIFIER DEBUG NUMBEROFDSNS(500) NOCOPY "
call proctso p1,cmd_area
interpret result
if Length(NOCOPY) > 0 then NOCOPY = 1      /* true */
else                         NOCOPY = 0    /* false*/
if debug ¬= '' then trace r
#digits = length(numberofdsns)
#from = length(fromqualifier)+1
#to   = length(toqualifier)  +1
x = outtrap(lv.,numberofdsns)
"LV '"fromqualifier"'"
X=OUTTRAP('OFF')
loop = lv.0
lqmx = ''
DO I = 1 BY 1 WHILE i <= loop
   lqmx = length(space(lv.i)) lqmx
   end i
#from = #from + rexxmax(lqmx)
DO I = 1 BY 1 WHILE i <= loop
   lowquals = LV.I
   select
      when wordpos("LV "fromqualifier, lowquals) >0 then nop
      when WORDPOS('LOCATE ERROR CODE =' ,lowquals) > 0 THEN DO
         SAY 'COPYMORE - Could not LOCATE 'fromqualifier' datasets.'
         EXIT
         END
      otherwise do
         lowquals = space(lowquals)
         Y = TOQUALIFIER"."lowquals
         lowquals = FROMQUALIFIER"."lowquals
         SELECT
            WHEN NOCOPY then DO
               j = right(i,#digits)
               say J' 'left(lowquals,#from)' -> 'y
               END /* End of when ........ */
            OTHERWISE DO
               "COPY '"lowquals"' '"Y"'"
               if rc ¬= 0 then,
                  say "COPYMORE - failed:" left(lowquals,#from) y
               end /* End of otherwise ... */
            end    /* End of Select ...... */
         end       /* End of otherwise ... */
      end          /* End of SELECT ...... */
   end i           /* End of DO I ........ */
/**************************************************************/
/*                                                            */
/*                                                            */
/*         This procedure will COPY starting with a Generic   */
/*         FROMQUALIFIER to a Generic TOQUALIFIER             */
/*                                                            */
/*    BY: R. IAPPINI                                          */
/*  DATE: 08/02/88                                            */
/*                                                            */
/*                                                            */
/*                                                            */
/*                                                            */
/**************************************************************/
