/* COPYNREC*************************************************** Rexx */
/*   Created By: IAPPINI,R.                                         */
/*   When: 03/24/93                                                 */
/*   What: COPYNREC will copy n records from file1 to file2.        */
/*                                                                  */
/********************************************************************/
Trace "OFF"
Address 'TSO'
Parse Source environment . exec_name .
Arg p1
proc_stmt = "PROC 3 FILE1 FILE2 NUMBEROFRECORDS SKIP()"
Call Proctso p1,proc_stmt
Interpret result
If rc ¬= 0 Then Do
   Say proctso_msg
   Say "Valid options are: "Subword(proc_stmt,3)
   Say "You entered: "p1
   Say "Re-enter "exec_name "with the correct options."
   Exit
   End
xx = Sysdsnri(file1)
Interpret xx
If rc ¬= 0 Then Do
   Say file1 "is not Cataloged, terminating "exec_name
   Exit
   End
xx = Sysdsnri(file2)
Interpret xx
If rc ¬= 0 Then,
   file_options = "LIKE("file1") REUSE"
Else,
   file_options = "SH REUSE"
"ALLOC FI(IF) DA("file1") SH REUSE"
"ALLOC FI(OF) DA("file2")" file_options
Say "Copying "numberofrecords "records from" file1 "to" file2"."
"DELSTACK"
Select
   When skip = ""  Then,
      "EXECIO "numberofrecords" DISKR  IF (FINIS)"
   Otherwise Do
      "EXECIO "skip" DISKR  IF "
      "DELSTACK"
      "EXECIO "numberofrecords" DISKR  IF (FINIS)"
      End
   End /* End of Select ... */
Queue ""
"EXECIO * DISKW  OF (FINIS)"
Say exec_name "has finished copying "numberofrecords "records from",
   file1 "to" file2"."
"DELSTACK"
"FREE FI(IF,OF)"
Exit
