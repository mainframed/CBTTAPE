ISREDIT  MACRO (APARM)
/*  MACRO NAME : SKELCHK                                            */
/*    FUNCTION : IDENTIFY ISPF SKELETON ")SEL" AND ")ENDSEL"        */
/*               MISMATCHES. IF NO MISMATCHES EXIST, ALL            */
/*               ")SEL-)ENDSEL" PAIRS WILL BE MATCHED AND           */
/*               IDENTIFIED WITH A COMMENT IN COLUMNS 55-71.        */
/*      SYNTAX : SKELCHK                                            */
/*               SKELCHK HELP OR H                                  */
/*       DEBUG : SKELCHK LIST                                       */
/*      AUTHOR : GARY FRICKEY (NASPA PUBLICATION : TECHNICAL        */
/*               SUPPORT, VOLUME 6, NUMBER 4, APRIL 1992).          */
/*               INSTALLED BY : MOINIL P.A.                         */
         ISPEXEC CONTROL ERRORS RETURN
         IF &STR(&APARM.) = &STR(HELP) OR +
           &STR(&APARM.) = &STR(H) THEN DO
             ISPEXEC CONTROL DISPLAY SAVE
             ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTSKC)
             SET &SKCC = &LASTCC
             ISPEXEC CONTROL DISPLAY RESTORE
             EXIT CODE(&SKCC)
             END
         ELSE IF &STR(&APARM.) = &STR(LIST) THEN +
           CONTROL LIST CONLIST SYMLIST
         ELSE IF &STR(&APARM.) Â¬= &Z THEN DO
           SET ZEDSMSG = &STR(INVALID OPERAND)
           SET ZEDLMSG = &STR(ONLY HELP OR H IS ACCEPTED AS OPERAND)
           ISPEXEC SETMSG MSG(ISRZ000)
           EXIT CODE(0)
           END
         ISREDIT SEEK ')SEL' ALL NX 1 4
         ISREDIT (SELCNT) = SEEK_COUNTS
         ISREDIT SEEK ')ENDSEL' ALL NX 1 7
         ISREDIT (ENDCNT) = SEEK_COUNTS
         IF &SELCNT = &ENDCNT THEN DO
           ISREDIT (ROW,COL) = CURSOR
           ISREDIT EXCLUDE ALL
           ISREDIT CURSOR = 1 1
           ISREDIT FIND ')SEL' ALL 1 4
           ISREDIT CURSOR = 1 1
           ISREDIT FIND ')ENDSEL' ALL 1 7
           SET CTR = 0
           SET FINDCC = 0
           DO WHILE &FINDCC = 0
             ISREDIT CURSOR = 1 1
             SET CTR = &CTR + 1
             ISREDIT FIND ')ENDSEL' NX 1 7
             SET FINDCC = &LASTCC
             IF &FINDCC = 0 THEN DO
               ISREDIT (ROW,COL) = CURSOR
               ISREDIT LINE &ROW = +
                 < 1 ')ENDSEL' 55 '/*' 59 'ENDSEL-&CTR' 70 '*/'>
               ISREDIT EXCLUDE )SEL PREV
               ISREDIT (ROW,COL) = CURSOR
               ISREDIT LINE_BEFORE &ROW = +
                 < 1 ')CM' 55 '/*' 59 'SEL-&CTR' 70 '*/'>
               ISREDIT (ROW,COL) = CURSOR
               SET ROW = &ROW - 2
               ISREDIT CURSOR = &ROW &COL
               ISREDIT (HOLDLN) = LINE &ROW
               SET HOLDLN1 = &SUBSTR(2:4,&HOLDLN)
               IF &STR(&HOLDLN1) = &STR(CM )THEN DO
                 SET HOLDLN2 = &SUBSTR(59:62,&HOLDLN)
                 IF &STR(&HOLDLN2) = &STR(SEL-)THEN DO
                   ISREDIT DELETE &ROW
                   END
                 END
               ISREDIT EXCLUDE )ENDSEL
               END
             END
           ISREDIT RESET
           IF &SELCNT = 0 THEN SET ZEDSMSG = &STR(NONE)
           ELSE SET ZEDSMSG = &STR(O.K.)
           END
         ELSE DO
           ISREDIT EXCLUDE ALL
           ISREDIT CURSOR = 1 1
           ISREDIT FIND ')SEL' ALL 1 4
           ISREDIT CURSOR = 1 1
           ISREDIT FIND ')ENDSEL' ALL 1 7
           ISREDIT CURSOR = 1 1
           SET FINDCC = 0
           DO WHILE &FINDCC = 0
             ISREDIT FIND ')ENDSEL' NX 1 7
             SET FINDCC = &LASTCC
             IF &FINDCC = 0 THEN DO
               ISREDIT EXCLUDE )SEL PREV
               SET EXCLCC = &LASTCC
               IF &EXCLCC = 0 THEN ISREDIT EXCLUDE )ENDSEL
               END
             END
           SET ZEDSMSG = &STR(ERROR)
           END
         SET SELTOT = &SELCNT
         SET ENDTOT = &ENDCNT
         SET ZEDLMSG = +
           &STR(SKELETON CHECK : &SELTOT SEL AND &ENDTOT ENDSEL)
         ISPEXEC SETMSG MSG(ISRZ001)
         ISREDIT CURSOR = .ZFIRST 0
         EXIT CODE(0)
