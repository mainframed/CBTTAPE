ISREDIT  MACRO (APARM) NOPROCESS
/*  MACRO NAME : CENTER                                             */
/*    FUNCTION : CENTER TEXT WITHIN DATA LINE(S)                    */
/*      SYNTAX : CENTER                                             */
/*               CENTER HELP OR H                                   */
/*       DEBUG : CENTER LIST                                        */
/*      ORIGIN : CBT TAPE 89 FEB, FILE 95                           */
  IF &STR(&APARM.) ¬= &STR() THEN DO
    IF &STR(&APARM.) = &STR(HELP) OR +
      &STR(&APARM.) = &STR(H) THEN DO
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTCNTR)
        SET &RCX = &LASTCC
        ISPEXEC CONTROL DISPLAY RESTORE
        EXIT CODE(&RCX)
        END
    IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
    ELSE DO
      SET ZEDSMSG = INVALID OPERAND
      SET ZEDLMSG = VALID OPERAND ARE HELP OR NULL
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    END
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
/*               PROCESS LINE COMMANDS, CHECK IF C WAS SPECIFIED    */
  ISREDIT PROCESS RANGE C
  SET LCC = &LASTCC
  ISREDIT (RC) = RANGE_CMD
  SET RCC = &LASTCC
  IF &LCC ¬=0 OR &RCC ¬=0 THEN DO
    IF &RCC = 4 THEN DO
      IF &STR(&RC) ¬= &STR(C) THEN DO
        IF &LCC = 4 THEN SET &ZEDSMSG = &STR(NO RANGE)
        ELSE SET &ZEDSMSG = &STR(INVALID RANGE)
        SET &ZEDLMSG = &STR(YOU MUST SPECIFY A TARGET RANGE OF +
                            LINES, USING "CN" OR "CC")
        END
      ELSE DO
        SET &ZEDSMSG = &STR(RANGE INCOMPLETE)
        SET &ZEDLMSG = &STR(ENTER A MATCHING "CC" TO COMPLETE +
                            THE BLOCK COMMAND PAIR)
        END
      END
    ELSE IF &RCC = 8 THEN DO
      SET &ZEDSMSG = &STR(NOT ACCEPTABLE)
      SET &ZEDLMSG = &STR(AN UNACCEPTABLE TARGET RANGE OF LINES +
                          HAS BEEN SPECIFIED)
      END
    ELSE DO
      SET &ZEDSMSG = &STR(SEVERE ERROR)
      SET &ZEDLMSG = &STR(SEVERE ERROR OCCURED, RETURN CODES ARE +
                          &LCC AND &RCC)
      END
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
    END
  ISREDIT (FR) = LINENUM .ZFRANGE
  ISREDIT (LR) = LINENUM .ZLRANGE
  ISREDIT (LB,RB) = BOUNDS
  SET I = &FR
  DO WHILE (&I <= &LR)
    ISREDIT
    ISREDIT LABEL &I = .LJ
    ISREDIT SEEK P'¬' .LJ .LJ &LB &RB FIRST
    ISREDIT (LLINE,LCOL) = CURSOR
    ISREDIT SEEK P'¬' .LJ .LJ &LB &RB LAST
    ISREDIT (RLINE,RCOL) = CURSOR
    SET MIDPT = &EVAL(&RB - &LB) / 2
    SET MIDPT = &MIDPT + &LB
    SET LEFT = &MIDPT - &LCOL
    SET RIGHT = &RCOL - &MIDPT
    SET COUNT = &EVAL(&LEFT - &RIGHT + 1) / 2
    IF &COUNT > 0 THEN ISREDIT SHIFT ) &I &COUNT
    ELSE IF &COUNT < 0 THEN DO
      SET COUNT = -1 * &COUNT
      ISREDIT SHIFT ( &I &COUNT
      END
    IF &I = &FR THEN ISREDIT LABEL &I = .LJX
    SET I = &I + 1
  END
  ISREDIT SEEK P'=' .LJX .LJX FIRST
EXIT CODE(0)
