ISREDIT  MACRO (APARM)
/*  MACRO NAME : EDITMACS                             (MOINIL P.A.) */
/*    FUNCTION : DISPLAY AVAILABLE ADDITIONAL LOCAL EDIT MACROS     */
/*      SYNTAX : EDITMACS                                           */
/*       DEBUG : EDITMACS LIST                                      */
  IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST SYMLIST CONLIST
START: +
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC TBOPEN EDITMACS NOWRITE SHARE
  SET &EMRCC = &LASTCC
  IF &EMRCC = 8 THEN DO
    SET &ZEDSMSG = NO MACROS
    SET &ZEDLMSG = THE TABLE "EDITMACS" DOES NOT EXIST
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &EMRCC = 12 THEN DO
    SET &ZEDSMSG = TABLE IN USE
    SET &ZEDLMSG = ENQ FAILED DUE TO TABLE IN USE BY ANOTHER USER OR +
                   CURRENT USER
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &EMRCC = 16 OR &EMRCC = 20 THEN DO
    SET &ZEDSMSG = TBOPEN ERROR
    SET &ZEDLMSG = TBOPEN RETURN CODE &EMRCC ... ABORT
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
LOOP: +
  ISPEXEC TBDISPL EDITMACS PANEL(PAJEML) POSITION(EMCRP)
  SET &EMTRC = &LASTCC
  IF &EMTRC = 8 THEN GOTO LEAVE
  IF &EMTRC = 12 OR &EMTRC = 20 THEN DO
    SET &ZEDSMSG = TBDISPL ERROR
    SET &ZEDLMSG = TBDISPL RETURN CODE &EMTRC ... ABORT
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &ZCMD = CANCEL THEN GOTO QUIT
  IF &ZTDSELS = 0 THEN GOTO LOOP
  IF &EMOPTN = S THEN DO
    ISREDIT &MACNAME HELP
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = HELP ERROR
      SET &ZEDLMSG = DISPLAY HELP HAS GIVEN BACK A &EMTRC RETURN CODE
      ISPEXEC SETMSG MSG(ISRZ000)
      END
    END
  ELSE DO
    SET &ZEDSMSG = INVALID OPTION
    SET &ZEDLMSG = ONLY VALID OPTION IS "S" TO GET HELP OF MACRO
    ISPEXEC SETMSG MSG(ISRZ000)
    END
  GOTO LOOP
LEAVE: +
  ISPEXEC TBEND EDITMACS
  SET &EMTRC = &LASTCC
  IF &EMTRC ¬= 0 THEN DO
    SET &ZEDSMSG = TBEND ERROR
    SET &ZEDLMSG = TBEND RETURN CODE &EMTRC ... ABORT
    END
  ELSE DO
    SET &ZEDSMSG = HELP DONE
    SET &ZEDLMSG = PROCESSING ENDED NORMALLY
    END
  ISPEXEC SETMSG MSG(ISRZ000)
  EXIT CODE(0)
QUIT: +
  ISPEXEC TBEND EDITMACS
  SET &EMTRC = &LASTCC
  IF &EMTRC ¬= 0 THEN DO
    SET &ZEDSMSG = TBEND ERROR
    SET &ZEDLMSG = TBEND RETURN CODE &EMTRC ... ABORT
    END
  ELSE DO
    SET &ZEDSMSG = CANCEL DONE
    SET &ZEDLMSG = IMMEDIATE STOP HAS BEEN REQUESTED
    END
  ISPEXEC SETMSG MSG(ISRZ000)
  EXIT CODE(12)
