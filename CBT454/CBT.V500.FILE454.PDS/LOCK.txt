LOCK     TITLE 'TSO - COMMAND TO LOCK TERMINAL'
LOCK     START   0
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
* Purpose :    TSO command to LOCK terminal (restricted to only       *
*              authorized users : SYSTEM).                            *
*                                                                     *
*              Function : terminal is locked until the correct two    *
*                         secret keywords are typed in, or, the       *
*                         SESSION is terminated when the defined      *
*                         maximum WAIT time (minutes) has expired     *
*                         (see module "LOCKT").                       *
*                                                                     *
*              Note : the above maximum WAIT time is not connected in *
*                         any way with the "JWT" time expressed in    *
*                         member SMFPRM00 of the PARMLIB. It starts   *
*                         at the moment the LOCK command is invoked   *
*                         and it decrements continuously (REAL time), *
*                         even if somebody push the ENTER key.        *
*                                                                     *
*              Mandatory : 1. define secret keys (see module "LOCKT") *
*                             for authorized users, and               *
*                          2. verify the value assigned at the DEFWUT *
*                             symbol defined below.                   *
*                                                                     *
*              Pre-requisite : KUMX (ISPF Edit macro, see the module  *
*                          "LOCKT").                                  *
*                                                                     *
* Syntax :     LOCK                                                   *
*                                                                     *
* Author :     MOINIL P.A.                                            *
*              Computing Centre (TP 361)                              *
*              J.R.C. - Ispra Establishment                           *
*              21020 Ispra (VA), Italy                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
*------- MISCELLANEOUS DEFINITIONS
         SPACE 1
DEFWUT   EQU   5              DEFAULT WAKE-UP TIME INTERVAL (MINUTES)
*                             (THE ABOVE VALUE MUST BE LESS THAN THE
*                             "JWT" VALUE IN "SMFPRM00" OF PARMLIB)
PSWR    $MDL@IX ,             AUTHORITY MODULE NAME
LV12     EQU   12             AUTHORITY LEVEL : SYSTEM GROUP
         SPACE 1
         PRINT NOGEN
        $DEFREG
         PRINT GEN
         EJECT
        $XENT  BASE=(R11,R12)
        $TEW$EC NOTTSO=(,ONLY),MSG=PUTMSG,ERR=SETRC
         SPACE 1
*------- CHECK USER AUTHORITY : SYSTEM
         SPACE 1
        $EACM  REQAUT
         CLI   XAUTH,LV12          AUTHORITY LEVEL OK?
         BL    NOTAUT              NO
         MVC   GROUPN,XGRNME       SET GROUP NAME
         MVC   USERID,XUSNME       SET USER ID
         MVC   CMDBUFU,XUSNME
         MVC   CMNDU,XUSNME
         LA    R1,CMDBUFU+L'CMDBUFU-2
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C')'
         SPACE 1
*------- CHECK CALLER TERMINAL : SCREEN
         SPACE 1
        GTSIZE ,                   GET SCREEN SIZE
         LTR   R0,R0               IS IT A SCREEN?
         BZ    NOTSCR              IF ZERO, NOT A SCREEN
         CH    R0,=H'24'           STANDARD NUMBER OF LINES?
         BL    NOTSTD              BRANCH IF LESS
         BE    *+L'*+12            BRANCH IF YES
         CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    NOTSTD              BRANCH IF LESS
         B     *+L'*+12
         CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    NOTSTD              BRANCH IF LESS
         BE    *+L'*+12            BRANCH IF EQUAL
         ST    R0,SZSCRN           SAVE SCREEN SIZE
         ST    R1,SZLINE           SAVE LINE SIZE
         OI    SWITCH,SCRST        INDICATE SCREEN SIZE TO BE SET
         SPACE 1
*------- GET LOCK TABLE AND CHECK USER DEFINED IN
         SPACE 1
         MVC   WORKC,USERID
         LA    R1,PLST0
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BZ    STKOK
         MVC   MSGERN,=CL6'SETKEY'
SDMSG    CVD   R15,WORKD
         MVC   WORKD(5),=XL5'4020202120'
         ED    WORKD(5),WORKD+L'WORKD-3
         MVC   MSGERC,WORKD+2
         LA    R1,MSGER
         B     XSMSG
STKOK    LA    R1,PLST4
         L     R15,=V(ENCIPH)
         BASR  R14,R15
         LTR   R15,R15
         BZ    *+L'*+10
         MVC   MSGERN,=CL6'ENCIPH'
         B     SDMSG
        LOAD   EP=LOCKT
         LTR   R15,R15
         BZ    *+L'*+10
         MVC   MSGERN,=CL6'"LOAD"'
         B     SDMSG
         LR    R1,R0
         CLC   =CL4'LCKT',4*4(R1)  CHECK TABLE IDENTIFICATION
         BE    CMXWT               OK
         LA    R1,MSGTW            WRONG
         B     GODDL
CMXWT    MVC   MAXWT,3*4(R1)       COPY MAX. WAIT TIME
         LM    R7,R9,0(R1)
         CLR   R7,R9
         BNH   CHKUS
         LA    R1,MSGTE            TABLE EMPTY
         B     GODDL
CHKUS    CLC   WORKC,0(R7)         THIS ONE?
         BE    CHKOK               YES, OK
         BXLE  R7,R8,CHKUS         NO, CHECK NEXT IF ANY
         LA    R1,MSGNT            NOT IN TABLE
GODDL    BAS   R14,PUTMSG
         LA    R7,SETRC
DODEL   DELETE EP=LOCKT
         BR    R7
CHKOK    MVC   VALUE,8(R7)         KEEP VALUE FOR CONTROL
         BAS   R7,DODEL
         SPACE 1
*------- VTAM INITIALIZATION
         SPACE 1
        STFSMODE ON,INITIAL=YES    TURN ON VTAM FULL SCREEN MODE
         TM    SWITCH,SCRST        SCREEN SIZE TO BE SET?
         BZ    NOSCRS              BRANCH IF NOT
        STSIZE SIZE=80,LINE=24     SET STANDARD SCREEN SIZE
         OI    SWITCH,SCRRS        INDICATE SCREEN SIZE TO BE RESET
         SPACE 1
*------- LOCK INITIALIZATION
         SPACE 1
         USING PSA,R0
NOSCRS   L     R2,PSAAOLD          ASCB ADDRESS
         USING ASCB,R2
         L     R3,ASCBASXB         ASXB ADDRESS
         DROP  R2
         USING ASXB,R3
         L     R2,ASXBSENV         RACF ENVIRONMENT ADDRESS
         DROP  R3
         USING ACEE,R2
         MVC   TERMNAME,ACEETRID   SET TERMINAL ID
         MVI   USERNAME,C' '
         MVC   USERNAME+1(L'USERNAME-1),USERNAME
         L     R3,ACEEUNAM
         XR    R1,R1
         IC    R1,0(R3)
         BCT   R1,*+L'*+4
         B     NOUSNM
         LA    R0,L'USERNAME
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         MVC   USERNAME(*-*),1(R3)
         EX    R1,*-6              SET USER NAME
         DROP  R2
NOUSNM   L     R3,CVTPTR           CVT POINTER
         USING CVT,R3
         L     R2,CVTSMCA          SMF AREA ADDRESS
         USING SMCABASE,R2
         ICM   R1,B'1111',SMCAJWT  JOB WAIT TIME LIMIT
         DROP  R2,R3
         BZ    STPNTM              NORMALLY NEVER BRANCH HERE
         M     R0,=F'10486'        BIT-31 REPRESENTS 1.048576 SECONDS
         D     R0,=F'10000'
         S     R1,=F'60'           JOB WAIT TIME (SECONDS) - 1 MINUTE
         BP    SETINT
STPNTM   L     R1,=A(DEFWUT*60)    DEFAULT WAKE-UP INTERVAL (SECONDS)
SETINT   ST    R1,WAKEUP           SET WAKE-UP INTERVAL (SECONDS)
         MH    R1,=H'100'
         ST    R1,WTTWTI           TIMER WAIT TIME INTERVAL
         L     R0,MAXWT            MAXIMUM WAIT TIME DESIRED (SECONDS)
         CL    R0,WAKEUP           WAKE-UP INTERVAL (SECONDS)
         BL    STLWTI              SMALLER INTERVAL
         LR    R1,R0
         XR    R0,R0
         D     R0,WAKEUP           WAKE-UP INTERVAL (SECONDS)
         ST    R1,WTTNUM           NUMBER OF WAKE-UP INTERVAL
         LTR   R0,R0
         BNP   STTLWT
STLWTI   MH    R0,=H'100'
STTLWT   ST    R0,WTLWTI           LAST WAKE-UP TIME INTERVAL
        IDENTIFY ENTRY=WTTIME,EP=WTTIME SET ENTRY POINT
        ATTACH EP=WTTIME           START SUB-TASK
         LTR   R15,R15             STARTED?
         BNZ   *+L'*+8             NO
         ST    R1,STTCBA           YES, RETAIN TCB ADDRESS
         MVC   WTRCC,=F'-1'        AND SET SUB-TASK STARTED
        ESTAE  INTER               INTERCEPT TASK ABNORMAL EXIT
        STTMPMD OFF,KEYS=ALL       INTERCEPT ALL KEYS
        TIME   DEC
         ST    R0,WORKF            R0 = HHMMSSTF
         UNPK  WORKC(7),WORKF
         MVC   HHMM(2),WORKC
         MVC   HHMM+3(2),WORKC+2
         SPACE 1
*------- ASK KEYWORDS TO UNLOCK TERMINAL
         SPACE 1
LOOP     TM    SWITCH,SCRST+SCRRS  FIRST TPUT DONE?
         BNO   SNDWR               BRANCH IF YES
        $FS    CC=EW,MF=(I,WSCR)   SET ERASE/WRITE
SNDWR   TPUT   WSCR,WSCRL,FULLSCR  WRITE THE SCREEN
         TM    SWITCH,SCRST+SCRRS  FIRST TPUT DONE?
         BNO   SNDGT               BRANCH IF YES
        $FS    CC=W,MF=(I,WSCR)    RESTORE WRITE
         NI    SWITCH,255-SCRST    SET FIRST TPUT DONE
SNDGT    MVI   ERRMSG,C' '
         MVC   ERRMSG+1(L'ERRMSG-1),ERRMSG
         XC    REPLY(REPLYL),REPLY
        TGET   REPLY,REPLYL-1,ASIS
         CH    R15,=H'20'          TERMINAL DISCONNECTED?
         BE    LEAVE               YES,... SPLIT
         TM    SWITCH,SLOFF        SESSION LOGOFF/CANCEL?
         BO    *+L'*+8             YES, SKIP
         CH    R15,=H'8'           ATTENTION?
         BE    LOOP                YES,... START OVER
         CH    R15,=H'12'          IS INPUT LONGER THAN BUFFER?
         BNE   SKCLRQ              NO
        TCLEARQ INPUT              FLUSH THE TRASH
SKCLRQ   TM    SWITCH,SLOFF        SESSION LOGOFF/CANCEL?
         BO    LEAVE               YES,... SPLIT
         LM    R2,R4,=A(1,REPLY+REPLYL-1,REPLY)
         CLI   0(R4),X'7D'         ENTER?
         BNE   LOOP                IF NOT, IGNORE, RESHOW
         LA    R4,3(R4)            POINT TO KEYWORD ADDRESS
         CLC   0(L'AKWD1,R4),AKWD1 IS IT RIGHT?
         BNE   LOOP                NO
         LA    R4,L'AKWD1(R4)      YES, POINT TO KEYWORD
         BAS   R14,GETKW           AND GET IT
         B     LOOP                +0 - NO OR BLANK KEYWORD
         MVC   KEYW1,WORKC         +4 - OK, KEEP IT
         CLC   0(L'AKWD2,R4),AKWD2 NEXT KEYWORD ADDRESS RIGHT?
         BNE   LOOP                NO
         LA    R4,L'AKWD2(R4)      YES, POINT TO KEYWORD
         BAS   R14,GETKW           AND GET IT
         B     LOOP                +0 - NO OR BLANK KEYWORD
         MVC   KEYW2,WORKC         +4 - OK, KEEP IT
         CLI   0(R4),0             END DETECTED?
         BE    CHKKW               YES
         B     LOOP                NO, WHAT'S THAT?
         SPACE 1
*- - - - GET KEYWORD ROUTINE       R14 = LINK REGISTER
*                                  R2-R4 = SCAN REGISTERS
*                                  R0-R1 = WORK REGISTERS
*                        RETURN +0 = NO OR WRONG KEYWORD PROCESS
*                               +4 = OK, KEYWORD IN "WORKC" FIELD
GETKW    MVC   WORKC,=CL8' '
         LA    R0,L'WORKC
         LA    R1,WORKC
GETKW1   CLC   0(L'SBACODE,R4),SBACODE
         BE    GETKW2
         CLI   0(R4),0
         BE    GETKW2
         OC    0(1,R1),0(R4)       COPY IN UPPER CASE
         BXLE  R4,R2,*+L'*+4
         B     GETKW2
         LA    R1,1(R1)
         BCT   R0,GETKW1
GETKW2   CLC   WORKC,=CL8' '       HOW IS KEYWORD?
         BER   R14                 WRONG, RETURN +0
         B     4(R14)              OK, RETURN +4
         SPACE 1
*------- CHECK KEYWORDS VALIDITY
         SPACE 1
CHKKW    MVC   WORKC,VALUE
         LA    R1,PLST1
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR1
         LA    R1,PLST4
         L     R15,=V(DECIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR2
         LA    R1,PLST2
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR3
         LA    R1,PLST4
         L     R15,=V(DECIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR4
         LA    R1,PLST3
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR5
         LA    R1,PLST4
         L     R15,=V(DECIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR6
         LA    R1,PLST5
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR7
         MVC   KEYW3(L'KEYW3),LOCK+10
         LA    R1,PLST6
         L     R15,=V(ENCIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR8
         CLC   CONTROL,KEYW3       AND NOW IS IT THOSE KEYWORDS?
         BE    LEAVE               YES, OK, UNLOCK IT
         MVC   ERRMSG(21),=CL21'Failed| Mental midget'
         B     LOOP                ELSE ...
ERR1     MVC   ERRMSG(3),=CL3'1st'
         MVC   ERRMSG+9(6),=CL6'SETKEY'
         B     ERRX
ERR2     MVC   ERRMSG(3),=CL3'1st'
         MVC   ERRMSG+9(6),=CL6'DECIPH'
         B     ERRX
ERR3     MVC   ERRMSG(3),=CL3'2nd'
         MVC   ERRMSG+9(6),=CL6'SETKEY'
         B     ERRX
ERR4     MVC   ERRMSG(3),=CL3'2nd'
         MVC   ERRMSG+9(6),=CL6'DECIPH'
         B     ERRX
ERR5     MVC   ERRMSG(3),=CL3'3rd'
         MVC   ERRMSG+9(6),=CL6'SETKEY'
         B     ERRX
ERR6     MVC   ERRMSG(3),=CL3'3rd'
         MVC   ERRMSG+9(6),=CL6'DECIPH'
         B     ERRX
ERR7     MVC   ERRMSG(3),=CL3'4th'
         MVC   ERRMSG+9(6),=CL6'SETKEY'
         B     ERRX
ERR8     MVC   ERRMSG(3),=CL3'End'
         MVC   ERRMSG+9(6),=CL6'ENCIPH'
ERRX     MVC   ERRMSG+4(4),=CL4'call'
         MVC   ERRMSG+16(5),=CL5'error'
         B     LOOP
         SPACE 1
*------- CLEAN UP AND GO HOME
         SPACE 1
LEAVE   STTMPMD ON,KEYS=NO         RESET
        ESTAE  0                   RESET
         OC    STTCBA,STTCBA       SUB-TASK NEVER STARTED?
         BZ    SKDET               YES
         CLC   WTRCC,=F'-1'        NO, STARTED, SUB-TASK TERMINATED?
         BNE   SKDET               YES
        DETACH STTCBA              NO, FORCE TERMINATE SUB-TASK
SKDET   $FS    CC=EW,MF=(I,CLEAR)  SET CLEAR SEQUENCE
         TM    SWITCH,SCRRS        WAS SCREEN SIZE CHANGED?
         BZ    ENDCL               BRANCH IF NOT
        STSIZE SIZELOC=SZLINE,LINELOC=SZSCRN RESTORE ORIGINAL SIZE
        $FS    CC=EWA,MF=(I,CLEAR)
ENDCL   TPUT   CLEAR,CLEARL,FULLSCR,,HOLD CLEAR THE SCREEN
        STFSMODE OFF               TELL VTAM WE'RE DONE
         XR    R15,R15             RC=0 -------------------------------
         B     QUIT
NOTAUT   LA    R1,NAUTM
         B     XSMSG
NOTSCR   LA    R1,NSCRM
         B     XSMSG
NOTSTD   LA    R1,NSTDM
XSMSG    BAS   R14,PUTMSG
SETRC    LA    R15,16              RC=16 ------------------------------
QUIT    $XRET  CC=(R15)
         EJECT
*------- RECOVERY ROUTINE
         SPACE 1
         CNOP  0,8
INTER    STM   R14,R12,12(R13)
         BASR  R10,0
         USING *,R10
        STTMPMD ON,KEYS=NO         RESET
        ESTAE  0                   RESET
         OC    STTCBA,STTCBA       SUB-TASK NEVER STARTED?
         BZ    SKINT               YES
         CLC   WTRCC,=F'-1'        NO, STARTED, SUB-TASK TERMINATED?
         BNE   SKINT               YES
        DETACH STTCBA              NO, FORCE TERMINATE SUB-TASK
SKINT    L     R1,24(R13)          RESTORE SDWA ADDRESS
        SETRP  ,                   CONTINUE WITH TERMINATION
         LM    R14,R12,12(R13)
         BR    R14
         DROP  R10
         EJECT
*--------------------------------------------------------------------*
*        SUB-TASK TO ALLOW A LONGER WAIT LIMIT.                      *
*--------------------------------------------------------------------*
         SPACE 1
         CNOP  0,8
WTTIME   STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R10,R15             SET BASE REGISTER
         USING WTTIME,R10
         LR    R15,R13             PREVIOUS SAVE AREA
         LA    R13,WTSVA           CURRENT SAVE AREA
         ST    R13,8(R15)          LINK SAVE AREAS
         ST    R15,4(R13)
         LA    R2,WTTWTI
         ICM   R3,B'1111',WTTNUM
         BNZ   WTLOOP
WTLSET   LA    R2,WTLWTI
WTLOOP   XC    WTTECB,WTTECB
        STIMER REAL,WTEXIT,BINTVL=(R2) WAIT TIME INTERVAL
         LTR   R15,R15             DAMAGED CLOCKS?
         BNZ   WTDCLK              YES
        WAIT   ECB=WTTECB,LONG=YES
        TTIMER CANCEL
         LTR   R15,R15             DAMAGED CLOCKS?
         BZ    WTDCOK              NO
WTDCLK   OI    SWITCH,DCLKS        YES, SAY IT
         B     WTQUIT              GO STOP ALL (TERMINATION)
WTDCOK   LTR   R3,R3
         BZ    WTQUIT
         BCT   R3,WTLOOP           CONTINUE UNTIL ANY MORE ALLOWED
         OC    WTLWTI,WTLWTI
         BNZ   WTLSET
WTQUIT   ST    R15,WTRCC           SAY HOW TERMINATED
         OI    SWITCH,SLOFF        NO MORE, SAY SESSION LOGOFF/CANCEL
        TCLEARQ INPUT              FLUSH THE TRASH
         TM    SWITCH,DCLKS        DAMAGED CLOCKS?
         BZ    *+L'*+6             NO, SKIP, MESSAGE OK
         MVC   CMDBUFM,LDMSG       YES
         L     R15,CVTPTR          CVT POINTER
         USING CVT,R15
         L     R1,CVTTVT           TSO VECTOR TABLE ADDRESS
         DROP  R15
         USING TSVT,R1
         L     R15,TSVTASF         TSO SERVICE ROUTINE ADDRESS
         DROP  R1
        CALL   (15),(SFLGS,CMDBUF,BUFLN,RTCC,RSCC,ABCC),VL
         LR    R3,R15              RETAIN ALL CC'S
         LM    R4,R6,RTCC
        ZEROKEY
         XR    R0,R0               SEND COMMAND
         XR    R15,R15
         LA    R2,CMND             COMMAND BUFFER ADDRESS
        MGCR   (R2)
         LR    R2,R15              SAVE RETURN CODE
        RESETKEY
         LTR   R2,R2               CHECK RETURN CODE?
         BNZ   WTTERR              BAD RETURN CODE FROM MGCR
         L     R13,4(R13)          PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         MVI   12(R13),X'FF'       SET RETURN INDICATOR
         XR    R15,R15             SET RETURN CODE
         BR    R14                 RETURN
WTTERR   XR    R7,R7               NORMALLY NEVER COME HERE
         XR    R8,R8               (CLEAR ALL REGISTERS UNLESS CC'S)
         XR    R9,R9
         XR    R10,R10
         XR    R11,R11
         XR    R12,R12
         XR    R13,R13
         XR    R14,R14
         XR    R15,R15
        ABEND  522,,STEP           KILL MYSELF
         DROP  R10
         SPACE 1
*------- TIMER EXIT ROUTINE
         SPACE 1
         CNOP  0,8
         USING *,R15
WTEXIT  POST   WTTECB
         BR    R14
         DROP  R15
         SPACE 1
*------- SUB-TASK CONSTANTS
         SPACE 1
WTSVA    DC    18F'0'              SUB-TASK SAVE AREA
WTRCC    DC    F'0'                SUB-TASK TERMINATION CODE
WTTECB   DC    F'0'                TIMER ECB
WAKEUP   DC    F'0'                TIMER WAIT TIME INTERVAL (SECONDS)
WTTNUM   DC    F'0'                NUMBER OF WAKE-UP INTERVAL
WTLWTI   DC    F'0'                LAST WAKE-UP TIME INTERVAL
WTTWTI   DC    F'0'                TIMER WAIT TIME INTERVAL
SFLGS    DC    0F'0',H'0',X'00',X'01' (NO DUMP, TSO COMMAND)
BUFLN    DC    A(CMDBUFL)          COMMAND LENGTH
RTCC     DC    F'0' ----------->=> RETURN CODE FROM COMMAND 1
RSCC     DC    F'0'            >   REASON CODE
ABCC     DC    F'0' ----------->   ABEND CODE
LDMSG    DC    C'damaged clocks has occured'
CMDBUF   DC    C'SEND '' LOCK : '  COMMAND 1
CMDBUFM  DC    C'max. WAIT time has expired'
         DC    C' / Code U0522 '' USER('
CMDBUFU  DC    CL8' ',C' SAVE'
CMDBUFL  EQU   *-CMDBUF
CMND     DC    0F'0',XL1'0'        FLAGS1
CMNDL    DC    AL1(LCMND),XL2'0'   BUFFER LENGTH, FLAGS2
CMNDT    DC    CL18'CANCEL U= '    COMMAND TEXT
CMNDU    EQU   CMNDT+9,8
LCMND    EQU   *-CMND
         SPACE 1
SWITCH   DC    XL1'0'              PROCESS INDICATORS
SCRST    EQU   X'80'                    SCREEN SIZE TO BE SET
SCRRS    EQU   X'40'                    SCREEN SIZE TO BE RESET
SLOFF    EQU   X'08'                    SESSION LOGOFF/CANCEL
DCLKS    EQU   X'01'                    DAMAGED CLOCKS
         EJECT
*------- CONSTANTS AND WORK-AREAS
         SPACE 1
WORKD    DC    D'0'
        $TEW$WA
WORKF    DC    F'0'
STTCBA   DC    A(*-*)              SUB-TASK TCB ADDRESS
SZLINE   DC    F'0'                LINE SIZE (LINE LENGTH)
SZSCRN   DC    F'0'                SCREEN SIZE (NUMBER OF LINES)
REQAUT   DC    0F'0',BL1'00010000',AL3(MDL@IX),AL4(XAUTH)
MAXWT    DC    F'0'                MAXIMUM WAIT TIME DESIRED (SECONDS)
PLST0    DC    A(WORKC)
PLST1    DC    A(KEYW2)
PLST2    DC    A(KEYW1)
PLST3    DC    A(XUSNME)
PLST4    DC    A(WORKC,*+4,L'WORKC)
PLST5    DC    A(WORKC)
PLST6    DC    A(KEYW3,*+4,L'KEYW3)
CONTROL  DC    XL8'E4CC2344D8B579B8'
VALUE    DC    CL8' '
WORKC    DC    CL8' '
KEYW1    DC    CL8' '
KEYW2    DC    CL8' '
KEYW3    DC    CL8' '
         SPACE 1
REPLY    DS    0F             AREA TO HOLD TGET REPLY (MAX. LENGTH)
         DC    XL3'0'              AID + CURSOR ADDRESS
         DC    XL11'0'             1ST FIELD ADDRESS + KEYWORD REPLY
         DC    XL11'0'             2ND FIELD ADDRESS + KEYWORD REPLY
         DC    XL4'0'              FILLER
REPLYL   EQU   (*-REPLY)-1
         SPACE 1
XAUTH    DC    XL1'0' --------->=> $EACM RESPONSE LIST
XUSNME   DC    CL8' '          >
XGRNME   DC    CL8' ' --------->
         SPACE 1
WSCR     $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(2,28),TEXT='Terminal',MF=L
         $FS   SF=(INT,SKIP),TEXT='"LOCKED"',MF=L
         $FS   SF=(SKIP),TEXT='at',MF=L
         $FS   SF=(INT,SKIP),MF=L
HHMM     $FS   TEXT='  .  ',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(7,8),TEXT='To',MF=L
         $FS   SF=(INT,SKIP),TEXT='unlock',MF=L
         $FS   SF=(SKIP),TEXT='this terminal,',MF=L
         $FS   SF=(INT,SKIP),TEXT='ENTER',MF=L
         $FS   SF=(SKIP),TEXT='below the',MF=L
         $FS   SF=(INT,SKIP),TEXT='TWO',MF=L
         $FS   SF=(SKIP),TEXT='top-secret keywords :',MF=L
         $FS   SBA=(12,31),SF=(INT,SKIP),TEXT='===>',MF=L
         $FS   SF=(DARK),MF=L      1ST KEYWORD
         $FS   SBA=(12,45),SF=(INT,SKIP),TEXT='<===',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(13,31),SF=(INT,SKIP),TEXT='===>',MF=L
         $FS   SF=(DARK),MF=L      2ND KEYWORD
         $FS   SBA=(13,45),SF=(INT,SKIP),TEXT='<===',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(18,1),TEXT='User-ID :',MF=L
         $FS   SF=(INT,SKIP),MF=L
USERID   $FS   TEXT=(' ',8),MF=L
         $FS   SF=(SKIP),TEXT='       RACF-group :',MF=L
         $FS   SF=(INT,SKIP),MF=L
GROUPN   $FS   TEXT=(' ',8),MF=L
         $FS   SF=(SKIP),TEXT='       Terminal-name :',MF=L
         $FS   SF=(INT,SKIP),MF=L
TERMNAME $FS   TEXT=(' ',8),MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(19,1),TEXT='User-name :',MF=L
         $FS   SF=(INT,SKIP),MF=L
USERNAME $FS   TEXT=(' ',60),MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(22,30),SF=(INT,SKIP),MF=L
ERRMSG   $FS   TEXT=(' ',21),MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(1,1),SF=(PT,IC),MF=L
WSCRL    EQU   *-WSCR
         SPACE 1
CLEAR   $FS    CC=EW,WCC=(AL,KBR,RMDT),SBA=(24,79),MF=L
SBACODE  EQU   *,1
        $FS    SBA=(1,1),RA=(1,1,00),MF=L
        $FS    SF=(INT,IC),MF=L
CLEARL   EQU   *-CLEAR
         SPACE 1
AKWD1   $FS    SBA=(12,37),MF=L
AKWD2   $FS    SBA=(13,37),MF=L
         SPACE 1
         PRINT NOGEN
NAUTM   WTO    ' LOCK : not authorized command',                       X
               ROUTCDE=11,DESC=7
NSCRM   WTO    ' LOCK : run only on a screen terminal',                X
               ROUTCDE=11,DESC=7
NSTDM   WTO    ' LOCK : want to be screen sized 24 x 80',              X
               ROUTCDE=11,DESC=7
MSGER   WTO    ' LOCK :        error - RC (R15) =    ',                X
               ROUTCDE=11,DESC=7
MSGERN   EQU   MSGER+12,6
MSGERC   EQU   MSGER+37,3
MSGTW   WTO    ' LOCK : loaded table wrong identification',            X
               ROUTCDE=11,DESC=7
MSGTE   WTO    ' LOCK : loaded table is empty',                        X
               ROUTCDE=11,DESC=7
MSGNT   WTO    ' LOCK : not defined in loaded table',                  X
               ROUTCDE=11,DESC=7
         PRINT GEN
         SPACE 1
*------- LITERALS POOL
         SPACE 1
        LTORG
         SPACE 1
*------- DSECT'S DEFINITION
         SPACE 1
         PRINT NOGEN
        $TEW$DS CVT=YES
        IHAPSA
        IHAASCB
        IHAASXB
        IHAACEE
        IKJTSVT
        IEESMCA
        IHASDWA
         PRINT GEN
         SPACE 1
         END
