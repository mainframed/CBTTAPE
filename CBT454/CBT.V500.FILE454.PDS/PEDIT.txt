ISREDIT  MACRO (APARM) NOPROCESS
/*  MACRO NAME : PEDIT                                              */
/*    FUNCTION : PRINT THE CURRENT EDIT FILE                        */
/*      SYNTAX : PEDIT                                              */
/*               PEDIT HELP OR H                                    */
/*       DEBUG : PEDIT LIST                                         */
/*      AUTHOR : MOINIL P.A.                                        */
  ISPEXEC CONTROL ERRORS RETURN
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTPED)
      SET &PECC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&PECC)
      END
  ELSE IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
  ELSE IF &STR(&APARM.) ¬= &Z THEN DO
    SET ZEDSMSG = &STR(INVALID OPERAND)
    SET ZEDLMSG = &STR(ONLY HELP OR H IS ACCEPTED AS OPERAND)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  ISREDIT PROCESS RANGE C
  SET &LCC = &LASTCC
  ISREDIT (RC) = RANGE_CMD
  SET &RCC = &LASTCC
  IF &LCC = 4 THEN DO
    ISREDIT (FIRST) = LINENUM .ZFRANGE
    ISREDIT (LAST) = LINENUM .ZLRANGE
    SET &PSW = ALL
    END
  ELSE DO
    IF &LCC ¬=0 OR &RCC ¬=0 THEN DO
      IF &RCC = 4 THEN DO
        IF &STR(&RC) = &STR(C) THEN DO
          SET &ZEDSMSG = &STR(RANGE INCOMPLETE)
          SET &ZEDLMSG = &STR(ENTER A MATCHING "CC" TO +
                              COMPLETE THE BLOCK COMMAND PAIR)
          END
        ELSE DO
          SET &ZEDSMSG = &STR(INVALID RANGE)
          SET &ZEDLMSG = &STR(YOU MUST SPECIFY A RANGE USING +
                            "CN" OR "CC")
          END
        END
      ELSE IF &RCC = 8 THEN DO
        SET &ZEDSMSG = &STR(NOT ACCEPTABLE)
        SET &ZEDLMSG = &STR(AN UNACCEPTABLE TARGET RANGE OF LINES +
                            HAS BEEN SPECIFIED)
        END
      ELSE DO
        SET &ZEDSMSG = &STR(SEVERE ERROR)
        SET &ZEDLMSG = &STR(SEVERE ERROR OCCURED, RETURN CODES ARE +
                            &LCC AND &RCC)
        END
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
      END
    ISREDIT (FIRST) = LINENUM .ZFRANGE
    ISREDIT (LAST) = LINENUM .ZLRANGE
    SET &PSW = RANGE
    END
  ISPEXEC DISPLAY PANEL(PAJPED)
  SET &PECC = &LASTCC
  IF &PECC > 4 THEN DO
    IF &PECC = 8 THEN DO
      SET ZEDSMSG = &STR(EXIT REQUEST)
      SET ZEDLMSG = &STR(EXIT "PEDIT" FACILITY HAS BEEN REQUESTED)
      END
    ELSE DO
      SET ZEDSMSG = &STR(ERROR RC=&PECC)
      SET ZEDLMSG = &STR(EXIT DUE TO DISPLAY ERROR ENCOUNTERED)
      END
    ISPEXEC SETMSG MSG(ISRZ000)
    IF &PSW = RANGE THEN ISREDIT RESET COMMAND
    EXIT CODE(8)
    END
  SET &XSW = &PEDCC
  SET &XCL = &PEDCL
  SET &XCP = &PEDCP
  SET &XHD = &PEDHD
  IF &PEDDT = THEN SET &XDT = LOCAL
  ELSE SET &XDT = &PEDDT
  CONTROL NOMSG
  FREE F(PEDOUT)
  ERROR DO
    ERROR OFF
    SET ZEDSMSG = &STR(ALLOC ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO ALLOCATE OUTPUT PRINT FILE ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  ALLOC F(PEDOUT) SYSOUT(&XCL) COPIES(&XCP) DEST(&XDT) &XHD RECFM(F A)
  ERROR OFF
  ERROR DO
    ERROR OFF
    FREE F(PEDOUT)
    SET ZEDSMSG = &STR(OPEN ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO OPEN OUTPUT PRINT FILE ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  OPENFILE PEDOUT OUTPUT
  ERROR OFF
  ERROR DO
    ERROR OFF
    CONTROL CAPS
    CLOSFILE PEDOUT
    FREE F(PEDOUT)
    SET ZEDSMSG = &STR(WRITE ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO COMPLETE OUTPUT PRINT FILE ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  SET &TL = &EVAL(&LAST-&FIRST+1)
  SET I = 0
  CONTROL ASIS
  DO WHILE &I < &TL
    ISREDIT (INDATA) = LINE &EVAL(&FIRST+&I)
    IF &XSW = YES THEN SET &PEDOUT = &NRSTR(&INDATA)
    ELSE SET &PEDOUT = &STR( &NRSTR(&INDATA))
    PUTFILE PEDOUT
    SET &I = &I + 1
    END
  ERROR OFF
  CONTROL CAPS
  CLOSFILE PEDOUT
  FREE F(PEDOUT)
  SET ZEDSMSG = &STR(&PSW PRINTED)
  SET ZEDLMSG = &STR(PRINT DONE FROM LINE &FIRST UP TO LINE &LAST)
  ISPEXEC SETMSG MSG(ISRZ000)
  CONTROL MSG
  EXIT CODE(0)
