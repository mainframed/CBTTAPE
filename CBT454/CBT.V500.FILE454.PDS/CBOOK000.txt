PROC     0
/* * --- DELETE FROM HERE ****************************************** */
/* * YOU MUST ADJUST THE VARIOUS LEVELS ACCESSES (WIZARD, MASTER   * */
/* *               AND PUPIL) DEFINED HERE BELOW BEFORE YOU MAY    * */
/* *               EXECUTE THIS PROCEDURE (REPLACE THESE NAMES BY  * */
/* *               THE DESIRED USER-IDS).                          * */
/* * THEN, DELETE ALL INDICATED STATEMENTS, GOOD LUCK.             * */
/* ***************************************************************** */
/* * PRE-REQUISITE : "PRINTO" TSO COMMAND (AVAILABLE IN CBT FILES  * */
/* *                 433 AND 437).                             (PM)* */
/* *---------------------------------------------------------------* */
WRITE *** MANDATORY - ISPF CLIST "CBOOK000" INTERVENTION REQUIRED ***
EXIT
/* * --- UP TO HERE WHEN ADJUSTMENT IS DONE ************************ */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       PURPOSE : IBM MANUALS MAINTENANCE DRIVER.               (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
CONTROL MAIN NOMSG
IF &SYSISPF EQ &STR(NOT ACTIVE) THEN EXIT
ISPEXEC VGET ZUSER
SET &CBAVL = NO
/* - - - HERE FOLLOWS THE "WIZARD" LEVEL ACCESS  - - - - - - - - - - */
/*       "WIZARD" CAN DO - TEST AND MODIFY THIS PROCEDURE            */
/*                       - CREATE NEW DATA-SETS                      */
/*                       - CREATE NEW TABLES                         */
/*                       - UPDATE ALL TABLES                         */
IF &ZUSER = WIZARD THEN DO
  SET &CBAVL = YES
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC DISPLAY PANEL(PBOOK000)
  IF &LASTCC NE 0 THEN EXIT
  ISPEXEC CONTROL ERRORS CANCEL
  IF &CBTPRO = &Z THEN SET &CBTPRO = &STR(SYS)
  IF &CBTLIB = &Z THEN SET &CBTLIB = &STR(BOOKS)
  IF &CBTTYP = &Z THEN SET &CBTTYP = &STR(DATA)
  IF &CBVOL = &Z THEN SET &CBVOL = &STR(USER00)
  IF &CBUNIT = &Z THEN SET &CBUNIT = &STR(DISK)
  IF &CBALLU = &Z THEN SET &CBALLU = &STR(CYLINDERS)
  IF &CBSPA1 = &Z THEN SET &CBSPA1 = &STR(1)
  IF &CBSPA2 = &Z THEN SET &CBSPA2 = &STR(1)
  IF &CBNDIR = &Z THEN SET &CBNDIR = &STR(1)
  IF &CBLIST = &Z THEN SET &CBLIST = &STR(CBLIST)
  END
/* - - - HERE FOLLOWS THE "MASTER" LEVEL ACCESS  - - - - - - - - - - */
/*       "MASTER" CAN DO - CREATE NEW DATA-SETS                      */
/*                       - CREATE NEW TABLES                         */
/*                       - UPDATE ALL TABLES                         */
ELSE IF &ZUSER = MASTER THEN DO
  SET &CBAVL = YES
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC DISPLAY PANEL(PBOOK00)
  IF &LASTCC NE 0 THEN EXIT
  ISPEXEC CONTROL ERRORS CANCEL
  IF &CBTPRO = &Z THEN SET &CBTPRO = &STR(SYS)
  IF &CBTLIB = &Z THEN SET &CBTLIB = &STR(BOOKS)
  IF &CBTTYP = &Z THEN SET &CBTTYP = &STR(DATA)
  IF &CBVOL = &Z THEN SET &CBVOL = &STR(USER00)
  IF &CBALLU = &Z THEN SET &CBALLU = &STR(CYLINDERS)
  IF &CBSPA1 = &Z THEN SET &CBSPA1 = &STR(1)
  IF &CBSPA2 = &Z THEN SET &CBSPA2 = &STR(1)
  IF &CBNDIR = &Z THEN SET &CBNDIR = &STR(1)
  IF &CBLIST = &Z THEN SET &CBLIST = &STR(CBLIST)
  SET &CBTEST = NO
  END
/* - - - HERE FOLLOWS THE "PUPIL" LEVEL ACCESS - - - - - - - - - - - */
/*       "PUPIL" CAN DO - UPDATE ONLY A PREDEFINED TABLE             */
ELSE IF &ZUSER = PUPIL THEN DO
  SET &CBAVL = YES
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC DISPLAY PANEL(PBOOK0)
  IF &LASTCC NE 0 THEN EXIT
  ISPEXEC CONTROL ERRORS CANCEL
  SET &CBTPRO = &STR(SYS)
  SET &CBTLIB = &STR(BOOKS)
  SET &CBTTYP = &STR(DATA)
  SET &CBVOL = &STR(******)
  SET &CBALLU =
  SET &CBSPA1 =
  SET &CBSPA2 =
  SET &CBNDIR =
  SET &CBLIST = &STR(CBLIST)
  SET &CBTEST = NO
  END
/* - - - HERE FOLLOWS THE "LOOK-ONLY" LEVEL ACCESS - - - - - - - - - */
/*       "LOOK-ONLY" CAN DO - READ ONLY A PREDEFINED TABLE           */
ELSE DO
  SET &CBUPDT = NO
  SET &CBTPRO = &STR(SYS)
  SET &CBTLIB = &STR(BOOKS)
  SET &CBTTYP = &STR(DATA)
  SET &CBVOL = &STR(******)
  SET &CBALLU =
  SET &CBSPA1 =
  SET &CBSPA2 =
  SET &CBNDIR =
  SET &CBLIST = &STR(CBLIST)
  SET &CBTEST = NO
  END
IF &CBTEST EQ YES THEN CONTROL LIST SYMLIST CONLIST MSG
/* - - - SET IBM MANUALS TABLE (DDNAME = CBTABL).  - - - - - - - - - */
ERROR DO
  ERROR DO
    ERROR OFF
    WRITE  *** UNABLE TO ALLOCATE "TABLE" LIBRARY DATA-SET ***
    EXIT
    END
  IF &CBVOL EQ &STR(******) THEN DO
    WRITE  *** UNEXISTANT "TABLE" LIBRARY DATA-SET ***
    EXIT
    END
  ALLOC F(CBTABL) DSN('&CBTPRO..&CBTLIB..&CBTTYP') NEW CATALOG +
    UNIT(&CBUNIT) VOLUME(&CBVOL) +
    SP(&CBSPA1 &CBSPA2) &CBALLU DIR(&CBNDIR) +
    RECFM(F B) LRECL(80) BLKSIZE(9040)
  FREE F(CBTABL)
  ERROR OFF
  END
ALLOC F(CBTABL) DSN('&CBTPRO..&CBTLIB..&CBTTYP') SHR
ERROR OFF
ISPEXEC LIBDEF ISPTLIB LIBRARY ID(CBTABL)
ISPEXEC LIBDEF ISPTABL LIBRARY ID(CBTABL)
ISPEXEC CONTROL ERRORS RETURN
IF &CBUPDT EQ YES THEN ISPEXEC TBOPEN &CBLIST WRITE SHARE
ELSE ISPEXEC TBOPEN &CBLIST NOWRITE SHARE
SET &RC = &LASTCC
ISPEXEC CONTROL ERRORS CANCEL
IF &RC EQ 12 THEN DO
  ISPEXEC SETMSG MSG(MBKS001R)
  GOTO QUIT
  END
IF &RC GT 12 THEN DO
  ISPEXEC SETMSG MSG(MBKS001S)
  GOTO QUIT
  END
IF &RC EQ 8 THEN DO
  IF &CBUPDT EQ YES THEN DO
    ISPEXEC TBCREATE &CBLIST WRITE SHARE +
      NAMES(CBKW1  CBKW2  CBMNO  CBLOC  CBDTE  +
            CBSORT CBROOM CBTMN  CBUSR  CBNW0  +
            CBNW1  CBNW2  CBNW3  CBNW4  CBNW5  +
            CBNW6  CBNW7  CBNW8  CBNW9  CBNW10 +
            CBNW11 CBNW12 CBNW13 CBNW14 CBNW15 +
            CBNW16 CBNW17 CBNW18 CBNW19 CBNW20 +
            CBNW21 CBNW22 CBNW23 CBNW24 CBNW25 +
            CBNW26 CBNW27 CBNW28 CBNW29 CBNW30)
    SET &RC = &LASTCC
    IF &RC NE 0 THEN DO
      ISPEXEC SETMSG MSG(MBKS001S)
      GOTO QUIT
      END
    ISPEXEC SETMSG MSG(MBKS001T)
    END
  ELSE DO
    ISPEXEC SETMSG MSG(MBKS001N)
    GOTO QUIT
    END
  END
ISPEXEC TBSORT &CBLIST +
  FIELDS(CBKW1,C,A CBKW2,C,A CBMNO,C,A CBLOC,C,A)
SET &CBSRTF = &STR( KEY-1 KEY-2 MANUAL LOCATION)
ISPEXEC VPUT (CBSRTF)
SET &CBC = &STR(&&)
SET &CBPDEL =
SET &RC = 0
SET &ZTDTOP = 0
SET &CBUPF =
SET &CBFND = NO
SET &CBSRCH = K1A
SET &CBALW = NO
/* - - - DISPLAY IBM MANUALS TABLE.  - - - - - - - - - - - - - - - - */
LOOP: IF &RC EQ 4 THEN ISPEXEC TBDISPL &CBLIST POSITION(CRP)
ELSE DO
  IF &CBFND EQ NO THEN DO
    ISPEXEC TBTOP &CBLIST
    ISPEXEC TBSKIP &CBLIST NUMBER(&ZTDTOP)
    ISPEXEC VGET (ZVERB ZSCROLLN)
    IF &ZVERB EQ UP THEN ISPEXEC TBSKIP &CBLIST NUMBER(-&ZSCROLLN)
    ELSE IF &ZVERB EQ DOWN THEN ISPEXEC TBSKIP &CBLIST +
      NUMBER(&ZSCROLLN)
    ELSE SET &CBFND = NO
    END
  ISPEXEC TBDISPL &CBLIST PANEL(PBOOK1) POSITION(CRP)
  END
SET &RC = &LASTCC
IF &CMD NE CONT THEN SET &CBALW = NO
IF &CMD EQ CANCEL OR &RC GT 4 THEN GOTO LEAVE
/* - - - CONT COMMAND. - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ CONT THEN DO
  IF &CBALW = YES THEN DO
    ISPEXEC TBTOP &CBLIST
    ISPEXEC TBSKIP &CBLIST NUMBER(&CBFCRP) POSITION(CRP)
    DO WHILE &CBFRC EQ 0
      ISPEXEC TBSKIP &CBLIST POSITION(CRP)
      SET &CBFRC = &LASTCC
      IF &CBFRC EQ 0 THEN DO
        IF &SYSINDEX(&NRSTR(&CBFT),&NRSTR(&CBTMN)) NE 0 THEN -
          SET &CBFRC = 1
        END
      END
    IF &CBFRC EQ 1 THEN DO
      SET &CBFRC = 0
      SET &CBALW = YES
      SET &CBFCRP = &CRP
      END
    ELSE DO
      SET &CBFRC = 8
      ISPEXEC SETMSG MSG(MBKS006Z)
      SET &CBALW = NO
      END
    END
  ELSE ISPEXEC SETMSG MSG(MBKS006X)
  END
/* - - - FIND COMMAND. - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ FIND THEN DO
  ISPEXEC CONTROL DISPLAY SAVE
  ISPEXEC DISPLAY PANEL(PBOOK5)
  SET &CBFRC = &LASTCC
  ISPEXEC CONTROL DISPLAY RESTORE
  IF &CBFRC EQ 0 THEN DO
    SET &CBFND = YES
    IF &CBF1 NE &Z OR &STR(&CBFN) NE &Z THEN DO
      IF &CBF1 NE &Z THEN DO
        SET &CBGL = &LENGTH(&NW)
        IF &CBGL NE 0 THEN DO
          IF &CBGL EQ 1 THEN SET &CBF3 = &STR( &NW)
          ELSE SET &CBF3 = &STR(&NW)
          SET &CBMNO = &STR(&CBF1-&CBF2-&CBF3)
          END
        ELSE IF &CBF2 NE &Z THEN SET &CBMNO = &STR(&CBF1-&CBF2)
        ELSE SET &CBMNO = &STR(&CBF1)
        END
      ELSE SET &CBMNO = &STR(&CBFN)
      IF &SUBSTR(1:1,&CBSRCH) NE M THEN DO
        SET &CBSRCH = MA
        SET &CBSFLD = &STR( CBMNO,C,A)
        SET &CBSRTF = &STR( MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ MA THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBMNO) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBMNO) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS006M)
      END
    ELSE IF &NRSTR(&CBFT) NE &Z THEN DO
      ISPEXEC TBTOP &CBLIST
      SET &CBFRC = &LASTCC
      DO WHILE &CBFRC EQ 0
        ISPEXEC TBSKIP &CBLIST POSITION(CRP)
        SET &CBFRC = &LASTCC
        IF &CBFRC EQ 0 THEN DO
          IF &SYSINDEX(&NRSTR(&CBFT),&NRSTR(&CBTMN)) NE 0 THEN -
            SET &CBFRC = 1
          END
        END
      IF &CBFRC EQ 1 THEN DO
        SET &CBFRC = 0
        SET &CBALW = YES
        SET &CBFCRP = &CRP
        END
      ELSE DO
        SET &CBFRC = 8
        ISPEXEC SETMSG MSG(MBKS006N)
        SET &CBALW = NO
        END
      END
    ELSE IF &STR(&CBFK1) NE &Z THEN DO
      SET &CBKW1 = &STR(&CBFK1)
      IF &SUBSTR(1:2,&CBSRCH) NE K1 THEN DO
        SET &CBSRCH = K1A
        SET &CBSFLD = &STR( CBKW1,C,A CBKW2,C,A CBMNO,C,A)
        SET &CBSRTF = &STR( KEY-1 KEY-2 MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ K1A THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBKW1) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBKW1) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS0061)
      END
    ELSE IF &STR(&CBFK2) NE &Z THEN DO
      SET &CBKW2 = &STR(&CBFK2)
      IF &SUBSTR(1:2,&CBSRCH) NE K2 THEN DO
        SET &CBSRCH = K2A
        SET &CBSFLD = &STR( CBKW2,C,A CBMNO,C,A)
        SET &CBSRTF = &STR( KEY-2 MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ K2A THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBKW2) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBKW2) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS0062)
      END
    ELSE IF &CBV NE &Z THEN DO
      SET &CBGL = &LENGTH(&CBV)
      SET &CBGT = &STR(   &CBV)
      SET &CBGI = &CBGL
      SET &CBGE = &CBGI + 3
      SET &CBG1 = &SUBSTR(&CBGI:&CBGE,&CBGT)
      SET &CBGL = &LENGTH(&CBW)
      SET &CBGT = &STR(  &CBW)
      SET &CBGI = &CBGL
      SET &CBGE = &CBGI + 2
      SET &CBG2 = &SUBSTR(&CBGI:&CBGE,&CBGT)
      SET &CBLOC = &STR(&CBG1 &CBG2)
      IF &SUBSTR(1:1,&CBSRCH) NE L THEN DO
        SET &CBSRCH = LA
        SET &CBSFLD = &STR( CBLOC,C,A CBMNO)
        SET &CBSRTF = &STR( LOCATION MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ LA THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBLOC) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBLOC) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS006L)
      END
    ELSE IF &STR(&CBFR) NE &Z THEN DO
      SET &CBROOM = &STR(&CBFR)
      IF &SUBSTR(1:1,&CBSRCH) NE R THEN DO
        SET &CBSRCH = RA
        SET &CBSFLD = &STR( CBROOM,C,A CBMNO,C,A)
        SET &CBSRTF = &STR( ROOM MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ RA THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBROOM) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBROOM) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS006R)
      END
    ELSE IF &CBFD NE &Z THEN DO
      SET &CBDTE = &CBFD
      SET &CBSORT = &CBFDS
      IF &SUBSTR(1:1,&CBSRCH) NE D THEN DO
        SET &CBSRCH = DA
        SET &CBSFLD = &STR( CBSORT,N,A CBMNO,C,A)
        SET &CBSRTF = &STR( DATE MANUAL)
        ISPEXEC VPUT (CBSRTF)
        ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
        END
      ISPEXEC TBTOP &CBLIST
      IF &CBSRCH EQ DA THEN DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBSORT) NOREAD +
          POSITION(CRP) CONDLIST(GE)
        SET &CBFRC = &LASTCC
        END
      ELSE DO
        ISPEXEC TBSCAN &CBLIST ARGLIST(CBSORT) NOREAD +
          POSITION(CRP) CONDLIST(LE)
        SET &CBFRC = &LASTCC
        END
      IF &CBFRC EQ 8 THEN ISPEXEC SETMSG MSG(MBKS006S)
      END
    ELSE IF &CBFX NE &Z THEN DO
      IF &CBFY NE &Z THEN SET &CBNWT = &STR(&CBFX-&CBFY)
      ELSE SET &CBNWT = &STR(&CBFX)
      ISPEXEC TBTOP &CBLIST
      SET &CBFRC = &LASTCC
      DO WHILE &CBFRC EQ 0
        ISPEXEC TBSKIP &CBLIST POSITION(CRP)
        SET &CBFRC = &LASTCC
        IF &CBNW0 NE 0 THEN DO
          SET &I = 1
          DO WHILE &I LE &CBNW0
            IF &SYSINDEX(&CBNWT,&CBC.CBNW&I) NE 0 THEN DO
              SET &CBFRC = 1
              SET &I = &CBNW0 + 1
              END
            ELSE SET &I = &I + 1
            END
          END
        END
      IF &CBFRC EQ 1 THEN SET &CBFRC = 0
      ELSE DO
        SET &CBFRC = 8
        ISPEXEC SETMSG MSG(MBKS006T)
        END
      END
    IF &CBFRC EQ 8 THEN ISPEXEC TBBOTTOM &CBLIST NOREAD POSITION(CRP)
    END
  END
/* - - - SORT COMMAND. - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ SORT THEN DO
  IF &SFCB1 EQ &Z THEN DO
    SET &SFCB1 = K1
    SET &SFCB2 = K2
    SET &SFCB3 = M
    SET &SFCB4 = L
    SET &SFCB5 =
    SET &SFCB6 =
    END
  SET &CBSFLD =
  SET &CBSRTF =
  SET &CBSRCH = K1A
  SET &I = 1
  DO WHILE &I LE 6
    SET &SFCB = SFCB&I
    SET &S = &&&SFCB
    IF &S EQ K1 THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = K1A
      SET &CBSFLD = &STR(&CBSFLD CBKW1,C,A)
      SET &CBSRTF = &STR(&CBSRTF KEY-1)
      END
    ELSE IF &S EQ K1I THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = K1D
      SET &CBSFLD = &STR(&CBSFLD CBKW1,C,D)
      SET &CBSRTF = &STR(&CBSRTF KEY-1(D))
      END
    ELSE IF &S EQ K2 THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = K2A
      SET &CBSFLD = &STR(&CBSFLD CBKW2,C,A)
      SET &CBSRTF = &STR(&CBSRTF KEY-2)
      END
    ELSE IF &S EQ K2I THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = K2D
      SET &CBSFLD = &STR(&CBSFLD CBKW2,C,D)
      SET &CBSRTF = &STR(&CBSRTF KEY-2(D))
      END
    ELSE IF &S EQ M THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = MA
      SET &CBSFLD = &STR(&CBSFLD CBMNO,C,A)
      SET &CBSRTF = &STR(&CBSRTF MANUAL)
      END
    ELSE IF &S EQ MI THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = MD
      SET &CBSFLD = &STR(&CBSFLD CBMNO,C,D)
      SET &CBSRTF = &STR(&CBSRTF MANUAL(D))
      END
    ELSE IF &S EQ L THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = LA
      SET &CBSFLD = &STR(&CBSFLD CBLOC,C,A)
      SET &CBSRTF = &STR(&CBSRTF LOCATION)
      END
    ELSE IF &S EQ LI THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = LD
      SET &CBSFLD = &STR(&CBSFLD CBLOC,C,D)
      SET &CBSRTF = &STR(&CBSRTF LOCATION(D))
      END
    ELSE IF &S EQ D THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = DA
      SET &CBSFLD = &STR(&CBSFLD CBSORT,N,A)
      SET &CBSRTF = &STR(&CBSRTF DATE)
      END
    ELSE IF &S EQ DI THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = DD
      SET &CBSFLD = &STR(&CBSFLD CBSORT,N,D)
      SET &CBSRTF = &STR(&CBSRTF DATE(D))
      END
    ELSE IF &S EQ R THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = RA
      SET &CBSFLD = &STR(&CBSFLD CBROOM,C,A)
      SET &CBSRTF = &STR(&CBSRTF ROOM)
      END
    ELSE IF &S EQ RI THEN DO
      IF &I EQ 1 THEN SET &CBSRCH = RD
      SET &CBSFLD = &STR(&CBSFLD CBROOM,C,D)
      SET &CBSRTF = &STR(&CBSRTF ROOM(D))
      END
    SET &I = &I + 1
    END
  ISPEXEC VPUT (CBSRTF)
  ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
  ISPEXEC SETMSG MSG(MBKS005R)
  END
/* - - - SAVE COMMAND. - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ SAVE THEN DO
  IF &CBUPDT EQ YES THEN DO
    ISPEXEC TBSORT &CBLIST +
      FIELDS(CBKW1,C,A CBKW2,C,A CBMNO,C,A CBLOC,C,A)
    ISPEXEC TBSAVE &CBLIST REPLCOPY PAD(15)
    SET &CBUPF =
    ISPEXEC TBSORT &CBLIST FIELDS(&CBSFLD)
    ISPEXEC SETMSG MSG(MBKS005S)
    END
  ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
  ELSE ISPEXEC SETMSG MSG(MBKS003T)
  END
/* - - - INPUT COMMAND.  - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ INPUT THEN DO
  IF &CBUPDT EQ YES THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    DO WHILE &CBIN GE 1
      ISPEXEC TBVCLEAR &CBLIST
      SET &CBN1 =
      SET &CBN2 =
      SET &NW =
      SET &CBGNS =
      SET &CBX =
      SET &CBY =
      SET &CBNW0 = 0
      SET &I = 1
      DO WHILE &I LE 30
        SET CBNW&I =
        SET &I = &I + 1
        END
      ISPEXEC DISPLAY PANEL(PBOOK2)
      IF &LASTCC EQ 0 THEN DO
        IF &STR(&CBGNS) NE &Z THEN SET &CBMNO = &STR(&CBGNS)
        ELSE DO
          SET &CBGL = &LENGTH(&NW)
          IF &CBGL EQ 1 THEN SET &CBN3 = &STR( &NW)
          ELSE SET &CBN3 = &STR(&NW)
          SET &CBMNO = &STR(&CBN1-&CBN2-&CBN3)
          END
        SET &CBGL = &LENGTH(&CBX)
        SET &CBGT = &STR(   &CBX)
        SET &CBGI = &CBGL
        SET &CBGE = &CBGI + 3
        SET &CBG1 = &SUBSTR(&CBGI:&CBGE,&CBGT)
        SET &CBGL = &LENGTH(&CBY)
        SET &CBGT = &STR(  &CBY)
        SET &CBGI = &CBGL
        SET &CBGE = &CBGI + 2
        SET &CBG2 = &SUBSTR(&CBGI:&CBGE,&CBGT)
        SET &CBLOC = &STR(&CBG1 &CBG2)
        IF &CBZ = YES THEN DO
          SET &I = 1
          DO WHILE &I LE 30
            IF &I LE 9 THEN SET &SSS = &STR(0&I)
            ELSE SET &SSS = &STR(&I)
            SET T&SSS =
            SET T&SSS.1 =
            SET T&SSS.2 =
            SET &I = &I + 1
            END
          ISPEXEC DISPLAY PANEL(PBOOK3)
          IF &LASTCC EQ 0 THEN DO
            SET &I = 1
            SET &IP = 1
            DO WHILE &I LE 30
              IF &I LE 9 THEN SET &SSS = &STR(0&I)
              ELSE SET &SSS = &STR(&I)
              SET &SST = &STR(&CBC.T&SSS)
              SET &SSG = &STR(&CBC.T&SSS.1)
              SET &SSN = &STR(&CBC.T&SSS.2)
              IF &SSG ¬= &Z THEN DO
                SET &CBNW0 = &CBNW0 + 1
                IF &SST = &Z THEN SET &SST = &STR(P)
                SET CBNW&IP = &STR(&SSG-&SSN &SST)
                SET &IP = &IP + 1
                END
              SET &I = &I + 1
              END
            IF &CBNW0 GT 1 THEN DO
              SET &I = 1
              DO WHILE &I LT &CBNW0
                SET &IP = &I
                SET &IN = &IP + 1
                DO WHILE &IN LE &CBNW0
                  IF &STR(&CBC.CBNW&IP) GT &STR(&CBC.CBNW&IN) THEN DO
                    SET CBPASS = &STR(&CBC.CBNW&IN)
                    SET CBNW&IN = &STR(&CBC.CBNW&IP)
                    SET CBNW&IP = &STR(&CBPASS)
                    END
                  SET &IN = &IN + 1
                  END
                SET &I = &I + 1
                END
              END
            END
          END
        SET &CBUSR = &ZUSER
        ISPEXEC TBADD &CBLIST ORDER
        SET &CBUPF = YES
        END
      SET &CBIN = &CBIN - 1
      END
    ISPEXEC CONTROL DISPLAY RESTORE
    END
  ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
  ELSE ISPEXEC SETMSG MSG(MBKS003T)
  END
/* - - - TNL COMMAND.  - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ TNL THEN DO
  IF &CBUPDT EQ YES THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    DO WHILE &CBIN GE 1
      ISPEXEC TBVCLEAR &CBLIST
      SET &CBN1 =
      SET &CBN2 =
      SET &NW =
      SET &CBGNS =
      SET &CBX =
      SET &CBY =
      SET &CBNW0 = 0
      SET &I = 1
      DO WHILE &I LE 30
        SET CBNW&I =
        IF &I LE 9 THEN SET &SSS = &STR(0&I)
        ELSE SET &SSS = &STR(&I)
        SET T&SSS.1 =
        SET T&SSS.2 =
        SET &I = &I + 1
        END
      ISPEXEC DISPLAY PANEL(PBOOK6)
      IF &LASTCC EQ 0 THEN DO
        IF &STR(&CBGNS) NE &Z THEN SET &CBMNO = &STR(&CBGNS)
        ELSE DO
          SET &CBGL = &LENGTH(&NW)
          IF &CBGL EQ 1 THEN SET &CBN3 = &STR( &NW)
          ELSE SET &CBN3 = &STR(&NW)
          SET &CBMNO = &STR(&CBN1-&CBN2-&CBN3)
          END
        SET &CBGL = &LENGTH(&CBX)
        SET &CBGT = &STR(   &CBX)
        SET &CBGI = &CBGL
        SET &CBGE = &CBGI + 3
        SET &CBG1 = &SUBSTR(&CBGI:&CBGE,&CBGT)
        SET &CBGL = &LENGTH(&CBY)
        SET &CBGT = &STR(  &CBY)
        SET &CBGI = &CBGL
        SET &CBGE = &CBGI + 2
        SET &CBG2 = &SUBSTR(&CBGI:&CBGE,&CBGT)
        SET &CBLOC = &STR(&CBG1 &CBG2)
        SET &I = 1
        SET &IP = 1
        DO WHILE &I LE 30
          IF &I LE 9 THEN SET &SSS = &STR(0&I)
          ELSE SET &SSS = &STR(&I)
          SET &SSG = &STR(&CBC.T&SSS.1)
          SET &SSN = &STR(&CBC.T&SSS.2)
          IF &SSG ¬= &Z THEN DO
            SET &CBNW0 = &CBNW0 + 1
            SET CBNW&IP = &STR(&SSG-&SSN P)
            SET &IP = &IP + 1
            END
          SET &I = &I + 1
          END
        IF &CBNW0 GT 1 THEN DO
          SET &I = 1
          DO WHILE &I LT &CBNW0
            SET &IP = &I
            SET &IN = &IP + 1
            DO WHILE &IN LE &CBNW0
              IF &STR(&CBC.CBNW&IP) GT &STR(&CBC.CBNW&IN) THEN DO
                SET CBPASS = &STR(&CBC.CBNW&IN)
                SET CBNW&IN = &STR(&CBC.CBNW&IP)
                SET CBNW&IP = &STR(&CBPASS)
                END
              SET &IN = &IN + 1
              END
            SET &I = &I + 1
            END
          END
        SET &CBTMN = &STR(*** MANUAL NOT RECEIVED ***)
        SET &CBKW1 =
        SET &CBKW2 =
        SET &CBUSR = &ZUSER
        ISPEXEC TBADD &CBLIST ORDER
        SET &CBUPF = YES
        END
      SET &CBIN = &CBIN - 1
      END
    ISPEXEC CONTROL DISPLAY RESTORE
    END
  ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
  ELSE ISPEXEC SETMSG MSG(MBKS003T)
  END
/* - - - LIST COMMAND. - - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &CMD EQ LIST OR &CMD EQ FULL THEN DO
  ERROR DO
    ERROR OFF
    FREE ATTR(VBA)
    ATTR VBA RECFM(V B A) LRECL(137) BLKSIZE(6356) DSORG(PS)
    ALLOC F(ISPFILE) DSN(BKTEMP.LISTING) REUSE CATALOG +
      USING(VBA) SP(5 5) TRACKS
    SET &CBPDEL = DELETE
    END
  ALLOC F(ISPFILE) DSN(BKTEMP.LISTING) OLD REUSE
  ERROR OFF
  IF &CMD EQ FULL THEN SET &CBPFULL = YES
  ELSE SET &CBPFULL = NO
  ISPEXEC FTOPEN
  ISPEXEC FTINCL SBOOK0
  ISPEXEC FTCLOSE
  SET &CBPOPT =
  IF &CBPDEST NE &Z THEN SET &CBPOPT = &STR(DEST(&CBPDEST))
  IF &CBPCLAS NE &Z THEN SET &CBPOPT = &STR(&CBPOPT CLASS(&CBPCLAS))
  PRINTO BKTEMP.LISTING &CBPOPT NOHEADING
  ISPEXEC SETMSG MSG(MBKS003P)
  END
/* - - - D LINE-COMMAND. - - - - - - - - - - - - - - - - - - - - - - */
ELSE IF &ZTDSELS GE 1 THEN DO
  IF &ACT EQ D THEN DO
    IF &CBUPDT EQ YES THEN -
      DO WHILE &N GE 1
        ISPEXEC TBDELETE &CBLIST
        ISPEXEC TBSKIP &CBLIST
        IF &LASTCC NE 0 THEN SET &N = 0
        ELSE SET &N = &N - 1
        SET &CBUPF = YES
        END
    ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
    ELSE ISPEXEC SETMSG MSG(MBKS003T)
    END
/* - - - R LINE-COMMAND. - - - - - - - - - - - - - - - - - - - - - - */
  ELSE IF &ACT EQ R THEN DO
    IF &CBUPDT EQ YES THEN -
      DO WHILE &N GE 1
        ISPEXEC TBADD &CBLIST ORDER
        SET &N = &N - 1
        SET &CBUPF = YES
        END
    ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
    ELSE ISPEXEC SETMSG MSG(MBKS003T)
    END
/* - - - S LINE-COMMAND. - - - - - - - - - - - - - - - - - - - - - - */
  ELSE IF &ACT EQ S THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    DO WHILE &N GE 1
      ISPEXEC DISPLAY PANEL(PBOOK4)
      IF &LASTCC NE 0 THEN SET &N = 0
      ELSE SET &N = &N - 1
      IF &N NE 0 THEN DO
        ISPEXEC TBSKIP &CBLIST
        IF &LASTCC NE 0 THEN SET &N = 0
        END
      END
    ISPEXEC CONTROL DISPLAY RESTORE
    END
/* - - - U LINE-COMMAND. - - - - - - - - - - - - - - - - - - - - - - */
  ELSE IF &ACT EQ U THEN DO
    IF &CBUPDT EQ YES THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      DO WHILE &N GE 1
        SET CBKW1X = &STR(&CBKW1)
        SET CBKW2X = &STR(&CBKW2)
        SET CBMNOX = &STR(&CBMNO)
        SET CBLOCX = &STR(&CBLOC)
        SET CBROOMX = &STR(&CBROOM)
        SET CBTMNX = &NRSTR(&CBTMN)
        SET CBNW0X = &CBNW0
        SET &I = 1
        DO WHILE &I LE 30
          SET CBNW&I.X = &STR(&CBC.CBNW&I)
          SET &I = &I + 1
          END
        SET &CBGT = &SUBSTR(5:5,&CBMNO)
        IF &STR(&CBGT) = &STR(-) THEN DO
          SET &CBN1 = &SUBSTR(1:4,&CBMNO)
          SET &CBN2 = &SUBSTR(6:9,&CBMNO)
          SET &CBGV = &SUBSTR(11:11,&CBMNO)
          IF &CBGV = &STR( ) THEN SET &NW = &SUBSTR(12:12,&CBMNO)
          ELSE SET &NW = &SUBSTR(11:12,&CBMNO)
          SET &CBGNS =
          END
        ELSE DO
          SET &CBN1 =
          SET &CBN2 =
          SET &NW =
          SET &CBGNS = &STR(&CBMNO)
          END
        SET &CBGV = &SUBSTR(1:4,&CBLOC)
        SET &CBX = &CBGV
        SET &CBGV = &SUBSTR(6:8,&CBLOC)
        SET &CBY = &CBGV
        ISPEXEC DISPLAY PANEL(PBOOK2)
        SET &RCLC = &LASTCC
        IF &RCLC EQ 0 THEN DO
          IF &STR(&CBGNS) NE &Z THEN SET &CBMNO = &STR(&CBGNS)
          ELSE DO
            SET &CBGL = &LENGTH(&NW)
            IF &CBGL EQ 1 THEN SET &CBN3 = &STR( &NW)
            ELSE SET &CBN3 = &STR(&NW)
            SET &CBMNO = &STR(&CBN1-&CBN2-&CBN3)
            END
          SET &CBGL = &LENGTH(&CBX)
          SET &CBGT = &STR(   &CBX)
          SET &CBGI = &CBGL
          SET &CBGE = &CBGI + 3
          SET &CBG1 = &SUBSTR(&CBGI:&CBGE,&CBGT)
          SET &CBGL = &LENGTH(&CBY)
          SET &CBGT = &STR(  &CBY)
          SET &CBGI = &CBGL
          SET &CBGE = &CBGI + 2
          SET &CBG2 = &SUBSTR(&CBGI:&CBGE,&CBGT)
          SET &CBLOC = &STR(&CBG1 &CBG2)
          IF &CBZ = YES THEN DO
            SET &I = 1
            DO WHILE &I LE 30
              IF &I LE 9 THEN SET &SSS = &STR(0&I)
              ELSE SET &SSS = &STR(&I)
              SET T&SSS =
              SET T&SSS.1 =
              SET T&SSS.2 =
              SET &I = &I + 1
              END
            IF &CBNW0 NE 0 THEN DO
              SET &I = 1
              DO WHILE &I LE &CBNW0
                IF &I LE 9 THEN SET &SSS = &STR(0&I)
                ELSE SET &SSS = &STR(&I)
                SET T&SSS.1 = &SUBSTR(1:4,&CBC.CBNW&I)
                SET T&SSS.2 = &SUBSTR(6:9,&CBC.CBNW&I)
                SET T&SSS = &SUBSTR(11:11,&CBC.CBNW&I)
                SET &I = &I + 1
                END
              END
            ISPEXEC DISPLAY PANEL(PBOOK3)
            SET &RCNC = &LASTCC
            IF &RCNC EQ 0 THEN DO
              SET &CBNW0 = 0
              SET &I = 1
              DO WHILE &I LE 30
                SET CBNW&I =
                SET &I = &I + 1
                END
              SET &I = 1
              SET &IP = 1
              DO WHILE &I LE 30
                IF &I LE 9 THEN SET &SSS = &STR(0&I)
                ELSE SET &SSS = &STR(&I)
                SET &SST = &STR(&CBC.T&SSS)
                SET &SSG = &STR(&CBC.T&SSS.1)
                SET &SSN = &STR(&CBC.T&SSS.2)
                IF &SSG ¬= &Z THEN DO
                  SET &CBNW0 = &CBNW0 + 1
                  IF &SST = &Z THEN SET &SST = &STR(P)
                  SET CBNW&IP = &STR(&SSG-&SSN &SST)
                  SET &IP = &IP + 1
                  END
                SET &I = &I + 1
                END
              IF &CBNW0 GT 1 THEN DO
                SET &I = 1
                DO WHILE &I LT &CBNW0
                  SET &IP = &I
                  SET &IN = &IP + 1
                  DO WHILE &IN LE &CBNW0
                    IF &STR(&CBC.CBNW&IP) GT &STR(&CBC.CBNW&IN) THEN DO
                      SET CBPASS = &STR(&CBC.CBNW&IN)
                      SET CBNW&IN = &STR(&CBC.CBNW&IP)
                      SET CBNW&IP = &STR(&CBPASS)
                      END
                    SET &IN = &IN + 1
                    END
                  SET &I = &I + 1
                  END
                END
              END
            ELSE IF &RCNC GT 8 THEN GOTO UPDX
            ELSE ISPEXEC SETMSG MSG(MBKS004U)
            END
          IF &STR(&CBMNOX) ¬= &STR(&CBMNO) THEN DO
            SET &CBNW0 = 0
            SET &I = 1
            DO WHILE &I LE 30
              SET CBNW&I =
              SET &I = &I + 1
              END
            GOTO UPDU
            END
          IF &STR(&CBKW1X) ¬= &STR(&CBKW1) THEN GOTO UPDU
          IF &STR(&CBKW2X) ¬= &STR(&CBKW2) THEN GOTO UPDU
          IF &STR(&CBLOCX) ¬= &STR(&CBLOC) THEN GOTO UPDU
          IF &STR(&CBROOMX) ¬= &STR(&CBROOM) THEN GOTO UPDU
          IF &NRSTR(&CBTMNX) ¬= &NRSTR(&CBTMN) THEN GOTO UPDU
          IF &CBNW0X ¬= &CBNW0 THEN GOTO UPDU
          SET &I = 1
          DO WHILE &I LE 30
            IF &STR(&CBC.CBNW&I.X) ¬= &STR(&CBC.CBNW&I) THEN GOTO UPDU
            SET &I = &I + 1
            END
          ISPEXEC SETMSG MSG(MBKS004M)
          GOTO UPDN
UPDU:     SET &CBUSR = &ZUSER
          ISPEXEC TBPUT &CBLIST ORDER
          ISPEXEC SETMSG MSG(MBKS004S)
          SET &CBUPF = YES
          END
        ELSE IF &RCLC GT 8 THEN GOTO UPDX
        ELSE ISPEXEC SETMSG MSG(MBKS004N)
UPDN:   IF &N EQ 1 THEN GOTO UPDX
        ISPEXEC TBSKIP &CBLIST
        IF &LASTCC NE 0 THEN SET &N = 0
        ELSE SET &N = &N - 1
        END
UPDX: ISPEXEC CONTROL DISPLAY RESTORE
      END
    ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
    ELSE ISPEXEC SETMSG MSG(MBKS003T)
    END
/* - - - T LINE-COMMAND. - - - - - - - - - - - - - - - - - - - - - - */
  ELSE IF &ACT EQ T THEN DO
    IF &CBUPDT EQ YES THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      DO WHILE &N GE 1
        IF &NRSTR(&CBTMN) NE &STR(*** MANUAL NOT RECEIVED ***) THEN -
          GOTO TNLN
        SET CBMNOX = &STR(&CBMNO)
        SET CBLOCX = &STR(&CBLOC)
        SET CBROOMX = &STR(&CBROOM)
        SET CBNW0X = &CBNW0
        SET &I = 1
        DO WHILE &I LE 30
          SET CBNW&I.X = &STR(&CBC.CBNW&I)
          SET &I = &I + 1
          END
        SET &CBGT = &SUBSTR(5:5,&CBMNO)
        IF &STR(&CBGT) = &STR(-) THEN DO
          SET &CBN1 = &SUBSTR(1:4,&CBMNO)
          SET &CBN2 = &SUBSTR(6:9,&CBMNO)
          SET &CBGV = &SUBSTR(11:11,&CBMNO)
          IF &CBGV = &STR( ) THEN SET &NW = &SUBSTR(12:12,&CBMNO)
          ELSE SET &NW = &SUBSTR(11:12,&CBMNO)
          SET &CBGNS =
          END
        ELSE DO
          SET &CBN1 =
          SET &CBN2 =
          SET &NW =
          SET &CBGNS = &STR(&CBMNO)
          END
        SET &CBGV = &SUBSTR(1:4,&CBLOC)
        SET &CBX = &CBGV
        SET &CBGV = &SUBSTR(6:8,&CBLOC)
        SET &CBY = &CBGV
        SET &I = 1
        DO WHILE &I LE 30
          IF &I LE 9 THEN SET &SSS = &STR(0&I)
          ELSE SET &SSS = &STR(&I)
          SET T&SSS.1 =
          SET T&SSS.2 =
          SET &I = &I + 1
          END
        IF &CBNW0 NE 0 THEN DO
          SET &I = 1
          DO WHILE &I LE &CBNW0
            IF &I LE 9 THEN SET &SSS = &STR(0&I)
            ELSE SET &SSS = &STR(&I)
            SET T&SSS.1 = &SUBSTR(1:4,&CBC.CBNW&I)
            SET T&SSS.2 = &SUBSTR(6:9,&CBC.CBNW&I)
            SET &I = &I + 1
            END
          END
        ISPEXEC DISPLAY PANEL(PBOOK6)
        SET &RCLC = &LASTCC
        IF &RCLC EQ 0 THEN DO
          IF &STR(&CBGNS) NE &Z THEN SET &CBMNO = &STR(&CBGNS)
          ELSE DO
            SET &CBGL = &LENGTH(&NW)
            IF &CBGL EQ 1 THEN SET &CBN3 = &STR( &NW)
            ELSE SET &CBN3 = &STR(&NW)
            SET &CBMNO = &STR(&CBN1-&CBN2-&CBN3)
            END
          SET &CBGL = &LENGTH(&CBX)
          SET &CBGT = &STR(   &CBX)
          SET &CBGI = &CBGL
          SET &CBGE = &CBGI + 3
          SET &CBG1 = &SUBSTR(&CBGI:&CBGE,&CBGT)
          SET &CBGL = &LENGTH(&CBY)
          SET &CBGT = &STR(  &CBY)
          SET &CBGI = &CBGL
          SET &CBGE = &CBGI + 2
          SET &CBG2 = &SUBSTR(&CBGI:&CBGE,&CBGT)
          SET &CBLOC = &STR(&CBG1 &CBG2)
          SET &CBNW0 = 0
          SET &I = 1
          DO WHILE &I LE 30
            SET CBNW&I =
            SET &I = &I + 1
            END
          SET &I = 1
          SET &IP = 1
          DO WHILE &I LE 30
            IF &I LE 9 THEN SET &SSS = &STR(0&I)
            ELSE SET &SSS = &STR(&I)
            SET &SSG = &STR(&CBC.T&SSS.1)
            SET &SSN = &STR(&CBC.T&SSS.2)
            IF &SSG ¬= &Z THEN DO
              SET &CBNW0 = &CBNW0 + 1
              SET CBNW&IP = &STR(&SSG-&SSN P)
              SET &IP = &IP + 1
              END
            SET &I = &I + 1
            END
          IF &CBNW0 GT 1 THEN DO
            SET &I = 1
            DO WHILE &I LT &CBNW0
              SET &IP = &I
              SET &IN = &IP + 1
              DO WHILE &IN LE &CBNW0
                IF &STR(&CBC.CBNW&IP) GT &STR(&CBC.CBNW&IN) THEN DO
                  SET CBPASS = &STR(&CBC.CBNW&IN)
                  SET CBNW&IN = &STR(&CBC.CBNW&IP)
                  SET CBNW&IP = &STR(&CBPASS)
                  END
                SET &IN = &IN + 1
                END
              SET &I = &I + 1
              END
            END
          IF &STR(&CBMNOX) ¬= &STR(&CBMNO) THEN GOTO TNLU
          IF &STR(&CBLOCX) ¬= &STR(&CBLOC) THEN GOTO TNLU
          IF &STR(&CBROOMX) ¬= &STR(&CBROOM) THEN GOTO TNLU
          IF &CBNW0X ¬= &CBNW0 THEN GOTO TNLU
          SET &I = 1
          DO WHILE &I LE 30
            IF &STR(&CBC.CBNW&I.X) ¬= &STR(&CBC.CBNW&I) THEN GOTO TNLU
            SET &I = &I + 1
            END
          ISPEXEC SETMSG MSG(MBKS004M)
          GOTO TNLN
TNLU:     SET &CBUSR = &ZUSER
          ISPEXEC TBPUT &CBLIST ORDER
          ISPEXEC SETMSG MSG(MBKS004S)
          SET &CBUPF = YES
          END
        ELSE IF &RCLC GT 8 THEN GOTO TNLX
        ELSE ISPEXEC SETMSG MSG(MBKS004N)
TNLN:   IF &N EQ 1 THEN GOTO TNLX
        ISPEXEC TBSKIP &CBLIST
        IF &LASTCC NE 0 THEN SET &N = 0
        ELSE SET &N = &N - 1
        END
TNLX: ISPEXEC CONTROL DISPLAY RESTORE
      END
    ELSE IF &CBAVL = YES THEN ISPEXEC SETMSG MSG(MBKS003U)
    ELSE ISPEXEC SETMSG MSG(MBKS003T)
    END
  END
GOTO LOOP
/* - - - END-CANCEL COMMANDS.  - - - - - - - - - - - - - - - - - - - */
LEAVE: FREE F(ISPFILE) ATTR(VBA) &CBPDEL
IF &CMD EQ CANCEL THEN DO
  ISPEXEC TBEND &CBLIST
  ISPEXEC SETMSG MSG(MBKS002C)
  END
ELSE IF &CBUPF EQ &Z THEN DO
  ISPEXEC TBEND &CBLIST
  ISPEXEC SETMSG MSG(MBKS002N)
  END
ELSE DO
  ISPEXEC TBSORT &CBLIST +
    FIELDS(CBKW1,C,A CBKW2,C,A CBMNO,C,A CBLOC,C,A)
  IF &CBUPDT EQ YES THEN DO
    ISPEXEC TBCLOSE &CBLIST REPLCOPY PAD(15)
    ISPEXEC SETMSG MSG(MBKS002S)
    END
  ELSE DO
    ISPEXEC TBCLOSE &CBLIST
    ISPEXEC SETMSG MSG(MBKS002N)
    END
  END
QUIT: ISPEXEC LIBDEF ISPTLIB LIBRARY
ISPEXEC LIBDEF ISPTABL LIBRARY
FREE F(CBTABL)
