ISREDIT  MACRO (APARM)
/* * --- DELETE FROM HERE ****************************************** */
/* * YOU MUST ADJUST THE "PASSWORD" (REPLACE IT BY YOUR OWN)       * */
/* * DEFINED HERE BELOW BEFORE YOU MAY EXECUTE THIS PROCEDURE.     * */
/* * THEN, DELETE ALL INDICATED STATEMENTS, GOOD LUCK.             * */
/* ***************************************************************** */
WRITE *** MANDATORY - ISPF CLIST "UPDTMACS" INTERVENTION REQUIRED ***
EXIT
/* * --- UP TO HERE WHEN ADJUSTMENT IS DONE ************************ */
/*  MACRO NAME : UPDTMACS                             (MOINIL P.A.) */
/*    FUNCTION : CREATE-UPDATE-DISPLAY AVAILABLE ADDITIONAL LOCAL   */
/*               EDIT MACROS TABLE.                                 */
/*      SYNTAX : UPDTMACS                                           */
/*       DEBUG : UPDTMACS LIST                                      */
  IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST SYMLIST CONLIST
  ISPEXEC DISPLAY PANEL(PAJEMP)
  IF &EMPASS ¬= PASSWORD THEN DO
    SET &ZEDSMSG = UPDATE REJECT
    SET &ZEDLMSG = SORRY ... YOU MAY NOT UPDATE THE TABLE
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  SET &EMPASS =
START: +
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC TBOPEN EDITMACS WRITE
  SET &EMRCC = &LASTCC
  IF &EMRCC = 12 THEN DO
    SET &ZEDSMSG = TABLE IN USE
    SET &ZEDLMSG = ENQ FAILED DUE TO TABLE IN USE BY ANOTHER USER OR +
                   CURRENT USER
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &EMRCC = 16 OR &EMRCC = 20 THEN DO
    SET &ZEDSMSG = TBOPEN ERROR
    SET &ZEDLMSG = TBOPEN RETURN CODE &EMRCC ... ABORT
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &EMRCC = 8 THEN DO
    SET &MACNAME = CREATE
    SET &MACDESC = THE TABLE "EDITMACS" DOES NOT EXIST BUT +
                   WE WILL MAKE ONE
    ISPEXEC TBCREATE EDITMACS NAMES(MACNAME MACDESC) WRITE
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBCREATE ERROR
      SET &ZEDLMSG = TBCREATE RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    ISPEXEC TBADD EDITMACS
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBADD ERROR
      SET &ZEDLMSG = TBADD RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    SET &EMUPDT = YES
    END
  ELSE SET &EMUPDT = NO
SORT: +
  ISPEXEC TBSORT EDITMACS FIELDS(MACNAME)
  SET &EMTRC = &LASTCC
  IF &EMTRC ¬= 0 THEN DO
    SET &ZEDSMSG = TBSORT ERROR
    SET &ZEDLMSG = TBSORT RETURN CODE &EMTRC ... ABORT
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  SET &EMSORT = NO
NORMAL: +
  ISPEXEC TBDISPL EDITMACS PANEL(PAJEMU) POSITION(EMCRP)
  SET &EMTRC = &LASTCC
  IF &EMTRC = 8 THEN GOTO LEAVE
  IF &EMTRC = 12 OR &EMTRC = 20 THEN DO
    SET &ZEDSMSG = TBDISPL ERROR
    SET &ZEDLMSG = TBDISPL RETURN CODE &EMTRC ... ABORT
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &ZCMD = CANCEL THEN GOTO QUIT
  IF &ZTDSELS = 0 THEN GOTO NORMAL
  IF &EMOPTN = S THEN DO
    ISREDIT &MACNAME HELP
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = HELP ERROR
      SET &ZEDLMSG = DISPLAY HELP HAS GIVEN BACK A &EMTRC RETURN CODE
      ISPEXEC SETMSG MSG(ISRZ000)
      END
    END
  ELSE IF &EMOPTN = A OR &EMOPTN = R THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    IF &EMOPTN = A THEN DO
      SET &MACNAME =
      SET &MACDESC =
      END
    ISPEXEC DISPLAY PANEL(PAJEMA)
    ISPEXEC TBADD EDITMACS
    SET &EMTRC = &LASTCC
    ISPEXEC CONTROL DISPLAY RESTORE
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBADD ERROR
      SET &ZEDLMSG = TBADD RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    SET &EMSORT = YES
    END
  ELSE IF &EMOPTN = D THEN DO
    ISPEXEC TBDELETE EDITMACS
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBDELETE ERROR
      SET &ZEDLMSG = TBDELETE RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    SET &EMSORT = YES
    END
  ELSE IF &EMOPTN = U THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    ISPEXEC DISPLAY PANEL(PAJEMA)
    ISPEXEC TBPUT EDITMACS
    SET &EMTRC = &LASTCC
    ISPEXEC CONTROL DISPLAY RESTORE
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBPUT ERROR
      SET &ZEDLMSG = TBPUT RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    END
  ELSE DO
    SET &ZEDSMSG = INVALID OPTION
    SET &ZEDLMSG = VALID OPTIONS ARE "A", "D", "R", "S" OR "U"
    ISPEXEC SETMSG MSG(ISRZ000)
    END
  IF &EMSORT = NO THEN GOTO NORMAL
  SET &EMUPDT = YES
  GOTO SORT
LEAVE: +
  IF &EMUPDT = YES THEN DO
    ISPEXEC TBSORT EDITMACS FIELDS(MACNAME)
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBSORT ERROR
      SET &ZEDLMSG = TBSORT RETURN CODE &EMTRC ... ABORT
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    ISPEXEC TBCLOSE EDITMACS REPLCOPY PAD(15)
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBCLOSE ERROR
      SET &ZEDLMSG = TBCLOSE RETURN CODE &EMTRC ... ABORT
      END
    ELSE DO
      SET &ZEDSMSG = UPDATES SAVED
      SET &ZEDLMSG = EVERYTHING ENDS NORMALLY AND UPDATES ARE SAVED
      END
    END
  ELSE DO
    ISPEXEC TBEND EDITMACS
    SET &EMTRC = &LASTCC
    IF &EMTRC ¬= 0 THEN DO
      SET &ZEDSMSG = TBEND ERROR
      SET &ZEDLMSG = TBEND RETURN CODE &EMTRC ... ABORT BUT NO UPDATES
      END
    ELSE DO
      SET &ZEDSMSG = NONE DONE
      SET &ZEDLMSG = ENDED NORMALLY BUT NO UPDATES WERE ENTERED
      END
    END
  ISPEXEC SETMSG MSG(ISRZ000)
  EXIT CODE(0)
QUIT: +
  ISPEXEC TBEND EDITMACS
  SET &EMTRC = &LASTCC
  IF &EMTRC ¬= 0 THEN DO
    SET &ZEDSMSG = TBEND ERROR
    SET &ZEDLMSG = TBEND RETURN CODE &EMTRC ... ABORT
    END
  ELSE DO
    SET &ZEDSMSG = CANCEL DONE
    SET &ZEDLMSG = IMMEDIATE STOP AND UPDATES ARE NOT SAVED
    END
  ISPEXEC SETMSG MSG(ISRZ000)
  EXIT CODE(12)
