ISREDIT  MACRO (APARM,BPARM) NOPROCESS
/* * --- DELETE FROM HERE ****************************************** */
/* * BEFORE YOU MAY EXECUTE THIS PROCEDURE :                       * */
/* * 1. VERIFY THE PRE-REQUISITE MODULES INTALLATION. SEE BELOW.   * */
/* * 2. ADJUST THE TASKLIB OPERAND OF THE "DOPROG" COMMAND         * */
/* *    (REPLACE "TARGET.LINKLIB" BY YOUR DATA-SET NAME).          * */
/* *    DO A 'FIND TASKLIB' TO GET IT BELOW.                       * */
/* * THEN, DELETE ALL INDICATED STATEMENTS, GOOD LUCK.             * */
/* ***************************************************************** */
WRITE *** MANDATORY - ISPF CLIST "GETPAN" INTERVENTION REQUIRED ***
EXIT
/* * --- UP TO HERE WHEN ADJUSTMENT IS DONE ************************ */
/*  MACRO NAME : GETPAN                                             */
/*    FUNCTION : COPY AN ISPF-PANEL IMAGE (EASIER TO INSERT IN A    */
/*               BOOK TEXT)                                         */
/*      SYNTAX : GETPAN PANEL-ID                                    */
/*               GETPAN PANEL-ID DS-NAME                            */
/*               GETPAN HELP OR H                                   */
/*               OPERANDS : 1. PANEL-ID NAME                        */
/*                          2. ISPF-PANELS FULLY QUALIFIED DATA-SET */
/*       DEBUG : GETPAN PANEL-ID LIST                               */
/*      AUTHOR : MOINIL P.A.                                        */
/* PRE-REQUISITE MODULES INSTALLATION :                             */
/*               DOPROG TSO-COMMAND AND TUTOLIST PROGRAM.           */
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTGPAN)
      SET &GPCC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&GPCC)
      END
  IF &STR(&BPARM.) = &STR(LIST) THEN DO
    CONTROL LIST CONLIST SYMLIST
    SET &BPARM = &Z
    END
  IF &STR(&APARM.) = &Z THEN DO
    SET ZEDSMSG = &STR(OPERAND MISSING)
    SET ZEDLMSG = &STR(== FIRST OPERAND PANEL-ID NAME IS MANDATORY ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &STR(&BPARM.) = &Z THEN SET &BPARM = &STR(EUR1.PANELS.DATA)
  CONTROL NOMSG
  FREE F(SYSPDS SYSPRINT SYSIN)
  ERROR DO
    ERROR OFF
    ALLOC F(SYSIN) DA(*)
    ALLOC F(SYSPRINT) DA(*)
    SET ZEDSMSG = &STR(ALLOC ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO ALLOCATE SYSPDS FOR TUTOLIST ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  ALLOC F(SYSPDS) DA('&BPARM') SHR
  ERROR OFF
  ERROR DO
    ERROR OFF
    FREE F(SYSPDS)
    ALLOC F(SYSIN) DA(*)
    ALLOC F(SYSPRINT) DA(*)
    SET ZEDSMSG = &STR(ALLOC ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO ALLOCATE SYSPRINT FOR TUTOLIST ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  ALLOC F(SYSPRINT) UNIT(VIO) SPACE(1 1) TRACKS -
        DSORG(PS) RECFM(F B A) LRECL(88) BLKSIZE(4048) NEW
  ERROR OFF
  ERROR DO
    ERROR OFF
    FREE F(SYSPDS SYSPRINT)
    ALLOC F(SYSIN) DA(*)
    ALLOC F(SYSPRINT) DA(*)
    SET ZEDSMSG = &STR(ALLOC ERROR)
    SET ZEDLMSG = &STR(== UNABLE TO ALLOCATE SYSIN FOR TUTOLIST ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  ALLOC F(SYSIN) UNIT(VIO) SPACE(1 1) TRACKS -
        DSORG(PS) RECFM(F B) LRECL(80) BLKSIZE(4080) NEW
  ERROR OFF
  OPENFILE SYSIN OUTPUT
  SET SYSIN = &STR(*VAR ZCMD )
  PUTFILE SYSIN
  SET SYSIN = &STR(*HDR )
  PUTFILE SYSIN
  SET SYSIN = &STR(&APARM )
  PUTFILE SYSIN
  CLOSFILE SYSIN
  DOPROG TUTOLIST TASKLIB('TARGET.LINKLIB')
  SET &XQCC = &LASTCC
  IF &XQCC Â¬= 0 THEN DO
    FREE F(SYSPDS SYSPRINT SYSIN)
    ALLOC F(SYSIN) DA(*)
    ALLOC F(SYSPRINT) DA(*)
    SET ZEDSMSG = &STR(EXEC ERROR)
    SET ZEDLMSG = &STR(== TUTOLIST EXECUTION RC : &XQCC ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    CONTROL MSG
    EXIT CODE(8)
    END
  FREE F(SYSPDS SYSIN)
  ERROR DO
    SET &GPCC = &LASTCC
    IF &GPCC EQ 400 THEN GOTO EOF
    SET ZEDSMSG = &STR(READ ERROR)
    SET ZEDLMSG = &STR(== SYSPRINT READ OPERATION RC : &GPCC ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    GOTO EOF
    END
  OPENFILE SYSPRINT INPUT
  SET &CN = 1
  CONTROL ASIS
RDF: GETFILE SYSPRINT
  SET &TS = &SUBSTR(1:2,&SYSPRINT)
  IF &TS = &STR(1 ) THEN SET TX&CN = &Z
  ELSE SET TX&CN = &SUBSTR(7:86,&SYSPRINT)
  SET &CN = &CN + 1
  GOTO RDF
EOF: ERROR OFF
  CLOSFILE SYSPRINT
  FREE F(SYSPRINT)
  ALLOC F(SYSPRINT) DA(*)
  SET &WH = 0
  SET &TN = 1
  SET &C = &STR(&&)
  ISREDIT PROCESS DEST
  IF &LASTCC = 0 THEN DO
    ISREDIT LOCATE .ZDEST
    IF &LASTCC = 8 THEN GOTO IN2
IN1: ISREDIT (LINEPTR) = LINENUM .ZDEST
    SET &LINEPTR = &LINEPTR + &WH
    SET TF = &NRSTR(&C.TX&TN)
    ISREDIT MASKLINE = "&TF"
    ISREDIT LINE_AFTER &LINEPTR = MASKLINE
    SET &WH = &WH + 1
    SET &TN = &TN + 1
    IF &TN < &CN THEN GOTO IN1
    END
  ELSE DO
IN2: ISREDIT (LINEPTR) = LINENUM .ZCSR
    SET &LINEPTR = .ZLAST
    SET TF = &NRSTR(&C.TX&TN)
    ISREDIT MASKLINE = "&TF"
    ISREDIT LINE_AFTER &WH = MASKLINE
    SET &WH = &WH + 1
    SET &TN = &TN + 1
    IF &TN < &CN THEN GOTO IN2
    END
  ISREDIT MASKLINE = " "
  CONTROL MSG
  CONTROL CAPS
  EXIT CODE(0)
