ISREDIT  MACRO (APARM,BPARM) NOPROCESS
/*  MACRO NAME : CONTROL                                            */
/*    FUNCTION : ADD CONTROL STATEMENT IN CLIST                     */
/*      SYNTAX : CONTROL OR CONTROL ON                              */
/*               CONTROL OFF                                        */
/*               CONTROL H(ELP)                                     */
/*       DEBUG : CONTROL L(IST)                                     */
/*               CONTROL ON/OFF L(IST)                              */
/*      ORIGIN : CBT TAPE 93 FEB, FILE 95                           */
  IF ((&STR(&BPARM.) = &STR(LIST)) OR +
    (&STR(&BPARM.) = &STR(L))) THEN DO
    CONTROL LIST CONLIST SYMLIST
    SET &BPARM = &Z
    END
  IF ((&STR(&APARM.) = &STR(LIST)) OR +
    (&STR(&APARM.) = &STR(L))) THEN DO
    CONTROL LIST CONLIST SYMLIST
    SET &APARM = &Z
    END
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTCTRL)
      SET &CNCC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&CNCC)
      END
  IF &STR(&APARM.) = &Z THEN SET &APARM = &STR(ON)
  IF ((&STR(&APARM.) ¬= &STR(ON)) AND +
    (&STR(&APARM.) ¬= &STR(OFF))) THEN DO
    SET ZEDSMSG = &STR(INVALID OPERAND)
    SET ZEDLMSG = ENTER "CONTROL HELP" COMMAND FOR OPERAND SYNTAX
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &STR(&APARM.) = ON THEN +
    SET &CNTL = &STR(     CONTROL MSG LIST SYMLIST CONLIST)
  ELSE SET &CNTL = &STR(     CONTROL NOMSG NOLIST NOSYMLIST NOCONLIST)
  ISREDIT PROCESS DEST
  SET &CNCC = &LASTCC
  IF ((&CNCC = 0) OR (&CNCC = 8)) THEN DO
    ISREDIT LOCATE .ZDEST
    SET &CNCC = &LASTCC
    IF &CNCC <= 8 THEN DO
      IF &CNCC = 0 THEN DO
        ISREDIT MASKLINE = "&CNTL"
        ISREDIT LINE_AFTER .ZDEST = MASKLINE
        END
      ELSE DO
        ISREDIT MASKLINE = "&CNTL"
        ISREDIT LINE_AFTER 0 = MASKLINE
        END
      ISREDIT MASKLINE = " "
      SET &CNCC = 0
      END
    END
  EXIT CODE(&CNCC)
