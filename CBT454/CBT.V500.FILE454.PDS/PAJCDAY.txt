PROC     0
/*  WEEKDAY COMPUTATION :                             MOINIL P.A.   */
/*       DATES ARE ACCEPTED FROM 1582 (GREGORIAN CALENDAR) UNTIL    */
/*       4315 INCLUDED (BECAUSE 4316 WILL BE THE YEAR IT WILL       */
/*       HAVE GAINED ONE DAY ON THE SUN.                            */
         CONTROL MAIN MSG
         SET &DI =
         SET &MI =
         SET &YI =
         SET &SDI =
         SET &TSFX =
         SET &SWOP = 0
         SET &TOLD =
         SET &DOLD =
         SET &MOLD =
         SET &YOLD =
         SET &SDOLD =
         SET &NORM = &STR(032060091121152182213244274305335366)
         SET &LEAP = &STR(032061092122153183214245275306336367)
DSPNL:   ISPEXEC DISPLAY PANEL(PAJDAY) CURSOR(DI)
SETCC:   SET &CC = &LASTCC
         IF &CC = 8 THEN DO
           IF &SWOP = 0 THEN GOTO NOCL
           SET &SWOP = 0
           ISPEXEC TBEND TWKD&TOLD
           ISPEXEC TBEND TMTH&TOLD
NOCL:      EXIT
           END
         SET &SWTS = 0
         IF &SWOP = 0 THEN GOTO OPW
         IF &STR(&TSFX) = &STR(&TOLD) THEN GOTO CMPTE
         SET &SWOP = 0
         ISPEXEC TBEND TWKD&TOLD
         ISPEXEC TBEND TMTH&TOLD
         SET &SWTS = 1
OPW:     ISPEXEC TBOPEN TWKD&TSFX NOWRITE
         SET &CC = &LASTCC
         IF &CC = 0 THEN GOTO OPM
         IF &CC = 8 THEN -
           ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000E) CURSOR(TSFX)
         ELSE -
           ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000F) CURSOR(TSFX)
         GOTO SETCC
OPM:     ISPEXEC TBOPEN TMTH&TSFX NOWRITE
         SET &CC = &LASTCC
         IF &CC = 0 THEN GOTO OPOK
         ISPEXEC TBEND TWKD&TSFX
         IF &CC = 8 THEN -
           ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000G) CURSOR(TSFX)
         ELSE -
           ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000H) CURSOR(TSFX)
         GOTO SETCC
OPOK:    SET &SWOP = 1
         SET &TOLD = &STR(&TSFX)
CMPTE:   SET &DNME =
         SET &DO =
         SET &MNME =
         IF &DI = AND &MI = AND &YI = AND &SDI = THEN GOTO USEDT
         IF &DI = &DOLD AND &MI = &MOLD AND &YI = &YOLD AND -
           &SDI = &SDOLD THEN GOTO CKTS
/*                                           ON WITH PREVIOUS DATES */
         IF &SDI = &SDOLD THEN GOTO CKYEAR
         IF &DATATYPE(&SDI) = CHAR THEN GOTO ERSER1
         IF &YI = THEN DO
           ISPEXEC VGET (ZYEAR)
           SET &YI = &STR(19&ZYEAR)
           END
         SET &IRE = &YI // 4                /* IF = ZERO, LEAP YEAR */
         IF &IRE = 0 THEN DO
           SET &CRE = &YI // 100
           IF &CRE = 0 THEN DO
             SET &IRE = (&YI/100) // 4
             END
           END
         IF &IRE = 0 THEN SET &UP = 366
         ELSE SET &UP = 365
         IF (&SDI < 1 OR &SDI > &UP) THEN GOTO ERSER2
         SET &MI = 0                                  /* INITIALIZE */
LOOPMI:  SET &LOW = (&MI * 3) + 1
         SET &HIGH = &LOW + 2
         SET &MI = &MI + 1
         IF &IRE = 0 THEN SET &DDW = &SUBSTR(&LOW:&HIGH,&LEAP)
         ELSE SET &DDW = &SUBSTR(&LOW:&HIGH,&NORM)
         IF &LOW = 1 THEN SET &SUB = 1
         ELSE DO
           SET &LOW = &LOW - 3
           SET &HIGH = &LOW + 2
           IF &IRE = 0 THEN SET &SUB = &SUBSTR(&LOW:&HIGH,&LEAP)
           ELSE SET &SUB = &SUBSTR(&LOW:&HIGH,&NORM)
           END
         IF &SDI < &DDW THEN GOTO OKMONN
         GOTO LOOPMI                         /* SHOULD NOT OVERFLOW */
OKMONN:  SET &DI = &SDI - &SUB + 1
         GOTO PROCESS
ERSER1:  ISPEXEC DISPLAY PANEL (PAJDAY) MSG(PAJD000D) CURSOR(SDI)
         GOTO SETCC
ERSER2:  ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000C) CURSOR(SDI)
         GOTO SETCC
USEDT:   ISPEXEC VGET (ZDAY ZMONTH ZYEAR ZJDATE)
         SET &DI = &ZDAY
         SET &MI = &ZMONTH
         SET &YI = &STR(19&ZYEAR)
         SET &SDI = &SUBSTR(4:6,&ZJDATE)
         GOTO PROCESS             /* WE HOPE SYSTEM DATE IS CORRECT */
CKTS:    IF &SWTS = 1 THEN GOTO CHANGED
CKYEAR:  IF &DATATYPE(&YI) = CHAR THEN GOTO ERYEAR
         IF &YI < 1582 THEN GOTO ERYEAR
         IF &YI > 4315 THEN GOTO ERYEAR
         IF &DATATYPE(&MI) = CHAR THEN GOTO ERMON
         IF (&MI > 0 AND &MI <= 12) THEN GOTO CKDAY
ERMON:   ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000B) CURSOR(MI)
         GOTO SETCC
ERYEAR:  ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000A) CURSOR(YI)
         GOTO SETCC
CKDAY:   IF &DATATYPE(&DI) = CHAR THEN GOTO ERDAY
         SET &IRE = &YI // 4                /* IF = ZERO, LEAP YEAR */
         IF &IRE = 0 THEN DO
           SET &CRE = &YI // 100
           IF &CRE = 0 THEN DO
             SET &IRE = (&YI/100) // 4
             END
           END
         SET &HIGH = &EVAL(&MI*2)
         SET &LOW = &EVAL(&HIGH-1)
         IF &IRE = 0 THEN -
           SET &UP = &SUBSTR(&LOW:&HIGH,312931303130313130313031)
         ELSE SET &UP = &SUBSTR(&LOW:&HIGH,312831303130313130313031)
         IF (&DI > 0 AND &DI <= &UP) THEN GOTO SETJUL
ERDAY:   ISPEXEC DISPLAY PANEL(PAJDAY) MSG(PAJD000C) CURSOR(DI)
         GOTO SETCC
SETJUL:  IF &MI = 1 THEN GOTO INITY
         SET &LOW = ((&MI - 2) * 3) + 1
         SET &HIGH = &LOW + 2
         SET &IRE = &YI // 4
         IF &IRE = 0 THEN DO
           SET &CRE = &YI // 100
           IF &CRE = 0 THEN DO
             SET &IRE = (&YI/100) // 4
             END
           END
         IF &IRE = 0 THEN SET &DDW = &SUBSTR(&LOW:&HIGH,&LEAP)
         ELSE SET &DDW = &SUBSTR(&LOW:&HIGH,&NORM)
         SET &SDI = &DDW + &DI - 1
         GOTO PROCESS
INITY:   SET &SDI = &DI
/*                   AT THIS POINT WE HAVE A CORRECT DATE AVAILABLE */
PROCESS: IF &DI ¬= &DOLD OR &MI ¬= &MOLD OR &YI ¬= &YOLD -
           THEN GOTO CHANGED
         IF &DI < &UP THEN GOTO INCRDAY
         SET &DI = 1                                  /* NEXT MONTH */
         IF &MI < 12 THEN GOTO INCRMON
         SET &MI = 1                                   /* NEXT YEAR */
         SET &UP = 31                                    /* JANUARY */
         SET &YI = &YI + 1
         SET &SDI= 1
         GOTO CHANGED
INCRDAY: SET &DI = &DI + 1
         SET &SDI = &SDI + 1
         GOTO CHANGED
INCRMON: SET &MI = &MI + 1
         SET &LOW = ((&MI - 2) * 3) + 1
         SET &HIGH = &LOW + 2
         IF &IRE = 0 THEN SET &SDI = &SUBSTR(&LOW:&HIGH,&LEAP)
         ELSE SET &SDI = &SUBSTR(&LOW:&HIGH,&NORM)
         SET &HIGH = &EVAL(&MI*2)
         SET &LOW = &EVAL(&HIGH-1)
         IF &IRE = 0 THEN -
           SET &UP = &SUBSTR(&LOW:&HIGH,312931303130313130313031)
         ELSE SET &UP = &SUBSTR(&LOW:&HIGH,312831303130313130313031)
CHANGED: SET &DDX = (&YI-1)/100
         SET &DDC = &DDX - (&DDX/4)
         SET &DDX = (&YI - 1) / 4
         SET &DDW = &YI + &DDX + &SDI - &DDC
         SET &N7 = &DDW // 7            /* REMAINDER FOR SUNDAY = 1 */
         IF &N7 = 0 THEN SET &N7 = 7 /* CHANGE SATURDAY FROM 0 TO 7 */
         ISPEXEC TBGET TWKD&TSFX
         SET &DNME = &STR(&DAYNAME)
         SET &N12 = &MI
         ISPEXEC TBGET TMTH&TSFX
         SET &MNME = &STR(&MONNAME)
         SET &DO = &STR(&DI &MNME &YI)
         SET &DOLD = &DI
         SET &MOLD = &MI
         SET &YOLD = &YI
         SET &SDOLD = &SDI
         GOTO DSPNL
