ISREDIT  MACRO (APARM)
/*  MACRO NAME : EDMODE                                             */
/*    FUNCTION : VERIFY/SET EDIT MODES                              */
/*      SYNTAX : EDMODE                                             */
/*               EDMODE HELP OR H                                   */
/*       DEBUG : EDMODE LIST                                        */
/*      AUTHOR : MOINIL P.A.                                        */
  ISPEXEC CONTROL ERRORS RETURN
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTEM)
      SET &EMCC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&EMCC)
      END
  ELSE IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
  ELSE IF &STR(&APARM.) ¬= &Z THEN DO
    SET ZEDSMSG = &STR(INVALID OPERAND)
    SET ZEDLMSG = &STR(ONLY HELP OR H IS ACCEPTED AS OPERAND)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  ISREDIT (EMDSNM) = DATASET
  ISREDIT (EMBLKS) = BLKSIZE
  ISREDIT (EMLRCL) = LRECL
  ISREDIT (EMRCFM) = RECFM
  ISREDIT (EMCTL1,EMCTL2) = CTL_LIBRARY
  ISREDIT (EMMEMB) = MEMBER
  IF &EMMEMB = &Z THEN DO
    SET &EMSTAT = &Z
    SET &EMVRSN = &Z
    SET &EMLEVL = &Z
    END
  ELSE DO
    ISREDIT (EMSTAT) = STATS
    IF &EMSTAT = ON THEN DO
      ISREDIT (EMVRSN) = VERSION
      ISREDIT (EMLEVL) = LEVEL
      END
    END
  ISREDIT (EMRCVR) = RECOVERY
  ISREDIT (EMCAPS) = CAPS
  ISREDIT (EMNBR1,EMNBR2) = NUMBER
  ISREDIT (EMNLS1,EMNLS2) = NULLS
  ISREDIT (EMHEX1,EMHEX2) = HEX
  ISREDIT (EMWDTH) = DATA_WIDTH
  ISREDIT (EMLEFT,EMRGHT) = BOUNDS
  ISREDIT (EMASV1,EMASV2) = AUTOSAVE
  ISREDIT (EMANUM) = AUTONUM
  ISREDIT (EMALST) = AUTOLIST
  SET &EMSTAT1 = &STR(&EMSTAT)
  SET &EMVRSN1 = &STR(&EMVRSN)
  SET &EMLEVL1 = &STR(&EMLEVL)
  SET &EMRCVR1 = &EMRCVR
  SET &EMCAPS1 = &STR(&EMCAPS)
  SET &EMNBR11 = &STR(&EMNBR1)
  SET &EMWORK = &SUBSTR(1:8,&STR(&EMNBR2))
  SET &EMNBRA = &EMWORK
  SET &EMWORK = &SUBSTR(9:16,&STR(&EMNBR2))
  SET &EMNBRB = &EMWORK
  SET &EMWORK = &SUBSTR(17:24,&STR(&EMNBR2))
  SET &EMNBRC = &EMWORK
  SET &EMNBR21 = &STR(&EMNBRA &EMNBRB &EMNBRC)
  SET &EMNLS11 = &STR(&EMNLS1)
  SET &EMNLS21 = &STR(&EMNLS2)
  SET &EMHEX11 = &STR(&EMHEX1)
  SET &EMHEX21 = &STR(&EMHEX2)
  SET &EMLEFT1 = &EMLEFT
  SET &EMRGHT1 = &EMRGHT
  SET &EMASV11 = &STR(&EMASV1)
  SET &EMASV21 = &STR(&EMASV2)
  SET &EMANUM1 = &STR(&EMANUM)
  SET &EMALST1 = &STR(&EMALST)
  ISPEXEC DISPLAY PANEL(PAJEM)
  SET &EMCC = &LASTCC
  IF &EMCC > 4 THEN DO
    IF &EMCC = 8 THEN DO
      SET ZEDSMSG = &STR(EXIT REQUEST)
      SET ZEDLMSG = &STR(EXIT "EDMODE" FACILITY HAS BEEN REQUESTED)
      END
    ELSE DO
      SET ZEDSMSG = &STR(ERROR RC=&EMCC)
      SET ZEDLMSG = &STR(EXIT DUE TO DISPLAY ERROR ENCOUNTERED)
      END
    END
  ELSE DO
    SET &EMSW = NO
    SET &EMBSW = NO
    SET &EMSTAT2 = &STR(&EMSTAT)
    SET &EMVRSN2 = &STR(&EMVRSN)
    SET &EMLEVL2 = &STR(&EMLEVL)
    SET &EMRCVR2 = &EMRCVR
    SET &EMCAPS2 = &STR(&EMCAPS)
    SET &EMNBR12 = &STR(&EMNBR1)
    SET &EMNBR22 = &STR(&EMNBR2)
    SET &EMNLS12 = &STR(&EMNLS1)
    SET &EMNLS22 = &STR(&EMNLS2)
    SET &EMHEX12 = &STR(&EMHEX1)
    SET &EMHEX22 = &STR(&EMHEX2)
    SET &EMLEFT2 = &EMLEFT
    SET &EMRGHT2 = &EMRGHT
    SET &EMASV12 = &STR(&EMASV1)
    SET &EMASV22 = &STR(&EMASV2)
    SET &EMANUM2 = &STR(&EMANUM)
    SET &EMALST2 = &STR(&EMALST)
    IF &EMSTAT1 ¬= &EMSTAT2 THEN DO
      ISREDIT STATS &EMSTAT
      SET &EMSW = YES
      END
    IF &EMSTAT = ON THEN DO
      IF &EMVRSN1 ¬= &EMVRSN2 THEN DO
        ISREDIT VERSION &EMVRSN
        SET &EMSW = YES
        END
      IF &EMLEVL1 ¬= &EMLEVL2 THEN DO
        ISREDIT LEVEL &EMLEVL
        SET &EMSW = YES
        END
      END
    IF &EMRCVR1 ¬= &EMRCVR2 THEN DO
      ISREDIT RECOVERY &EMRCVR
      SET &EMSW = YES
      END
    IF &EMCAPS1 ¬= &EMCAPS2 THEN DO
      ISREDIT CAPS &EMCAPS
      SET &EMSW = YES
      END
    IF (&EMNBR11 ¬= &EMNBR12 OR &EMNBR21 ¬= &EMNBR22) THEN DO
      IF (&EMNBR11 ¬= &EMNBR12 AND &EMNBR12 = OFF) THEN ISREDIT UNNUM
      ISREDIT NUMBER = (EMNBR1 EMNBR2)
      SET &EMSW = YES
      END
    IF (&EMNLS11 ¬= &EMNLS12 OR &EMNLS21 ¬= &EMNLS22) THEN DO
      ISREDIT NULLS = (EMNLS1 EMNLS2)
      SET &EMSW = YES
      END
    IF &EMHEX11 ¬= &EMHEX12 THEN DO
      IF &EMHEX1 = OFF THEN ISREDIT HEX &EMHEX1
      ELSE ISREDIT HEX &EMHEX1 &EMHEX2
      SET &EMSW = YES
      END
    ELSE IF (&EMHEX21 ¬= &EMHEX22 AND &EMHEX1 = ON) THEN DO
      ISREDIT HEX &EMHEX1 &EMHEX2
      SET &EMSW = YES
      END
    IF &EMASV11 ¬= &EMASV12 THEN DO
      IF &EMASV1 = ON THEN ISREDIT AUTOSAVE &EMASV1
      ELSE ISREDIT AUTOSAVE &EMASV1 &EMASV2
      SET &EMSW = YES
      END
    ELSE IF (&EMASV21 ¬= &EMASV22 AND &EMASV1 = OFF) THEN DO
      ISREDIT AUTOSAVE &EMASV1 &EMASV2
      SET &EMSW = YES
      END
    IF &EMANUM1 ¬= &EMANUM2 THEN DO
      ISREDIT AUTONUM &EMANUM
      SET &EMSW = YES
      END
    IF &EMALST1 ¬= &EMALST2 THEN DO
      ISREDIT AUTOLIST &EMALST
      SET &EMSW = YES
      END
    IF (&EMLEFT1 ¬= &EMLEFT2 OR &EMRGHT1 ¬= &EMRGHT2) THEN DO
      ISREDIT (EMWDTH) = DATA_WIDTH
      IF &EMLEFT >= &EMRGHT THEN DO
        SET ZEDSMSG = &STR(BOUNDS ERROR)
        SET ZEDLMSG = &STR(LEFT BOUNDARY .GE. RIGHT BOUNDARY)
        SET &EMBSW = YES
        END
      ELSE IF &EMRGHT > &EMWDTH THEN DO
        SET ZEDSMSG = &STR(BOUNDS ERROR)
        SET ZEDLMSG = &STR(RIGHT BOUNDARY .GT. LOGICAL DATA WIDTH)
        SET &EMBSW = YES
        END
      ELSE DO
        ISREDIT BOUNDS = (EMLEFT EMRGHT)
        SET &EMSW = YES
        END
      END
    IF &EMBSW = NO THEN DO
      IF &EMSW = YES THEN DO
        SET ZEDSMSG = &STR(MODES DONE)
        SET ZEDLMSG = &STR(EDIT MODES HAS BEEN SET AS DEFINED)
        END
      ELSE DO
        SET ZEDSMSG = &STR(MODES LEFT)
        SET ZEDLMSG = &STR(EDIT MODES HAS BEEN LEFT AS DEFINED)
        ISREDIT RESET
        END
      END
    END
  ISPEXEC SETMSG MSG(ISRZ000)
  EXIT CODE(0)
