ISREDIT  MACRO (APARM)
/*  MACRO NAME : ZOOM                                               */
/*    FUNCTION : BROWSE JCL PROCEDURE FEATURED ON A JCL STATEMENT   */
/*      SYNTAX : ZOOM                                               */
/*               ZOOM HELP OR H                                     */
/*       DEBUG : ZOOM LIST                                          */
/*      ORIGIN : EXTRACTED FROM CBT 94 AUG TAPE, FILE 244           */
/*               ADAPTED BY MOINIL P.A.                             */
/* ---------------------------------------------------------------- */
/* Title :       ZOOM                                               */
/* Author :      Rob Mellor, Systems Programming Group, LOLA        */
/* Date :        12th October 1989                                  */
/* Function :    By placing the cursor on a JCL card containing an  */
/*               EXEC statement, and issueing the ZOOM command,     */
/*               this edit macro will 'zoom in' on (ie. display in  */
/*               browse) the JCL PROC featured on the statement.    */
/*               This command is most useful when defined to a PF   */
/*               key, thus leaving the cursor free to move to the   */
/*               line containing the EXEC statement.                */
/* ---------------------------------------------------------------- */
/* Notes :       You must customise this macro to reflect the       */
/*               PROCLIBs used at your shop (set PROC1, PROC2, ...  */
/*               to PROCLIBs data-sets names). These should be      */
/*               obtained from your installation's JES2 procedure   */
/*               (make sure you use the right concatenation if      */
/*               more than one exists).                             */
/*               Then, set MAXNUM to the total number of PROCLIBs.  */
/* ---------------------------------------------------------------- */
  ISPEXEC CONTROL ERRORS RETURN
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTZOOM)
      SET ZMCC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&ZMCC)
      END
  ELSE IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
  ELSE IF &STR(&APARM.) Â¬= &Z THEN DO
    SET ZEDSMSG = &STR(INVALID OPERAND)
    SET ZEDLMSG = &STR(ONLY HELP OR H IS ACCEPTED AS OPERAND)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(12)
    END
  ELSE CONTROL NOPROMPT NOMSG
  ISPEXEC CONTROL DISPLAY REFRESH
  ISREDIT (SYSDVAL) = LINE .ZCSR
  READDVAL DUMMY FUNCTION MEMBER
  IF &STR(&FUNCTION) = EXEC THEN DO
    IF &LENGTH(&STR(&MEMBER)) >= 4 THEN DO
      IF &SUBSTR(1:4,&STR(&MEMBER)) = &STR(PGM=) THEN DO
        SET ZEDSMSG = &STR(ERROR)
        SET ZEDLMSG = &STR(CLIST &SYSICMD ONLY WORKS ON EXEC +
                       PROCEDURE)
        ISPEXEC SETMSG MSG(ISRZ001)
        EXIT CODE(12)
        END
      END
    END
  ELSE DO
    SET ZEDSMSG = &STR(INVALID)
    SET ZEDLMSG = &STR(CLIST &SYSICMD ONLY WORKS ON JCL EXEC +
                   CARD TYPE)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
    END
  SET PROC1  = SYS1.PROCLIB
  SET PROC2  = EUR1.STC.PROCLIB
  SET PROC3  = EUR1.PROCLIB
  SET PROC4  = SYSDMS.EURSTC.PROCLIB
  SET PROC5  = SYSDMS.EUR.PROCLIB
  SET PROC6  = EUR1.USER.PROCLIB
  SET MAXNUM = 6
  SET PROC = &&PROC
  SET FOUND = NO
  DO &I = 1 TO &MAXNUM UNTIL &FOUND = YES
    IF &SYSDSN('&STR(&PROC&I)(&MEMBER)') = OK THEN SET FOUND = YES
  END
  IF &FOUND = YES THEN DO
    SET DSN = &STR(&PROC&I(&MEMBER))
    ISPEXEC BROWSE DATASET('&DSN')
    SET ZMCC = &LASTCC
    END
  ELSE DO
    SET ZEDSMSG = &STR(NOT FOUND)
    SET ZEDLMSG = &STR(MEMBER &MEMBER COULD NOT BE FOUND IN ANY +
                    JCL PROCEDURE LIBRARY)
    ISPEXEC SETMSG MSG(ISRZ001)
    SET ZMCC = 12
    END
  EXIT CODE(&ZMCC)
