DCAT     TITLE 'NASA/GSFC -- DECONCAT (TSO) COMMAND PROCESSOR.'
DECONCAT START 0
         SPACE 1
* PURPOSE :    THIS IS A TSO COMMAND PROCESSOR DESIGNED TO PERFORM
* ---------    DECONCATENATION OF DYNAMICALLY CONCATENTATED FILES.
*              'IKJDAIR' IS USED TO PERFORM ACTUAL DECONCATENATION.
*              TO EXECUTE THIS CP, ONLY THE PRIME FILENAME/DDNAME
*              CAN AND SHOULD BE SPECIFIED. ONCE DECONCATENATED, THE
*              ORIGINAL FILENAMES ARE RESTORED AND AGAIN AVAILABLE.
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 300.
* --------     GSFTSODC, VERSION 1.0, GENE CZARCINSKI, 09JUN73
*              DECONCAT, VERSION 1.1, GENE CZARCINSKI, 10APR77
*              - RENAME CSECT FROM 'GSFTSODC' TO 'DECONCAT'
*                (MORE CONSISTANCY AND CLEANER CMDLIB)
*              - ADAPTED BY MOINIL P.A.
* ATTRIBUTES : RENT
* ------------
* ALIAS :      DECAT
* -------
         SPACE 1
DECONCAT AMODE 24
DECONCAT RMODE 24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=@SIZE,SP=1,TYPE=RENT
         LR    R11,R13
         USING @STORAGE,R11        STORAGE BASE REGISTER
         LR    R9,R1
        $TEW$EC REG=R9,MSG=PUTMSG,ERR=QUIT
         USING CPPL,R9             SET CPPL ADDRESSABILITY
         XC    RCODE,RCODE         RC=0 -------------------------------
         XC    ECB,ECB
         LA    R1,PL1              INIT FOR TERM I/O
         USING IOPL,R1
         MVC   IOPLUPT,CPPLUPT
         MVC   IOPLECT,CPPLECT
         LA    R0,ECB
         ST    R0,IOPLECB
         LA    R1,PL2              INIT FOR COMMAND PARSE
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R0,ECB
         ST    R0,PPLECB
         MVC   PPLPCL,=A($PARSEDC)
         LA    R0,APDL
         ST    R0,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         LA    R1,PL3              INIT PL FOR DAIR
         USING DAPL,R1
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    R0,ECB
         ST    R0,ECB
         LA    R5,PL4              A(DAPB)
         ST    R5,DAPLDAPB
         DROP  R1
         USING DAPB10,R5
         XC    PL4(16),PL4
         MVI   DA10CD+1,X'10'      FOR DECONCATENATE
*------- PARSE THE COMMAND OPERANDS
        CALLTSSR EP=IKJPARS,MF=(E,PL2)
         LTR   R15,R15             PARSE OK?
         BZ    PARSE1              YES.
PARSE0   LA    R2,MSG0             BAD PARSE... ISSUE MSG & ABORT
         B     PUTLNE
PARSE1   L     R2,APDL             GOOD PARSE
         USING @PDL,R2
         MVI   DA10DDN,C' '        CLEAR DDNAME AREA
         MVC   DA10DDN+1(7),DA10DDN
         L     R15,#NAME           PNTR TO DDNAME
         LTR   R15,R15             SPECIFIED?
         BZ    PARSE0              NO, BAD
         LH    R1,#NAME+4          LENGTH OF DDNAME
         LTR   R1,R1
         BZ    PARSE0
         LA    R14,DA10DDN
         BCT   R1,*+L'*+6
         MVC   0(*-*,R14),0(R15)   <<EXECUTED>>
         EX    R1,*-6
*------- INVOKE DAIR TO DECONCATENATE
        CALLTSSR EP=IKJDAIR,MF=(E,PL3)
         LTR   R15,R15             OK?
         BZ    DONE1               RC=0, YES.
         CVD   R15,WORK            RCÂ¬=0, BAD
         OI    WORK+7,X'0F'
         MVC   MSGA(MSG1L),MSG1
         UNPK  MSGA+(MSG1A-MSG1)(L'MSG1A),WORK
         UNPK  WORK(5),DA10DARC(3)
         MVZ   WORK(4),=XL4'0'
         TR    WORK(4),=C'0123456789ABCDEF'
         MVC   MSGA+(MSG1B-MSG1)(L'MSG1B),WORK
         LA    R2,MSGA
*------- OUTPUT ERROR MSG
PUTLNE  PUTLINE PARM=PTPB,OUTPUT=(0(R2),TERM,SINGLE,DATA),             X
               TERMPUT=(EDIT,WAIT,NOHOLD),MF=(E,PL1)
         MVI   RCODE+1,12          RC=12 ------------------------------
DONE1    CLI   PL2+(PPLANS-PPL),X'FF'
         BE    DONE2
         L     R1,PL2+(PPLANS-PPL)
        IKJRLSA (1)
DONE2    CLI   RCODE+1,12
         BNE   DONE3
        STACK  PARM=STPB,DELETE=ALL,MF=(E,PL1)
        TCLEARQ INPUT
DONE3    LH    R2,RCODE
         B     LEAVE
QUIT     LA    R2,12               RC=12 ------------------------------
LEAVE   $XRET  CC=(R2),LV=@SIZE,SP=1,TYPE=RENT
         EJECT
*------- CONSTANTS
         SPACE 1
MSG0     DC    0F'0',Y(MSG0L,0),C'  ERROR PARSING COMMAND'
         DC    C' OPERAND, EXECUTION SUPPRESSED.'
MSG0L    EQU   *-MSG0
MSG1     DC    0F'0',Y(MSG1L,0),C'  DECONCATENATION ERROR, DAIR RC='
MSG1A    DC    CL2' ',C', DARC='
MSG1B    DC    CL4' ',C'.'
MSG1L    EQU   *-MSG1
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
$PARSEDC IKJPARM DSECT=@PDL
#NAME    IKJIDENT 'FILENAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,     X
               PROMPT='FILENAME/DDNAME FOR DECONCATENATION'
         IKJENDP
        PRINT  GEN
         EJECT
@STORAGE DSECT
         DS    18F                 S.A.
WORK     DS    D
        $TEW$WA
ECB      DS    F
APDL     DS    A
PTPB    PUTLINE MF=L
STPB    STACK  MF=L
PL1      DS    7F                  IOPL
PL2      DS    7F                  PPL
PL3      DS    5F                  DAPL
PL4      DS    4F                  DAPB10
MSGA     DS    0F,H,H,CL100
RCODE    DS    H
         SPACE 1
@SIZE    EQU   (((*-@STORAGE)+7)/8)*8
         SPACE 1
        PRINT  NOGEN
        $TEW$DS CVT=YES
        IKJIOPL
        IKJPPL
        IKJDAPL
        IKJDAP10
        PRINT  GEN
         SPACE 1
         END
