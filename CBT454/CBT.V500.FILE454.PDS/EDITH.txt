EDTH     TITLE 'EDIT HEADER SUBROUTINE.'
EDITH    START 0
         SPACE 2
* CALLING SEQUENCE : CALL EDITH,(A,B,C,D),VL
* ------------------            (A,B,C)
*                               (A,B)
*                               (A)
*
*        WHERE : A - IS THE ADDRESS OF THE TEXT TO BE EXPANDED AND
*                    BLOCKS PRINTED BY GROUP OF 16 CHARACTERS.
*
*                B - IS THE ADDRESS OF TEXT LENGTH (NO. OF CHARACTERS :
*                    FORMAT FULLWORD - F BOUNDARY). WHEN THIS ARGUMENT
*                    IS OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, THE LENGTH OF
*                    THE TEXT IS SUPPOSED TO BE IN FRONT OF THE TEXT,
*                    AT THE ADDRESS OF A-1 (FORMAT XL1).
*
*                C - IS THE ADDRESS OF THE DD-NAME (FORMAT CL8) IF
*                    SUPPLIED (OPTIONAL). WHEN THIS ARGUMENT IS
*                    OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS SET
*                    TO ZERO IN THE PARAMETERS LIST, THE DEFAULT
*                    DD-NAME USED IS 'SYSPRINT'.
*
*                D - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - PRINT CORRECTLY EXECUTED.
*                     1 - DD-CARD MISSING.
*                     2 - OPEN ERROR.
*                     3 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*                     4 - LENGTH IS NEGATIVE, ZERO OR GREATER THAN THE
*                         LOGICAL RECORD LENGTH (MAXIMUM ALLOWABLE IS
*                         145). THE LINE IS PRINTED, BUT TRUNCATED.
*                     5 - PRINT ERROR.
*
* GENERAL NOTICE : THIS SUBROUTINE USES THE "PRINT" SUBROUTINE, AND
* ---------------- THUS IT IS NECESSARY TO CALL "PCLOSE" IN ORDER TO
*                  TERMINATE THE HEADER EDIT FILE.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
        $XENT  BASE=R12
         EJECT
         SR    R6,R6               SET DEFAULTS.
         SR    R8,R8
         SR    R9,R9
         SR    R10,R10             R10 = RETURN CODE.
         TM    0(R1),X'80'         CONTROL ARGUMENTS.
         BO    ED1
         TM    7(R1),X'03'
         BNZ   ERR3
         L     R8,4(R1)
         LA    R8,0(R8)
         TM    4(R1),X'80'
         BO    ED1
         L     R7,8(R1)
         LA    R7,0(R7)
         LTR   R7,R7
         BZ    *+L'*+2
         LR    R6,R7               R6 = DDNAME ADDRESS.
         TM    8(R1),X'80'
         BO    ED1
         TM    12(R1),X'80'
         BZ    ERR3
         TM    15(R1),X'03'
         BNZ   ERR3
         L     R9,12(R1)
         LA    R9,0(R9)            R9 = IND. RESPONSE ADDRESS.
ED1      L     R7,0(R1)
         LA    R7,0(R7)            R7 = TEXT ADDRESS.
         LTR   R8,R8
         BZ    ED2
         L     R8,0(R8)
         B     ED3
ED2      BCTR  R7,0
         IC    R8,0(R7)
         LA    R7,1(R7)
ED3      LTR   R8,R8               R8 = TEXT LENGTH.
         BNP   ERR4
         SPACE 2
NEXTG    LA    R1,16
         SR    R8,R1
         BNM   *+L'*+4
         AR    R1,R8               R1 = NUMBER OF CHARACTERS.
         SR    R8,R8               SET END OF TEXT.
         SR    R2,R2               R2 = LINES STARTING INDEX.
         CL    R1,=F'14'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'12'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'10'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'8'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'6'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'4'
         BH    EXPD
         LA    R2,8(R2)
         CL    R1,=F'2'
         BH    EXPD
         LA    R2,8(R2)
EXPD     MVC   LINE1+2(128),LINE1+1
         MVC   LINE2+2(128),LINE2+1
         MVC   LINE3+2(128),LINE3+1
         MVC   LINE4+2(128),LINE4+1
         MVC   LINE5+2(128),LINE5+1
         MVC   LINE6+2(128),LINE6+1
         MVC   LINE7+2(128),LINE7+1
         MVC   LINE8+2(128),LINE8+1
NEXTC    CLI   0(R7),C' '
         BE    SKIP
         LM    R3,R5,SCAN
LOOP     CLC   0(1,R7),0(R3)
         BE    SET
         BXLE  R3,R4,LOOP
         B     SKIP
SET      LA    R4,LINE1+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE2+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE3+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE4+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE5+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE6+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE7+2(R2)
         BAL   R14,BUILD
         LA    R4,LINE8+2(R2)
         BAL   R14,BUILD
SKIP     LA    R2,8(R2)
         LA    R7,1(R7)
         BCT   R1,NEXTC
        $PRINT SPCE
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE1,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE2,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE3,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE4,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE5,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE6,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE7,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT LINE8,,(R6)
         LTR   R10,R15
         BNZ   EDRET
        $PRINT SPCE
         LTR   R10,R15
         BNZ   EDRET
         LTR   R8,R8
         BNZ   NEXTG
         B     EDRET
         SPACE 2
ERR3     LA    R10,3
         B     EDRET
ERR4     LA    R10,4
EDRET    LTR   R9,R9
         BZ    *+L'*+4
         ST    R10,0(R9)           STORE INDICATOR.
        $XRET  CC=(R10)
         SPACE 2
BUILD    LA    R0,8
         LA    R3,1(R3)
         TM    0(R3),X'FF'
         BZR   R14
         LA    R15,X'80'
BUILDL   EX    R15,BUILDT
         BZ    *+L'*+4
         MVI   0(R4),C'*'
         LA    R4,1(R4)
         SRL   R15,1
         BCT   R0,BUILDL
         BR    R14
BUILDT   TM    0(R3),*-*
         SPACE 1
         DROP  R12
         SPACE 2
         LTORG
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
SCAN     DC    A(CHTAB,L'CHTAB,CHTABE-L'CHTAB)
         SPACE 1
         DC    AL1(L'SPCE)
SPCE     DC    CL5'     '
         DC    AL1(L'LINE1)
LINE1    DC    CL133' '
         DC    AL1(L'LINE2)
LINE2    DC    CL133' '
         DC    AL1(L'LINE3)
LINE3    DC    CL133' '
         DC    AL1(L'LINE4)
LINE4    DC    CL133' '
         DC    AL1(L'LINE5)
LINE5    DC    CL133' '
         DC    AL1(L'LINE6)
LINE6    DC    CL133' '
         DC    AL1(L'LINE7)
LINE7    DC    CL133' '
         DC    AL1(L'LINE8)
LINE8    DC    CL133' '
         SPACE 2
*        BLOCK CHARACTERS TABLE.
         SPACE 1
CHTAB    DS    0XL9
         DC    C'A',XL8'081C3663637F6363'
         DC    C'B',XL8'7E63637E6363637E'
         DC    C'C',XL8'3F6060606060603F'
         DC    C'D',XL8'7E6363636363637E'
         DC    C'E',XL8'7E6060786060607F'
         DC    C'F',XL8'7F60607C60606060'
         DC    C'G',XL8'3E6360606763633E'
         DC    C'H',XL8'6363637F63636363'
         DC    C'I',XL8'3C1818181818183C'
         DC    C'J',XL8'030303030363633E'
         DC    C'K',XL8'63666C78786C6663'
         DC    C'L',XL8'606060606060607F'
         DC    C'M',XL8'4163777F6D636363'
         DC    C'N',XL8'6373737D6F676763'
         DC    C'O',XL8'3E6363636363633E'
         DC    C'P',XL8'7E6363637E606060'
         DC    C'Q',XL8'3E636363636F673E'
         DC    C'R',XL8'7E6363637E6C6663'
         DC    C'S',XL8'3E63603E0303633E'
         DC    C'T',XL8'7F18181818181818'
         DC    C'U',XL8'636363636363633E'
         DC    C'V',XL8'6363636363361C08'
         DC    C'W',XL8'636363636D7F7722'
         DC    C'X',XL8'6363361C1C366363'
         DC    C'Y',XL8'636363361C181818'
         DC    C'Z',XL8'7F03060C1830607F'
         DC    C'1',XL8'081808080808081C'
         DC    C'2',XL8'1C2202021C20203E'
         DC    C'3',XL8'1C22020C0222221C'
         DC    C'4',XL8'040C14247E040404'
         DC    C'5',XL8'3C20203C0222221C'
         DC    C'6',XL8'1C22203C2222221C'
         DC    C'7',XL8'3E02020408080808'
         DC    C'8',XL8'1C22221C2222221C'
         DC    C'9',XL8'1C2222221E02221C'
         DC    C'0',XL8'1C2222222222221C'
         DC    C'$',XL8'083E49483E09493E'
         DC    C'#',XL8'00123F12123F1200'
         DC    C'@',XL8'00001C22021A2A14'
         DC    C',',XL8'0000000018180810'
         DC    C'.',XL8'0000000000001818'
         DC    C'(',XL8'0810202020201008'
         DC    C')',XL8'1008040404040810'
         DC    C'+',XL8'000008083E080800'
         DC    C'/',XL8'0001020408102040'
         DC    C'''',XL8'1818081000000000'
         DC    C'*',XL8'00002A1C3E1C2A00'
         DC    C'=',XL8'0000003E003E0000'
         DC    C':',XL8'0000181800181800'
         DC    C';',XL8'0018180018180810'
         DC    C'-',XL8'000000003E000000'
         DC    C'?',XL8'1C22020408080008'
         DC    C'%',XL8'0031320408102646'
         DC    C'|',XL8'0808080808080008'
         DC    C'&&',XL8'00182424182A241A'
         DC    C'"',XL8'3636122400000000'
CHTABE   EQU   *
         SPACE 2
         END
