ISREDIT  MACRO (PLGS TCOL) NOPROCESS
/*  MACRO NAME : PLUG                                               */
/* DESCRIPTION : EDIT MACRO TO PLUG DATA INTO A RANGE OF LINES      */
/*      AUTHOR : J. KALINICH, X4521                                 */
/*  CBT ORIGIN : EXTRACTED FROM CBT TAPE 94 APR, FILE 078           */
/*  ADAPTED BY : MOINIL P.A.                                        */
/*    MODIFIED : TO USE ONLY "ON" OR "OO" AS TARGET RANGE OF LINES  */
  IF (&NRSTR(&PLGS) EQ &STR(HELP)) OR +
    (&NRSTR(&PLGS) EQ &STR(H)) THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJPLUG)
    SET &PLGCC = &LASTCC
    ISPEXEC CONTROL DISPLAY RESTORE
    EXIT CODE(&PLGCC)
    END
  ELSE IF &NRSTR(&PLGS) EQ &Z THEN DO
    SET ZEDSMSG = &STR(PLUG MISSING)
    SET ZEDLMSG = &STR(YOU MUST SPECIFY A PLUG STRING)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(20)
    END
/*               CHECK FOR PLUG STRING DELIMITERS                   */
  CONTROL ASIS
  SET PLGL = &LENGTH(&NRSTR(&PLGS))
  IF (&SUBSTR(1:1,&PLGS) EQ &STR(') AND +
    &SUBSTR(&PLGL:&PLGL,&PLGS) EQ &STR(')) OR +
    (&SUBSTR(1:1,&PLGS) EQ &STR(") AND +
    &SUBSTR(&PLGL:&PLGL,&PLGS) EQ &STR(")) THEN DO
    SET PLGS = &SUBSTR(2:&PLGL-1,&PLGS)
    SET PLGL = &PLGL - 2
    END
  IF &PLGL EQ 0 THEN DO
    SET ZEDSMSG = &STR(PLUG NULL)
    SET ZEDLMSG = &STR(A NULL PLUG STRING HAS NO SENSE)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(20)
    END
/*               IF NO COLUMN SPECIFIED, THEN USE CURSOR POSITION   */
  ISREDIT (LEFTM,RIGHTM) = BOUNDS
  IF &STR(&TCOL) = &Z THEN DO
    ISREDIT (CROW,CCOL) = CURSOR
    IF (&CCOL LT &LEFTM) OR (&CCOL GT &RIGHTM) THEN DO
      SET ZEDSMSG = &STR(START COLUMN)
      SET ZEDLMSG = &STR(USE A NUMBER OR SET CORRECT CURSOR LOCATION +
                         FOR START OF PLUG : BOUNDS)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(20)
      END
    ELSE SET TCOL = &CCOL
    END
  ELSE IF &DATATYPE(&TCOL) NE &STR(NUM) THEN DO
    SET ZEDSMSG = &STR(INVALID START)
    SET ZEDLMSG = &STR(START COLUMN NUMBER FOR PLUG IS NOT NUMERIC)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(20)
    END
  ELSE IF (&TCOL LT &LEFTM) OR (&TCOL GT &RIGHTM) THEN DO
    SET ZEDSMSG = &STR(START COLUMN)
    SET ZEDLMSG = &STR(START COLUMN NUMBER FOR PLUG IS NOT IN DATA +
                       LINES : BOUNDS)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(20)
    END
/*               CHECK PLUG STRING TARGET WIDTH VALIDITY            */
  SET LCOL = &TCOL + &PLGL - 1
  IF &LCOL GT &RIGHTM THEN DO
    SET ZEDSMSG = &STR(PLUG LENGTH)
    SET ZEDLMSG = &STR(PLUG STRING TOO LONG OR WRONG START COLUMN +
                       NUMBER : BOUNDS)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(20)
    END
/*               PROCESS LINE COMMANDS, CHECK IF "O" WAS SPECIFIED  */
  ISPEXEC CONTROL ERRORS RETURN
  ISREDIT PROCESS RANGE O
  SET LCC = &LASTCC
  ISREDIT (RC) = RANGE_CMD
  SET RCC = &LASTCC
  IF &LCC ¬=0 OR &RCC ¬=0 THEN DO
    IF &RCC = 4 THEN DO
      IF &STR(&RC) ¬= &STR(O) THEN DO
        IF &LCC = 4 THEN SET &ZEDSMSG = &STR(NO RANGE)
        ELSE SET &ZEDSMSG = &STR(INVALID RANGE)
        SET &ZEDLMSG = &STR(YOU MUST SPECIFY A TARGET RANGE OF +
                            LINES, USING "ON" OR "OO")
        END
      ELSE DO
        SET &ZEDSMSG = &STR(RANGE INCOMPLETE)
        SET &ZEDLMSG = &STR(ENTER A MATCHING "OO" TO COMPLETE +
                            THE BLOCK COMMAND PAIR)
        END
      END
    ELSE IF &RCC = 8 THEN DO
      SET &ZEDSMSG = &STR(NOT ACCEPTABLE)
      SET &ZEDLMSG = &STR(AN UNACCEPTABLE TARGET RANGE OF LINES +
                          HAS BEEN SPECIFIED)
      END
    ELSE DO
      SET &ZEDSMSG = &STR(SEVERE ERROR)
      SET &ZEDLMSG = &STR(SEVERE ERROR OCCURED, RETURN CODES ARE +
                          &LCC AND &RCC)
      END
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
    END
  ISREDIT (FR) = LINENUM .ZFRANGE
  ISREDIT (LR) = LINENUM .ZLRANGE
  SET I = 1
  SET ANYCHAR = &Z
  DO WHILE &I LE &PLGL
    SET ANYCHAR = &STR(&ANYCHAR.=)
    SET I = &I + 1
    END
  ISREDIT CURSOR = .ZFRANGE 1    /* SET CURSOR ON 1ST LINE IN RANGE */
  SET &I = &FR
  DO WHILE &I LE &LR
    ISREDIT CHANGE P'&STR(&ANYCHAR)' '&NRSTR(&PLGS)' &TCOL
    SET CHGCC = &LASTCC
    IF &CHGCC NE 0 THEN DO
      SET ZEDSMSG = &STR(PLUG FAILED)
      SET ZEDLMSG = &STR(CHANGE RC=&CHGCC, TARGET LINES &FR TO +
                         &LR AT COLUMN &TCOL)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
      END
    SET I = &I + 1
    END
  SET ZEDSMSG = &STR(PLUG DONE)
  SET ZEDLMSG = &STR(STRING &PLGS PLUGGED IN LINES &FR TO &LR AT +
                     COLUMN &TCOL)
  ISPEXEC SETMSG MSG(ISRZ001)
  EXIT CODE(0)
