CRY      TITLE 'ENCRYPT-DECRYPT ISPF/PDF EDIT COMMAND.'
CRY      START 0
         SPACE 1
***********************************************************************
*                                                                     *
* AUTHOR :    D.L.SCHWARZ                                             *
*                                                                     *
* DATE :      OCT. 17, 1985 - VERSION 2, MOD. 0                       *
*                                                                     *
* FUNCTION :  THIS MODULE PERFORMS THE FUNCTION OF ENCRYPTION AND     *
*             DECRYPTION OF DATA OPERATING AS A PRIMARY COMMAND       *
*             UNDER ISPF/PDF EDIT. THE MODULE IS LINKED INTO THE      *
*             COMMAND LIBRARY UNDER THE NAME 'CRY'.                   *
*                                                                     *
* OPERATION : THE ENCRYPTION IS PERFORMED BY UTILIZING A KEY VALUE    *
*             SUPPLIED AS AN OPERAND OF THE PRIMARY COMMAND. THIS     *
*             VALUE IS PASSED TO PROGRAM R050A90 AT ENTRY 'SETKEY',   *
*             FOLLOWED BY RECURSIVE CALLS TO THE PROGRAM AT ENTRY     *
*             POINTS 'ENCIPH' OR 'DECIPH', DEPENDING ON THE FUNCTION  *
*             DESIRED.                                                *
*                                                                     *
* EXAMPLES OF USE :     !CRY ENCRYPT,KEYVALUE                         *
*                       !CRY DECRYPT,KEYVALUE                         *
*             THE 'KEYVALUE' IS A USER SUPPLIED KEYWORD WHICH IS      *
*             MESSAGED AND USED AS THE ENCRYPT/DECRYPT CODE VALUE.    *
*             THE KEYWORD IS LIMITED TO EIGHT (8) BYTES IN LENGTH,    *
*             BUT MAY BE SHORTER, THIS PROGRAM WILL PAD IT TO THE     *
*             CORRECT LENGTH.                                         *
*                                                                     *
* CHANGES/UPDATES :                                                   *
*  10/17/85 - VERSION 2, MODIFICATION 0.                              *
*             EXTRACTED FROM CBT TAPE 88/FEB, FILE 270.           -EU-*
*  05/16/89 - ACCEPT SHORT FORMS COMMAND :           (MOINIL P.A.)-EU-*
*                       !CRY EN,KEYVALUE                          -EU-*
*                       !CRY E,KEYVALUE                           -EU-*
*                       !CRY DE,KEYVALUE                          -EU-*
*                       !CRY D,KEYVALUE                           -EU-*
*  05/19/89 - DISPLAY ERROR MESSAGES.                (MOINIL P.A.)-EU-*
*  05/23/89 - WHEN KEYVALUE IS OMITED, PROMPT THE    (MOINIL P.A.)-EU-*
*             USER TO SUPPLY IT UNVIEWABLE.                       -EU-*
*  21/06/89 - ACCEPT NEW COMMAND :                   (MOINIL P.A.)-EU-*
*                       !CRY HELP                                 -EU-*
*                       !CRY H                                    -EU-*
*  05/09/89 - ACCEPT RANGE COMMAND INDICATED BY      (MOINIL P.A.)-EU-*
*             "CN" OR "CC".                                       -EU-*
*  12/11/92 - CHECK LRECL NOT GREATER THAN BUFFER    (MOINIL P.A.)-EU-*
*             AND SET CRYPT LENGTH = MULTIPLE OF 8.               -EU-*
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=(R11,R12)
         SPACE 1
*-------------------  I N I T I A L I Z A T I O N  --------------------
*        THE FOLLOWING CALLS TO ISPLINK ARE TO OBTAIN THE VALUES
*        NECESSARY FOR THE OPERATION OF THE PROGRAM.
*----------------------------------------------------------------------
         SPACE 1
* ------ CALL TO OBTAIN THE FUNCTION (ENCRYPT/DECRYPT/HELP)
*        DESIRED, AND THE KEY VALUE.
         SPACE 1
         CALL  ISPLINK,(VDEF,NL,FCDE,TYPEC,LEN8),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR1                NOT ZERO, ERROR
         SPACE 1
* ------ CALL TO DEFINE THE BUFFER AREA THAT WE WILL BE USING.
         SPACE 1
         CALL  ISPLINK,(VDEF,LL,BUFFER,TYPEC,LEN256),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR2                NOT ZERO, ERROR
         SPACE 1
* ------ CALL TO DEFINE THE VARIABLES USED TO OBTAIN THE LOGICAL
*        RECORD LENGTH (LRECL) OF THE DATA WE ARE OPERATING ON,
*        AND THE EVENTUAL RANGE OF DATA LINES TO BE PROCESSED.
         SPACE 1
         CALL  ISPLINK,(VDEF,DL,LRECL,TYPEF,LEN4),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR3                NOT ZERO, ERROR
         CALL  ISPLINK,(VDEF,RL,LRC,TYPEC,LEN1),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR4                NOT ZERO, ERROR
         SPACE 1
* ------ CALL TO DEFINE THIS PROGRAM AS AN EDIT MACRO.
         SPACE 1
         CALL  ISPLINK,(EDIT,LEN0,EDMAC),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR5                NOT ZERO, ERROR
         SPACE 1
* ------ CALL TO CONTROL DATA LINES TO BE PROCESSED.
         SPACE 1
         CALL  ISPLINK,(EDIT,LEN0,EDPRC),VL
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CHKRG               0 = NORMAL COMPLETION
         CL    R15,LEN4
         BE    NORNG               4 = NO RANGE
         B     ERR6                OTHER, ERROR
         SPACE 1
* ------ CALL TO CHECK RANGE OF DATA LINES TO BE PROCESSED.
         SPACE 1
CHKRG    CALL  ISPLINK,(EDIT,LEN0,EDQUE),VL
         LTR   R15,R15             CHECK RETURN CODE
         BZ    GTRNG               0 = RANGE INDICATED
         CL    R15,LEN4
         BE    NORNG               4 = NO RANGE
         CL    R15,LEN8
         BE    RGERR               8 = RANGE ERROR
         B     ERR7                OTHER, ERROR
GTRNG    CLI   LRC,C'C'            RANGE CHARACTER = C?
         BNE   RGINV               NO, ERROR
         CALL  ISPLINK,(EDIT,LEN0,EDFRG),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR8                NOT ZERO, ERROR
         CALL  ISPLINK,(EDIT,LEN0,EDLRG),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR9                NOT ZERO, ERROR
         L     R1,LINENO           ADJUST FIRST LINE NUMBER TO LOOP
         LTR   R1,R1
         BP    *+L'*+6
         XR    R1,R1
         B     *+L'*+2
         BCTR  R1,0
         ST    R1,LINENO
         B     GTLRL
         SPACE 1
* ------ CALL TO OBTAIN THE LINE NUMBER OF THE LAST RECORD IN
*        THE CURRENT DATA-SET.
         SPACE 1
NORNG    XC    LINENO,LINENO       SET LINE NUMBER TO ZERO
         CALL  ISPLINK,(EDIT,LEN0,EDLLR),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR10               NOT ZERO, ERROR
         SPACE 1
* ------ CALL TO DEFINE THE LOGICAL RECORD LENGTH (LRECL) TO EDIT.
         SPACE 1
GTLRL    CALL  ISPLINK,(EDIT,LEN0,EDLRL),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR11               NOT ZERO, ERROR
         SPACE 1
* ------ CHECK THE LOGICAL RECORD LENGTH (LRECL) TO EDIT
*        (ENCRYPT/DECRYPT MAXIMUM = BUFFER LENGTH), AND
*        SET THE LOGICAL RECORD LENGTH MULTIPLE OF 8 BYTES
*        (DON'T CHANGE EXTRA BYTES, BECAUSE THEY CANNOT BE
*        CORRECTLY DECRYPTED AFTER AN ENCRYPT OPERATION).
         SPACE 1
         L     R1,LRECL
         CL    R1,=A(BUFLEN)
         BH    ERMLG
         SRL   R1,3
         SLL   R1,3
         ST    R1,BUFLVA
         SPACE 1
* ------ CALL TO SET THE CURRENT PROFILE TO 'CAPS OFF' TO ASSURE
*        THAT SPF WILL NOT FOLD THE ENCRYPTED DATA TO UPPER CASE.
         SPACE 1
         CALL  ISPLINK,(EDIT,LEN0,EDOFF),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR12               NOT ZERO, ERROR
         SPACE 1
*--------------------------  R E Q U E S T  ---------------------------
*        ALL NECESSARY PARAMETERS HAVE NOW BEEN OBTAINED AND DEFINED.
*        NOW CHECK THE FUNCTION CODE FOR VALIDITY (E/D/H).
*----------------------------------------------------------------------
         SPACE 1
         OC    FCDE,=CL8' '        FOLD THE CODE TO UPPER CASE
         CLC   FCDE,=CL8'HELP    ' LOOK IF REQUEST IS HELP ONLY
         BE    DSHELP
         CLC   FCDE,=CL8'H       '
         BE    DSHELP
         CLC   FCDE,=CL8'ENCRYPT ' COMPARE FOR OURS
         BE    SETFEN
         CLC   FCDE,=CL8'EN      '
         BE    SETFEN
         CLC   FCDE,=CL8'E       '
         BE    SETFEN
         CLC   FCDE,=CL8'DECRYPT '
         BE    SETFDE
         CLC   FCDE,=CL8'DE      '
         BE    SETFDE
         CLC   FCDE,=CL8'D       '
         BNE   EXIT                NOT US, EXIT
SETFDE   MVI   FUNC+3,4            SET FUNCTION FOR DECRYPT
         B     SETFEN+L'SETFEN     GO PROCESS
SETFEN   MVI   FUNC+3,0            SET FUNCTION FOR ENCRYPT
         SPACE 1
*------------------------  K E Y - V A L U E  -------------------------
*        CHECK AND SET THE SPECIFIED KEY VALUE.
*----------------------------------------------------------------------
         SPACE 1
* ------ CHECK IF THE KEY VALUE HAS BEEN SPECIFIED.
         SPACE 1
         OC    KEY,=CL8' '         FOLD TO UPPER CASE
         CLC   KEY,=CL8' '         ANY DATA?
         BNE   EDRUN               YES
         SPACE 1
* ------ CALL ISPLINK TO PROMPT FOR A KEY VALUE.
         SPACE 1
         CALL  ISPLINK,(DSPL,PNLN),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR13               NOT ZERO, ERROR
         CALL  ISPLINK,(VCPY,CL,LEN8,KEY,MVM),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR14               NOT ZERO, ERROR
         SPACE 1
* ------ NOW CHECK AGAIN THE KEY VALUE SPECIFIED.
         SPACE 1
         OC    KEY,=CL8' '         FOLD TO UPPER CASE
         CLC   KEY,=CL8' '         ANY DATA?
         BE    EXIT                NO (AGAIN), EXIT
         SPACE 1
* ------ LINK TO THE EN/DECIPH PROGRAM AT ENTRY POINT 'SETKEY'
*        TO ESTABLISH THE KEY VALUE.
         SPACE 1
EDRUN    LA    R1,KEYADD           SET UP PARM REG
         L     R15,SETADD          ADDRESS OF SETKEY ENTRY
         BASR  R14,R15             GO TO SETKEY
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR15               NOT ZERO, ERROR
         SPACE 1
*--------------------------  P R O C E S S  ---------------------------
*        PROCESS THE DATA LINES.
*----------------------------------------------------------------------
         SPACE 1
LOOP     L     R1,LINENO           GET LINE NUMBER
         LA    R1,1(,R1)           INCREMENT
         ST    R1,LINENO           STORE BACK
         CLC   LINENO,LLINE        LAST LINE DONE?
         BH    LSTMSG              YES, DONE
         SPACE 1
* ------ CALL ISPLINK TO READ IN THE DESIGNATED LINE FROM THE
*        DATA-SET.
         SPACE 1
         CALL  ISPLINK,(EDIT,LEN0,EDRLN),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR16               NOT ZERO, ERROR
         SPACE 1
* ------ CALL THE EN/DECIPH ROUTINE TO PERFORM THE EN/DECRYPTION
*        ON THE CURRENT LINE.
         SPACE 1
         L     R1,FUNC             GET FUNCTION
         L     R15,CIPHADD(R1)     GET ENTRY POINT REQUESTED
         LA    R1,PARMLIST         SET R1 FOR CIPH ROUTINE
         BASR  R14,R15             GO DO THE DESIRED FUNCTION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ERR17               NOT ZERO, ERROR
         SPACE 1
* ------ CALL ISPLINK TO WRITE BACK THE NEWLY EN/DECRYPTED LINE.
         SPACE 1
         CALL  ISPLINK,(EDIT,LEN0,EDWLN),VL
         LTR   R15,R15             CHECK RETURN CODE
         BZ    LOOP                ZERO, GET THE NEXT RECORD
         CL    R15,LEN4            DATA TRUNCATED?
         BE    LOOP                YES, GET THE NEXT RECORD
         B     ERR18               NO, ERROR
         SPACE 1
* ------ SET MESSAGE IF ENCRYPT/DECRYPT TRUNCATION OCCURED.
         SPACE 1
LSTMSG   CLC   LRECL,BUFLVA
         BE    EXIT
         MVC   MLTLV+5(1),LRECL+L'LRECL-1
         NI    MLTLV+5,X'07'
         OI    MLTLV+5,C'0'
         CALL  ISPLINK,(VREP,MLTM,MLTML,MLTMV),VL
         LR    R10,R15
         CALL  ISPLINK,(VREP,MLTL,MLTLL,MLTLV),VL
         B     QUIT
         SPACE 1
*-----------------------------  H E L P  ------------------------------
*        DISPLAY THE HELP PANELS.
*----------------------------------------------------------------------
         SPACE 1
* ------ CALL ISPLINK TO DISPLAY THE HELP PANELS.
         SPACE 1
DSHELP   CALL  ISPLINK,(DSPL,PNLH),VL
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   DSHXIT              NOT ZERO
         CALL  ISPLINK,(DSPL,PNLH1),VL
         LTR   R15,R15             CHECK RETURN CODE
         BZ    EXIT                OK
DSHXIT   CL    R15,LEN8            CHECK IF END
         BNE   ERR19               NOT END, ERROR
         SPACE 1
*------------------------------  E N D  -------------------------------
*        PROCESSING TERMINATION.
*----------------------------------------------------------------------
         SPACE 1
* ------ EXIT CODE FOLLOWS.
         SPACE 1
EXIT     XR    R10,R10             SET RETURN CODE
LEAVE   $XRET  CC=(R10)            RETURN
         SPACE 1
*-------------------------  D I A G N O S E  --------------------------
*        GENERATE ERROR MESSAGES.
*----------------------------------------------------------------------
         SPACE 1
* ------ SET LONG MESSAGE SPECIFIC TEXT FOR ERROR DIAGNOSE.
         SPACE 1
ERR1     MVC   MLOP,=CL8'FCDE    '
ERRA     MVC   MLFU,VDEF
         B     ERWTO
ERR2     MVC   MLOP,=CL8'BUFFER  '
         B     ERRA
ERR3     MVC   MLOP,=CL8'LRECL   '
         B     ERRA
ERR4     MVC   MLOP,=CL8'LRC     '
         B     ERRA
ERR5     MVC   MLOP,=CL8'EDMAC   '
         MVC   MLFU,EDIT
         B     ERWTO
ERR6     MVC   MLOP,=CL8'EDPRC   '
ERRB     MVC   MLFU,EDIT
         B     ERRB
ERR7     MVC   MLOP,=CL8'EDQUE   '
         B     ERRB
ERR8     MVC   MLOP,=CL8'EDFRG   '
         B     ERRB
ERR9     MVC   MLOP,=CL8'EDLRG   '
         B     ERRB
ERR10    MVC   MLOP,=CL8'EDLLR   '
         B     ERRB
ERR11    MVC   MLOP,=CL8'EDLRL   '
         B     ERRB
ERR12    MVC   MLOP,=CL8'EDOFF   '
         B     ERRB
ERR13    MVC   MLOP,PNLN
ERRC     MVC   MLFU,DSPL
         B     ERRET
ERR14    MVC   MLOP,CL
         MVC   MLFU,VCPY
         B     ERRET
ERR15    MVC   MLFU,=CL8'SETKEY  '
         MVC   MLOP,=CL8'KEYVALUE'
         B     ERRET
ERR16    MVC   MLOP,=CL8'EDRLN   '
         B     ERRB
ERR17    MVC   MLFU+2(6),=CL6'CRYPT'
         L     R1,FUNC             GET FUNCTION
         B     *+L'*(R1)
         B     *+L'*+10
         MVC   MLFU(2),=CL2'DE'
         B     *+L'*+6
         MVC   MLFU(2),=CL2'EN'
         MVC   MLOP(2),=CL2'AT'
         MVC   MLOP+2(6),=XL6'402020202120'
         L     R1,LINENO           GET LINE NUMBER
         CVD   R1,WORKD
         ED    MLOP+2(6),WORKD+5
         B     ERRET
ERR18    MVC   MLOP,=CL8'EDWLN   '
         B     ERRB
ERR19    MVC   MLOP,=CL8'HELP    '
         B     ERRC
         SPACE 1
* ------ CALL ISPLINK TO SET SHORT/LONG MESSAGES.
         SPACE 1
ERRET    CVD   R15,WORKD           EDIT RETURN CODE RECEIVED
         MVC   MSRC,=XL4'40202120'
         ED    MSRC,WORKD+6
         CALL  ISPLINK,(VREP,MS,MSL,MSV),VL
         LR    R10,R15
         CALL  ISPLINK,(VREP,ML,MLL,MLV),VL
QUIT     LTR   R10,R10
         BZ    *+L'*+6
         SLL   R15,8
         OR    R10,R15
         CALL  ISPLINK,(SETM,MSGID),VL
         LTR   R10,R10
         BZ    LEAVE
         SLL   R15,16
         OR    R10,R15
         B     LEAVE
         SPACE 1
RGERR    CALL  ISPLINK,(VREP,RGM,RGML,RGMV),VL
         LR    R10,R15
         CALL  ISPLINK,(VREP,RGL,RGLL,RGLV),VL
         B     QUIT
RGINV    CALL  ISPLINK,(VREP,RIM,RIML,RIMV),VL
         LR    R10,R15
         CALL  ISPLINK,(VREP,RIL,RILL,RILV),VL
         B     QUIT
ERMLG    CALL  ISPLINK,(VREP,MLGM,MLGML,MLGMV),VL
         LR    R10,R15
         CALL  ISPLINK,(VREP,MLGL,MLGLL,MLGLV),VL
         B     QUIT
         SPACE 1
* ------ WTO SHORT/LONG MESSAGES (WE ARE NOT YET EDIT MACRO).
         SPACE 1
ERWTO    CVD   R15,WORKD           EDIT RETURN CODE RECEIVED
         MVC   MSRC,=XL4'40202120'
         ED    MSRC,WORKD+6
         MVC   WTOLST+4(MSVL),MSV
        WTO    MF=(E,WTOLST)
         MVC   WTOLST+4(MLVL),MLV
        WTO    MF=(E,WTOLST)
         LA    R10,16
         B     LEAVE
         PRINT GEN
         EJECT
* ------ CONSTANTS AND WORK-AREAS.
         SPACE 1
WORKD    DC    D'0'
SETADD   DC    V(SETKEY)           VCON OF SETKEY ROUTINE
CIPHADD  DC    V(ENCIPH)           VCON OF ENCIPH ROUTINE
         DC    V(DECIPH)           VCON OF DECIPH ROUTINE
FUNC     DC    F'0'                FUNCTION SAVE
         SPACE 1
NL       DC    C'(FCDE,KEY)'
         DS    0F                  ALIGN
FCDE     DC    CL8' '              AREA FOR THE FUNCTION
KEY      DC    CL8' '              AREA FOR THE KEY VALUE
         SPACE 1
LL       DC    C'(BUFFER)'
         DS    0F                  ALIGN
         SPACE 1
DL       DC    C'(LRECL,LLINE,LINENO)'
         DS    0F                  ALIGN
LRECL    DC    F'0'                AREA FOR THE DATA LENGTH
LLINE    DC    F'0'                AREA FOR THE LAST LINE NUMBER
LINENO   DC    F'0'                CURRENT LINE NUMBER
         SPACE 1
RL       DC    C'(LRC)'
         DS    0F                  ALIGN
LRC      DC    CL1' '              AREA FOR THE LINE COMMAND SETTING
         SPACE 1
CL       DC    C'(CRYKEY)'
         DS    0F                  ALIGN
         SPACE 1
MS       DC    C'(ZEDSMSG)'
         DS    0F                  ALIGN
MSL      DC    A(MSVL)
MSV      DC    C'RC ='
MSRC     DC    CL4' '
MSVL     EQU   *-MSV
ML       DC    C'(ZEDLMSG)'
         DS    0F                  ALIGN
MLL      DC    A(MLVL)
MLV      DC    C'-- ERROR AT FUNCTION : '
MLFU     DC    CL8' ',C' / OPERATION : '
MLOP     DC    CL8' ',C' --'
MLVL     EQU   *-MLV
         SPACE 1
RGM      DC    C'(ZEDSMSG)'
         DS    0F                  ALIGN
RGML     DC    A(L'RGMV)
RGMV     DC    C'RANGE INCOMPLETE'
RGL      DC    C'(ZEDLMSG)'
         DS    0F                  ALIGN
RGLL     DC    A(L'RGLV)
RGLV     DC    C'ENTER A MATCHING "CC" TO COMPLETE THE BLOCK COMMAND PA1
               IR'
         SPACE 1
RIM      DC    C'(ZEDSMSG)'
         DS    0F                  ALIGN
RIML     DC    A(L'RIMV)
RIMV     DC    C'INVALID RANGE'
RIL      DC    C'(ZEDLMSG)'
         DS    0F                  ALIGN
RILL     DC    A(L'RILV)
RILV     DC    C'YOU MUST SPECIFY THE TARGET RANGE OF LINES USING "CN" 1
               OR "CC"'
         SPACE 1
MLGM     DC    C'(ZEDSMSG)'
         DS    0F                  ALIGN
MLGML    DC    A(L'MLGMV)
MLGMV    DC    C'LRECL GT 256'
MLGL     DC    C'(ZEDLMSG)'
         DS    0F                  ALIGN
MLGLL    DC    A(L'MLGLV)
MLGLV    DC    C'AN "LRECL" GREATER THAN 256 IS NOT SUPPORTED, SORRY'
         SPACE 1
MLTM     DC    C'(ZEDSMSG)'
         DS    0F                  ALIGN
MLTML    DC    A(L'MLTMV)
MLTMV    DC    C'TRUNCATION'
MLTL     DC    C'(ZEDLMSG)'
         DS    0F                  ALIGN
MLTLL    DC    A(L'MLTLV)
MLTLV    DC    C'LAST   BYTES SKIPPED ("LRECL" NOT MULTIPLE OF 8)'
         SPACE 1
VDEF     DC    CL8'VDEFINE '
EDIT     DC    CL8'ISREDIT '
DSPL     DC    CL8'DISPLAY '
VCPY     DC    CL8'VCOPY   '
VREP     DC    CL8'VREPLACE'
SETM     DC    CL8'SETMSG  '
TYPEC    DC    CL8'CHAR    '
TYPEF    DC    CL8'FIXED   '
MVM      DC    CL8'MOVE    '
PNLN     DC    CL8'PAJCRY  '
PNLH     DC    CL8'PAJTCRY '
PNLH1    DC    CL8'PAJTCRY1'
MSGID    DC    CL8'ISRZ001 '
LEN0     DC    F'0'
LEN1     DC    F'1'
LEN4     DC    F'4'
LEN8     DC    F'8'
LEN256   DC    F'256'
         SPACE 1
EDMAC    DC    C'¢MACRO (FCDE,KEY) NOPROCESS¢'
EDPRC    DC    C'¢PROCESS RANGE C¢'
EDQUE    DC    C'¢(LRC) = RANGE_CMD¢'
EDFRG    DC    C'¢(LINENO) = LINENUM .ZFRANGE¢'
EDLRG    DC    C'¢(LLINE) = LINENUM .ZLRANGE¢'
EDLLR    DC    C'¢(LLINE) = LINENUM .ZLAST¢'
EDLRL    DC    C'¢(LRECL) = LRECL¢'
EDOFF    DC    C'¢CAPS OFF¢'
EDRLN    DC    C'¢(BUFFER) = LINE &&LINENO¢'
EDWLN    DC    C'¢LINE &&LINENO =(BUFFER)¢'
         SPACE 1
         DS    0F                  ALIGN
KEYADD   DC    A(KEY)              KEY ADDRESS
PARMLIST DC    A(BUFFER)           BUFFER ADDRESS
         DC    A(BUFLVA)           BUFFER LENGTH VALUE ADDRESS
BUFLVA   DC    A(*-*)              BUFFER LENGTH VALUE
         SPACE 1
*               ----+----1----+----2----+----3----+----4----+----5----+
*              ----6----+----7----+---
WTOLST  WTO    '                                                       1
                                      ',ROUTCDE=11,DESC=7,MF=L
         SPACE 1
         LTORG
         SPACE 1
         DROP  R11,R12 - - - - - - END OF BASE REGISTER ADDRESSABILITY
         SPACE 1
         DS    0F
BUFFER   DC    CL256' '            ENCRYPT/DECRYPT BUFFER
BUFLEN   EQU   *-BUFFER            BUFFER LENGTH
         SPACE 2
         END
