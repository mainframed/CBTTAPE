PROC     0 SMID() SMDSG(SPF) SMDST(PROF) SMMEM(SM$PROF) SMTEST()
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN DO
           WRITE *** NOT AVAILABLE OUTSIDE ISPF ***
           EXIT CODE(0)
           END
         IF &SUBSTR(1:3,&SYSUID) NE SYS AND +
           &SUBSTR(1:3,&SYSUID) NE S32 THEN DO
             ISPEXEC SETMSG MSG(PAJS000X)
             EXIT CODE(0)
             END
         IF &SMTEST EQ TEST THEN CONTROL LIST CONLIST SYMLIST MSG
         IF &SMID EQ THEN DO
           SET &SMSWU = YES
           SET &SMSWC = NO
           SET &SMID = &SYSUID
           END
         ELSE SET &SMSWU = NO
         SET &SMSWT = NO
         SET &SMSWM = NO
         ISPEXEC CONTROL ERRORS RETURN
         IF &SMSWU EQ YES THEN -
         ISPEXEC LMINIT DATAID(SMFIN) -
           DATASET('&SMID..&SMDSG..&SMDST') ENQ(EXCLU)
         ELSE ISPEXEC LMINIT DATAID(SMFIN) -
           DATASET('&SMID..&SMDSG..&SMDST')
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001A)
           ELSE ISPEXEC SETMSG MSG(PAJS002A)
           SET &SMSWM = YES
           GOTO ERIFI
           END
         ISPEXEC LMOPEN DATAID(&SMFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001B)
           ELSE ISPEXEC SETMSG MSG(PAJS002B)
           SET &SMSWM = YES
           GOTO EROFI
           END
         ISPEXEC LMMFIND DATAID(&SMFIN) MEMBER(&SMMEM)
         SET &RC = &LASTCC
         IF &SMSWU EQ YES THEN DO
           IF &RC EQ 8 THEN DO
             SET &SMSWC = YES
             GOTO ERFFI
             END
           ELSE IF &RC EQ 4 THEN DO
             ISPEXEC SETMSG MSG(PAJS002C)
             SET &SMSWM = YES
             GOTO ERFFI
             END
           END
         IF &RC NE 0 THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001D)
           ELSE ISPEXEC SETMSG MSG(PAJS002D)
           SET &SMSWM = YES
           GOTO ERFFI
           END
         ISPEXEC TBCREATE SMTABLE REPLACE NOWRITE NAMES(SMAROU)
         CONTROL ASIS
GETFI:   ISPEXEC LMGET DATAID(&SMFIN) MODE(INVAR) +
           DATALOC(SMARIN) DATALEN(SMALIN) MAXLEN(80)
         SET &RC = &LASTCC
         IF &RC EQ 8 THEN GOTO EOFFI
         IF &RC NE 0 THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001E)
           ELSE ISPEXEC SETMSG MSG(PAJS002E)
           SET &SMSWM = YES
           SET &SMSWT = NO
           GOTO EOFFI
           END
         SET &SMARLIN = &LENGTH(&STR(&SMARIN))
         IF &SMARLIN GE 11 THEN DO
           SET &SMTT = &SUBSTR(1:11,&SMARIN)
           IF &SMSWU EQ NO THEN DO
             IF &STR(&SMTT) EQ &STR(STOP       ) THEN GOTO EOFFI
             END
           IF &SMSWU EQ YES THEN DO
             IF &STR(&SMTT) EQ &STR(Aggiornato ) THEN GOTO GETFI
             IF &STR(&SMTT) EQ &STR(Ajourn! au ) THEN GOTO GETFI
             IF &STR(&SMTT) EQ &STR(Updated on ) THEN GOTO GETFI
             END
           END
         IF &SMARLIN GT 72 THEN SET &SMARLIN = 72
         SET &SMAROU = &STR(&SUBSTR(1:&SMARLIN,&SMARIN))
         ISPEXEC TBADD SMTABLE
         SET &SMSWT = YES
         GOTO GETFI
EOFFI:   CONTROL CAPS
ERFFI:   ISPEXEC LMCLOSE DATAID(&SMFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001F)
             ELSE ISPEXEC SETMSG MSG(PAJS002F)
             SET &SMSWM = YES
             END
           END
         IF &SMSWU EQ NO THEN GOTO EROFI
         IF &SMSWC EQ YES THEN GOTO UPDFI
         IF &SMSWT EQ YES THEN GOTO UPDFI
EROFI:   ISPEXEC LMFREE DATAID(&SMFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001G)
             ELSE ISPEXEC SETMSG MSG(PAJS002G)
             SET &SMSWM = YES
             END
           END
         IF &SMSWT EQ NO THEN GOTO ENDTB
         ISPEXEC TBTOP SMTABLE
DSTSC:   ISPEXEC TBDISPL SMTABLE PANEL(PAJSM1) POSITION(CRP)
         SET &RC = &LASTCC
         IF &RC LE 4 THEN GOTO DSTSC
         IF &RC NE 8 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS001H)
             END
           END
ENDTB:   ISPEXEC TBEND SMTABLE
ERIFI:   ISPEXEC CONTROL ERRORS CANCEL
         IF &SMSWM EQ NO THEN ISPEXEC SETMSG MSG(PAJS009A)
         EXIT CODE(0)
UPDFI:   CONTROL ASIS
         IF &SMSWC EQ YES THEN DO
           SET &SMTIT = &STR(Create)
           ISPEXEC TBCREATE SMTABLE REPLACE NOWRITE NAMES(SMAROU)
           SET &SMAROU = &STR( )
           ISPEXEC TBADD SMTABLE
           END
         ELSE SET &SMTIT = &STR(Update)
         ISPEXEC VPUT (SMTIT SMID)
         ISPEXEC VGET (SMLANG) PROFILE
         SET &RC = 0
         SET &ZTDTOP = 0
ULOOP:   IF &RC EQ 4 THEN ISPEXEC TBDISPL SMTABLE POSITION(CRP)
         ELSE DO
           ISPEXEC TBTOP SMTABLE
           ISPEXEC TBSKIP SMTABLE NUMBER(&ZTDTOP)
           ISPEXEC VGET (ZVERB ZSCROLLN)
           IF &ZVERB EQ UP THEN -
             ISPEXEC TBSKIP SMTABLE NUMBER(-&ZSCROLLN)
           ELSE IF &ZVERB EQ DOWN THEN -
             ISPEXEC TBSKIP SMTABLE NUMBER(&ZSCROLLN)
           ISPEXEC TBDISPL SMTABLE PANEL(PAJSM2) POSITION(CRP)
           END
         SET &RC = &LASTCC
         IF &RC GT 8 THEN DO
           ISPEXEC SETMSG MSG(PAJS009B)
           SET &SMSWM = YES
           GOTO XLOOP
           END
         ELSE IF &RC EQ 8 OR &CMD EQ SAVE THEN GOTO TSAVE
         ELSE IF &CMD EQ CANCEL THEN DO
           ISPEXEC SETMSG MSG(PAJS009C)
           SET &SMSWM = YES
           GOTO XLOOP
           END
         ELSE IF &ZTDSELS GE 1 THEN DO
           IF &SMA EQ A OR &SMA EQ B THEN DO
             SET &HIGH = &MM*9
             SET &LOW = &HIGH-8
             IF &SMLANG EQ I THEN DO
               SET &GN = &STR(Gennaio  Febbraio Marzo    Aprile   )
               SET &GN = &GN&STR(Maggio   Giugno   Luglio   Agosto   )
               SET &GN = &GN&STR(SettembreOttobre  Novembre Dicembre )
               END
             ELSE IF &SMLANG EQ F THEN DO
               SET &GN = &STR(Janvier  Fevrier  Mars     Avril    )
               SET &GN = &GN&STR(Mai      Juin     Juillet  Aout     )
               SET &GN = &GN&STR(SeptembreOctobre  Novembre D!cembre )
               END
             ELSE DO
               SET &GN = &STR(January  February March    April    )
               SET &GN = &GN&STR(May      June     July     August   )
               SET &GN = &GN&STR(SeptemberOctober  November December )
               END
             SET &MONTH = &SUBSTR(&LOW:&HIGH,&GN)
             SET &MONTH = &MONTH          /* TO LOSE TRAILING BLANKS */
             SET &ISEC = 1900
             IF &AA = 0 THEN SET &ISEC = 2000
             SET &AA1 = &AA+&ISEC
             SET &N0 = ((&AA1/4)*4)
             SET &N2 = &MM*2
             SET &N1 = &N2-1
             IF &AA1 EQ &N0 THEN -
               SET &GGT = &SUBSTR(&N1:&N2,312931303130313130313031)
             ELSE SET &GGT = &SUBSTR(&N1:&N2,312831303130313130313031)
             IF &GG GT &GGT THEN DO
               ISPEXEC SETMSG MSG(PAJS000Y)
               GOTO ULOOP
               END
             SET &MM1 = &SUBSTR(&MM,032503514624)
             SET &GGX = &GG+&MM1
             SET &AA2 = ((&AA1*100)+&MM-3)/100
             SET &N0 = &AA2/100
             SET &N01 = &AA2//100
             SET &N1 = &N0//4
             SET &N2 = &N01//4
             SET &N4 = &N01/4 +&N1
             SET &N7 = &GGX+&N2+&N4*5
             SET &N8 = &N7//7        /* DAY NUMBER, WHERE SUNDAY = 0 */
             SET &LOW = &N8*10+1
             SET &HIGH = &LOW+9
             IF &SMLANG EQ I THEN DO
               SET &GN = &STR(Domenica  Lunedi'   Martedi'  Mercoledi')
               SET &GN = &GN&STR(Giovedi'  Venerdi'  Sabato    )
               END
             ELSE IF &SMLANG EQ F THEN DO
               SET &GN = &STR(Dimanche  Lundi     Mardi     Mercredi  )
               SET &GN = &GN&STR(Jeudi     Vendredi  Samedi    )
               END
             ELSE DO
               SET &GN = &STR(Sunday    Monday    Tuesday   Wenesday  )
               SET &GN = &GN&STR(Thursday  Friday    Saterday  )
               END
             SET &SMD = &SUBSTR(&LOW:&HIGH,&GN)
             SET &SMD = &SMD              /* TO LOSE TRAILING BLANKS */
             IF &SUBSTR(1,&GG) ¬= 0 THEN DO
               IF &LENGTH(&GG) = 1 THEN SET &GG = &STR(0&GG)
               END
             IF &SUBSTR(1,&MM) ¬= 0 THEN DO
               IF &LENGTH(&MM) = 1 THEN SET &MM = &STR(0&MM)
               END
             IF &SUBSTR(1,&AA) ¬= 0 THEN DO
               IF &LENGTH(&AA) = 1 THEN SET &AA = &STR(0&AA)
               END
             SET &SMAROU = &STR(--- &SMD &GG &MONTH &AA1 :)
             IF &SMA EQ B THEN ISPEXEC TBSKIP SMTABLE NUMBER(-1)
             ISPEXEC TBADD SMTABLE
             END
           ELSE IF &SMA EQ D THEN DO
             DO WHILE &SMN GE 1
               ISPEXEC TBDELETE SMTABLE
               ISPEXEC TBSKIP SMTABLE
               IF &LASTCC NE 0 THEN SET &SMN = 0
               ELSE SET &SMN = &SMN - 1
               END
             END
           ELSE IF &SMA EQ I THEN DO
             DO WHILE &SMN GE 1
               SET &SMAROU = &STR( )
               ISPEXEC TBADD SMTABLE
               SET &SMN = &SMN - 1
               END
             END
           ELSE IF &SMA EQ R THEN DO
             DO WHILE &SMN GE 1
               ISPEXEC TBADD SMTABLE
               SET &SMN = &SMN - 1
               END
             END
           ELSE IF &SMA EQ S THEN DO
             SET &SMAROU = &STR(STOP       )
             ISPEXEC TBADD SMTABLE
             END
           ELSE IF &SMA EQ &STR( ) THEN ISPEXEC TBPUT SMTABLE
           END
         GOTO ULOOP
TSAVE:   SET &GG = &SUBSTR(4:5,&SYSDATE)
         SET &MM = &SUBSTR(1:2,&SYSDATE)
         SET &AA = &SUBSTR(7:8,&SYSDATE)
         SET &ISEC = 1900
         IF &AA = 0 THEN SET &ISEC = 2000
         SET &MM1 = &SUBSTR(&MM,032503514624)
         SET &GGX = &GG+&MM1
         SET &AA1 = &AA+&ISEC
         SET &AA2 = ((&AA1*100)+&MM-3)/100
         SET &N0 = &AA2/100
         SET &N01 = &AA2//100
         SET &N1 = &N0//4
         SET &N2 = &N01//4
         SET &N4 = &N01/4 +&N1
         SET &N7 = &GGX+&N2+&N4*5
         SET &N8 = &N7//7            /* DAY NUMBER, WHERE SUNDAY = 0 */
         SET &LOW = &N8*10+1
         SET &HIGH = &LOW+9
         IF &SMLANG EQ I THEN DO
           SET &GN = &STR(Domenica  Lunedi'   Martedi'  Mercoledi')
           SET &GN = &GN&STR(Giovedi'  Venerdi'  Sabato    )
           END
         ELSE IF &SMLANG EQ F THEN DO
           SET &GN = &STR(Dimanche  Lundi     Mardi     Mercredi  )
           SET &GN = &GN&STR(Jeudi     Vendredi  Samedi    )
           END
         ELSE DO
           SET &GN = &STR(Sunday    Monday    Tuesday   Wenesday  )
           SET &GN = &GN&STR(Thursday  Friday    Saterday  )
           END
         SET &SMD = &SUBSTR(&LOW:&HIGH,&GN)
         SET &SMD = &SMD                  /* TO LOSE TRAILING BLANKS */
         SET &HIGH = &MM*9
         SET &LOW = &HIGH-8
         IF &SMLANG EQ I THEN DO
           SET &GN = &STR(Gennaio  Febbraio Marzo    Aprile   )
           SET &GN = &GN&STR(Maggio   Giugno   Luglio   Agosto   )
           SET &GN = &GN&STR(SettembreOttobre  Novembre Dicembre )
           END
         ELSE IF &SMLANG EQ F THEN DO
           SET &GN = &STR(Janvier  Fevrier  Mars     Avril    )
           SET &GN = &GN&STR(Mai      Juin     Juillet  Aout     )
           SET &GN = &GN&STR(SeptembreOctobre  Novembre D!cembre )
           END
         ELSE DO
           SET &GN = &STR(January  February March    April    )
           SET &GN = &GN&STR(May      June     July     August   )
           SET &GN = &GN&STR(SeptemberOctober  November December )
           END
         SET &MONTH = &SUBSTR(&LOW:&HIGH,&GN)
         SET &MONTH = &MONTH              /* TO LOSE TRAILING BLANKS */
         IF &SUBSTR(1,&GG) ¬= 0 THEN DO
           IF &LENGTH(&GG) = 1 THEN SET &GG = &STR(0&GG)
           END
         IF &SUBSTR(1,&MM) ¬= 0 THEN DO
           IF &LENGTH(&MM) = 1 THEN SET &MM = &STR(0&MM)
           END
         IF &SUBSTR(1,&AA) ¬= 0 THEN DO
           IF &LENGTH(&AA) = 1 THEN SET &AA = &STR(0&AA)
           END
         SET &SMO = &SUBSTR(1:5,&SYSTIME)
         IF &SMLANG EQ I THEN DO
         SET &SMUPDT = &STR(Aggiornato &SMD &GG &MONTH &AA1 ore &SMO..)
           END
         ELSE IF &SMLANG EQ F THEN DO
         SET &SMUPDT = &STR(Ajourn! au &SMD &GG &MONTH &AA1 { &SMO..)
           END
         ELSE DO
         SET &SMUPDT = &STR(Updated on &SMD &GG &MONTH &AA1 at &SMO..)
           END
         SET &SMSWI = NO
         ISPEXEC LMOPEN DATAID(&SMFIN) OPTION(OUTPUT)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMTEST EQ TEST THEN DO
             WRITE *** OPEN ERROR - RC=&RC ***
             WRITE &ZERRLM
             END
           ELSE WRITE "&SMID" communication cannot be saved
           GOTO XLOOP
           END
         SET &SMSWW = NO
         ISPEXEC TBTOP SMTABLE
WLOOP:   ISPEXEC TBSKIP SMTABLE NUMBER(+1)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN GOTO WLEND
         ISPEXEC TBGET SMTABLE
         SET &RC = &LASTCC
         IF &RC GT 4 THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003A)
           ELSE ISPEXEC SETMSG MSG(PAJS004A)
           SET &SMSWM = YES
           GOTO WRCLS
           END
         SET &SMSWW = YES
         IF &SMSWI EQ NO THEN DO
           SET &SMARLOU = &LENGTH(&STR(&SMAROU))
           IF &SMARLOU GE 11 THEN DO
             SET &SMTT = &SUBSTR(1:11,&SMAROU)
             IF &STR(&SMTT) EQ &STR(STOP       ) THEN DO
               SET &SMARIN = &STR(&SMUPDT)
               ISPEXEC LMPUT DATAID(&SMFIN) MODE(INVAR) +
                 DATALOC(SMARIN) DATALEN(80) NOBSCAN
               SET &RC = &LASTCC
               IF &RC NE 0 THEN DO
                 IF &SMSWM EQ NO THEN DO
                   IF &SMTEST EQ TEST THEN -
                     ISPEXEC SETMSG MSG(PAJS003B)
                   ELSE ISPEXEC SETMSG MSG(PAJS004B)
                   SET &SMSWM = YES
                   END
                 GOTO WRCLS
                 END
               SET &SMSWI = YES
               END
             END
           END
         SET &SMARIN = &STR(&SMAROU)
         ISPEXEC LMPUT DATAID(&SMFIN) MODE(INVAR) +
           DATALOC(SMARIN) DATALEN(80) NOBSCAN
         SET &RC = &LASTCC
         IF &RC EQ 0 THEN GOTO WLOOP
         IF &SMSWM EQ NO THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003B)
           ELSE ISPEXEC SETMSG MSG(PAJS004B)
           SET &SMSWM = YES
           END
         GOTO WRCLS
WLEND:   IF &RC GT 8 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003C)
             ELSE ISPEXEC SETMSG MSG(PAJS004C)
             SET &SMSWM = YES
             END
           GOTO WRCLS
           END
         IF &SMSWW EQ NO THEN DO
           IF &SMSWC EQ NO THEN DO
             ISPEXEC LMMDEL DATAID(&SMFIN) MEMBER(&SMMEM)
             SET &RC = &LASTCC
             IF &RC EQ 0 THEN ISPEXEC SETMSG MSG(PAJS009D)
             ELSE IF &SMSWM EQ NO THEN DO
               IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003D)
               ELSE ISPEXEC SETMSG MSG(PAJS004D)
               END
             END
           ELSE ISPEXEC SETMSG MSG(PAJS009E)
           SET &SMSWM = YES
           GOTO WRCLS
           END
         IF &SMSWI EQ NO THEN DO
           SET &SMARIN = &STR(&SMUPDT)
           ISPEXEC LMPUT DATAID(&SMFIN) MODE(INVAR) +
             DATALOC(SMARIN) DATALEN(80) NOBSCAN
           SET &RC = &LASTCC
           IF &RC NE 0 THEN DO
             IF &SMSWM EQ NO THEN DO
               IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003B)
               ELSE ISPEXEC SETMSG MSG(PAJS004B)
               SET &SMSWM = YES
               END
             GOTO WRCLS
             END
           END
         IF &SMSWC EQ YES THEN -
           ISPEXEC LMMADD DATAID(&SMFIN) MEMBER(&SMMEM)
         ELSE ISPEXEC LMMREP DATAID(&SMFIN) MEMBER(&SMMEM)
         SET &RC = &LASTCC
         IF &RC EQ 0 THEN GOTO WRCLS
         ELSE IF &SMSWM EQ NO THEN DO
           IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003E)
           ELSE ISPEXEC SETMSG MSG(PAJS004E)
           SET &SMSWM = YES
           END
WRCLS:   ISPEXEC LMCLOSE DATAID(&SMFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003F)
             SET &SMSWM = YES
             END
           END
XLOOP:   CONTROL CAPS
         ISPEXEC LMFREE DATAID(&SMFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SMSWM EQ NO THEN DO
             IF &SMTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJS003G)
             SET &SMSWM = YES
             END
           END
         ISPEXEC TBEND SMTABLE
         ISPEXEC CONTROL ERRORS CANCEL
         IF &SMSWM EQ NO THEN ISPEXEC SETMSG MSG(PAJS009F)
         EXIT CODE(0)
