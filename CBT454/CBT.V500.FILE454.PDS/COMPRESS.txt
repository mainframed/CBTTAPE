ISREDIT  MACRO (APARM)
/*  MACRO NAME : COMPRESS                                           */
/*    FUNCTION : COMPRESS IN PLACE CURRENT PDS DATA-SET             */
/*      SYNTAX : COMPRESS                                           */
/*               COMPRESS HELP OR H                                 */
/*       DEBUG : COMPRESS LIST                                      */
/*      ORIGIN : CBT TAPE 89 FEB, FILE 95                           */
  IF &STR(&APARM.) ¬= &STR() THEN DO
    IF &STR(&APARM.) = &STR(HELP) OR +
      &STR(&APARM.) = &STR(H) THEN DO
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTCOMP)
        SET &RCX = &LASTCC
        ISPEXEC CONTROL DISPLAY RESTORE
        EXIT CODE(&RCX)
        END
    ELSE IF &STR(&APARM.) ¬= &STR(LIST) THEN DO
      SET ZEDSMSG = INVALID OPERAND
      SET ZEDLMSG = VALID OPERAND ARE HELP OR NULL
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    END
  ISREDIT (WORKDSN) = DATASET
  LISTDSI '&WORKDSN'
  SET &DSORG = &SYSDSORG
  IF &DSORG NE PO THEN DO
    SET ZEDSMSG = &STR(INVALID DSORG)
    SET ZEDLMSG = &STR('&WORKDSN' DSORG IS &DSORG, ONLY PO IS ALLOWED)
    END
  ELSE DO
    SET  PDSDSN = &WORKDSN
    IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
    ISPEXEC LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                   ENQ(EXCLU) ORG(PDSORG)
    SET &INITCC = &LASTCC
    IF &INITCC ¬= 0 THEN DO
      SET ZEDSMSG = &STR(LMINIT RC=&INITCC)
      SET ZEDLMSG = &STR('&PDSDSN' ALLOCATION FAILED OR DSORG NOT PO)
      END
    ELSE DO
      ISPEXEC LMCOMP DATAID(&PDSID)
      SET &COMPCC = &LASTCC
      SET ZEDSMSG = &STR(LMCOMP RC=&COMPCC)
      IF &COMPCC = 0 THEN +
        SET ZEDLMSG = &STR('&PDSDSN' SUCCESSFULLY COMPRESSED)
      ELSE IF &COMPCC = 10 THEN +
        SET ZEDLMSG = &STR('&PDSDSN' NOT ASSOCIATED WITH THE DATA-ID)
      ELSE IF &COMPCC = 12 THEN +
        SET ZEDLMSG = &STR('&PDSDSN' IS NOT A PARTITIONED DATA-SET)
      ELSE IF &COMPCC = 20 THEN +
        SET ZEDLMSG = &STR(UNABLE TO COMPRESS '&PDSDSN')
      ELSE SET ZEDLMSG = &STR(UNKNOWN RC FROM COMPRESS '&PDSDSN')
      END
    END
  ISPEXEC SETMSG MSG(ISRZ001)
  IF &DSORG NE PO THEN EXIT
  IF &INITCC ¬= 0 THEN EXIT
  ISPEXEC LMCLOSE DATAID(&PDSID)
  ISPEXEC LMFREE DATAID(&PDSID)
EXIT
