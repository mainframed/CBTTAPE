ISREDIT  MACRO (APARM)
/*  MACRO NAME : RESUME                                             */
/*    FUNCTION : RELOAD LAST SAVED COPY OF A MEMBER                 */
/*      SYNTAX : RESUME                                             */
/*               RESUME HELP OR H                                   */
/*       DEBUG : RESUME LIST                                        */
/*      ORIGIN : CBT TAPE 89 FEB, FILE 15                           */
  IF &STR(&APARM.) Â¬= &STR() THEN DO
    IF &STR(&APARM.) = &STR(HELP) OR +
      &STR(&APARM.) = &STR(H) THEN DO
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTRSM)
        SET &RCX = &LASTCC
        ISPEXEC CONTROL DISPLAY RESTORE
        EXIT CODE(&RCX)
        END
    IF &STR(&APARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
    ELSE DO
      SET ZEDSMSG = INVALID OPERAND
      SET ZEDLMSG = VALID OPERAND ARE HELP OR NULL
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    END
  CONTROL ASIS
  ISREDIT (MEMBER) = MEMBER
  IF &MEMBER = THEN DO
    SET ZEDSMSG = PO ONLY ALLOWED
    SET ZEDLMSG = ON PS FILES YOU MUST PERFORM THIS FUNCTION YOURSELF
    END
  ELSE DO
    ISREDIT (CHANGED) = DATA_CHANGED
    IF &CHANGED = YES THEN DO
      ISREDIT EXCLUDE ALL
      ISREDIT COPY &MEMBER BEFORE .ZFIRST
      ISREDIT DELETE ALL EX
      SET ZEDSMSG = RESUME DONE
      SET ZEDLMSG = THIS COPY IS NOW REPLACED WITH THE LAST DISK ONE
      END
    ELSE DO
      SET ZEDSMSG = NOT REQUIRED
      SET ZEDLMSG = THIS COPY WAS NOT CHANGED FROM THE LAST DISK ONE
      END
    END
  ISPEXEC SETMSG MSG(ISRZ000)
EXIT
