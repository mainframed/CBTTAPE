++ USERMOD(AHMW002) /* HSM TAPE DATASET EXIT   :

       THIS MODIFICATION WILL CAUSE DFHSM TO HOLD BACKUP OR SECOND
       LEVEL MIGRATION, INSTEAD OF ASKING FOR A SCRATCH TAPE MOUNT.

       NOTE: THIS EXIT WILL GO TO SYS2.LINKLIB (DDDEF HSSLINK).
                                                                 */ .
++ VER(Z038) FMID(HHM2302) .
++ JCLIN .
//GEN11 JOB WC07N0323,'531-1  CM SINGER'
//SG22 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLINK,P1=' ',
//    MOD=,P2=' ',CLASS=T
//SYSPUNCH DD DUMMY
//INCLUDE DD DISP=SHR,DSNAME=MVS3.INCLUDE
//SYSLIN DD *
 INCLUDE INCLUDE(ARCTDEXT)
 NAME ARCTDEXT(R)
/*
++ SRC(ARCTDEXT) DISTLIB(ASRCLIB) .
ARCTDEXT TITLE 'DFHSM ARCTDEXT - DFHSM TAPE DATASET EXIT'
***********************************************************************
*                                                                     *
*    NAME: DFHSM EXIT ARCTDEXT                                        *
*                                                                     *
*          SETSYS EXITS(NNNNNNYNN)                                    *
*                                        ****                         *
*   SPECIAL OPTIONS FOR LINKAGE EDITOR:  RENT,NCAL,LIST,MAP,LET       *
*                                        ****                         *
*                                                                     *
*   PURPOSE:  TO EXAMINE THE JFCB TO SEE IF THE VOLSER IS OUT OF      *
* SPECIFIED RANGE FOR HSM USE.  IF IT IS, ABEND AND CUASE WHAT EVER   *
* FUNCTION (IE. BACKUP MIGRATION OR DELVOL) TO BE HELD.               *
*                                                                     *
*       WHO    |    DATE     |     PURPOSE                            *
*   -----------+-------------+------------------------------------    *
*     CROWLEY  |  14 JAN 87  | STARTED !!!                            *
*     CROWLEY  |  14 JAN 87  | TESTED                                 *
*     CROWLEY  |  15 JAN 87  | CHANGED ROUTE CODE DESCRIPTOR          *
*     SINGER   |  21 APR 88  | CHANGED FMID FOR DFHSM 2.3.1 (HHM2302) *
*                                                                     *
***********************************************************************
ARCTDEXT CSECT
*
*    SETUP SAVE AREA AND SAVE CALLERS REGS AND GETMAIN STORAGE
*
         SAVE  (14,12),,*
         LR    R12,R15
         LR    R10,R1
         USING ARCTDEXT,R12
         GETMAIN R,LV=WORKSIZE,SP=0      GET SAVEAREA + WORKAREA
         LR    R11,R1
         USING SAVEAREA,R11
         ST    R13,SAVE+4
         ST    R11,8(,R13)
*
*     SETUP BASE REGISTERS
*
         LM    R3,R3,0(R10)          PICKUP PARAMETER ADDRESS VECTOR
*                                  R3 @ JFCB --->CHAR(176)
*
         MVC   VOLSER,118(R3)        MOVE THE VOLSER
         CLC   VOLSER,=C'21'         BEGIN WITH 21
         BE    DONE                  YES DONE
         CLC   VOLSER,=C'22'         BEGIN WITH 22
         BE    DONE                  YES DONE
         WTO   'WARNING, DFHSM HAS CALLED FOR A NON HSM TAPE'
         WTO   'FUNCTION BEING HELD'
         LR     R1,R11
         L      R13,SAVE+4
         FREEMAIN R,A=(1),LV=WORKSIZE,SP=0
         ABEND  122
DONE     EQU    *
         LR     R1,R11
         L      R13,SAVE+4
         FREEMAIN R,A=(1),LV=WORKSIZE,SP=0
         RETURN (14,12),T,RC=0
*
         ORG
*
         LTORG
*
SAVEAREA DSECT
SAVE     DS     18F
SAVESIZE EQU    *-SAVEAREA
VOLSER   DS     CL2
         ORG
WORKSIZE EQU    *-SAVEAREA
         EJECT
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10
R11      EQU    11
R12      EQU    12
R13      EQU    13
R14      EQU    14
R15      EQU    15
         END
