++ USERMOD(AHMW001) /* DFHSM MIGRATION VOLUME EXIT :

       THIS EXIT WILL EXAMINE THE CURRENT FRAGMENTATION OF A VOLUME
       AND WILL SUBMIT A DFDSS DEFRAG JOB IF NECESSARY.

       NOTE: THIS EXIT WILL GO TO SYS2.LINKLIB (DDDEF HSSLINK).
                                                                 */ .
++ VER(Z038) FMID(HHM2302) .
++ JCLIN .
//GEN11 JOB WC07N0323,'531-1  CM SINGER'
//SG22 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,RENT,AC=1',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLINK,P1=' ',
//    MOD=,P2=' ',CLASS=T
//SYSPUNCH DD DUMMY
//INCLUDE DD DISP=SHR,DSNAME=MVS3.INCLUDE
//SYSLIN DD *
 INCLUDE INCLUDE(ARCMVEXT)
 NAME ARCMVEXT(R)
/*
++ SRC(ARCMVEXT) DISTLIB(ASRCLIB) .
ARCMVEXT TITLE 'DFHSM ARCMVEXT - DFHSM MIGRATION VOLUME EXIT'
***********************************************************************
*                                                                     *
*    NAME: DFHSM EXIT ARCMVEXT                                        *
*                                                                     *
*          SETSYS EXITS(YNNNNNNNN)                                    *
*                                        **** ****                    *
*   SPECIAL OPTIONS FOR LINKAGE EDITOR:  RENT,AC=1,NCAL,LIST,MAP,LET  *
*                                        **** ****                    *
*                                                                     *
*   PURPOSE:  TO EXAMINE THE CURRENT FRAGMENTATION OF A VOLUME TO     *
* SEE IF IT NEEDS TO BE DE-FRAGED BY A DF/DSS JOB.                    *
*                                                                     *
*                                                                     *
*       WHO    |    DATE     |     PURPOSE                            *
*   -----------+-------------+------------------------------------    *
*     CROWLEY  |  10 FEB 88  | STARTED !!!                            *
*     CROWLEY  |  12 FEB 88  | TESTED                                 *
*     CROWLEY  |  06 APR 88  | ADDED TIME CHECK AND VOL CHECK         *
*     CROWLEY  |  14 APR 88  | REDID VOLUME CHECK LOGIC TO ALL MPR VOL*
*     CROWLEY  |  27 JUL 88  | CHANGED TO ONLY START THE JOB ON SY3   *
*     CROWLEY  |  11 JAN 89  | CHANGED TO DEFRAG SHR80 VOLS           *
*                                                                     *
***********************************************************************
*        SAVE REGS AND GETMAIN MY STORAGE
ARCMVEXT CSECT
         SAVE    (14,12),,*
         LR      R12,R15
*                         LOAD REG 10 WITH PARMETER LIST
         LR      R10,R1
         USING   ARCMVEXT,R12
         GETMAIN R,LV=WORKSIZE,SP=0
         LR      R11,R1
         USING   SAVEAREA,R11
         ST      R13,SAVE+4
         ST      R11,8(,R13)
         LR      R13,R11
         LM      R3,R5,0(R10)    PICKUP PARAMETER LIST
*                                R3 @ VOLSER ---=> CHAR(6)
*                                R4 @ DEVTYPE --=> FIXED(31)
*                                R5 @ FRAG. IN -=> FIXED(31) 1-1000
        L       R5,0(R5)
        C       R5,=F'250'       IS FRAGMENTATION OVER .250
        BL      EXIT             NO, EXIT.
        MVC     PARM,SKELETON    YES, BUILD START COMMAND
        MVC     VOLSER,0(R3)
        CLC     =C'TSO',VOLSER   IS IT A TSO VOLUME
        BE      TIME             YES, GOOD VOLUMES GO CHECK TIME
        CLC     =C'SHR80',VOLSER IS IT A SHR80 VOLUME
        BE      TIME             YES, GOOD VOLUMES GO CHECK TIME
        C       R5,=F'450'       IS FRAGMENTATION OVER .450
        BL      EXIT             NO, EXIT.
        CLC     =C'MPR',VOLSER   IS IT A MPR VOLUME
        BE      TIME             YES, GOOD VOLUMES GO CHECK TIME
        B       EXIT             NOT A KNOWN VOLUME
TIME    EQU     *
        TIME    DEC,ERRET=EXIT
        CLM     R0,B'1100',=X'1800'   IS IT AFTER SIX PM
        BH      START               YES START JOB
        CLM     R0,B'1100',=X'0559'   IS IT BEFORE SIX AM
        BL      START               YES START JOB
        B       EXIT                NOT THE RIGHT TIME
START   EQU     *
        MODESET KEY=ZERO,MODE=SUP  GOTO SUPERVISOR STATE
        SLR     R0,R0
        LA      R1,PARM
        SVC     34                ISSUE THE START
        MODESET KEY=NZERO,MODE=PROB
EXIT    EQU     *
        LR      R1,R11
        L       R13,SAVE+4
        FREEMAIN R,A=(1),LV=WORKSIZE,SP=0      FREE THE STORAGE
        RETURN (14,12),T,RC=0                  AND RETURN
SKELETON DC     CL80' '
        ORG    SKELETON
LENGTH  DC      H'50'
        DC      H'0'
COMMAND DC      C'S JESCMD,PARM=''*T SY3,S HSMDFDSS,V='
VOLUME  DC      C'XXXXXX'
TICK    DC      X'7D'
        ORG
        LTORG
SAVEAREA DSECT
SAVE     DS    18F
SAVESIZE EQU   *-SAVEAREA
PARM     DS    CL80
         ORG   PARM
PARMLEN  DS    H
         DS    H
CMDTEXT  DS    CL(L'COMMAND)
VOLSER   DS    CL(L'VOLUME)
QUOTE    DS    CL(L'TICK)      CLOSING QUOTE MARK
         ORG
WORKSIZE EQU   *-SAVEAREA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
A        EQU   10
B        EQU   11
C        EQU   12
D        EQU   13
E        EQU   14
F        EQU   15
FF       EQU   255
         END
