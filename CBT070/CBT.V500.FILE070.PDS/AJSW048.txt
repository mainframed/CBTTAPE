++ USERMOD(AJSW048) /* JESOPER PROGRAM:

       THIS IS A PROGRAM THAT WILL SIMULATE A JES3 CONSOLE.
                                                                 */ .
++ VER(Z038) FMID(HJS2215).
++ JCLIN .
//GEN11 JOB Y51024508,'531-0  SINGER '
//SG22 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,AC=1',
//    UNIT='3350',SER=IPLXXX,N=GEN1,NAME=JES3LIB,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//INCLUDE DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=MVS3.INCLUDE
//SYSLIN DD *
 INCLUDE INCLUDE(JESOPER)
 NAME JESOPER(R)
/*
++ SRC(JESOPER) DISTLIB(ASRCLIB) .
*  PRINT NOGEN
***********************************************************************
*                                                                     *
*        JESOPER : SIMULATE 3770 RJP TERMINAL TO JES.                 *
*                                                                     *
*                                                                     *
*        REQUIRES :                                                   *
*                                                                     *
*        1. APPL DEFINITIONS TO VTAM LIKE:                            *
*                                                                     *
*        WCVTAM1  APPL  AUTH=(ACQ,PASS),BUFFACT=2,EAS=3               *
*              |                                                      *
*        WCVTAMN  APPL  AUTH=(ACQ,PASS),BUFFACT=2,EAS=3               *
*                                                                     *
*        2. WORK STATION DEFINITIONS IN JES(3) LIKE:                  *
*                                                                     *
*        RJPWS,N=VTAM1,RD=0,PR=0,PU=0,G=VTAM1,CONPACT=NO,LU=(WCVTAM1) *
*                    |                      |                      |  *
*        RJPWS,N=VTAMN,RD=0,PR=0,PU=0,G=VTAMN,CONPACT=NO,LU=(WCVTAMN) *
*                                                                     *
*        3. CONSOLE DEFINITIONS IN JES(3) LIKE:                       *
*                                                                     *
*        CONSOLE,JNAME=VTAM1,TYPE=RJP,DEST=ALL,LEVEL=15,DEPTH=255     *
*                          |                                          *
*        CONSOLE,JNAME=VTAMN,TYPE=RJP,DEST=ALL,LEVEL=15,DEPTH=255     *
*                                                                     *
*                                                                     *
*        NOTE : JES(3) APPLID MUST BE 'WCJES3' OR BE CHANGED          *
*               WHERE IT OCCURS IN THIS PROGRAM.                      *
*                                                                     *
***********************************************************************
JESOPER  CSECT
* * * * * * * * * * * *
*                     *
*  REGISTER EQUATES   *
*                     *
* * * * * * * * * * * *
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
* * * * * * * * * *
*                 *
*     LINKAGE     *
*                 *
* * * * * * * * * *
         SAVE  (14,12)
         LR    R10,R15
         LA    R11,2048(R10)
         LA    R11,2048(R11)
         USING JESOPER,R10,R11
         ST    R13,SAV+4
         LA    R14,SAV
         ST    R14,8(R13)
         LR    R13,R14
         STM   R10,R11,SAVEBASE
         ST    R1,CPPLPTR               SAVE
         L     R2,16                    CVT
         TM    X'74'(R2),X'80'          IS THIS MVS/XA ?
         BZ    AMODE31                  NO: SKIP SETTING TO 31
         L     R15,=A(X'80000000'+AMODE31) SET AMODE
         DC    X'0B0F'                  BSM R0,R15
AMODE31  L     R4,0(R2)                 TCB WORDS
         L     R4,12(R4)                CURRENT ASCB
         L     R8,172(R4)               POINTER TO JOBNAME
         L     R4,X'90'(R4)             OUCB POINTER
         L     R4,X'10'(R4)             OUCB FLAG BYTE
         ST    R4,OUCBFLG               SAVE IT
         TM    X'74'(R2),X'80'          IS THIS MVS/XA ?
         BZ    MVS                      NO: SKIP SETTING BACK TO 24
         LA    R15,MVS                  SET AMODE
         DC    X'0B0F'                  BSM R0,R15
MVS      TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BO    MVSLOOP                  YES - DONT OPEN
         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT))
         B     *+8                      SKIP OVER
MVSLOOP  L     R8,8(,R1)                A(PSCB)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        TM    16(R8),X'80'             OPER AUTH?                    *
*        BZ    RETURN                   NONFUNCTIONAL IF NOT          *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*    CHECK ACF2 JESOPER CAPABILITY                                    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         ACFINCVT R3,NONE=RETURN        FIND ACF2 CVT
         USING ACCVT,R3                 ADDRESS IT
         ACFGUCB R4,NONE=RETURN,        GET ACF2 ACUCB                 X
               INLINE=YES,                                             X
               SYS=AOS2
         USING ACUCB,R4                 ADDRESS IT
         DROP  R3
         L     R3,ACULRECP              POINT AT LOGONID RECORD
         USING LIDREC,R3                ADDRESS IT
         TM    LIDI2FLG,LIDI2F3         IS JESOPER BIT ON?
         BO    CONTINUE                 ..YES, KEEP GOING
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BZ    BATOUT1                  NO - USE DCBS
         TPUT  ERRMSG,50                GIVE ERROR MESSAGE
         B     RETURN                   ALL DONE
BATOUT1  CLC   LIDLID(3),=C'WC0'        IS IT PRODUCTION?
         BE    CONTINUE                 ..YES, KEEP GOING
         MVC   LINE+00(4),=X'00360000'  LENGTH
         MVC   LINE+04(50),ERRMSG       TEXT
         PUT   SYSPRINT,LINE            PRINT IT
         B     RETURN                   ALL DONE
         DROP  R3,R4
*
CONTINUE DS    0H
         MVC   WTOL+60(8),0(R8)         SAVE USERID
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BZ    BATREAD                  NO - USE DCBS
         L     R2,CPPLPTR               CPPL
         L     R3,4(R2)                 UPT
         L     R4,12(R2)                ECT
        PUTGET PARM=PUTGET,UPT=(3),ECT=(4),MF=(E,IOPL)
         L     R3,PUTGET+12
         LH    R8,0(R3)
         S     R8,=F'4'
         STC   R8,PASSWORD
         BCTR  R8,R0
         LTR   R8,R8
         BM    RETURN
         EX    R8,MOVPASS
         B     ACBLOOP
BATREAD  MVC   LINE+00(36),MSG2         MOVE IN MESSAGE
         PUT   SYSPRINT,LINE            PRINT IT
         GET   SYSIN,GBUF
         MVC   LINE+00(4),=X'004C0000'  72 BYTES
         MVC   LINE+04(72),GBUF         MOVE IN TEXT
         MVC   LINE+04(10),=CL10'SUPPRESSED'
         PUT   SYSPRINT,LINE            PRINT IT
         LA    R3,0                     INIT COUNT
         LA    R8,GBUF                  INIT POINTER
CHRLOOP  CLI   0(R8),C' '               BLANK ?
         BE    CHRDONE                  YES - GOT COUNT
         LA    R3,1(R3)                 ADD ONE
         LA    R8,1(R8)                 ADD ONE
         B     CHRLOOP                  AND TRY AGAIN
CHRDONE  STC   R3,PASSWORD              SAVE IT
         MVC   PASSWORD+1(8),GBUF       MOVE IN TEXT
* * * * * * * * * * * * * * * * *
*                               *
*   FIND AN AVAILABLE ACB       *
*                               *
* * * * * * * * * * * * * * * * *
ACBLOOP  EQU   *
         OPEN  VTAMACB                  TRY VTAMN
         LTR   R15,R15                  OK
         BZ    SETLOG                   YES
         SR    R3,R3
         IC    R3,APPLID+7              SEVENTH CHAR OF APPLID
         LA    R3,1(,R3)                INCR EBCDIC CHAR (RANGE 1-5)
         STC   R3,APPLID+7              SEVENTH CHAR OF APPLID
         CH    R3,=XL2'00FA'            TOO FAR
         BL    ACBLOOP                  TRY NEXT ACB
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BZ    BATOUT2                  NO - USE DCBS
         TPUT  MSG1,40                  WRITE MESSAGE
         B     RETURN                   ALL DONE
BATOUT2  MVC   LINE+00(4),=X'002C0000'  LENGTH
         MVC   LINE+04(40),MSG1         TEXT
         PUT   SYSPRINT,LINE            PRINT IT
         B     RETURN                   ALL DONE
         EJECT
* * * * * * * * * * * * * * * * *
*                               *
*          SETLOGON             *
*                               *
* * * * * * * * * * * * * * * * *
SETLOG   EQU   *
         SETLOGON RPL=INITRPL,ACB=VTAMACB,OPTCD=(START,SYN),           X
               ECB=INTERNAL
         LTR   R15,R15
         BNZ   RETURN                   FORGET IT IF NOT PERFECT
* * * * * * * * * * * * * * * * *
*                               *
*  STAX TO ALLOW PA1 BREAKOUT   *
*                               *
* * * * * * * * * * * * * * * * *
         STAX  ATTNEX
* * * * * * * * * * * * * * * * *
*                               *
*            REQSESS            *
*                               *
* * * * * * * * * * * * * * * * *
REQS1    EQU   *
    REQSESS RPL=RPLREQS,AAREA=0,OPTCD=(SYN,NQ),RECLEN=5,AREA=APPLID+3
         LTR   R15,R15
         BNZ   RETURN                   N.G. IF NOT PERFECT
* * * * * * * * * * * * * * * * *
*                               *
*  ISSUE THE (FIRST) RCV ANY    *
*                               *
* * * * * * * * * * * * * * * * *
         BAL   R12,RCVA                 SET UP THE RCV ANY
* * * * * * * * * * * * * * * * *
*                               *
*  WAIT FOR END OF MESSAGE (EB) *
*                               *
* * * * * * * * * * * * * * * * *
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BO    NOSTIME                  YES - DONT PUT UP STIMER
        STIMER REAL,ATTNEX,BINTVL=AWHILE WAIT FOR AWHILE
NOSTIME  WAIT  ECB=MAINECB
         CLI   MAINECB+3,X'80'          TERMINATE ?
         BE    RETURN                   YES
         CLI   MAINECB+3,X'40'          ATTN/TIMER ?
         BNE   RETURN                   NO - THEN WHY ARE WE HERE
         XC    MAINECB,MAINECB          RESET ECB
         CLOSE VTAMACB
         B     ACBLOOP
* * * * * * * * * * * * * * *
*                           *
*    RETURN TO CALLER       *
*                           *
* * * * * * * * * * * * * * *
RETURN   EQU   *
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BO    RETURNX                  YES - DONT CLOSE
         CLOSE (SYSIN,,SYSPRINT)
RETURNX  L     R13,SAV+4
         RETURN (14,12),RC=0
* * * * * * * * * * * * * * *
*                           *
*  ATTN EXIT ROUTINE        *
*                           *
* * * * * * * * * * * * * * *
MAINECB  DC    F'0'
ATTNEX   EQU   *
         USING *,R15
         STM   R0,R15,SAV3
         LM    R10,R11,SAVEBASE
         DROP  R15
         USING JESOPER,R10,R11
         LA    R13,SAVEXT2
         POST  MAINECB,X'40'            SET ATTN/TIMER ECB
         LM    R0,R15,SAV3
         BR    R14
* * * * * * * * * * * * * * *
*                           *
*  ISSUE ASYNCH RECEIVE ANY *
*                           *
* * * * * * * * * * * * * * *
RCVA     EQU   *
         RECEIVE RPL=RPLRCV,AREA=RAREA,AREALEN=256,EXIT=RCVEXIT,       X
               RTYPE=(DFSYN,DFASY,RESP),OPTCD=(CA,ANY,TRUNC,ASY,Q)
         BR    R12
         EJECT
* * * * * * * * * * * * * * * *
*                             *
*  SESSION ESTABLISH EXIT     *
*                             *
* * * * * * * * * * * * * * * *
SCIPEXIT EQU   *
         USING *,R15
         STM   R0,R15,SAV2
         LM    R10,R11,SAVEBASE
         DROP  R15
         USING JESOPER,R10,R11
         LA    R13,SAVEXT
         LM    R3,R7,0(R1)              PICK UP PARMS
*              R3=ACB
*              R4=CID                   (UNLESS BIND)
*              R5=USERFLD               (UNLESS BIND)
*              R6=BIND PARM             (IF BIND)
*              R7=RPL
         SHOWCB RPL=(7),AM=VTAM,FIELDS=ARG,AREA=CID,LENGTH=4
         L     R2,CID
         MODCB  AM=VTAM,RPL=SESSRPL,ARG=(R2)
         MODCB  AM=VTAM,RPL=RPLSEND,ARG=(R2)
         MODCB  AM=VTAM,RPL=RPLRESP,ARG=(R2)
         TESTCB RPL=(7),AM=VTAM,CONTROL=(BIND)
         BE    BIND                     BIND COMMAND
         SHOWCB RPL=(7),AM=VTAM,FIELDS=SEQNO,AREA=SEQNO,LENGTH=4
         L     R2,SEQNO
         MODCB  AM=VTAM,RPL=SESSRPL,SEQNO=(R2)
         TESTCB RPL=(7),AM=VTAM,CONTROL=(SDT)
         BE    SDT                      SDT COMMAND
         TESTCB RPL=(7),AM=VTAM,CONTROL=(STSN)
         BE    STSN                     STSN COMMAND
SCIPRET  EQU   *
         LM    R0,R15,SAV2
         SR    R15,R15
         BR    R14
BIND     EQU   *
         OPNSEC RPL=SCIPRPL,ACB=(3),NIB=REQNIB
         LTR   R15,R15
         BNZ   REJ
         B     SCIPRET
REJ      EQU   *
         SESSIONC RPL=SESSRPL,CONTROL=BIND,RESPOND=(EX,FME),ACB=(3)
         B      SCIPRET
SDT      EQU   *
         SESSIONC RPL=SESSRPL,CONTROL=SDT,RESPOND=(NEX,FME),ACB=(3)
         B     SCIPRET
STSN     EQU   *
         SESSIONC RPL=SESSRPL,CONTROL=STSN,RESPOND=(NEX,FME),ACB=(R3)
         B     SCIPRET
         EJECT
* * * * * * * * * * * * * * * *
*                             *
*  RECEIVE VTAM RESPONSES     *
*                             *
* * * * * * * * * * * * * * * *
RCVEXIT  EQU   *
         USING *,R15
         STM   R0,R15,SAV2
         LM    R10,R11,SAVEBASE
         DROP  R15
         USING JESOPER,R10,R11
         LA    R13,SAVEXT
         CHECK RPL=RPLRCV
         MVC   RPLSEND+36(4),RPLRCV+36  MOVE CID TO SEND RPL
         MVC   RPLRESP+36(4),RPLRCV+36  MOVE CID TO SEND RPL
         MVC   RPLRESP+66(2),RPLRCV+66  MOVE SEQNO TO RESP RPL
         TESTCB AM=VTAM,RPL=RPLRCV,RTYPE=(RESP)
         BE    RERCV                    GO IF THIS IS A RESPONSE
         TESTCB AM=VTAM,RPL=RPLRCV,RESPOND=(NEX,FME)
         BNE   EXCONLY                  GO IF +FME NOT REQD
         MVC   RPLRESP+77(3),RPLRCV+77  SET TYPES IN RESP
         SEND  RPL=RPLRESP,STYPE=RESP,OPTCD=SYN
EXCONLY  EQU   *
         TESTCB RPL=RPLRCV,AM=VTAM,BRACKET=EB
         BE    TGET                     END OF THIS - GO TRY NEXT
TRYBB    EQU   *
         TESTCB RPL=RPLRCV,AM=VTAM,BRACKET=BB
         BE    RERCV                    SKIP THE DUMB BEGIN BRACKET
         TESTCB RPL=RPLRCV,AM=VTAM,CONTROL=DATA
         BNE   RERCV                    DON'T IF NO DATA TO DEBLOCK
         L     R3,RPLRCV+48  RPLRLEN
         LTR   R3,R3                    ANY DATA REALLY?
         BZ    RERCV                    DON'T LOOP IF NOT
         L     R4,OUTPTR                A(OUTPUT BUFFER)
         LA    R5,RAREA
RCV1     EQU   *
         LR    R6,R5                    SAVE A(INPUT AREA)
RCV2     EQU   *
         CLI   0(R5),X'15'              NL
         BE    RCV3                     GO IF YES
         LA    R5,1(,R5)                NEXT INPUT CHAR
         BCT   R3,RCV2                  BRANCH IF NOT THE LAST
* OUT OF INPUT RECORD WITHOUT A X'15'
         SR    R5,R6
         L     R9,LEN
         AR    R9,R5                    THIS PLUS PRIOR
         ST    R9,LEN                   CURRENT LENGTH
         BCTR  R9,0
         L     R1,OUTPTR
         EX    R9,MVCI
         AR    R1,R5                    NEW OUTPUT BUFF ADDR
         ST    R1,OUTPTR
         B     RERCV
RCV3     EQU   *
         LA    R7,1(,R5)                SAVE ADDR OF NEXT
         SR    R5,R6
         L     R9,LEN
         AR    R9,R5
         ST    R9,LEN                   LENGTH IN OUTBUF
         BCTR  R9,0
         L     R1,OUTPTR
         EX    R9,MVCI
         LA    R1,OUTBUF                A(BUFF) FOR TPUT
         ST    R1,OUTPTR                RESET OUTPUT PTR
         L     R0,LEN
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BZ    BATOUT3                  NO - USE DCBS
         SVC   93                       TPUT
         B     BATDUN3                  ALL DONE
BATMOVR  MVC   LINE+04(0),OUTBUF        TEXT
BATOUT3  XC    LINE+00(4),LINE+00       ZERO OUT
         L     R1,LEN                   GET LENGTH
         LTR   R1,R1                    GET LENGTH
         BZ    *+6                      ZERO?
         BCTR  R1,R0                    DECREMENT
         EX    R1,BATMOVR               MOVE IN TEXT
         LA    R1,5(R1)                 ADD FOUR FOR HEADER
         STC   R1,LINE+01               LENGTH
         PUT   SYSPRINT,LINE            PRINT IT
BATDUN3  L     R1,OUTPTR                RESET OUTPUT PTR
         CLC   9(8,R1),=C'IAT2810 '     IS THIS SESSION ESTABLISHED
         BNE   SKTPUT                   NO
         MVC   WTOL+4(50),9(R1)         MOVE MESSAGE
         WTO   MF=(E,WTOL)
SKTPUT   EQU   *
         LR    R5,R7                    NEW FIRST CHAR
         SR    R1,R1
         ST    R1,LEN                   CLEAR LENGTH
         BCT   R3,RCV1                  GO IF NOT AT END OF INPUT
RERCV    EQU   *
         BAL   R12,RCVA
RCVEX    EQU   *
         LM    R0,R15,SAV2
         BR    R14
MVCI     MVC   0(0,R1),0(R6)
POSTOP   EQU   *
         POST  MAINECB,X'80'            TERMINATE
         B     RCVEX
TGET     EQU   *  GET NEXT JES CMD FROM TERMINAL  *
         TM    OUCBFLG+2,X'20'          LOGON CREATED ?
         BZ    BATOUT4                  NO - USE DCBS
         LA    R0,7                     LENGTH OF COMMAND
         LA    R1,=CL7'JESOPER'
         SVC   93                       ISSUE TPUT
         TGET  GBUF,80                  80 BYTES MAX
         OC    GBUF,X40
         C     R1,=F'0'  ANYTHING THERE ?
         BH    GOTCMD    YES
SETNULL  LA    R1,3      SET NULL
         MVC   GBUF(3),=CL3'*E '        PUT IN DUMMY COMMAND
         B     GOTCMD                   ALL DONE
BATOUT4  GET   SYSIN,GBUF
         MVC   LINE+00(4),=X'004C0000'  72 BYTES
         MVC   LINE+04(72),GBUF         MOVE IN TEXT
         PUT   SYSPRINT,LINE            PRINT IT
         CLC   GBUF(16),=CL16' '        BLANKS
         BE    SETNULL
         LA    R1,0                     INIT COUNT
         LA    R8,GBUF                  INIT POINTER
CHRLUP2  CLC   0(8,R8),=CL8'        '   BLANK ?
         BE    GOTCMD                   YES - GOT COUNT
         LA    R1,1(R1)                 ADD ONE
         LA    R8,1(R8)                 ADD ONE
         B     CHRLUP2                  AND TRY AGAIN
GOTCMD   ST    R1,RPLSEND+48            SET SEND LEN
         CLC   GBUF(3),=C'END'          COMMAND END ?
         BE    POSTOP                   THEN END
        SEND  RPL=RPLSEND,AREA=GBUF,STYPE=REQ,CONTROL=DATA,            X
               RESPOND=(NEX,FME,NRRN),POST=RESP,BRACKET=(BB,EB),       X
               OPTCD=(SYN,CA),RTYPE=(DFSYN,DFASY,RESP)
         B     RERCV
*
OUTPTR   DC    A(OUTBUF)
LEN      DC    F'0'
SAV      DC    18F'0'
SAV2     DC    16F'0'
SAV3     DC    16F'0'
SAVEXT   DC    18F'0'
SAVEXT2  DC    18F'0'
SAVEBASE DC    2F'0'
CID      DC    F'0'
SEQNO    DC    F'0'
X40      DC    80X'40'                  MAKE UPPERCASE
GBUF     DC    XL80'00'                 BEST ON HALFWORD BOUNDARY
RAREA    DC    512X'00'
*
RPLSEND  RPL   AM=VTAM,ACB=VTAMACB
RPLRESP  RPL   AM=VTAM,ACB=VTAMACB
RPLREQS  RPL   AM=VTAM,ACB=VTAMACB,NIB=REQNIB
RPLRCV   RPL   AM=VTAM,ACB=VTAMACB
SCIPRPL  RPL   AM=VTAM,ACB=VTAMACB,OPTCD=SYN
SESSRPL  RPL   AM=VTAM,ACB=VTAMACB,OPTCD=SYN,STYPE=RESP
INITRPL  RPL   AM=VTAM
REQNIB   NIB   MODE=RECORD,LOGMODE=INTERACT,NAME=WCJES3
VTAMACB  ACB   AM=VTAM,APPLID=APPLID,PASSWD=PASSWORD,EXLST=XLST
XLST     EXLST AM=VTAM,SCIP=SCIPEXIT
APPLID   DC    X'07',C'WCVTAM1'
PASSWORD DC    X'00',CL80' '
MOVPASS  MVC   PASSWORD+1(0),4(R3)
MSG1     DC    CL40'UNABLE TO ESTABLISH SESSION WITH JES3   '
ERRMSG   DC    CL50'*** NOT AUTHORIZED TO USE THE JESOPER COMMAND ***'
PUTGET  PUTGET OUTPUT=(OLD,,PTBYPS),MF=L
IOPL     DC    4F'0'
OLD      DC    F'1'
         DC    AL4(MSG2)
MSG2     DC    H'36',H'0',CL32'JESOPER: PLEASE ENTER PASSWORD -'
WTOL     WTO   'IAT2810 SESSION ESTABLISHED, WS= VTAM1 LU= WCVTAM1 UID=+
                USERID  ',ROUTCDE=(2),DESC=7,MF=L
CPPLPTR  DS    F
OUCBFLG  DS    F
AWHILE   DC    F'06000'            WAIT TIME (60 SEC)
SYSIN    DCB   DDNAME=SYSIN,MACRF=GM,DSORG=PS,EODAD=POSTOP             X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,                      X
               RECFM=VB,LRECL=255,BLKSIZE=6160
         LTORG
LINE     DC    CL256' '
OUTBUF   DC    512X'00'
*
         EJECT
         LIDREC
         ACUCB
         ACFASVT
         ACCVT
         IHAPSA
         IHAASCB
         END
