++ USERMOD(AJSW012) /*

       THIS MODIFICATION WILL:
         - CANCEL ANY OUTPUT THAT HAS INVALID DEST, FORM OR FCB
           (SEE CODE FOR DETAILS).
         - RECOMPUTE THE OSE PRIORITY USING THE DATASET LINE COUNT.
         - CHANGE CLASS OF CERTAIN STARTED TASKS JESDS TO 'P'
           AND THEIR DESTINATION TO 'PANSPOOL
         - ISSUE A VPS PRINTER START COMMAND FOR THE SPECIFIED
           PRINTER WHEN VPS OUTPUT IS DETECTED.
         - ACCEPT A TSO USERID AS A VALID "DEST" WHEN A FILE IS
           RECEIVED VIA NJE AND THE TSO SEND COMMAND.
           SEE ALSO AJSW051.


 CHANGE HISTORY:

 05/22/88 J. DAILEY             ADD FCS TO STC PANSPOOL TABLE
 06/05/88 J. DAILEY             ADD IDMS TO PANSPOOL TABLE
 06/26/88 J. DAILEY             ADD DBTPMSTR DBTPDBM1 TO PANSPOOL
 06/26/88 J. DAILEY             ADD DBIPMSTR DBIPDBM1 TO PANSPOOL
 07/03/88 J. DAILEY             ALLOW TSO USERID AS A VALID DEST
                                FOR FILES SENT VIA NJE
 07/17/88 J. DAILEY             CORRECTIONS PANSPOOL TABLE
                                ACCEPT PRXX (XX=NUMERIC) AS A VALID
                                FORM SPECIFICATION
 08/07/88 J. DAILEY             ACCEPT CLASS I AS VPS ONLY CLASS
 11/13/88 J. DAILEY             1) ADD DBS5MSTR,DBS5DBM1,DBCTMSTR,
                                AND DBCTDBM1 TO PANSPOOL ROUTING
                                TABLE.
 11/20/88 J. DAILEY             ADD TASK BEGINNING WITH "IDMT" TO THE
                                PANSPOOL ROUTING TABLE.

                                                                 */ .
++ VER(Z038) FMID(HJS2215).
++ SRCUPD(IATUX19) DISTLIB(AJES3SRC) .
./ CHANGE NAME=IATUX19
*        USER MODS =                                            AJSW012
*                                                               AJSW012
*             - CANCEL ANY OUTPUT HAVING INVALID DEST, FORM,    AJSW012
*               OR FCB                                          AJSW012
*                                                               AJSW012
*             - RECOMPUTE THE OSE PRIORITY USING DS LINECOUNT   AJSW012
*                                                               AJSW012
*             - CHANGE CLASS OF CERTAIN STARTED TASKS TO 'P'    AJSW012
*                                                               AJSW012
*             - FOR CLASS V SYSOUT ENSURE THE DESTINATION IS    AJSW012
*               A VALID VPS PRINTER ID.                         AJSW012
*                                                               AJSW012
*             - FORCE ANY DEST OF 'LOCAL' TO 'ANYLOCAL'         AJSW012
*                                                               AJSW012
*@ CONTROL BLOCKS:                                              AJSW012
*@             LIST MAPPING MACROS FOLLOWED BY ATTRIBUTE.       AJSW012
*@             R/O - READ/ONLY  R/W - READ/WRITE                AJSW012
*@             IATYOSE - R/O                                    AJSW012
*@             IATYRSQ - R/O                                    AJSW012
*@             IATYMPC - R/O                                    AJSW012
*@             IATYJQE - R/O                                    AJSW012
*@             IATYDST - R/O                                    AJSW012
*@             IATYJDA - R/O                                    AJSW012
*@ CHANGE ACTIVITY                                              AJSW012
*@             INITIAL IMPLEMENTATION 4/1/87                    AJSW012
*@             IGNORE CHECKING FOR A DEST OF WCMRO?? 1/24/88JBD-AJSW012
*@             REFIT USERMOD TO OBTAIN SEQ NO SPACE  3/24/88JBD-AJSW012
         IATYDST                                                AJSW012
         IATYMPC                                                AJSW012
         IATYSUP  TYPE=DSECT                                    AJSW012
         EJECT                                                  AJSW012
         IATYRLT  TYPE=DSECT                                    AJSW012
         EJECT                                                  AJSW012
         IATYRSQ ,                                              AJSW012
         EJECT                                                  AJSW012
         IATYJQE ,                                              AJSW012
         EJECT                                                  AJSW012
         IATYMCL ,                                              AJSW012
         EJECT                                                  AJSW012
         IATYJDS ,                                              AJSW012
         EJECT                                                  AJSW012
         IATYOSS ,                                              AJSW012
         EJECT                                                  AJSW012
WDPRTMAP DSECT                                                  AJSW012
WDPRTNAM DC    CL8' '              TRUE PRINTER NAME            AJSW012
WDPRTDST DC    CL8' '              PRINTER DESTINATION NAME     AJSW012
WDPRTLNG EQU   *-WDPRTMAP          LENGTH OF AN NETRY           AJSW012
WDPRTNXT DC    0XL8'00'            NEXT TABLE ENTRY             AJSW012
WDPLUTBL DSECT                                                  AJSW012
WDPLUNAM DC    CL8' '              TRUE PRINTER NAME            AJSW012
WDPLUDST DC    CL8' '              PRINTER DESTINATION NAME     AJSW012
WDPLUNXT DC    0CL8' '             NEXT ENTRY IN TABLE          AJSW012
WDPRTYPX DSECT                                                  AJSW012
WDPRTYPL DC    XL1'00'             LENGTH-1 OF FOLLOWING ENTRY  AJSW012
WDPRTYPF DC    CL1'RSCS'           FIXED PRTY JOBNAME PREFIX    AJSW012
WDPRTYNX DC    0CL1'00'            NEXT TABLE ENTRY             AJSW012
WDPRTYID DSECT                                                  AJSW012
WDPRTYNM DC    CL7'IDMSPROD'       FIXED PRTY ELIGIBLE UID      AJSW012
WDPRTYFX DC    AL1(250)            PRTY FOR THIS USER           AJSW012
WDNXTUID DC    0CL8' '             NEXT ENTRY IN UID TABLE      AJSW012
         B     WDCUX19             BRANCH TO USERMOD CODE       AJSW012
*                                                               AJSW012
WDCUX19  DS    0H                  START OF USER MOD CODE       AJSW012
         LR    R10,R15             SET UP BASE REG              AJSW012
         USING IATUX19,R10                                      AJSW012
         LR    R2,R1               SAVE OSE ADDRESS             AJSW012
         USING OSEENTRY,R2                                      AJSW012
         LR    R3,R9               GET RQ ADDR (IATOSDR)        AJSW012
         USING RQSTART,R3                                       AJSW012
*                                                               AJSW012
         ST    R2,OSE@             SAVE -> THE OSE              AJSW012
         ST    R3,RESQ@            SAVE -> RESQ                 AJSW012
         ST    R8,JDS@             SAVE -> JDS                  AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        DETERMINE IF JOB NAME AND NUMBER HAS CHANGED         * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
         CLC   SVJOBNAM,RQJOBNAM   CHANGE IN JOB NAME           AJSW012
         BC    NE,RSCTL            YES,RESET CONTROLS           AJSW012
         CLC   SVJNUM,RQJNUM       NO,BUT DIFFERENT JOB NUMBER  AJSW012
         BC    EQ,SAMEJOB          NO,JUST DATA SET CHANGE      AJSW012
*                                                               AJSW012
RSCTL    DS    0H                                               AJSW012
         MVC   SVJOBNAM,RQJOBNAM   SAVE JOB NAME                AJSW012
         MVC   SVJNUM,RQJNUM       SAVE JOB NUMBER              AJSW012
         MVI   EFLAGS,0            RE-SET ERROR FLAGS           AJSW012
         MVI   MFLAGS,0            RE-SET MISC FLAGS            AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        CHANGE CLASS TO P AND DEST TO PANSPOOL FOR SOME STCS * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
SAMEJOB  DS    0H                                               AJSW012
         TM    RQFLG5,RQDSEL       IS THIS A DEMAND SELECT JOB? AJSW012
         BC    NALLON,GDW0001      NO, SKIP NAME CHECK          AJSW012
         LA    R1,WDPFX8           -> LIST OF 8-BYTE ENTRIES    AJSW012
         LA    R15,7(,0)           LENGTH-1 OF ENTRIES          AJSW012
         BAL   R14,WDPFXCHK        CHECK JOBNAME V.LIST         AJSW012
         LA    R1,WDPFX7           -> LIST OF 7-BYTE PREFIXES   AJSW012
         LA    R15,6(,0)           LENGTH-1 OF ENTRIES          AJSW012
         BAL   R14,WDPFXCHK        CHECK JOBNAME V. LIST        AJSW012
         LA    R1,WDPFX6           -> LIST OF 6-BYTE PREFIXES   AJSW012
         LA    R15,5(,0)           LENGTH-1 OF ENTRIES          AJSW012
         BAL   R14,WDPFXCHK        CHECK JOBNAME V. LIST        AJSW012
         LA    R1,WDPFX5           -> LIST OF 5-BYTE PREFIXES   AJSW012
         LA    R15,4(,0)           LENGTH-1 OF ENTRIES          AJSW012
         BAL   R14,WDPFXCHK        CHECK JOBNAME V. LIST        AJSW012
         LA    R1,WDPFX4           -> LIST OF 4-BYTE PREFIXES   AJSW012
         LA    R15,3(,0)           LENGTH-1 OF ENTRIES          AJSW012
         BAL   R14,WDPFXCHK        CHECK JOBNAME V. LIST        AJSW012
         LA    R1,WDPFX3           -> LIST OF 3-BYTE PREFIXES   AJSW012
         LA    R15,2(,0)           LENGTH-1 OF ENTRIES          AJSW012
         LA    R14,GDW0001         SKIP CHANGE IF NOT FOUND     AJSW012
*                                                               AJSW012
WDPFXCHK DC    0H'0'                                            AJSW012
         EX    R15,WDCHKPFX        JOBNAME OF INTEREST?         AJSW012
         BC    EQ,CHKDDNM          YES, GO CHECK DDNAME         AJSW012
         LA    R1,1(R15,R1)        NO, STEP TO NEXT ENTRY       AJSW012
         CLI   0(R1),FF            END OF LIST?                 AJSW012
         BC    NE,WDPFXCHK         NO, TRY NEXT ENTRY           AJSW012
         BR    R14                 YES, RETURN                  AJSW012
*                                                               AJSW012
WDCHKPFX CLC   0(,R1),RQJOBNAM     PREFIX LIST ENTRY V. JOBNAME AJSW012
*                                                               AJSW012
CHKDDNM  DS    0H                                               AJSW012
         L     R1,JDS@             GET -> JDS                   AJSW012
         USING JDSENTRY,R1         TEMP JDS DS ENTRY ADDRBLTY   AJSW012
         CLC   JDSDDNAM,=CL8'JESMSG' DATA SET OF INTEREST?      AJSW012
         BC    EQ,CHGCLASS         YES, CHANGE CLASS OF D.S.    AJSW012
         CLC   JDSDDNAM,=CL8'JESJCL' DATA SET OF INTEREST?      AJSW012
         BC    EQ,CHGCLASS         YES, CHANGE CLASS OF D.S.    AJSW012
         CLC   JDSDDNAM,=CL8'SYSMSG' DATA SET OF INTEREST?      AJSW012
         BC    NE,GDW0001          NO, SKIP CLASS CHANGE        AJSW012
         DROP  R1                  TEMP JDS DS ENTRY ADDRBLTY   AJSW012
*                                                               AJSW012
CHGCLASS DS    0H                                               AJSW012
         MVI   OSECLASS,C'P'       SET PANSPOOL OUTPUT CLASS    AJSW012
         MVI   OSECOPYT,X'01'      SET COPY COUNT TO 1          AJSW012
         MVC   OSEDEST,=CL8'PANSPOOL' SET PANSPOOL AS DEST      AJSW012
         NI    OSEDFLG2,FF-OSEDHOLD ENSURE D/S NOT HELD         AJSW012
         B     GDW9000             DO NO MORE CHECKING          AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        ANY CLASS=G WILL BE PURGED REGARDLESS OF COPY NUMBER * AJSW012
*-------------------------------------------------------------* AJSW012
GDW0001  DS    0H                                               AJSW012
         CLC   OSETYPE,=CL8'INTRDR' *IS IT INTRDR?              AJSW012
         BC    EQ,GDW9000          YES - SKIP ALL CHECKS        AJSW012
         CLI   OSECLASS,C'G'       IS IT GARBAGE CLASS?         AJSW012
         BC    EQ,KILLOSE9         YES - GO KILL IT.            AJSW012
         EJECT                                                  AJSW012
*-------------------------------------------------------------* AJSW012
*        VALIDATE OSE DESTINATION                             * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        CHECK THE NJE NODE TABLE FOR A MATCH WITH THE        * AJSW012
*        SPECIFIED DESTINATION.  SKIP ALL CHECKS FOR AN OSE   * AJSW012
*        W/DEST=NJENODE.  THEN CHECK FOR DEST MATCH WITH      * AJSW012
*        EITHER A SUPUNIT GROUP, OR SUPUNIT DD (J) NAME       * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0100  DC    0H'0'                                            AJSW012
         CLC   =C'WCMRO',OSEDEST   DEST=VM SPECIAL DEST         AJSW012
         BC    EQ,GDW9000          YES,SKIP CHECKS              AJSW012
         CLC   =C'SAR',OSEDEST     SAR OUTPUT                   AJSW012
         BC    EQ,GDW9000          YES,SKIP CHECKS              AJSW012
*        CLC   =C'EXP',OSEDEST     EXPRESS DELIVERY             AJSW012
*        BC    EQ,ESI              YES,SKIP CHECKS              AJSW012
         CLC   =C'LOCAL ',OSEDEST  ORIGIN OF LOCAL              AJSW012
         BC    NE,GDW0105          NO,CONTINUE DEST CHECK       AJSW012
         MVC   OSEDEST,=C'ANYLOCAL' YES,OVERRIDE TO 'ANYLOCAL'  AJSW012
         B     GDW1000             DO LOCAL CHECKS NOW          AJSW012
*                                                               AJSW012
GDW0105  DS    0H                                               AJSW012
         LA    R1,OSEDEST          -> DESTINATION FOR DATASET   AJSW012
         L     R15,ANJESRCH        -> NJE NODE NAME SEARCH RTN  AJSW012
         ACALL ENTER=(15)          SEARCH NJE TABLE FOR DEST    AJSW012
         B     GDW0110             DEST NOT NJE NODE, CHECK     AJSW012
         B     GDW9000             DEST=NJE NODE, SKIP CHECKS   AJSW012
*                                                               AJSW012
GDW0110  DC    0H'0'                                            AJSW012
         LA    R9,SUPUNITS-(SUPCHAIN-SUPSTART) DUMMY UP POINTER AJSW012
         USING SUPSTART,R9         SUPUNIT ENTRY ADDRESSABILITY AJSW012
*                                                               AJSW012
GDW0120  DC    0H'0'                                            AJSW012
         ICM   R9,B'1111',SUPCHAIN -> NEXT SUPUNIT ENTRY        AJSW012
         BC    ZERO,GDW0200        END OF SUPUNITS, DO RJPTABLE AJSW012
         CLC   SUPTGEN,=C'PRT'     GENERAL TYPE = PRT?          AJSW012
         BC    EQ,GDW0130          YES, CHECK DEST W/SUPUNIT    AJSW012
         CLC   SUPTGEN,=C'PUN'     GENERAL TYPE = PUNCH?        AJSW012
         BC    NE,GDW0120          NO, DON'T USE THIS ENTRY     AJSW012
*                                                               AJSW012
GDW0130  DC    0H'0'                                            AJSW012
         CLC   OSEDEST,SUPGROUP    DEST:THIS UNIT'S DGROUP      AJSW012
         BC    EQ,GDW1000          MATCH, DO LOCAL CHECKS       AJSW012
         CLC   OSEDEST,SUPDD       DEST:THIS UNIT'S JNAME       AJSW012
         BC    EQ,GDW1000          MATCH, DO LOCAL CHECKS       AJSW012
         B     GDW0120             LOOP FOR NEXT SUPUNIT ENTRY  AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*       ARE FIRST FIVE CHARS OF DEST IN RJP TERM TABLE?       * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0200  L     R9,RJPRTERM         POINT TO RJP TERM TABLE      AJSW012
         LA    R9,0(0,R9)          CLR HI BYTE                  AJSW012
         LTR   R9,R9               ANY TABLE THERE?             AJSW012
         BC    ZERO,GDW0300        YES, GO CHECK OTHERS         AJSW012
         USING RTTSTART,R9         ESTABLISH ADDRESSABILITY     AJSW012
*                                                               AJSW012
GDW0250  DC    0H'0'                                            AJSW012
         CLC   OSEDEST(5),RTTGNAME GROUP MATCH?                 AJSW012
         BC    EQ,GDW1000          VALID RJP DEST CARRY ON      AJSW012
*                                                               AJSW012
GDW0255  DC    0H'0'                                            AJSW012
         CLC   OSEDEST(5),RTTNAME  NAME MATCH?                  AJSW012
         BC    EQ,GDW1000          VALID RJP DEST, CARRY ON     AJSW012
         LA    R9,RTTEND           ELSE BUMP TO NEXT            AJSW012
         CLI   0(R9),X'FF'         END OF TABLE?                AJSW012
         BC    NE,GDW0250          NO - CONTINUE SEARCH         AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*       ARE FIRST FIVE CHARS OF DEST IN SNA TERM TABLE?       * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0300  DS    0H                                               AJSW012
         L     R9,SRJPRTRM         POINT TO SNA TERM TABLE      AJSW012
         LA    R9,0(0,R9)          CLR HI BYTE/BIT              AJSW012
         LTR   R9,R9               ANY TABLE THERE?             AJSW012
         BC    ZERO,GDW0400        YES, GO CHECK OTHERS         AJSW012
*                                                               AJSW012
GDW0350  DC    0H'0'                                            AJSW012
         CLC   OSEDEST(5),RTTGNAME GROUP MATCH?                 AJSW012
         BC    EQ,GDW1000          VALID SNARJP DEST, CARRY ON  AJSW012
*                                                               AJSW012
GDW0355  DC    0H'0'                                            AJSW012
         CLC   OSEDEST(5),RTTNAME  NAME MATCH?                  AJSW012
         BC    EQ,GDW1000          YES, VALID SNARJP DEST       AJSW012
         LA    R9,RTTEND           ELSE BUMP TO NEXT            AJSW012
         CLI   0(R9),X'FF'         END OF TABLE?                AJSW012
         BC    NE,GDW0350          NO - CONTINUE SEARCH         AJSW012
         DROP  R9                  DROP ADDRESSABILITY          AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        CHECK FOR PANSPOOL                                   * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0400  DS    0H                                               AJSW012
         CLC   OSEDEST,=CL8'PANSPOOL'  IS IT PANSPOOL?          AJSW012
         BC    EQ,GDW1000              YES - GOOD DEST          AJSW012
         CLC   OSEDEST,=CL8'PANSPTST'  IS IT PANTEST?           AJSW012
         BC    EQ,GDW1000              YES - GOOD DEST          AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        CHECK FOR VPS (SNA PRINTER) OUTPUT                   * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0500  DS    0H                                               AJSW012
         CLI   OSEDEST+7,C' '      LAST CHAR A BLANK?           AJSW012
         BC    NE,GDW0900          NO - CANNOT BE VPS           AJSW012
         CLI   OSEDEST+6,C' '      NEXT TO LAST ALSO BLANK?     AJSW012
         BC    EQ,GDW0900          YES - CANNOT BE VPS          AJSW012
         CLI   OSECLASS,C'V'       CLASS MUST BE = V            AJSW012
         BC    EQ,WC053STP         YES - GO CHECK PRINTER DEST  AJSW012
         CLI   OSECLASS,C'I'       CLASS MUST BE = I            AJSW012
         BC    EQ,WC053STP         YES - GO CHECK PRINTER DEST  AJSW012
*                                                               AJSW012
*                                                               AJSW012
GDW0900  DS    0H                                               AJSW012
         CLI   OSEDEST,C'$'        POSSIBLE TSO USERID          AJSW012
         BC    NE,GDW0900A         NO,CONTINUE                  AJSW012
         LH    R1,RQJOBNO          GET THE JOB NUMBER IN BINARY AJSW012
         IATXJQE JOBNUM=(R1),      GET JQE FOR THE JOB          AJSW012$
               ERROR=GDW0900A                                   AJSW012
*                                                               AJSW012
         LA    R1,JQEORG-JQESTART(,R1) -> JOB ORIGIN            AJSW012
         L     R15,ANJESRCH        -> NJE NODE NAME SRCH RTN    AJSW012
         ACALL ENTER=(R15)         SEE IF ORIGIN IS A NJE NODE  AJSW012
         B     GDW0900A            ORIGIN NOT AN NJE NODE       AJSW012
*                                                               AJSW012
         NI    MFLAGS,255-MFACFUID INIT LOGONID FOUND FLAG      AJSW012
*                                                               AJSW012
         LA    R0,ACFWRK#                                       AJSW012
         AGETMAIN SIZE=(R0),       ALLOCATE ACF WORK AREAS      AJSW012$
               LOC=BELOW                                        AJSW012
         LR    R9,R1               -> TO GOTTEN STORAGE         AJSW012
*                                                               AJSW012
         PUSH  USING               SAVE USING STATUS            AJSW012
         USING ACFWRK,R9           MAKE IT ADDRESSABLE          AJSW012
*                                                               AJSW012
         ACFINCVT 3,NONE=GDW0910   GET THE ACF CVT              AJSW012
         USING ACCVT,R3            MAKE IT ADDRESSABLE          AJSW012
*                                                               AJSW012
         XC    ACALT(ACALEN),ACALT INITIALIZE PARAMETER BLOCK   AJSW012
         MVI   ACAFCN,X'02'        SET UP ACF SVC CALL          AJSW012
         MVI   ACASFCN,ACASURT     RETURN UNFORMATTED LOGONID   AJSW012
         LA    R1,ERRINFO          GET MESSAGE AREA ADDRESS     AJSW012
         ST    R1,ACAMSG              AND STOW IN PARAM BLOCK   AJSW012
         MVC   ACALID,OSEDEST      POSSIBLE LOGONID TO RETURN   AJSW012
         LA    R1,LOGONREC         GET RECORD AREA ADDRESS      AJSW012
         ST    R1,ACARETB             AND STOW IN PARAM BLOCK   AJSW012
         MVC   ACARETL,=A(L'LOGONREC) SET RECORD LENGTH         AJSW012
*                                                               AJSW012
         ACFSVC ACALT,TYPE=A,      FIND THE LOGONID RECORD      AJSW012$
               NONE=GDW0910,                                    AJSW012$
               CVT=HAVE                                         AJSW012
         LTR   R15,R15             LOGON RECORD FOUND           AJSW012
         BC    NZERO,GDW0910       NO, JUST FREE ACF WORK AREA  AJSW012
         OI    MFLAGS,MFACFUID     YES,REMEMBER IT              AJSW012
*                                                               AJSW012
GDW0910  DS    0H                                               AJSW012
         L     R3,RESQ@            RESTORE R3                   AJSW012
         LA    R0,ACFWRK#          SIZE OF AREA TO BE FREED     AJSW012
         APUTMAIN AREA=(R9),       FREE THE ACFWORK AREA        AJSW012$
               SIZE=(R0)                                        AJSW012
         L     R2,OSE@             RESTORE OSE ->               AJSW012
*                                                               AJSW012
         TM    MFLAGS,MFACFUID     LOGONID RECORD FOUND         AJSW012
         BC    NZERO,GDW1000       YES,CONTINUE                 AJSW012
*                                                               AJSW012
         POP   USING                                            AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        END OF THE LINE.  NO GOOD DEST SO KILL OSE.          * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW0900A DS    0H                                               AJSW012
         B     DO016              *NOTHING MATCHED. KILL OSE.   AJSW012
         EJECT                                                  AJSW012
*-------------------------------------------------------------* AJSW012
*        VALIDATE OSE FORM SPECIFICATION                      * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        FORM MUST BE 4 CHARACTERS LONG AND NUMERIC           * AJSW012
*        OR BE PRECEEDED BY 'P' OR 'PVT', OTHERWISE JUNK.     * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW1000  DS    0H                                               AJSW012
         CLC   OSEFORMS+4(4),=CL4' '  IS IT 4 OR LESS LONG?     AJSW012
         BC    NE,GDW1100          NO. GO CHECK PVT DATA.       AJSW012
         CLI   OSEFORMS+3,C' '     IS IT AT LEAST 4 LONG?       AJSW012
         BC    EQ,DO017            NO. KILL OSE.                AJSW012
         CLC   =C'PR',OSEFORMS     SPECIAL PAYROLL FORMS        AJSW012
         BC    EQ,GDW1010          YES,JECK REST OF FORM ID     AJSW012
         LA    R9,OSEFORMS         POINT TO BEGINNING           AJSW012
         B     GDW1300             GO CHECK NUMERICS            AJSW012
*                                                               AJSW012
GDW1010  DS    0H                                               AJSW012
         ICM   R9,B'1100',OSEFORMS+2 FILL REGISTER WITH         AJSW012
         ICM   R9,B'0011',OSEFORMS+2   FORM NUMBER              AJSW012
         B     GDW1310                                          AJSW012
*                                                               AJSW012
GDW1100  DS    0H                                               AJSW012
         CLC   OSEFORMS(3),=C'PVT'  BEGIN WITH 'PVT'?           AJSW012
         BC    NE,GDW1200          NO. CHECK 'P'.               AJSW012
         CLI   OSEFORMS+7,C' '     LAST CHAR BLANK?             AJSW012
         BC    NE,DO017            NO. BAD FORM.                AJSW012
         LA    R9,OSEFORMS+3       POINT TO BEGINNING           AJSW012
         B     GDW1300             GO CHECK NUMERICS            AJSW012
*                                                               AJSW012
GDW1200  DS    0H                                               AJSW012
         CLI   OSEFORMS,C'P'       BEGIN WITH 'P'?              AJSW012
         BC    NE,DO017            NO. CHECK PR.                AJSW012
         CLC   OSEFORMS+5(3),=CL3' '  LAST THREE BLANKS?        AJSW012
         BC    NE,DO017            NO. BAD FORM.                AJSW012
         LA    R9,OSEFORMS+1       POINT TO BEGINNING           AJSW012
*                                                               AJSW012
GDW1300  DS    0H                                               AJSW012
         L     R9,0(R9)            PUT FORM IN REG              AJSW012
*                                                               AJSW012
GDW1310  DS    0H                                               AJSW012
         O     R9,=X'0F0F0F0F'     SET NUMERICS TO X'FF'        AJSW012
         C     R9,=X'FFFFFFFF'     WERE THEY ALL NUMERIC?       AJSW012
         BC    NE,DO017            NO. KILL OSE.                AJSW012
         EJECT                                                  AJSW012
*-------------------------------------------------------------* AJSW012
*        VALIDATE OSE FCB SPECIFICATION                       * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        FCB MUST BE 4 CHARACTERS LONG OR =6 OR =8 .          * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW2000  DS    0H                                               AJSW012
         CLC   OSETYPE,=CL8'PRT'   ONLY CHECK FOR PRT TYPE      AJSW012
         BC    NE,GDW5000          NO. SKIP CHECK               AJSW012
         CLC   OSECARR+4(4),=CL4' '  IS IT 4 OR LESS LONG?      AJSW012
         BC    NE,DO018            NO. KILL OSE.                AJSW012
         CLI   OSECARR+3,C' '      IS IT NOT 4 LONG?            AJSW012
         BC    EQ,GDW2100          NO. ONLY TWO POSSIBLE.       AJSW012
         CLI   OSECARR,C'C'        CDSC FCB?                    AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
         CLI   OSECARR,C'D'        DX FCB?                      AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
         CLI   OSECARR,C'E'        EDSC FCB?                    AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
         CLI   OSECARR,C'W'        WDSC FCB?                    AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
         CLC   OSECARR(3),=CL3'STD'  IS IT A STD?               AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
         CLC   OSECARR,=CL8'TILT'  IS IT TILT?                  AJSW012
         BC    EQ,GDW5000          YES. LET IT GO.              AJSW012
*                                                               AJSW012
GDW2100  DS    0H                                               AJSW012
         CLC   OSECARR,=CL8'6'     IS IT =6?                    AJSW012
         BC    EQ,GDW5000         *YES. LET IT GO.              AJSW012
         CLC   OSECARR,=CL8'8'    *IS IT =8?                    AJSW012
         BC    NE,DO018           *NO. KILL OSE.                AJSW012
         EJECT                                                  AJSW012
*-------------------------------------------------------------* AJSW012
*        COMPUTE OSE PRIORITY                                 * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
*-------------------------------------------------------------* AJSW012
*        CHECK IF ALREADY SPECIFIED & VALIDATE CLASS.         * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW5000  DS    0H                                               AJSW012
         CLI   OSEPRTY,30          IS THERE ONE THERE ALREADY?  AJSW012
         BNH   GDW5100             NO - GO COMPUTE IT           AJSW012
         TM    RQFLG5,RQDSEL       DEMAND SELECT JOB?           AJSW012
         BNO   GDW5050             NO, CHECK JOB CLASS          AJSW012
         TM    RQFLG5,RQMVSTSO     YES, IS TSO JOB?             AJSW012
         BNO   GDW9000             NO, IS STC, ACCEPT PRTY      AJSW012
         BO    GDW5100             YES, SKIP JOB CLASS CHECK    AJSW012
*                                                               AJSW012
GDW5050  DS    0H                                               AJSW012
         LH    R1,RQJOBNO          GET JOB NUMBER IN BINARY     AJSW012
*                                                               AJSW012
       IATXJQE JOBNUM=(R1),ERROR=GDW5100  GET JQE ADDRESS       AJSW012
*                                                               AJSW012
         SLR   R6,R6               CLEAR REG                    AJSW012
         USING JQESTART,R1         SET ADDRESSABILITY           AJSW012
         IC    R6,JQECLS           GET JOB CLASS                AJSW012
         DROP  R1                  DROP REG 1                   AJSW012
         LTR   R6,R6               DID WE GET SOMETHING?        AJSW012
         BZ    GDW5100             NO - TOO BAD - COMPUTE PRTY  AJSW012
         BCTR  R6,0                MAKE OFFSET INSTEAD OF COUNT AJSW012
         L     R7,MCLASS           GET CLASS TABLE ADDRESS      AJSW012
         USING MCNAME,R7           SET ADDRESSING               AJSW012
         MH    R6,MCLNG            DEVELOP POINTER TO CLASS     AJSW012
         LA    R7,0(R7,R6)         POINT TO OUR ENTRY           AJSW012
         CLC   MCNAME,=CL8'PROD'   IS THIS OUR CLASS?           AJSW012
         BE    GDW9000             YES - PROCESS OSE AS IS.     AJSW012
         CLC   MCNAME,=CL8'URGENT'  IS THIS OUR CLASS?          AJSW012
         BE    GDW9000             YES - PROCESS OSE AS IS.     AJSW012
         CLC   MCNAME,=CL8'TP'     IS THIS OUR CLASS?           AJSW003
         BE    GDW9000             YES - PROCESS OSE AS IS.     AJSW003
*                                                               AJSW012
GDW5100  DC    0H'0'                                            AJSW012
         LA    R15,FXPRTYPX        ->FIXED PRIORITY TABLE PFXS  AJSW012
         USING WDPRTYPX,R15        FIXED PRTY JOBNAME PFX TBL   AJSW012
         LA    R14,FXPRTYID        ->FIXED PRIORITY TABLE UIDS  AJSW012
         USING WDPRTYID,R14        FIXED PRTY UID VALUES        AJSW012
         XR    R1,R1               CLEAR FOR INSERTIONS         AJSW012
*                                                               AJSW012
GDCMPPFX DC    0H'0'                                            AJSW012
         ICM   R1,B'0001',WDPRTYPL LENGTH OF ENTRY              AJSW012
         BC    ZERO,GDNOTFXP       NOT A FIXED PRIORITY JOB     AJSW012
         EX    R1,GDFXPCMP         THIS JOB NAME V. PREFIX NTRY AJSW012
         BC    EQ,GDCMPUID         FOUND A POSSIBILITY          AJSW012
         LA    R15,WDPRTYNX(R1)    STEP TO NEXT ENTRY           AJSW012
         B     GDCMPPFX                                         AJSW012
*                                                               AJSW012
GDFXPCMP CLC   WDPRTYPF(0),RQJOBNAM * * * E X E C U T E D * * * AJSW012
         DROP  R15                                              AJSW012
*                                                               AJSW012
GDCMPUID DC    0H'0'                                            AJSW012
         CLI   WDPRTYNM,XFF        END OF UID TABLE?            AJSW012
         BC    EQ,GDNOTFXP         END OF TBL, NO MATCH         AJSW012
         CLC   WDPRTYNM,RQTUSID    USERID MATCH?                AJSW012
         BC    EQ,GDGOTFXP         YES, SET PRIORITY            AJSW012
         LA    R14,WDNXTUID        STEP TO NEXT TABLE ENTRY     AJSW012
         B     GDCMPUID            LOOP FOR NEXT TABLE ENTRY    AJSW012
*                                                               AJSW012
GDGOTFXP DC    0H'0'                                            AJSW012
         MVC   OSEPRTY,WDPRTYFX    SET REQ'D PRIORITY           AJSW012
         B     GDW9000             EXIT W/FIXED PRIORITY        AJSW012
         DROP  R14                                              AJSW012
*                                                               AJSW012
GDNOTFXP DC    0H'0'                                            AJSW012
         SPACE 1                                                AJSW012
*-------------------------------------------------------------* AJSW012
*        COMPUTE OSE PRIORITY                                 * AJSW012
*                                                             * AJSW012
*        OSEPRTY = 200 - ( 25 * LOG (#LINES/100) )            * AJSW012
*                                                             * AJSW012
*        WHICH IS TO SAY:                                     * AJSW012
*                                                             * AJSW012
*              0 < #LINES < 1000      => OSEPRTY = 200        * AJSW012
*           1000 < #LINES < 10000     => OSEPRTY = 175        * AJSW012
*          10000 < #LINES < 100000    => OSEPRTY = 150        * AJSW012
*         100000 < #LINES < 1000000   => OSEPRTY = 125        * AJSW012
*        1000000 < #LINES < 10000000  => OSEPRTY = 100        * AJSW012
*                                                             * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
         TM    MFLAGS,MFPRTY       PRIORITY CALCULATED          AJSW012
         BC    ALLON,GDW5400       YES,CONTINUE                 AJSW012
*                                                               AJSW012
         XC    JOBRECDS,JOBRECDS   CLEAR ACCUMULATOR            AJSW012
         L     R1,RESQ@            -> RESQ FOR JOB              AJSW012
         USING RQSTART,R1                                       AJSW012
         L     R1,RQJDSFDB         -> JDS FOR JOB               AJSW012
         DROP  R1                                               AJSW012
         USING JDSSTART,R1                                      AJSW012
         LH    R0,JDSTOTL          TOTAL JDS LENGTH             AJSW012
         LH    R5,JDSFIXL          LENGTH OF JDS FIXED PORTION  AJSW012
         LA    R1,0(R5,R1)         BUMP TO FOR DATA SET ENTRY   AJSW012
         SR    R0,R5               ADJUST LENGTH                AJSW012
         DROP  R1                                               AJSW012
         USING JDSENTRY,R1                                      AJSW012
*                                                               AJSW012
PJDS0000 DS    0H                                               AJSW012
         ICM   R5,B'1111',JDSRECDS TOTAL RECORDS FOR DATA SET   AJSW012
         BC    ZERO,PJDS2000       NEXT JDS IF COUNT = 0        AJSW012
         SLR   R6,R6               CLEAR COPY COUNT             AJSW012
         ICM   R6,B'0001',JDSCOPY  GET COPY COUNT               AJSW012
         BC    ZERO,PJDS1000       DEFAULT TO 1 COPY            AJSW012
         MR    R4,R6               CALC DATA SET TOTAL LINES    AJSW012
*                                                               AJSW012
PJDS1000 DS    0H                                               AJSW012
         A     R5,JOBRECDS         ACCUMULATE JOB TOTAL LINES   AJSW012
         ST    R5,JOBRECDS         SAVE ACCUMULATED RECORDS     AJSW012
*                                                               AJSW012
PJDS2000 DS    0H                                               AJSW012
         LH    R5,JDSVARL          GET LENGTH OF THIS ENTRY     AJSW012
         SR    R0,R5               GET RESIDUAL LENGTH          AJSW012
         BC    ZERO,PJDS9999       CONTINUE IF DONE             AJSW012
         LA    R1,0(R5,R1)         IF NOT GET NEXT DATA SET ENT AJSW012
         B     PJDS0000            LOOP BACK TIL DONE           AJSW012
*                                                               AJSW012
         DROP  R1                                               AJSW012
*                                                               AJSW012
PJDS9999 DS    0H                                               AJSW012
         SLR   R4,R4               PREP REGISTER FOR DIVIDE     AJSW012
         L     R5,JOBRECDS         TOTAL SPOOL RCDS FOR JOB     AJSW012
         LA    R6,10               LOAD DIVISOR INTO REG 6      AJSW012
         SLR   R9,R9               LOAD COUNT INTO REG 9        AJSW012
         D     R4,=F'100'          DIVIDE BY 100                AJSW012
*                                                               AJSW012
GDW5200  DS    0H                                               AJSW012
         SLR   R4,R4               CLEAR REMAINDER              AJSW012
         DR    R4,R6               DIVIDE BY 10                 AJSW012
         LTR   R5,R5               ZERO?                        AJSW012
         BZ    GDW5300             YES - OUT OF LOOP.           AJSW012
         LA    R9,1(R9)            ADD ONE TO COUNT             AJSW012
         B     GDW5200             AND AROUND AGAIN             AJSW012
*                                                               AJSW012
GDW5300  DS    0H                                               AJSW012
         LR    R5,R9               GET NUMBER OF X1000 LINES    AJSW012
         M     R4,=F'25'           MULTIPLY BY 25               AJSW012
         LA    R4,200              PUT BASE IN REG 4            AJSW012
         SR    R4,R5               SUBTRACT FROM 200            AJSW012
         STC   R4,SVPRTY           AND SAVE IT DOWN             AJSW012
         OI    MFLAGS,MFPRTY       INDICATE PRIORITY CALC'ED    AJSW012
*                                                               AJSW012
GDW5400  DS    0H                                               AJSW012
         MVC   OSEPRTY,SVPRTY      SET OSE PRIORITY             AJSW012
         EJECT                                                  AJSW012
*-------------------------------------------------------------* AJSW012
*        RETURN TO IATOSDR                                    * AJSW012
*-------------------------------------------------------------* AJSW012
         SPACE 1                                                AJSW012
*-------------------------------------------------------------* AJSW012
*        NORMAL RETURN                                        * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
GDW9000  DS    0H                                               AJSW012
         SLR   R15,R15             CLEAR REGISTER 15            AJSW012
         ARETURN RC=4              NORMAL RETURN                AJSW012
         SPACE 1                                                AJSW012
*-------------------------------------------------------------* AJSW012
*        RETURN TO KILL OSE                                   * AJSW012
*-------------------------------------------------------------* AJSW012
*                                                               AJSW012
KILLOSE  DS    0H                                               AJSW012
         MVC   ERRMSG+WC015I1(L'WC015I1),SVJOBNAM               AJSW012
         MVC   ERRMSG+WC015I2(L'WC015I2),SVJNUM                 AJSW012
*                                                               AJSW012
         MESSAGE TEXT=ERRMSG,     ISSUE THE ERROR MESSAGE       AJSW012$
               CLASS=S2                                         AJSW012
*                                                               AJSW012
KILLOSE9 DS    0H                                               AJSW012
         SLR   R15,R15             CLEAR REG 15                 AJSW012
         ARETURN RC=0              DON'T BUILD OSE              AJSW012
*                                                               AJSW012
         EJECT                                                  AJSW012
WC053STP DC    0H'0'                                            AJSW012
*                                                               AJSW012
*        THIS  ROUTINE IS DESIGNED TO VERIFY CLASS V SYSOUT     AJSW012
*        IS DESTINED FOR A VPS PRINTER.                         AJSW012
*                                                               AJSW012
*        A VPS DESTINATION IS DETECTED WHEN THE OUTPUT CLASS    AJSW012
*        IS "V" AND THE DESTINATION NAME IS EXACTLY 7           AJSW012
*        CHARACTERS LONG, AND THE 1ST 5 CHARACTERS DO *NOT*     AJSW012
*        APPEAR IN EITHER THE RJP OR SNARJP TABLES              AJSW012
*                                                               AJSW012
         SPACE 3                                                AJSW012
**                                                              AJSW012
*                                                               AJSW012
*        FIRST, CHECK TO SEE IF THE VPS PRINTER TABLE IS        AJSW012
*              AVAILABLE.                                       AJSW012
*              IF THE VPS PRINTER TABLE IS NOT AVAILABLE,       AJSW012
*              LOAD IT INTO MEMORY, GET STORAGE FOR OUR         AJSW012
*              OWN (ABOVE THE LINE) COPY OF THE TABLE,          AJSW012
*              COPY THE LOADED DATA INTO THE ACQUIRED           AJSW012
*              STORAGE.                                         AJSW012
*              WHEN THE VPS PRINTER TABLE IS AVAILABLE,         AJSW012
*              SEARCH IT FOR THE SPECIFIED DESTINATION          AJSW012
*              NAME, AND EXTRACT THE CORRESPONDING PRINTER      AJSW012
*              NAME.                                            AJSW012
*                                                               AJSW012
**                                                              AJSW012
         SPACE 1                                                AJSW012
         ICM   R9,B'1111',WDVPSTBL -> TABLE OF VPS DEST/PRTRS   AJSW012
         BC    NZERO,WDGOTVPS      TABLE HAS BEEN ACQUIRED.     AJSW012
         IATXCSF ENTER=WD053L,     LOAD TABLE UNDER SUBTASK     AJSW012*
               ID=154              GROUP LOAD/DELETE W/SUBTASK  AJSW012
*                                                               AJSW012
         USING WDPRTMAP,R3         LOAD MODULE ADDRBLTY         AJSW012
         LR    R3,R0               COPY EP ADDRESS              AJSW012
         LA    R3,0(R3)            CLEAR ANY FLAGS              AJSW012
         LR    R15,R0              COPY EPA FOR LENGTH CALC     AJSW012
*                                                               AJSW012
WDPRTCT  DS    0H                                               AJSW012
         CLI   0(R15),XFF          END OF TABLE?                AJSW012
         LA    R15,WDPRTLNG(,R15)  GET NEXT ENTRY.              AJSW012
         BC    NE,WDPRTCT          NOT END OF TABLE             AJSW012
*                                                               AJSW012
         SR    R15,R3              GET TOTAL TABLE LENGTH       AJSW012
         ST    R15,WDNAMSIZ        SAVE FOR LATER               AJSW012
         LR    R0,R15              COPY STORAGE REQUIREMENT     AJSW012
*                                                               AJSW012
*        GET STORAGE FOR NAME-TO-DESTINATION LOOKUP TABLE       AJSW012
*                                                               AJSW012
         AGETMAIN SIZE=(R0),LOC=(ANY,ANY) FOR LOOKUP TABLE      AJSW012
*                                                               AJSW012
         LR    R9,R1               COPY ACQUIRED STORAGE ADDR   AJSW012
         ST    R9,WDVPSTBL         SAVE -> VPS DEST TABLE       AJSW012
         USING WDPLUTBL,R9         PRINTER LOOKUP TBL ADDRBLTY  AJSW012
*                                                               AJSW012
*        COPY LOAD MODULE ENTRIES TO LOOKUP TABLE               AJSW012
*                                                               AJSW012
         LA    R14,WDPRTNAM        -> LOAD MODULE TABLE         AJSW012
         L     R1,WDNAMSIZ         SET LENGTH TO MOVE           AJSW012
         LR    R15,R1              COPY LENGTH TO MOVE          AJSW012
         MVCL  R0,R14              COPY TO HI-STORAGE TABLE     AJSW012
*                                                               AJSW012
*        DELETE VPS PRINT MODULE                                AJSW012
*                                                               AJSW012
         IATXCSF ENTER=WD053D,     DELETE TABLE UNDER SUBTASK   AJSW012*
               ID=154              GROUP LOAD/DELETE W/SUBTASK  AJSW012
         DROP  R3                  PRINTER NAME ADDRESSABILITY  AJSW012
*                                                               AJSW012
*        LOOK UP THE DESTINATION IN THE PRINTER LOOKUP TABLE    AJSW012
*                                                               AJSW012
WDGOTVPS DC    0H'0'                                            AJSW012
         CLC   OSEDEST,WDPLUDST    OSE DEST MATCH LOOKUP ENTRY? AJSW012
         BC    EQ,GDW9000          YES, EXIT                    AJSW012
         LA    R9,WDPLUNXT         NO, STEP TO NEXT ENTRY       AJSW012
         CLI   WDPLUNAM,XFF        END OF PRINTER DEST TABLE?   AJSW012
         BC    EQ,DO015            YES, NO HOPE DELETE D.S.     AJSW012
         B     WDGOTVPS            NO, CHECK NEXT ENTRY         AJSW012
*                                                               AJSW012
**                                                              AJSW012
*                                                               AJSW012
*        LOAD VPS MODULE CONTAINING TERMINAL NAMES AND          AJSW012
*              DESTINATION EQUIVALENTS                          AJSW012
*                                                               AJSW012
**                                                              AJSW012
*                                                               AJSW012
WD053L   DC    0D'0'                                            AJSW012
         LR    R3,R14              SAVE RETURN ADDRESS          AJSW012
         LR    R4,R13              SAVE CALLER'S SAVEAREA ADDR  AJSW012
         LOAD  EPLOC=WDVPSNAM      LOAD VPS TABLE               AJSW012
         LR    R14,R3              RESTORE RETURN ADDRESS       AJSW012
         LR    R13,R4              RESTORE SAVEAREA ADDRESS     AJSW012
         BR    R14                 RETURN TO CALLER             AJSW012
**                                                              AJSW012
*                                                               AJSW012
*        DELETE VPS MODULE CONTAINING TERMINAL NAMES AND        AJSW012
*              DESTINATION EQUIVALENTS                          AJSW012
*                                                               AJSW012
**                                                              AJSW012
*                                                               AJSW012
WD053D   DC    0D'0'                                            AJSW012
         LR    R3,R14              SAVE RETURN ADDRESS          AJSW012
         LR    R4,R13              SAVE CALLER'S SAVE AREA ADDR AJSW012
         DELETE EPLOC=WDVPSNAM     DELETE VPS TABLE             AJSW012
         LR    R14,R3              RESTORE RETURN ADDRESS       AJSW012
         LR    R13,R4              RESTORE SAVE AREA ADDRESS    AJSW012
         BR    R14                 RETURN TO CALLER             AJSW012
         AGO  .NOESI         CHANGE TO EJECT                    AJSW012
*
***********************************************************************
*                                                                     *
* IATUX19 -                                                           *
*        JES3 USER EXIT TO MODIFY AN OSE.  IT TRANSLATES A            *
*        DESTINATION INTO A NODE NAME AND DESTINATION BASED           *
*        UPON A TRANSLATE TABLE.                                      *
*                                                                     *
* COPYRIGHT -                                                         *
*        COPYRIGHT 1988 ESSENTIAL SOFTWARE, INC.                      *
*                                                                     *
* ATTRIBUTES -                                                        *
*        SERIALLY REUSABLE, SUPERVISOR STATE, JES PROTECT KEY.        *
*                                                                     *
***********************************************************************
*
ESI       DS    0H
*
*         SEARCH TABLE FOR DESTINATION
*
          LA      R4,ESITAB         LOAD ADDRESS FIRST ENTRY
*
ESI010    DS      0H
          CLI     0(R4),X'FF'       END OF TABLE ???
          BE      GDW9000           YES
          CLC     OSEDEST,0(R4)     ENTRY FOUND ???
          BE      ESI020            YES
          LA      R4,24(,R4)        NEXT ENTRY
          B       ESI010            LOOP BACK
*
*         REPLACE NODE AND DESTINATION
*
ESI020    DS      0H
          MVC     OSEDEST,8(R4)     REPLACE NODE
          MVC     OSEDEST2,16(R4)   REPLACE DESTINATION
          OI      OSENJE1,OSENJEOP  INDEICATE OSE FOR NETWORK
          CLC     OSEDEST,HOMENODE  HOME NODE?
          BNE     GDW9000           NO
          MVC     OSEDEST,OSEDEST2  IGNORE NODE
          XC      OSEDEST2,OSEDEST2
          NI      OSENJE1,X'FF'-OSENJEOP
          B       GDW9000
*
***********************************************************************
*         D E S T I N A T I O N   T R A N S L A T E   T A B L E       *
*                                                                     *
*        THE DESTINATION TRANSLATE TABLE CONTAINS ENTRIES OF          *
*        24 BYTES IN THE FOLLOWING FORMAT:                            *
*        01-08 - ORIGINAL DESTINATION FROM JCL                        *
*        09-16 - TRANSLATED NODE NAME                                 *
*        17-24 - TRANSLATED DESITNATION                               *
*                                                                     *
*                               1         2                           *
*                      123456789012345678901234                       *
*                      +------+-------+-------+                       *
*                      | ORG  |  TRANSLATED   |                       *
*                      | DEST | NODE    DEST  |                       *
*                      +------+-------+-------+                       *
*                      DDDDDDDDNNNNNNNNDDDDDDDD                       *
*                                                                     *
*         DC      CL24'DESTFROMESI1    DESTO   '                      *
*                                                                     *
***********************************************************************
*
ESITAB   DS    0C
         DC      CL24'EXPLS01 EDSCE   STRLH   '
ESITABEN DC      XL24'FF'            END OF TABLE
.NOESI   ANOP    CHANGE TO EJECT
*************************************************************** AJSW012
*          MESSAGE FORMATTING ROUTINES                        * AJSW012
*************************************************************** AJSW012
*                                                               AJSW012
DO015    DS    0H                  INVALID VPS DEST             AJSW012
         TM    EFLAGS,EFVPS        HAS THIS MESSAGE BEEN ISSUED AJSW012
         BC    ALLON,KILLOSE9      YES, DON'T RE-ISSUE          AJSW012
         MVC   ERRMSG(WC015#),IATWC015 COPY MESSAGE SKELETON    AJSW012
         MVC   ERRMSG+WC015I3(L'WC015I3),OSEDEST                AJSW012
         OI    EFLAGS,EFVPS        INDICATE IATWC015 ISSUED     AJSW012
         B     KILLOSE             SEND MESSAGE TO MLOG         AJSW012
*                                                               AJSW012
DO016    DS    0H                  INVALID DESTINATION          AJSW012
         TM    EFLAGS,EFDEST       HAS THIS MESSAGE BEEN ISSUED AJSW012
         BC    ALLON,KILLOSE9      YES, DON'T RE-ISSUE          AJSW012
         MVC   ERRMSG(WC016#),IATWC016 COPY MESSAGE SKELETON    AJSW012
         MVC   ERRMSG+WC016I3(L'WC016I3),OSEDEST                AJSW012
         OI    EFLAGS,EFDEST       INDICATE IATWC016 ISSUED     AJSW012
         B     KILLOSE             SEND MESSAGE TO MLOG         AJSW012
*                                                               AJSW012
DO017    DS    0H                  INVALID FORMS SPECIFICATION  AJSW012
         TM    EFLAGS,EFFORM       HAS THIS MESSAGE BEEN ISSUED AJSW012
         BC    ALLON,KILLOSE9      YES, DON'T RE-ISSUE          AJSW012
         MVC   ERRMSG(WC017#),IATWC017 COPY MESSAGE SKELETON    AJSW012
         MVC   ERRMSG+WC017I3(L'WC017I3),OSEFORMS               AJSW012
         OI    EFLAGS,EFFORM       INDICATE IATWC017 ISSUED     AJSW012
         B     KILLOSE             SEND MESSAGE TO MLOG         AJSW012
*                                                               AJSW012
DO018    DS    0H                  INVALID FCB SPECIFICATION    AJSW012
         TM    EFLAGS,EFFCB        HAS THIS MESSAGE BEEN ISSUED AJSW012
         BC    ALLON,KILLOSE9      YES, DON'T RE-ISSUE          AJSW012
         MVC   ERRMSG(WC018#),IATWC018 COPY MESSAGE SKELETON    AJSW012
         MVC   ERRMSG+WC018I3(L'WC018I3),OSECARR                AJSW012
         OI    EFLAGS,EFFCB        INDICATE IATWC018 ISSUED     AJSW012
         B     KILLOSE             SEND MESSAGE TO MLOG         AJSW012
         EJECT                                                  AJSW012
WDVPSNAM DC    CL8'PID$DEST'       E.P. FOR VPS NAME TABLE      AJSW012
WDPFX8   DS    0CL8                8 BYTE PREFIX LIST           AJSW012
         DC    CL8'DBCTDBM1'                               @JBD AJSW012
         DC    CL8'DBCTMSTR'                               @JBD AJSW012
         DC    CL8'DBIPDBM1'                               @JBD AJSW012
         DC    CL8'DBIPMSTR'                               @JBD AJSW012
         DC    CL8'DBS5DBM1'                               @JBD AJSW012
         DC    CL8'DBS5MSTR'                               @JBD AJSW012
         DC    CL8'DBTPDBM1'                               @JBD AJSW012
         DC    CL8'DBTPMSTR'                               @JBD AJSW012
         DC    CL8'IPLMPROC'                                    AJSW012
         DC    CL8'ITLMPROC'                                    AJSW012
         DC    CL8'TRLMPROC'                                    AJSW012
         DC    CL8'XRLMPROC'                                    AJSW012
         DC    XL8'FFFFFFFFFFFFFFFF'                            AJSW012
WDPFX7   DC    0CL7' '             DEMAND SELECT PREFIXES       AJSW012
         DC    CL7'IMSPROD'                                     AJSW012
         DC    CL7'IMSTEST'                                     AJSW012
         DC    XL7'FFFFFFFFFFFFFF' END OF 7-BYTE PREFIX LIST    AJSW012
WDPFX6   DC    0CL6' '             6-BYTE DEMAND SELECT JOB PFX AJSW012
         DC    CL6'IMSSY5'                                      AJSW012
         DC    XL6'FFFFFFFFFFFF'   END OF 6-BYTE PREFIX LIST    AJSW012
WDPFX5   DC    0CL5' '             5-BYTE DEMAND SELECT JOB PFX AJSW012
         DC    CL5'CICSB'          CICSB, CICSBJNL, CICSBE      AJSW012
* THIS LINE DELETED 06/05/88                               @JBD AJSW012
         DC    CL5'OMEGA'                                       AJSW012
         DC    CL5'WCEMR'                                       AJSW012
         DC    CL5'WCJNL'                                       AJSW012
         DC    CL5'WCMRO'                                       AJSW012
         DC    XL5'FFFFFFFFFF'     END OF 5-BYTE PREFIX LIST    AJSW012
WDPFX4   DC    0CL4' '             4-BYTE DEMAND SELECT JOB PFX AJSW012
         DC    CL4'ARCH'                                        AJSW012
         DC    CL4'DBRC'                                        AJSW012
         DC    CL4'IDMS'                                   @JBD AJSW012
         DC    CL4'IDMT'                                   @JBD AJSW012
         DC    CL4'M204'                                   @JBD AJSW012
         DC    XL4'FFFFFFFF'       END OF 4-BYTE PREFIX LIST    AJSW012
WDPFX3   DC    0CL3' '             3-BYTE DEMAND SELECT JOB PFX AJSW012
         DC    CL3'CLS'                                         AJSW012
         DC    CL3'DB2'                                         AJSW012
         DC    CL3'DLI'                                         AJSW012
         DC    CL3'FCS'                                         AJSW012
         DC    XL3'FFFFFF'         END OF 3-BYTE PREFIX LIST    AJSW012
FXPRTYPX DC    0D'0'               FIXED PRIORITY TABLE PFXS    AJSW012
         DC    XL1'03'             LENGTH-1 OF FOLLOWING ENTRY  AJSW012
         DC    CL4'RSCS'           FIXED PRTY JOBNAME PREFIX    AJSW012
         DC    XL1'00'             END OF TABLE SENTINEL        AJSW012
FXPRTYID DC    0D'0'                                            AJSW012
* THIS LINE DELETED 06/05/88                               @JBD AJSW012
* THIS LINE DELETED 06/05/88                               @JBD AJSW012
         DC    CL7'$WC3TAF'        FIXED PRTY ELIGIBLE UID      AJSW012
         DC    AL1(201)            PRTY FOR THIS USER           AJSW012
         DC    8X'FF'              END OF TABLE SENTINEL        AJSW012
OSE@     DS    A                   -> OSE                       AJSW012
RESQ@    DS    A                   -> RESQ                      AJSW012
JDS@     DS    A                   -> JDS                       AJSW012
WDVPSTBL DC    A(0)                -> TABLE OF VPS DEST/PRTRS   AJSW012
WDNAMSIZ DC    F'0'                SIZE OF TABLE OF VPS DEST/PR AJSW012
XFF      EQU   X'FF'               USEFUL CONSTANT              AJSW012
X00      EQU   X'00'               ANOTHER ONE                  AJSW012
*                                                               AJSW012
JOBRECDS DS    F                   PRIORIT CALC WORK AREA       AJSW012
SVJOBNAM DC    CL8' '              SAVED JOB NAME               AJSW012
SVJNUM   DC    CL4' '              SAVED JOB NUMBER             AJSW012
SVPRTY   DC    X'00'               SAVED OUTPUT PRIORITY        AJSW012
*                                                               AJSW012
EFLAGS   DC    X'00'               DETECTED ERROR FLAGS         AJSW012
EFVPS    EQU   B'10000000'         INVALID VPS DESTINATION      AJSW012
EFDEST   EQU   B'01000000'         INVALID DESTINATION          AJSW012
EFFORM   EQU   B'00100000'         INVALID FORM                 AJSW012
EFFCB    EQU   B'00010000'         INVALID FCB                  AJSW012
*        EQU   B'00001000'         *** UNUSED ****              AJSW012
*        EQU   B'00000100'         *** UNUSED ****              AJSW012
*        EQU   B'00000010'         *** UNUSED ****              AJSW012
*        EQU   B'00000001'         *** UNUSED ****              AJSW012
*                                                               AJSW012
MFLAGS   DC    X'00'               MISC FLAGS                   AJSW012
MFPRTY   EQU   B'10000000'         PRIORITY HAS BEEN CALC'ED    AJSW012
MFACFUID EQU   B'01000000'         ACF LOGONID RECORD FOUND     AJSW012
*        EQU   B'00100000'         *** UNUSED ****              AJSW012
*        EQU   B'00010000'         *** UNUSED ****              AJSW012
*        EQU   B'00001000'         *** UNUSED ****              AJSW012
*        EQU   B'00000100'         *** UNUSED ****              AJSW012
*        EQU   B'00000010'         *** UNUSED ****              AJSW012
*        EQU   B'00000001'         *** UNUSED ****              AJSW012
         EJECT                                                  AJSW012
*************************************************************** AJSW012
*                                                             * AJSW012
*            ERROR MESSAGES                                     AJSW012
*************************************************************** AJSW012
*                                                               AJSW012
IATWC015 DS    0C                                               AJSW012
         DC    AL1(WC015#-1)       MESSAGE LENGTH               AJSW012
         DC    C'IATWC015E (UX19) '                             AJSW012
WC015I1  EQU   *-IATWC015,8,C'C'   OFFSET TO JOBNAME INSERT     AJSW012
         DC    CL(L'WC015I1)' '                                 AJSW012
         DC    C' ('                                            AJSW012
WC015I2  EQU   *-IATWC015,4,C'C'   OFFSET TO JOBNUMBER INSERT   AJSW012
         DC    CL(L'WC015I2)' '                                 AJSW012
         DC    C') '                                            AJSW012
WC015I3  EQU   *-IATWC015,8,C'C'   OFFSET TO VPS DEST INSERT    AJSW012
         DC    CL(L'WC015I3)' '                                 AJSW012
         DC    C' IS AN INVALID VPS DESTINATION'                AJSW012
WC015#   EQU   *-IATWC015                                       AJSW012
*                                                               AJSW012
IATWC016 DS    0C                                               AJSW012
         DC    AL1(WC016#-1)       MESSAGE LENGTH               AJSW012
         DC    C'IATWC016E (UX19) '                             AJSW012
WC016I1  EQU   *-IATWC016,8,C'C'   OFFSET TO JOBNAME INSERT     AJSW012
         DC    CL(L'WC016I1)' '                                 AJSW012
         DC    C' ('                                            AJSW012
WC016I2  EQU   *-IATWC016,4,C'C'   OFFSET TO JOBNUMBER INSERT   AJSW012
         DC    CL(L'WC016I2)' '                                 AJSW012
         DC    C') '                                            AJSW012
WC016I3  EQU   *-IATWC016,8,C'C'   OFFSET TO DESTINATION INSERT AJSW012
         DC    CL(L'WC016I3)' '                                 AJSW012
         DC    C' IS AN UNKNOW DESTINATION'                     AJSW012
WC016#   EQU   *-IATWC016                                       AJSW012
*                                                               AJSW012
IATWC017 DS    0C                                               AJSW012
         DC    AL1(WC017#-1)       MESSAGE LENGTH               AJSW012
         DC    C'IATWC017E (UX19) '                             AJSW012
WC017I1  EQU   *-IATWC017,8,C'C'   OFFSET TO JOBNAME INSERT     AJSW012
         DC    CL(L'WC017I1)' '                                 AJSW012
         DC    C' ('                                            AJSW012
WC017I2  EQU   *-IATWC017,4,C'C'   OFFSET TO JOBNUMBER INSERT   AJSW012
         DC    CL(L'WC017I2)' '                                 AJSW012
         DC    C') '                                            AJSW012
WC017I3  EQU   *-IATWC017,8,C'C'   OFFSET TO FORM INSERT        AJSW012
         DC    CL(L'WC017I3)' '                                 AJSW012
         DC    C' IS AN INVALID FORM SPECIFICATION'             AJSW012
WC017#   EQU   *-IATWC017                                       AJSW012
*                                                               AJSW012
IATWC018 DS    0C                                               AJSW012
         DC    AL1(WC018#-1)       MESSAGE LENGTH               AJSW012
         DC    C'IATWC018E (UX19) '                             AJSW012
WC018I1  EQU   *-IATWC018,8,C'C'   OFFSET TO JOBNAME INSERT     AJSW012
         DC    CL(L'WC018I1)' '                                 AJSW012
         DC    C' ('                                            AJSW012
WC018I2  EQU   *-IATWC018,4,C'C'   OFFSET TO JOBNUMBER INSERT   AJSW012
         DC    CL(L'WC018I2)' '                                 AJSW012
         DC    C') '                                            AJSW012
WC018I3  EQU   *-IATWC018,8,C'C'   OFFSET TO FCB INSERT         AJSW012
         DC    CL(L'WC018I3)' '                                 AJSW012
         DC    C' IS AN INVALID FCB SPECIFICATION'              AJSW012
WC018#   EQU   *-IATWC018                                       AJSW012
*                                                               AJSW012
ERRMSG   DS    0CL121               WORK AREA FOR MESSAGES      AJSW012
         DC    AL1(120)                                         AJSW012
         DC    CL120'***** UNINITIALIZED MESSAGE AREA *****'    AJSW012
*                                                               AJSW012
ACFWRK   DSECT ,                                                AJSW012
         ACALT DSECT=NO           RESOURCE PARAM BLOCK          AJSW012
ACALEN   EQU   *-ACALT            LENGTH OF RESOURCE PARAM BLK  AJSW012
*                                                               AJSW012
ERRINFO  DS    CL128              RETURNED MESSAGE AREA         AJSW012
LOGONREC DS    CL1024             LOGONID RECORD                AJSW012
ACFWRK#  EQU   *-ACFWRK           LENGTH OF ACF WORK AREA       AJSW012
IATUX19  CSECT ,                                                AJSW012
*                                                               AJSW012
         ACCVT ,                                                AJSW012
IATUX19  CSECT ,                                                AJSW012
*                                                               AJSW012
UMODNUM  DC    CL7'AJSW012'                                     AJSW012
