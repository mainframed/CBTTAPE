++ USERMOD(ABBW013) /* INSTALL OUR IEFU83 SMF EXIT:

    THIS MODIFICATION INSTALLS THE IEFU83 SMF EXIT WHICH WILL:

    1. CHANGE TYPE 14/15 RECORDS INTO TYPE 214/215 RECORDS.
       TYPE 214/215 RECORDS ARE SHORTENED 14/15 RECORDS AND
       CONTAIN ONLY THE SMF HEADER PLUS: DDNAME, DSNAME, JFCBNVOL,
       JFCBVOL, AND SMFEXCP(ALL UCBS). TYPE 214/215 RECORDS
       ARE NOT GENERATED FOR THE FOLLOWING TYPES OF CLOSES:
             1. TEMPORARY OR VIO DATASETS.
             2. TSO USER TO DATASET BEGINNING WITH USERID.
             3. TSO USER TO DATASET BEGINNING WITH 'SPF.' FOR INPUT.

    2. WHEN A TYPE 21 RECORD (TAPE UNLOAD) IS ENCOUNTERED THE
       TEMP IO ERROR FIELDS ARE COMPARED TO THE SIO COUNT DIVIDED
       BY 500 AND IF GREATER THEN A MESSAGE IS WRITTEN TO THE
       OPERATOR (HOPEFULLY HE/IT WILL CLEAN THE DRIVE AND OR
       SEND THE VOLUME TO THE TAPE LIBRARY TO BE CLEANED).

    3. WHEN A TYPE 40 RECORD IS ENCOUNTERED THEN IF IT IS A DYNAMIC
       UNALLOCATION THEN THE I/O COUNT IS ACCUMULATED IN THE FIRST
       FOUR BYTES IN THE USER SMF WORK AREA.  THIS WORKAREA IS POINT-
       ED TO BY THE JMRUCOM FIELD AND IS ALSO USED BY THE IEFACTRT
       SMF EXIT TO ACCUMULATE DATA FOR JOBS BETWEEN EXITS. THIS AREA
       IS GETMAINED BY THE FIRST EXIT TO NEED IT (IEFU83, IEFU84 OR
       IEFACTRT) AND FREEMAINED (ONLY) BY IEFACTRT WHEN PROCESSING
       A TYPE 05 OR TYPE 35 SMF RECORD.  WHENEVER A TYPE 04 OR 34
       RECORD IS ENCOUNTERED THEN THE TYPE 40 I/O COUNT ACCUMULATION
       IS ADDED TO THE FIRST DISK DEVICE ENTRY IN THAT RECORD. ALL
       TYPE40 RECORDS ARE SUPPRESSED FROM BEING PHYSICALLY WRITTEN
       TO THE SMF DATASETS (THEY ARE STOPPED BY THIS ROUTINE RETURN-
       ING WITH RC=4 BUT THEY MUST BE ENABLED IN THE SMFPRM00 MEMBER
       OF PARMLIB FOR THIS ROUTINE TO EVER SEE THEM).

       THE BASIC REASON FOR DOING ALL THIS IS TO ALLOW FOR THE
       ACCOUNTING ROUTINES TO CORRECTLY CHARGE FOR ALL I/O WITHOUT
       COLLECTING MILLIONS OF TYPE40 RECORDS.

       TAPE I/O IS ADJUSTED TO DISK I/O BY K03/K02 AND THAT CODE
       MUST BE CHANGED IF THE ALGORITHM VALUES CHANGE.  THERE IS
       NO ATTEMPT TO COMPENSATE FOR THE (SLIGHT) DIFFERENCE IN
       I/O TIME USED IN COMPUTING STORAGE OCCUPANCY.

       THIS CODE MUST BE IN BOTH IEFU83 AND IEFU84 TO WORK.

    4. WILL INHIBIT ACTUAL WRITING OF SELECTED ACF2 RECORDS:

       'D' (DATASET ACCESS RECORDS) FOR HSMS*.
       'L' (LOGONID DATABASE MODIFICATION JOURNAL RECORD) FOR
           JES3 (BATCH JOB ACCESS) AND MSTRJCL00 (TSO LOGONS).
       'V' (GENERALIZED RESOURCE ACCESS JOURNAL RECORD) FOR
           FLASHER JOBNAME RESOURCE.

    5. JCLIN IS REQUIRED ONLY IF UCC7 IS INSTALLED.
                                                                 */ .
++ VER(Z038) FMID(HBB2102) /*   MVSXA VER 2  */ .
++ JCLIN .
//GEN11 JOB WC07N0322,'531-1 RC CREWS'
//SG21 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//INCLUDE  DD  DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
  CHANGE USERU831(SASSU83)
  INCLUDE INCLUDE(UCCU83)
  INCLUDE INCLUDE(SASSU83)
  INCLUDE INCLUDE(ICMDSECT)
  CHANGE IEFU83(USERU832)
  INCLUDE SYSPUNCH(IEFU83)
  CHANGE IEFU83(USERU833)
  INCLUDE INCLUDE(FNMU83)
  ENTRY UCCU83
  NAME IEFU83(R)
/*
++ MOD(UCCU83)   LKLIB(INCLUDE) .
++ MOD(SASSU83)  LKLIB(INCLUDE) .
++ MOD(ICMDSECT) LKLIB(INCLUDE) .
++ MOD(FNMU83)   LKLIB(INCLUDE) .
++ SRC(IEFU83) DISTLIB(ASRCLIB) .
         MACRO
         REGS
         LCLA  &R
.R       AIF   (&R EQ 16).END
R&R      EQU   &R
&R       SETA  &R+1
         AGO   .R
.END     MEND
         TITLE 'LAST CHANCE SMF RECORD EXAMINATION ROUTINE - IEFU83'
IEFU83   CSECT
         REGS
         STM   R14,R12,12(R13)     SAVE REGS
         BALR  R10,R0              SET UP BASE REG
         USING *,R10               USING
         L     R12,0(R1)           GET SMF RDW ADDRESS
         LH    R14,0(R12)          GET SMF RECORD LENGTH
         C     R14,=F'6'           CHECK IF LONG ENOUGH
         BL    EXIT                NO - DONT LOOK AT IT
         SPACE 2
*
*        THIS PART OF CODE CHANGES TYPE 14/15 RECORDS
*        INTO (SHORTENED) TYPE 214/215 RECORDS.
*
         SPACE 1
         CLI   5(R12),14           TYPE 14 RECORD
         BE    TYPE14              YES - GO SHORTEN IT
         CLI   5(R12),15           TYPE 15 RECORD
         BNE   CHECK21             NO - CHECK IF TYPE21
*
         USING SMFTYP14,R12        ESTABLISH ADDRESSING
TYPE14   TM    SMF14RIN,SMF14VIO+SMF14TDS IS IT VIO OR TEMP
         BNZ   SUPRESS             YES - DON'T WRITE THIS RECORD
         LA    R7,SMFJFCB1         GET POINTER TO JFCB SECTION
         USING JFCBDSNM,R7         ESTABLISH ADDRESSING
         CLI   SMF14JBN,C'$'       IS IT TSO ?
         BNE   PROCESS             NO - SHORTEN AND WRITE RECORD
         CLI   SMF14JBN+7,C' '     IS IT TSO ?
         BNE   PROCESS             NO - SHORTEN AND WRITE RECORD
         MVI   SMF14JBN+7,C'.'     PUT IN '.'
         CLC   SMF14JBN,JFCBDSNM   DOES DSNAME BEGIN WITH USERID ?
         MVI   SMF14JBN+7,C' '     PUT BLANK BACK IN
         BE    SUPRESS             YES - DON'T WRITE THIS RECORD
         CLC   JFCBDSNM(4),=C'SPF.' WAS IT AN SPF DATASET
         BNE   PROCESS             NO - SHORTEN AND WRITE RECORD
         CLI   SMF14RTY,14         YES - WAS IT FOR INPUT ?
         BE    SUPRESS             YES - DON'T WRITE THIS RECORD
*
PROCESS  IC    R2,SMF14RTY         PICK UP RECORD TYPE
         LA    R2,200(R2)          ADD 200
         STC   R2,SMF14RTY         AND SAVE IT BACK
         LA    R2,L214RD           GET LENGTH OF TYPE 214/215 RECORDS
         STH   R2,SMF14LEN         FORCE NEW RECORD LENGTH
*
         LM    R2,R5,ZEROS         CLEAR REGS
         IC    R2,SMF14SDC         GET DCB/DEB SECTION LENGTH
         IC    R3,SMF14NUC         GET NUMBER OF UCB SECTIONS
         IC    R4,SMF14SUC         GET SIZE OF EACH UCB SECTION
*
         LA    R8,SMF14SDC         SET POINTER JUST PAST HEADER
         USING SMF214RD,R8         ESTABLISH ADDRESSING
         MVC   DDNAME,SMFTIOE5     MOVE DDNAME
         MVC   DSNAME,JFCBDSNM     MOVE DSNAME
         MVC   NVOL,JFCBNVOL       MOVE NUMBER OF VOLUMES
         MVC   VOLS(30),JFCBVOLS   MOVE VOLUME SERIAL #S
         DROP  R7
*
         LA    R9,SMFDCBDE         GET POINTER TO DCB/DEB SECTION
         LA    R9,0(R2,R9)         POINT PAST DCB/DEB SECTION
         USING SMF14UCB,R9         ESTABLISH ADDRESSING
UCBLOOP  A     R5,SMFEXCP          ADD IN COUNT FOR THIS UCB
         LA    R9,0(R4,R9)         POINT TO NEXT UCB SECTION
         BCT   R3,UCBLOOP          AND LOOP
         ST    R5,EXCP             SAVE EXCP COUNT IN RECORD
         B     EXIT                AND THEN EXIT
         DROP  R8,R9,R12
         SPACE 2
*
*        THIS PART OF CODE WRITES WTO MESSAGE WHENEVER TYPE 21
*        RECORD INDICATES EXCESSIVE (>.005*SIOS) TEMP IO ERRORS.
*
         SPACE 1
CHECK21  CLI   5(R12),21           TYPE 21 RECORD
         BNE   CHECK40             NO - CHECK FOR TYPE 04/34/40
*
         LM    R2,R5,ZEROS         CLEAR REGS
         USING SMFTYP21,R12        ESTABLISH ADDRESSING
         IC    R2,SMF21TR          GET TEMP READS
         IC    R3,SMF21TW          GET TEMP WRITES
         ICM   R5,3,SMF21SIO       GET SIO COUNT
         D     R4,=F'500'          DIVIDE
         LA    R5,1(R5)            ADD ONE
         LA    R7,0(R2,R3)         TOTAL NUMBER OF TEMP ERRORS
         CR    R7,R5               TEMP ERRORS > SIO/500 + 1 ?
         BNH   EXIT                NO - WE DON'T NEED TO DO ANYTHING
*
         LA    R0,LENGTH           GET LENGTH OF AREA
       GETMAIN R,LV=(0)            GETMAIN
         ST    R13,4(R1)           SAVE AREA
         ST    R1,8(R13)           CHAINING
         LR    R13,R1              BASE FOR WORK AREA
         USING GETAREA,R13
*
         MVC   WTO1(WTOLEN1),WTOMSG1 MOVE WTO1 TO GETMAINED AREA
         MVC   WTO2(WTOLEN2),WTOMSG2 MOVE WTO2 TO GETMAINED AREA
         MVC   WTO3(WTOLEN3),WTOMSG3 MOVE WTO3 TO GETMAINED AREA
*
         MVC   VOL1(6),SMF21VOL    MOVE VOLUME SER
         MVC   VOL3(6),SMF21VOL    DITTO
*
         ICM   R7,3,SMF21CA        GET UNIT ADDRESS
         MVI   WORK+2,X'0C'        PUT IN SIGN
         STH   R7,WORK             STORE UNIT ADDRESS
         UNPK  WORK+3(5),WORK(3)   UNPACK DIGITS
         TR    WORK+4(3),HEXTABLE  CONVERT HEX TO CHARS
         MVC   UNIT1(3),WORK+4     MOVE TO MESSAGE
         MVC   UNIT3(3),WORK+4     MOVE TO MESSAGE
*
         ICM   R5,3,SMF21SIO       GET SIO COUNT
         CVD   R5,WORK             DECIMAL
         OI    WORK+7,X'0F'        FIX SIGN
         MVC   SIOS-1(6),=X'402020202120' MOVE IN EDIT MASK
         ED    SIOS-1(6),WORK+5    CONVERT TO CHARS
*
         CVD   R2,WORK             MAKE READ ERRORS DECIMAL
         OI    WORK+7,X'0F'        FIX SIGN
         MVC   READS-1(4),=X'40202120' MOVE IN EDIT MASK
         ED    READS-1(4),WORK+6   CONVERT TO CHARS
*
         CVD   R3,WORK             MAKE WRITE ERRORS DECIMAL
         OI    WORK+7,X'0F'        FIX SIGN
         MVC   WRITES-1(4),=X'40202120' MOVE IN EDIT MASK
         ED    WRITES-1(4),WORK+6  CONVERT TO CHARS
*
         WTO   MF=(E,WTO1)         WRITE WTO 1
         WTO   MF=(E,WTO2)         WRITE WTO 2
         WTO   MF=(E,WTO3)         WRITE WTO 3
*
         LR    R1,R13              UNCHAIN
         L     R13,4(R13)          SAVE AREAS
         LA    R0,LENGTH           GET LENGTH TO FREE
      FREEMAIN R,LV=(0),A=(1)      FREE STORAGE
         B     EXIT                THEN END
         DROP  R12
         SPACE 2
*
*        THIS PART OF CODE SAVES IO COUNTS FROM TYPE40 RECORDS
*        AND ADDS THEM IN TO THE IO COUNTS IN THE 04/34 RECORDS.
*
         SPACE 1
CHECK40  CLI   5(R12),40           TYPE 40 RECORD ?
         BNE   CHECK04             NO - CHECK FOR TYPE 04
         USING SMFTYP40,R12        ESTABLISH ADDRESSING
*
         CLI   TDDFLG,X'02'        DYNAMIC UNALLOCATION (ONLY)
         BNE   SUPRESS             NO - DON'T TOUCH ANYTHING
         SR    R6,R6               CLEAR FOR DIVIDE
         LH    R7,TDDVAR           GET LENGTH OF DEVICE ENTRY SECTION
         D     R6,=F'8'            GET COUNT OF DEVICES
         LTR   R7,R7               ANY THERE ?
         BZ    SUPRESS             NO - GET OUT
         L     R3,16               CVT POINTER
         L     R3,0(R3)            GET TCB WORDS
         L     R3,4(R3)            GET CURRENT TCB
         L     R3,X'A4'(R3)        GET TCT POINTER
         LTR   R3,R3               IS THERE ONE ?
         BZ    SUPRESS             NO - GET OUT
         L     R3,X'1C'(R3)        GET JMR POINTER
         USING JMR,R3              ESTABLISH ADDRESSING
*
         SR    R9,R9               CLEAR REG 9
         ICM   R9,7,JMRUCOM+1      PICK UP USER COMMUNICATION FIELD
         LTR   R9,R9               ANY THERE NOW ?
         BNZ   T40HAVE             YES BYPASS GETMAIN & INIT
         LA    R4,SMFWSIZ          GET SIZE OF WORKAREA
         A     R4,=F'7'            ROUND UP TO
         N     R4,=X'FFFFFFF8'     A DOUBLE WORD
       GETMAIN R,LV=(R4),SP=255    GETMAIN WORK AREA
         LR    R9,R1               SAVE POINTER
         USING SMFWORK,R9          SET UP ADDRESSING
         MVI   0(R9),X'0'          ZERO ONE
         MVC   1(SMFWSIZ-1,R9),0(R9)  ZERO IT ALL OUT
         STCM  R9,7,JMRUCOM+1      SAVE POINTER
*
T40HAVE  L     R6,T40SAVE          PICK UP ACCUMULATOR
         LA    R8,TDDEXCP          POINT TO FIRST DEVICE SECTION
T40LOOP  CLI   0(R8),X'20'         DISK UNIT ?
         BNE   *+12                NO - LOOK FOR TAPE
         A     R6,4(R8)            ADD EXCP COUNT TO BUCKET
         B     T40NEXT             AND GO GET NEXT DEVICE
         CLI   0(R8),X'80'         TAPE UNIT ?
         BNE   T40NEXT             NO - SKIP
         L     R1,4(R8)            GET TAPE EXCP COUNT
*
*        1984  ALGORITHM :
*
*        M     R0,=F'12272'        MULTIPLY BY K03 * BLKSTAPE * 10E5
*        D     R0,=F'12052'        DIVIDE BY K02 * BLKSDISK * 10E5
*
*        1985  ALGORITHM :
*
         M     R0,=F'95070'        MULTIPLY BY K03 * BLKSTAPE * 10E6
         D     R0,=F'18416'        DIVIDE BY K02 * BLKSDISK * 10E6
*
         AR    R6,R1               AND ADD TO ACCUMULATOR
T40NEXT  LA    R8,8(R8)            POINT TO NEXT DEVICE SECTION
         BCT   R7,T40LOOP          DO ALL DEVICES
         ST    R6,T40SAVE          SAVE ACCUMULATED VALUE
         B     SUPRESS             THEN END
         DROP  R3,R9,R12
*
CHECK04  CLI   5(R12),04           TYPE 04 RECORD ?
         BE    *+12                YES - GO UNLOAD I/O COUNTS
         CLI   5(R12),34           TYPE 34 RECORD ?
         BNE   CHECK250            NO - END
         USING SMFTYP04,R12        ESTABLISH ADDRESSING
*
         L     R3,16               CVT POINTER
         L     R3,0(R3)            GET TCB WORDS
         L     R3,4(R3)            GET CURRENT TCB
         L     R3,X'A4'(R3)        GET TCT POINTER
         LTR   R3,R3               IS THERE ONE ?
         BZ    EXIT                NO - GET OUT
         L     R3,X'1C'(R3)        GET JMR POINTER
         USING JMR,R3              ESTABLISH ADDRESSING
         SR    R9,R9               CLEAR REG 9
         ICM   R9,7,JMRUCOM+1      PICK UP USER COMMUNICATION FIELD
         LTR   R9,R9               ANY THERE NOW ?
         BZ    EXIT                NO - NOTHING TO REPORT
         USING SMFWORK,R9          SET UP ADDRESSING
         CLC   T40SAVE(4),=F'0'    ANYTHING TO REPORT ?
         BE    EXIT                NO - SKIP REST
*
         SR    R6,R6               CLEAR FOR DIVIDE
         LH    R7,SMF4LENN         GET LENGTH OF DEVICE ENTRY SECTION
         D     R6,=F'8'            GET COUNT OF DEVICES
         LTR   R7,R7               ANY THERE ?
         BZ    EXIT                NO - GET OUT
         LA    R8,SMF4DEVC         POINT TO FIRST DEVICE SECTION
T04LOOP  CLI   0(R8),X'20'         DISK UNIT ?
         BE    T04HIT              YES - UNLOAD HERE
         LA    R8,8(R8)            POINT TO NEXT DEVICE SECTION
         BCT   R7,T04LOOP          DO ALL DEVICES
         B     EXIT                NONE FOUND .....
T04HIT   A     R6,T40SAVE          ADD EXISTING TYPE 04 COUNT
*
         ST    R6,4(R8)            UPDATE TYPE04|34 EXCP COUNT
         MVC   T40SAVE(4),=F'0'    RESET T40SAVE FIELD
         B     EXIT                THEN END
         DROP  R3,R9,R12
*
CHECK250 CLI   5(R12),250          TYPE 250 RECORD ?
         BNE   EXIT                NO - END
*
ACFD     CLI   44(R12),C'D'        SUB TYPE 'D' RECORD ?
         BNE   ACFL                NO - CHECK FOR NEXT
         CLC   48(4,R12),=C'ADMP'  LOGON ID = 'ADMP****' ?
         BE    SUPRESS             YES - DUMP IT
         CLC   48(4,R12),=C'HSMS'  LOGON ID = 'HSMS****' ?
         BE    SUPRESS             YES - DUMP IT
         B     EXIT                NO - WRITE THE RECORD
*
ACFL     CLI   44(R12),C'L'        SUB TYPE 'L' RECORD ?
         BNE   ACFV                NO - CHECK FOR NEXT
         CLC   18(8,R12),=C'JES3    ' JES3 ?
         BE    SUPRESS             YES - SUPRESS IT
         CLC   18(8,R12),=C'MSTJCL00' MSTRJCL0 ?
         BE    SUPRESS             YES - SUPRESS IT
         B     EXIT                NO - WRITE IT
*
ACFV     CLI   44(R12),C'V'        SUB TYPE 'V' RECORD ?
         BNE   EXIT                NO - CHECK FOR NEXT
         CLC   136(4,R12),=C'RFLH' FLASHER RESOURCE ?
         BE    SUPRESS             YES - SUPRESS IT
*
*        COMMON EXIT CODE
*
         SPACE 1
EXIT     LM    R14,R12,12(R13)     RESTORE REGS
         SR    R15,R15             CLEAR REG TO WRITE RECORD
         BR    R14                 RETURN
         SPACE 1
SUPRESS  LM    R14,R12,12(R13)     RESTORE REGS
         LA    R15,4               DON'T WRITE THIS RECORD
         BR    R14                 RETURN
         SPACE 2
ZEROS    DC    4F'0'               ZEROS
*
WTOMSG1  WTO   'IGD083I TAPE ( VVVVVV ) ON UNIT ( UUU ) HAD EXCESSIVE T*
               EMPORARY IO ERRORS',ROUTCDE=(1,3,5,14),DESC=2,MF=L
WTOLEN1  EQU   *-WTOMSG1
*
WTOMSG2  WTO   'IGD083I ( SIO COUNT = NNNNN ; READ ERRORS = NNN ; WRITE*
                ERRORS = NNN )   ',ROUTCDE=(1,3,5,14),DESC=4,MF=L
WTOLEN2  EQU   *-WTOMSG2
*
WTOMSG3  WTO   'IGD083I TAKE APPROPRIATE ACTION FOR VOLUME ( VVVVVV ) O*
               R UNIT ( UUU )    ',ROUTCDE=(1,3,5,14),DESC=4,MF=L
WTOLEN3  EQU   *-WTOMSG3
         SPACE 1
HEXTABLE DC    240C'?'
         DC    C'0123456789ABCDEF'
         SPACE 1
         LTORG
         SPACE 1
TOTWORK  DSECT
SMFWORK  EQU   *
T40SAVE  DS    F                  TYPE 40 I/O COUNTER (FROM IEFU83)
         DS    F                  EXTRA WORD FOR FUTURE USE
C1ASAVE  DS    F                  TOTAL JOB TCB MILLISECONDS (C1A)
C1CSAVE  DS    F                  TOTAL JOB TAPE MOUNTS (C1C)
         DS    F                  EXTRA WORD FOR FUTURE USE
         DS    F                  EXTRA WORD FOR FUTURE USE
C1BSAVE  DS    D                  TOTAL JOB VMEM OCCUPANCY (C1B)
C1FSAVE  DS    D                  TOTAL JOB COMM (C1F) (TPUT,TGET)
C1JSAVE  DS    D                  TOTAL JOB I/O TIME (C1J) (DISK,TAPE)
         DS    D                  EXTRA DOUBLE WORD FOR FUTURE USE
         DS    D                  EXTRA DOUBLE WORD FOR FUTURE USE
SMFWSIZ  EQU   *-SMFWORK          LENGTH OF WORK AREA
         EJECT
SMFTYP14 DSECT
       IFASMFR 14
         EJECT
IEFJFCBN DSECT
      IEFJFCBN
         EJECT
SMFTYP21 DSECT
       IFASMFR 21
         EJECT
        IEFJMR
         EJECT
SMFTYP04 DSECT
       IFASMFR 04
         EJECT
SMFTYP40 DSECT
       IFASMFR 40
         EJECT
SMF214RD DSECT
DDNAME   DS    CL8
DSNAME   DS    CL44
EXCP     DS    F
NVOL     DS    AL1
VOLS     DS    5CL6
L214RD   EQU   (SMF14SDC-SMF14HDR)+(*-DDNAME)
         SPACE 2
GETAREA  DSECT
SAVEAREA DS    9D                  SAVEAREA
WORK     DS    D                   WORKAREA
WTO1     DS    CL(WTOLEN1)         WTO MESSAGE AREA
VOL1     EQU   WTO1+4+15
UNIT1    EQU   WTO1+4+34
WTO2     DS    CL(WTOLEN2)         WTO MESSAGE AREA
SIOS     EQU   WTO2+4+22
READS    EQU   WTO2+4+44
WRITES   EQU   WTO2+4+65
WTO3     DS    CL(WTOLEN3)         WTO MESSAGE AREA
VOL3     EQU   WTO3+4+45
UNIT3    EQU   WTO3+4+64
LENGTH   EQU   *-GETAREA           LENGTH OF GETMAINED AREA
         END
