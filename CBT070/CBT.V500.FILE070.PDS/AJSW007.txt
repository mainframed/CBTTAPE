++ USERMOD(AJSW007) /*

           PRINT JOB STATS ON JOB HEADER PAGE
                                                                        $
 CHANGE HISTORY:                                                        $
                                                                        $
 05/15/88 J.DAILEY          RE-STRUCTURED CODE FOR MORE SEQUENCE NUMBER $
                            SPACE. ADDED LABELS TO FIELDS IN THE LINES  $
                            MAKING THE JOB STATS BOX. ADDED THE ABILITY $
                            TO INSERT ROUTING INFORMATION FOR STARTED   $
                            TASK OUTPUT.                                $
                                                                        $
                                                                        $
                                             */.
++ VER (Z038) FMID(HJS2215).
++ SRCUPD(IATUX20) DISTLIB(AJES3SRC) .
./ CHANGE NAME=IATUX20
*        USER MODS =                                            AJSW007
*                                                               AJSW007
*             - JOB HEADER PAGE INFORMATION                     AJSW007
*                                                               AJSW007
*-------------------------------------------------------------* AJSW007
*        IATUX20 - FORMAT JOB STAT'S ON HEADER PAGE           * AJSW007
*-------------------------------------------------------------* AJSW007
         IATYJCT ,                                              AJSW007
         IATYJMR ,                                              AJSW007
*        MVC   4(8,R7),WTRDOTYP    MOVE IN THE TYPE             AJSW007
*        BAL   R9,UX20SUB          GO PRINT THE DATA            AJSW007
         B     WCUX20              DSD USERMOD ENTRY            AJSW007
WCUX20RT DS    0H                  RETURN POINT FROM DSD MOD    AJSW007
*UXPRTWK EQU   1360              PR.WORK AREA SIZE 5012@ZA10720 AJSW007
UXPRTWK  EQU   1688               *PR.WORK AREA SIZE            AJSW007
         TITLE 'WC-DSD USER MOD ''AJSW007'''                    AJSW007
**************************************************************  AJSW007
*                                                               AJSW007
*  START OF WC-DSD USERMOD                                      AJSW007
*                                                               AJSW007
**************************************************************  AJSW007
*                                                               AJSW007
*                                                               AJSW007
WCUX20   DS    0H                                               AJSW007
         LA    R2,20(R7)          *INITIALIZE ALL LINES         AJSW007
         LM    R3,R5,=A(GDLSIZE,GDLINES,GDLSIZE)                AJSW007
         MVCL  R2,R4                 ..                         AJSW007
         LA    R6,20(,R7)         -> DSD STATISTICS LINES       AJSW007
         USING GDLINES,R6         MAKE IT ADDRESSABLE           AJSW007
         IATXTOD ,                                              AJSW007
         MVC   GDL03HH,0(R1)      *TIME                         AJSW007
         MVC   GDL03MM,2(R1)         ..                         AJSW007
         MVC   GDL03SS,4(R1)         ..                         AJSW007
         IATXTOD BIN                                            AJSW007
         ST    R1,12(R7)          *DATE                         AJSW007
         MVI   FEB+3,28           *ASSUME NOT LEAP YEAR         AJSW007
         TM    13(R7),X'01'       *IS IT AN ODD YEAR?           AJSW007
         BC    ALLON,AJS001       *YES, SKIP FURTHER CHECKING   AJSW007
         TM    13(R7),X'12'       *IS IT A LEAP YEAR?           AJSW007
         BC    MIXED,AJS001       *NO, CONTINUE                 AJSW007
         MVI   FEB+3,29           *YES, SET FEBRUARY TO 29 DAYS AJSW007
AJS001   DS    0H                 *DATE                         AJSW007
         UNPK  GDL03YR1,12(4,R7)     ..                         AJSW007
         MVC   GDL03YR2,TVTBLANK     ..                         AJSW007
         MVC   8(6,R7),TVTZERO       ..                         AJSW007
         CVB   R3,8(R7)              ..                         AJSW007
         LA    R4,GDTABL             ..                         AJSW007
GDLOOKUP S     R3,0(R4)              ..                         AJSW007
         BNP   GDFND                 ..                         AJSW007
         CLC   GDTABLND,4(R4)        ..                         AJSW007
         BE    GDNOTFND              ..                         AJSW007
         LA    R4,8(R4)              ..                         AJSW007
         B     GDLOOKUP              ..                         AJSW007
GDFND    A     R3,0(R4)              ..                         AJSW007
         MVC   GDL03MO,4(R4)         ..                         AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         UNPK  GDL03DA,8(8,R7)       ..                         AJSW007
         OI    GDL03DA+(L'GDL03DA-1),X'F0'                      AJSW007
         B     GDGETJCT              ..                         AJSW007
GDNOTFND MVC   GDL03UNK,=CL7' UNK XX' .                         AJSW007
GDGETJCT PACK  8(8,R7),WTRDJNO    *GET THE JCT                  AJSW007
         CVB   R1,8(R7)              ..                         AJSW007
         IATXJCT TYPE=RW,JOBNUM=(R1),ERROR=GDPRINT  ..          AJSW007
         LR    R4,R1                                            AJSW007
         USING JCTSTART,R4                                      AJSW007
         OI    JCTFL8,JCTNCHNG                                  AJSW007
         MVI   0(R7),0            *NO GET THE JMR               AJSW007
         LA    R1,JCTJMRFD        *GET JMR FDB                  AJSW007
         USING FDBSTART,R1                                      AJSW007
         TM    FDBSPFL1,FDBONSP   *IS IT ON SPOOL?              AJSW007
         BC    ALLOFF,GDSETJMR    *YES, DONT READ IT            AJSW007
         DROP  R1                                               AJSW007
         MVI   0(R7),128                                        AJSW007
         JESREAD FDB=(R1),ID=JMR  *READ IN JMR                  AJSW007
GDSETJMR L     R5,JCTJMRFD        *FINISH UP HEADER LINE        AJSW007
         USING JMR,R5                                           AJSW007
         MVC   GDL03OR1,SMF26DEV  *ORIGIN                       AJSW007
         MVC   GDL03OR2,SMF26DEV  *ORIGIN                       AJSW007
         MVC   GDL03PG1,SMF26NAM  *PROGRAMMER NAME              AJSW007
         MVC   GDL03PG2,SMF26NAM  *PROGRAMMER NAME              AJSW007
         MVC   GDL07DJC,SMF26NET  *NETWORK ID                   AJSW007
         LR    R1,R5              *SAVE BASE                    AJSW007
         AH    R5,SMF26LN1        *POINT TO EVENT SECTION       AJSW007
         AH    R5,SMF26LN2        *POINT TO ACTUAL SECTION      AJSW007
         MVC   GDL09EPI,SMF26XID  *EXEC PROCESSOR ID            AJSW007
         LR    R5,R1              *RESTORE BASE                 AJSW007
         MVC   GDL11SJR,SMF26NJI  *FROM SYSTEM VIA NJP          AJSW007
         MVC   GDL13MC,SMF26MSG   *MSGCLASS                     AJSW007
         MVC   GDL07JC,SMF26CLS   *JOB CLASS                    AJSW007
         CLI   GDL07JC,X'40'         ..                         AJSW007
         BNE   GDBJCLAS              ..                         AJSW007
         MVC   GDL07JJC,SMF26CLN     ..                         AJSW007
GDBJCLAS DS    0H                                               AJSW007
         LR    R1,R5              *SAVE BASE                    AJSW007
         AH    R5,SMF26LN1        *POINT TO EVENT SECTION       AJSW007
         AH    R5,SMF26LN2        *POINT TO ACTUAL SECTION      AJSW007
         CLC   SMF26XID,TVTBLANK  *SEE IF JOB EXECUTED          AJSW007
         BNE   GDACTUAL           *BRANCH IF YES                AJSW007
         MVC   GDL09CR,BLANKS                                   AJSW007
         MVC   GDL11CP,BLANKS                                   AJSW007
         MVC   GDL13LP,BLANKS                                   AJSW007
         MVC   GDL07ETM,BLANKS                                  AJSW007
         B     GDEST                 ..                         AJSW007
GDACTUAL ICM   R3,15,SMF26ICD     *CARDS READ                   AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL09CR,12(R7)        ..                         AJSW007
         ICM   R3,15,SMF26XPU     *CARDS PUNCHED                AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL11CP,12(R7)        ..                         AJSW007
         ICM   R3,15,SMF26XLN     *LINES PRINTED                AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL13LP,12(R7)        ..                         AJSW007
         LR    R5,R1              *RESTORE BASE                 AJSW007
         AH    R5,SMF26LN1        *POINT TO EVENT SECTION       AJSW007
         ICM   R3,15,SMF26XPT     *EXECUTION TIME               AJSW007
         ICM   R2,15,SMF26XST        ..                         AJSW007
         SR    R3,R2                 ..                         AJSW007
         BNM   *+8                   ..                         AJSW007
         A     R3,=A(24*60*60*100)   ..                         AJSW007
         MH    R3,=H'10'             ..                         AJSW007
         SR    R2,R2                 ..                         AJSW007
         D     R2,=F'60'             ..                         AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL07ETM,12(R7)       ..                         AJSW007
GDEST    LR    R5,R1              *RESTORE BASE                 AJSW007
         ICM   R3,15,SMF26EPU     *ESTIMATED PUNCH              AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL09EP,12(R7)        ..                         AJSW007
         ICM   R3,15,SMF26ELN     *ESTIMATED LINES              AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL11EP,12(R7)        ..                         AJSW007
         ICM   R3,15,SMF26XTM     *ESTIMATED EXECUTION TIME     AJSW007
         BNZ   *+8                   ..                         AJSW007
         L     R3,=A(3*60)           ..                         AJSW007
         MH    R3,=H'1000'           ..                         AJSW007
         SR    R2,R2                 ..                         AJSW007
         D     R2,=F'60'             ..                         AJSW007
         CVD   R3,8(R7)              ..                         AJSW007
         ED    GDL13ECM,12(R7)       ..                         AJSW007
         CLI   0(R7),128          *RELEASE THE JMR              AJSW007
         BNE   GDRELJCT              ..                         AJSW007
         MVI   0(R7),0               ..                         AJSW007
         LA    R1,JCTJMRFD           ..                         AJSW007
         ARELEASE FDB=(R1)           ..                         AJSW007
GDRELJCT PACK  8(8,R7),WTRDJNO    *RELEASE THE JCT              AJSW007
         CVB   R1,8(R7)              ..                         AJSW007
         IATXJCT TYPE=REL,JOBNUM=(R1),ERROR=GDPRINT  ..         AJSW007
GDPRINT  DS    0H                                               AJSW007
         CLC   =C'STC ',GDL03OR1   OUTPUT FOR AN STC            AJSW007
         BC    EQ,PROCSTC          YES,CHECK SPECIAL ROUTING    AJSW007
         CLC   =C'JES5 ',WTRDJNAM   OUTPUT FOR JES5             AJSW007
         BC    EQ,JESJOB           YES, FIXED ROUTING           AJSW007
         CLC   =C'JES3 ',WTRDJNAM   OUTPUT FOR JES3             AJSW007
         BC    NE,NOROUTE          NO,CONTINUE                  AJSW007
*                                                               AJSW007
JESJOB   DS    0H                                               AJSW007
         MVC   GDL07ETM,BLANKS     CLEAR INFO FROM SMF FIELDS   AJSW007
         MVC   GDL09CR,BLANKS      FOR ALL JES3 JOBS            AJSW007
         MVC   GDL09EP,BLANKS             "                     AJSW007
         MVC   GDL11CP,BLANKS             "                     AJSW007
         MVC   GDL11EP,BLANKS             "                     AJSW007
         MVC   GDL13LP,BLANKS             "                     AJSW007
         MVC   GDL13ECM,BLANKS            "                     AJSW007
         MVC   GDL03OR1,WTRDJNAM   JOB NAME IS ORIGIN 1         AJSW007
         MVC   GDL03OR2,WTRDJNAM   JOB NAME IS ORIGIN 2         AJSW007
*                                                               AJSW007
PROCSTC  DS    0H                                               AJSW007
         ICM   R1,B'1111',STCREPA  -> STC ROUTE TABLE           AJSW007
         BNZ   HAVETAB             CONTINUE IF TABLE LOADED     AJSW007
         ALOAD EPLOC=STCRNAME      LOAD ROUTING TABLE.          AJSW007
         LR    R1,R0               COPY EPA TO WORK REG.        AJSW007
         ST    R1,STCREPA          SAVE EPA FOR NEXT STC        AJSW007
*                                                               AJSW007
HAVETAB  DS    0H                                               AJSW007
         USING STCTABLE,R1         ADDRESSABILITY               AJSW007
         L     R0,0(,R1)           NUMBER OF ENTRIES IN TABLE   AJSW007
         L     R1,4(,R1)           -> FIRST ENTRY IN TABLE      AJSW007
         SLR   R3,R3               CLEAR LENGTH REG.            AJSW007
*                                                               AJSW007
STCHECK  DS    0H                                               AJSW007
         IC    R3,STCJNL           GET LENGTH TO COMPARE        AJSW007
         EX    R3,STCCLC           STC TO BE ROUTED             AJSW007
         BC    EQ,STCMRI           YES,MOVE ROUTEING INFO       AJSW007
         LA    R1,STCTABL(,R1)     BUMP TO NEXT ENTRY           AJSW007
         BCT   R0,STCHECK          LOOP BACK TIL DONE.          AJSW007
         B     NOROUTE             DO NOT ROUTE                 AJSW007
*                                                               AJSW007
STCCLC   CLC   STCSTCJN(0),WTRDJNAM **** OBJECT OF EXECUTE      AJSW007
*                                                               AJSW007
STCMRI   DS    0H                                               AJSW007
         MVC   GDL03PG1,STCROUTE   COPY ROUTING INFORMATION     AJSW007
         MVC   GDL03PG2,STCROUTE   COPY ROUTING INFORMATION     AJSW007
         DROP  R1                                               AJSW007
*                                                               AJSW007
NOROUTE  DS    0H                                               AJSW007
         LA    R3,GDLENT#          NUMBER OF LINES TO CLEAN UP  AJSW007
         LR    R2,R6               -> FIRST LINE                AJSW007
*                                                               AJSW007
GDTRT    TR    0(GDL#,R2),TRTAB    REMOVE BINARY ZEROES         AJSW007
         LA    R2,GDL#(,R2)        NEXT LINE                    AJSW007
         BCT   R3,GDTRT            LOOP BACK FOR NEXT LINE      AJSW007
         LA    R3,GDLENT#          NUMBER OF LINES TO PRINT     AJSW007
         LR    R2,R6               -> FIRST LINE                AJSW007
         BAL   R9,UX20S150         PRINT OUR LITTLE BOX         AJSW007
         B     WCUX20RT           BACK TO THE MAIN LINE         AJSW007
         EJECT                                                  AJSW007
         LTORG ,                                                AJSW007
*                                                               AJSW007
BLANKS   DC    CL11' '             FIELD OF 11 BLANKS           AJSW007
*                                                               AJSW007
GDTABL   DC    A(31)              *JANUARY                      AJSW007
         DC    C'JAN'                ..                         AJSW007
FEB      DC    A(28)              *FEBRUARY                     AJSW007
         DC    C'FEB'                ..                         AJSW007
         DC    A(31)              *MARCH                        AJSW007
         DC    C'MAR'                ..                         AJSW007
         DC    A(30)              *APRIL                        AJSW007
         DC    C'APR'                ..                         AJSW007
         DC    A(31)              *MAY                          AJSW007
         DC    C'MAY'                ..                         AJSW007
         DC    A(30)              *JUNE                         AJSW007
         DC    C'JUN'                ..                         AJSW007
         DC    A(31)              *JULY                         AJSW007
         DC    C'JUL'                ..                         AJSW007
         DC    A(31)              *AUGUST                       AJSW007
         DC    C'AUG'                ..                         AJSW007
         DC    A(30)              *SEPTEMBER                    AJSW007
         DC    C'SEP'                ..                         AJSW007
         DC    A(31)              *OCTOBER                      AJSW007
         DC    C'OCT'                ..                         AJSW007
         DC    A(30)              *NOVEMBER                     AJSW007
         DC    C'NOV'                ..                         AJSW007
         DC    A(31)              *DECEMBER                     AJSW007
GDTABLND DC    C'DEC'                ..                         AJSW007
TRTAB    DC    256AL1(*-TRTAB)                                  AJSW007
         ORG   TRTAB                                            AJSW007
         DC    C' '                                             AJSW007
         ORG   ,                                                AJSW007
         EJECT                                                  AJSW007
STCRNAME DC    CL8'STCROUTE'       NAME OF STC ROUTING TABLE    AJSW007
STCREPA  DC    A(0)                EPA OF STC ROUTING TABLE     AJSW007
*                                                               AJSW007
STCTABLE DSECT ,                                                AJSW007
STCJNL   DS    AL1                 LENGTH OF THE STC JOBNAME    AJSW007
STCSTCJN DS    CL8                 STC JOBNAME                  AJSW007
STCROUTE DS    CL20                ROUTING INFORMATION          AJSW007
STCTABL  EQU   *-STCTABLE          LENGTH OF AN ENTRY           AJSW007
IATUX20  CSECT ,                                                AJSW007
         EJECT                                                  AJSW007
GDLINES  CSECT ,                                                AJSW007
GDL#     EQU   111                 LENGTH OF EACH LINE          AJSW007
GDL01    DC    C' ',(GDL#-1)C'*'                                AJSW007
*                                                               AJSW007
GDL02    DC    CL(GDL#-1)' *'                                   AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL03    DC    CL4' *'                                          AJSW007
GDL03OR1 DC    CL8' '                                           AJSW007
         DC    CL3' '                                           AJSW007
GDL03PG1 DC    CL20' '                                          AJSW007
         DC    CL8' '                                           AJSW007
GDL03MO  DC    CL3' '                                           AJSW007
         DC    C' '                                             AJSW007
GDL03DA  DC    CL2' '                                           AJSW007
         ORG   GDL03MO                                          AJSW007
GDL03UNK DS    0CL7                                             AJSW007
         ORG   ,                                                AJSW007
         DC    C', 19'                                          AJSW007
GDL03YR1 DS    0CL5                                             AJSW007
         DC    CL2' '                                           AJSW007
GDL03YR2 DC    CL3' '                                           AJSW007
GDL03HH  DC    CL2' '                                           AJSW007
         DC    C'.'                                             AJSW007
GDL03MM  DC    CL2' '                                           AJSW007
         DC    C'.'                                             AJSW007
GDL03SS  DC    CL2' '                                           AJSW007
         DC    CL12' '                                          AJSW007
GDL03PG2 DC    CL20' '                                          AJSW007
         DC    CL3' '                                           AJSW007
GDL03OR2 DC    CL8' '                                           AJSW007
         DC    C' *'                                            AJSW007
*                                                               AJSW007
GDL04    DC    CL(GDL#-1)' *'                                   AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL05    DC    C' ',(GDL#-1)C'*'                                AJSW007
*                                                               AJSW007
GDL06    DC    CL40' *'                                         AJSW007
         DC    CL30' *'                                         AJSW007
         DC    CL40' *'                                         AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL07    DC    CL5' *'                                          AJSW007
GDL07DJC DC    CL8' '                                           AJSW007
         DC    CL17' - DJC NETWORK ID'                          AJSW007
         DC    CL11' '                                          AJSW007
         DC    CL4'* '                                          AJSW007
GDL07JJC DC    CL8' '                                           AJSW007
         ORG   *-1                                              AJSW007
GDL07JC  DC    C' '                                             AJSW007
         DC    CL17' - JOB CLASS'                               AJSW007
         DC    CL3' *'                                          AJSW007
GDL07ETM DC    XL11'40206B2020214B20202040'                     AJSW007
         DC    CL26'- ELAPSED TIME IN MINUTES'                  AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL08    DC    CL40' *'                                         AJSW007
         DC    CL30' *'                                         AJSW007
         DC    CL40' *'                                         AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL09    DC    CL9' *'                                          AJSW007
GDL09EPI DC    CL4' '                                           AJSW007
         DC    CL27' - EXECUTION PROCESSOR ID'                  AJSW007
         DC    CL3' *'                                          AJSW007
GDL09CR  DC    XL11'40206B2020206B20202040'                     AJSW007
         DC    CL16'- CARDS READ'                               AJSW007
         DC    CL3' *'                                          AJSW007
GDL09EP  DC    XL11'40206B2020206B20202040'                     AJSW007
         DC    CL26'- ESTIMATED PUNCH'                          AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL10    DC    CL40' *'                                         AJSW007
         DC    CL30' *'                                         AJSW007
         DC    CL40' *'                                         AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL11    DC    CL5' *'                                          AJSW007
GDL11SJR DC    CL8' '                                           AJSW007
         DC    CL27' - SYSTEM JOB RECEIVED FROM'                AJSW007
         DC    CL3' *'                                          AJSW007
GDL11CP  DC    XL11'40206B2020206B20202040'                     AJSW007
         DC    CL16'- CARDS PUNCHED'                            AJSW007
         DC    CL3' *'                                          AJSW007
GDL11EP  DC    XL11'40206B2020206B20202040'                     AJSW007
         DC    CL26'- ESTIMATED PRINT'                          AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL12    DC    CL40' *'                                         AJSW007
         DC    CL30' *'                                         AJSW007
         DC    CL40' *'                                         AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL13    DC    CL12' *'                                         AJSW007
GDL13MC  DC    C' '                                             AJSW007
         DC    CL27' - MSGCLASS'                                AJSW007
         DC    CL3' *'                                          AJSW007
GDL13LP  DC    XL11'40206B2020206B20202040'                     AJSW007
         DC    CL16'- LINES PRINTED'                            AJSW007
         DC    CL3' *'                                          AJSW007
GDL13ECM DC    XL11'40206B2020214B20202040'                     AJSW007
         DC    CL26'- ESTIMATED CPU MINUTES'                    AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL14    DC    CL40' *'                                         AJSW007
         DC    CL30' *'                                         AJSW007
         DC    CL40' *'                                         AJSW007
         DC    C'*'                                             AJSW007
*                                                               AJSW007
GDL15    DC    C' ',(GDL#-1)C'*'                                AJSW007
*                                                               AJSW007
GDLENT#  EQU   (*-GDLINES)/GDL#   *NUMBER OF LINES IN TABLE     AJSW007
GDLSIZE  EQU   *-GDLINES          *SIZE OF TABLE IN BYTES       AJSW007
IATUX20  CSECT ,                  *BACK TO NORMAL CSECT         AJSW007
UMODNUM  DC    CL7'AJSW007'       *USER NUMBER                  AJSW007
