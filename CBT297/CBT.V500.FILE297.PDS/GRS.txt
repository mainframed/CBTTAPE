//&SYSUID.GRS JOB (GRS),'GRS',
//             CLASS=U,MSGCLASS=X,NOTIFY=&SYSUID.
//JESDS OUTPUT PAGEDEF=LDUP,FORMDEF=LDUP,JESDS=ALL,FORMS=3H25
//ASM     EXEC PGM=ASMA90,PARM='NODECK,OBJ,RENT'
//STEPLIB   DD DISP=SHR,DSN=SYS1.SASMMOD1
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=SYS1.MODGEN
//          DD DISP=SHR,DSN=ZTGP01.MJCUTIL.MACLIB         <=== $COMMON
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSPRINT  DD SYSOUT=*,OUTPUT=*.JESDS
//SYSLIN    DD DSN=&&OBJECT,SPACE=(CYL,(9,9)),UNIT=SYSDA,
//             DISP=(NEW,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FBS)
//SYSIN     DD *
         TITLE 'GRS - GRS ISPF Interface'
GRS      START 0
**********************************************************************
*                                                                    *
* Name:            GRS                                               *
*                                                                    *
* Description:     GRS ISPF Interface                                *
*                                                                    *
* Function:                                                          *
*   Provides an interactive view of the Global Resource              *
*   Serialization (GRS) queue utilizing the ISPF Dialog              *
*   Manager. A high level resource list is displayed based on        *
*   user specified selection criteria.  From the high level          *
*   resource list, individual resource details can be                *
*   accessed.                                                        *
*                                                                    *
* Installation:                                                      *
*   1) GRS PDS contains the following.                               *
*      Member    Type      Contents                                  *
*      ======    ====      ========                                  *
*      $COMMON   ASM/MACRO Common Macros                             *
*      GRS       ASM/JCL   GRS ISPF Interface                        *
*      GRS@PRIM  ISPPLIB   GRS ISPF Interface                        *
*      GRSMAJ    ISPPLIB   GRS ISPF Interface                        *
*      GRSP002   ISPPLIB   GRS ISPF Interface                        *
*      GRSP003   ISPPLIB   GRS ISPF Interface                        *
*                                                                    *
*   2) Assemble and link-edit GRS.  The assembly and linkedit        *
*   must both get a return code zero.                                *
*                                                                    *
*   3) The load module and panels need to be available on ISPLLIB    *
*   and ISPPLIB allocations respectively.  For the young at          *
*   heart, this can easily be accomplished via LIBDEF's in a         *
*   CLIST.                                                           *
*                                                                    *
*   PROC 0                                                           *
*   CONTROL MAIN MSG                                                 *
*   ISPEXEC LIBDEF ISPLLIB DATASET ID('ZTGP01.MJCUTIL.LINKLIB')      *
*   ISPEXEC LIBDEF ISPPLIB DATASET ID('ZTGP01.MJCUTIL.ISPPLIB')      *
*   ISPEXEC SELECT PGM(GRS)                                          *
*   ISPEXEC LIBDEF ISPLLIB                                           *
*   ISPEXEC LIBDEF ISPPLIB                                           *
*   EXIT                                                             *
*                                                                    *
* Contact:         Michael J. Cleary                                 *
*                  IBM Global Services                               *
*                  877-343-1849 (voice)                              *
*                  mailto:michaeljosephcleary@yahoo.com              *
*                  http://www.geocities.com/michaeljosephcleary/     *
*                                                                    *
* Abend codes:     None                                              *
*                                                                    *
* Addressing mode: 31                                                *
*                                                                    *
* ASC mode:        Primary                                           *
*                                                                    *
* Change Log:                                                        *
* 2007-03-15 - Version 1 Release 4 Modification 2                    *
*              RIBETCBF - TCB Abending flag removed in z/OS 1.6      *
*              Change default waiters to 0                           *
*              Change default propagate to NO                        *
*              Enhanced GQSCAN return code checking                  *
*              Add RIBDMIN RIB detail minimum length (64KB)          *
*              Add RIBDMAX RIB detail maximum length (256KB)         *
*              Add RIBLMIN RIB list minimum length (4MB)             *
*              Add RIBLMAX RIB list maximum length (20MB)            *
* 2005-10-19 - Version 1 Release 4 Modification 1                    *
* 2004-09-04 - Version 1 Release 4 Modification 0                    *
* 2003-08-29 - Version 1 Release 3 Modification 0                    *
*              Modified GQSCAN return code checking                  *
*              Modified selection criteria manipulation              *
*              Changed GRSSCSCO references from CL7 to CL8           *
*              Add message - No Unknown Major Names                  *
*              Add XSYS=NO as default to GQSCAN (GRSSCAN)            *
*              Move Major Enqueue Tabble to $COMMON                  *
* 1999-04-27 - Version 2 Release 6 Modification 0                    *
*                                                                    *
* Copyright:       Michael J. Cleary                                 *
*                                                                    *
* Control blocks:  Numerous                                          *
*                                                                    *
* CSECTs:          Numerous                                          *
*                                                                    *
* Dependencies:    None                                              *
*                                                                    *
* Disclaimer:      IBM neither expresses nor implies any warranty as *
*                  to the fitness of these computer programs for any *
*                  function.  The use of these programs or the       *
*                  results therefrom is entirely at the risk of the  *
*                  user.  Consequently, the user may modify these    *
*                  programs in any way they thinks fit.  These       *
*                  programs are Freeware and may be freely copied.   *
*                  They may be freely distributed to any other party *
*                  on condition that no inducement beyond reasonable *
*                  handling costs is offered or accepted by either   *
*                  side for such distribution.                       *
*                                                                    *
* DSECTs:          Numerous                                          *
*                                                                    *
* Entry points:    GRS                                               *
*                                                                    *
* Environment:     MVS/ESA, TSO, ISPF                                *
*                                                                    *
* Input:           Selection criteria                                *
*                                                                    *
* Key:             8                                                 *
*                                                                    *
* Language:        MVS 370 assembler                                 *
*                                                                    *
* Linkage:         Standard linkage for entry/exit                   *
*                                                                    *
* Location:        Private                                           *
*                                                                    *
* Macros:                                                            *
*   Name     Description                                             *
*   ======== ===========                                             *
*   $COMMON  Common Macros                                           *
*                                                                    *
* Messages:        Numerous                                          *
*                                                                    *
* Mode:            Task                                              *
*                                                                    *
* Module type:     Procedure                                         *
*                                                                    *
* Output:          GRS queue information                             *
*                                                                    *
* Parameter list:  None                                              *
*                                                                    *
* Patch label:     None                                              *
*                                                                    *
* Reason codes:    None                                              *
*                                                                    *
* Recovery:        None                                              *
*                                                                    *
* Reentrancy:      Reentrant                                         *
*                                                                    *
* Registers:                                                         *
*   Saved:         0-12,14-15 via SAVE(14,12)                        *
*   Restored:      0-12,14 via RETURN(14,12),RC=(15)                 *
*   Base:          11/12                                             *
*   Dynamic work:  10                                                *
*   Static work:   9                                                 *
*                                                                    *
* Return codes:                                                      *
*   Zero - Normal                                                    *
*                                                                    *
* Residence mode:  Any                                               *
*                                                                    *
* Savearea:        Dynamically obtained storage 4KB                  *
*                                                                    *
* Serialization:   None                                              *
*                                                                    *
* Size:            Approximately 32KB                                *
*                                                                    *
* State:           Problem                                           *
*                                                                    *
* Tables:          Major queue name table                            *
*                                                                    *
* User exits:      None                                              *
*                                                                    *
* Virtual Storage:                                                   *
*   RIB Detail 64KB-256KB in the extended private area               *
*   RIB List   4MB-20MB   in the extended private area               *
*                                                                    *
* Wait states:     None                                              *
*                                                                    *
* X-memory mode:   HASID=PASID=SASID                                 *
*                                                                    *
**********************************************************************
         TITLE '$COMMON - Common Macros'
         PRINT OFF                 Stop source and object printing
         COPY  $COMMON             Common Macros
         PRINT ON                  Resume source and object printing
         TITLE 'GRS - Mainline'
GRS      $ENTRY DWA=(GRSDWA,R10,GRSDWA#), Entry                        X
               SABNDRY=PAGE,SALENGTH=4096,                             X
               SWA=(GRSSWA,R9)
         GBLA  &GCOMMON            Common Macros Level
         AIF   (&GCOMMON GE 11).COMMONOK
         MNOTE 8,'$COMMON Macro Mismatch:  Required=11, Used=&GCOMMON'
         AGO   .COMMONOK
.COMMONOK      ANOP
RIBDMIN  EQU   (64*1024)           RIB detail minimum length (64KB)
RIBDMAX  EQU   (256*1024)          RIB detail maximum length (256KB)
RIBLMIN  EQU   (4*1024*1024)       RIB list minimum length (4MB)
RIBLMAX  EQU   (20*1024*1024)      RIB list maximum length (20MB)
         GBLC  &GVRM               Version Release Modification (v.r.m)
&GVRM    SETC '1.4.2'              Version Release Modification (v.r.m)
         CALL  GRSINIT             Initialization
         CLC   GRSMAXRC,=F'0'      OK ?
         BNE   GRSNOPRO            . No
         CALL  GRSPROC             Process
GRSNOPRO EQU   *
         CALL  GRSTERM             Termination
         L     R15,GRSMAXRC        Maximum return code
         B     GRSR                Exit
GRS      $EXIT                     Exit
         TITLE 'GRSEXPL - Explode'
GRSEXPL  $ENTRY                    Entry
         TBCREATE TABLE=GRS3,      Create table                        X
               NAMES=(GRS3JOB,GRS3SYS,GRS3USA,GRS3STA)
*
**       Save Selection Criteria
*
         LA    R0,GRSSCSAV         Selection criteria save address
         LA    R1,L'GRSSCSAV       Selection criteria save length
         LA    R14,GRSSCSCO        Selection criteria address
         LR    R15,R1              Selection criteria length
         MVCL  R0,R14              Save selection criteria
*
**       Current ?
*
         CLI   GRS2LC,C'C'         Current ?
         BE    EXPLSELC            . Yes
         L     R4,GRS2PTR          RIB extent pointer
         B     EXPLSELS
*
**       Process current
*
EXPLSELC EQU   *
         MVC   GRSSCMAJ,GRS2MAJ    Major
         MVC   GRSSCMIN,GRS2MIN    Minor
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRDA          RIB detail address
         ST    R15,W_GQSCAN+00     RIB detail address
         L     R15,GRSRDL          RIB detail length
         ST    R15,W_GQSCAN+04     RIB detail length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         CALL  GRSSCAN             Scan
         L     R15,GRSRIBC         RIB count
         LTR   R15,R15             RIB count ?
         BNZ   EXPLOK              . Yes
         MVC   ZEDSMSG,=CL24'Not Found'
         MVC   ZEDLMSG,=CL80'Specified resources not found'
         SETMSG MESSAGE=ISRZ001
         B     EXPLGRS3            Exit - RC=0
EXPLOK   L     R4,GRSRDA           RIB detail address
         USING RIB,R4              RIB area
         MVC   GRS2TO,RIBNTO       Tasks owning
         MVC   GRS2TWE,RIBNTWE     Tasks waiting exclusive
         MVC   GRS2TWS,RIBNTWS     Tasks waiting shared
EXPLSELS LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
EXPLEXT  EQU   *
         MVC   GRS3JOB,RIBEJBNM    Jobname
         MVC   GRS3SYS,RIBESYSN    Sysname
EXPLTYP0 EQU   *
         MVC   GRS3USA,=CL9'Shared' Shared
         TM    RIBERFLG,RIBETYPE   Type ?
         BO    EXPLTYPX            . Shared
         MVC   GRS3USA,=CL9'Exclusive'
EXPLTYPX EQU   *
EXPLSTA0 EQU   *
         MVC   GRS3STA,=CL8'Owner' Owner
         TM    RIBESFLG,RIBESTAT   Status ?
         BO    EXPLSTAX            . Owner
         MVC   GRS3STA,=CL8'Waiting'
EXPLSTAX EQU   *
         TBADD TABLE=GRS3          Add table entry
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,EXPLEXT          Process next extent, if any
*
**       Major Description
*
         LA    R15,MAJORNUM        Major table size
         L     R14,=A(TBLMAJOR)    Major table address
CHKMAJOR EQU    *
         CLC   GRS2MAJ,1(R14)      Major match ?
         BE    GOTMAJOR            . Yes
         LA    R14,MAJORLEN(R14)   Next major
         BCT   R15,CHKMAJOR        Process next major, if any
GOTMAJOR EQU   *
         MVC   GRSMAJOR,9(R14)     Major
         TBSORT TABLE=GRS3,        Sort table                          X
               FIELDS=(GRS3JOB,C,A,GRS3SYS,C,A)
         TBTOP TABLE=GRS3          Top of table
GRSP003  TBDISPL TABLE=GRS3,       Display table                       X
               PANEL=GRSP003
EXPLGRS3 EQU   *                   Exit
         TBEND TABLE=GRS3          Delete table
*
**       Restore Selection Criteria
*
         LA    R0,GRSSCSCO         Selection criteria address
         LA    R1,L'GRSSCSAV       Selection criteria length
         LA    R14,GRSSCSAV        Selection criteria save address
         LR    R15,R1              Selection criteria save length
         MVCL  R0,R14              Restore selection criteria
GRSEXPL  $EXIT                     Exit
         TITLE 'GRSINIT - Initialization'
GRSINIT  $ENTRY                    Entry
*
**       Initialize ISPF Environment
*
         ISPFINIT                  Initialize ISPF environment
*
**       Define ISPF Variables
*
         VDEFINE NAMES=(GRSLEVEL),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(GRSMAJOR),FORMAT=CHAR,LENGTH=32
         VDEFINE NAMES=(GRSRDLKB),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSRIBC),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSRIBS),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSRLLKB),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCJOB),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCMAJ),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCMIN),FORMAT=CHAR,LENGTH=255
         VDEFINE NAMES=(GRSSCOWN),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCPRO),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(GRSSCREQ),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCSCO),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCSYS),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCTYP),FORMAT=CHAR,LENGTH=7
         VDEFINE NAMES=(GRSSCWAI),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2FLAG),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(GRS2LC),FORMAT=CHAR,LENGTH=1
         VDEFINE NAMES=(GRS2MAJ),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS2MIN),FORMAT=CHAR,LENGTH=255
         VDEFINE NAMES=(GRS2PTR),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2SCO),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS2TO),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2TWE),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2TWS),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS3JOB),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3LC),FORMAT=CHAR,LENGTH=1
         VDEFINE NAMES=(GRS3STA),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3SYS),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3USA),FORMAT=CHAR,LENGTH=9
*
**       Obtain RIB List Storage
*
         STORAGE OBTAIN,LENGTH=(RIBLMAX,RIBLMIN),LOC=ANY
         LTR   R15,R15             OK ?
         BZ    INITRLOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Obtained'
         MVC   ZEDLMSG,=CL80'RIB List Storage Not Obtained'
         SETMSG MESSAGE=ISRZ001
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSINIT4            Exit
INITRLOK EQU   *
         ST    R0,GRSRLL           RIB list length
         ST    R1,GRSRLA           RIB list address
         L     R15,GRSRLL          RIB list length
         SRL   R15,10              Convert to KB
         ST    R15,GRSRLLKB        RIB list length (KB)
*
**       Obtain RIB Detail Storage
*
         STORAGE OBTAIN,LENGTH=(RIBDMAX,RIBDMIN),LOC=ANY
         LTR   R15,R15             OK ?
         BZ    INITRDOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Obtained'
         MVC   ZEDLMSG,=CL80'RIB Detail Storage Not Obtained'
         SETMSG MESSAGE=ISRZ001
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSINIT4            Exit
INITRDOK EQU   *
         ST    R0,GRSRDL           RIB detail length
         ST    R1,GRSRDA           RIB detail address
         L     R15,GRSRDL          RIB detail length
         SRL   R15,10              Convert to KB
         ST    R15,GRSRDLKB        RIB detail length (KB)
GRSINIT  $EXIT                     Exit
         TITLE 'GRSLIST - List'
GRSLIST  $ENTRY                    Entry
         TBCREATE TABLE=GRS2,      Create table                        X
               NAMES=(GRS2MAJ,GRS2MIN,GRS2SCO,GRS2TO,GRS2TWE,GRS2TWS,GRX
               S2FLAG,GRS2PTR)
         XC    GRSCSEL,GRSCSEL     Initialize selected count
         L     R7,GRSRIBC          RIB count
         L     R4,GRSRLA           RIB list address
         USING RIB,R4              RIB area
LISTRIB  EQU   *                   Process RIB
         ST    R4,GRS2PTR          RIB extent pointer
         MVI   GRSJBFND,C'Y'       Job found (default)
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTRIB2            . No
         MVI   GRSJBFND,C'N'       Job not found
LISTRIB2 EQU   *                   PROCESS RIB
         LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5               RIB address
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
         TM    RIBSCOPE,RIBSYS     System ?
         BNO   LISTSNO1            . No
         MVC   GRS2SCO,=CL8'System' . Yes
         B     LISTFLAG
LISTSNO1 EQU   *
         TM    RIBSCOPE,RIBSYSS    Systems ?
         BNO   LISTSNO2            . No
         MVC   GRS2SCO,=CL8'Systems' . Yes
         B     LISTFLAG
LISTSNO2 EQU   *
         TM    RIBSCOPE,RIBSTEP    Step ?
         BNO   LISTSNO3            . No
         MVC   GRS2SCO,=CL8'Step'  . Yes
         B     LISTFLAG
LISTSNO3 EQU   *
LISTFLAG EQU   *
         $FILL GRS2FLAG,C' '       Initialize
         TM    RIBERFLG,RIBEMC     Must complete ?
         BNO   LISTFNOM            . No
         MVI   GRS2FLAG+00,C'M'    Must complete
LISTFNOM EQU   *
         TM    RIBERFLG,RIBERESV   Reserve ?
         BNO   LISTFNOR            . No
         MVI   GRS2FLAG+01,C'R'    Reserve
LISTFNOR EQU   *
         TM    RIBERFLG,RIBERESC   Converted ?
         BNO   LISTFNOC            . No
         MVI   GRS2FLAG+02,C'C'    Converted
LISTFNOC EQU   *
         TM    RIBERFLG,RIBEAUTH   Authorized ?
         BNO   LISTFNOA            . No
         MVI   GRS2FLAG+03,C'A'    Authorized
LISTFNOA EQU   *
         MVC   GRS2MAJ,RIBQNAME    Major
         $FILL GRS2MIN,C' '        Clear minor
         SR    R15,R15             Minor length
         IC    R15,RIBRNMLN        Minor length
         $VLM  GRS2MIN,RIBRNAME,(R15) Minor
         MVC   GRS2TO,RIBNTO       Tasks owning
         MVC   GRS2TWE,RIBNTWE     Tasks waiting exclusive
         MVC   GRS2TWS,RIBNTWS     Tasks waiting shared
LISTEXT  EQU   *                   Process extent
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTJOB2            . No
         $FIND GRSSCJOB,C' '       Find first blank
         $VLC  GRSSCJOB,RIBEJBNM,(R15),DECREMENT=NO Match ?
         BNE   LISTJOB2            . No
         MVI   GRSJBFND,C'Y'       Job found
LISTJOB2 EQU   *
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,LISTEXT          Process next extent, if any
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTJOB3            . No
         CLI   GRSJBFND,C'Y'       Job found ?
         BNE   CONT4               . No
LISTJOB3 EQU   *
         TBADD TABLE=GRS2          Add table entry
         $INCR GRSCSEL             Increment selected count
CONT4    LR    R4,R6
         BCT   R7,LISTRIB          Process next RIB, if any
         L     R15,GRSCSEL         Selected count
         LTR   R15,R15             Any selected ?
         BNZ   LISTOK              . Yes
         MVC   ZEDSMSG,=CL24'Not Found'
         MVC   ZEDLMSG,=CL80'Specified resources not found'
         SETMSG MESSAGE=ISRZ001
         B     LISTTBEN            Exit
LISTOK   EQU   *
         L     R7,GRSRIBC          RIB count
         S     R4,GRSRLA           Determine RIB list address used
         SRL   R4,10               Convert to KB
         LA    R4,1(R4)            Round up
         ST    R4,GRSRIBS          RIB size
         TBSORT TABLE=GRS2,        Sort table                          X
               FIELDS=(GRS2MAJ,C,A,GRS2MIN,C,A)
         TBTOP TABLE=GRS2          Top of table
GRSP002  TBDISPL TABLE=GRS2,       Display table                       X
               PANEL=GRSP002,                                          X
               LC=(GRS2LC,C,GRSEXPL,S,GRSEXPL)
LISTTBEN EQU   *
         TBEND TABLE=GRS2          Delete table
GRSLIST  $EXIT                     Exit
         TITLE 'GRSMAJ - Major Audit'
GRSMAJ   $ENTRY                    Entry
*
**       Save Selection Criteria
*
         LA    R0,GRSSCSAV         Selection criteria save address
         LA    R1,L'GRSSCSAV       Selection criteria save length
         LA    R14,GRSSCSCO        Selection criteria address
         LR    R15,R1              Selection criteria length
         MVCL  R0,R14              Save selection criteria
*
**       Process Major Audit
*
         MVC   GRSSCSCO,=CL8'ALL'  Scope - All
         $FILL GRSSCSYS,C' '       System - Blank
         MVC   GRSSCPRO,=CL3'NO'   GRSPlex - Propagate
         MVC   GRSSCTYP,=CL7'BOTH' Type - Both reserves and enqueues
         $FILL GRSSCJOB,C' '       Jobname - Blank
         $FILL GRSSCMAJ,C' '       Major - Blank
         $FILL GRSSCMIN,C' '       Minor - Blank
         MVC   GRSSCREQ,=F'0'      Requestors - Zero
         MVC   GRSSCOWN,=F'0'      Owners - Zero
         MVC   GRSSCWAI,=F'0'      Waiters - Zero
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRLA          RIB list address
         ST    R15,W_GQSCAN+00     RIB list address
         L     R15,GRSRLL          RIB list length
         ST    R15,W_GQSCAN+04     RIB list length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         XC    GRSCMAJ,GRSCMAJ     Initailize Major count
         CALL  GRSSCAN             Scan
         TBCREATE TABLE=GRSMAJ,    Create table                        X
               KEYS=(GRSSCMAJ)
         XC    GRSCSEL,GRSCSEL     Initialize selected count
         L     R7,GRSRIBC          RIB count
         L     R4,GRSRLA           RIB list address
         USING RIB,R4              RIB area
MAJRIB   EQU   *                   Process RIB
         ST    R4,GRS2PTR          RIB extent pointer
         LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5               RIB address
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
         MVC   GRSSCMAJ,RIBQNAME   Major
*
**       Major Description
*
         LA    R15,MAJORNUM        Major table size
         L     R14,=A(TBLMAJOR)    Major table address
CHKMAJ   EQU   *
         CLC   GRSSCMAJ,1(R14)     Major match ?
         BE    GOTMAJ              . Yes
         LA    R14,MAJORLEN(R14)   Next major
         BCT   R15,CHKMAJ          Process next major, if any
         $INCR GRSCMAJ             Increment Major count
         TBADD TABLE=GRSMAJ        Add table entry
GOTMAJ   EQU   *
MAJEXT   EQU   *                   Process RIB
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,MAJEXT           Process next extent, if any
         LR    R4,R6
         BCT   R7,MAJRIB           Process next RIB, if any
         L     R15,GRSCMAJ         Major count
         LTR   R15,R15             Any ?
         BNZ   MAJOK               . Yes
         MVC   ZEDSMSG,=CL24'No Unknown Major Names'
         MVC   ZEDLMSG,=CL80'No unknown major enqueue names found'
         SETMSG MESSAGE=ISRZ001
         B     MAJTBEN             Continue
MAJOK    EQU   *
         TBSORT TABLE=GRSMAJ,      Sort table                          X
               FIELDS=(GRSSCMAJ,C,A)
         TBTOP TABLE=GRSMAJ        Top of table
         TBDISPL TABLE=GRSMAJ,     Display table                       X
               PANEL=GRSMAJ
MAJTBEN  EQU   *
         TBEND TABLE=GRSMAJ        Delete table
         $FILL ZCMD,C' '           Command line - blank
*
**       Restore Selection Criteria
*
         LA    R0,GRSSCSCO         Selection criteria address
         LA    R1,L'GRSSCSAV       Selection criteria length
         LA    R14,GRSSCSAV        Selection criteria save address
         LR    R15,R1              Selection criteria save length
         MVCL  R0,R14              Restore selection criteria
GRSMAJ   $EXIT                     Exit
         TITLE 'GRSPROC - Process'
GRSPROC  $ENTRY                    Entry
*
**       Default Values
*
         MVC   GRSSCSCO,=CL8'ALL'  Scope - All
         $FILL GRSSCSYS,C' '       System - Blank
         MVC   GRSSCPRO,=CL3'NO'   GRSPlex - Propagate
         MVC   GRSSCTYP,=CL7'BOTH' Type - Both reserves and enqueues
         $FILL GRSSCJOB,C' '       Jobname - Blank
         MVC   GRSSCMAJ,=CL8'SYSDSN' Major - SYSDSN
         $FILL GRSSCMIN,C' '       Minor - Blank
         MVC   GRSSCREQ,=F'0'      Requestors - Zero
         MVC   GRSSCOWN,=F'0'      Owners - Zero
         MVC   GRSSCWAI,=F'0'      Waiters - Zero
GRS@PRIM DISPLAY PANEL=GRS@PRIM,   Selection criteria                  X
               END=GRSPROC0,                                           X
               PC=(ZCMD(5),AUDIT,GRSMAJ)
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRLA          RIB list address
         ST    R15,W_GQSCAN+00     RIB list address
         L     R15,GRSRLL          RIB list length
         ST    R15,W_GQSCAN+04     RIB list length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         CALL  GRSSCAN             Scan
         C     R15,=XL4'00000000'  RC=00 Resource found ?
         BNE   NOTRC00             . No
         CALL  GRSLIST             List
         B     GRS@PRIM            Selection criteria
NOTRC00  EQU   *
*
**       Return Code / Reason Code
*
         LA    R15,RTNRSNNUM       Return / Reason table size
         L     R7,=A(RTNRSNTBL)    Return / Reason table address
CHKRTNRSN EQU   *
         CLC   GRSRTNRSNCD,0(R7)   Return / Reason match ?
         BE    GOTRTNRSN           . Yes
         LA    R7,RTNRSNLEN(R7)    Next Return / Reason
         BCT   R15,CHKRTNRSN       Process next Return / Reason, if any
GOTRTNRSN EQU  *
         MVC   ZEDSMSG(24),02(R7)   Short message
         MVC   ZEDLMSG+00(08),=CL8'RTNCD=x"' Return code
         $HEX  ZEDLMSG+08,GRSRTNRSNCD+00,LEN=1 Return code
         MVC   ZEDLMSG+10(10),=CL10'" RSNCD=x"' Reason code
         $HEX  ZEDLMSG+20,GRSRTNRSNCD+01,LEN=1 Reason code
         MVC   ZEDLMSG+22(02),=CL2'" ' Reason code
         MVC   ZEDLMSG+24(40),26(R7)   Long message
         SETMSG MESSAGE=ISRZ001
         B     GRS@PRIM            Selection criteria
GRSPROC  $EXIT                     Exit
         TITLE 'GRSSCAN - Scan'
GRSSCAN  $ENTRY                    Entry
*
**       Major name
*
         CLI   GRSSCMAJ,C' '       Major specified ?
         BE    MAJEND              . No
         LA    R15,GRSSCMAJ        Major address
         ST    R15,W_GQSCAN+08     Major address
         $FIND GRSSCMAJ,C' ',DECREMENT=NO Find first blank
         STCM  R15,B'0001',W_GQSCAN+47 Major length
         OC    W_GQSCAN+45(1),=BL1'00000010' Major length specified
MAJEND   EQU   *
*
**       Minor name
*
         CLI   GRSSCMIN,C' '       Minor specified ?
         BE    MINEND              . No
         LA    R15,GRSSCMIN        Minor address
         ST    R15,W_GQSCAN+12     Minor address
         $FIND GRSSCMIN,C' ',DECREMENT=NO Find first blank
         STCM  R15,B'0001',W_GQSCAN+46 Minor length
         OC    W_GQSCAN+45(1),=BL1'00000100' Generic minor
MINEND   EQU   *
*
**       Sysname
*
         CLI   GRSSCSYS,C' '       Sysname specified ?
         BE    SYSEND              . No
         LA    R15,GRSSCSYS        Sysname address
         ST    R15,W_GQSCAN+16     Sysname address
         OC    W_GQSCAN+45(1),=BL1'10000000' Sysname specified
SYSEND   EQU   *
*
**       Request count
*
         CLC   GRSSCREQ,=F'0'      Request count specified ?
         BE    REQEND              . No
         MVC   W_GQSCAN+24(4),GRSSCREQ Request count
         OC    W_GQSCAN+45(1),=BL1'01000000' Request count specified
REQEND   EQU   *
*
**       Owner count
*
         CLC   GRSSCOWN,=F'0'      Owner count specified ?
         BE    OWNEND              . No
         MVC   W_GQSCAN+28(4),GRSSCOWN Owner count
         OC    W_GQSCAN+45(1),=BL1'00100000' Owner count specified
OWNEND   EQU   *
*
**       Waitor count
*
         CLC   GRSSCWAI,=F'0'      Wait count specified ?
         BE    WAIEND              . No
         MVC   W_GQSCAN+32(4),GRSSCWAI Wait count
         OC    W_GQSCAN+45(1),=BL1'00010000' Wait count specified
WAIEND   EQU   *
*
**       Scope
*
         LA    R15,SCOPENUM        Scope table size
         LA    R14,SCOPETBL        Scope table address
CHKSCOPE EQU    *
         CLC   GRSSCSCO,0(R14)     Scope match ?
         BE    GOTSCOPE            . Yes
         LA    R14,SCOPELEN(R14)   Next scope
         BCT   R15,CHKSCOPE        Process next scope, if any
GOTSCOPE EQU   *
         NC    W_GQSCAN+44(1),=BL1'00000111' Clear scope
         OC    W_GQSCAN+44(1),8(R14) Set scope
*
**       Reserve
*
         CLI   GRSSCTYP,C'B'       Reserves and enqueues specified ?
         BE    RESEND              . Yes
         CLI   GRSSCTYP,C'R'       Reserve specified ?
         BE    RESRES              . No
         CLI   GRSSCTYP,C'E'       Enqueue specified ?
         BE    RESENQ              . Yes
         B     RESEND
RESRES   EQU   *
         OC    W_GQSCAN+44(1),=BL1'00000010' Reserve specified
         B     RESEND
RESENQ   EQU   *
         OC    W_GQSCAN+44(1),=BL1'00000100' Enqueue specified
         B     RESEND
RESEND   EQU   *
*
**       GRSPlex
*
         CLI   GRSSCPRO,C'Y'       GRSPlex Yes specified ?
         BE    PLXYES              . Yes
         CLI   GRSSCPRO,C'N'       GRSPlex No specified ?
         BE    PLXNO               . Yes
         B     PLXNO               Default to No
PLXYES   EQU   *
         NC    W_GQSCAN+45(1),=BL1'00000001' Set GRSPlex Yes specified
         B     PLXEND
PLXNO    EQU   *
         OC    W_GQSCAN+45(1),=BL1'00000001' Set GRSPlex No specified
         B     PLXEND
PLXEND   EQU   *
*
**       Issue GQSCAN
*
         GQSCAN MF=(E,W_GQSCAN)    GQSCAN
         ST    R1,GRSRIBC          RIB count
         ST    R15,GRSRTNCD        GQSCAN Return Code
         CLI   GRSRTNCD+3,X'00'    Return code 00 ?
         BE    GRSNORSN            . Yes, skip reason code
         CLI   GRSRTNCD+3,X'04'    Return code 04 ?
         BE    GRSNORSN            . Yes, skip reason code
         CLI   GRSRTNCD+3,X'08'    Return code 08 ?
         BE    GRSNORSN            . Yes, skip reason code
         CLI   GRSRTNCD+3,X'10'    Return code 10 ?
         BE    GRSNORSN            . Yes, skip reason code
         CLI   GRSRTNCD+3,X'14'    Return code 14 ?
         BE    GRSNORSN            . Yes, skip reason code
         ST    R0,GRSRSNCD         GQSCAN Reason Code
GRSNORSN EQU   *
         MVC   GRSRTNRSNCD+0(1),GRSRTNCD+3 GQSCAN Return Code
         MVC   GRSRTNRSNCD+1(1),GRSRSNCD+3 GQSCAN Reason Code
         B     GRSSCANR            Exit - RC=R15
GRSSCAN  $EXIT                     Exit
         TITLE 'GRSTERM - Termination'
GRSTERM  $ENTRY                    Entry
*
**       Terminate ISPF Environment
*
         ISPFTERM                  Terminate ISPF environment
*
**       Release RIB List Storage
*
         L     R0,GRSRLL           RIB list length
         STORAGE RELEASE,ADDR=GRSRLA,LENGTH=(0) Release RIB list
         LTR   R15,R15             OK ?
         BZ    TERMRLOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Released'
         MVC   ZEDLMSG,=CL80'RIB List Storage Not Released'
         SETMSG MESSAGE=ISRZ001
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSTERM4            Exit
TERMRLOK EQU   *
*
**       Release RIB Detail Storage
*
         L     R0,GRSRDL           RIB detail length
         STORAGE RELEASE,ADDR=GRSRDA,LENGTH=(0) Release RIB detail
         LTR   R15,R15             OK ?
         BZ    TERMRDOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Released'
         MVC   ZEDLMSG,=CL80'RIB Detail Storage Not Released'
         SETMSG MESSAGE=ISRZ001
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSTERM4            Exit
TERMRDOK EQU   *
GRSTERM  $EXIT                     Exit
         TITLE 'GRSDWA - Dynamic Workarea'
GRSDWA   DSECT                     Dynamic Workarea
GRSCSEL  DS    F                   Count - Selected
GRSJBFND DS    CL001               Job found
GRSCMAJ  DS    F                   Major count
GRSMAJOR DS    CL032               Major Description
GRSRDA   DS    F                   RIB detail adress
GRSRDL   DS    F                   RIB detail length
GRSRDLKB DS    F                   RIB detail length (KB)
GRSRIBC  DS    F                   RIB count
GRSRIBS  DS    F                   RIB size
GRSMAXRC DS    F                   Maximum Return Code
GRSRLA   DS    F                   RIB list address
GRSRLL   DS    F                   RIB list length
GRSRLLKB DS    F                   RIB list length (KB)
GRSTOKEN DS    F                   GQSCAN Token
GRSRTNCD DS    F                   GQSCAN Return Code
GRSRSNCD DS    F                   GQSCAN Reason Code
GRSRTNRSNCD DS CL002               GQSCAN Return / Reason Code
*
**       Selection Criteria
*
GRSSCSCO DS    CL008               Selection Criteria - Scope
GRSSCSYS DS    CL008               Selection Criteria - System
GRSSCPRO DS    CL003               Selection Criteria - GRSPlex
GRSSCTYP DS    CL007               Selection Criteria - Type
GRSSCJOB DS    CL008               Selection Criteria - Job
GRSSCMAJ DS    CL008               Selection Criteria - Major
GRSSCMIN DS    CL255               Selection Criteria - Minor
GRSSCREQ DS    F                   Selection Criteria - Requestors
GRSSCOWN DS    F                   Selection Criteria - Owners
GRSSCWAI DS    F                   Selection Criteria - Waiters
GRSSCSAV DS    CL(*-GRSSCSCO)      Selection Criteria - Save
*
**
*
GRS2CRP  DS    CL008               ISPF Current Row Pointer
GRS2FLAG DS    CL004               List - Flags
GRS2LC   DS    CL001               ISPF Line Command
GRS2MAJ  DS    CL008               List - Major
GRS2MIN  DS    CL255               List - Minor
GRS2PTR  DS    F                   List - RIB extent pointer
GRS2SCO  DS    CL008               List - Scope
GRS2SEL  DS    CL004               ISPF Selections
GRS2TO   DS    F                   List - Tasks owning
GRS2TWE  DS    F                   List - Tasks waiting exclusive
GRS2TWS  DS    F                   List - Tasks waiting shrared
GRS3CRP  DS    CL008               ISPF Current Row Pointer
GRS3JOB  DS    CL008               Detail - Job
GRS3LC   DS    CL001               ISPF Line Command
GRS3SELS DS    CL004               ISPF Selections
GRS3STA  DS    CL008               Detail - Status
GRS3SYS  DS    CL008               Detail - System
GRS3USA  DS    CL009               Detail - Usage
         DS    0F                  Fullword alignment
W_GQSCAN EQU   *                   GQSCAN parameter list
         ORG   *+L_GQSCAN          GQSCAN parameter list length
         ISPFVARS                  Define ISPF variables
GRSDWA#  EQU   *-GRSDWA            Dynamic workarea length
         TITLE 'GRSSWA - Static Workarea'
GRSSWA   $ENTRY BASE=,DWA=,MODID=NO,SAVEMETH=NO,SWA=
T_GQSCAN GQSCAN QUIT=NO,REQLIM=MAX,MF=L GQSCAN list
L_GQSCAN EQU   *-T_GQSCAN          GQSCAN length
GRSLEVEL DC    CL005'&GVRM'        Level
RTNRSNTBL EQU *
 DC X'0400',CL24'Not Found',CL80'No resources matched request'
RTNRSNLEN EQU  *-RTNRSNTBL         Scope table entry length
 DC X'0800',CL24'RIB Area Full',CL80'AREA parameter too small'
 DC X'0A04',CL24'Parameter Error',CL80'GRS address space not active'
 DC X'0A08',CL24'Parameter Error',CL80'AREA parameter too small'
 DC X'0A0C',CL24'Parameter Error',CL80'Mutually exclusive arguments'
 DC X'0A10',CL24'Parameter Error',CL80'Local lock error'
 DC X'0A14',CL24'Parameter Error',CL80'RESNAME parameter invalid'
 DC X'0A18',CL24'Parameter Error',CL80'ASID parameter invalid'
 DC X'0A1C',CL24'Parameter Error'
 DC         CL80'REQCNT mutually exclusive with OWNERCNT/WAITCNT'
 DC X'0A20',CL24'Parameter Error',CL80'SCOPE parameter invalid'
 DC X'0A28',CL24'Parameter Error',CL80'Storage protect key error'
 DC X'0A2C',CL24'Parameter Error',CL80'TOKEN parameter invalid'
 DC X'0A30',CL24'Parameter Error',CL80'Authorization error'
 DC X'0A34',CL24'Parameter Error'
 DC         CL80'QUIT=YES requires TOKEN parameter'
 DC X'0A38',CL24'Parameter Error',CL80'CMS lock error'
 DC X'0A3C',CL24'Parameter Error',CL80'Other lock error'
 DC X'0A40',CL24'Parameter Error',CL80'SRB mode error'
 DC X'0A44',CL24'Parameter Error',CL80'REQLIM parameter invalid'
 DC X'0A48',CL24'Parameter Error',CL80'REQCNT parameter invalid'
 DC X'0A4C',CL24'Parameter Error',CL80'OWNERCT parameter invalid'
 DC X'0A50',CL24'Parameter Error',CL80'WAITCNT parameter invalid'
 DC X'0A58',CL24'Parameter Error',CL80'MLACTIVE error'
 DC X'0C00',CL24'System Error',CL80'Unrecoverable error'
 DC X'0C04',CL24'System Error',CL80'Resuming ring/star error'
 DC X'0C08',CL24'System Error',CL80'Storage obtain error'
 DC X'0C0C',CL24'System Error',CL80'Sysplex error'
 DC X'0C10',CL24'System Error',CL80'Migrating ring/star error'
 DC X'0C14',CL24'System Error',CL80'Inconsistent data error'
 DC X'1000',CL24'Program Error',CL80'Specified system unknown'
 DC X'1400',CL24'Environmental Error',CL80'Environmental Error'
RTNRSNNUM EQU  (*-RTNRSNTBL)/RTNRSNLEN Scope table entry count
 DC X'0000',CL24'Unknown Error',CL80'Unknown Error'
SCOPETBL DC    CL008'STEP    ',BL1'10000000'
SCOPELEN EQU   *-SCOPETBL          Scope table entry length
         DC    CL008'SYSTEM  ',BL1'01000000'
         DC    CL008'SYSTEMS ',BL1'00100000'
         DC    CL008'ALL     ',BL1'11100000'
SCOPENUM EQU   (*-SCOPETBL)/SCOPELEN Scope table entry count
         DC    CL008'ALL     ',BL1'11100000' Default
*
**       Hexadecimal Table
*
TBLHEX   CSECT
         DC    256X'00'            Assume all characters are invalid
         ORG   TBLHEX+X'F0'        0 is valid
         DC    1X'F0'              0 is valid
         ORG   TBLHEX+X'F1'        1 is valid
         DC    1X'F1'              1 is valid
         ORG   TBLHEX+X'F2'        2 is valid
         DC    1X'F2'              2 is valid
         ORG   TBLHEX+X'F3'        3 is valid
         DC    1X'F3'              3 is valid
         ORG   TBLHEX+X'F4'        4 is valid
         DC    1X'F4'              4 is valid
         ORG   TBLHEX+X'F5'        5 is valid
         DC    1X'F5'              5 is valid
         ORG   TBLHEX+X'F6'        6 is valid
         DC    1X'F6'              6 is valid
         ORG   TBLHEX+X'F7'        7 is valid
         DC    1X'F7'              7 is valid
         ORG   TBLHEX+X'F8'        8 is valid
         DC    1X'F8'              8 is valid
         ORG   TBLHEX+X'F9'        9 is valid
         DC    1X'F9'              9 is valid
         ORG   TBLHEX+X'FA'        A is valid
         DC    1X'C1'              A is valid
         ORG   TBLHEX+X'FB'        B is valid
         DC    1X'C2'              B is valid
         ORG   TBLHEX+X'FC'        C is valid
         DC    1X'C3'              C is valid
         ORG   TBLHEX+X'FD'        D is valid
         DC    1X'C4'              D is valid
         ORG   TBLHEX+X'FE'        E is valid
         DC    1X'C5'              E is valid
         ORG   TBLHEX+X'FF'        F is valid
         DC    1X'C6'              F is valid
         TITLE 'Major Enqueue Table'
TBLMAJOR $ENTRY BASE=,DWA=,MODID=NO,SAVEMETH=NO,SWA=
         TBLMAJOR                  Major Enqueue Table
         TITLE 'Resource Information Block (RIB)'
RIB      DSECT                     Resource Information Block (RIB)
RIB      ISGRIB                    Resource Information Block (RIB)
         END
//LINK    EXEC PGM=HEWL,PARM='MAP,LET,LIST',COND=(0,NE)
//SYSLIN    DD DSN=&&OBJECT,DISP=(OLD,DELETE)
//SYSLMOD   DD DISP=SHR,DSN=ZTGP01.MJCUTIL.LINKLIB(GRS) <= DESTINATION
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSPRINT  DD SYSOUT=*,OUTPUT=*.JESDS
