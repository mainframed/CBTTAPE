**********************************************************************
*                                                                    *
* $COMMON      Common macros                                         *
*                                                                    *
**********************************************************************
*                                                                    *
* Name         Description                                           *
* ========     ================                                      *
* $ADD         Add                                                   *
* $ADDRESS     Address macro operands                                *
* $CVD         Convert to decimal                                    *
* $DECR        Decrement                                             *
* $ENTRY       Standard Linkage - Entry                              *
* $EXIT        Standard Linkage - Exit                               *
* $FILL        Fill a Field                                          *
* $FIND        Find character                                        *
* $HEX         Hex                                                   *
* $INCR        Increment                                             *
* $STRIP       Strip character                                       *
* $VLC         Variable length compare                               *
* $VLM         Variable length move                                  *
* $VLT         Variable Length Translate and Test                    *
* ADDPOP       Start Pop-Up Window Mode                              *
* BRIF         Browse interface                                      *
* BROWSE       Browse a dataset                                      *
* CONTROL      Set processing modes                                  *
* DISPLAY      Display panels and messages                           *
* DSINFO       Data Set Information Dialog Service                   *
* EDIT         Edit a dataset                                        *
* IEEMBRDS     SMF RDS                                               *
* IEEMBSST     SMF Selectivity Table                                 *
* IHALLT       LNKLST Table (LLT)                                    *
* IHALPAT      LPALST Table (LPAT)                                   *
* IKJEFTSR     TSO/E Service Facility Routine                        *
* IKJZT301     TSO/E Broadcast Data Set Record 1                     *
* IKJZT302     TSO/E Broadcast Notices Directory Record              *
* IKJZT303     TSO/E Broadcast Notices Message Record                *
* IKJZT304     TSO/E Broadcast Mail Directory Record                 *
* IKJZT305     TSO/E Broadcast Mail Message Record                   *
* ISPFINIT     Initialize ISPF Environment                           *
* ISPFTERM     Terminate ISPF Environment                            *
* ISPFVARS     Define ISPF Variables                                 *
* LMCLOSE      Close a Data Set                                      *
* LMDFREE      Free a Data Set List ID                               *
* LMDINIT      Initialize a Data Set List                            *
* LMDLIST      List Data Sets                                        *
* LMFREE       Free Data Set from its Association with Data ID       *
* LMINIT       Generate a Data ID for a Data Set                     *
* LMMLIST      List a Library's Members                              *
* LMOPEN       Open a Data Set                                       *
* LOG          Write a message to the log data set                   *
* PQUERY       Obtain panel information                              *
* REMPOP       Remove a Pop-Up Window                                *
* SELECT       Select a panel or function                            *
* SETMSG       Set next message                                      *
* TBADD        Add a row to a table                                  *
* TBBOTTOM     Set the row pointer to the bottom                     *
* TBCLOSE      Close and save a table                                *
* TBCREATE     Create a new table                                    *
* TBDELETE     Delete a row from a table                             *
* TBDISPL      Display table information                             *
* TBEND        Close a table without saving                          *
* TBERASE      Erase a table                                         *
* TBEXIST      Determine whether a row exists in a table             *
* TBGET        Retrieve a row from a table                           *
* TBLMAJOR     Major Enqueue Table                                   *
* TBMOD        Modify a row from table                               *
* TBOPEN       Open a table                                          *
* TBPUT        Update a row from table                               *
* TBQUERY      Obtain table information                              *
* TBSARG       Define a search argument                              *
* TBSAVE       Save a table                                          *
* TBSCAN       Search a table                                        *
* TBSKIP       Move the row pointer                                  *
* TBSORT       Sort a table                                          *
* TBTOP        Set the row pointer to the top                        *
* TBVCLEAR     Clear table variables                                 *
* VCOPY        Create a copy of a variable                           *
* VDEFINE      Define function variables                             *
* VDELETE      Remove a definition of function variables             *
* VGET         Retrieve variables from a pool or profile             *
* VMASK        Mask and Edit Processing                              *
* VPUT         Update variables in the shared or profile pool        *
* VREPLACE     Replace a variable                                    *
* VRESET       Reset function variables                              *
*                                                                    *
**********************************************************************
*                                                                    *
* Name     Syntax                                                    *
* ======== ================                                          *
* $ADD     To,From                                                   *
* $ADDRESS Address macro operands                                    *
* $CVD     To,From,mask                                              *
* $DECR    Operand                                                   *
* $ENTRY   Standard Linkage - Entry                                  *
* $EXIT    Standard Linkage - Exit                                   *
* $FILL    Field,Immediate_Character                                 *
* $FIND    Find character                                            *
* $HEX     To,From,Length                                            *
* $INCR    Operand                                                   *
* $STRIP   Field,Immediate_Character                                 *
* $VLC     Variable length compare                                   *
* $VLM     Variable length move                                      *
* $VLT     Variable Length Translate and Test                        *
* BRIF     DATA=,RECFM=,LRECL=,READ=0,COMMAND=0,DIALOG=,PANEL=,      *
*          FORMAT=,MIXED=NO,ERRET=                                   *
* BROWSE   DATASET=                                                  *
* CONTROL  CTL1,CTL2,CTL3,CTL4                                       *
* DISPLAY  PANEL=,MESSAGE=,CURSOR=,CSRPOS=,COMMAND=,RETBUFFR=,       *
*          RETLGTH=,MSGLOC=                                          *
* EDIT     DSNAME,VOLSER                                             *
* IHALLT                                                             *
* IHALPAT                                                            *
* IKJEFTSR                                                           *
* IKJZT301                                                           *
* IKJZT302                                                           *
* IKJZT303                                                           *
* IKJZT304                                                           *
* IKJZT305                                                           *
* ISPFINIT ZDL=YES/NO,ZDS=YES/NO,ZERR=YES/NO                         *
* ISPFTERM                                                           *
* ISPFVARS ZDL=YES/NO,ZDS=YES/NO,ZERR=YES/NO                         *
* LMCLOSE                                                            *
* LMDFREE                                                            *
* LMDINIT                                                            *
* LMDLIST                                                            *
* LMFREE                                                             *
* LMINIT                                                             *
* LMMLIST                                                            *
* LMOPEN                                                             *
* LOG      MESSAGE=                                                  *
* PQUERY   PLF2P2,ZDATA                                              *
* SELECT   PANEL(PPPP),OPT(OOOO)                                     *
* SELECT   CMD(CCCC),LANG(LLLL),MODE(MMMM)                           *
* SELECT   PGM(PPPP),MODE(MMMM)                                      *
* SELECT   * NEWAPPL(AAAA),PASSLIB/NEWPOOL,SUSPEND                   *
* SETMSG   MESSAGE=,COND=,MSGLOC=                                    *
* TBADD    TABLE=,SAVE=(),ORDER=,MULT=1                              *
* TBBOTTOM TABLE=                                                    *
* TBCLOSE  TABLE=                                                    *
* TBCREATE TABLE=,KEYS=(),NAMES=(),WRITE=,REPLACE=,LIBRARY=,SHARE=   *
* TBDELETE TABLE=                                                    *
* TBDISPL  TABLE=,PANEL=,MESSAGE=,CURSOR=,CSRROW=,CSRPOS=,AUTOSEL=   *
*          POSITION=,ROWID=,MSGLOC,LC=(V,VALUE,MODULE)               *
* TBEND    TABLE=                                                    *
* TBERASE  TABLE=,LIBRARY=ISPTABL                                    *
* TBEXIST  TABLE=                                                    *
* TBGET    TABLE=                                                    *
* TBLMAJOR                                                           *
* TBMOD    TABLE=,SAVE=(),ORDER=                                     *
* TBOPEN   TABLE=,WRITE=WRITE,LIBRARY=ISPTABL,SHARE=                 *
* TBPUT    TABLE=                                                    *
* TBQUERY  TABLE=,KEYS(V),NAMES(V),ROWNUM(V),KEYNUM(V),NAMENUM(V),   *
*          POSITION(V)                                               *
* TBSARG   TABLE=,ARGLIST=(),MODE=NEXT,NAMECOND=()                   *
* TBSAVE   TABLE=                                                    *
* TBSCAN   TABLE=,ARGLIST=(),SAVENAME=,ROWID=,MODE=NEXT,NOREAD=      *
*          POSITION=,CONDLIST=()                                     *
* TBSKIP   TABLE=,NUMBER=1                                           *
* TBSORT   TABLE=,FILEDS=()                                          *
* TBTOP    TABLE=                                                    *
* TBVCLEAR TABLE=                                                    *
* VCOPY    NAMES=(),LENGTH=8,VARIABLE=,MODE=                         *
* VDEFINE  NAMES=(),VARIABLE=,FORMAT=CHAR,LENGTH=8,OPTIONS=,USERDATA=*
* VDELETE  NAMES=()                                                  *
* VGET     NAMES=(),POOL=                                            *
* VMASK    NAMES=(),FORMAT=,USER=,LENGTH=                            *
* VPUT     NAMES=(),POOL=                                            *
* VREPLACE VARIABLE-NAME,FULL-WORD-LENGTH,DATA-AREA                  *
* VRESET                                                             *
*                                                                    *
**********************************************************************
         MACRO
**********************************************************************
* $ADD         Add                                                   *
**********************************************************************
&LABEL   $ADD  &TO,&FROM
         AIF   (T'&TO EQ 'F').FULL
         AIF   (T'&TO EQ 'H').HALF
         AIF   (T'&TO EQ 'P').PACKED
         MNOTE 8,'$ADD error:  Unknown operand - &TO'
         AGO   .MEND
.FULL    ANOP
&LABEL   L     R15,&TO             Get current value
         A     R15,&FROM           Add value
         ST    R15,&TO             Save new value
         AGO   .MEND
.HALF    ANOP
&LABEL   LH    R15,&TO             Get current value
         AH    R15,&FROM           Add value
         STH   R15,&TO             Save new value
         AGO   .MEND
.PACKED  ANOP
&LABEL   AP    &TO,&FROM           Add value
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $ADDRESS     Address macro operands                                *
**********************************************************************
* Usage:       $ADDRESS RN,ADDR  --->  LA  RN,ADDR                   *
*              $ADDRESS RN,(R3)  --->  LR  RN,R3                     *
*              $ADDRESS RN,@ADDR --->  L   RN,ADDR                   *
*              $ADDRESS RN,C'LIT' ---> LA  RN,=C'LIT'                *
*              $ADDRESS RN,F'1'  --->  LA  RN,=F'1'                  *
**********************************************************************
&LABEL   $ADDRESS
         LCLC  &REG,&OP
         AIF   (N'&SYSLIST EQ 2).OKPARM
         MNOTE 8,'$ADDRESS error - 2 parameters required'
         AGO   .MEND
.OKPARM  ANOP
&REG     SETC  '&SYSLIST(1)'
&OP      SETC  '&SYSLIST(2)'
         AIF   (T'&LABEL EQ 'O').BYNAME
&LABEL   DS    0H
.BYNAME  AIF   ('&OP'(1,1) EQ '(').LR
         AIF   ('&OP'(1,1) EQ '@').L
         AIF   ('&OP'(K'&OP,1) EQ '''').LIT
         LA    &REG,&OP            Point by address
         MEXIT
.LR      ANOP
&OP      SETC  '&OP'(2,K'&OP-2)
         AIF   ('&OP' EQ '&REG').LRXIT
         LR    &REG,&OP            Point by register
.LRXIT   MEXIT
.L       ANOP
&OP      SETC  '&OP'(2,K'&OP-1)
         L     &REG,&OP
         MEXIT
.LIT     ANOP
         LA    &REG,=&OP
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $CVD         Convert to decimal                                    *
**********************************************************************
&LABEL   $CVD  &TO,&FROM,&MASK
         AIF   (T'&FROM EQ 'F').FULL
         AIF   (T'&FROM EQ 'H').HALF
         MNOTE 8,'$CVD error:  Unknown operand - &FROM'
         AGO   .MEND
.FULL    ANOP
&LABEL   L     R15,&FROM           Get current value
         AGO   .CVD
.HALF    ANOP
&LABEL   LH    R15,&FROM           Get current value
         AGO   .CVD
.CVD     ANOP
         CVD   R15,DOUBLE          Convert to decimal
         MVC   &TO,&MASK           Mask
         EDMK  &TO,DOUBLE+2        Edit
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $DECR        Decrement                                             *
**********************************************************************
&LABEL   $DECR &OP
         LCLC  &REG
         AIF   (T'&OP EQ 'F').F
         AIF   (T'&OP EQ 'H').H
         AIF   (T'&OP EQ 'P').P
         AIF   ('&OP'(1,1) EQ '(').R
         MNOTE 8,'$DECR error:  Unknown operand - &OP'
         AGO   .MEND
.F       ANOP
&LABEL   L     R15,&OP             Get current value
         BCTR  R15,0               Decrement
         ST    R15,&OP             Save new value
         AGO   .MEND
.H       ANOP
&LABEL   LH    R15,&OP             Get current value
         BCTR  R15,0               Decrement
         STH   R15,&OP             Save new value
         AGO   .MEND
.P       ANOP
&LABEL   SP    &OP,=P'1'           Decrement
         AGO   .MEND
.R       ANOP
&REG     SETC  '&OP'(2,K'&OP-2)
&LABEL   LR    R15,&OP             Get current value
         BCTR  R15,0               Decrement
         LR    &OP,R15             Save new value
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $HEX         Hex                                                   *
**********************************************************************
&LABEL   $HEX  &TO,&FROM,&LEN=
         LCLA  &OFFSET
         LCLC  &RF
         LCLC  &HEX
&HEX     SETC  'HEX'.'&SYSNDX'
         AIF   ('&FROM'(1,1) EQ '(').REG
&LABEL   LA    R15,&TO             Load TO address
         LA    R1,&FROM            Load FROM address
         LA    R0,&LEN             Load LEN value
         AGO   .COMMON
.REG     ANOP
&LABEL   LA    R15,&TO             Load TO address
&RF      SETC  '&FROM'(2,K'&FROM-2)
         ST    &RF,W_FULL1         Store register FROM value
         LA    R1,W_FULL1          Load FROM address
&OFFSET  SETA  4-&LEN              Determine FROM offset
         LA    R1,&OFFSET.(,R1)    Add offset to FROM address
         LA    R0,&LEN             Load LEN value
         AGO   .COMMON
.COMMON  ANOP
&HEX     EQU   *
         MVC   1(1,R15),0(R1)      Move FROM byte
         UNPK  0(3,R15),1(2,R15)   Unpack
         L     R14,=A(TBLHEX)      Set HEX table address
         USING TBLHEX,R14          Reference HEX table
         TR    0(2,R15),TBLHEX     Convert to HEX
         DROP  R14                 Dereference HEX table
         LA    R15,2(,R15)         Increment TO address by 2
         LA    R1,1(,R1)           Increment FROM address by 1
         BCT   R0,&HEX             Do again until LEN is zero
         MVI   0(R15),C' '         Blank residual byte
         MEND
         MACRO
**********************************************************************
*                                                                    *
* Usage:                                                             *
* Name     $ENTRY    Keywords...                                     *
*                                                                    *
* Name     Module name                                               *
*          Values:   Any valid module name                           *
*          Default:  NULL                                            *
*                                                                    *
* Keywords:                                                          *
* AMODE    Addressing mode                                           *
*          Values:   24/31/ANY                                       *
*          Default:  31                                              *
*                                                                    *
* BASE     Base registers                                            *
*          Values:   (Rxx,Ryy,Rzz...) - Any valid registers          *
*          Default:  (R12)                                           *
*                                                                    *
* DWA      Dynamic work area                                         *
*          Values:   (ddd,rrr,lll)                                   *
*                    ddd  - Dynamic work area dummy section (DSECT)  *
*                    rrr  - Dynamic work area base register          *
*                    lll  - Dynamic work area length                 *
*          Default:  NULL - Dynamic work area not used               *
*                                                                    *
* DWALOC   Location of getmained dynamic work area                   *
*          Values:   BELOW/ANY/RES/EXPLICIT                          *
*          Default:  RES                                             *
*                                                                    *
* MODID    Module identification                                     *
*          Values:   YES/NO                                          *
*          Default:  YES                                             *
*                                                                    *
* RMODE    Residence mode                                            *
*          Values:   24/ANY                                          *
*          Default:  ANY                                             *
*                                                                    *
* SABNDRY  Savearea boundary                                         *
*          Values:   DBLWD|PAGE                                      *
*          Default:  DBLWD                                           *
*                                                                    *
* SALENGTH Savearea length                                           *
*          Values:   Any valid length                                *
*          Default:  0                                               *
*                                                                    *
* SALOC    Savearea location                                         *
*          Values:   BELOW/ANY/RES/EXPLICIT                          *
*          Default:  RES                                             *
*                                                                    *
* SASP     Savearea subpool                                          *
*          Values:   Any valid subpool                               *
*          Default:  0                                               *
*                                                                    *
* SAVEMETH Save/restore methodology                                  *
*          Values:   NO/STACK/STORAGE                                *
*                    NO      - Do not save/restore                   *
*                    SHARED  - Shared storage                        *
*                    STACK   - Save status on stack                  *
*                    STORAGE - Save registers in storage             *
*          Default:  STORAGE                                         *
*                                                                    *
* SP       Subpool of getmained savearea/work area                   *
*          Values:   Any valid subpool                               *
*          Default:  0                                               *
*                                                                    *
* SPLEVEL  Macro level                                               *
*          Values:   0/1/2/3/4/5/6                                   *
*          Default:  0 - Use default macro level                     *
*                                                                    *
* SWA      Static work area                                          *
*          Values:   (ccc,rrr)                                       *
*                    ccc  - Static work area control section (CSECT) *
*                    rrr  - Static work area base register           *
*          Default:  NULL - Static work area not used                *
*                                                                    *
**********************************************************************
* $ENTRY       Standard Linkage - Entry                              *
**********************************************************************
&LABEL   $ENTRY &AMODE=31,         Addressing mode                     X
               &BASE=(R11,R12),    Base Registers                      X
               &DWA=,              Dynamic work area                   X
               &DWALOC=RES,        Location of dynamic work area       X
               &MODID=YES,         Module identification               X
               &RMODE=ANY,         Residence mode                      X
               &SABNDRY=DBLWD,     Savearea boundary                   X
               &SALENGTH=0,        Savearea length                     X
               &SALOC=RES,         Savearea location                   X
               &SASP=0,            Savearea subpool                    X
               &SAVEMETH=SHARED,   Save/restore methodology            X
               &SPLEVEL=0,         Macro level                         X
               &SUBPOOL=0,         Subpool of savearea/work area       X
               &SWA=               Static work area
         GBLA  &GCOMMON            Common Macros Level
         GBLC  &GAMODE             Addressing Mode
         GBLC  &GBRU               Base register using
         GBLC  &GCSECT             Control Section
         GBLC  &GDWAL              Dynamic work area length
         GBLC  &GDWAN              Dynamic work area name
         GBLC  &GDWAR              Dynamic work area register
         GBLC  &GRMODE             Residence Mode
         GBLC  &GSABNDRY           Savearea boundary
         GBLC  &GSALENGTH          Savearea length
         GBLC  &GSALOC             Savearea location
         GBLC  &GSASP              Savearea subpool
         GBLC  &GSAVEMETH          Save/restore methodology
         GBLC  &GSPLEVEL           Macro level
         GBLC  &GSUBPOOL           Subpool of savearea/work area
         GBLC  &GSWAN              Static work area name
         GBLC  &GSWAR              Static work area register
         GBLC  &GTRACE             Trace (YES/NO)
         GBLC  &GTRACEDD           Trace DDNAME (DDNAME)
         GBLC  &SYSSPLV            Macro level
         LCLA  &BRC                Base register count
         LCLC  &BC                 Base register current
         LCLC  &BP                 Base register Caller's
         LCLC  &DWALBL             Dynamic work area label
&GCOMMON SETA  11                  Common Macros Level
         MNOTE *,'$ENTRY Utilizing the following parameters:'
         MNOTE *,'       AMODE=&AMODE'
         MNOTE *,'       BASE=&BASE'
         MNOTE *,'       DWA=&DWA'
         MNOTE *,'       DWALOC=&DWALOC'
         MNOTE *,'       MODID=&MODID'
         MNOTE *,'       RMODE=&RMODE'
         MNOTE *,'       SABNDRY=&SABNDRY'
         MNOTE *,'       SALENGTH=&SALENGTH'
         MNOTE *,'       SALOC=&SALOC'
         MNOTE *,'       SASP=&SASP'
         MNOTE *,'       SAVEMETH=&SAVEMETH'
         MNOTE *,'       SPLEVEL=&SPLEVEL'
         MNOTE *,'       SUBPOOL=&SUBPOOL'
         MNOTE *,'       SWA=&SWA'
         MNOTE *,'       GTRACE=&GTRACE'
         MNOTE *,'       GTRACEDD=&GTRACEDD'
&GAMODE  SETC  '&AMODE'            Addressing Mode
&GCSECT  SETC  '&LABEL'            Control Section
&GRMODE  SETC  '&RMODE'            Residence Mode
&GSABNDRY SETC '&SABNDRY'          Savearea boundary
&GSALENGTH SETC '&SALENGTH'        Savearea length
&GSALOC SETC '&SALOC'              Savearea location
&GSASP  SETC '&SASP'               Savearea subpool
&GSAVEMETH SETC  '&SAVEMETH'       Save/restore methodology
&GSPLEVEL SETC '&SPLEVEL'          Macro level
&GSUBPOOL SETC  '&SUBPOOL'         Subpool of savearea/work area
         AIF   ('&DWALOC' EQ 'BELOW').DWALOCOK,                        X
               ('&DWALOC' EQ 'ANY').DWALOCOK,                          X
               ('&DWALOC' EQ 'RES').DWALOCOK,                          X
               ('&DWALOC' EQ 'EXPLICIT').DWALOCOK
         MNOTE 8,'$ENTRY Invalid DWALOC=&DWALOC'
         MNOTE 8,'BELOW/ANY/RES/EXPLICIT are valid'
         MEXIT
.DWALOCOK ANOP
         AIF   ('&SALOC' EQ 'BELOW').SALOCOK,                          X
               ('&SALOC' EQ 'ANY').SALOCOK,                            X
               ('&SALOC' EQ 'RES').SALOCOK,                            X
               ('&SALOC' EQ 'EXPLICIT').SALOCOK
         MNOTE 8,'$ENTRY Invalid SALOC=&SALOC'
         MNOTE 8,'BELOW/ANY/RES/EXPLICIT are valid'
         MEXIT
.SALOCOK ANOP
         AIF   ('&SABNDRY' EQ 'DBLWD').SABNDRYOK,                      X
               ('&SABNDRY' EQ 'PAGE').SABNDRYOK
         MNOTE 8,'$ENTRY Invalid SABNDRY=&SABNDRY'
         MNOTE 8,'DBLWD|PAGE are valid'
         MEXIT
.SABNDRYOK ANOP
&GCSECT  CSECT                     Control Section
.**********************************************************************
.*       AMODE                                                        *
.**********************************************************************
         AIF   ('&AMODE' EQ '24').AMODEOK,                             X
               ('&AMODE' EQ '31').AMODEOK,                             X
               ('&AMODE' EQ 'ANY').AMODEOK
         MNOTE 8,'$ENTRY Invalid AMODE=&AMODE - 24/31/ANY'
         MEXIT
.AMODEOK ANOP
&GCSECT  AMODE &GAMODE             Addressing Mode
         AGO   .AMODEEND
.AMODENO ANOP
         AGO   .AMODEEND
.AMODEEND ANOP
.**********************************************************************
.*       RMODE                                                        *
.**********************************************************************
         AIF   ('&RMODE' EQ '24').RMODEOK,                             X
               ('&RMODE' EQ 'ANY').RMODEOK
         MNOTE 8,'$ENTRY Invalid RMODE=&RMODE - 24/ANY'
         MEXIT
.RMODEOK ANOP
&GCSECT  RMODE &GRMODE             Residence Mode
         AGO   .RMODEEND
.RMODENO ANOP
         AGO   .RMODEEND
.RMODEEND ANOP
.**********************************************************************
.*       SPLEVEL                                                      *
.**********************************************************************
         AIF   ('&SPLEVEL' EQ '0').SPLVLNO,                            X
               ('&SPLEVEL' EQ '1').SPLVLYES,                           X
               ('&SPLEVEL' EQ '2').SPLVLYES,                           X
               ('&SPLEVEL' EQ '3').SPLVLYES,                           X
               ('&SPLEVEL' EQ '4').SPLVLYES,                           X
               ('&SPLEVEL' EQ '5').SPLVLYES,                           X
               ('&SPLEVEL' EQ '6').SPLVLYES
         MNOTE 8,'$ENTRY Invalid SPLEVEL=&SPLEVEL - 0/1/2/3/4/5/6'
         MEXIT
.SPLVLYES ANOP
         SPLEVEL SET=&SPLEVEL      Set macro level
         AGO   .SPLVLEND
.SPLVLNO ANOP
         SPLEVEL SET               Use default macro level
         AGO   .SPLVLEND
.SPLVLEND ANOP
.**********************************************************************
.*       Module identification                                        *
.**********************************************************************
         AIF   ('&MODID' EQ 'NO').MODIDNO,                             X
               ('&MODID' EQ 'YES').MODIDYES
         MNOTE 8,'$ENTRY Invalid MODID=&MODID - YES/NO'
         MEXIT
.MODIDYES ANOP
         B     4+1+24+3(0,R15)     Branch around module identification
         DC    AL1(24)             Module identification length
         DC    CL8'&GCSECT'        CSECT name
         DC    CL8'&SYSDATE'       Assembly date
         DC    CL8'&SYSTIME'       Assembly time
         DC    AL3(0)              Filler
         AGO   .MODIDEND
.MODIDNO ANOP
         AGO   .MODIDEND
.MODIDEND ANOP
.**********************************************************************
.*       Register / Savearea Equates                                  *
.**********************************************************************
         AIF   (D'R0).REGEQUNO
A0       EQU    0                  Access register
A1       EQU    1                  Access register
A2       EQU    2                  Access register
A3       EQU    3                  Access register
A4       EQU    4                  Access register
A5       EQU    5                  Access register
A6       EQU    6                  Access register
A7       EQU    7                  Access register
A8       EQU    8                  Access register
A9       EQU    9                  Access register
A10      EQU   10                  Access register
A11      EQU   11                  Access register
A12      EQU   12                  Access register
A13      EQU   13                  Access register
A14      EQU   14                  Access register
A15      EQU   15                  Access register
C0       EQU    0                  Control register
C1       EQU    1                  Control register
C2       EQU    2                  Control register
C3       EQU    3                  Control register
C4       EQU    4                  Control register
C5       EQU    5                  Control register
C6       EQU    6                  Control register
C7       EQU    7                  Control register
C8       EQU    8                  Control register
C9       EQU    9                  Control register
C10      EQU   10                  Control register
C11      EQU   11                  Control register
C12      EQU   12                  Control register
C13      EQU   13                  Control register
C14      EQU   14                  Control register
C15      EQU   15                  Control register
F0       EQU    0                  Floating point register
F1       EQU    2                  Floating point register
F2       EQU    4                  Floating point register
F3       EQU    6                  Floating point register
R0       EQU    0                  General register - System
R1       EQU    1                  General register - System
R2       EQU    2                  General register - System
R3       EQU    3                  General register
R4       EQU    4                  General register
R5       EQU    5                  General register
R6       EQU    6                  General register
R7       EQU    7                  General register
R8       EQU    8                  General register
R9       EQU    9                  General register
R10      EQU   10                  General register
R11      EQU   11                  General register
R12      EQU   12                  General register
R13      EQU   13                  General register - Savearea
R14      EQU   14                  General register - System
R15      EQU   15                  General register - System
SAVEPL1  EQU    0                  Savearea PL1
SAVEPREV EQU    4                  Savearea Previous (from caller)
SAVENEXT EQU    8                  Savearea Next (from target)
SAVER14  EQU   12                  Savearea R14 (Return address)
SAVER15  EQU   16                  Savearea R15 (Entry address)
SAVER0   EQU   20                  Savearea R0
SAVER1   EQU   24                  Savearea R1
SAVER2   EQU   28                  Savearea R2
SAVER3   EQU   32                  Savearea R3
SAVER4   EQU   36                  Savearea R4
SAVER5   EQU   40                  Savearea R5
SAVER6   EQU   44                  Savearea R6
SAVER7   EQU   48                  Savearea R7
SAVER8   EQU   52                  Savearea R8
SAVER9   EQU   56                  Savearea R9
SAVER10  EQU   60                  Savearea R10
SAVER11  EQU   64                  Savearea R11
SAVER12  EQU   68                  Savearea R12
&GREGEQU SETA  1
.REGEQUNO ANOP
.**********************************************************************
.*       Save Registers                                               *
.**********************************************************************
         AIF   ('&GSAVEMETH' EQ 'STORAGE').SAVESTORAGE,                X
               ('&GSAVEMETH' EQ 'SHARED').SAVESHARED,                  X
               ('&GSAVEMETH' EQ 'STACK').SAVESTACK,                    X
               ('&GSAVEMETH' EQ 'NO').SAVENO
         MNOTE 8,'$ENTRY Invalid SAVEMETH=&GSAVEMETH'
         MNOTE 8,'$ENTRY Syntax: SAVEMETH=NO/STACK/STORAGE'
         MEXIT
.SAVESHARED ANOP
.SAVESTORAGE ANOP
         SAVE  (14,12)             Save registers in storage
         AGO   .SAVEEND
.SAVESTACK ANOP
         BAKR  R14,0               Save status on stack
         AGO   .SAVEEND
.SAVENO  ANOP
.*                                 Do not save
.SAVEEND ANOP
.**********************************************************************
.*       Base Register(s)                                             *
.**********************************************************************
         AIF   (N'&BASE EQ 0).BASENO
         AIF   (T'&BASE NE 'O').BASEOK
         MNOTE 8,'$ENTRY Missing Required Keyword BASE'
         MEXIT
.BASEOK  ANOP
         LR    &BASE(1),R15        Load base register
&BRC     SETA 1
.BASENXT ANOP
         AIF   (&BRC EQ N'&BASE).BASEEND
&BRC     SETA  &BRC+1              Increment base register count
&BC      SETC  '&BASE(&BRC)'       Current base register
&BP      SETC  '&BASE(&BRC-1)'     Caller's base register
         LA    &BC,4095(,&BP)      Load base register
         LA    &BC,1(,&BC)         Load base register
         AGO   .BASENXT
.BASEEND ANOP
&GBRU    SETC  '&BASE'
         AIF   ('&BASE'(1,1) NE '(').USING1
&GBRU    SETC  '&BASE'(2,K'&BASE-2)
.USING1  ANOP
         USING &LABEL,&GBRU        Reference control section
.BASENO  ANOP
.**********************************************************************
.*       Obtain savearea                                              *
.**********************************************************************
         AIF   ('&GSALENGTH' EQ '0').SANO
         LR    R3,R0               Caller's R0
         LR    R4,R1               Caller's R1
         MNOTE *,'STORAGE OBTAIN, '
         MNOTE *,'        BNDRY=&GSABNDRY, '
         MNOTE *,'        LENGTH=&GSALENGTH, '
         MNOTE *,'        LOC=&GSALOC, '
         MNOTE *,'        SP=&GSASP '
         STORAGE OBTAIN,                                               X
               BNDRY=&GSABNDRY,                                        X
               LENGTH=&GSALENGTH,                                      X
               LOC=&GSALOC,                                            X
               SP=&GSASP
         ST    R13,SAVEPREV(,R1)   Savearea previous
         ST    R1,SAVENEXT(,R13)   Savearea next
         AIF   ('&GSAVEMETH' NE 'STACK').SANOSTACK
         MVC   SAVEPL1(,R13),=C'F1SA' Indicate linkage stack usage
.SANOSTACK ANOP
         LR    R13,R1              Reference savearea
         LR    R0,R3               Caller's R0
         LR    R1,R4               Caller's R1
         AGO   .SAEND
.SANO    ANOP
         AIF   ('&GSAVEMETH' EQ 'SHARED').SASHARED
.*                                 Do not obtain savearea storage
         AGO   .SAEND
.SASHARED ANOP
         AH    R13,=H'72'          Shared savearea
         AGO   .SAEND
.SAEND   ANOP
.**********************************************************************
.*       Dynamic work area                                            *
.**********************************************************************
         AIF   (N'&DWA EQ 0).DWANO
         AIF   (N'&DWA EQ 3).DWAOK
         MNOTE 8,'$ENTRY Invalid DWA=&DWA'
         MNOTE 8,'$ENTRY Syntax  DWA=(DSECT_NAME,REGISTER,LENGTH)'
         MEXIT
.DWAOK   ANOP
&GDWAN   SETC  '&DWA(1)'           Dynamic work area name
&GDWAR   SETC  '&DWA(2)'           Dynamic work area register
&GDWAL   SETC  '&DWA(3)'           Dynamic work area length
&DWALBL  SETC  'DWA'.'&SYSNDX'     Dynamic work area label
         LR    R3,R0               Caller's R0
         LR    R4,R1               Caller's R1
         MNOTE *,'STORAGE OBTAIN,LENGTH=&GDWAL,LOC=&DWALOC Obtain DWA'
         STORAGE OBTAIN,LENGTH=&GDWAL,LOC=&DWALOC,BNDRY=PAGE Obtain DWA
         LR    &GDWAR,R1           Set Dynamic work area base
         USING &GDWAN,&GDWAR       Reference Dynamic work area
         LR    R0,R3               Caller's R0
         LR    R1,R4               Caller's R1
         AGO   .DWAEND
.DWANO   ANOP
&GDWAN   SETC  ''                  Dynamic work area name
&GDWAR   SETC  ''                  Dynamic work area register
&GDWAL   SETC  ''                  Dynamic work area length
         AGO   .DWAEND
.DWAEND  ANOP
.**********************************************************************
.*       Static work area                                             *
.**********************************************************************
         AIF   (N'&SWA EQ 0).SWANO
         AIF   (N'&SWA EQ 2).SWAOK
         MNOTE 8,'$ENTRY Invalid SWA=&SWA'
         MNOTE 8,'$ENTRY Syntax: SWA=(CSECT_NAME,REGISTER)'
         MEXIT
.SWAOK   ANOP
&GSWAN   SETC  '&SWA(1)'           Static work area name
&GSWAR   SETC  '&SWA(2)'           Static work area register
         L     &GSWAR,=A(&GSWAN)   Set static work area base
         USING &GSWAN,&GSWAR       Reference static work area
         AGO   .SWAEND
.SWANO   ANOP
&GSWAN   SETC  ''                  Static work area name
&GSWAR   SETC  ''                  Static work area register
         AGO   .SWAEND
.SWAEND  ANOP
.**********************************************************************
.*       Trace                                                        *
.**********************************************************************
         AIF   ('&GTRACE' NE 'YES').TRACEEND
         LCLC  &NOTRACE
&NOTRACE SETC  'NOTRACE'.'&SYSNDX'
         LR    R3,R1               Save parameter list
         TM    &GTRACEDD+48,X'10'  &GTRACEDD open ?
         BZ    &NOTRACE            . No
         $FILL W_TRACE,C' '        Blank tracer record
         MVC   W_TRACE(020),=CL020' Entering &GCSECT'
         PUT   &GTRACEDD,W_TRACE
         LR    R1,R3               Restore parameter list
&NOTRACE EQU   *
         AGO   .TRACEEND
.TRACEEND ANOP
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
*        Macro Name:                                                 *
*        Operation:      Standard Linkage - Exit                     *
*        Usage:                                                      *
**********************************************************************
*                                                                    *
* Syntax:                                                            *
* Name     $EXIT     Keywords...                                     *
*                                                                    *
* Name     Module name                                               *
*          Values:   Any valid module name                           *
*          Default:  NULL                                            *
*                                                                    *
* Keywords:                                                          *
* LTORG    Literal pool at end of module                             *
*          Values:   YES/NO                                          *
*          Default:  YES                                             *
*                                                                    *
**********************************************************************
**********************************************************************
* $EXIT        Standard Linkage - Exit                               *
**********************************************************************
&LABEL   $EXIT &LTORG=YES          Literal pool
         GBLC  &GBRU               Base register using
         GBLC  &GCSECT             Control Section
         GBLC  &GDWAL              Dynamic work area length
         GBLC  &GDWAR              Dynamic work area register
         GBLC  &GSALENGTH          Savearea length
         GBLC  &GSAVEMETH          Save/restore methodology
         GBLC  &GTRACE             Trace (YES/NO)
         GBLC  &GTRACEDD           Trace DDNAME (DDNAME)
         MNOTE *,'$EXIT  Utilizing the following parameters:'
         MNOTE *,'       LTORG=&LTORG'
         MNOTE *,'       GTRACE=&GTRACE'
         MNOTE *,'       GTRACEDD=&GTRACEDD'
.**********************************************************************
.*       Return Code                                                  *
.**********************************************************************
&LABEL.0 L     R3,=F'0'            Return code 0
         B     &LABEL.X            Exit
&LABEL.4 L     R3,=F'4'            Return code 4
         B     &LABEL.X            Exit
&LABEL.8 L     R3,=F'8'            Return code 8
         B     &LABEL.X            Exit
&LABEL.C L     R3,=F'12'           Return code 12
         B     &LABEL.X            Exit
&LABEL.R LR    R3,R15              Return code in R15
         B     &LABEL.X            Exit
&LABEL.X EQU   *                   Exit
.**********************************************************************
.*       Trace                                                        *
.**********************************************************************
         AIF   ('&GTRACE' NE 'YES').TRACEEND
         LCLC  &NOTRACE
&NOTRACE SETC  'NOTRACE'.'&SYSNDX'
         TM    &GTRACEDD+48,X'10'  &GTRACEDD open ?
         BZ    &NOTRACE            . No
         $FILL W_TRACE,C' '        Blank tracer record
         MVC   W_TRACE(020),=CL020' Exiting  &GCSECT'
         PUT   &GTRACEDD,W_TRACE
&NOTRACE EQU   *
         AGO   .TRACEEND
.TRACEEND ANOP
.**********************************************************************
.*       Dynamic work area                                            *
.**********************************************************************
         AIF   ('&GDWAR' EQ '').DWANO
*        L     R0,=F'&GDWAL'       Dynamic work area length
         MNOTE *,'STORAGE RELEASE,ADDR=(&GDWAR),LENGTH=&GDWAL DWA'
         STORAGE RELEASE,ADDR=(&GDWAR),LENGTH=&GDWAL Release DWA
         AGO   .DWAEND
.DWANO   ANOP
         AGO   .DWAEND
.DWAEND  ANOP
.**********************************************************************
.*       Release savearea                                             *
.**********************************************************************
         AIF   ('&GSALENGTH' EQ '0').SANO
         LR    R1,R13              Savearea address
         L     R13,4(R13)          Restore previous savearea
         MNOTE *,'STORAGE RELEASE,ADDR=(R1),LENGTH=&GSALENGTH'
         STORAGE RELEASE,ADDR=(R1),LENGTH=&GSALENGTH Release savearea
         AGO   .SAEND
.SANO    ANOP
         AIF   ('&GSAVEMETH' EQ 'SHARED').SASHARED
         AGO   .SAEND
.*                                 Do not release savearea
.SASHARED ANOP
         SH    R13,=H'72'          Shared savearea
         AGO   .SAEND
.SAEND   ANOP
.**********************************************************************
.*       Restore environment                                          *
.**********************************************************************
         AIF   ('&GSAVEMETH' EQ 'SHARED').SAVESHARED,                  X
               ('&GSAVEMETH' EQ 'STORAGE').SAVESTORAGE,                X
               ('&GSAVEMETH' EQ 'STACK').SAVESTACK,                    X
               ('&GSAVEMETH' EQ 'NO').SAVENO
         MNOTE 8,'$EXIT Invalid SAVEMETH=&GSAVEMETH'
         MNOTE 8,'$EXIT Syntax: SAVEMETH=NO/STACK/STORAGE'
         MEXIT
.SAVESHARED ANOP
.SAVESTORAGE ANOP
         LR    R15,R3              Set return code
         L     14,12(0,13)         Restore R14 from storage
         LM    0,12,20(13)         Restore R0:R12 from storage
         BSM   0,14                Return control
         AGO   .SAVEEND
.SAVESTACK ANOP
         LR    R15,R3              Set return code
         PR                        Restore status from stack
         AGO   .SAVEEND
.SAVENO  ANOP
         LR    R15,R3              Set return code
         BSM   0,14                Return control
         AGO   .SAVEEND
.SAVEEND ANOP
.**********************************************************************
.*       LTORG                                                        *
.**********************************************************************
         AIF   ('&LTORG' EQ 'YES').LTORGYES,                           X
               ('&LTORG' EQ 'NO').LTORGNO
         MNOTE 8,'$EXIT Invalid LTORG=&LTORG - YES/NO'
         MEXIT
.LTORGYES ANOP
         LTORG                     Literal pool
.LTORGNO ANOP
         AGO   .LTORGEND
.LTORGEND ANOP
.MEND    ANOP
         DROP  &GBRU               Dereference control section
         MEND
         MACRO
**********************************************************************
* $FILL        Fill a Field                                          *
**********************************************************************
&LABEL   $FILL &F,&C
&LABEL   LA    R0,&F               Receiving field address
         LA    R1,L'&F             Receiving field length
         XR    R14,R14             Sending field address - none
         XR    R15,R15             Sending field length - none
         ICM   R15,B'1000',=&C     Pad character
         MVCL  R0,R14              Fill the field
         MEND
         MACRO
**********************************************************************
* $FIND        Find character                                        *
**********************************************************************
&LABEL   $FIND &OP,                Operand                             X
               &CHAR,              Character                           X
               &DECREMENT=YES,     Decrement - Yes/No                  X
               &MODE=BE            Mode - BE/BNE/...
         LCLC  &FIND1,&FIND2
&FIND1   SETC  'FIND1'.'&SYSNDX'
&FIND2   SETC  'FIND2'.'&SYSNDX'
&LABEL   $ADDRESS R14,&OP(1)       Operand
         AIF   (N'&OP EQ 2).OPL
         LA    R1,L'&OP(1)         Operand length default
         AGO   .OPEND
.OPL     ANOP
         $ADDRESS R1,&OP(2)        Operand length specified
.OPEND   ANOP
         XR    R15,R15             Clear length
&FIND1   EQU   *
         CLI   0(R14),&CHAR        Match ?
         &MODE &FIND2              . Yes
         LA    R14,1(R14)          Increment operand address
         LA    R15,1(R15)          Increment current length
         BCT   R1,&FIND1           Process next operand, if any
&FIND2   EQU   *
         AIF   ('&DECREMENT' EQ 'NO').DECNO
         BCTR  R15,0               Length - 1
.DECNO   ANOP
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $INCR        Increment                                             *
**********************************************************************
&LABEL   $INCR &OP,&TYPE
         AIF   (T'&OP EQ 'F').F
         AIF   (T'&OP EQ 'H').H
         AIF   (T'&OP EQ 'P').P
         AIF   ('&TYPE' EQ 'F').F
         AIF   ('&TYPE' EQ 'H').H
         AIF   ('&TYPE' EQ 'P').P
         AIF   ('&OP'(1,1) EQ '(').R
         MNOTE 8,'$INCR error:  Unknown operand - &OP - &TYPE'
         AGO   .MEND
.F       ANOP
&LABEL   L     R15,&OP             Get current value
         LA    R15,1(,R15)         Increment
         ST    R15,&OP             Save new value
         AGO   .MEND
.H       ANOP
&LABEL   LH    R15,&OP             Get current value
         LA    R15,1(,R15)         Increment
         STH   R15,&OP             Save new value
         AGO   .MEND
.P       ANOP
&LABEL   AP    &OP,=P'1'           Increment
         AGO   .MEND
.R       ANOP
&REG     SETC  '&OP'(2,K'&OP-2)
&LABEL   LA    &OP,1(&OP)          Increment
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $STRIP       Strip character                                       *
**********************************************************************
&LABEL   $STRIP &FIELD,            Field                               X
               &CHAR               Character
         LCLC  &STRIP1,&STRIP2,&STRIP3
&STRIP1  SETC  'STRIP1'.'&SYSNDX'
&STRIP2  SETC  'STRIP2'.'&SYSNDX'
&STRIP3  SETC  'STRIP3'.'&SYSNDX'
&LABEL   $ADDRESS R14,&FIELD(1)    Field
         AIF   (N'&FIELD EQ 2).LENGTH
         LA    R1,L'&FIELD(1)      Field length default
         AGO   .NOLENGTH
.LENGTH  ANOP
         $ADDRESS R1,&FIELD(2)     Field length specified
.NOLENGTH ANOP
         LA    R0,W_WRK            Work address
         LA    R1,L'&FIELD         Field length
         LA    R14,&FIELD          Field address
         LR    R15,R1              Field length
         MVCL  R0,R14              Copy to work area
         $FILL &FIELD,C' '         Clear field
         $ADDRESS R1,&FIELD(1)     Field address
         LA    R14,W_WRK           Work address
         LA    R15,L'&FIELD        Field length
&STRIP1  EQU   *
         CLI   0(R14),&CHAR        Match ?
         BE    &STRIP2             . Yes
         MVC   0(1,R1),0(R14)      Copy the character
         LA    R14,1(R14)          Increment work address
         LA    R1,1(R1)            Increment field address
         B     &STRIP3             Continue
&STRIP2  EQU   *
         LA    R14,1(R14)          Increment work address
&STRIP3  EQU   *
         BCT   R15,&STRIP1         Process next character, if any
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* $VLC         Variable length compare                               *
**********************************************************************
&LABEL   $VLC  &OP1,               Operand 1                           X
               &OP2,               Operand 2                           X
               &LEN,               Length                              X
               &DECREMENT=YES      Decrement - Yes/No
         LCLC  &VLC1,&VLC2
&VLC1    SETC  'VLC1'.'&SYSNDX'
&VLC2    SETC  'VLC2'.'&SYSNDX'
&LABEL   $ADDRESS R14,&OP1         Operand 1
         $ADDRESS R1,&LEN          Length
         $ADDRESS R15,&OP2         Operand 2
         AIF   ('&DECREMENT' EQ 'NO').DECNO
         BCTR  R1,0                Length - 1
.DECNO   ANOP
         B     &VLC2
&VLC1    CLC   0(*-*,R14),0(R15)   Variable length compare (executed)
&VLC2    EQU   *
         EX    R1,&VLC1            Variable length compare
         MEND
         MACRO
**********************************************************************
* $VLM         Variable length move                                  *
**********************************************************************
&LABEL   $VLM  &OP1,               Operand 1                           X
               &OP2,               Operand 2                           X
               &LEN,               Length                              X
               &DECREMENT=YES      Decrement - Yes/No
         LCLC  &VLM1,&VLM2
&VLM1    SETC  'VLM1'.'&SYSNDX'
&VLM2    SETC  'VLM2'.'&SYSNDX'
&LABEL   $ADDRESS R14,&OP1         Operand 1
         $ADDRESS R1,&LEN          Length
         $ADDRESS R15,&OP2         Operand 2
         AIF   ('&DECREMENT' EQ 'NO').DECNO
         BCTR  R1,0                Length - 1
.DECNO   ANOP
         B     &VLM2
&VLM1    MVC   0(*-*,R14),0(R15)   Variable length move (executed)
&VLM2    EQU   *
         EX    R1,&VLM1            Variable length move
         MEND
         MACRO
**********************************************************************
* $VLTRT       Variable Length Translate and Test                    *
**********************************************************************
&LABEL   $VLTRT  &OP1,             Operand 1                           X
               &OP2,               Operand 2                           X
               &LEN,               Length                              X
               &DECREMENT=YES      Decrement - Yes/No
&LABEL   $ADDRESS R14,&OP1         Storage 1
         $ADDRESS R15,&OP2         Storage 2
         $ADDRESS R1,&LEN          Length
         AIF   ('&DECREMENT' EQ 'NO').DECNO
         BCTR  R1,0                Length - 1
.DECNO   ANOP
         EX    R1,P_VLTRT          Variable Length Translate and Test
         MEND
         MACRO
**********************************************************************
* ADDPOP       Start Pop-Up Window Mode                              *
**********************************************************************
&LABEL   ADDPOP &POPLOC=,          Pop Location                        X
               &ROW=,              Row                                 X
               &COL=               Column
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'ADDPOP ',                                           X
               =C'&POPLOC ',                                           X
               =C'&ROW ',                                              X
               =C'&COL '),                                             X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* BRIF         Browse Interface                                      *
**********************************************************************
&LABEL   BRIF  &DATA=,             Dataset name                        X
               &RECFM=,            Record format                       X
               &LRECL=,            Logical record length               X
               &READ=0,            Read routine                        X
               &COMMAND=0,         Command routine                     X
               &DIALOG=,           Dialog data                         X
               &PANEL=,            Panel name                          X
               &FORMAT=,           Format name                         X
               &MIXED=NO,          YES/NO                              X
               &ERRET=             Error Routine
         AIF   ('&MIXED' EQ 'YES').MIXEDOK,                            X
               ('&MIXED' EQ 'NO').MIXEDOK
         MNOTE 8,'BRIF Invalid MIXED=&MIXED - YES/NO'
         AGO   .MEND
.MIXEDOK ANOP
         AIF   ('&RECFM' EQ 'F').RECFMOK,                              X
               ('&RECFM' EQ 'FA').RECFMOK,                             X
               ('&RECFM' EQ 'FM').RECFMOK,                             X
               ('&RECFM' EQ 'U').RECFMOK,                              X
               ('&RECFM' EQ 'V').RECFMOK,                              X
               ('&RECFM' EQ 'VA').RECFMOK,                             X
               ('&RECFM' EQ 'VM').RECFMOK
         MNOTE 8,'BRIF Invalid RECFM=&RECFM - F/FA/FM/U/V/VA/VM'
         AGO   .MEND
.RECFMOK ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'BRIF ',                                             X
               &DATA,                                                  X
               =C'&RECFM ',                                            X
               &LRECL,                                                 X
               =A(&READ),                                              X
               =A(&COMMAND),                                           X
               &DIALOG,                                                X
               =C'&PANEL ',                                            X
               =C'&FORMAT ',                                           X
               =C'&MIXED '),                                           X
               VL,MF=(E,W_CL512)
         AIF   ('&ERRET' EQ '').MEND
         LTR   15,15               Okay ?
         AIF   ('&ERRET'(1,1) EQ '(').ERRETR
         BNZ   &ERRET              . No
         AGO   .MEND
.ERRETR  ANOP
         BR    &ERRET(1)           . No
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* BROWSE       Browse a dataset                                      *
**********************************************************************
&LABEL   BROWSE &DATASET=,         DSNAME                              X
               &ERRET=,            Error Routine                       X
               &VOLUME=,           SERIAL                              X
               &PASSWORD=,         PSWD-VALUE                          X
               &PANEL=,            PANEL-NAME                          X
               &FORMAT=,           FORMAT-NAME                         X
               &MIXED=             YES/NO
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'BROWSE ',                                           X
               &DATASET),                                              X
               VL,MF=(E,W_CL512)
         AIF   ('&ERRET' EQ '').MEND
         LTR   15,15               Okay ?
         AIF   ('&ERRET'(1,1) EQ '(').ERRETR
         BNZ   &ERRET              . No
         AGO   .MEND
.ERRETR  ANOP
         BR    &ERRET(1)           . No
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* CONTROL      Set processing modes                                  *
**********************************************************************
&LABEL   CONTROL &CTL1,&CTL2,&CTL3,&CTL4
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'CONTROL ',                                          X
               =C'&CTL1 ',                                             X
               =C'&CTL2 ',                                             X
               =C'&CTL3 ',                                             X
               =C'&CTL4 '),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* DISPLAY      Display panels and messages                           *
**********************************************************************
&LABEL   DISPLAY &PANEL=,          PANEL-NAME                          X
               &END=,              End label                           X
               &MESSAGE=,          MESSAGE-ID                          X
               &CURSOR=,           CURSOR-FIELD-NAME                   X
               &CSRPOS=,           CURSOR-POSITION                     X
               &COMMAND=,          STACK-BUFFER-NAME                   X
               &RETBUFFR=,         RET-BUFFER-NAME                     X
               &RETLGTH=,          RET-LENGTH-NAME                     X
               &MSGLOC=,           MESSAGE-FIELD-NAME                  X
               &PC=()              Primary-command
         AIF   ('&PC'(1,1) EQ '(').OKPC
         MNOTE 8,'DISPLAY PC=&PC - MUST BE IN PARENTHESIS'
.OKPC    ANOP
         LCLA  &CNT
         LCLC  &NLLBL
         LCLC  &NPLBL,&PCLBL
         LCLC  &CMP,&LBL,&ID,&SEL
&PCLBL   SETC  'PC'.'&SYSNDX'
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'DISPLAY ',                                          X
               =C'&PANEL ',                                            X
               =C'&MESSAGE ',                                          X
               =C'&CURSOR ',                                           X
               =C'&CSRPOS ',                                           X
               =C'&COMMAND ',                                          X
               =C'&RETBUFFR ',                                         X
               =C'&RETLGTH ',                                          X
               =C'&MSGLOC '),                                          X
               VL,MF=(E,W_CL512)
         C     R15,=F'8'           End ?
         BNL   &END                . Yes
         AIF   ('&PC'(1,2) EQ '()').MEND
.PC      ANOP
&PCLBL   EQU   *
&CNT     SETA  0
&CNT     SETA  &CNT+1
&SEL     SETC  '&PC(&CNT)'
.NEXTPC  ANOP
&CNT     SETA  &CNT+1
         AIF   (&CNT GT N'&PC).ENDPC
&CMP     SETC  '&PC(&CNT)'
&NLLBL   SETC  'N'.'&CMP'.'&SYSNDX'
&CNT     SETA  &CNT+1
&LBL     SETC  '&PC(&CNT)'
         CLC   &SEL,=C'&CMP'       Match ?
         BNE   &NLLBL              . No
         CONTROL DISPLAY,SAVE      Save display environment
         CALL  &LBL                &LBL
         CONTROL DISPLAY,RESTORE   Restore display environment
         B     &LABEL              Redisplay
&NLLBL   EQU *
         AGO   .NEXTPC
.ENDPC   ANOP
*        B     &LABEL              Redisplay
.ENDLBL  ANOP
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* DSINFO       Data Set Information Dialog Service                   *
**********************************************************************
&LABEL   DSINFO &DSN,&SER
         AIF   ('&SER' EQ '').NOSER
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'DSINFO ',                                           X
               &DSN,                                                   X
               &SER),                                                  X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.NOSER   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'DSINFO ',                                           X
               &DSN),                                                  X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* EDIT         Edit a dataset                                        *
**********************************************************************
&LABEL   EDIT &EDSN,&EVOL
         AIF   ('&EVOL' EQ '').NOVOL
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'EDIT ',                                             X
               &EDSN,                                                  X
               &EVOL),                                                 X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.NOVOL   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'EDIT ',                                             X
               &EDSN),                                                 X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* IHALLT       LNKLST Table (LLT)                                    *
**********************************************************************
         IHALLT
LLT      DSECT                     LNKLST Table (LLT)
LLTHEAD  DS    0CL8                Header section
LLTID    DS    CL4                 Table id 'LLT '
LLTCOUNT DS    F                   Number of entries in table
LLTENTRY DS    0CL45               Table entry
LLTDSLTH DS    AL1                 Length of data set name
LLTDSN   DS    CL44                Data set name
         MEND
         MACRO
**********************************************************************
* IEEMBRDS     SMF RDS                                               *
**********************************************************************
         IEEMBRDS
IEEMBRDS DSECT                         SMF RDS
RDSID    DS    C'RDS '                 CONTROL BLOCK ID
RDSNEXT  DS    A(IEEMBRDS)             FORWARD CHAIN POINTER
RDSPREV  DS    A(IEEMBRDS)             BACKWARD CHAIN POINTER
*
RDSFLG1  DS    X
RDSFREE  EQU   X'10'                   NEED TO FREE DATASET
RDSDUMP  EQU   X'08'                   OPER HAS BEEN TOLD TO DUMP DS
RDSREADY EQU   X'04'                   DS IS OPEN AND READY
RDSCLOSE EQU   X'02'                   DS NEEDS TO BE CLOSED
RDSNDOP  EQU   X'01'                   DS NEEDS TO BE OPENED
*
RDSFLG2  DS    X
RDSLOST  EQU   X'80'                   DUMMY RDS FOR LOST DATA
RDSPHYER EQU   X'40'                   PHYSICAL I/O ERROR
RDSLOGER EQU   X'20'                   LOGICAL I/O ERROR
RDSFLG3  DS    X                       RESERVED
RDSDSLEN DS    AL1(L'RDSNAME)          LENGTH OF DATA SET NAME  HBB5510
RDSNAME  DS    CL44'SYS1.MANX '        DATA SET NAME            HBB5510
RDSVOLID DS    C'SYSRES'               VOLUME SERIAL
         DS    2X                      alignment                HBB5510
RDSDDN   DS    C'SYS00001'             DDNAME RETURNED BY DYNALLOC
RDSCAPTY DS    F                       CAPACITY IN # OF BLOCKS
RDSNXTBL DS    F                       NUMBER OF NEXT AVAIL BLOCK
RDSBQE   DS    A                       ADDR OF BQE BEING WRITTEN
RDSAVSPC DS    F                       AVAIL SPACE FROM SHOWCB
RDSENRBA DS    F                       ENDRBA FROM SHOWCB
RDSCINV  DS    F                       CONTROL INTERVAL SIZE
RDSDOMID DS    F                       MSG # OF DUMP MSG
         DS    F                                                HBB5510
RDSRPL   DS    19F                     VSAM RPL
RDSACB   DS    19F                     VSAM ACB
         MEND
         MACRO
**********************************************************************
* IEEMBSST     SMF Selectivity Table                                 *
**********************************************************************
         IEEMBSST
SST      DSECT                         SMF Selectivity Table
SSTNAME  DS    CL4'SYS'                sub-system name
SSTFLAGS DS    X
SSTDETAL EQU   X'80'                   DETAILED ACCT'NG
SSTACTIV EQU   X'40'                   SUBSYSTEM ACTIVE
SSTRESET EQU   X'20'
         DS    XL3
SSTINTVL DS    XL8                     INTERVAL LENGTH
SSTRCDON DS    32X                     ONE BIT PER RECORD
SSTEXTAB DS    A(EXITTAB)              A(EXIT TABLE)
*
EXITTAB  DSECT
EXITSPL  DS    XL4                     SUBPOOL ID AND LENGTH
EXITNMBR DS    F                       NUMBER OF EXITS
EXITNTRY EQU   *
EXITNAME DS    CL8                     EXIT NAME
EXITFLGS DS    XL4                     Flags
EXITADDR DS    A                       A(EXIT)
EXITINAC EQU   X'80'                   EXIT ACTIVE
EXITEND  DS    0H
         MEND
         MACRO
**********************************************************************
* IHALPAT      LPALST Table (LPAT)                                   *
**********************************************************************
         IHALPAT
LPAT     DSECT                     LPALST Table (LPAT)
LPATHDR  DS    0CL8                Header section
LPATID   DS    CL4                 Table id 'LPAT'
LPATCNT  DS    F                   Number of entries in table
LPATNTRY DS    0CL45               Table entry
LPATDSLN DS    AL1                 Length of data set name
LPATDSN  DS    CL44                Data set name
         MEND
         MACRO
**********************************************************************
* IKJEFTSR     TSO/E Service Facility Routine                        *
**********************************************************************
&LABEL   IKJEFTSR
&LABEL   L     R15,TSRADDR         IKJEFTSR address
         CALL  (15),                                                   X
               (TSRFLAGS,TSRBUF,TSRLEN,TSRRETRN,TSRREASN,TSRABEND),    *
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* IKJZT301     TSO/E Broadcast Data Set Record 1                     *
**********************************************************************
         IKJZT301
R1BC     DSECT
R1BCPTRP DS    0AL4
R1BCFLGS DS    AL1                 Notices flags - not used
R1BCPTR  DS    AL3                 First notices directory RBA
R1USPTRP DS    0AL4
R1USFLGS DS    AL1                 User mail flags - not used
R1USPTR  DS    AL3                 First user mail directory RBA
R1RECNUM DS    AL4                 Total number of records
R1BCMAX  DS    AL2                 Maximum Notices records
R1DSN    DS    CL24                Broadcast dataset name
R1LEVEL  DS    CL7                 Level
         DS    CL1                 Reserved
R1FRESRH DS    AL3                 Free search record RBA
R1GENNUM DS    AL4                 Notice table generation number
         DS    CL76                Reserved
         MEND
         MACRO
**********************************************************************
* IKJZT302     TSO/E Broadcast Notices Directory Record              *
**********************************************************************
         IKJZT302
BCDIR    DSECT
BCDENTRY DS    0CL5
BCDNOMSG DS    0AL1
BCDMSGNO DS    XL2                 Notice message number in hex
BCDMRBA  DS    AL3                 Notice message RBA
         DS    24CL5               Next 24 notices
         DS    CL8                 Reserved
BCDREND  DS    AL1                 End of record indicator - x'7F'
BCDNEXT  DS    AL3                 Next notice directory (zero if end)
         MEND
         MACRO
**********************************************************************
* IKJZT303     TSO/E Broadcast Notices Message Record                *
**********************************************************************
         IKJZT303
BCMSG    DSECT
BCMLNG   DS    AL1                 Notice message length
BCMTEXT  DS    CL125               Notice message
         DS    CL3                 Reserved
         MEND
         MACRO
**********************************************************************
* IKJZT304     TSO/E Broadcast Mail Directory Record                 *
**********************************************************************
         IKJZT304
USDIR    DSECT
USDENTRY DS    0CL13
USDID    DS    CL7                 Userid
USDRBA   DS    AL3                 First message (zero if none)
USDEND   DS    AL3                 Last message (zero if none)
         DS    8CL13               Next 8 userids
         DS    CL8                 Reserved
USDREND  DS    AL1                 End of record indicator - x'7F'
USDNEXT  DS    AL3                 Next mail directory (zero if end)
         MEND
         MACRO
**********************************************************************
* IKJZT305     TSO/E Broadcast Mail Message Record                   *
**********************************************************************
         IKJZT305
USMSG    DSECT
USMLNG   DS    AL1                 Mail message length
USMTEXT  DS    CL125               Mail message
USMNEXT  DS    AL3                 Next mail message (zero if last)
         MEND
         MACRO
**********************************************************************
* ILRPART      Paging Activity Reference Table (PART)                *
**********************************************************************
         ILRPART
PART     DSECT
PARTIDEN DS    C'PART'
PARTSIZE DS    F
PARTEUSE DS    F
PARTCIR0 DS    A
PARTCIR1 DS    A
PARTCIR2 DS    A
PARTTPAR DS    0A
PARTDSNL DS    A
PARTPCTQ DS    A
PARTLCNT DS    H
PARTFLG1 DS    B
         DS    X
PARTNPCF DS    A
PARTNPCL DS    A
PARTTIME DS    F
         DS    CL32
PARTENTS EQU   *
*
PARTENT  DSECT
PAREPARE DS    A
PAREBRST DS    AL1
PAREIORN DS    AL1
         DS    XL2
PARETYPE DS    B
PAREPLPA EQU   X'80'
PARECOMM EQU   X'40'
PAREDPLX EQU   X'20'
PARELOCL EQU   X'10'
PAREBPF  EQU   X'08'
PARESPP  EQU   X'04'
PAREPD   EQU   X'02'
PAREDRN  EQU   X'01'
PAREFLG1 DS    B
PARENUSE EQU   X'80'
PARENVIO EQU   X'10'
PARENN   DS    H
PAREDEIB DS    A
PARESZSL DS    F
PARESLTA DS    F
PARERRCT DS    F
PAREIORB DS    A
PAREPATP DS    A
PAREPCTP DS    A
PAREEDBP DS    A
PAREUCBP DS    A(UCBOB)
PARETIOR DS    A
PARETIME DS    F
PARERQTM DS    F
         DS    H
PAREREQS DS    H
PAREFLG2 DS    B
PARELSLT DS    XL3
PAREOCTB DS    A
PAREMIGA DS    A
PARECADR DS    FL1
         DS    XL19
PARTELEN EQU   *-PARTENT
         MEND
         MACRO
**********************************************************************
* ISPFINIT     Initialize ISPF Environment                           *
**********************************************************************
&LABEL   ISPFINIT &ZDL=NO,         ZDL Variables                       X
               &ZDS=NO,            ZDS Variables                       X
               &ZERR=NO            ZERR Variables
*
**       Validate ISPF Environment
*
&LABEL   LOAD  EP=ISPQRY,ERRET=IIEXIT Load current ISPQRY module
         LR    R15,R0              ISPQRY address
         BALR  R14,R15             Invoke ISPQRY
         LTR   15,15               ISPF environment ?
         BNZ   IIEXIT              . No
         DELETE EP=ISPQRY          Delete current ISPQRY module
*
**       Load ISPF Interface Module (ISPLINK)
*
         LOAD  EP=ISPLINK,ERRET=IIEXIT Load current ISPLINK module
         ST    R0,ISPLINK@         Save ISPLINK address
*
**       Load TSO Service Facility Routine
*
         LOAD  EP=IKJEFTSR,ERRET=IIEXIT Load current IKJEFTSR module
         ST    R0,TSRADDR          Save IKJEFTSR address
         MVC   TSRLEN,=F'100'      IKJEFTSR length
         MVC   TSRFLAGS,=X'00000101' IKJEFTSR flags
*
**       Define ISPF System Variables
*
         VDEFINE NAMES=(DATAID),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(LISTID),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(MEMBER),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZAPPLID),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZCMD),FORMAT=CHAR,LENGTH=80
         VDEFINE NAMES=(ZDATE),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDATEF),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDATEFD),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDAY),FORMAT=CHAR,LENGTH=2
         VDEFINE NAMES=(ZEDLMSG),FORMAT=CHAR,LENGTH=80
         VDEFINE NAMES=(ZEDSMSG),FORMAT=CHAR,LENGTH=24
         VDEFINE NAMES=(ZJDATE),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZLANG),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZLOGO),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(ZMONTH),FORMAT=CHAR,LENGTH=2
         VDEFINE NAMES=(ZPLACE),FORMAT=CHAR,LENGTH=7
         VDEFINE NAMES=(ZPREFIX),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZSTDYEAR),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(ZSYSID),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZTDSELS),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(ZTDTOP),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZTIME),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(ZTIMEL),FORMAT=CHAR,LENGTH=11
         VDEFINE NAMES=(ZUDMVOL),FORMAT=CHAR,LENGTH=1
         VDEFINE NAMES=(ZUSER),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZYEAR),FORMAT=CHAR,LENGTH=2
         AIF   ('&ZDL' EQ 'NO').ZDLNO
         VDEFINE NAMES=(ZDLBLKSZ),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(ZDLCATNM),FORMAT=CHAR,LENGTH=44
         VDEFINE NAMES=(ZDLCDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDLDEV),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDLDSNTP),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDLDSORG),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(ZDLEDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDLEXT),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(ZDLLRECL),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(ZDLMIGR),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(ZDLRDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDLRECFM),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(ZDLSIZE),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZDLSPACU),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDLUSED),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(ZDLVOL),FORMAT=CHAR,LENGTH=6
.ZDLNO   ANOP
         AIF   ('&ZDS' EQ 'NO').ZDSNO
         VDEFINE NAMES=(ZDS#MEM),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDS#VOLS),FORMAT=CHAR,LENGTH=2
         VDEFINE NAMES=(ZDSBLK),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZDSCB1),FORMAT=CHAR,LENGTH=96
         VDEFINE NAMES=(ZDSCDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDSDC),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSDEVT),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSDIR),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSDIRA),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZDSDIRU),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSDSNT),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSEXTA),FORMAT=CHAR,LENGTH=7
         VDEFINE NAMES=(ZDSEXTU),FORMAT=CHAR,LENGTH=7
         VDEFINE NAMES=(ZDSLREC),FORMAT=CHAR,LENGTH=7
         VDEFINE NAMES=(ZDSMC),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSORG),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSPAGU),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDSPERU),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDSRDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDSRF),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZDSSC),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSSEQ),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(ZDSSPC),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZDSTOTA),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDSTOTU),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDSVOL),FORMAT=CHAR,LENGTH=6
         VDEFINE NAMES=(ZDSVTAB),FORMAT=CHAR,LENGTH=354
         VDEFINE NAMES=(ZDSXDATE),FORMAT=CHAR,LENGTH=10
         VDEFINE NAMES=(ZDS1EX),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDS2EX),FORMAT=CHAR,LENGTH=13
         VDEFINE NAMES=(ZDS2SPC),FORMAT=CHAR,LENGTH=8
.ZDSNO   ANOP
         AIF   ('&ZERR' EQ 'NO').ZERRNO
         VDEFINE NAMES=(ZERRALRM),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(ZERRHM),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZERRLM),FORMAT=CHAR,LENGTH=512
         VDEFINE NAMES=(ZERRMSG),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(ZERRSM),FORMAT=CHAR,LENGTH=24
.ZERRNO  ANOP
IIEXIT   EQU   *
         MEND
         MACRO
**********************************************************************
* ISPFTERM     Terminate ISPF Environment                            *
**********************************************************************
&LABEL   ISPFTERM
&LABEL   VDELETE NAMES=(*)         Delete all ISPF variables
         DELETE EP=ISPLINK         Delete current ISPLINK module
         DELETE EP=IKJEFTSR        Delete current IKJEFTSR module
         MEND
         MACRO
**********************************************************************
* ISPFVARS     Define ISPF Variables                                 *
**********************************************************************
&LABEL   ISPFVARS &ZDL=NO,         ZDL Variables                       X
               &ZDS=NO,            ZDS Variables                       X
               &ZERR=NO            ZERR Variables
*
**       Define ISPF System Variables
*
*BRIFADR  DS    F                   BRIF address
BRIFDSN  DS    CL044               BRIF data set name
BRIFDWA  DS    F                   BRIF dynamic work area
BRIFRECL DS    F                   BRIF record length
BRIFRECN DS    F                   BRIF record number
BRIFREM  DS    F                   BRIF remainder
DATAID   DS    CL008               ISPF Data Identifier
DOUBLE   DS    D                   Doubleword work area
FULL     DS    F                   Fullword work area
HALF     DS    H                   Halfword work area
ISPLINK@ DS    F                   ISPLINK address
LISTID   DS    CL008               ISPF List Identifier
MEMBER   DS    CL008               ISPF Member Name
TABLE    DS    CL008               ISPF Table Name
TSRABEND DS    F                   IKJEFTSR abend code
TSRADDR  DS    F                   IKJEFTSR address
TSRBUF   DS    CL133               IKJEFTSR buffer
TSRFLAGS DS    F                   IKJEFTSR flags
TSRLEN   DS    F                   IKJEFTSR length
TSRREASN DS    F                   IKJEFTSR reason code
TSRRETRN DS    F                   IKJEFTSR return code
W_CL016  DS    CL016               16 byte general work area
W_CL512  DS    CL512               512 byte general work area
ZAPPLID  DS    CL008               ISPF Application Identifier
ZCMD     DS    CL080               ISPF Command Line
ZDATE    DS    CL008               ISPF Date
ZDATEF   DS    CL008               ISPF Date
ZDATEFD  DS    CL008               ISPF Date
ZDAY     DS    CL002               ISPF Day (dd)
ZEDLMSG  DS    CL080               ISPF Generic Message - Long
ZEDSMSG  DS    CL024               ISPF Generic Message - Short
ZJDATE   DS    CL006               ISPF Julian Date (yy.ddd)
ZLANG    DS    CL008               ISPF Session Language
ZLOGO    DS    CL003               ISPF Logo (YES/NO)
ZMONTH   DS    CL002               ISPF Month (mm)
ZPLACE   DS    CL007               ISPF Command Line Placement
ZPREFIX  DS    CL008               ISPF Prefix
ZSCREEND DS    CL004               ISPF Screen Depth
ZSCREENW DS    CL004               ISPF Screen Width
ZSTDYEAR DS    CL004               ISPF Year (yyyy)
ZSYSID   DS    CL008               ISPF System Name
ZTDSELS  DS    CL004               ISPF Selections
ZTDTOP   DS    CL006               ISPF CRP of most recent display
ZTIME    DS    CL005               ISPF Time (hh:mm)
ZTIMEL   DS    CL011               ISPF Time (hh:mm:ss:th)
ZUDMVOL  DS    CL001               Multivolume 'Y' 'N'
ZUSER    DS    CL008               ISPF User Identification
ZYEAR    DS    CL002               ISPF Year (yy)
         AIF   ('&ZDL' EQ 'NO').ZDLNO
ZDLBLKSZ DS    CL005               Block size
ZDLCATNM DS    CL044               Catalog name
ZDLCDATE DS    CL010               Creation date
ZDLDEV   DS    CL008               Device type
ZDLDSNTP DS    CL008               Dsname type 'PDS' 'LIBRARY' ' '
ZDLDSORG DS    CL004               Data set organization
ZDLEDATE DS    CL010               Expiration date
ZDLEXT   DS    CL003               Number of extents used
ZDLLRECL DS    CL005               Logical record length
ZDLMIGR  DS    CL003               Migrated 'YES' 'NO'
ZDLRDATE DS    CL010               Last referenced date
ZDLRECFM DS    CL005               Record format
ZDLSIZE  DS    CL006               Data set size in tracks
ZDLSPACU DS    CL010               Space units CYLINDERS,MEGABYTES...
ZDLUSED  DS    CL003               Percentage of used tracks/pages
ZDLVOL   DS    CL006               Volume serial
.ZDLNO   ANOP
         AIF   ('&ZDS' EQ 'NO').ZDSNO
ZDS#MEM  DS    CL013               Number of members
ZDS#VOLS DS    CL002               Number of volumes
ZDSBLK   DS    CL006               Block size
ZDSCB1   DS    CL096               Format 1 data control block
ZDSCDATE DS    CL010               Creation date
ZDSDC    DS    CL008               Data class
ZDSDEVT  DS    CL008               Device type
ZDSDIR   DS    CL008               Directory blocks (PDSE)
ZDSDIRA  DS    CL006               Allocated directory blocks
ZDSDIRU  DS    CL008               Used directory blocks
ZDSDSNT  DS    CL008               Data set name type
ZDSEXTA  DS    CL007               Allocated extents
ZDSEXTU  DS    CL007               Used extents
ZDSLREC  DS    CL007               Logical record length
ZDSMC    DS    CL008               Management class
ZDSORG   DS    CL008               Data set organization
ZDSPAGU  DS    CL013               Pages used (PDSE)
ZDSPERU  DS    CL013               Percent used (PDSE)
ZDSRDATE DS    CL010               Referenced date
ZDSRF    DS    CL006               Record format
ZDSSC    DS    CL008               Storage class
ZDSSEQ   DS    CL004               Compressible YES/NO
ZDSSPC   DS    CL008               Primary space units
ZDSTOTA  DS    CL013               Allocated space units
ZDSTOTU  DS    CL013               Used space units
ZDSVOL   DS    CL006               First or only volume
ZDSVTAB  DS    CL354               Volume table (all volumes)
ZDSXDATE DS    CL010               Expiration date
ZDS1EX   DS    CL013               Primary space allocation
ZDS2EX   DS    CL013               Secondary space allocation
ZDS2SPC  DS    CL008               Secondary space units
.ZDSNO   ANOP
         AIF   ('&ZERR' EQ 'NO').ZERRNO
ZERRALRM DS    CL003               Alarm 'YES' 'NO'
ZERRHM   DS    CL008               Help panel name
ZERRLM   DS    CL512               Long-message text
ZERRMSG  DS    CL008               Message ID
ZERRSM   DS    CL024               Short-message text
.ZERRNO  ANOP
         MEND
         MACRO
**********************************************************************
* LMCLOSE      Close a Data Set                                      *
**********************************************************************
&LABEL   LMCLOSE &DATAID=DATAID    Data Identifier
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMCLOSE ',                                          X
               &DATAID),                                               X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMDFREE      Free a Data Set List ID                               *
**********************************************************************
&LABEL   LMDFREE &LISTID=LISTID    List Identifier
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMDFREE ',                                          X
               &LISTID),                                               X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMDINIT      Initialize a Data Set List                            *
**********************************************************************
&LABEL   LMDINIT &LISTID=LISTID,   List Identifier                     X
               &LEVEL=0,           Dataset-level                       X
               &VOLUME=            Volume
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMDINIT ',                                          X
               =C'&LISTID ',                                           X
               &LEVEL),                                                X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMDLIST      List Data Sets                                        *
**********************************************************************
&LABEL   LMDLIST &LISTID=LISTID,   List Identifier                     X
               &OPTION=LIST,       LIST/FREE/SAVE                      X
               &DATASET=,          Dataset                             X
               &STATS=NO,          YES/NO                              X
               &GROUP=0            Group
         AIF   ('&OPTION' EQ 'LIST').OPTIONOK,                         X
               ('&OPTION' EQ 'FREE').OPTIONOK,                         X
               ('&OPTION' EQ 'SAVE').OPTIONOK
         MNOTE 8,'LMDLIST Invalid OPTION=&OPTION - LIST/FREE/SAVE'
         AGO   .MEND
.OPTIONOK ANOP
         AIF   ('&STATS' EQ 'YES').STATSOK,                            X
               ('&STATS' EQ 'NO').STATSOK
         MNOTE 8,'LMDLIST Invalid STATS=&STATS - YES/NO'
         AGO   .MEND
.STATSOK ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMDLIST ',                                          X
               &LISTID,                                                X
               =C'&OPTION ',                                           X
               =C'&DATASET ',                                          X
               =C'&STATS '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMFREE       Free Data Set from its Association with Data ID       *
**********************************************************************
&LABEL   LMFREE &DATAID=DATAID     Data Identifier
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMFREE ',                                           X
               &DATAID),                                               X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMINIT       Generate a Data ID for a Data Set                     *
**********************************************************************
&LABEL   LMINIT &DATAID=DATAID,    Data Identifier                     X
               &DATASET=0,         Dataset-name                        X
               &DDNAME=0,          Dd-name                             X
               &VOLUME=0,          Volume                              X
               &PASSWORD=0,        Password                            X
               &ENQ=SHR            Enqueue
         AIF   ('&ENQ' EQ 'SHR').ENQOK,                                X
               ('&ENQ' EQ 'EXCLU').ENQOK,                              X
               ('&ENQ' EQ 'SHRW').ENQOK,                               X
               ('&ENQ' EQ 'MOD').ENQOK
         MNOTE 8,'LMINIT Invalid ENQ=&ENQ - SHR/EXCLU/SHRW/MOD'
         AGO   .MEND
.ENQOK   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMINIT ',                                           X
               =C'&DATAID ',                                           X
               0,0,0,0,0,0,                                            X
               &DATASET,                                               X
               &DDNAME,                                                X
               &VOLUME,                                                X
               &PASSWORD,                                              X
               =C'&ENQ '),                                             X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMMLIST      List a Library's Members                              *
**********************************************************************
&LABEL   LMMLIST &DATAID=DATAID,   Data Identifier                     X
               &OPTION=LIST,       LIST/FREE/SAVE                      X
               &MEMBER=,           Member-var                          X
               &STATS=NO           YES/NO
         AIF   ('&OPTION' EQ 'LIST').OPTIONOK,                         X
               ('&OPTION' EQ 'FREE').OPTIONOK,                         X
               ('&OPTION' EQ 'SAVE').OPTIONOK
         MNOTE 8,'LMMLIST Invalid OPTION=&OPTION - LIST/FREE/SAVE'
         AGO   .MEND
.OPTIONOK ANOP
         AIF   ('&STATS' EQ 'YES').STATSOK,                            X
               ('&STATS' EQ 'NO').STATSOK
         MNOTE 8,'LMMLIST Invalid STATS=&STATS - YES/NO'
         AGO   .MEND
.STATSOK ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMMLIST ',                                          X
               &DATAID,                                                X
               =C'&OPTION ',                                           X
               =C'&MEMBER ',                                           X
               =C'&STATS '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* LMOPEN       Open a Data Set                                       *
**********************************************************************
&LABEL   LMOPEN &DATAID=DATAID,    Data Identifier                     X
               &OPTION=INPUT       Option
         AIF   ('&OPTION' EQ 'INPUT').OPTIONOK,                        X
               ('&OPTION' EQ 'OUTPUT').OPTIONOK
         MNOTE 8,'LMOPEN Invalid OPTION=&OPTION - INPUT/OUTPUT'
         AGO   .MEND
.OPTIONOK ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LMOPEN ',                                           X
               &DATAID,                                                X
               =C'&OPTION '),                                          X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
         TBLMAJOR
**********************************************************************
* TBLMAJOR     Major Enqueue Table                                   *
**********************************************************************
 DC CL1'N',CL8'!CANDLE ',CL32'Candle-Omegamon'
MAJORLEN EQU   *-TBLMAJOR          MAJOR table entry length
 DC CL1'N',CL8'$CACHENV',CL32'Candle-Omegamon'
 DC CL1'N',CL8'@MVS2   ',CL32'CA-ENF'
 DC CL1'N',CL8'$PDSFAST',CL32'SEA-PDSFAST'
 DC CL1'N',CL8'$RXQUEUE',CL32'CA-ASM2_Reload_Queue'
 DC CL1'N',CL8'ABENDAID',CL32'CW-AbendAid'
 DC CL1'N',CL8'ABENDDAM',CL32'CW-AbendAid'
 DC CL1'N',CL8'ABENDSMF',CL32'CW-AbendAid'
 DC CL1'N',CL8'ACFVSAM ',CL32'CA-ACF2'
 DC CL1'N',CL8'ACIFRDR ',CL32'CA-DISPATCH_ACIF_External'
 DC CL1'N',CL8'ADABASLX',CL32'ADABAS'
 DC CL1'N',CL8'ADRPRDCT',CL32'CA-LIBRARIAN_Master'
 DC CL1'N',CL8'AICVMSTR',CL32'INFOHUB'
 DC CL1'N',CL8'AMFORDB2',CL32'EDA/SQL'
 DC CL1'Y',CL8'ANFGDATS',CL32'IP/Printway_Queue_Dataset'
 DC CL1'N',CL8'ANFMIOCB',CL32'IP/Printway'
 DC CL1'Y',CL8'ARCDSN  ',CL32'DFSMS/HSM_Dataset'
 DC CL1'Y',CL8'ARCENQG ',CL32'DFSMS/HSM_Dataset'
 DC CL1'N',CL8'ARCGPA  ',CL32'DFSMS/HSM'
 DC CL1'N',CL8'ARCHBKLG',CL32'CA-ASM2_Backup_Log'
 DC CL1'N',CL8'ARCLOG  ',CL32'CA-ASM2_Activity_Log'
 DC CL1'N',CL8'ARCPVOL ',CL32'DFSMS/HSM'
 DC CL1'N',CL8'ASM     ',CL32'CA-ASM2_Tape'
 DC CL1'N',CL8'ASM2    ',CL32'CA-ASM2'
 DC CL1'N',CL8'ASM2.IXR',CL32'CA-ASM2_VSAM_Dataset_Reload'
 DC CL1'N',CL8'ASM2IXR ',CL32'CA-ASM2'
 DC CL1'N',CL8'ASM2SYSA',CL32'CA-ASM2_Archive_Volume'
 DC CL1'N',CL8'ASM2SYSB',CL32'CA-ASM2_Backup_Volume'
 DC CL1'N',CL8'ASTEXMM ',CL32'CA-ASTEX_FSR'
 DC CL1'N',CL8'ATMSVDB ',CL32'CA-OPS_Status_Variable'
 DC CL1'N',CL8'BBI     ',CL32'BB-MAINVIEW'
 DC CL1'N',CL8'BBISS   ',CL32'BB-MAINVIEW'
 DC CL1'N',CL8'BLGAPI  ',CL32'I/M_API'
 DC CL1'N',CL8'BLGDICTN',CL32'I/M_Dictionary'
 DC CL1'N',CL8'BLGMAIL ',CL32'I/M_Mail'
 DC CL1'N',CL8'BLGPMFPU',CL32'I/M_Panel'
 DC CL1'N',CL8'BLGRNID ',CL32'I/M_Record'
 DC CL1'N',CL8'BLGSEQN ',CL32'I/M_Record Key'
 DC CL1'N',CL8'BLGUT4  ',CL32'I/M_SDLDS'
 DC CL1'N',CL8'BLGVCGZR',CL32'I/M_Cognizer'
 DC CL1'N',CL8'BLXCAS  ',CL32'I/M_Server'
 DC CL1'N',CL8'BLXDAS  ',CL32'I/M'
 DC CL1'N',CL8'BLXDASDS',CL32'I/M'
 DC CL1'N',CL8'BLXLCDS ',CL32'I/M'
 DC CL1'N',CL8'BLXMSDA ',CL32'I/M_APPC'
 DC CL1'N',CL8'BLXSNAP ',CL32'I/M'
 DC CL1'N',CL8'BLXSPCAS',CL32'I/M'
 DC CL1'N',CL8'BLXVSI  ',CL32'I/M'
 DC CL1'N',CL8'BMCSRDSN',CL32'BMC-Registry'
 DC CL1'N',CL8'BUNDL   ',CL32'CA-BUNDL'
 DC CL1'N',CL8'CA-AM   ',CL32'CA-ASM2_Control_Blocks'
 DC CL1'N',CL8'CA-ENF  ',CL32'CA-ENF'
 DC CL1'N',CL8'CA-GNMP ',CL32'CA-ASM2'
 DC CL1'N',CL8'CA-XCF  ',CL32'CA-ENF'
 DC CL1'Y',CL8'CADB2   ',CL32'CA-Platinum'
 DC CL1'N',CL8'CAIENQBL',CL32'CA-EZTEST'
 DC CL1'N',CL8'CANDLE  ',CL32'Candle'
 DC CL1'N',CL8'CANDLEDD',CL32'Candle-Omegamon'
 DC CL1'N',CL8'CANDLENQ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'CAOPERA ',CL32'CA-Opera'
 DC CL1'Y',CL8'CAOUPRTA',CL32'CA-Symdump_Dataset'
 DC CL1'N',CL8'CASCHDSC',CL32'CA-Scheduler/Smart_Console'
 DC CL1'N',CL8'CASNMLGR',CL32'CA-CAICCI'
 DC CL1'N',CL8'CASNMNET',CL32'CA-ENF'
 DC CL1'N',CL8'CASULLCK',CL32'CA-APCDOC'
 DC CL1'N',CL8'CASUSLCK',CL32'CA-APCDOC'
 DC CL1'N',CL8'CA1     ',CL32'CA-1'
 DC CL1'N',CL8'CA11CMT ',CL32'CA-11_CMT'
 DC CL1'N',CL8'CA11JEHF',CL32'CA-11_JEHF'
 DC CL1'N',CL8'CA7SC   ',CL32'CA-7/SmartConsole'
 DC CL1'N',CL8'CBKCICS ',CL32'CICS_BAC'
 DC CL1'N',CL8'CEEM@MOU',CL32'LE_MSGFILE'
 DC CL1'N',CL8'CFXCFMON',CL32'CF_Monitor'
 DC CL1'N',CL8'CNMINIT ',CL32'Netview'
 DC CL1'N',CL8'COMPLETE',CL32'COM-PLETE'
 DC CL1'N',CL8'COMPUWRE',CL32'CW'
 DC CL1'N',CL8'COSMOS  ',CL32'CA-Dynam/TLMS'
 DC CL1'N',CL8'CPWKFXTC',CL32'CW-AbendAid/FX'
 DC CL1'Y',CL8'CPXCSLQN',CL32'CA-Symdump_CSL_Dataset'
 DC CL1'N',CL8'CSAAMGR ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'CSLENQBL',CL32'CA-EZTEST'
 DC CL1'N',CL8'CSTADSYS',CL32'Supra'
 DC CL1'N',CL8'CTLIELEM',CL32'CA-ENDEVOR_Element'
 DC CL1'Y',CL8'CTLIMSTR',CL32'CA-ENDEVOR_Master_Control_Dataset'
 DC CL1'N',CL8'CTLIPROC',CL32'CA-ENDEVOR'
 DC CL1'N',CL8'CTS     ',CL32'CA-1'
 DC CL1'N',CL8'CWCATALG',CL32'CW-AbendAid/FX'
 DC CL1'Y',CL8'DADSBI  ',CL32'CA-DADSPlus_Batch_Interface_Dataset'
 DC CL1'Y',CL8'DCONTROL',CL32'CA-Detector_Dataset'
 DC CL1'N',CL8'DFHSTRM ',CL32'CICS_Log_Stream'
 DC CL1'N',CL8'DIFOPEN ',CL32'DTS-DIF'
 DC CL1'N',CL8'DRS31SYS',CL32'LRS-DRS'
 DC CL1'N',CL8'DSAMANGR',CL32'CA-ASM2_Archive/Backup'
 DC CL1'N',CL8'DSAPREFX',CL32'CA-ASM2_Date_Time_Stamp'
 DC CL1'N',CL8'DSARACT ',CL32'CA-DISPATCH_External_Archive'
 DC CL1'N',CL8'DSARREQ ',CL32'CA-DISPATCH_External_Archive'
 DC CL1'N',CL8'DSD1ACT ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD1REQ ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD2ACT ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD2REQ ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD3ACT ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD3REQ ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD4ACT ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSD4REQ ',CL32'CA-DISPATCH_External_Distribution'
 DC CL1'N',CL8'DSEXACT ',CL32'CA-DISPATCH_External_Extract'
 DC CL1'N',CL8'DSEXREQ ',CL32'CA-DISPATCH_External_Extract'
 DC CL1'Y',CL8'DSPURI01',CL32'IBM-IMS_DBMS_RECON_Dataset'
 DC CL1'N',CL8'DSSI0001',CL32'DSSI'
 DC CL1'N',CL8'DS50DIST',CL32'CA-DISPATCH'
 DC CL1'N',CL8'DS50OVDB',CL32'CA-DISPATCH'
 DC CL1'N',CL8'DS50OVSP',CL32'CA-DISPATCH'
 DC CL1'N',CL8'DS50QRLS',CL32'CA-DISPATCH'
 DC CL1'Y',CL8'DTMJBMQ ',CL32'MVS_Solutions-Thruput_Manager_Dataset'
 DC CL1'N',CL8'DVG221@@',CL32'Netview_FTP'
 DC CL1'N',CL8'DVG221QH',CL32'Netview_FTP'
 DC CL1'N',CL8'EBCASB  ',CL32'CA-Deliver'
 DC CL1'N',CL8'ECMCONFL',CL32'CA-MIM'
 DC CL1'N',CL8'ECMGLOBL',CL32'CA-MIM'
 DC CL1'N',CL8'EDIDSN  ',CL32'CA-MIM'
 DC CL1'N',CL8'EDIGLOBL',CL32'CA-MIM'
 DC CL1'N',CL8'EMCSERVR',CL32'EMC-Server'
 DC CL1'N',CL8'ENDEVOR ',CL32'CA-ENDEVOR_Utility'
 DC CL1'N',CL8'ENF     ',CL32'CA-ENF'
 DC CL1'N',CL8'ENFDBASE',CL32'CA-ENF'
 DC CL1'N',CL8'ENQ$AVRS',CL32'SEA-$AVRS'
 DC CL1'N',CL8'EPILOG  ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'EPVTMA I',CL32'Candle-Omegamon'
 DC CL1'N',CL8'ESFCHKPT',CL32'CA-Spool_Checkpoint'
 DC CL1'N',CL8'ESFSSVT ',CL32'CA-Spool_Initialization'
 DC CL1'N',CL8'ETCLEAN ',CL32'CA-eTrust'
 DC CL1'N',CL8'ETEAS   ',CL32'Candle-Omegamon'
 DC CL1'Y',CL8'FDRABR  ',CL32'IDP-FDR/ABR'
 DC CL1'N',CL8'FDRABRV ',CL32'IDP-FDR/ABR'
 DC CL1'N',CL8'FILEAID ',CL32'CW-FileAid'
 DC CL1'N',CL8'FILEMASK',CL32'????'
 DC CL1'N',CL8'FOCUS   ',CL32'Focus'
 DC CL1'N',CL8'GDI     ',CL32'CA-MIM'
 DC CL1'N',CL8'GIMISCVE',CL32'SMPE'
 DC CL1'N',CL8'GIMSMP  ',CL32'SMPE'
 DC CL1'N',CL8'GSVXQNAM',CL32'CA-SYSVIEW/E_Security'
 DC CL1'N',CL8'GTALOCAL',CL32'CA-MIA'
 DC CL1'N',CL8'HCIQNAME',CL32'CW-AbendAid/FX'
 DC CL1'N',CL8'H2000000',CL32'Candle-Omegamon'
 DC CL1'N',CL8'IAMCRT  ',CL32'IDP-IAM'
 DC CL1'N',CL8'IAMENQ  ',CL32'IDP-IAM'
 DC CL1'N',CL8'IBMNCCF ',CL32'Netview'
 DC CL1'N',CL8'ICEHIPER',CL32'DFSORT_Hiperspace'
 DC CL1'N',CL8'IDMSCV  ',CL32'CA-IDMS_CV'
 DC CL1'N',CL8'IDMSDC  ',CL32'CA-IDMS_DC'
 DC CL1'Y',CL8'IGDCDS  ',CL32'SMS_Control_Dataset'
 DC CL1'Y',CL8'IGDCDSXS',CL32'SMS_Control_Dataset'
 DC CL1'N',CL8'IMSKILL ',CL32'Focus'
 DC CL1'N',CL8'IMSSINK ',CL32'Focus'
 DC CL1'N',CL8'INFOMAN ',CL32'GEAC-Information_Expert'
 DC CL1'N',CL8'INFOPAC ',CL32'ViewDirect'
 DC CL1'N',CL8'INGPLEX ',CL32'System_Automation'
 DC CL1'N',CL8'INGXSG  ',CL32'System_Automation'
 DC CL1'N',CL8'INGXSG00',CL32'System_Automation'
 DC CL1'Y',CL8'INTERTST',CL32'CA-InterTest_Dataset'
 DC CL1'N',CL8'IOAP500 ',CL32'CONTROL-D'
 DC CL1'N',CL8'IOBSNMP ',CL32'OSASNMP'
 DC CL1'N',CL8'IPCPCDS ',CL32'IPCP'
 DC CL1'N',CL8'IPGMGREX',CL32'CA-GSS_ISET'
 DC CL1'N',CL8'IXRA    ',CL32'CA-ASM2_IXR'
 DC CL1'N',CL8'JOBHIST ',CL32'CA-JOBTRAC_History'
 DC CL1'N',CL8'KCNDLCFQ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'KO2     ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'KO200071',CL32'Candle-Omegamon'
 DC CL1'N',CL8'LANDMARK',CL32'ASG-TMON'
 DC CL1'N',CL8'LCMMAIN ',CL32'STK-LCM'
 DC CL1'N',CL8'LIBRLAM ',CL32'CA-Librarian'
 DC CL1'N',CL8'LMSA0AR ',CL32'LMS'
 DC CL1'N',CL8'LMSLOCAL',CL32'LMS'
 DC CL1'Y',CL8'LSERVDSN',CL32'CA-L-Serv_Dataset'
 DC CL1'N',CL8'MFDDINFO',CL32'CW-AbendAid'
 DC CL1'N',CL8'MIGRATOR',CL32'CA-Platinum'
 DC CL1'N',CL8'MIMCKP  ',CL32'CA-MIM'
 DC CL1'N',CL8'MIMGLOBL',CL32'CA-MIM_Control'
 DC CL1'N',CL8'MIMLOCAL',CL32'CA-MIM'
 DC CL1'N',CL8'MSIGLOBL',CL32'CA-MIM'
 DC CL1'N',CL8'MSP     ',CL32'HARBOR'
 DC CL1'N',CL8'NETMAN  ',CL32'CA-Netman'
 DC CL1'N',CL8'NMSSID  ',CL32'Solve'
 DC CL1'N',CL8'NMVFS   ',CL32'Solve'
 DC CL1'N',CL8'NPM     ',CL32'NPM'
 DC CL1'N',CL8'NTMNBRD ',CL32'CA-Netman'
 DC CL1'N',CL8'NVIVSAMP',CL32'CA-N-Vision_Record'
 DC CL1'Y',CL8'NVIVSAMS',CL32'CA-N-Vision_Dataset'
 DC CL1'N',CL8'OCENQUES',CL32'Candle-Omegamon'
 DC CL1'N',CL8'OMCI    ',CL32'Candle-Omegamon'
 DC CL1'N',CL8'OPS/MVS ',CL32'CA-OPS/MVS'
 DC CL1'N',CL8'ORACLE  ',CL32'ORACLE'
 DC CL1'Y',CL8'OTPRMLIB',CL32'OTS-Tapecopy_Parmlib_Dataset'
 DC CL1'N',CL8'PANVALET',CL32'CA-Panvalet'
 DC CL1'N',CL8'PDSMAN  ',CL32'CA-PDSMAN'
 DC CL1'N',CL8'PDSMENQ ',CL32'CA-PDSMAN'
 DC CL1'N',CL8'PDSMTITL',CL32'CA-PDSMAN'
 DC CL1'N',CL8'PDT$DIN ',CL32'CA-Detector'
 DC CL1'N',CL8'PDTDINCC',CL32'CA-Detector'
 DC CL1'N',CL8'PDVSFILE',CL32'CW-AbendAid'
 DC CL1'N',CL8'PLATINUM',CL32'CA-Platinum'
 DC CL1'Y',CL8'PSACNTL ',CL32'CA-SQLAnalyzer_Dataset'
 DC CL1'N',CL8'PSNCPSNC',CL32'MTECH-PSYNC'
 DC CL1'N',CL8'PSPOPX21',CL32'SW-VSAM_Assist'
 DC CL1'N',CL8'PSPOPX22',CL32'SW-VSAM_Assist'
 DC CL1'N',CL8'PXMINI  ',CL32'CA-XManager'
 DC CL1'N',CL8'PXMTSS  ',CL32'CA-XManager'
 DC CL1'N',CL8'QSYSARCH',CL32'TWS'
 DC CL1'N',CL8'QUIKSTRF',CL32'QuickStart'
 DC CL1'N',CL8'QUIKSTRT',CL32'QuickStart'
 DC CL1'N',CL8'RAIJVTCB',CL32'????'
 DC CL1'N',CL8'RAMIS   ',CL32'CA-RAMIS'
 DC CL1'N',CL8'RAMISIO ',CL32'CA-RAMIS'
 DC CL1'N',CL8'RAMISLOG',CL32'CA-RAMIS'
 DC CL1'N',CL8'RAMISOP ',CL32'CA-RAMIS'
 DC CL1'N',CL8'RBP0200 ',CL32'CA-Platinum'
 DC CL1'N',CL8'RC/EDIT ',CL32'CA-Platinum-RC/EDIT'
 DC CL1'N',CL8'RMOCKPT ',CL32'CA-Deliver_Checkpoint'
 DC CL1'Y',CL8'RMOUPD  ',CL32'CA-Deliver_Database_Dataset'
 DC CL1'N',CL8'ROSACT  ',CL32'CA-ROSCOE'
 DC CL1'N',CL8'ROSCLIBE',CL32'CA-ROSCOE'
 DC CL1'N',CL8'SA$CIN  ',CL32'CA-Detector'
 DC CL1'N',CL8'SA$COE  ',CL32'CA-Detector'
 DC CL1'N',CL8'SAGCTIMO',CL32'COM-PLETE'
 DC CL1'Y',CL8'SARACT  ',CL32'CA-VIEW_Accounting_Dataset'
 DC CL1'N',CL8'SARPAC  ',CL32'CA-VIEW'
 DC CL1'N',CL8'SARSTC  ',CL32'CA-VIEW'
 DC CL1'Y',CL8'SARUPD  ',CL32'CA-VIEW_Database_Dataset'
 DC CL1'N',CL8'SASDATA ',CL32'SAS-SAS'
 DC CL1'N',CL8'SDSI    ',CL32'CA-MIM'
 DC CL1'Y',CL8'SERLOG  ',CL32'Logrec_Dataset'
 DC CL1'N',CL8'SIBIXFP ',CL32'IXFP'
 DC CL1'Y',CL8'SPFDSN  ',CL32'ISPF_Dataset'
 DC CL1'Y',CL8'SPFEDIT ',CL32'ISPF_Edit_Dataset'
 DC CL1'N',CL8'SPFPAN  ',CL32'CA-Panvalet_Member'
 DC CL1'N',CL8'SPFUSER ',CL32'ISPF_User'
 DC CL1'N',CL8'SPZAPLIB',CL32'Superzap'
 DC CL1'N',CL8'SRSBATCH',CL32'RA-Smart/RESTART'
 DC CL1'Y',CL8'STKALSQN',CL32'STK-SILO_Database_Dataset'
 DC CL1'N',CL8'STKENQNM',CL32'STK-SILO'
 DC CL1'N',CL8'STROBE-B',CL32'CW-Strobe'
 DC CL1'N',CL8'STROBECV',CL32'CW-Strobe'
 DC CL1'N',CL8'SYSAOP  ',CL32'Printserver'
 DC CL1'N',CL8'SYSCATLG',CL32'Catalog'
 DC CL1'N',CL8'SYSCBR  ',CL32'OAM'
 DC CL1'Y',CL8'SYSDSN  ',CL32'System_Dataset'
 DC CL1'N',CL8'SYSDSNSY',CL32'VTAM'
 DC CL1'N',CL8'SYSIEA01',CL32'Dump'
 DC CL1'N',CL8'SYSIEFSD',CL32'Allocation'
 DC CL1'Y',CL8'SYSIEWLP',CL32'Linkage_Editor_Dataset'
 DC CL1'Y',CL8'SYSIGGV1',CL32'Master_Catalog_Dataset'
 DC CL1'Y',CL8'SYSIGGV2',CL32'User_Catalog_Dataset'
 DC CL1'N',CL8'SYSIKJBC',CL32'TSOE_Broadcast'
 DC CL1'N',CL8'SYSIKJUA',CL32'TSOE_UADS'
 DC CL1'N',CL8'SYSNFSC ',CL32'Unix_System_Services_-_NSF'
 DC CL1'N',CL8'SYSOSR02',CL32'OTIS/OAM'
 DC CL1'Y',CL8'SYSSMF01',CL32'SMF_Dataset'
 DC CL1'N',CL8'SYSVSAM ',CL32'VSAM'
 DC CL1'N',CL8'SYSVTOC ',CL32'VTOC'
 DC CL1'N',CL8'SYSZ-TSS',CL32'CA-Top_Secret_Initialization'
 DC CL1'N',CL8'SYSZ#SSI',CL32'SSI'
 DC CL1'N',CL8'SYSZALCF',CL32'IEFAUTO'
 DC CL1'N',CL8'SYSZAPPC',CL32'APPC'
 DC CL1'N',CL8'SYSZARC ',CL32'DFSMS/HSM'
 DC CL1'N',CL8'SYSZASCH',CL32'ASCH'
 DC CL1'N',CL8'SYSZATR ',CL32'Logger'
 DC CL1'N',CL8'SYSZATS ',CL32'Tape'
 DC CL1'N',CL8'SYSZAVM ',CL32'AVM'
 DC CL1'N',CL8'SYSZBDT ',CL32'BDT'
 DC CL1'N',CL8'SYSZBPX ',CL32'BPX'
 DC CL1'N',CL8'SYSZCAXW',CL32'CAXW'
 DC CL1'N',CL8'SYSZCBR ',CL32'OAM'
 DC CL1'N',CL8'SYSZCMDS',CL32'Commands'
 DC CL1'N',CL8'SYSZCOMM',CL32'GRS'
 DC CL1'N',CL8'SYSZCSD ',CL32'CSD'
 DC CL1'N',CL8'SYSZCSV ',CL32'CSV'
 DC CL1'N',CL8'SYSZCT  ',CL32'Component_Trace'
 DC CL1'Y',CL8'SYSZDAE ',CL32'DAE_Dataset'
 DC CL1'N',CL8'SYSZDFH ',CL32'CICS'
 DC CL1'Y',CL8'SYSZDRK ',CL32'TWS'
 DC CL1'N',CL8'SYSZDSCB',CL32'DSCB'
 DC CL1'Y',CL8'SYSZDSN ',CL32'HFS_Dataset'
 DC CL1'N',CL8'SYSZDSN3',CL32'DB2'
 DC CL1'N',CL8'SYSZDTS2',CL32'DTS'
 DC CL1'N',CL8'SYSZDTS3',CL32'DTS'
 DC CL1'N',CL8'SYSZDTS4',CL32'DTS'
 DC CL1'N',CL8'SYSZDTS5',CL32'DTS'
 DC CL1'N',CL8'SYSZEC16',CL32'Purge'
 DC CL1'N',CL8'SYSZERB ',CL32'RMF'
 DC CL1'N',CL8'SYSZGGLG',CL32'BDAM'
 DC CL1'N',CL8'SYSZGSYS',CL32'Sysplex_Group'
 DC CL1'N',CL8'SYSZGTF1',CL32'GTF'
 DC CL1'N',CL8'SYSZIAT ',CL32'JES3_Checkpoint'
 DC CL1'N',CL8'SYSZIGDI',CL32'SMS'
 DC CL1'N',CL8'SYSZIGGI',CL32'TSB'
 DC CL1'N',CL8'SYSZIGW0',CL32'PDSE'
 DC CL1'N',CL8'SYSZIGW1',CL32'PDSE'
 DC CL1'N',CL8'SYSZIHV ',CL32'ESCON_Manager'
 DC CL1'N',CL8'SYSZIKTC',CL32'TSOE'
 DC CL1'N',CL8'SYSZILRD',CL32'Page'
 DC CL1'N',CL8'SYSZIO  ',CL32'PAGEDEL_Command'
 DC CL1'N',CL8'SYSZIOA ',CL32'????'
 DC CL1'N',CL8'SYSZIOS ',CL32'IOS'
 DC CL1'N',CL8'SYSZISTC',CL32'Restart'
 DC CL1'N',CL8'SYSZIXFP',CL32'IXFP'
 DC CL1'N',CL8'SYSZJES2',CL32'JES2_Checkpoint'
 DC CL1'N',CL8'SYSZJWTP',CL32'Job_Step_Messages'
 DC CL1'N',CL8'SYSZLLA1',CL32'LLACOPY'
 DC CL1'N',CL8'SYSZLOGR',CL32'Logrec'
 DC CL1'N',CL8'SYSZMCS ',CL32'MCS'
 DC CL1'N',CL8'SYSZNIP ',CL32'NIP'
 DC CL1'Y',CL8'SYSZOPEN',CL32'Opening_Dataset'
 DC CL1'N',CL8'SYSZPCCB',CL32'PCCB'
 DC CL1'N',CL8'SYSZPGAD',CL32'PAGEADD_Command'
 DC CL1'N',CL8'SYSZPGDL',CL32'PAGEDEL Command'
 DC CL1'Y',CL8'SYSZPSWD',CL32'Password_Dataset'
 DC CL1'N',CL8'SYSZRACF',CL32'RACF'
 DC CL1'N',CL8'SYSZRAC2',CL32'RACF'
 DC CL1'N',CL8'SYSZRBMF',CL32'RMF'
 DC CL1'N',CL8'SYSZRBZZ',CL32'RMF'
 DC CL1'N',CL8'SYSZREUS',CL32'????'
 DC CL1'N',CL8'SYSZRMM ',CL32'RMM'
 DC CL1'N',CL8'SYSZRPLW',CL32'Catalog'
 DC CL1'N',CL8'SYSZSDO ',CL32'DLF'
 DC CL1'N',CL8'SYSZSDSF',CL32'SDSF'
 DC CL1'N',CL8'SYSZSMF1',CL32'SMF_Buffer'
 DC CL1'N',CL8'SYSZSPI ',CL32'SPI'
 DC CL1'N',CL8'SYSZSVC ',CL32'SVC_Table'
 DC CL1'N',CL8'SYSZTCPI',CL32'TCPIP'
 DC CL1'N',CL8'SYSZTIOT',CL32'TIOT'
 DC CL1'N',CL8'SYSZTRC ',CL32'System_Trace'
 DC CL1'N',CL8'SYSZTSOE',CL32'TSOE'
 DC CL1'N',CL8'SYSZUSRL',CL32'User_Labels'
 DC CL1'N',CL8'SYSZVARY',CL32'Reconfiguration_Command'
 DC CL1'N',CL8'SYSZVMV ',CL32'Volume_Mount_and_Verify'
 DC CL1'N',CL8'SYSZVOLS',CL32'Volume_Serial'
 DC CL1'N',CL8'SYSZVVDS',CL32'VVDS'
 DC CL1'N',CL8'SYSZWLM ',CL32'WLM'
 DC CL1'N',CL8'SYSZWTOR',CL32'WTOR'
 DC CL1'N',CL8'TAOTCPIP',CL32'Fischer-TAO'
 DC CL1'N',CL8'TAOTCPIT',CL32'Fischer-TAO'
 DC CL1'N',CL8'TASA    ',CL32'ETCL'
 DC CL1'N',CL8'TBLBASE ',CL32'Tablebase'
 DC CL1'N',CL8'TCPIP   ',CL32'TCPIP'
 DC CL1'N',CL8'TCPIPL62',CL32'TCPIP'
 DC CL1'N',CL8'TCPIPSYS',CL32'TCPIP'
 DC CL1'Y',CL8'TDMFRESV',CL32'Softek-Replicator'
 DC CL1'N',CL8'TELONTDF',CL32'CA-TELON'
 DC CL1'N',CL8'TELONTST',CL32'CA-TELON'
 DC CL1'N',CL8'TICMVS  ',CL32'TICTOC'
 DC CL1'N',CL8'TMONMVS ',CL32'ASG-TMONMVS'
 DC CL1'Y',CL8'TMRMCF  ',CL32'MVS_Solutions-Thruput_Manager_Dataset'
 DC CL1'N',CL8'TMSAPEC ',CL32'CA-1'
 DC CL1'N',CL8'TMSQNAME',CL32'CA-1'
 DC CL1'N',CL8'TPCLOCAL',CL32'CA-MIA'
 DC CL1'N',CL8'TPXBRCST',CL32'CA-TPX'
 DC CL1'N',CL8'TPXVSAMP',CL32'CA-TPX'
 DC CL1'N',CL8'TPXVSAMS',CL32'CA-TPX'
 DC CL1'N',CL8'UCCONEQN',CL32'CA-1'
 DC CL1'N',CL8'UCC7CMDS',CL32'CA-7'
 DC CL1'N',CL8'UCC7SVT ',CL32'CA-7'
 DC CL1'N',CL8'UC07    ',CL32'CA-7'
 DC CL1'N',CL8'VALETPAN',CL32'CA-Panvalet'
 DC CL1'N',CL8'VIOOPX21',CL32'SW-VIO_Plus'
 DC CL1'N',CL8'VIOOPX22',CL32'SW-VIO_Plus'
 DC CL1'Y',CL8'VLAM-MDB',CL32'Documerge_Dataset'
 DC CL1'N',CL8'VSVSSI  ',CL32'LRS-VPS_SSI'
 DC CL1'N',CL8'XCOMDYNA',CL32'CA-XCOM'
 DC CL1'N',CL8'XCOMGLOB',CL32'CA-XCOM'
 DC CL1'N',CL8'XCOM62  ',CL32'CA-XCOM'
 DC CL1'N',CL8'XEROXOSF',CL32'XPAF'
 DC CL1'N',CL8'XPAF    ',CL32'XPAF'
 DC CL1'N',CL8'XPE/MVS ',CL32'CA-L-Serv'
 DC CL1'N',CL8'XPEDITER',CL32'CW-Xpediter'
 DC CL1'N',CL8'XPNOTIFY',CL32'CW-Xpediter'
 DC CL1'N',CL8'ZEDLNK  ',CL32'Session_Manager'
 DC CL1'Y',CL8'24X7    ',CL32'Neon-MissionControl_Dataset'
 DC CL1'Y',CL8'24X7NTSO',CL32'Neon-MissionControl_Dataset'
MAJORNUM EQU   (*-TBLMAJOR)/MAJORLEN MAJOR table entry count
 DC CL1'N',CL8'????????',CL32'Unknown'
         MEND
         MACRO
**********************************************************************
* LOG          Write a message to the log data set                   *
**********************************************************************
&LABEL   LOG   &MESSAGE=           MESSAGE-ID
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'LOG ',                                              X
               =C'&MESSAGE '),                                         X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* PQUERY       Obtain panel information                              *
*                                                                    *
*               PARAMETERS ARE :                                     *
*               1. PANEL-NAME                                        *
*               2. AREA-NAME                                         *
* "AREATYP"  3. VARIABLE-NAME THAT WILL BE SET TO DYNAMIC OR GRAPHIC *
* "COLS"     4. VARIABLE-NAME THAT WILL BE SET TO AREA-WIDTH (COLS)  *
* "ROWS"     5. VARIABLE-NAME THAT WILL BE SET TO AREA-LENGTH (ROWS) *
* "TLROW"    6. VARIABLE-NAME THAT WILL BE SET TO TOP LEFT ROW NUM   *
* "TLCOL"    7. VARIABLE-NAME THAT WILL BE SET TO TOP LEFT COL NUM   *
* BEFORE ISSUING "PQUERY", VDEFINES MUST BE DONE FOR 3 THROUGH 7.    *
**********************************************************************
&LABEL   PQUERY  &QPNL,&QAREA
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'PQUERY ',                                           X
               =C'&QPNL',                                              X
               =C'&QAREA',                                             X
               =C'AREATYP',                                            X
               =C'COLS',                                               X
               =C'ROWS',                                               X
               =C'TLROW',                                              X
               =C'TLCOL'),                                             X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* REMPOP       Remove a Pop-Up Window                                *
**********************************************************************
&LABEL   REMPOP &ALL=              All pop-ups
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'REMPOP ',                                           X
               =C'&ALL '),                                             X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* SELECT       Select a panel or function                            *
**********************************************************************
&LABEL   SELECT  &SEL1,&SEL2,&SEL3,&SEL4,&SEL5
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'SELECT ',                                           X
               =F'60.',                                                X
               =CL60'&SEL1 &SEL2 &SEL3 &SEL4 &SEL5'),                  X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* SETMSG       Set next message                                      *
**********************************************************************
&LABEL   SETMSG &MESSAGE=,         MESSAGE-ID                          X
               &COND=,             CONDITIONAL                         X
               &MSGLOC=            MESSAGE-FIELD-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'SETMSG ',                                           X
               =C'&MESSAGE ',                                          X
               =C'&COND ',                                             X
               =C'&MSGLOC '),                                          X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBADD        Add a row to a table                                  *
**********************************************************************
&LABEL   TBADD &TABLE=,            TABLE-NAME                          X
               &SAVE=(),           SAVE-NAME-LIST                      X
               &ORDER=,            ORDER                               X
               &MULT=1             NUMBER OF ROWS
         AIF   ('&SAVE'(1,1) EQ '(').OKSAVE
         MNOTE 8,'TBADD SAVE=&SAVE - MUST BE IN PARENTHESIS'
         AGO   .MEND
.OKSAVE ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBADD ',                                            X
               =C'&TABLE ',                                            X
               =C'&SAVE',                                              X
               =C'&ORDER ',                                            X
               =F'&MULT.'),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBBOTTOM     Set the row pointer to the bottom                     *
**********************************************************************
&LABEL   TBBOTTOM &TABLE=          TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBBOTTOM ',                                         X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBCLOSE      Close and save a table                                *
**********************************************************************
&LABEL   TBCLOSE &TABLE=           TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBCLOSE ',                                          X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBCREATE     Create a new table                                    *
**********************************************************************
&LABEL   TBCREATE &TABLE=,         TABLE-NAME                          X
               &KEYS=(),           KEY-NAME-LIST                       X
               &NAMES=(),          NAME-LIST                           X
               &WRITE=NOWRITE,     WRITE/NOWRITE                       X
               &REPLACE=REPLACE,   REPLACE                             X
               &LIBRARY=,          LIBRARY                             X
               &SHARE=             SHARE
         AIF   ('&KEYS'(1,1) EQ '(').OKKEYS
         MNOTE 8,'TBCREATE KEYS=&KEYS - MUST BE IN PARENTHESIS'
.OKKEYS  ANOP
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'TBCREATE NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBCREATE ',                                         X
               =C'&TABLE ',                                            X
               =C'&KEYS',                                              X
               =C'&NAMES',                                             X
               =C'&WRITE ',                                            X
               =C'&REPLACE ',                                          X
               =C'&LIBRARY ',                                          X
               =C'&SHARE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBDELETE     Delete a row from a table                             *
**********************************************************************
&LABEL   TBDELETE &TABLE=          TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBDELETE ',                                         X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBDISPL      Display table information                             *
**********************************************************************
&LABEL   TBDISPL &TABLE=,          TABLE-NAME                          X
               &PANEL=,            PANEL-NAME                          X
               &MESSAGE=,          MESSAGE-ID                          X
               &CURSOR=,           FIELD-NAME                          X
               &CSRROW=,           TABLE-ROW-NUMBER                    X
               &CSRPOS=,           CURSOR-POSITION                     X
               &AUTOSEL=NO,        AUTOSEL                             X
               &POSITION=,         CRP-NAME                            X
               &ROWID=,            ROWID-NAME                          X
               &MSGLOC=,           MESSAGE-FIELD-NAME                  X
               &PCLOC=,            Primary command locate              X
               &PCSORT=,           Primary command sort                X
               &LC=()              LINE-COMMAND
         AIF   ('&LC'(1,1) EQ '(').OKLC
         MNOTE 8,'TBDISPL LC=&LC - MUST BE IN PARENTHESIS'
.OKLC    ANOP
         LCLA  &CNT
         LCLC  &NLLBL
         LCLC  &ENDLBL,&DSP1LBL,&DSP2LBL,&NPLBL,&PRCLBL,&LCLBL,&TOPLBL
         LCLC  &LOCLBL
         LCLC  &CMP,&LBL,&ID,&SEL
         AIF   ('&POSITION' NE '').POSITION
&POSTION SETC  '&TABLE'.'CRP'
.POSITION ANOP
&TOPLBL  SETC  '&LABEL'
         AIF   ('&TOPLBL' NE '').LABEL
&TOPLBL  SETC  'TOP'.'&SYSNDX'
.LABEL   ANOP
&DSP1LBL SETC  'DSP1'.'&SYSNDX'
&DSP2LBL SETC  'DSP2'.'&SYSNDX'
&ENDLBL  SETC  'END'.'&SYSNDX'
&LOCLBL  SETC  'LOC'.'&SYSNDX'
&SRTLBL  SETC  'SRT'.'&SYSNDX'
&PRCLBL  SETC  'PRC'.'&SYSNDX'
&LCLBL   SETC  'LC'.'&SYSNDX'
         MVC   TABLE,=CL8'&TABLE'  Table name
&TOPLBL  MVC   ZCMD,=CL80' '       Clear primary command
         L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBDISPL ',                                          X
               =C'&TABLE ',                                            X
               =C'&PANEL ',                                            X
               =C'&MESSAGE ',                                          X
               =C'&CURSOR ',                                           X
               =C'&CSRROW ',                                           X
               =C'&CSRPOS ',                                           X
               =C'&AUTOSEL ',                                          X
               =C'&POSITION ',                                         X
               =C'&ROWID ',                                            X
               =C'&MSGLOC '),                                          X
               VL,MF=(E,W_CL512)
         C     R15,=F'8'           End ?
         BNL   &ENDLBL             . Yes
         AIF   ('&PCLOC' EQ '').PCLOC
         CLC   ZCMD(1),=CL1'L'     Primary command - Locate ?
         BNE   &LOCLBL             . No
         CALL  &PCLOC              Primary command - Locate
         B     &TOPLBL
&LOCLBL  EQU *
.PCLOC   ANOP
         AIF   ('&PCSORT' EQ '').PCSORT
         CLC   ZCMD(1),=CL1'S'     Primary command - Sort ?
         BNE   &SRTLBL             . No
         CALL  &PCSORT             Primary command - Sort
         B     &TOPLBL
&SRTLBL  EQU *
.PCSORT  ANOP
&PRCLBL  EQU *
         AIF   ('&LC' NE '()').LC
         B     &TOPLBL             Redisplay
         AGO   .ENDLBL
.LC      ANOP
         MVC   &TABLE.SEL,ZTDSELS
&LCLBL   EQU   *
&CNT     SETA  0
&CNT     SETA  &CNT+1
&SEL     SETC  '&LC(&CNT)'
.NEXTLC  ANOP
&CNT     SETA  &CNT+1
         AIF   (&CNT GT N'&LC).ENDLC
&CMP     SETC  '&LC(&CNT)'
&NLLBL   SETC  'N'.'&CMP'.'&SYSNDX'
&CNT     SETA  &CNT+1
&LBL     SETC  '&LC(&CNT)'
         CLC   &SEL,=C'&CMP'       Match ?
         BNE   &NLLBL              . No
         CONTROL DISPLAY,SAVE      Save display environment
         CALL  &LBL                &LBL
         CONTROL DISPLAY,RESTORE   Restore display environment
         B     &DSP1LBL
&NLLBL   EQU *
         AGO   .NEXTLC
.ENDLC   ANOP
         CLI   &SEL,C' '           Blank ?
         BE    &DSP2LBL            . Yes
&DSP1LBL MVI   &SEL,C' '           Clear line command
         CLC   &TABLE.SEL,=CL4'0001'
         BNH   &TOPLBL
&DSP2LBL L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBDISPL ',                                          X
               =C'&TABLE ',                                            X
               =C' ',                                                  X
               =C' ',                                                  X
               =C' ',                                                  X
               =C' ',                                                  X
               =C' ',                                                  X
               =C' ',                                                  X
               =C'&POSITION ',                                         X
               =C' ',                                                  X
               =C' '),                                                 X
               VL,MF=(E,W_CL512)
         C     R15,=F'8'           End ?
         BNL   &ENDLBL             . Yes
         B     &PRCLBL             Reprocess selections
.ENDLBL  ANOP
&ENDLBL  EQU   *                   End/Return
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBEND        Close a table without saving                          *
**********************************************************************
&LABEL   TBEND &TABLE=             TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBEND ',                                            X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBERASE      Erase a table                                         *
**********************************************************************
&LABEL   TBERASE &TABLE=,          TABLE-NAME                          X
               &LIBRARY=ISPTABL    LIBRARY-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBERASE ',                                          X
               =C'&TABLE ',                                            X
               =C'&LIBRARY '),                                         X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBEXIST      Determine whether a row exists in a table             *
**********************************************************************
&LABEL   TBEXIST &TABLE=           TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBEXIST ',                                          X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBGET        Retrieve a row from a table                           *
**********************************************************************
&LABEL   TBGET &TABLE=             TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBGET ',                                            X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBMOD        Modify a row from table                               *
**********************************************************************
&LABEL   TBMOD &TABLE=,            TABLE-NAME                          X
               &SAVE=(),           SAVE-NAME-LIST                      X
               &ORDER=             ORDER
         AIF   ('&SAVE'(1,1) EQ '(').OKSAVE
         MNOTE 8,'TBMOD SAVE=&SAVE - MUST BE IN PARENTHESIS'
.OKSAVE  ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBMOD ',                                            X
               =C'&TABLE ',                                            X
               =C'&SAVE',                                              X
               =C'&ORDER '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBOPEN       Open a table                                          *
**********************************************************************
&LABEL   TBOPEN &TABLE=,           TABLE-NAME                          X
               &WRITE=WRITE,       WRITE/NOWRITE                       X
               &LIBRARY=ISPTLIB,   LIBRARY                             X
               &SHARE=             SHARE
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBOPEN ',                                           X
               =C'&TABLE ',                                            X
               =C'&WRITE ',                                            X
               =C'&LIBRARY ',                                          X
               =C'&SHARE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBPUT        Update a row from table                               *
**********************************************************************
&LABEL   TBPUT &TABLE=             TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBPUT ',                                            X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBQUERY      Obtain table information                              *
**********************************************************************
&LABEL   TBQUERY &TABLE=,          TABLE-NAME                          X
               &KEYS=,             KEY-NAME                            X
               &NAMES=,            VAR-NAME                            X
               &ROWNUM=,           ROWNUM-NAME                         X
               &KEYNUM=,           KEYNUM-NAME                         X
               &NAMENUM=,          NAMENUM-NAME                        X
               &POSITION=          CRP-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBQUERY ',                                          X
               =C'&TABLE ',                                            X
               =C'&KEYS ,',                                            X
               =C'&NAMES ,',                                           X
               =C'&ROWNUM ,',                                          X
               =C'&KEYNUM ,',                                          X
               =C'&NAMENUM ,',                                         X
               =C'&POSITION '),                                        X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBSARG       Define a search argument                              *
**********************************************************************
&LABEL   TBSARG &TABLE=,           TABLE-NAME                          X
               &ARGLIST=(),        NAME-LIST                           X
               &MODE=NEXT,         NEXT/PREVIOUS                       X
               &NAMECOND=()        NAME-COND-PAIRS
         AIF   ('&ARGLIST'(1,1) EQ '(').ARGLIST
         MNOTE 8,'TBSARG ARGLIST=&ARGLIST - MUST BE IN PARENTHESIS'
.ARGLIST ANOP
         AIF   ('&MODE' EQ 'NEXT').MODE
         AIF   ('&MODE' EQ 'PREVIOUS').MODE
         MNOTE 8,'TBSARG MODE=&MODE - MUST BE NEXT OR PREVIOUS'
.MODE    ANOP
         AIF   ('&NAMECOND'(1,1) EQ '(').NAMECOND
         MNOTE 8,'TBSARG NAMECOND=&NAMECOND - MUST BE IN PARENTHESIS'
.NAMECOND ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSARG ',                                           X
               =C'&TABLE ',                                            X
               =C'&ARGLIST',                                           X
               =C'&MODE ',                                             X
               =C'&NAMECOND'),                                         X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBSAVE       Save a table                                          *
**********************************************************************
&LABEL   TBSAVE &TABLE=            TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSAVE ',                                           X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBSCAN       Search a table                                        *
**********************************************************************
&LABEL   TBSCAN &TABLE=,           TABLE-NAME                          X
               &ARGLIST=(),        NAME-LIST                           X
               &SAVENAME=,         VAR-NAME                            X
               &ROWID=,            ROWID-NAME                          X
               &MODE=NEXT,         NEXT/PREVIOUS                       X
               &NOREAD=,           NOREAD                              X
               &POSITION=,         CRP-NAME                            X
               &CONDLIST=()        CONDITION-VALUE-LIST
         AIF   ('&ARGLIST'(1,1) EQ '(').ARGLIST
         MNOTE 8,'TBSCAN ARGLIST=&ARGLIST - MUST BE IN PARENTHESIS'
.ARGLIST ANOP
         AIF   ('&MODE' EQ 'NEXT').MODE
         AIF   ('&MODE' EQ 'PREVIOUS').MODE
         MNOTE 8,'TBSCAN MODE=&MODE - MUST BE NEXT OR PREVIOUS'
.MODE    ANOP
         AIF   ('&CONDLIST'(1,1) EQ '(').CONDLIST
         MNOTE 8,'TBSCAN CONDLIST=&CONDLIST - MUST BE IN PARENTHESIS'
.CONDLIST ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSCAN ',                                           X
               =C'&TABLE ',                                            X
               =C'&ARGLIST',                                           X
               =C'&SAVENAME ',                                         X
               =C'&ROWID ',                                            X
               =C'&MODE ',                                             X
               =C'&NOREAD ',                                           X
               =C'&POSITION ',                                         X
               =C'&CONDLIST'),                                         X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBSKIP       Move the row pointer                                  *
**********************************************************************
&LABEL   TBSKIP &TABLE=,           TABLE-NAME                          X
               &NUMBER=+1,         NUMBER                              X
               &VAR=,              VAR-NAME                            X
               &ROWID=,            ROWID-NAME                          X
               &ROW=,              ROWID                               X
               &NOREAD=,           NOREAD                              X
               &CRP=,              CRP-NAME                            X
               &END=               END
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSKIP ',                                           X
               =C'&TABLE ',                                            X
               =F'&NUMBER',                                            X
               =C'&VAR ',                                              X
               =C'&ROWID ',                                            X
               =C'&ROW ',                                              X
               =C'&NOREAD ',                                           X
               =C'&CRP '),                                             X
               VL,MF=(E,W_CL512)
         AIF   ('&END' EQ '').MEND
         C     R15,=F'0'           End ?
         BNE   &END                . Yes
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBSORT       Sort a table                                          *
**********************************************************************
&LABEL   TBSORT &TABLE=,           TABLE-NAME                          X
               &FIELDS=()          SORT-LIST - FIELD,B|C|N,A|D
         LCLC  &IFIELDS,&ITABLE
         AIF   ('&TABLE'(1,1) NE '@').TABLE
&ITABLE  SETC  '&TABLE'(2,K'&TABLE-1)
         AIF   ('&FIELDS'(1,1) NE '@').TIFD
&IFIELDS SETC  '&FIELDS'(2,K'&FIELDS-1)
.TIFI    ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSORT ',                                           X
               &ITABLE,                                                X
               &IFIELDS),                                              X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.TIFD    ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSORT ',                                           X
               &ITABLE,                                                X
               =C'&FIELDS'),                                           X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.TABLE   ANOP
         AIF   ('&FIELDS'(1,1) NE '@').TDFD
&IFIELDS SETC  '&FIELDS'(2,K'&FIELDS-1)
.TDFI    ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSORT ',                                           X
               =C'&TABLE ',                                            X
               &IFIELDS),                                              X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.TDFD    ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBSORT ',                                           X
               =C'&TABLE ',                                            X
               =C'&FIELDS'),                                           X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBTOP        Set the row pointer to the top                        *
**********************************************************************
&LABEL   TBTOP &TABLE=             TABLE-NAME
         LCLC  &ITABLE
         AIF   ('&TABLE'(1,1) NE '@').TABLE
&ITABLE  SETC  '&TABLE'(2,K'&FIELDS-1)
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBTOP ',                                            X
               &ITABLE),                                               X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.TABLE   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBTOP ',                                            X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* TBVCLEAR     Clear table variables                                 *
**********************************************************************
&LABEL   TBVCLEAR &TABLE=          TABLE-NAME
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'TBVCLEAR ',                                         X
               =C'&TABLE '),                                           X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VCOPY        Create a copy of a variable                           *
**********************************************************************
&LABEL   VCOPY &NAME=,             NAME                                X
               &LENGTH=,           LENGTH                              X
               &VARIABLE=,         VARIABLE                            X
               &MODE=              LOCATE/MOVE
         LCLA  &WRKLENA
         LCLC  &WRKLEN,&WRKVAR
         AIF   ('&LENGTH' NE '').LEN
         LA    R0,L'&NAME              max length of dsname
         ST    R0,FULL                 max length of dsname
         MNOTE 0,'VCOPY - LENGTH= DEFAULTING TO LENGTH OF &NAME'
         AGO   .LENEND
.LEN     ANOP
         LA    R0,&LENGTH              max length of dsname
         ST    R0,FULL                 max length of dsname
         AGO   .LENEND
.LENEND  ANOP
&WRKVAR  SETC  '&VARIABLE'
         AIF   ('&WRKVAR' NE '').VARIABLE
&WRKVAR  SETC '&NAME'
         MNOTE 0,'VCOPY - VARIABLE= DEFAULTING TO &NAME'
.VARIABLE ANOP
         AIF   ('&MODE' EQ 'LOCATE').MODE
         AIF   ('&MODE' EQ 'MOVE').MODE
         MNOTE 8,'VCOPY - MODE=&MODE INVALID - MUST BE LOCATE OR MOVE'
         MEXIT
.MODE    ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VCOPY ',                                            X
               =C'(&NAME) ',                                           X
               FULL,                                                   X
               &WRKVAR,                                                X
               =C'&MODE '),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VDEFINE      Define function variables                             *
**********************************************************************
&LABEL   VDEFINE &NAMES=(),        NAME-LIST                           X
               &VARIABLE=,         VARIABLE                            X
               &FORMAT=CHAR,       FORMAT                              X
               &LENGTH=8,          LENGTH                              X
               &OPTIONS=,          OPTIONS                             X
               &USERDATA=          USER-DATA
         LCLC  &VAR
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'VDEFINE NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
         AIF   ('&VARIABLE' EQ '').NOVAR
&VAR     SETC  '&VARIABLE'
         AGO   .ENDVAR
.NOVAR   ANOP
&VAR     SETC  '&NAMES'(2,K'&NAMES-2)
.ENDVAR  ANOP
         AIF   ('&OPTIONS' EQ '').NOOPT
         L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VDEFINE ',                                          X
               =C'&NAMES',                                             X
               &VAR,                                                   X
               =C'&FORMAT ',                                           X
               =F'&LENGTH.',                                           X
               =C'&OPTIONS '),                                         X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.NOOPT   ANOP
         L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VDEFINE ',                                          X
               =C'&NAMES',                                             X
               &VAR,                                                   X
               =C'&FORMAT ',                                           X
               =F'&LENGTH.'),                                          X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VDELETE      Remove a definition of function variables             *
**********************************************************************
&LABEL   VDELETE &NAMES=()         NAME-LIST
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'VDELETE NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VDELETE ',                                          X
               =C'&NAMES'),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VGET         Retrieve variables from a pool or profile             *
**********************************************************************
&LABEL   VGET  &NAMES=(),          NAME-LIST                           X
               &POOL=PROFILE       ASIS/SHARED/PROFILE
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'VGET NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VGET ',                                             X
               =C'&NAMES ',                                            X
               =C'&POOL '),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VMASK        Mask and Edit Processing                              *
**********************************************************************
&LABEL   VMASK &NAMES=(),          NAME-LIST                           X
               &FORMAT=,           SYSTEM-FORMAT                       X
               &LENGTH=,           LENGTH                              X
               &USER=              USER-FORMAT
         LCLC  &VAR
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'VMASK NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
         AIF   ('&FORMAT' EQ '').NOFORMAT
         AIF   ('&FORMAT' EQ 'LOCATE').FORMAT
         AIF   ('&FORMAT' EQ 'IDATE').FORMAT
         AIF   ('&FORMAT' EQ 'STDDATE').FORMAT
         AIF   ('&FORMAT' EQ 'ITIME').FORMAT
         AIF   ('&FORMAT' EQ 'STDTIME').FORMAT
         AIF   ('&FORMAT' EQ 'JDATE').FORMAT
         AIF   ('&FORMAT' EQ 'JSTD').FORMAT
         MNOTE 8,'VMASK - FORMAT=&FORMAT Invalid'
         MEXIT
         L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VMASK ',                                            X
               =C'&NAMES ',                                            X
               =C'FORMAT ',                                            X
               =C'&FORMAT '),                                          X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.NOFORMAT ANOP
         L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VMASK ',                                            X
               =C'&NAMES ',                                            X
               =C'USER ',                                              X
               =C'&USER ',                                             X
               =F'&LENGTH.'),                                          X
               VL,MF=(E,W_CL512)
         AGO   .MEND
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VPUT         Update variables in the shared or profile pool        *
**********************************************************************
&LABEL   VPUT  &NAMES=(),          NAME-LIST                           X
               &POOL=PROFILE       ASIS/SHARED/PROFILE
         AIF   ('&NAMES'(1,1) EQ '(').NAMES
         MNOTE 8,'VPUT NAMES=&NAMES - MUST BE IN PARENTHESIS'
.NAMES   ANOP
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VPUT ',                                             X
               =C'&NAMES ',                                            X
               =C'&POOL '),                                            X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VREPLACE     Replace a variable                                    *
*                                                                    *
*              THE FIRST PARAMETER IS VARIABLE-NAME, SECOND FULLWORD *
*              LENGTH, THIRD THE DATA.                               *
**********************************************************************
&LABEL   VREPLACE &FLD,&LEN,&DATA
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VREPLACE ',                                         X
               =C'(&FLD) ',                                            X
               &LEN,                                                   X
               &DATA),                                                 X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
         MACRO
**********************************************************************
* VRESET       Reset function variables                              *
**********************************************************************
&LABEL   VRESET
&LABEL   L     R15,ISPLINK@        ISPLINK address
         CALL  (15),                                                   X
               (=C'VRESET '),                                          X
               VL,MF=(E,W_CL512)
.MEND    ANOP
         MEND
