NCP status display

At our installation we have three 3745s (one local and two
remote), each of which has the current and previous NCPs
stored on its disk. Changes to an NCP are carried out on the
older NCP, which is then loaded into the 3745 and activated
for testing.

The following REXX EXEC was developed to allow the status
of all NCPs and 3745 disk contents to be monitored while
changes were taking place. Commands can be issued from the
panel, and their effect on the 3745/NCP can be seen when the
panel is refreshed.

The EXEC issues a NetView 'DIS' command for each NCP to
find the status of each NCP. It then issues a NetView 'DISK'
command for each active NCP to find the status of the 3745
disk. This EXEC could easily be adapted to match other
installations' naming conventions and a different number of
3745/NCPs. If more than two NCP names are used for each
3745, modification to the loop which obtains the status of 3745
disks will be required.


NCP STATUS DISPLAY EXEC (NCPDIS)

/**********************************************************************/
/* NCPDIS     :- REXX EXEC to display status of NCPs and 3745s        */
/**********************************************************************/
/* Initialize error handling.                                         */
/**********************************************************************/
Trace E
/**********************************************************************/
/* Initialize static panel field attributes.                          */
/**********************************************************************/
$NCP. = 'FA CY'
$COMMAND = 'FI UY'
$DIS = 'FI'
/**********************************************************************/
/* Initialize static panel variables.                                 */
/**********************************************************************/
VER = '1.0'
NCP.1 = 'NCPEDIA'
NCP.2 = 'NCPEDIB'
NCP.3 = 'NCPMANA'
NCP.4 = 'NCPMANB'
NCP.5 = 'NCPREAA'
NCP.6 = 'NCPREAB'
COMMAND = ' '
DIS = 'NO'
/**********************************************************************/
/* Initialize variables.                                              */
/**********************************************************************/
NUMNCP = 6
/**********************************************************************/
/* Get information on NCPs from VTAM for display.                     */
/**********************************************************************/
Call GETINFO
/**********************************************************************/
/* MAIN LOOP                                                          */
/**********************************************************************/
Do forever
   /*******************************************************************/
   /* Display main panel.                                             */
   /*******************************************************************/
   'VIEW NCPAPPL NCPDISP INPUT'
   /*******************************************************************/
   /* Convert input fields to upper case.                             */
   /*******************************************************************/
   Upper COMMAND
   Upper DIS
   If substr(DIS,1,1) = 'Y' then DIS = 'YES'
   Else DIS = 'NO'
   /*******************************************************************/
   /* Process panel responses.                                        */
   /*******************************************************************/
   Select
      When viewaid = PF3 then exit
      When viewaid = PF5 then call GETINFO
      When viewaid = PF6 then CMD HIGH ROLL
      When viewaid = ENTER then
         Select
            When COMMAND ¬= ' ' then
               Do
                  If DIS = 'NO' then 'TRAP AND SUPPRESS MESSAGES IST*'
                  'CMD HIGH' COMMAND
                  COMMAND = ' '
                  'TRAP NO MESSAGES'
                  'FLUSHQ'
                  Call GETINFO
               End
            Otherwise nop
         End
      Otherwise nop
   End
End
Exit
/**********************************************************************/
/* Subroutine GETINFO = Get information on NCPs from VTAM.            */
/**********************************************************************/
GETINFO:
   'OVERRIDE DISPLAY=NO'
/**********************************************************************/
/* Reset panel field attributes to defaults.                          */
/**********************************************************************/
   $STATV. = 'FA CP'
   $STAT3. = 'FA CP'
   $ADL. = 'FA CP'
/**********************************************************************/
/* Reset panel variables to defaults.                                 */
/**********************************************************************/
   STATV. = 'UNKNOWN'
   STAT3. = 'UNKNOWN'
   ADL. = ' ?'
/**********************************************************************/
/* Loop to get status of NCPs.                                        */
/**********************************************************************/
   Do I = 1 to NUMNCP
      'TRAP AND SUPPRESS MESSAGES IST453I IST075I'
      'DIS 'NCP.I
      'WAIT 20 SECONDS FOR MESSAGES'
      Select
         When EVENT() = 'M' then
            Do
               'MSGREAD'
               Select
                  When (MSGID() = 'IST453I') then
                     Do
                        STATV.I = 'INACTIVE'
                        $STATV.I = 'FA CR'
                     End
                  When (MSGID() = 'IST075I') then
                     Do
                        'GETMLINE LINE 2'
                        PARSE VAR LINE V1 V2 STAT REST
                        Select
                           When STAT = 'ACTIV' then
                              Do
                                 STATV.I = ' ACTIVE '
                                 $STATV.I = 'FA CG'
                              End
                           When STAT = 'PLOAD' then
                              Do
                                 STATV.I = 'LOADING '
                                 $STATV.I = 'FA CY'
                              End
                           Otherwise
                              Do
                                 STATV.I = ' 'STAT
                                 $STATV.I = 'FA CP'
                              End
                        End
                     End
                  Otherwise Say MSGID()
               End
            End
         When EVENT() = 'T' then
            Do
               STATV.I = 'TIMEOUT'
               $STATV.I = 'FA CP'
            End
         When EVENT() = 'E' then
         Otherwise
            Do
               STATV.I = ' ERROR '
               $STATV.I = 'FA CP'
            End
         Otherwise nop
      End
      'TRAP NO MESSAGES'
      'FLUSHQ'
   End
/**********************************************************************/
/* Loop to get status of 3745 disks via ACTIVE NCPs.                  */
/**********************************************************************/
   Do I = 1 to NUMNCP
      Select
         When Value('STATV.'I) = ' ACTIVE ' then
            Do
               J = ((I + 1) % 2) * 4 - 1 - I
               'TRAP AND SUPPRESS MESSAGES IST951I'
               'DISK 'NCP.I
               'WAIT 20 SECONDS FOR MESSAGES'
               Select
                  When EVENT() = 'M' then
                     Do
                        'MSGREAD'
                        Select
                           When (MSGID() = 'IST951I') then
                              Do
                                 'GETMSIZE SIZE'
                                 Do K = 1 to SIZE
                                    'GETMLINE MLINE 'K
                                    'PARSEL2R MLINE V0 V1 V2 V3 V4 V5'
                                    Select
                                    When V0 = 'IST955I' then
                                       Do
                                          Select
                                       When V1 = 'NCPEDIA' then LOC = 1
                                       When V1 = 'NCPEDIB' then LOC = 2
                                       When V1 = 'NCPMANA' then LOC = 3
                                       When V1 = 'NCPMANB' then LOC = 4
                                       When V1 = 'NCPREAA' then LOC = 5
                                       When V1 = 'NCPREAB' then LOC = 6
                                             Otherwise nop
                                          End
                                          IF V4 = 'STORED' then
                                             Do
                                                STAT3.LOC = ' STORED'
                                                $STAT3.LOC = 'FA CY'
                                                ADL.LOC = ' NO'
                                                $ADL.LOC = 'FA CR'
                                             End
                                          IF V4 = 'STORING' then
                                             Do
                                                STAT3.LOC = 'STORING'
                                                $STAT3.LOC = 'FA CY'
                                                ADL.LOC = ' NO'
                                                $ADL.LOC = 'FA CR'
                                             End
                                          IF V5 = 'YES' then
                                             Do
                                                STAT3.LOC = ' LOADED'
                                                $STAT3.LOC = 'FA CG'
                                             End
                                       End
                                    When V0 = 'IST965I' then
                                       Do
        If V3 = 'YES' & Value('STAT3.'I) = 'LOADED' then
                                             Do
                                                ADL.I = 'YES'
                                                $ADL.I = 'FA CG'
                                             End
        If V3 = 'YES' & Value('STAT3.'J) = 'LOADED' then
                                             Do
                                                ADL.J = 'YES'
                                                $ADL.J = 'FA CG'
                                             End
                                       End
                                    Otherwise nop
                                    End
                                 End
                              End
                           Otherwise Say MSGID()
                        End
                     End
                  When EVENT() = 'T' then
                     Do
                        STAT3.I = 'TIMEOUT'
                        $STAT3.I = 'FA CP'
                     End
                  When EVENT() = 'E' then
                     Do
                        STAT3.I = ' ERROR '
                        $STAT3.I = 'FA CP'
                     End
                  Otherwise nop
               End
               'TRAP NO MESSAGES'
               'FLUSHQ'
            End
         Otherwise nop
      End
   End
/**********************************************************************/
/* Reset 3745 disk status of module to PURGED if it has not been found*/
/**********************************************************************/
   Do I = 1 to NUMNCP
      J = ((I + 1) % 2) * 4 - 1 - I
   If Value('STAT3.'I) = 'UNKNOWN' & Value('STAT3.'J) ¬= 'UNKNOWN' then
         Do
            STAT3.I = ' PURGED'
            $STAT3.I = 'FA CR'
            ADL.I = ' NO'
            $ADL.I = 'FA CR'
         End
   End
/**********************************************************************/
/* Interpret stem variables into panel variables.                     */
/**********************************************************************/
   Do I = 1 to NUMNCP
      Interpret('NCP'I) '= NCP.I'
      Interpret('STATV'I) '= STATV.I'
      Interpret('STAT3'I) '= STAT3.I'
      Interpret('ADL'I) '= ADL.I'
      Interpret('$NCP'I) '= $NCP.I'
      Interpret('$STATV'I) '= $STATV.I'
      Interpret('$STAT3'I) '= $STAT3.I'
      Interpret('$ADL'I) '= $ADL.I'
   End
   'OVERRIDE DISPLAY=YES'
Return


NCP STATUS DISPLAY PANEL (NCPDISP)

/**********************************************************************/
/* NCPDISP :- MAIN DISPLAY PANEL FOR NCP CLIST                        */
/**********************************************************************/
*** AT1
+NCPDISP             %N C P   S T A T U S   D I S P L A Y          +Version &VER
$
$                            VTAM              3745               AUTO
$      \NCP NAME+          \STATUS+          \STATUS+          \DUMP/LOAD+
$
$       &NCP1             &STATV1           &STAT31               &ADL1
$       &NCP2             &STATV2           &STAT32               &ADL2
$
$       &NCP3             &STATV3           &STAT33               &ADL3
$       &NCP4             &STATV4           &STAT34               &ADL4
$
$       &NCP5             &STATV5           &STAT35               &ADL5
$       &NCP6             &STATV6           &STAT36               &ADL6
$
$
$
$
$
%Command==>&COMMAND
$
%Message Display==>&DIS$
$                       PF3= Return    PF5= Refresh
$           PF6= Roll


John Cameron
Technical Specialist
The Life Association of Scotland (UK)       c Life Association of Scotland 1992

