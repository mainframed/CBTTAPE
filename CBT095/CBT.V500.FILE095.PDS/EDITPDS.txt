PROC 2 PDSDSN MACRO +
       MACPARM1() MACPARM2() MACPARM3() MACPARM4() +
       LIST
/**********************************************************************/
/* THIS CLIST CAN BE USED IN CONJUNCTION WITH AN INITIAL EDIT MACRO   */
/* TO EDIT ALL MEMBERS OF A PARTITIONED DATA SET.                     */
/*                                                                    */
/* THE LAST COMMAND OF THE INITIAL EDIT MACRO THAT YOU USE SHOULD BE  */
/* AN ISREDIT END OR ISREDIT CANCEL COMMAND.  IF THIS IS THE CASE,    */
/* THIS CLIST WILL EDIT EACH MEMBER WITHOUT EVER DISPLAYING THE EDIT  */
/* SCREEN.                                                            */
/*                                                                    */
/* UP TO 4 PARAMETERS CAN BE SPECIFIED.  THESE PARAMETERS ARE VPUT TO */
/* THE SHARED POOL.  YOUR EDIT MACRO CAN VGET THESE PARAMETERS IF     */
/* NECESSARY.  PARAMETERS CANNOT BE DIRECTLY SPECIFIED WHEN USING AN  */
/* INITIAL EDIT MACRO.                                                */
/*                                                                    */
/* TO RUN: AT ISPF/PDF OPTION 6, ENTER EDITPDS FOLLOWED BY THE        */
/* DATA SET NAME AND THE MACRO TO BE RUN.  IF YOU WISH TO USE THE     */
/* MACRO PARAMETERS, ENTER THE VARIABLE NAME (MACPARM1) AND PLACE ITS */
/* VALUE IN PARENTHESIS.                                              */
/*                                                                    */
/* FOR EXAMPLE:                                                       */
/*      EDITPDS USER.PDS EDITMAC MACPARM1(XXXX) MACPARM2(YYYY)        */
/*                                                                    */
/**********************************************************************/
IF &LIST = LIST THEN CONTROL LIST CONLIST SYMLIST

/* VPUT PARAMETERS TO THE SHARED POOL */
ISPEXEC  VPUT (MACPARM1,MACPARM2,MACPARM3,MACPARM4) SHARED

/* INITIALIZE DATASET */
ISPEXEC  LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                ENQ(SHRW)                        +
                ORG(PDSORG)
SET LMRC = &LASTCC
IF &LMRC Â¬= 0 THEN +
   DO
      WRITE LMINIT FAILED - RETURN CODE &LMRC
      WRITE DATA SET IS NOT ALLOCATED OR NOT PARTITIONED
      EXIT CODE(8)
   END

/* OPEN DATA SET */
ISPEXEC LMOPEN DATAID(&PDSID) OPTION(INPUT)
SET LMRC = &LASTCC

/* CALL EDIT WITH EACH MEMBER OF THE DATA SET AND SPECIFY THE */
/* INITIAL EDIT MACRO.                                        */
DO WHILE (&LMRC = 0)
  /* BUILD MEMBER LIST AND RETURN NEXT MEMBER */
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER) -
        STATS(NO)
  SET LMRC = &LASTCC

  IF &LMRC = 0 THEN /* NOT END OF MEMBER LIST */ +
     DO
       WRITENR &SYSTIME - PROCESSING &PDSDSN(&MEMBER)
       ISPEXEC EDIT DATAID(&PDSID) MEMBER(&MEMBER) +
               MACRO(&MACRO)
       SET EDRC = &LASTCC
       WRITE -- RETURN CODE &EDRC
     END
 END

/* FREE MEMBER LIST */
ISPEXEC LMMLIST DATAID(&PDSID) OPTION(FREE)
/* CLOSE DATA SET */
ISPEXEC LMCLOSE DATAID(&PDSID)
/* FREE DATA ID */
ISPEXEC LMFREE  DATAID(&PDSID)

 WRITE &SYSTIME - ALL MEMBERS OF &PDSDSN HAVE BEEN PROCESSED

EXIT CODE(0)
