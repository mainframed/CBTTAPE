ISREDIT MACRO (SCOL1 SCOL2 ACTION DEBUG)
 IF &STR(&SCOL1) = ? THEN DO
 ISPEXEC DISPLAY PANEL(SORTDUPS)
 EXIT
 END
 /*
 /*       DATASET:  H562813.PVTLIB.MACSOUT(SORTX)
 /*        AUTHOR:  NANCY T. MYERS
 /*     DATE/TIME:  09/12/88     21:58:15
 /*
 /*   DESCRIPTION:  SORT CURRENT DATA SET FIND / DELETE DUPLICATES
 /*      MODIFIED:  PAUL G. DAVIS ENRON CORP
 /*                 ADDED HELP AND DISPLAY ALL RECORDS CONTAINING DUPS
 /*                 IF SHODUPS SELECTED. IF NO PARAMETERS ENTERED SORT
 /*                 DEFAULTS TO BOUNDS SETTINGS OF DATASET.
 /*

/* ISPF EDIT "SORTDUPS" MACRO USING ISPF 2.3 PDF FEATURES
/*DETERMINE DUPLICATE STRINGS WITHIN DATASETS

/* COPYRIGHT (C) 1988 - NANCY T. MYERS

    CONTROL MSG
    ISREDIT (BND1,BND2) = BOUNDS
    IF &SCOL1 = THEN DO
    SET &SCOL1 = &BND1
    SET &SCOL2 = &BND2
    SET &ACTION = SHODUPS
    END
    ISPEXEC CONTROL ERRORS RETURN
    IF &ACTION = THEN SET &ACTION = SHODUPS
   SET &RC = 0
   ISPEXEC VGET (ZENVIR)     /*CHECK FOR 2.3 */
    IF '&STR(&ZENVIR)' NE '&STR(ISPF 2.3MVS/XA  TSO)' THEN DO
    WRITE           ERROR
    WRITE THE MACRO "SORTDUPS" CAN ONLY BE USED WITH ISPF/PDF 2.3
    WRITE
    GOTO HELP
    END
   ISPEXEC VGET (ZUSER)       /* UNRELEASED CODE */

CKPARM: IF &SCOL2 = DEBUG THEN CONTROL SYM CON LIST
    IF &SCOL1 EQ &STR(HELP) THEN GOTO HELP
    IF &DEBUG =  DEBUG THEN CONTROL SYM CON LIST
    IF &SCOL1 < 1 OR &SCOL1 > 255 OR +
    &SCOL2 < 1 OR &SCOL2 > 255 THEN DO
    WRITE           ERROR
    WRITE THE FIRST AND SECOND PARM FOR THIS MACRO MUST BE NUMERIC
    WRITE
    GOTO HELP
    END
    IF &ACTION  NE  &STR(SHODUPS)  AND +
    &ACTION   NE  &STR(SHOWDUPS) AND  +
    &ACTION   NE &STR(DELDUPS)  THEN DO
    WRITE           ERROR
    WRITE THE THIRD PARM FOR THIS MACRO MUST BE SHODUPS OR DELDUPS
    WRITE
    GOTO HELP
    END

INIT: SET &DUPCNT  =  0
    ISREDIT (START1) = LINENUM .ZFIRST
    ISREDIT (END1) = LINENUM .ZLAST
    SET &TOTRECS = &EVAL(&END1)
    ISREDIT RESET
    ISREDIT SORT NX A &SCOL1 &SCOL2

   ISREDIT CURSOR   1,0
    ISREDIT BOUNDS = &SCOL1 &SCOL2
   ISREDIT EXCLUDE ALL
    SET &LINENO1 = &EVAL(&START1)
    SET &LINENO2 = &LINENO1 + 1

PEAT: ISREDIT (REC1) =  LINE &LINENO1
    SET &FLD1 =  &SUBSTR(&SCOL1:&SCOL2,&NRSTR(&REC1))
REPEAT: ISREDIT (REC2) = LINE &LINENO2
    IF &NRSTR(&REC2) Â¬=  THEN +
    SET  &FLD2 =  &SUBSTR(&SCOL1:&SCOL2,&NRSTR(&REC2))
        IF &NRSTR(&FLD1) = &NRSTR(&FLD2) THEN DO
        IF &ACTION = SHODUPS OR &ACTION = SHOWDUPS THEN
        IF &ACTION = DELDUPS THEN DO
        ISREDIT DELETE &LINENO2 &LINENO2
        SET &LINENO2 = &LINENO2 - 1
        SET &RECSX = &RECSX + 1
        IF &RECSX = &TOTRECS THEN GOTO CLOSE
        IF &RECSX > &TOTRECS THEN GOTO CLOSE
        END
    ISREDIT LOCATE &LINENO1
    ISREDIT LOCATE &LINENO2
    SET &LINENO2 =  &LINENO2 + 1
    SET &DUPCNT =  &DUPCNT + 1
    END
   ELSE DO
    SET &FLD1 =  &NRSTR(&FLD2)
    SET &LINENO1 = &LINENO2
    SET &LINENO2 = &LINENO2 + 1
    END
   IF &LINENO2 < &EVAL(&END1) THEN GOTO REPEAT

CLOSE: IF &DUPCNT =  0 THEN DO
    SET ZEDSMSG =  &STR(NO DUPLLCATES)
    SET ZEDLMSG = &STR(NO DUPLLCATES FOUND +
             IN COLUMNS &SCOL1 THRU &SCOL2)
    ISPEXEC SETMSG MSG(ISRZ000)
    GOTO SETSCR
    END
   IF &ACTION  =  &STR(DELDUPS) THEN DO
    ISREDIT RESET
    ISREDIT BOUNDS &BND1 &BND2
    SET ZEDSMSG = &STR(&DUPCNT DUP(S) DELETED)
    SET ZEDLMSG = &STR(&DUPCNT DUPLICATE(S) DELETED +
             IN COLUMNS &SCOL1 THRU &SCOL2)
    ISPEXEC SETMSG MSG(ISRZ000)
    END
   ELSE DO
    SET ZEDSMSG = &STR(&DUPCNT DUP(S) FOUND)
    SET ZEDLMSG = &STR(&DUPCNT DUPLICATE(S) FOUND +
             IN COLUMNS &SCOL1 THRU &SCOL2)
    ISPEXEC SETMSG MSG(ISRZ000)
    END

SETSCR: ISREDIT BOUNDS = &BND1,&BND2
   ISREDIT UP MAX
   IF &RC  =   0 THEN SET &RC = 1

EXIT: EXIT CODE(&RC)


HELP: WRITE &SYSICMD IS AN ISPF EDIT MACRO DESIGNED TO FIND AND/OR
   WRITE ELIMINATE OCCURANCES OF DUPLICATE STRINGS WITHIN USER
   WRITE DEFINED COLUMNS OF A DATASET. THE DATASET DOES NOT HAVE
   WRITE TO BE PRESORTED BECAUSE THE EDIT MACRO WILL SORT IT.
   WRITE
   WRITE
   WRITE IT NEEDS THREE PARAMETERS:
   WRITE 1 - SCOL1 - BEGINNING SORT COLUMN
   WRITE
   WRITE 2 - SCOL2 - ENDING SORT COLUMN


 WRITE
WRITE 3 - ACTION  ACTION TO BE TAKEN ON THE DUPLLCATES
WRITE      DELDUPS - DELETE ALL DUPLICATES
WRITE      SHODUPS - DISPLAY ALL DUPLICATES ONLY
WRITE
WRITE       OR
WRITE
WRITE IT CAN BE USED WITH ONLY ONE PARAMETER:
WRITE 1 - HELP - TO DISPLAY THIS MESSAGE
WRITE      MUST BE "HELP"
WRITE
WRITE IF YOU DETERMINE THAT YOU WANT TO UNDO WHAT SORTDUPS
WRITE DID, SIMPLY CANCEL YOUR EDIT SESSION.
WRITE
WRITE THIS MACRO IS CREATED AND MAINTAINED BY CONTROL AND
WRITE INTEGRATION.
GOTO EXIT
