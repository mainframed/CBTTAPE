 ISREDIT MACRO (PARM) NOPROCESS
 ISPEXEC CONTROL ERRORS RETURN
 IF &PARM ¬= &Z THEN +
   DO
     IF &PARM = ? THEN +
       DO
         ISPEXEC DISPLAY PANEL(CUT)
         EXIT
       END
     IF &PARM = DELETE THEN DO
  DELC2
         EXIT
  END
     IF &PARM = SHOW THEN DO
  SCUT2
         EXIT
       END
     ELSE +
      DO
       SET SYSDVAL = &PARM
       READDVAL LEVEL,KEYWORD
        IF &DATATYPE(&LEVEL) ¬= NUM THEN +
          DO
           SET ZEDSMSG = &STR(INVALID ARGUMENT)
             SET ZEDLMSG = &STR(VALID ARGUMENTS ARE A "?" OR A LEVEL +
                               NUMBER, WITH OPTIONAL "SHOW")
           ISPEXEC SETMSG MSG(ISRZ001)
          EXIT
        END
        IF &KEYWORD ¬= &Z THEN +
          IF &KEYWORD = SHOW THEN +
             DO
               SET POOL = PROFILE
               ISPEXEC VPUT (LEVEL,POOL) PROFILE
               SET &RC = &LASTCC
               ISPEXEC CONTROL DISPLAY SM START(2)
               ISPEXEC SELECT CMD(CUTSHOW)
               EXIT CODE(&LASTCC)
             END
           ELSE +
             DO
           SET ZEDSMSG = &STR(INVALID ARGUMENT)
             SET ZEDLMSG = &STR(VALID ARGUMENTS ARE A "?" OR A LEVEL +
                               NUMBER, WITH OPTIONAL "SHOW")
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT
          END
     ELSE
    END
  END
ELSE +
  SET LEVEL = 1
ISREDIT PROCESS RANGE C M
SET RC = &LASTCC
IF &RC = 4 THEN +
  DO
     SET ZEDSMSG = &STR(RANGE NOT SPECIFIED)
     SET ZEDLMSG = &STR(SPECIFY RANGE FOR THE CUT AND RE-EXECUTE +
                        THE MACRO)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(4)
   END
ELSE +
    IF &RC > 0 THEN +
      DO
     SET ZEDSMSG = &STR(SEVERE ERROR)
     SET ZEDLMSG = &STR(SEVERE ERROR OCCURED, RETURN CODE WAS +
                        &RC)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(&RC)
   END
 ISREDIT (CMD) = RANGE_CMD
 ISREDIT (FIRST) = LINENUM .ZFRANGE
 ISREDIT (LAST) = LINENUM .ZLRANGE
 SET   LINESTOCUT = &EVAL(&LAST-&FIRST+1)
 SET RCUTLM&LEVEL = &LINESTOCUT
 SET &TCUT = &LINESTOCUT
 ISREDIT (LBND,RBND) = BOUNDS
 SET RCUTLB&LEVEL = &LBND
 SET RCUTRB&LEVEL = &RBND
 ISPEXEC VPUT (RCUTLM&LEVEL,RCUTLB&LEVEL,RCUTRB&LEVEL) PROFILE
 SET RC = &LASTCC
 SET INDEX = 0
 CONTROL ASIS
 DO WHILE &INDEX < &LINESTOCUT
   ISREDIT (WKLINE) = LINE &EVAL(&FIRST + &INDEX)
   SET RCUT&LEVEL&EVAL(&INDEX+1) = &SUBSTR(&LBND:&RBND,&NRSTR(&WKLINE))
   ISPEXEC VPUT (RCUT&LEVEL&EVAL(&INDEX+1)) PROFILE
   SET &RC = &LASTCC
   SET INDEX  = &INDEX + 1
END
IF &CMD = M THEN +
  DO
    ISREDIT (WIDTH) = DATA_WIDTH
    IF &RBND-&LBND+1 = &WIDTH THEN +
      ISREDIT DELETE &FIRST &LAST
    ELSE +
      DO
        SET INDEX = 0
        SET BLANK = &STR(                                              -
                                                                       -
                                                                       -
                                                               )
      DO WHILE &INDEX < &LINESTOCUT
       ISREDIT (WKLINE) = LINE &EVAL(&FIRST + &INDEX)
       IF &LBND > 1 THEN +
         SET NULINE = &SUBSTR(1:&LBND-1,&NRSTR(&WKLINE))
       ELSE
         SET NULINE = &Z
       SET BLINE = &NRSTR(&NULINE)&SUBSTR(1:&RBND-&LBND+1,+
           &STR(BLANK))
       IF &RBND < &WIDTH THEN +
         SET NULINE = &NRSTR(&BLINE)&SUBSTR(&RBND+1:&WIDTH,+
           &NRSTR(&WKLINE))
       ELSE +
         SET NULINE = &NRSTR(&BLINE)
       ISREDIT LINE &EVAL(&FIRST + &INDEX) = (NULINE)
       SET INDEX = &INDEX + 1
     END
   END
 END
           SET ZEDSMSG = &STR(&TCUT LINE(S) CUT LEVEL &LEVEL)
           ISPEXEC SETMSG MSG(ISRZ001)
