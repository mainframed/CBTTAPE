  ISREDIT MACRO (STR1 STR2 RANG1 RANG2 KEY1 COL1 COL2 KEYZ)
/* MACRO: CONLY                                                       */
/*                                                                    */
/* FUNCTION: Displays only those lines with changes in them.          */
/*  Accepts most CHANGE command parameters.
/*                                                                    */
/*                                                                    */
/*  Written by MICHAEL BOTOS  --  05/29/85                            */
/*                                                                    */
/*   UPDATE to add NOCAPS for TSO/E: 12/04/85 - MZB                   */
/*                                                                    */
/*                                                                    */
  CONTROL NOCAPS
  ISPEXEC CONTROL ERRORS RETURN

/* ----------------->   ITEM DOCUMENTATION    <--------------------- */
/* |                                                               | */
/* | SYNTAX HELP                                                   | */
/* |                                                               | */
/* ----------------------------------------------------------------- */
  IF &STR(&STR1.) = &STR(?) THEN +
    DO
       ISPEXEC DISPLAY PANEL(#CONLY)
     EXIT CODE(0)
    END

/* ----------------->   ITEM DOCUMENTATION    <--------------------- */
/* |                                                               | */
/* | Test for too many parameters                                  | */
/* |                                                               | */
/* ----------------------------------------------------------------- */
  IF X&KEYZ. NE &STR(X) THEN +
    DO
       SET ZEDSMSG = &STR(Too many KEYWORDS)
       SET ZEDLMSG = &STR(Enter CONLY HELP FOR SYNTAX and supported KEY)
       ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(12)
    END

/* ----------------->   ITEM DOCUMENTATION    <--------------------- */
/* |                                                               | */
/* | Testing for unsupported keywords: ALL, FIRST, LAST, NEXT, NX, | */
/* |  PREV, and X.                                                 | */
/* |                                                               | */
/* | NOTE: Shift back to CAPS for this processing only.            | */
/* |                                                               | */
/* ----------------------------------------------------------------- */

  CONTROL CAPS
  IF X&RANG1. = &STR(XALL) OR X&RANG2. = &STR(XALL) +
    OR X&KEY1. = &STR(XALL) OR X&COL1. = &STR(XALL) +
    OR X&COL2. = &STR(XALL) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XFIRST) OR X&RANG2. = &STR(XFIRST) +
    OR X&KEY1. = &STR(XFIRST) OR X&COL1. = &STR(XFIRST) +
    OR X&COL2. = &STR(XFIRST) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XLAST) OR X&RANG2. = &STR(XLAST) +
    OR X&KEY1. = &STR(XLAST) OR X&COL1. = &STR(XLAST) +
    OR X&COL2. = &STR(XLAST) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XNEXT) OR X&RANG2. = &STR(XNEXT) +
    OR X&KEY1. = &STR(XNEXT) OR X&COL1. = &STR(XNEXT) +
    OR X&COL2. = &STR(XNEXT) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XNX) OR X&RANG2. = &STR(XNX) +
    OR X&KEY1. = &STR(XNX) OR X&COL1. = &STR(XNX) +
    OR X&COL2. = &STR(XNX) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XPREV) OR X&RANG2. = &STR(XPREV) +
    OR X&KEY1. = &STR(XPREV) OR X&COL1. = &STR(XPREV) +
    OR X&COL2. = &STR(XPREV) THEN GOTO BADKEY
  IF X&RANG1. = &STR(XX) OR X&RANG2. = &STR(XX) +
    OR X&KEY1. = &STR(XX) OR X&COL1. = &STR(XX) +
    OR X&COL2. = &STR(XX) THEN GOTO BADKEY
  CONTROL NOCAPS


  ISREDIT EXCLUDE ALL
  ISREDIT CHANGE &STR1. &STR2. &RANG1. &RANG2. &KEY1. &COL1. &COL2. ALL

  EXIT CODE(0)

/* ----------------->   ITEM DOCUMENTATION    <--------------------- */
/* |                                                               | */
/* | Set message for unsupported keywords.                         | */
/* |                                                               | */
/* ----------------------------------------------------------------- */
  BADKEY: +
    DO
       SET ZEDSMSG = &STR(Invalid KEYWORD)
       SET ZEDLMSG = &STR(Enter CONLY ? to list SYNTAX and supported KEY
       ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(12)
    END
