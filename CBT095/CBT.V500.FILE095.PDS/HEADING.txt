 ISREDIT MACRO (P1,P2,P3,P4,P5,P6,P7,P8,P9,P10,P11,P12) NOPROCESS
 CONTROL END(ENDO)

 /* HELP                                                             */

 IF &STR(&P1) = &STR(?) OR &STR(&P1) = HELP THEN +
  DO
   ISREDIT (CAPMOD) = CAPS
   ISREDIT CAPS = OFF
   CONTROL ASIS
   ISPEXEC CONTROL ERRORS RETURN
   ISREDIT (FLNUM) = DISPLAY_LINES
   SET &CUTX  = &STR(TO EXECUTE, TYPE HEADING ON YOUR EDIT COMMAND LIN)
   SET &CUTX  = &STR(&CUTX.E.)
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   SET &CUTX  = &STR()
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   SET &CUTX  = &STR(PRINT DEPTH.)
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   SET &CUTX  = &STR(YOU MAY CREATE A  MAXIMUM  OF  EIGHT  (8)  HEADING)
   SET &CUTX  = &STR(&CUTX. LINES AND CHOOSE THE)
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   SET &CUTX  = &STR(CONTROL CHARACTERS.)
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   SET &CUTX  = &STR(HEADING WILL ADD HEADINGS   TO  ANY  EDITABLE FILE)
   SET &CUTX  = &STR(&CUTX. USING ASCII CARRIAGE)
   ISREDIT LINE_AFTER &FLNUM = MSGLINE (CUTX)
   ISREDIT CAPS = &CAPMOD
   EXIT
  ENDO

 /* HOUSEKEEPING                                                     */

 CONTROL ASIS
 ISREDIT (CAPMOD) = CAPS
 ISREDIT CAPS = OFF
 ISREDIT (LINMAX) = LINENUM .ZLAST
 SET &LINEMAX = &LINEMAX
 ISREDIT (WIDTH) = DATA_WIDTH
 SET &PADCT = (&WIDTH - 72) / 2
 IF  &PADCT < 1 THEN &PADCT = 1
 SET &WIDTH = &WIDTH - 8
 SET &B = &STR(                                            )
 SET &B = &STR(&B&B&B)
 SET &PAD = &SUBSTR(1:&PADCT,&B)
 SET &A = &STR(PAGE )

 /* GET HEADING LINES                                                */

 ISPEXEC DISPLAY PANEL(MGB00064)
 IF &LASTCC = 8 THEN +
  DO
   ISREDIT CAPS = &CAPMOD
   EXIT
  ENDO
 IF &NRSTR(&H8) NE &STR() THEN SET &HDGCNT = 8
 ELSE IF &NRSTR(&H7) NE &NRSTR() THEN SET &HDGCNT = 7
 ELSE IF &NRSTR(&H6) NE &STR() THEN SET &HDGCNT = 6
 ELSE IF &NRSTR(&H5) NE &STR() THEN SET &HDGCNT = 5
 ELSE IF &NRSTR(&H4) NE &STR() THEN SET &HDGCNT = 4
 ELSE IF &NRSTR(&H3) NE &STR() THEN SET &HDGCNT = 3
 ELSE IF &NRSTR(&H2) NE &STR() THEN SET &HDGCNT = 2
 ELSE SET &HDGCNT = 1
 IF  &PADCT < &HDGCNT THEN &PADCNT = &HDGCNT + 2
 SET &HX8 = &NRSTR(&PAD&H8)
 SET &HX7 = &NRSTR(&PAD&H7)
 SET &HX6 = &NRSTR(&PAD&H6)
 SET &HX5 = &NRSTR(&PAD&H5)
 SET &HX4 = &NRSTR(&PAD&H4)
 SET &HX3 = &NRSTR(&PAD&H3)
 SET &HX2 = &NRSTR(&PAD&H2)
 SET &PAD1 = &SUBSTR(2:&LENGTH(&STR(&PAD)),&STR(&PAD))
 SET &HX1 = &NRSTR(&PAD1&H1)
 SET &HX1 = &SUBSTR(1:&WIDTH,&NRSTR(1&HX1&B))
 SET &HX1 = &NRSTR(&HX1&A)
 SET &LINCNT = &PD-&HDGCNT
 SET &LINMAX = &LINMAX
 ISREDIT FIND P'¬' .ZFIRST .ZLAST 1
 SET &RC = &LASTCC

 /* SINCE NO CARRIAGE CONTROL CHARACTERS, INSERT HEADINGS BY         */
 /* ((PAGE DEPTH) - (HEADING COUNT)) IN FILE. (QUICKEST)             */

 IF &RC ¬= 0 THEN +
  DO
   SET &PAGCNT = &LINMAX/&LINCNT
   SET &I = &PAGCNT*&LINCNT
   IF &I ¬= &LINMAX THEN +
    DO
     SET &I = &I + 1
     SET &PAGCNT = &PAGCNT + 1
    ENDO
   ELSE +
    DO
     SET &I = &I - &LINCNT + 1
    ENDO
   DO WHILE &I ¬< 1
    ISREDIT CURSOR = &I 1
 IF &NRSTR(&H8) NE &STR() THEN SET &HDGCNT = 8
 ELSE IF &NRSTR(&H7) NE &NRSTR() THEN SET &HDGCNT = 7
 ELSE IF &NRSTR(&H6) NE &STR() THEN SET &HDGCNT = 6
 ELSE IF &NRSTR(&H5) NE &STR() THEN SET &HDGCNT = 5
 ELSE IF &NRSTR(&H4) NE &STR() THEN SET &HDGCNT = 4
 ELSE IF &NRSTR(&H3) NE &STR() THEN SET &HDGCNT = 3
 ELSE IF &NRSTR(&H2) NE &STR() THEN SET &HDGCNT = 2
 ELSE SET &HDGCNT = 1
    IF &NRSTR(&HX8) = THEN SET &HX8 = &STR( )
    IF &NRSTR(&HX7) = THEN SET &HX7 = &STR( )
    IF &NRSTR(&HX6) = THEN SET &HX6 = &STR( )
    IF &NRSTR(&HX5) = THEN SET &HX5 = &STR( )
    IF &NRSTR(&HX4) = THEN SET &HX4 = &STR( )
    IF &NRSTR(&HX3) = THEN SET &HX3 = &STR( )
    IF &NRSTR(&HX2) = THEN SET &HX2 = &STR( )
        SET &HX8 = &NRSTR("&HX8")
        SET &HX7 = &NRSTR("&HX7")
        SET &HX6 = &NRSTR("&HX6")
        SET &HX5 = &NRSTR("&HX5")
        SET &HX4 = &NRSTR("&HX4")
        SET &HX3 = &NRSTR("&HX3")
        SET &HX2 = &NRSTR("&HX2")
    IF &HDGCNT > 7 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX8)
    IF &HDGCNT > 6 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX7)
    IF &HDGCNT > 5 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX6)
    IF &HDGCNT > 4 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX5)
    IF &HDGCNT > 3 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX4)
    IF &HDGCNT > 2 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX3)
    IF &HDGCNT > 1 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX2)
    SET &Y = &NRSTR("&HX1&PAGCNT")
    ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&Y)
    SET &PAGCNT = &PAGCNT - 1
    SET &I = &I - &LINCNT
   ENDO
  ENDO

 /* SINCE CARRIAGE CONTROL CHARACTERS ARE PRESENT, USE THE CARRIAGE  */
 /* CONTROL CHARACTERS TO DETERMINE WHERE TO INSERT HEADINGS IN FILE */

 /* CONTROL LIST SYMLIST CONLIST /* */
 IF &RC = 0 THEN +
  DO
   SET &PAGDPH = &LINCNT + 30
   SET &PAGCNT = 1
   SET &I      = 1
   DO WHILE &I ¬> &LINMAX
    ISREDIT (INPUT) = LINE &I
    SET &CC = &SUBSTR(1:1,&NRSTR(&INPUT))
    IF &NRSTR(&CC) = &STR( ) THEN SET &PAGDPH = &PAGDPH + 1
    ELSE IF &STR(&CC) = &STR(0) THEN SET &PAGDPH = &PAGDPH + 2
    ELSE IF &STR(&CC) = &STR(-) THEN SET &PAGDPH = &PAGDPH + 3
    ELSE IF &STR(&CC) = &STR(+) THEN SET &PAGDPH = &PAGDPH
    ELSE IF &STR(&CC) = &STR(1) THEN +
     DO
      SET &PAGDPH = &LINCNT + 30
      SET &LINLEN = &LENGTH(&NRSTR(&INPUT)) - 1
      SET &NINPUT = &SUBSTR(2:&LINLEN,&NRSTR(&INPUT))
      SET &NINPUT = &STR( &NRSTR(&NINPUT))
      ISREDIT LINE &I = '&NRSTR(&NINPUT)'
     ENDO
    ELSE +
     DO
      SET ZEDSMSG = &NRSTR(INVALID CC)
      SET ZERRALRM = YES
      SET ZEDLMSG = &STR(INVALID CARRIAGE CONTROL CHARACTER IN COL 1)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(16)
     ENDO
    IF &PAGDPH > &LINCNT THEN +
     DO
      ISREDIT CURSOR = &I 1
      IF &DONE ¬= YES THEN DO
        SET &HX8 = &NRSTR("&HX8")
        SET &HX7 = &NRSTR("&HX7")
        SET &HX6 = &NRSTR("&HX6")
        SET &HX5 = &NRSTR("&HX5")
        SET &HX4 = &NRSTR("&HX4")
        SET &HX3 = &NRSTR("&HX3")
        SET &HX2 = &NRSTR("&HX2")
        SET &DONE = YES
      ENDO
    IF &HDGCNT > 7 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX8)
    IF &HDGCNT > 6 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX7)
    IF &HDGCNT > 5 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX6)
    IF &HDGCNT > 4 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX5)
    IF &HDGCNT > 3 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX4)
    IF &HDGCNT > 2 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX3)
    IF &HDGCNT > 1 THEN ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&HX2)
      SET &Y = &NRSTR("&HX1&PAGCNT")
      ISREDIT LINE_BEFORE &I = DATALINE &NRSTR(&Y)
      SET &PAGCNT = &PAGCNT + 1
      SET &PAGDPH = &HDGCNT
      SET &I      = &I + &HDGCNT
      SET &LINMAX = &LINMAX + &HDGCNT
     ENDO
    SET &I = &I + 1
   ENDO
  ENDO

 /* END OF MACRO                                                      */

 ISREDIT CAPS = &CAPMOD
 SET ZEDSMSG = &STR(NORMAL EOM)
 SET ZEDLMSG = &STR(NORMAL END OF HEADING; HEADINGS INSERTED)
 ISPEXEC SETMSG MSG(ISRZ001)
 EXIT
/* --------------------------- HEADING ----------------------------- */
/* ================================================================== */
/*            E L E C T R O N I C   D A T A   S Y S T E M S           */
/*                HOUSTON INFORMATION PROCESSING CENTER               */
/*                  OPERATING SYSTEMS SOFTWARE GROUP                  */
/*                            1301 FANNIN                             */
/*                          HOUSTON, TX 77002                         */
/* ------------------------------------------------------------------ */
/*+ HEADING IS AN EDIT MACRO THAT CREATES HEADINGS FOR FILES          */
/* ------------------------------------------------------------------ */
/*                           DEPENDENCIES                             */
/* PANELS: MGB00064                                                   */
/* ------------------------------------------------------------------ */
/*                         MODIFICATION LOG                           */
/*   DATE     AUTHOR             DESCRIPTION                          */
/* 06/01/88 M. G. BERRYHILL      CREATION                             */
/* 05/01/90 M. G. BERRYHILL      ADD CARRIAGE CONTROL SPACING         */
/* 05/22/90 M. G. BERRYHILL      ADD HELP                             */
/* 05/23/90 P. G. DAVIS          MODIFIED TO ACCEPT "'" AND "&"       */
/* ================================================================== */
