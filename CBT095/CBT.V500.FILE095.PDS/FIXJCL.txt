ISREDIT MACRO (HELP) NOPROCESS
      IF &HELP = ? THEN DO
      ISPEXEC DISPLAY PANEL(FIXJCL)
      EXIT
      END
/*
/*    ALIGN JCL JOB, EXEC, AND DD STATEMENTS ON STANDARD BOUNDARIES
/*    T. M. HANES, ASYST INC. 02/01/88
/*    D.E.M.I., 03/01/88
/*
/*      SITE DEPENDENT DEFINITIONS:
/*      ALIGNMENT COLUMNS FOR CARD TYPES
SET     &JOBA = 12
SET     &JOBB = 17
SET     &PROCA = 12
SET     &PROCB = 17
SET     &PENDA = 12
SET     &PENDB = 17
SET     &DDA = 12
SET     &DDB = 17
SET     &EXECA = 12
SET     &EXECB = 17
SET     &CONTA = 12
SET     &CONTB = 17
/*NTROL LIST SYMLIST CONLIST
/*
/*      PRESERVE CONTINUATION CHARACTERS IN 72
ISREDIT PROCESS RANGE J
IF      &LASTCC = 0 THEN DO
        ISREDIT (RCMD) = RANGE_CMD
        ISREDIT (JZF) = LINENUM .ZFRANGE
        ISREDIT (JZL) = LINENUM .ZLRANGE
        END
ELSE +
        DO
        ISREDIT (JZF) = LINENUM .ZFIRST
        ISREDIT (JZL) = LINENUM .ZLAST
        END
ISREDIT LABEL &JZF = .JZF
ISREDIT LABEL &JZL = .JZL
SET     &LJZF = &STR(.JZF)
SET     &LJZL = &STR(.JZL)
IF      &JZF  = &JZL THEN SET &LJZF = &STR(.JZL)
/*
/*ITE   * JZF = &JZF   JZL = &JZL   LJZF = &LJZF   LJZL = &LJZL
/*REDIT X ALL  &LJZF &LJZL
/*REDIT MEND
/*IT
ISREDIT (CL,CR) = CURSOR
ISREDIT (LB,RB) = BOUNDS
ISREDIT BOUNDS    = 1 71
ISREDIT X ALL
ISREDIT F '//'  1 ALL &LJZF &LJZL
ISREDIT X '//*' 1 ALL &LJZF &LJZL
ISREDIT X '/*'  1 ALL &LJZF &LJZL
/*      REMOVE EXTRA SPACES
ISREDIT C ' ' '{'      3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{{{{{{' '{' 3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{{{{{' '{'  3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{{{{' '{'   3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{{{' '{'    3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{{' '{'     3 71 ALL NX  &LJZF &LJZL
ISREDIT C '{' ' '      3 71 ALL NX  &LJZF &LJZL
/*      JOB, PROC, EXEC, DD AND PEND CONVERSION
SET     &KEYWD = &STR(JOB)
SET     &FLDA=&DDA-1
SET     &FLDB=&DDB
DO      WHILE &KEYWD NE &STR()
        ISREDIT X ALL
        ISREDIT F ' &STR(&KEYWD) ' 3 25 ALL                &LJZF &LJZL
        IF &LASTCC=4 THEN GOTO BYEDT
        ISREDIT X '//*' 1 ALL                              &LJZF &LJZL
        ISREDIT C ' &STR(&KEYWD) ' '{&STR(&KEYWD)}' ALL NX &LJZF &LJZL
        ISREDIT C '{' '{{'  3 25 ALL NX                    &LJZF &LJZL
        ISREDIT C '}' '}}'  3 40 ALL NX                    &LJZF &LJZL
        ISREDIT C '{' '{{'  3 25 ALL NX                    &LJZF &LJZL
        ISREDIT C '}' '}}'  3 40 ALL NX                    &LJZF &LJZL
        ISREDIT C '{' '{{'  3 25 ALL NX                    &LJZF &LJZL
        ISREDIT C '}' '}}'  3 40 ALL NX                    &LJZF &LJZL
        ISREDIT C '{' '{{'  3 25 ALL NX                    &LJZF &LJZL
        ISREDIT C '{&STR(&KEYWD)}' '_&STR(&KEYWD)_' ALL NX &LJZF &LJZL
        ISREDIT C ' ' '¬'        ALL NX                    &LJZF &LJZL
        ISREDIT C '{' ' '   3 &FLDA    ALL NX              &LJZF &LJZL
        ISREDIT C '{' ''         ALL NX                    &LJZF &LJZL
        ISREDIT C '}¬' '}'       ALL NX                    &LJZF &LJZL
        ISREDIT C '}' ' '  10 &FLDB    ALL NX              &LJZF &LJZL
        ISREDIT C '}' ''         ALL NX                    &LJZF &LJZL
        ISREDIT C '_&STR(&KEYWD)_' ' &STR(&KEYWD) ' ALL    &LJZF &LJZL
        ISREDIT C '¬' ' '        ALL NX                    &LJZF &LJZL
BYEDT:+
        IF &KEYWD = DD   THEN DO
             SET &KEYWD = &STR()
             END
        IF &KEYWD = PEND THEN DO
             SET &KEYWD = &STR(DD)
             SET &FLDA=&DDA-1
             SET &FLDB=&DDB
             END
        IF &KEYWD = EXEC THEN DO
             SET &KEYWD = &STR(PEND)
             SET &FLDA=&PENDA-1
             SET &FLDB=&PENDB
             END
        IF &KEYWD = PROC THEN DO
             SET &KEYWD = &STR(EXEC)
             SET &FLDA=&EXECA-1
             SET &FLDB=&EXECB
             END
        IF &KEYWD = JOB  THEN DO
             SET &KEYWD = &STR(PROC)
             SET &FLDA=&PROCA-1
             SET &FLDB=&PROCB
             END
        END
/*      CONTINUATION CARD CONVERSION
ISREDIT X ALL
ISREDIT F '// '  1 ALL                             &LJZF &LJZL
ISREDIT X ' EXEC ' 3 20 ALL                        &LJZF &LJZL
ISREDIT X ' DD '   3 20 ALL                        &LJZF &LJZL
ISREDIT X ' PEND ' 3 20 ALL                        &LJZF &LJZL
ISREDIT C ' ' '}'  3 ALL NX                        &LJZF &LJZL
ISREDIT C '}' '}}' 3 25 ALL NX                     &LJZF &LJZL
ISREDIT C '}' '}}' 3 25 ALL NX                     &LJZF &LJZL
ISREDIT C '}' '}}' 3 25 ALL NX                     &LJZF &LJZL
ISREDIT C '}' '}}' 3 25 ALL NX                     &LJZF &LJZL
ISREDIT C '}' '}}' 3 25 ALL NX                     &LJZF &LJZL
ISREDIT C ' ' '¬'  3 70 ALL NX                     &LJZF &LJZL
ISREDIT C '}¬' '}' 3 70 ALL NX                     &LJZF &LJZL
ISREDIT C '}' ' '  3 &CONTA ALL NX                 &LJZF &LJZL
ISREDIT C '}' ''        ALL NX                     &LJZF &LJZL
ISREDIT C '¬' ' '       ALL NX                     &LJZF &LJZL
ISREDIT BOUNDS =  &LB &RB
ISREDIT LEFT MAX
ISREDIT RESET
ISREDIT CURSOR = &CL &CR
EXIT    CODE(0)
