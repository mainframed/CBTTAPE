ISREDIT MACRO (LEFT,RIGHT) NOPROCESS
                         /* ANOTHER FINE EDIT MACRO */
                             /* BROUGHT TO YOU */
                        /* BY YOUR TECHNICAL SUPPORT */
                                   /* STAFF */
  IF &STR(&LEFT) = ? THEN +
    DO
      ISPEXEC DISPLAY PANEL(CENTER)
      EXIT CODE(1)
    END
    IF &LEFT ¬=  AND &RIGHT = THEN DO
    ISREDIT (L2,RIGHT) = BOUNDS
    END
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  /* PROCESS LINE COMMANDS, CHECK IF C  WAS SPECIFIED */
  ISREDIT PROCESS RANGE C
  SET LCC = &LASTCC
  ISREDIT (RC) = RANGE_CMD
  IF &LASTCC = 4 | &STR(&RC) ¬= &STR(C) THEN +
    DO
      SET &ZEDSMSG = &STR(NO RANGE SPECIFIED)
      SET &ZEDLMSG = &STR(YOU MUST SPECIFY A RANGE OF LINES +
                          TO BE CENTERED, USING "CN" OR "CC")
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
    END

  ISREDIT (FR)  = LINENUM .ZFRANGE
  ISREDIT (LR)  = LINENUM .ZLRANGE
  ISREDIT (LB,RB) = BOUNDS
  IF &RIGHT ¬= THEN DO
  SET &RB = &RIGHT
  SET &LB = &LEFT
  END
  SET I = &FR
  DO WHILE (&I <= &LR)
    ISREDIT
    ISREDIT LABEL &I = .LJ
    ISREDIT SEEK P'¬' .LJ .LJ &LB &RB FIRST
    ISREDIT (LLINE,LCOL) = CURSOR
    ISREDIT SEEK P'¬' .LJ .LJ &LB &RB LAST
    ISREDIT (RLINE,RCOL) = CURSOR
    SET MIDPT = &EVAL(&RB - &LB) / 2
    SET MIDPT = &MIDPT + &LB
    SET LEFT = &MIDPT - &LCOL
    SET RIGHT = &RCOL - &MIDPT
    SET COUNT = &EVAL(&LEFT - &RIGHT + 1) / 2
    IF &COUNT > 0 THEN +
      ISREDIT SHIFT ) &I &COUNT
    ELSE +
      IF &COUNT < 0 THEN +
        DO
          SET COUNT = -1*&COUNT
          ISREDIT SHIFT ( &I &COUNT
        END
    IF &I = &FR THEN ISREDIT LABEL &I = .LJX
    SET I = &I + 1
  END
  ISREDIT SEEK P'=' .LJX .LJX FIRST
  EXIT CODE(0)
