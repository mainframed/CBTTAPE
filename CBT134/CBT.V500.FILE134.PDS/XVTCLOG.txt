XVTCLOG  TITLE 'ROUTINE TO CALCULATE THE NATURAL LOGARITHM OF A SINGLE-*
               PRECISION FLOATING POINT NUMBER'
***********************************************************************
*                                                                     *
*  XVTCLOG DETERMINES THE LOGARITHM OF A SINGLE-PRECISION FLOATING    *
*  POINT ARGUMENT (POINTED TO IN THE USUAL WAY VIA REGISTER 1) AND    *
*  RETURNS IT IN FLOATING POINT REGISTER 0.                           *
*                                                                     *
*  THE LOGIC OF XVTCLOG IS THE SAME AS THAT OF VS/FORTRAN FUNCTION    *
*  ALOG:                                                              *
*                                                                     *
*  1. WRITE X=16**P*2**(-Q)*M, WHERE P IS THE EXPONENT, Q IS AN       *
*     INTEGER (0.LE.Q.LE.3) AND 1/2.LE.M.LT.1 .                       *
*  2. IF 1/2.LE.M.LT.1/SQRT(2), SET A=1/2 AND B=1;                    *
*     IF 1/SQRT(2).LE.M.LT.1, SET A=1 AND B=0. THEN 2**(-B)=A.        *
*  3. WRITE Z=(M-A)/(M+A). THEN M=A*(1+Z)/(1-Z) AND |Z|<0.1716.       *
*  4. NOW X=2**(4*P-Q-B)*(1+Z)/(1-Z), AND                             *
*     LOG(X)=(4*P-Q-B)*LOG(2) + LOG((1+Z)/(1-Z)).                     *
*  5. TO OBTAIN LOG((1+Z)/(1-Z)), FIRST COMPUTE                       *
*     W=2*Z=(M-A)/(0.5*M+0.5*A),                                      *
*     AND APPLY AN APPROXIMATION OF THE FORM                          *
*     LOG((1+Z)/(1-Z))=W*(C0+C1*W**2/(C2-W**2), WITH C0=1.            *
*                                                                     *
***********************************************************************
         EJECT
XVTCLOG  CSECT
         SPACE
E0       EQU   0                   FLOATING POINT REGISTER 0
E2       EQU   2                   FLOATING POINT REGISTER 2
E4       EQU   4                   FLOATING POINT REGISTER 4
R0       EQU   0                   WORK REGISTER
R1       EQU   1                   WORK REGISTER
R13      EQU   13                  SAVE REGISTER
R14      EQU   14                  LINK REGISTER
R15      EQU   15                  BASE REGISTER
         SPACE
         USING XVTCLOG,R15
         B     START               BRANCH ROUND MODULE ID
         DC    X'07'
         DC    CL7'XVTCLOG'
START    STM   R14,R1,12(R13)      SAVE REGISTERS
         L     R1,0(,R1)           PICK UP ARGUMENT ADDRESS
         LE    E0,0(,R1)           LOAD REAL ARGUMENT
         IC    R0,0(,R1)           AND DO SOME CLEVER THINGS
         LH    R1,0(,R1)
         SRA   R1,3
         N     R1,HEX1C
         X     R0,HEX7F
         STC   R0,MULTAB(R1)
         O     R0,COMP7F
         SLL   R0,2
         ME    E0,MULTAB(R1)
         CE    E0,LIMIT
         BNH   LAB3
         HER   E2,E0
         SE    E0,REC256
         LD    E4,DMASK
         BNZ   LAB1
         SDR   E4,E4
LAB1     AE    E2,REC512
         DER   E0,E2
         LER   E4,E0
         MER   E0,E0
         LE    E2,C2
         SER   E2,E0
         ME    E0,C1
         DER   E0,E2
         MER   E0,E4
         ADR   E4,E0
         A     R0,ADDTAB1(R1)
         BNM   LAB2
         LCR   R0,R0
         STH   R0,MULT1+2
         LE    E0,LOG2
         ME    E0,MULT1
         ADR   E0,E4
         B     RETURN              RETURN
LAB2     STH   R0,MULT2+2
         LE    E0,LOG2
         ME    E0,MULT2
         ADR   E0,E4
         B     RETURN              RETURN
LAB3     HER   E2,E0
         SE    E0,REC512
         LD    E4,DMASK
         BNZ   LAB4
         SDR   E4,E4
LAB4     AE    E2,REC1024
         DER   E0,E2
         LER   E4,E0
         MER   E0,E0
         LE    E2,C2
         SER   E2,E0
         ME    E0,C1
         DER   E0,E2
         MER   E0,E4
         ADR   E4,E0
         A     R0,ADDTAB2(R1)
         BNM   LAB5
         LCR   R0,R0
         STH   R0,MULT1+2
         LE    E0,LOG2
         ME    E0,MULT1
         ADR   E0,E4
         B     RETURN              RETURN
LAB5     STH   R0,MULT2+2
         LE    E0,LOG2
         ME    E0,MULT2
         ADR   E0,E4
RETURN   LM    R0,R1,20(R13)       RESTORE WORK REGISTERS
         SR    R15,R15             SET RETURN CODE
         BR    R14                 RETURN
         DS    0D
DMASK    DC    X'46000000'         0.9375       (15/16)
         DC    X'F0000000'
REC256   DC    X'3F100000'         0.00390625   (1/256)
REC512   DC    X'3E800000'         0.001953125  (1/512)
REC1024  DC    X'3E400000'         0.0009765625 (1/1024)
LIMIT    DC    X'3EB504F3'         0.0027621363 (SQRT(2)/512)
C1       DC    X'408D8BC7'         0.55291413
C2       DC    X'416A298C'         6.6351437
LOG2     DC    X'40B17219'         0.69314729   (LOG(2))
MULTAB   DC    X'00800000'
         DC    X'00400000'
         DC    X'00200000'
         DC    X'00200000'
         DC    X'00100000'
         DC    X'00100000'
         DC    X'00100000'
         DC    X'00100000'
ADDTAB1  DC    F'263'
         DC    F'262'
         DC    F'261'
         DC    F'261'
         DC    F'260'
         DC    F'260'
         DC    F'260'
         DC    F'260'
ADDTAB2  DC    F'264'
         DC    F'263'
         DC    F'262'
         DC    F'262'
         DC    F'261'
         DC    F'261'
         DC    F'261'
         DC    F'261'
MULT1    DC    X'46000000'         +16**6 * ....
MULT2    DC    X'C6000000'         -16**6 * ....
HEX1C    DC    X'0000001C'
HEX7F    DC    X'0000007F'
COMP7F   DC    X'FFFFFF80'
         SPACE 2
         END
