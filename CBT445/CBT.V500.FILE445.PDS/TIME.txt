TIME     TITLE 'TIMING TABLE FOR MOD 40'
***********************************************************************
*                                                                     *
*  add instruction timings to the listing                             *
*                                                                     *
*  called by FTXTA                                                    *
*                                                                     *
*  parms @ r1:                                                        *
*    +0 - @ Print Area in PASS1                                       *
*    +4 - input instruction                                           *
*    +8 - total time so far                                           *
*                                                                     *
*  Return is to @ r14.                                                *
*                                                                     *
*  Add dummy timing entries for new instructions         CFK jly 1999 *
*                                                                     *
***********************************************************************
TIME     CSECT
         BALR  15,0               setup base reg
         USING *,15               tell assembler
         MVI   FLG,X'00'          clear * needed flag
         MVC   OP(4),4(1)         copy input
         MVC   TOTAL(4),8(1)      copy total
         SR    3,3
         IC    3,OP               get opcode
         CLI   OP,X'7F'           RR or RX instr ?
         BNH   CRRX               br if yes.
         CLI   OP,X'CF'           RS or SI instr.
         BNH   CRSI               br if yes.
         CLI   OP,X'EF'           SS instr. with 1 length ?
         BNH   CSSL               br if yes
         B     CSSD               else ss with 2 lengths
*                    RR or RX instruction found
CRRX     SLL   3,2                mul. opcode by 4
         A     3,ARRX             calc @ time for this instr.
         L     5,0(3)             get time for this instr.
FORMC    CLI   OP,X'40'           RX instr. ?
         BL    FORM               no - print timings
         TM    RR,X'0F'           index reg = 0 ?
         BZ    FORME              br if so
         CLI   OP,X'60'           floating point instr ?
         BL    SHIN               br if not
         A     5,XRTC             add floating point index time (125)
SHIN     A     5,XRT              add 63
FORME    CLI   OP,X'44'           EXecute instr. ?
         BNE   FORM               no - print timings
         MVI   FLG,X'01'          else flag * needed
         B     FORM               and print timings
*                    SS instruction with 1 length found
CSSL     S     3,D0               sub x'000000D0'
         SLL   3,3                mul opcode by 8
         A     3,ASSL             calc @ opcode timing
         SR    4,4
         IC    4,RR               get length field
         LA    4,1(4)             calc bytes to process
         L     5,4(3)             get time per byte
         MR    4,4                calc extra time
         A     5,0(3)             calc total time
         B     FORM               and print timings
*                    SS instruction with 2 lengths found
CSSD     S     3,F0               sub x'000000F0'
         SLL   3,3                mul opcode by 8
         A     3,ASSD             calc @ opcode timing
         MVC   L1(1),RR           copy length fields
         MVC   L2(1),RR           copy lengths again
         NI    L1,X'F0'           mask out 1st length
         NI    L2,X'0F'           mask out 2nd length
         SR    4,4                ) move
         IC    4,L1               )  1st
         SRL   4,4                )   length
         STC   4,L1               )    down
         CLC   L1(1),L2           1st length shorter than 2nd
         BNL   NE12               br if not
         IC    4,L2               else get 2nd (longer) length
NE12     LA    4,1(4)             calc bytes to process
         L     5,4(3)             get time per byte
         MR    4,4                calc extra time
         A     5,0(3)             calc total time
         B     FORM               and print timings
*                     RS or SI instruction found
CRSI     S     3,EGO              sub x'00000080'
         SLL   3,3                mul opcode by 8
         A     3,ARSI             get @ timing table entries
         TM    OP,X'07'           low 3 bits clear ?
         BZ    STLM               br if yes
         TM    OP,X'88'           x8 - xF instr. ?
         BO    SHIO               br if yes
LRS      L     5,0(3)             else get instr. timing
         B     FORM               and print timings
STLM     TM    OP,X'88'           x8 - xF instr. ?
         BO    SHIO               br if yes
STLME    MVC   L1(1),RR           copy r1,r2
         MVC   L2(1),RR           copy r1,r2
         CLI   OP,X'80'           SSM instr. ?
         BE    LRS                br if yes (done)
*                  LM or STM instruction
         SR    4,4                ) move
         IC    4,L1               )  r1
         SRL   4,4                )  down
         STC   4,L1               )
         SR    5,5
         NI    L2,X'0F'           mask out r2
         CLC   L1(1),L2           r1 > r2 ?
         BNH   AFT                br if not
         LA    5,16               else add 16 to r2
AFT      IC    4,L2               get r2
         AR    5,4                add fudge factor
         IC    4,L1               get r1
         SR    5,4                ) calc number of
         LA    5,1(5)             )  registers
         M     4,4(3)             mul by time per register
         A     5,0(3)             calc total time
         B     FORM               and print timings
*                     x8 - xF instr.
SHIO     CLI   OP,X'98'           LM instr. ?
         BE    STLME              br if yes
         TM    OP,X'10'           9x instr. ?
         BO    IO                 br if yes
*                   shift instruction (89 - 8F)
         LH    4,ADD              get address
         TM    OP,X'04'           8C - 8F instr. ?
         BO    SHD                br if yes (double reg shift)
         SLL   4,27               ) mask out no. bits
         SRL   4,27               )  to shift (single reg shift)
         B     FSH                skip double shift code
SHD      SLL   4,26               ) mask out no. bits
         SRL   4,26               )  to shift (double reg shift)
FSH      SRDL  4,4                calc actual
         LR    5,4                 bits to shift
         MH    5,6(3)             mul by time per bit
         A     5,0(3)             add base time
         TM    ADD,X'F0'          reg zero ?
         BZ    FORM               yes - print timings
         MVI   FLG,X'01'          flag * needed since time approx
         B     FORM               and print timings
IO       L     5,0(3)             get instr. timing
         MVI   FLG,X'01'          flag * needed
         B     FORM               and print timings
*                       Print the time
FORM     LR    4,5                ) calc new
         A     4,TOTAL            )  total time
         CVD   4,TT               make total PD
         CVD   5,TI               make this time PD
         ST    4,8(1)             save new total time
         UNPK  TTD(8),TT+3(5)     unpack total time
         UNPK  TID(8),TI+3(5)     unpack this instr's time
         OI    TID+7,C'0'         make total printable
         OI    TTD+7,C'0'         make this time printable
         LA    3,5                get max no. leading zeroes
         LA    2,TTD              get @ total time
BL1      CLI   0(2),C'0'          zero ?
         BNE   OL1                quit loop if not
         MVI   0(2),C' '          else blank out the zero
         LA    2,1(2)             point @ next byte
         BCT   3,BL1              and loop if necessary
OL1      LA    3,5                get max no. leading zeroes
         LA    2,TID              get @ this instr's time
BL2      CLI   0(2),C'0'          zero ?
         BNE   OL2                quit loop if not
         MVI   0(2),C' '          else blank it out
         LA    2,1(2)             point @ next byte
         BCT   3,BL2              and loop if necessary
OL2      L     2,0(1)             get @ print area
         CLI   FLG,X'01'          * needed ?
         BNE   NS                 br if not
         MVI   99(2),C'*'         add *
NS       MVC   100(6,2),TID       copy integer part of instr. time
         MVI   106(2),C'.'        add .
         MVC   107(2,2),TID+6     copy decimals
         MVC   110(6,2),TTD       copy integer part of total
         MVI   116(2),C'.'        add .
         MVC   117(2,2),TTD+6     copy decimals
         BR    14                 return
*                   constants
TOTAL    DC    F'0'               working total time
XRT      DC    F'125'             constant
XRTC     DC    F'63'              constant
TID      DC    D'0'
TTD      DC    D'0'
TT       DC    D'0'
TI       DC    D'0'
D0       DC    X'000000D0'        constant
F0       DC    X'000000F0'        constant
EGO      DC    X'00000080'        constant
OP       DC    X'00'              ) input opcode
RR       DC    X'00'              ) input reg
ADD      DC    X'0000'            ) input address
FLG      DC    X'00'              flag set to x'01' if * needed
L1       DC    X'00'              work 1st SS length
L2       DC    X'00'              work 2nd SS length
ASSD     DC    A(SSD)             @ timings for SS 2 length instr.
ASSL     DC    A(SSL)             @ timings for SS 1 length instr.
ARSI     DC    A(RSI)             @ timings for RS and SI instr.
ARRX     DC    A(RRX)             @ timings for RR and RX instr.
RRX      DC    F'0'                         00
         DC    F'0'              (UPT)      01
         DC    F'0'                         02
         DC    F'0'                         03
         DC    F'500'             SPM       04
         DC    F'876'             BALR      05
         DC    F'813'             BCTR      06
         DC    F'502'             BCR       07
         DC    F'875'            <SSK>      08
         DC    F'750'            <ISK>      09
         DC    F'2375'            SVC       0A
         DC    F'0'              (BSM)      0B
         DC    F'0'              (BASSM)    0C
         DC    F'0'              (BASR)     0D
         DC    F'0'              (MVCL)     0E
         DC    F'0'              (CLCL)     0F
         DC    F'875'             LPR       10
         DC    F'750'             LNR       11
         DC    F'750'             LTR       12
         DC    F'875'             LCR       13
         DC    F'750'             NR        14
         DC    F'750'             CLR       15
         DC    F'750'             OR        16
         DC    F'750'             XR        17
         DC    F'750'             LR        18
         DC    F'750'             CR        19
         DC    F'750'             AR        1A
         DC    F'875'             SR        1B
         DC    F'8330'            MR        1C
         DC    F'19250'           DR        1D
         DC    F'750'             ALR       1E
         DC    F'750'             SLR       1F
         DC    F'1250'            LPDR      20
         DC    F'1250'            LNDR      21
         DC    F'1250'            LTDR      22
         DC    F'1250'            LCDR      23
         DC    F'1250'            HDR       24
         DC    F'0'              (LRDR)     25
         DC    F'0'              (MXR)      26
         DC    F'0'              (MXDR)     27
         DC    F'1250'            LDR       28
         DC    F'1921'            CDR       29
         DC    F'2253'            ADR       2A
         DC    F'2253'            SDR       2B
         DC    F'25500'           MDR       2C
         DC    F'47250'           DDR       2D
         DC    F'2086'            AWR       2E
         DC    F'2087'            SWR       2F
         DC    F'750'             LPER      30
         DC    F'750'             LNER      31
         DC    F'750'             LTER      32
         DC    F'750'             LCER      33
         DC    F'750'             HER       34
         DC    F'0'              (LRER)     35
         DC    F'0'              (AXR)      36
         DC    F'0'              (SXR)      37
         DC    F'750'             LER       38
         DC    F'1174'            CER       39
         DC    F'1430'            AER       3A
         DC    F'1430'            SER       3B
         DC    F'7630'            MER       3C
         DC    F'12810'           DER       3D
         DC    F'1359'            AUR       3E
         DC    F'1359'            SUR       3F
         DC    F'1000'            STH       40
         DC    F'1000'            LA        41
         DC    F'1000'            STC       42
         DC    F'938'             IC        43
         DC    F'875'             EX    *   44
         DC    F'1125'            BAL       45
         DC    F'1126'            BCT       46
         DC    F'938'             BC        47
         DC    F'1063'            LH        48
         DC    F'1000'            CH        49
         DC    F'1063'            AH        4A
         DC    F'1063'            SH        4B
         DC    F'4500'            MH        4C
         DC    F'0'              (BAS)      4D
         DC    F'7250'            CVD       4E
         DC    F'6063'            CVB       4F
         DC    F'1250'            ST        50
         DC    F'0'                         51
         DC    F'0'                         52
         DC    F'0'                         53
         DC    F'1188'            N         54
         DC    F'1188'            CL        55
         DC    F'1188'            O         56
         DC    F'1188'            X         57
         DC    F'1188'            L         58
         DC    F'1188'            C         59
         DC    F'1188'            A         5A
         DC    F'1188'            S         5B
         DC    F'8640'            M         5C
         DC    F'19670'           D         5D
         DC    F'1188'            AL        5E
         DC    F'1188'            SL        5F
         DC    F'1750'            STD       60
         DC    F'0'                         61
         DC    F'0'                         62
         DC    F'0'                         63
         DC    F'0'                         64
         DC    F'0'                         65
         DC    F'0'                         66
         DC    F'0'              (MXD)      67
         DC    F'1688'            LD        68
         DC    F'2453'            CD        69
         DC    F'2766'            AD        6A
         DC    F'2766'            SD        6B
         DC    F'25940'           MD        6C
         DC    F'47690'           DD        6D
         DC    F'2599'            AW        6E
         DC    F'2599'            SW        6F
         DC    F'1250'            STE       70
         DC    F'0'                         71
         DC    F'0'                         72
         DC    F'0'                         73
         DC    F'0'                         74
         DC    F'0'                         75
         DC    F'0'                         76
         DC    F'0'                         77
         DC    F'1188'            LE        78
         DC    F'1638'            CE        79
         DC    F'1866'            AE        7A
         DC    F'1866'            SE        7B
         DC    F'8060'            ME        7C
         DC    F'13250'           DE        7D
         DC    F'1795'            AU        7E
         DC    F'1866'            SU        7F
RSI      DC    F'1000'            SSM       80
         DC    F'0'
         DC    F'0'                         81
         DC    F'0'
         DC    F'1625'            LPSW      82
         DC    F'0'
         DC    F'0'              (diagnose) 83
         DC    F'0'
         DC    F'937'            <WRD>      84
         DC    F'0'
         DC    F'1250'           <RDD>      85
         DC    F'0'
         DC    F'1625'            BXH       86
         DC    F'0'
         DC    F'1625'            BXLE      87
         DC    F'0'
         DC    F'1800'            SRL       88
         DC    F'344'
         DC    F'1800'            SLL       89
         DC    F'282'
         DC    F'2100'            SRA       8A
         DC    F'344'
         DC    F'2300'            SLA       8B
         DC    F'344'
         DC    F'2650'            SRDL      8C
         DC    F'720'
         DC    F'2500'            SLDL      8D
         DC    F'533'
         DC    F'3200'            SRDA      8E
         DC    F'720'
         DC    F'3300'            SLDA      8F
         DC    F'590'
         DC    F'687'             STM       90
         DC    F'500'
         DC    F'875'             TM        91
         DC    F'0'
         DC    F'938'             MVI       92
         DC    F'0'
         DC    F'0'              (TS)       93
         DC    F'0'
         DC    F'938'             NI        94
         DC    F'0'
         DC    F'875'             CLI       95
         DC    F'0'
         DC    F'938'             OI        96
         DC    F'0'
         DC    F'938'             XI        97
         DC    F'0'
         DC    F'625'             LM        98
         DC    F'500'
         DC    F'0'              (TRACE)    99
         DC    F'0'
         DC    F'0'                         9A
         DC    F'0'
         DC    F'0'                         9B
         DC    F'0'
         DC    F'4875'           <SIO>      9C
         DC    F'0'
         DC    F'4875'           <TIO>      9D
         DC    F'0'
         DC    F'3250'           <HIO>      9E
         DC    F'0'
         DC    F'1375'           <TCH>      9F
         DC    F'0'
         DC    96F'0'                       A0 to CF          CFK*JLY99
SSL      DC    F'0'                         D0
         DC    F'0'
         DC    F'1688'            MVN       D1
         DC    F'375'
         DC    F'1657'            MVC       D2
         DC    F'250'
         DC    F'1688'            MVZ       D3
         DC    F'375'
         DC    F'1688'            NC        D4
         DC    F'313'
         DC    F'1204'            CLC       D5
         DC    F'654'
         DC    F'1688'            OC        D6
         DC    F'313'
         DC    F'1688'            XC        D7
         DC    F'313'
         DC    F'0'                         D8
         DC    F'0'
         DC    F'0'              (MVCK)     D9
         DC    F'0'
         DC    F'0'              (MVCP)     DA
         DC    F'0'
         DC    F'0'              (MVCS)     DB
         DC    F'0'
         DC    F'1688'            TR        DC
         DC    F'625'
         DC    F'2188'            TRT       DD
         DC    F'375'
         DC    F'2125'            ED        DE
         DC    F'1000'
         DC    F'2125'            EDMK      DF
         DC    F'1000'
         DC    32F'0'                       E0 to EF          CFK*JLY99
SSD      DC    F'0'              (SRP)      F0
         DC    F'0'
         DC    F'1375'            MVO       F1
         DC    F'563'
         DC    F'1188'            PACK      F2
         DC    F'700'
         DC    F'1835'            UNPK      F3
         DC    F'390'
         DC    F'0'                         F4
         DC    F'0'
         DC    F'0'                         F5
         DC    F'0'
         DC    F'0'                         F6
         DC    F'0'
         DC    F'0'                         F7
         DC    F'0'
         DC    F'2532'            ZAP       F8
         DC    F'250'
         DC    F'2345'            CP        F9
         DC    F'250'
         DC    F'2875'            AP        FA
         DC    F'375'
         DC    F'2875'            SP        FB
         DC    F'375'
         DC    F'2667'            MP        FC
         DC    F'1500'
         DC    F'1313'            DP        FD
         DC    F'2100'
         DC    F'0'                         FE
         DC    F'0'
         DC    F'0'                         FF
         DC    F'0'
         END

