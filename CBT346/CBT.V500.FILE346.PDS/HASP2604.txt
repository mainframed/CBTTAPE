*********************************************************************
*                                                                   *
*                        HASP EXIT 2                                *
*                                                                   *
*     This exit gets control when each JOB statement or JOB         *
*     JOB continuation statement is processed and performs the      *
*     following functions:                                          *
*                                                                   *
*     >  Obtain storage for and initialize the SWBTJCT control      *
*        block, as an extension to the IBM JCT (JCTX).              *
*     >  Scan a jcl job card for a symbolic jobclass on the         *
*        CLASS= keyword and convert the symbolic designation to     *
*        a standard jobclass (A-Z, or 0-9).  An installation        *
*        defined table (HASP2TAB) is used to equate the symbolic    *
*        designations with the standard classes.  A symbolic        *
*        jobclass may be composed of one to eight alphanumeric      *
*        characters.                                                *
*     >  Scan a jcl job card for a symbolic msgclass on the         *
*        MSGCLASS= keyword and convert the symbolic designation     *
*        to a standard msgclass (A-Z, or 0-9).  An installation     *
*        defined table (HASP2TAB) is used to equate the symbolic    *
*        designations with the standard classes.  A symbolic        *
*        msgclass may be composed of one to eight alphanumeric      *
*        characters.                                                *
*                                                                   *
*     Entry point: EXIT02                                           *
*                                                                   *
*     Input registers:                                              *
*        R0     Code indicating type of job statement being scanned *
*               0  - initial JOB statement image                    *
*               4  - subsequent JOB statement continuation image    *
*        R1     Address of 3-word parameter list                    *
*        R2-9   N/A                                                 *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    N/A                                                 *
*        R13    PCE address                                         *
*        R14    Return address                                      *
*        R15    Entry address                                       *
*                                                                   *
*     Output registers:                                             *
*        R0-14  Unchanged                                           *
*        R15    Return code                                         *
*                                                                   *
*     Register usage(internal):                                     *
*        R0-8   Work                                                *
*        R9     EXIT02 workarea address                             *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    Base register                                       *
*        R13    PCE address                                         *
*        R14-15 Work and linkage                                    *
*                                                                   *
*     NOTE:  HASP2TAB is built by started task HASP2BLD, which      *
*            reads entries from the SUPT.HASP2TAB dataset.          *
*                                                                   *
*            This module is for use with JES2 Version 6.0.4         *
*                                                                   *
*     Author: Bob Break                                             *
*                                                                   *
*********************************************************************

         COPY  $HASPGBL

HASPEX02 $MODULE ENVIRON=JES2,                                         X
               RMODE=ANY,                                              X
               SPLEVEL=NOCHECK,                                        X
               CVT,                                                    X
               JESCT,                                                  X
               PSA,                                                    X
               RPL,                                                    X
               SSCT,                                                   X
               $BUFFER,                                                X
               $CADDR,                                                 X
               $HASPEQU,                                               X
               $HCCT,                                                  X
               $HCT,                                                   X
               $JCT,                                                   X
               $JCTX,                                                  X
               $JQE,                                                   X
               $KIT,                                                   X
               $MIT,                                                   X
               $MITETBL,                                               X
               $PADDR,                                                 X
               $PARMLST,                                               X
               $PCE,                                                   X
               $PSV,                                                   X
               $TRE,                                                   X
               $USERCBS,                                               X
               $XECB

EXIT02  $ENTRY BASE=R12            EXIT02 routine entry point

*********************************************************************
*                                                                   *
*        Save caller's registers and establish addressabilities.    *
*                                                                   *
*********************************************************************

EXIT000 $SAVE  NAME=EXIT02         Save caller's registers
         LR    R12,R15             Set base register
         ST    R0,PCEUSER0         Save entry R0
         L     R2,0(,R1)           Get job statement buffer pointer
         USING JCT,R10

*********************************************************************
*                                                                   *
*        Obtain a module workarea.                                  *
*                                                                   *
*********************************************************************

         LA    R1,EXIT02WL         Get workarea length
         LA    R1,3(,R1)           Round up to word boundary
         SRL   R1,2                Convert to number of words
         LA    R1,1(,R1)           Add one for identifier
        $GETWORK WORDS=(R1),USE=EX02
         LA    R1,4(,R1)           Point past identifier
         LR    R9,R1               Set workarea address
         LR    R14,R1              Set move to address
         LA    R15,EXIT02WL        Set move to length
         XR    R1,R1               Set move from length and pad
         MVCL  R14,R0              Clear workarea
         USING EXIT02W,R9
         ST    R2,JOBBUFP          Save job statement image buffer ptr
         L     R0,PCEUSER0         Restore entry register 0
         LTR   R0,R0               Continuation job statement?
         BNZ   EXIT020             Yes - branch, continue

*********************************************************************
*                                                                   *
*        Obtain storage for the SWBTJCT control block and           *
*        initialize it.                                             *
*                                                                   *
*********************************************************************

EXIT010 $JCTXADD JCT=JCT,TYPE='SWBT',MOD=1,LENGTH=JCTXTLEN,            X
               OKRET=EXIT011,FOUND=EXIT019,ERRET=EXIT019
EXIT011  LTR   R15,R15             Extension added?
         BNZ   EXIT019             No - branch, continue
         IPK   0(0)                Get current psw key
         SPKA  0(0)                Set psw key zero
         LA    R3,JCXBASLN(,R1)    Point to SWBTJCT
         LR    R14,R3              Get SWBTJCT addr in work register
         USING SWBTJCT,R3
         LA    R15,SWBTJCTL        Set move to length
         XR    R1,R1               Set move from length and pad
         MVCL  R14,R0              Clear SWBTJCT
         MVC   SWBJCTID,=C'SJCT'   Set identifier
         MVI   SWBJCTVR,SWBJCTV#   Set version number
         LA    R15,SWBTJCTL        Get SWBTJCT length
         STCM  R15,7,SWBJCTLN      Set length
         MVC   SWBJSYMJ,$BLANKS    Blank init symbolic jobclass
         SPKA  0(R2)               Reset psw key
EXIT019  DS    0H
JCTXTLEN EQU   JCXBASLN+SWBTJCTL   JCTX total length
         DROP  R3                  SWBTJCT

*********************************************************************
*                                                                   *
*        Find and set the pointer to symbolic jobclass/msgclass     *
*        table (HASP2TAB).                                          *
*                                                                   *
*********************************************************************

EXIT020  L     R15,CVTPTR          Get pointer to CVT
         L     R15,CVTUSER-CVT(,R15)  Get USERCVT pointer
         L     R15,UCVTJES2-UCVT(,R15)  Get JES2UVT pointer
         L     R15,JES2SYMT-JES2UVT(,R15)  Get HASP2TAB pointer
         ST    R15,H2TAB           Set HASP2TAB pointer

*********************************************************************
*                                                                   *
*        Read the SWBTJCT.                                          *
*                                                                   *
*********************************************************************

EXIT030 $JCTXGET JCT=JCT,TYPE='SWBT',MOD=1,OKRET=EXIT031,ERRET=RETURN
EXIT031  LTR   R15,R15             JCTXGET successful?
         BNZ   RETURN              No - branch, return
         LA    R1,JCXBASLN(,R1)    Point to SWBTJCT
         ST    R1,SWBTJCTP         Save SWBJCT pointer

*********************************************************************
*                                                                   *
*        Determine if a symbolic jobclass has already been set.     *
*                                                                   *
*********************************************************************

EXIT100  L     R1,SWBTJCTP         Get SWBTJCT pointer
         USING SWBTJCT,R1
         CLC   SWBJSYMJ,$BLANKS    Symbolic jobclass previously set?
         BNE   EXIT399             Yes - branch, continue
         MVI   JCLASS,C' '         Blank init actual jobclass
         CLC   JCTNONDE,$NDENAME   Input node = local node?
         BE    EXIT101             Yes - branch, continue
         CLI   JCTXWRK,0           Symbolic class set on input node?
         BE    EXIT399             No - branch, continue
         MVC   JOBCLASS,JCTXWRK    Set symbolic class from input node
         XC    JCTXWRK,JCTXWRK     Clear JCTXWRK
         B     EXIT221             Branch - continue
EXIT101  MVC   JOBCLASS,$BLANKS    Blank init symbolic jobclass
         DROP  R1                  SWBTJCT

*********************************************************************
*                                                                   *
*        Search the job statement image for the CLASS= keyword.     *
*                                                                   *
*********************************************************************

         LA    R3,64               Set maximum search length
         L     R4,JOBBUFP          Get job statement buffer pointer
EXIT150  CLC   0(7,R4),=C',CLASS='  CLASS= keyword?
         BE    EXIT200             Yes - branch, continue
         CLC   0(7,R4),=C' CLASS='  CLASS= Keyword?
         BE    EXIT200             Yes - branch, continue
         LA    R4,1(,R4)           Bump to next buffer position
         BCT   R3,EXIT150          Loop back to check next position
         B     EXIT399             Branch - no CLASS= keyword found

*********************************************************************
*                                                                   *
*        The CLASS= keyword has been found, so check if a symbolic  *
*        jobclass has been specified.                               *
*                                                                   *
*********************************************************************

EXIT200  LA    R4,7(,R4)           Point to actual jobclass
         LA    R5,1(,R4)           Point past possible length 1 class
         CLI   0(R5),C' '          Length 1 class followed by blank?
         BE    EXIT399             Yes - branch, continue
         CLI   0(R5),C','          Length 1 class followed by comma?
         BE    EXIT399             Yes - branch, continue

*********************************************************************
*                                                                   *
*        Isolate the symbolic jobclass and length.                  *
*                                                                   *
*********************************************************************

         LA    R5,1(,R5)           Point to next position
         LA    R3,6                Set loop control
EXIT210  CLI   0(R5),C' '          Blank after symbolic jobclass?
         BE    EXIT220             Yes - branch, get symbolic length
         CLI   0(R5),C','          Comma after symbolic jobclass?
         BE    EXIT220             Yes - branch, get symbolic length
         LA    R5,1(,R5)           Point to next position
         BCT   R3,EXIT210          Branch - check next position
EXIT220  SR    R5,R4               Get symbolic jobclass length
         STH   R5,JCLASSL          Set symbolic jobclass length
         BCTR  R5,0                Minus 1 for execute
         MVC   JOBCLASS(0),0(R4)   Executed instruction
         EX    R5,*-6              Set symbolic jobclass

*********************************************************************
*                                                                   *
*        Issue the WTO with the symbolic jobclass information.      *
*        Set the symbolic jobclass in the SWBTJCT for insertion     *
*        into the JSABUVT at execution time.                        *
*                                                                   *
*********************************************************************

EXIT221  MVC   MSGAREA(MSGLEN),MSGSKEL  Set message skeleton
         MVC   MSGJOBID,JCTJOBID   Set jobid in message area
         MVC   MSGJNAM,JCTJNAME    Set jobname in message area
         MVC   MSGSYMJ,JOBCLASS    Set jobclass in message area
         LA    R1,MSGAREA          Point to WTO message area
         LA    R0,MSGLEN           Set WTO message length
        $WTO   (R1),(R0),JOB=NO    Issue the WTO
         L     R1,SWBTJCTP         Get SWBTJCT pointer
         USING SWBTJCT,R1
         IPK   0(0)                Get current psw key
         SPKA  0(0)                Set psw key zero
         MVC   SWBJSYMJ,JOBCLASS   Set symbolic jobclass in SWBTJCT
         SPKA  0(R2)               Reset psw key
         DROP  R1                  SWBTJCT

*********************************************************************
*                                                                   *
*        Search HASP2TAB for a match for the symbolic jobclass.     *
*                                                                   *
*********************************************************************

         ICM   R6,15,H2TAB         Point to the HASP2TAB
         BZ    EXIT399             No HASP2TAB - branch, continue
         USING HASP2TAB,R6
         LH    R3,HASP2CNT         Get number of HASP2TAB entries
         MVC   JCLASS,HASP2JER     Save error jobclass
         L     R6,HASP2SCP         Point to symbolic class entries
         DROP  R6                  HASP2TAB
         USING H2SYMCLS,R6
EXIT230  CLI   H2SYMTYP,C'J'       Symbolic jobclass entry?
         BNE   EXIT240             No - branch, check next entry
         CLC   JOBCLASS,H2SYMNAM   Jobclass match?
         BE    EXIT250             Yes - translate to actual jobclass
EXIT240  LA    R6,H2ENTLEN(,R6)    Point to next table entry
         BCT   R3,EXIT230          Loop back to check next table entry

*********************************************************************
*                                                                   *
*        The specified symbolic jobclass has not been found in the  *
*        HASP2TAB.  Check if a error jobclass has been supplied     *
*        in HASP2TAB.  If so, use it.  Otherwise, return to the     *
*        caller and fail the job with a jcl error.                  *
*                                                                   *
*********************************************************************

         CLI   JCLASS,C' '         Error jobclass supplied?
         BNE   EXIT300             Yes - branch, continue
         MVC   RETCODE,$F8         Set cancel job w/output return code
         NI    JCTXMASK,B'11011111'    Turn off job's EXIT2 mask
*                         ||||||||       so this exit is not entered
*                 EXITS = 01234567         again for continuation
*                                            statements
         B     RETURN              Branch - return

*********************************************************************
*                                                                   *
*        The symbolic jobclass has been found in HASP2TAB.          *
*        Translate it to an actual jobclass and set it in the JCT.  *
*                                                                   *
*********************************************************************

EXIT250  MVC   JCLASS,H2SYMMAP     Save mapped jobclass
EXIT300  MVC   JCTJCLAS,JCLASS     Set execution jobclass in JCT
         MVC   JCTCLASS,JCLASS     Set execution jobclass in JCT
         MVC   JCTAXCLS,JCLASS     Set execution jobclass in JCT
         DROP  R6                  H2SYMCLS

*********************************************************************
*                                                                   *
*        Replace the symbolic jobclass with the actual jobclass in  *
*        the buffer.  This must be done to make the change          *
*        effective and also to prevent a jcl error.                 *
*        At this point, R4 points to the symbolic jobclass.         *
*                                                                   *
*********************************************************************

         CLC   JCTNONDE,$NDENAME   Input node = local node?
         BNE   EXIT390             No - branch, continue
         MVC   0(1,R4),JCLASS      Set actual jobclass
         L     R5,JOBBUFP          Point to buffer start
         LA    R5,72(,R5)          Point to last buffer position
         SR    R5,R4               Get text length past CLASS=
         LH    R3,JCLASSL          Get length of symbolic jobclass
         SR    R5,R3               Get text length to adjust backwards
         LA    R7,0(R4,R3)         Get from position for compress
         LA    R6,1(,R4)           Get receive position for compress
         BCTR  R5,0                Minus 1 for execute
         MVC   0(0,R6),0(R7)       Executed instruction
         EX    R5,*-6              Compress the text
         LA    R5,1(,R5)           Get adjust length back
         LA    R6,1(R4,R5)         Get receive position for blank pad
         MVI   0(R6),C' '          Blank pad first position
         CH    R3,$H2              Symbolic length > 2?
         BNH   EXIT390             No - branch, blank pad complete
         LA    R7,1(,R6)           Get next position for blank pad
         SH    R3,$H2              Adjust pad length to CLASSLEN-1
*                                    less the 1st byte already padded
         BCTR  R3,0                Minus 1 for execute
         MVC   0(0,R7),0(R6)       Executed instruction
         EX    R3,*-6              Blank pad remainder of text buffer
EXIT390  DS    0H
        $QSUSE                     Request access to checkpoint queues
        $DOGJQE ACTION=(FETCH,UPDATE),JQE=PCEJQE,                      X
               ERRET=EXIT399,OKRET=EXIT391
EXIT391  LR    R1,R0               Get JQA address
         USING JQE,R1
         MVC   JQESYMJC,JOBCLASS   Set symbolic jobclass in JQE
        $DOGJQE ACTION=RETURN,CBADDR=JQE
         DROP  R1                  JQE
EXIT399  DS    0H

*********************************************************************
*                                                                   *
*        Determine if "MSGCLASS=" has been previously found.        *
*                                                                   *
*********************************************************************

EXIT400  L     R1,SWBTJCTP         Get SWBTJCT pointer
         USING SWBTJCT,R1
         TM    SWBJFLAG,SWBJMSGC   "MSGCLASS=" previously found?
         BO    EXIT699             Yes - branch, continue
         MVI   MCLASS,C' '         Blank init actual msgclass
         MVC   MSGCLASS,$BLANKS    Blank init symbolic msgclass

*********************************************************************
*                                                                   *
*        Search the job statement image for the MSGCLASS= keyword.  *
*                                                                   *
*********************************************************************

         LA    R3,61               Set maximum search length
         L     R4,JOBBUFP          Get job statement buffer pointer
EXIT450  CLC   0(10,R4),=C',MSGCLASS='  MSGCLASS= keyword?
         BE    EXIT500             Yes - branch, continue
         CLC   0(10,R4),=C' MSGCLASS='  MSGCLASS= keyword?
         BE    EXIT500             Yes - branch, continue
         LA    R4,1(,R4)           Bump to next buffer position
         BCT   R3,EXIT450          Loop back to check next position
         B     EXIT699             Branch - no MSGCLASS= keyword found

*********************************************************************
*                                                                   *
*        The MSGCLASS= keyword has been found, so check if a        *
*        symbolic msgclass has been specified.                      *
*                                                                   *
*********************************************************************

EXIT500  OI    SWBJFLAG,SWBJMSGC   Set "MSGCLASS=" keyword found flag
         DROP  R1                  SWBTJCT
         LA    R4,10(,R4)          Point to actual msgclass
         LA    R5,1(,R4)           Point past possible length 1 class
         CLI   0(R5),C' '          Length 1 msgclass followed by blank?
         BE    EXIT699             Yes - branch, continue
         CLI   0(R5),C','          Length 1 msgclass followed by comma?
         BE    EXIT699             Yes - branch, continue

*********************************************************************
*                                                                   *
*        Isolate the symbolic msgclass and length.                  *
*                                                                   *
*********************************************************************

         LA    R5,1(,R5)           Point to next position
         LA    R3,6                Set loop control
EXIT510  CLI   0(R5),C' '          Blank after symbolic msgclass?
         BE    EXIT520             Yes - branch, get symbolic length
         CLI   0(R5),C','          Comma after symbolic msgclass?
         BE    EXIT520             Yes - branch, get symbolic length
         LA    R5,1(,R5)           Point to next position
         BCT   R3,EXIT510          Branch - check next position
EXIT520  SR    R5,R4               Get symbolic msgclass length
         STH   R5,MCLASSL          Set symbolic msgclass length
         BCTR  R5,0                Minus 1 for execute
         MVC   MSGCLASS(0),0(R4)   Executed instruction
         EX    R5,*-6              Set symbolic msgclass

*********************************************************************
*                                                                   *
*        Search HASP2TAB for a match for the symbolic msgclass.     *
*                                                                   *
*********************************************************************

         ICM   R6,15,H2TAB         Point to the HASP2TAB
         BZ    EXIT699             No HASP2TAB - branch, continue
         USING HASP2TAB,R6
         LH    R3,HASP2CNT         Get number of HASP2TAB entries
         MVC   MCLASS,HASP2MER     Save error msgclass
         L     R6,HASP2SCP         Point to symbolic class entries
         DROP  R6                  HASP2TAB
         USING H2SYMCLS,R6
EXIT530  CLI   H2SYMTYP,C'M'       Symbolic msgclass entry?
         BNE   EXIT540             No - branch, check next entry
         CLC   MSGCLASS,H2SYMNAM   Msgclass match?
         BE    EXIT600             Yes - translate to actual msgclass
EXIT540  LA    R6,H2ENTLEN(,R6)    Point to next table entry
         BCT   R3,EXIT530          Loop back to check next table entry

*********************************************************************
*                                                                   *
*        The specified symbolic msgclass has not been found in      *
*        HASP2TAB.  Check if a error msgclass has been supplied     *
*        in HASP2TAB.  If so, use it.  Otherwise, return to the     *
*        caller and let the job get a jcl error.                    *
*                                                                   *
*********************************************************************

         CLI   MCLASS,C' '         Error msgclass supplied?
         BNE   EXIT650             Yes - replace symbolic msgclass
         MVC   RETCODE,$F8         Set cancel job w/output return code
         NI    JCTXMASK,B'11011111'    Turn off job's EXIT2 mask
*                         ||||||||       so this exit is not entered
*                 EXITS = 01234567         again for continuation
*                                            statements
         B     RETURN              Branch - return

*********************************************************************
*                                                                   *
*        The symbolic msgclass has been found in HASP2TAB.          *
*        Translate it to an actual msgclass and put it in the JCT.  *
*                                                                   *
*********************************************************************

EXIT600  MVC   MCLASS,H2SYMMAP     Save mapped msgclass
EXIT650  MVC   JCTMCLAS,MCLASS     Set actual msgclass in JCT
         DROP  R6                  H2SYMCLS

*********************************************************************
*                                                                   *
*        Replace the symbolic msgclass with the actual msgclass in  *
*        the buffer.  This must be done to make the change          *
*        effective and also to prevent a jcl error.                 *
*        At this point, R4 points to the symbolic msgclass.         *
*                                                                   *
*********************************************************************

         MVC   0(1,R4),MCLASS      Set actual msgclass
         L     R5,JOBBUFP          Point to job buffer start
         LA    R5,72(,R5)          Point to last buffer position
         SR    R5,R4               Get text length past CLASS=
         LH    R3,MCLASSL          Get length of symbolic msgclass
         SR    R5,R3               Get text lengtht to adjust backwards
         LA    R7,0(R4,R3)         Get from position for compress
         LA    R6,1(,R4)           Get receive position for compress
         BCTR  R5,0                Minus 1 for execute
         MVC   0(0,R6),0(R7)       Executed instruction
         EX    R5,*-6              Compress the text
         LA    R5,1(,R5)           Get adjust length back
         LA    R6,1(R4,R5)         Get receive position for blank pad
         MVI   0(R6),C' '          Blank pad first position
         CH    R3,$H2              Symbolic length > 2?
         BNH   RETURN              No - blank pad complete
         LA    R7,1(,R6)           Get next position for blank pad
         SH    R3,$H2              Adjust pad length to CLASSLEN-1
*                                    less the 1st byte already padded
         BCTR  R3,0                Minus 1 for execute
         MVC   0(0,R7),0(R6)       Executed instruction
         EX    R3,*-6              Blank pad remainder of text buffer
EXIT699  DS    0H

*********************************************************************
*                                                                   *
*        Set return code, restore caller's registers and return.    *
*                                                                   *
*********************************************************************

RETURN   L     R2,RETCODE          Get routine return code
         S     R9,$F4              Get EXIT02 workarea address
        $RETWORK (R9)              Return workarea
         LR    R15,R2              Set routine return code
        $RETURN RC=(R15)           Return to caller

         DROP  R9,R10              EXIT02W, JCT

         LTORG

MSGSKEL  EQU   *                   WTO message skeleton
         DC    X'900F'             Message id
         DC    CL8' '              Jobid from JCTJOBID
         DC    C' '
         DC    CL8' '              Jobname from JCTJNAME
         DC    C' '
         DC    CL8' '              Symbolic jobclass from jobcard
MSGLEN   EQU   *-MSGSKEL           Total length of WTO message

        $MODEND

EXIT02W  DSECT                     EXIT02 workarea
RETCODE  DS    F                   EXIT02 return code
JOBBUFP  DS    A                   Job statement image buffer pointer
SWBTJCTP DS    A                   SWBTJCT pointer
H2TAB    DS    A                   HASP2TAB pointer
JCLASS   DS    CL1                 Actual jobclass
MCLASS   DS    CL1                 Actual msgclass
JOBCLASS DS    CL8                 Symbolic jobclass
MSGCLASS DS    CL8                 Symbolic msgclass
JCLASSL  DS    H                   Symbolic jobclass length
MCLASSL  DS    H                   Symbolic msgclass length
MSGAREA  DS    0C                  WTO message area
MSGIDENT DS    XL2                 Message identifier
MSGJOBID DS    CL8                 Jobid from JCTJOBID
         DS    C
MSGJNAM  DS    CL8                 Jobname from JCTJNAME
         DS    C
MSGSYMJ  DS    CL8                 Symbolic jobclass from jobcard
EXIT02WL EQU   *-EXIT02W           EXIT02 workarea length

        $SWBTJCT
         HASP2TAB
         JES2UVT
         UCVT

         END
