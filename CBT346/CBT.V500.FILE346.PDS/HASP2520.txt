*********************************************************************
*                                                                   *
*                        HASP EXIT 2                                *
*                                                                   *
*     Functions:                                                    *
*                                                                   *
*     1. To scan a jcl job card for a symbolic jobclass on the      *
*        CLASS= keyword and to convert this symbolic designation to *
*        a standard jobclass (A-Z, or 0-9).  An installation        *
*        defined table (HASP2TAB) is used to equate the symbolic    *
*        designations with the standard classes.  A symbolic        *
*        jobclass may be composed of one to eight alphanumeric      *
*        characters.                                                *
*                                                                   *
*     2. To scan a jcl job card for a symbolic msgclass on the      *
*        MSGCLASS= keyword and to convert this symbolic designation *
*        to a standard msgclass (A-Z, or 0-9).  An installation     *
*        defined table (HASP2TAB) is used to equate the symbolic    *
*        designations with the standard classes.  A symbolic        *
*        msgclass may be composed of one to eight alphanumeric      *
*        characters.                                                *
*                                                                   *
*     Entry point: EXIT02                                           *
*                                                                   *
*     Input registers:                                              *
*        R0     Code indicating type of job statement being scanned *
*               0  - initial JOB statement image                    *
*               4  - subsequent JOB statement continuation image    *
*        R1     Address of 3-word parameter list                    *
*        R2-9   N/A                                                 *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    N/A                                                 *
*        R13    PCE address                                         *
*        R14    Return address                                      *
*        R15    Entry address                                       *
*                                                                   *
*     Output registers:                                             *
*        R0-13  N/A                                                 *
*        R14    Return address                                      *
*        R15    Return code                                         *
*                                                                   *
*     Register usage(internal):                                     *
*        R0-1   Work                                                *
*        R2     Job statement image buffer                          *
*        R3-R9  Work                                                *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    Base register                                       *
*        R13    PCE address                                         *
*        R14-15 Work and linkage                                    *
*                                                                   *
*     NOTES: This exit uses the JCTUSER3 and JCTUSER4 fields.       *
*            HASP2TAB is built by started task HASP2BLD, which      *
*            reads entries from the SUPT.HASP2TAB dataset.          *
*            This module is for use with JES2 Version 5.2.0         *
*                                                                   *
*                                                                   *
*     Author: Bob Break                                             *
*                                                                   *
*                                                                   *
*********************************************************************

         COPY  $HASPGBL                  Copy HASP globals

HASPEX02 $MODULE ENVIRON=JES2,                                         X
               $BUFFER,                                                X
               $CADDR,                                                 X
               $HASPEQU,                                               X
               $HCT,                                                   X
               $JCT,                                                   X
               $JQE,                                                   X
               $KIT,                                                   X
               $MIT,                                                   X
               $MITETBL,                                               X
               $PADDR,                                                 X
               $PARMLST,                                               X
               $PCE,                                                   X
               $PSV,                                                   X
               RPL,                                                    X
               $USERCBS,                                               X
               $XECB

EXIT02  $ENTRY BASE=R12            EXIT02 routine entry point

*********************************************************************
*        Save caller's registers and establish addressabilities.    *
*********************************************************************

        $SAVE                      Save caller's registers
         LR    R12,R15             Load base register
         ST    R0,PCEUSER1         Save entry register 0

         USING JCT,R10             Establish JCT addressability
         L     R2,0(,R1)           Load job statement image buffer
         LA    R1,WRKSIZE          Get workarea size
         LA    R1,3(,R1)           Round up to word boundary
         SRL   R1,2                Convert to number of words
         LA    R1,1(,R1)           Add one for identifier
        $GETWORK WORDS=(R1),USE=EX02
         LA    R1,4(,R1)           Point past identifier
         ST    R1,PCEUSER0         Save storage address
         LR    R9,R1               Set workarea address
         USING EXIT02W,R9
         XC    RETCODE,RETCODE     Clear EXIT02 return code
         L     R0,PCEUSER1         Restore entry register 0
         LTR   R0,R0               Continuation job statement?
         BZ    EXIT010             No - branch, continue
         CLC   JCTUSER3(8),$BLANKS  Symbolic jobclass already found?
         BNE   EXIT400             Yes - branch, check msgclass
EXIT010  MVI   JCLASS,C' '         Clear actual jobclass
         MVC   JOBCLASS,$BLANKS    Clear symbolic jobclass
         ICM   R1,15,PCEJQE        Get JQE address
         BZ    EXIT050             Branch - no JQE
         USING JQE,R1
         MVC   JQESYMJC(8),$BLANKS  Clear symbolic jobclass in JQE
         DROP  R1                  JQE
EXIT050  MVI   MCLASS,C' '         Clear actual msgclass
         MVC   MSGCLASS,$BLANKS    Clear symbolic msgclass

*********************************************************************
*        Find and save the pointer to HASP2TAB.                     *
*********************************************************************

         L     R15,CVTPTR          Get pointer to CVT
         ICM   R15,15,CVTUSER-CVT(R15)  Get USERCVT pointer
         BZ    RETURN1             Branch - no USERCVT
         ICM   R15,15,UCVTJES2-UCVT(R15)  Get JES2UVT pointer
         BZ    RETURN1             Branch - no JES2UVT
         ICM   R15,15,JES2SYMT-JES2UVT(R15)  Get HASP2TAB pointer
         BZ    RETURN1             Branch - no HASP2TAB
         ST    R15,H2TAB           Save HASP2TAB pointer

*********************************************************************
*        Search the job statement image for the CLASS= keyword      *
*********************************************************************

EXIT100  LA    R3,64               Set maximum search length
         LR    R4,R2               Preserve buffer pointer in R2
EXIT150  CLC   0(7,R4),=C',CLASS=' CLASS= keyword?
         BE    EXIT200             Yes
         CLC   0(7,R4),=C' CLASS=' CLASS= Keyword?
         BE    EXIT200             Yes
         LA    R4,1(,R4)           Bump to next buffer position
         BCT   R3,EXIT150          Branch - check next position
         B     EXIT400             No CLASS= - branch, check MSGCLASS

*********************************************************************
*        The CLASS= keyword has been found.  Now see if a symbolic  *
*        jobclass has been specified.                               *
*********************************************************************

EXIT200  LA    R4,7(,R4)           Point to actual jobclass
         LA    R5,1(,R4)           Past possible length 1 jobclass
         CLI   0(R5),C' '          Length 1 jobclass followed by blank?
         BE    EXIT390             Yes - do not translate symbolic
         CLI   0(R5),C','          Length 1 jobclass followed by comma?
         BE    EXIT390             Yes - do not translate symbolic

*********************************************************************
*        Isolate the symbolic jobclass and length.                  *
*********************************************************************

         LA    R5,1(,R5)           Point to next position
         LA    R3,6                Set loop control
EXIT210  CLI   0(R5),C' '          Blank after symbolic jobclass?
         BE    EXIT220             Yes - branch, get symbolic length
         CLI   0(R5),C','          Comma after symbolic jobclass?
         BE    EXIT220             Yes - branch, get symbolic length
         LA    R5,1(,R5)           Point to next position
         BCT   R3,EXIT210          Branch - check next position
EXIT220  SR    R5,R4               Get symbolic jobclass length
         STH   R5,JCLASSL          Save symbolic jobclass length
         BCTR  R5,0                Minus 1 for execute
         MVC   JOBCLASS(0),0(R4)   Executed instruction
         EX    R5,*-6              Save symbolic jobclass
         ICM   R1,15,PCEJQE        Get JQE address
         BZ    EXIT230             Branch - no JQE
         LH    R5,JCLASSL          Get symbolic jobclass length back
         BCTR  R5,0                Minus 1 for execute
         USING JQE,R1
         MVC   JQESYMJC(8),$BLANKS  Preset symbolic class to blanks
         MVC   JQESYMJC(0),0(R4)   Executed instruction
         EX    R5,*-6              Set symbolic class in JQE
         DROP  R1                  JQE

*********************************************************************
*        Issue the WTO with the symbolic jobclass information.      *
*        Save the symbolic jobclass in JCTUSER3/JCTUSER4 for        *
*        insertion into the JSABUVT in later processing.            *
*********************************************************************

EXIT230  MVC   MSGAREA(MSGLEN),MSGSKEL  Set message skeleton
         MVC   MSGJOBID,JCTJOBID   Set jobid in message area
         MVC   MSGJNAM,JCTJNAME    Set jobname in message area
         MVC   MSGSYMJ,JOBCLASS    Set jobclass in message area
         LA    R1,MSGAREA          Point to WTO message area
         LA    R0,MSGLEN           Set WTO message length
        $WTO   (R1),(R0),JOB=NO    Issue the WTO
         MVC   JCTUSER3(8),JOBCLASS  Set symbolic jobclass in JCT

*********************************************************************
*        Search HASP2TAB for a match for the symbolic jobclass.     *
*********************************************************************

         ICM   R6,15,H2TAB         Point to the HASP2TAB module
         BZ    RETURN1             No HASP2TAB - return to caller
         USING HASP2TAB,R6
         LH    R3,HASP2CNT         Get number of HASP2TAB entries
         MVC   JCLASS,HASP2JER     Save error jobclass
         L     R6,HASP2SCP         Point to symbolic class entries
         DROP  R6                  HASP2TAB
         USING H2SYMCLS,R6
EXIT240  CLI   H2SYMTYP,C'J'       Symbolic jobclass entry?
         BNE   EXIT250             No - branch, check next entry
         CLC   JOBCLASS,H2SYMNAM   Jobclass match?
         BE    EXIT260             Yes - convert to actual jobclass
EXIT250  LA    R6,H2ENTLEN(,R6)    Point to next table entry
         BCT   R3,EXIT240          Branch - check next table entry

*********************************************************************
*        The specified symbolic jobclass has not been found in      *
*        HASP2TAB.  Check if a error jobclass has been supplied     *
*        in HASP2TAB.  If so, use it.  Otherwise, return to the     *
*        caller and let the job get a jcl error.                    *
*********************************************************************

         CLI   JCLASS,C' '         Error jobclass supplied?
         BNE   EXIT300             Yes - replace symbolic jobclass
         MVC   RETCODE,$F8         Tell JES to cancel job w/output
         NI    JCTXMASK,B'11011111'    Turn off job's EXIT2 mask
*                         ||||||||       so this exit is not entered
*                 EXITS = 01234567         again for continuation
*                                            statements
         B     RETURN1             Branch - return to caller

*********************************************************************
*        The symbolic jobclass has been found in HASP2TAB.          *
*        Translate it to an actual jobclass and put it in the JCT.  *
*********************************************************************

EXIT260  MVC   JCLASS,H2SYMMAP     Save mapped jobclass
EXIT300  MVC   JCTJCLAS,JCLASS     Set execution jobclass
         MVC   JCTCLASS,JCLASS     Set execution jobclass
         MVC   JCTAXCLS,JCLASS     Set execution jobclass
         DROP  R6                  H2SYMCLS

*********************************************************************
*        Replace the symbolic jobclass with the actual jobclass in  *
*        the buffer.  This must be done to make the change          *
*        effective and also to prevent a jcl error.                 *
*        At this point, R4 points to the symbolic jobclass.         *
*********************************************************************

         MVC   0(1,R4),JCLASS      Set actual jobclass
         LR    R5,R2               Set R5 to buffer start
         LA    R5,72(,R5)          Set R5 to last buffer position
         SR    R5,R4               Text length past CLASS=
         LH    R3,JCLASSL          Get length of symbolic jobclass
         SR    R5,R3               Length of text to adjust backwards
         LA    R7,0(R4,R3)         From position for compress
         LA    R6,1(,R4)           Receive position for compress
         BCTR  R5,0                Minus 1 for execute
         MVC   0(0,R6),0(R7)       Executed instruction
         EX    R5,*-6              Compress the text
         LA    R5,1(,R5)           Get adjust length back
         LA    R6,1(R4,R5)         Receive position for blank pad
         MVI   0(R6),C' '          Blank pad first position
         CH    R3,$H2              Symbolic length > 2?
         BNH   EXIT390             No - blank pad finished
         LA    R7,1(,R6)           Get next position for blank pad
         SH    R3,$H2              Adjust pad lenght to CLASSLEN-1
*                                  less the 1st byte already padded
         BCTR  R3,0                Minus 1 for execute
         MVC   0(0,R7),0(R6)       Executed instruction
         EX    R3,*-6              Blank pad remainder of text buffer
EXIT390  ICM   R1,15,PCEJQE        Get JQE address
         BZ    EXIT400             Branch - no JQE
        $QSUSE                     Obtain queue access
        $CKPT  ID=JQE,ADDR=(R1)    Checkpoint the JQE

*********************************************************************
*        Search the job statement image for the MSGCLASS= keyword   *
*********************************************************************

EXIT400  LA    R3,61               Set maximum search length
         LR    R4,R2               Preserve buffer pointerinN R2
EXIT450  CLC   0(10,R4),=C',MSGCLASS='   MSGCLASS= keyword?
         BE    EXIT500             Yes
         CLC   0(10,R4),=C' MSGCLASS='   MSGCLASS= keyword?
         BE    EXIT500             Yes
         LA    R4,1(,R4)           Bump to next buffer position
         BCT   R3,EXIT450          Branch - check next position
         B     RETURN1             Retrun to caller - no MSGCLASS=

*********************************************************************
*        The MSGCLASS= keyword has been found.  Now see if a        *
*        symbolic msgclass has been specified.                      *
*********************************************************************

EXIT500  LA    R4,10(,R4)          Point to actual msgclass
         LA    R5,1(,R4)           Past possible length 1 msgclass
         CLI   0(R5),C' '          Length 1 msgclass followed by blank?
         BE    RETURN1             Yes - do not translate symbolic
         CLI   0(R5),C','          length 1 msgclass followed by comma?
         BE    RETURN1             Yes - do not translate symbolic

*********************************************************************
*        Isolate the symbolic msgclass and length.                  *
*********************************************************************

         LA    R5,1(,R5)           Point to next position
         LA    R3,6                Set loop control
EXIT510  CLI   0(R5),C' '          Blank after symbolic msgclass?
         BE    EXIT520             Yes - branch, get symbolic length
         CLI   0(R5),C','          Comma after symbolic msgclass?
         BE    EXIT520             Yes - branch, get symbolic length
         LA    R5,1(,R5)           Point to next position
         BCT   R3,EXIT510          Branch - check next position
EXIT520  SR    R5,R4               Get symbolic msgclass length
         STH   R5,MCLASSL          Save symbolic msgclass length
         BCTR  R5,0                Minus 1 for execute
         MVC   MSGCLASS(0),0(R4)   Executed instruction
         EX    R5,*-6              Save symbolic msgclass

*********************************************************************
*        Search HASP2TAB for a match for the symbolic msgclass.     *
*********************************************************************

         ICM   R6,15,H2TAB         Point to the HASP2TAB module
         BZ    RETURN1             No HASP2TAB - return to caller
         USING HASP2TAB,R6
         LH    R3,HASP2CNT         Get number of HASP2TAB entries
         MVC   MCLASS,HASP2MER     Save error msgclass
         L     R6,HASP2SCP         Point to symbolic class entries
         DROP  R6                  HASP2TAB
         USING H2SYMCLS,R6
EXIT530  CLI   H2SYMTYP,C'M'       Symbolic msgclass entry?
         BNE   EXIT540             No - branch, check next entry
         CLC   MSGCLASS,H2SYMNAM   Msgclass match?
         BE    EXIT600             Yes - convert to actual msgclass
EXIT540  LA    R6,H2ENTLEN(,R6)    Point to next table entry
         BCT   R3,EXIT530          Branch - check next table entry

*********************************************************************
*        The specified symbolic msgclass has not been found in      *
*        HASP2TAB.  Check if a error msgclass has been supplied     *
*        in HASP2TAB.  If so, use it.  Otherwise, return to the     *
*        caller and let the job get a jcl error.                    *
*********************************************************************

         CLI   MCLASS,C' '         Error msgclass supplied?
         BNE   EXIT650             Yes - replace symbolic msgclass
         MVC   RETCODE,$F8         Tell JES to cancel job w/output
         NI    JCTXMASK,B'11011111'    Turn off job's EXIT2 mask
*                         ||||||||       so this exit is not entered
*                 EXITS = 01234567         again for continuation
*                                            statements
         B     RETURN1             Branch - return to caller

*********************************************************************
*        The symbolic msgclass has been found in HASP2TAB.          *
*        Translate it to an actual msgclass and put it in the JCT.  *
*********************************************************************

EXIT600  MVC   MCLASS,H2SYMMAP     Save mapped msgclass
EXIT650  MVC   JCTMCLAS,MCLASS     Set actual msgclass
         DROP  R6                  H2SYMCLS

*********************************************************************
*        Replace the symbolic msgclass with the actual msgclass in  *
*        the buffer.  This must be done to make the change          *
*        effective and also to prevent a jcl error.                 *
*        At this point, R4 points to the symbolic msgclass.         *
*********************************************************************

         MVC   0(1,R4),MCLASS      Set actual msgclass
         LR    R5,R2               Set R5 to buffer start
         LA    R5,72(,R5)          Set R5 to last buffer position
         SR    R5,R4               Text length past CLASS=
         LH    R3,MCLASSL          Get length of symbolic msgclass
         SR    R5,R3               Length of text to adjust backwards
         LA    R7,0(R4,R3)         From position for compress
         LA    R6,1(,R4)           Receive position for compress
         BCTR  R5,0                Minus 1 for execute
         MVC   0(0,R6),0(R7)       Executed instruction
         EX    R5,*-6              Compress the text
         LA    R5,1(,R5)           Get adjust length back
         LA    R6,1(R4,R5)         Receive position for blank pad
         MVI   0(R6),C' '          Blank pad first position
         CH    R3,$H2              Symbolic length > 2?
         BNH   RETURN1             No - blank pad finished
         LA    R7,1(,R6)           Get next position for blank pad
         SH    R3,$H2              Adjust pad length to CLASSLEN-1
*                                  less the 1st byte already padded
         BCTR  R3,0                Minus 1 for execute
         MVC   0(0,R7),0(R6)       Executed instruction
         EX    R3,*-6              Blank pad remainder of text buffer

*********************************************************************
*        Check if JCTUSER3/JCTUSER4 have been set with a symbolic   *
*        jobclass or if they have been blanked out by a previous    *
*        pass thru this exit.  If not zeros (original content),     *
*        set return code, restore caller's registers and return.    *
*********************************************************************

RETURN1  CLC   JCTUSER3(8),$ZEROS  JCTUSER3/JCTUSER4 modified?
         BNE   RETURN2             Yes - branch, return to caller
         MVC   JCTUSER3(8),$BLANKS  Blank out JCTUSER3/JCTUSER4
RETURN2  L     R2,RETCODE          Get routine return code
         L     R15,PCEUSER0        Get workarea address
         S     R15,$F4             Subtract four for identifier
        $RETWORK (R15)             Return workarea
         LR    R15,R2              Set routine return code
         $RETURN RC=(R15)          Return to caller

         DROP  R9                  EXIT02W

         LTORG

MSGSKEL  EQU   *                   WTO message skeleton
         DC    X'900F'             Message id
         DC    CL8' '              Jobid from JCTJOBID
         DC    C' '
         DC    CL8' '              Jobname from JCTJNAME
         DC    C' '
         DC    CL8' '              Symbolic jobclass from jobcard
MSGLEN   EQU   *-MSGSKEL           Total length of WTO message


         $MODEND

EXIT02W  DSECT                     EXIT02 workarea
RETCODE  DS    F                   EXIT02 return code
H2TAB    DS    A                   HASP2TAB pointer
JCLASS   DS    CL1                 Actual jobclass
JOBCLASS DS    CL8                 Symbolic jobclass
JCLASSL  DS    H                   Symbolic jobclass length
MCLASS   DS    CL1                 Actual msgclass
MSGCLASS DS    CL8                 Symbolic msgclass
MCLASSL  DS    H                   Symbolic msgclass length
MSGAREA  DS    0C                  WTO message area
MSGIDENT DS    XL2                 Message identifier
MSGJOBID DS    CL8                 Jobid from JCTJOBID
         DS    C
MSGJNAM  DS    CL8                 Jobname from JCTJNAME
         DS    C
MSGSYMJ  DS    CL8                 Symbolic jobclass from jobcard
WRKSIZE  EQU   *-EXIT02W           Length of EXIT02 workarea


         PRINT NOGEN
         CVT DSECT=YES
         HASP2TAB
         IEFJESCT
         IEFJSCVT
         IHAPSA
         JES2UVT
         UCVT
         END
