EXITRDR  TITLE ' H W D C  USER EXIT CODE FOR HASPRDR ROUTINES'    HWDC
***************************************************************** HWDC
*                                                               * HWDC
* MODULE NAME = EXITRDR      H W D C  EXIT 002, 003 AND 004.    * HWDC
*                                                               * HWDC
* DESCRIPTIVE NAME = USER EXIT MODULE FOR JOBCARD SCAN,         * HWDC
*                    AND JCL/JECL SCAN                          * HWDC
* STATUS =  OS/VS2 MVS/SP3  SEE &VERSION (BELOW) FOR JES2 LEVEL.* HWDC
*                                                               * HWDC
* FUNCTION = PROVIDE SUPPORT FOR HWDC MODS THAT HANDLE OUR      * HWDC
*            SPECIAL REQUIREMENTS FOR THE JOBCARD, AND OUR OWN  * HWDC
*            SPECIAL CONTROL CARDS. BOTH ARE TAKE FROM HASPRDR. * HWDC
*            INSERT JCL FOR STARTED TASKS                       * HWDC
*                                                               * HWDC
* NOTES = SEE BELOW                                             * HWDC
*                                                               * HWDC
*    DEPENDENCIES =                                             * HWDC
*                                                               * HWDC
*    RESTRICTIONS =                                             * HWDC
*                                                               * HWDC
*    REGISTER CONVENTIONS = SEE ENTRY POINT DOCUMENTATION.      * HWDC
*                                                               * HWDC
*                                                               * HWDC
*    PATCH LABEL = NONE                                         * HWDC
*                                                               * HWDC
* MODULE TYPE = PROCEDURE ( CSECT TYPE )                        * HWDC
*                                                               * HWDC
*    PROCESSOR = ASSEMBLER F                                    * HWDC
*                                                               * HWDC
*    MODULE SIZE = SEE $DLENGTH MACRO EXPANSION(S) AT END OF    * HWDC
*                  ASSEMBLY.                                    * HWDC
*                                                               * HWDC
*    ATTRIBUTES = READ ONLY, REENTRANT (BY JES2 DEFINITION).    * HWDC
*                                                               * HWDC
* ENTRY POINT = HWDCX002 AND   HWDCX004 AND HWDCX003            * HWDC
*                                                               * HWDC
*    PURPOSE = SEE FUNCTION (ABOVE).                            * HWDC
*                                                               * HWDC
*    LINKAGE = SEE ENTRY POINT DOCUMENTATION.                   * HWDC
*                                                               * HWDC
* INPUT =  SEE ENTRY POINT DOCUMENTATION.                       * HWDC
*                                                               * HWDC
* OUTPUT = MODIFIED JCT JRT AND JQE AS NECESSARY                * HWDC
*          AND IF NECCESSARY THE JOB                            * HWDC
*          IS FLUSHED.                                          * HWDC
*                                                               * HWDC
* EXIT-NORMAL = RETURN TO CALLER USING RETURN MACRO             * HWDC
*                                                               * HWDC
* EXIT-ERROR = NONE                                             * HWDC
*                                                               * HWDC
* EXTERNAL REFERENCES = SEE BELOW                               * HWDC
*                                                               * HWDC
*    ROUTINES = NONE                                            * HWDC
*                                                               * HWDC
*    DATA AREAS = SEE BELOW                                     * HWDC
*                                                               * HWDC
*    CONTROL BLOCKS = SEE $HASPCB MACRO EXPANSION               * HWDC
*                                                               * HWDC
* TABLES = SEE BELOW                                            * HWDC
*                                                               * HWDC
* MACROS =                                                      * HWDC
*                                                               * HWDC
* CHANGE ACTIVITY                                               * HWDC
*                                                               * HWDC
*     RELEASE SP3 = NONE                                        * HWDC
*                                                               * HWDC
*                                                               * HWDC
***************************************************************** HWDC
         TITLE '    H A S P G E N   P A R A M E T E R S    '      HWDC
         COPY  $HASPGBL            COPY HASPGEN PARAMETERS        HWDC
         TITLE 'HASP MODULE INFORMATION TABLE (MIT) '             HWDC
EXITRDR  $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                  X
               $HASPEQU,                                               X
               ASCB,                                                   X
               CVT,                                                    X
               RPL,                                                    X
               $BUFFER,                                                X
               $JCT,                                                   X
               $JQE,                                                   X
               $PCE,                                                   X
               $SCAT,                                                  X
               $XECB,                                                  X
               $PDDB,                                                  X
               $TAB,                                                   X
               $CAT,                                                   X
               $DCT,                                                   X
               $HCT,                                                   X
               $HFAME,                                                 X
               $PADDR,                                                 X
               $IOT,                                                   X
               $KIT,                                                   X
               $LRC,                                                   X
               $MITETBL,                                               X
               $MIT,                                                   X
               $NHD,                                                   X
               $RDRWORK,                                               X
               $QSE,                                                   X
               $RAT,                                                   X
               $RDT,                                                   X
               $SMF,                                                   X
               $SVT,                                                   X
               UCB,                                                    X
               $XIT
         $JRT  DOC=NO
         SPACE 3                                                  HWDC
  TITLE 'HWDC EX002 (JOBCARD) EX003 (ACCT) EX004 (JCL/JECL)'      HWDC
         SPACE 5                                                  HWDC
*                                                                 HWDC
         TITLE 'HWDC EXIT002 FOR JOBCARD SCAN SUPPORT    '        HWDC
***************************************************************** HWDC
*                                                               * HWDC
*           J O B  C A R D  S C A N  R O U T I N E  .  EXIT002. * HWDC
*                                                               * HWDC
*               ALSO KNOWN AS :                                 * HWDC
*                                                               * HWDC
*              HWDC013 - INITIAL CODE FOR HWDC RESOURCE CONTROL * HW013
*                                                               * HWDC
***************************************************************** HWDC
* ENTRY POINT DOCUMENTATION:                                    * HWDC
*                                                               * HWDC
*     REGISTERS AT ENTRY;                                       * HWDC
*                                                               * HWDC
*           R0  =       = +0 - INITIAL JOBCARD.                 * HWDC
*                         +4 - CONTINUATION JOBCARD.            * HWDC
*           R1  =       = ADDRESS OF 3 WORD PARM LIST.          * HWDC
*                         WORD1 - (+0) POINTER TO JOB           * HWDC
*                                      STATEMENT IMAGE BUFFER.  * HWDC
*                         WORD2 - (+4) POINTS TO EXIT FLAG      * HWDC
*                                      BYTE RDWFLAGX IN PCE.    * HWDC
*                         WORD3 - (+8) POINTS TO THE JCTXWRK    * HWDC
*                                      FIELD IN THE JCT.        * HWDC
*           R2  =       = N/A                                   * HWDC
*           R3  =       = N/A                                   * HWDC
*           R4  =       = N/A                                   * HWDC
*           R5  =       = N/A                                   * HWDC
*           R6  = BASE4 = N/A                                   * HWDC
*           R7  =       = N/A                                   * HWDC
*           R8  = BASE3 = N/A                                   * HWDC
*           R9  = JRT   = JRT                                   * HWDC
*           R10 = JCT   = ADDRESS OF JCT                        * HWDC
*           R11 = R11 = ADDRESS OF HCT                          * HWDC
*           R12 = R12 = OUR BASE                                * HWDC
*           R13 = SAVE  = ADDRESS OF PCE                        * HWDC
*           R14 = LINK  = RETURN ADDRESS                        * HWDC
*           R15 =       = ENTRY ADDRESS                         * HWDC
*                                                               * HWDC
***************************************************************** HWDC
         SPACE 3                                                  HWDC
HWDCX002 $ENTRY BASE=R12           PROVIDE PROCESSOR ENTRY POINTHWDC
         $SAVE                     SAVE CALLERS REGISTERS         HWDC
         LR    R12,R15             ESTABLISH BASE                 HWDC
         USING JCT,R10
         LM    R2,R4,0(R1)         GET PARAMETERS
         CLI   JCTJOBID,C'J'       BYPASS CREATE OF JRT         HW00012
         BNE   EX2STC00             IF NOT A BATCH JOB          HW00012
         LTR   R7,R0               IS THIS FIRST TIME FOR JOB
         BNZ   CHKNJEX2             NO - GO CHECK NJE
         USING JCT,R10             ADDRESS THE JCT                HWDC
         USING PCE,R13             ADDRESS THE PCE                HWDC
         MVI   JCTUSER4,C' '       BLANK -                        HW013
         MVC   JCTUSER4+1(15),JCTUSER4 OUT NAMES                  HW013
         XC    JCTUSER4+16(1),JCTUSER4+16 CLEAR FLAG BYTE.        HW013
*                                                               HW00012
*        C R E A T E   T H E   J R T                            HW00012
*                                                               HW00012
         SPACE 1                                                HW00012
         BAL   R6,RGETBUF          GET A BUFFER FOR THE JRT     HW00012
         LR    R9,R1               ESTABLISH ADDRESSABILITY     HW00012
         ST    R9,JCTUSER1         SAVE POINTER                 HW00012
         ST    R9,PCEUSER0         SAVE POINTER                 HW00012
         MVI   JCTUSERF,X'00'
         LA    R14,JRTSTART        GET ADDRESS OF START OF JRT  HW00012
         LH    R15,$BUFSIZE         AND LENGTH OF JRT FIELDS    HW00012
         SLR   R1,R1               SET TO ZERO FOR CLEARING     HW00012
         MVCL  R14,R0               AND CLEAR JRT               HW00012
         MVC   JRTID,RJRTID        SET JRT ID                   HW00012
         LH    R1,$BUFLENG         SET BLOCK                    HW00012
         STH   R1,JRTLENG           LENGTH                      HW00012
CHKNJEX2 EQU   *            ROUTINE TO CHECK FOR USER= FROM NJE
         TM    JCTUSERF,X'01'      ALREADY FOUND USER
         BO    EX2RET00            YES
         L     R6,PCEDCT
         USING DCT,R6
         CLC   DCTRDNOD,=AL2(28)    I 'OWN' BELOW HERE
         BL    EX2RET00
         CLC   DCTRDNOD,=AL2(46)    MY NODE
         BE    EX2RET00
         CLC   DCTRDNOD,=AL2(127)   I 'OWN' ABOVE HERE
         DROP  R6
         BH    EX2RET00
         LA    R8,70
         LTR   R7,R7
         BNZ   CONTEX2
JOBLOOPA EQU   *               FIND ACCOUNT CODE ON JOBCARD
         LA    R2,1(,R2)       POINT TO NEXT POSITION
         CLC   =C' JOB ',0(R2) IS THIS THE WORD JOB
         BE    FNDJOB          YES - GET TO MAIN LOOP
         BCT   R8,JOBLOOPA     NO - CONTINUE SCAN
         B     EX2RET08        BAD CARD
FNDJOB   EQU   *
         LA    R2,4(R2)        POINT TO BLANK
         SH    R8,=H'5'        ADJUST COUNTER
         BZ    EX2RET08        BAD CARD
         B     FNDPARAM
CONTEX2  EQU   *
         LA    R2,2(R2)        POINT TO BLANK
         CLI   0(R2),C'*'      IS THIS A COMMENT
         BE    EX2RET00        YES - GET OUT
         BCTR  R8,0            ADJUST COUNTER
         BCTR  R8,0            ADJUST COUNTER
FNDPARAM LA    R2,1(R2)       POINT TO NEXT POSITION
         CLI   0(R2),C' '     IS IT BLANK
         BNE   FND1STD        NO - FOUND A PARAMETER
         BCT   R8,FNDPARAM    CONTINUE SCAN
         B     EX2RET08       BAD CARD
FND1ST   EQU   *
         CLI   0(R2),X'7D'    IS IT QOUTE
         BE    FNDQUO         YES - GO FIND MATCH
         CLC   =C'USER=',0(R2)
         BE    USRFND
         CLC   =C', ',0(R2)        VALID END TO THIS CARD
         BE    EX2RET00            YES - OK
         CLI   0(R2),C' '          END OF JOB STATEMENT?
         BE    EX2RET08            YES - BAD JOB STATEMENT
FSTLOOP  EQU   *
         LA    R2,1(R2)            POINT TO NEXT POSITION
FND1STD  EQU   *
         BCT   R8,FND1ST           CONTINUE SCAN
         CLI   0(R2),C','          COMMA IN LAST POSITION?
         BE    EX2RET00            YES - OK
         B     EX2RET08            NO - BAD JOBCARD
FNDQUO   EQU   *
         BCT   R8,QUOLOOP         ADJUST COUNTER
         B     EX2RET00           CHECK FOR CONTINUATION
QUOLOOP  EQU   *
         LA    R2,1(R2)
QUOLOOP2 EQU   *
         CLI   0(R2),X'7D'        MATCHING QUOTE
         BE    FSTLOOP
         BCT   R8,QUOLOOP
         B     EX2RET00
USRFND   EQU   *
         CLI   5(R2),C'A'          IS THERE A USER
         BL    EX2RET08            NO - BAD JOBCARD
         OI    JCTUSERF,X'01'      TURN ON SWITCH
         B     EX2RET00
EX2STC00 EQU   *
         CLI   JCTJOBID,C'S'       IS IT A STC
         BNE   EX2RET00            NO - MUST BE A TSU
STBLOAD  EQU   *
         L     R15,$USER1          HAS THE TABLE ADDRESS BEEN LOADED?
         LTR   R15,R15
         BP    STBLOK              YES - OK
         BZ    STDEFLT             EMPTY - USE THE DEFAULT?
         LA    R14,10
LOOPSTBL EQU   *
        $WAIT ABIT
         BCT   R14,LOOPSTBL
         L     R15,$USER1          HAS THE TABLE ADDRESS BEEN LOADED?
         LTR   R15,R15
         BP    STBLOK              YES - OK
         BZ    STDEFLT             EMPTY - USE THE DEFAULT?
        $WTO   TBLMSG,L'TBLMSG,JOB=NO,CLASS=$ACTION,ROUTE=$MAIN
         B     STBLOAD
STDEFLT  EQU   *
         LA    R14,EX2STBL         NO - GET TABLE POINTER
         ST    R14,$USER1          SAVE IT FOR LATER
STBLOK   EQU   *
         LA    R14,2(R2)           POINT TO JOB IMAGE
STCJBLP1 EQU   *
         CLC   0(5,R14),=C' JOB '  IS IT THE WORD?
         BE    ENDSTCJB            YES - GO MOVE OUR STUFF
         LA    R14,1(,R14)         NO - POINT TO THE NEXT POSITION
         B     STCJBLP1            AND CHECK IT
ENDSTCJB EQU   *
         LR    R15,R14             GET POINTER
         SR    R15,R2              SUBTRACT STARTING POINT
SCTJBLP2 EQU   *
         CH    R15,=H'10'          ARE THERE 10 POSITIONS?
         BNL   ESTCLP2             YES - MOVE MY STUFF
         LA    R15,1(,R15)         NO - ADD 1
         MVI   0(R14),C' '         BLANK  OUT THE POSITION
         LA    R14,1(,R14)         POINT TO NEXT POSITION
         B     SCTJBLP2
ESTCLP2  EQU   *
         MVC   0(L'STCDEF,R14),STCDEF
         LR    R14,R2              POINT BACK TO BEGINNING
         L     R15,$USER1
STCLP1   EQU   *
         CLI   0(R15),X'00'        IS IT END
         BE    STCLP3I
         CLI   0(R15),X'FF'        TABLE EXTENSION?
         BE    EX2RET00            LOAD NEXT POINTER
         CLC   2(8,R14),2(R15)     DOES JOB MATCH TABLE?
         BE    EX2STCFN            YES - GO MODIFY CARD
         CLC   76(4,R15),=C'MORE'  ARE THERE 2 CARDS?
         BNE   STCLP2              NO - JUST JOB CARD
         LA    R15,STCENT(,R15)    EXTRA BUMP FOR OUTPUT
STCLP2   EQU   *
         LA    R15,STCENT(,R15)    POINT TO NEXT ENTRY
         B     STCLP1
STCLP3I  EQU   *
         SLR   R14,R14             CLEAR REGISTER
         LA    R15,EX2SGEN         POINT TO GENERIC TABLE
STCLPG   EQU   *
         CLI   1(R15),X'00'        IS IT END
         BE    ESTCLPG             YES - GO CHECK OLD TABLE
         IC    R14,0(R15)          GET LENGTH
         EX    R14,EX2GCLC         EXECUTE CLC
         BE    EX2MVGEN            FOUND ONE
         LA    R15,160(,R15)       POINT TO NEXT ENTRY
         B     STCLPG              AND CHECK
EX2GCLC  CLC   1(,R15),1(R2)
EX2MVGEN EQU   *
         MVC   10(70,R2),10(R15)   MOVE GENERIC JOB CARD TO JES2
         LA    R14,2(R2,R14)       POINT TO SUFFIX
         MVC   21(3,R2),0(R14)     MOVE ACCOUNT SUFFIX
         B     EX2GEND             GO CHECK FOR SECOND CARD
ESTCLPG  EQU   *
         LA    R14,STCTABLE       POINT TO STARTED TASK TABLE
         SLR   R15,R15             ZERO WORK REG
STCLP3   NULL
         CLI   0(R14),X'FF'        END OF TABLE?
         BE    EX2RET00            YES - BRANCH
         IC    R15,0(R14)          GET LENGTH
         EX    R15,STCLC3          EXECUTE COMPARE
         BNE   STCINCR3            NO - GO CHECK NEXT
         MVC   18(3,R2),9(R14)     OVERLAY DEFAULT ACCOUNT CODE
         MVC   28(3,R2),9(R14)     OVERLAY DEFAULT ROOM NUMBER
         B     EX2RET00
STCINCR3 NULL
         LA    R14,1+8+3(R14)
         B     STCLP3
STCLC3   CLC   1(0,R14),2(R2)      ***EXECUTED***
EX2STCFN EQU   *
         MVC   0(80,R2),0(R15)
EX2GEND  EQU   *
         CLC   76(4,R15),=C'MORE'  IS THERE AN OUTPUT
         BNE   EX2RET00            NO - ALL DONE
         OI    RDWFLAGX,RDWXXSNC
         MVC   JCTXWRK(80),STCENT(R15)
         B     EX2RET00
EX2RET08 MVC   JCTXWRK(L'MYMESS),MYMESS MOVE IN MY MESSAGE
         OI    RDWFLAGX,RDWXXSEM INDICATE MY MESSAGE
EX2RE08A LA    R15,8               SET FLUSH RETURN CODE          HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
EX2RET00 LA    R15,0               SET ZERO RETURN CODE.        HW00012
         $RETURN RC=(R15)          RETURN TO CALLER             HW00012
         SPACE 3                                                HW00012
RGETBUF  CLI   PCEBUFCT,20         IS BUF COUNT UP TO 20...     HW00012
         BNH   RGETOK              BR IF NO TO GET BUFFER       HW00012
        $WAIT  BUF                 WAIT IF 20 BUFFS THIS D/T    HW00012
         B     RGETBUF             TRY AGAIN                    HW00012
RGETOK   EQU   *                                                HW00012
        $GETBUF WAIT=YES           GET OUTPUT BUFFER            HW00012
         BR    R6                  RETURN                       HW00012
         SPACE 3                                                  HWDC
         TITLE 'HWDC EXIT 002 CONSTANTS AND DSECTS      '         HWDC
***************************************************************** HWDC
*    CONSTANTS, DSECTS AND EQUATES FOR JOBCARD SUPPORT.         * HWDC
***************************************************************** HWDC
         SPACE 1                                                  HWDC
RJRTID   DC    CL4'JRT'
MYMESS   DC    C'USERID AND PASSWORD REQUIRED'
STCDEF   DC    C' JOB (HW5130000P,C513,,100),MSGLEVEL=1 '
STCTABLE DC    X'03',CL8'ASAP',C'610'
         DC    X'04',CL8'OLTEP',C'620'
         DC    X'05',CL8'SASWTR',C'321'
         DC    X'02',CL8'VPS',C'532'
         DC    X'06',CL8'EPILOGC',C'511'
         DC    X'04',CL8'MPWTR',C'532'
         DC    X'03',CL8'OCCI',C'532'
         DC    X'03',CL8'OMST',C'532'
         DC    X'04',CL8'HWHSE',C'610'
         DC    X'02',CL8'VCI',C'422'
         DC    X'03',CL8'DCMS',C'422'
         DC    X'03',CL8'EMC2',C'330'
         DC    X'03',CL8'HWCI',C'532'
         DC    X'02',CL8'DB2',C'512'
         DC    X'02',CL8'DBS',C'512'
         DC    X'02',CL8'DHD',C'512'
         DC    X'02',CL8'DHW',C'512'
         DC    X'03',CL8'ZSAM',C'512'
         DC    X'02',CL8'NET',C'531'
         DC    X'06',CL8'WITNESS',C'532'
         DC    X'02',CL8'CDC',C'610'
         DC    X'02',CL8'STC',C'630'
         DC    X'02',CL8'STK',C'630'
         DC    X'02',CL8'IBM',C'620'
         DC    X'02',CL8'AMD',C'640'
         DC    X'02',CL8'AMD',C'640'
         DC    X'FF',CL8'    ',C'600'
         DC    X'FF',CL8'    ',C'600'
         DC    X'FF',CL8'    ',C'600'
         DC    X'FF',CL8'    ',C'600'
         LTORG                                                    HWDC
TBLMSG  $MSG   999,'THE STARTED TASK JOB CARD TABLE MAY BE BROKEN'
EX2SGEN  EQU   *
         DC    X'03'
         DC    CL39'/ADA???   JOB (HWADATRN0T,C512,,900),MS'
         DC    CL36'GCLASS=X,NOTIFY=HWADAB     '
         DC    CL04'MORE'
         DC    CL40'//SAR   OUTPUT DEFAULT=Y,JESDS=ALL,CLASS'
         DC    CL40'=A,DEST=DBSAR                           '
         DC    X'05'
         DC    CL39'/HWADA??? JOB (HWADATRN0T,C512,,900),MS'
         DC    CL36'GCLASS=0,NOTIFY=HWADAB      '
         DC    CL04'MORE'
         DC    CL40'//SAR   OUTPUT DEFAULT=Y,JESDS=ALL,CLASS'
         DC    CL40'=A,DEST=DBSAR                           '
         DC    X'0000'
STCENT   EQU   80
EX2STBL  EQU   *
         DC    CL40'//SARHW    JOB (HW5130000P,C513,,100) '
         DC    CL40' '
         DC    CL40'//SARHW1   JOB (HW5130000P,C513,,100) '
         DC    CL40' '
         DC    CL40'//SARHW2   JOB (HW5120000P,C512,,100) '
         DC    CL40' '
         DC    CL40'//SARCS    JOB (CSSAR0000P,TSAR,,100) '
         DC    CL40' '
         DC    CL40'//SAREM    JOB (EMPSG7000P,EZDX,,500) '
         DC    CL40' '
*                            1         2         3         4         5
*                   12345678901234567890123456789012345678901234567890
         DC    CL40' '
         DC    CL40' '
         COPY  STCTBL
         DC    X'0000'      END OF TABLE
         SPACE 1                                                  HWDC
         TITLE 'HWDC EXIT003 ACCOUNT FIELD SCAN FOR ROOM#'        HWDC
***************************************************************** HWDC
* ENTRY POINT = HWDCX003                                        * HWDC
*                                                               * HWDC
*    PURPOSE = SEE FUNCTION (ABOVE).                            * HWDC
*                                                               * HWDC
*    LINKAGE = SEE ENTRY POINT DOCUMENTATION.                   * HWDC
*                                                               * HWDC
* INPUT =  SEE ENTRY POINT DOCUMENTATION.                       * HWDC
*                                                               * HWDC
* OUTPUT = MODIFIED JCT JRT AND JQE AS NECESSARY                * HWDC
*          AND IF NECCESSARY THE JOB                            * HWDC
*          IS FLUSHED.                                          * HWDC
*                                                               * HWDC
* EXIT-NORMAL = RETURN TO CALLER USING RETURN MACRO             * HWDC
*                                                               * HWDC
* EXIT-ERROR = NONE                                             * HWDC
*                                                               * HWDC
* EXTERNAL REFERENCES = SEE BELOW                               * HWDC
*                                                               * HWDC
*    ROUTINES = NONE                                            * HWDC
*                                                               * HWDC
*    DATA AREAS = SEE BELOW                                     * HWDC
*                                                               * HWDC
*    CONTROL BLOCKS = SEE $HASPCB MACRO EXPANSION               * HWDC
*                                                               * HWDC
* TABLES = SEE BELOW                                            * HWDC
*                                                               * HWDC
* MACROS =                                                      * HWDC
*                                                               * HWDC
* CHANGE ACTIVITY                                               * HWDC
*                                                               * HWDC
*     RELEASE SP3 = NONE                                        * HWDC
*                                                               * HWDC
*                                                               * HWDC
***************************************************************** HWDC
***************************************************************** HWDC
*                                                               * HWDC
*  A C C O U N T FIELD S C A N R O U T I N E . E X I T 0 0 3 .  * HWDC
*                                                               * HWDC
*                                                               * HWDC
***************************************************************** HWDC
* ENTRY POINT DOCUMENTATION:                                    * HWDC
*                                                               * HWDC
*     REGISTERS AT ENTRY;                                       * HWDC
*                                                               * HWDC
*           R0  =       = LENGTH OF ACCOUNT FIELD               * HWDC
*           R1  =       = ADDRESS OF 3 WORD PARM LIST.          * HWDC
*                         WORD1 - (+0) POINTER TO ACCOUNTING    * HWDC
*                                      FIELD(JCTWORK IN THE JCT.* HWDC
*                         WORD2 - (+4) POINTS TO EXIT FLAG      * HWDC
*                                      BYTE RDWFLAGX IN PCE.    * HWDC
*                         WORD3 - (+8) POINTS TO THE JCTXWRK    * HWDC
*                                      FIELD IN THE JCT.        * HWDC
*           R2  =       = ADDRESS OF TYPE 6 SMF RECORD.         * HWDC
*           R3  =       = N/A                                   * HWDC
*           R4  =       = N/A                                   * HWDC
*           R5  =       = N/A                                   * HWDC
*           R6  = BASE4 = N/A                                   * HWDC
*           R7  =       = N/A                                   * HWDC
*           R8  = BASE3 = N/A                                   * HWDC
*           R9  = JRT   = JRT                                   * HWDC
*           R10 = JCT   = ADDRESS OF JCT                        * HWDC
*           R11 = R11 = ADDRESS OF HCT                          * HWDC
*           R12 = R12 = OUR BASE                                * HWDC
*           R13 = SAVE  = ADDRESS OF PCE                        * HWDC
*           R14 = LINK  = RETURN ADDRESS                        * HWDC
*           R15 =       = ENTRY ADDRESS                         * HWDC
*                                                               * HWDC
***************************************************************** HWDC
         SPACE 3                                                  HWDC
HWDCX003 $ENTRY BASE=R12             PROVIDE PROCESSOR ENTRY POINTHWDC
         $SAVE                     SAVE CALLERS REGISTERS         HWDC
         LR    R12,R15             ESTABLISH BASE                 HWDC
         USING JCT,R10             ADDRESS THE JCT                HWDC
         USING PCE,R13             ADDRESS THE PCE                HWDC
         MVC   JCTUSER1,PCEUSER0
         TM    JCTJOBFL,JCTBATCH
         BO    CHKNJE00
         TM    JCTJOBFL,JCTSTCJB
         BO    C513
DBUMP    LA    R3,JCTWORK        BUILD PHONY ROOM IN CASE
BUMPLOOP CLI   0(R3),X'7D'
         BE    BUMP
         CLI   0(R3),C'('
         BNE   NOBUMP
BUMP     LA    R3,1(R3)          BUMP 1 POSITION
         B     BUMPLOOP
NOBUMP   EQU   *
         CLI   0(R3),C' '        IS THERE AN ACCOUNT CODE?
         BNH   EX3RET00          NO - ALL DONE
         MVC   JCTROOMN(1),0(R3) PICK UP 1ST POS. OF ACCOUNT
         MVC   JCTROOMN+1(3),=C'$$$'
         MVC   JCTUSER9(8),2(R3) MOVE LAST 8 POSITIONS OF ACCOUNT
         MVC   JCTACCTN,0(R3)       SAVE ACCOUNT CODE
         MVC   JCTNACCT(6),0(R3)       SAVE ACCOUNT CODE
         MVC   JCTNACCT+2(3),=C'950'
         LA    R1,IDTABLE-3      ID TABLE MINUS 1 ENTRY LENGTH
IDLOOP   NULL
         LA    R1,3(R1)           BUMP TO NEXT ENTRY
         CLI   0(R1),X'FF'        END OF TABLE?
         BE    ENDID               YES
         CLC   0(2,R3),0(R1)      CHECK NEXT
         BNE   IDLOOP             NOT THE ONE
         MVC   JCTROOMN(1),2(R1)  MOVE IN THE TMS CODE
ENDID    CLC   0(2,R3),=C'HW'
         BNE   EX3RET00
         MVI   JCTROOMN,C'C'
         MVC   JCTROOMN+1(3),2(R3)
         B     EX3RET00
C513     EQU   *                    SAVE ACCOUNT INFO FOR STCS
         LA    R3,JCTWORK           POINT TO ACCOUNT FIELD
         MVC   JCTACCTN,1(R3)       SAVE ACCOUNT CODE
         MVC   JCTNACCT(6),1(R3)       SAVE ACCOUNT CODE
         MVC   JCTNACCT+2(3),=C'950'
         MVC   JCTUSER9(8),3(R3) MOVE LAST 8 POSITIONS OF ACCOUNT
         B     EX3RET00
IDTABLE  NULL
         DC    C'CCI'
         DC    C'CHF'
         DC    C'CST'
         DC    C'EOO'
         DC    C'MMI'
         DC    C'OAA'
         DC    C'RBP'
         DC    C'AGG'
         DC    C'BCK'
         DC    C'AUU'
         DC    C'GHN'
         DC    C'TWJ'
         DC    C'BZL'
         DC    X'FFFFFF'
         DC    X'FFFFFF'
         DC    X'FFFFFF'
         DC    X'FFFFFF'
         DC    X'FFFFFF'
CHKNJE00 L     R2,PCEDCT
         USING DCT,R2
         CLC   DCTRDNOD,=AL2(72)
         DROP  R2
         BNE   DBUMP
CHKNJE01 LA    R3,JCTWORK
LOOPBUMP CLI   0(R3),C'('
         BNE   NOBUMP2
         LA    R3,1(R3)          BUMP 1 POSITION
         B     LOOPBUMP
NOBUMP2  CLC   11(4,R3),=C'C513' IS THIS FOR ME
         BE    EX3RET00           YES - OK
CHKNJE02 EQU   *
         CLC   0(2,R3),=C'EM'
         BNE   EX3RE12A
EX3RET00 LA    R15,0               SET ZERO RETURN CODE.          HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
EX3RE12A LA    R15,12              SET FLUSH RETURN CODE          HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
         SPACE 3                                                  HWDC
         TITLE 'HWDC EXIT 003 CONSTANTS AND DSECTS      '         HWDC
***************************************************************** HWDC
*    CONSTANTS, DSECTS AND EQUATES FOR JOBCARD SUPPORT.         * HWDC
***************************************************************** HWDC
         SPACE 1                                                  HWDC
         LTORG                                                    HWDC
PATCH03  DC    C'PATCH '
         DC    250C' '
***************************************************************** HWDC
         SPACE 1                                                  HWDC
***************************************************************** HWDC
         TITLE 'HWDC EXIT004 FOR JCL/JECL SCAN SUPPORT.  '        HWDC
***************************************************************** HWDC
*                                                               * HWDC
*   J C L  /  J E C L  S C A N  S U P P O R T .     EXIT004.    * HWDC
*                                                               * HWDC
*         ALSO KNOWN AS :                                       * HWDC
*                                                               * HWDC
*        HWDC013 - HWDC RESOURCE ROUTING (CONTROL CARD SUPPORT) * HW013
*                                                               * HWDC
***************************************************************** HWDC
* ENTRY POINT DOCUMENTATION:                                    * HWDC
*                                                               * HWDC
*  REGISTERS AT ENTRY;                                          * HWDC
*                                                               * HWDC
*             R0  =       = +0 - INDICATES JES2 CONTROL CARD.   * HWDC
*                           +4 - INDICATES JCL CONTROL CARD.    * HWDC
*             R1  =       = ADDRESS OF 3 WORD PARM LIST.        * HWDC
*                           WORD1 - (+0) POINTS TO THE CONTROL  * HWDC
*                                        STATEMENT IMAGE BUFFER.* HWDC
*                           WORD2 - (+4) POINTS TO EXIT FLAG    * HWDC
*                                        BYTE RDWFLAGX IN PCE.  * HWDC
*                           WORD3 - (+8) POINTS TO THE JCTXWRK  * HWDC
*                                        FIELD IN THE JCT.      * HWDC
*             R2  =       = N/A                                 * HWDC
*             R3  =       = N/A                                 * HWDC
*             R4  =       = N/A                                 * HWDC
*             R5  =       = N/A                                 * HWDC
*             R6  = BASE4 = N/A                                 * HWDC
*             R7  =       = N/A                                 * HWDC
*             R8  = BASE3 = N/A                                 * HWDC
*             R9  = JRT   = JRT                                 * HWDC
*             R10 = JCT   = ADDRESS OF JCT                      * HWDC
*             R11 = R11 = ADDRESS OF HCT                        * HWDC
*             R12 = R12 = OUR BASE                              * HWDC
*             R13 = SAVE  = ADDRESS OF PCE                      * HWDC
*             R14 = LINK  = RETURN ADDRESS                      * HWDC
*             R15 =       = ENTRY ADDRESS                       * HWDC
*                                                               * HWDC
***************************************************************** HWDC
         SPACE 3                                                  HWDC
HWDCX004 $ENTRY BASE=R12           PROVIDE PROCESSOR ENTRY POINT  HWDC
         $SAVE                     SAVE CALLERS REGISTERS         HWDC
         LR    R12,R15             ESTABLISH BASE                 HWDC
         USING JCT,R10             ADDRESS THE JCT                HWDC
         USING JRTDSECT,R9         ADDRESS THE JRT                HWDC
         USING PCE,R13             ADDRESS THE PCE                HWDC
         USING NJH,R7              ADDRESS THE NET. JOB HDR       HWDC
         LM    R2,R4,0(R1)         POINT TO PARAMETERS.           HWDC
         TM    RDWFLAGX,RDWXCONT   IS THIS A CONTINUATION CARD    HWDC
         BO    EX4RET00            YES, RETURN TO JES2.           HWDC
         LTR   R4,R4               WE ARE IN A JOB.               HWDC
         BZ    EX4RET00            BRANCH IF NOT.                 HWDC
         LTR   R10,R10             ARE WE IN A JOB (DOUBLE CHECK) HWDC
         BZ    EX4RET00            BRANCH IF NO.                  HWDC
         CLC   =C'¢¢¢¢¢¢¢¢',72(R2) IS THIS MINE?                  HWDC
         BE    EX4STC00            YES - GO PROCESS               HWDC
         TM    JCTJOBFL,JCTTSUJB+JCTSTCJB BYPASS CREATE OF JRT    HWDC
         BO    EX4RET00            IF A TSU JOB                   HWDC
EX4JOB00 EQU   *
         LTR   R0,R0               IS THIS A JES2 JECL STATEMENT? HW013
         BNZ   EX4RET00            BR IF YES OTHERWISE ASSUME JCL HW013
         L     R9,JCTUSER1        GET JRT POINTER                 HWDC
         LTR   R9,R9               ?                              HWDC
         BZ    RNOJRT              ERROR IF NO JRT                HWDC
         SPACE 1                                                  HW013
***************************************************************** HW013
*                                                               * HW013
*    JES2 'WHILE' 'AFTER'         CONTROL STATEMENT             * HW013
*                                           PROCESSING ROUTINE. * HW013
*    THIS ROUTINE ALSO LOOKS FOR JOBPARM FOR HWDC PARAMETERS    * HW013
*                                                               * HW013
***************************************************************** HW013
         SPACE 3                                                  HW013
HWX4JECL DS    0H                                                 HW013
         LR    R8,R2               SAVE POINTER TO CARD          *HW013
         CLC   2(5,R2),RAFTRCRD    IS IT HWDC AFTER CARD?         HW013
         BE    RAFTCARD            YES GO PROCESS IT.             HW013
         CLC   2(5,R2),RWHILCRD    IS IT HWDC WHILE CARD?         HW013
         BE    RWHLCARD            YES GO PROCESS IT.             HW013
         CLC   2(5,R2),RJOBPCRD    IS IT JES2 JOBPARM CARD?       HW013
         BE    RJPMCARD            YES GO PROCESS IT.             HW013
         CLC   2(5,R2),RHWDCCRD    IS IT HWDC JOBPARM CARD?       HW013
         BE    RHPMCARD            YES GO PROCESS IT.             HW013
         B     EX4RET00            IF NONE OF THOSE RETURN TO JES HW013
RWHLCARD DS    0H                                                 HW013
         CLC   FILLER+2(10),8(R2)  IS IT PRODUCTION?              HW013
         BE    RSETPROD            YES - SET FLAG                 HW013
         OI    JCTUSER4+16,QUEWHL  SET TO WHILE CARD              HW013
         LA    R2,8(,R2)           START OF SCAN                  HW013
         LA    R5,JCTUSER4         DEPENDENT JOB NAME AREA        HW013
         TM    JCTUSER4+16,QUEAFT  /*AFTER SPECIFIED              HW013
         BNO   RMVENAM             NO - USE FIRST DEPENDENT AREA  HW013
         LA    R5,8(0,R5)          USE SECOND DEPENDENT AREA      HW013
RMVENAM  LA    R6,50               MAX POSITIONS TO SEARCH        HW013
RFINDCAR CLI   0(R2),C'A'          FIND START OF JOBNAME          HW013
         BL    RFINDCON            NOT IN GOOD RANGE              HW013
         CLI   0(R2),C'Z'          TRY OTHER SIDE                 HW013
         BNH   RPUTNAM             THIS IS GOOD                   HW013
RFINDCON LA    R2,1(,R2)           POINT TO NEXT POSITION         HW013
         BCT   R6,RFINDCAR         LOOP TILL END                  HW013
         NI    JCTUSER4+16,255-QUEWHL-QUEAFT SET OFF FLAGS        HW013
         B     RBADWHIL            ILLEGAL WHILE CARD             HW013
RPUTNAM  DS    0H                                                 HW013
         MVC   0(8,R5),0(R2)       PUT NAME IN                    HW013
         B     EX4RET08            GO TERMINATE NORMALLY          HW013
RSETPROD OI    JRTFLAG,JRTBYP    SET SWITCH                       HW013
         MVC   0(L'FILLER,R2),FILLER WIPE OUT WHILE               HW013
         B     EX4RET08          TELL JES2 TO MAKE IT A COMMENT   HW012
RBADWHIL EQU   *                                                  HW013
         $MID  935                 SHOW MESSAGE ID.               HW013
         MVC   JCTXWRK(80),RBADWHLM SET IN ERROR MESSAGE          HW013
         OI    RDWFLAGX,RDWXXSEM   SHOW MESSAGE SUPPLIED          HW013
         B     EX4RET12            GO TERMINATE JOB               HW013
         SPACE 3                                                  HW013
RNOJRT   EQU   *                                                  HW013
         $MID  940                 SHOW MESSAGE ID.               HW013
         MVC   JCTXWRK(80),RNOJRTM SET IN ERROR MESSAGE           HW013
         OI    RDWFLAGX,RDWXXSEM   SHOW MESSAGE SUPPLIED          HW013
         B     EX4RET00            GO TERMINATE JOB               HW013
ROUTORDR EQU   *                                                  HW013
         $MID  939                 SHOW MESSAGE ID.               HW013
         MVC   JCTXWRK(80),ROUTORDM SET IN ERROR MESSAGE          HW013
         OI    RDWFLAGX,RDWXXSEM   SHOW MESSAGE SUPPLIED          HW013
         B     EX4RET12            GO TERMINATE JOB               HW013
         SPACE 3                                                  HW013
RAFTCARD DS    0H                                                 HW013
         TM    JCTUSER4+16,QUEWHL  WHILE SPEC                     HW013
         BNZ   ROUTORDR            YES - CARDS ARE OUT OF ORDER   HW013
         OI    JCTUSER4+16,QUEAFT  SET TO AFTER CARD              HW013
         LA    R2,8(,R2)           SET START OF SCAN              HW013
         LA    R5,JCTUSER4         DEPENDENT JOB NAME AREA        HW013
RMVENAM1 LA    R6,50               MAX POSITIONS TO SEARCH        HW013
RFINDCA1 CLI   0(R2),C'A'          FIND START OF JOBNAME          HW013
         BL    RFINDCO1            NOT IN GOOD RANGE              HW013
         CLI   0(R2),C'Z'          TRY OTHER SIDE                 HW013
         BNH   RPUTNAM             THIS IS GOOD                   HW013
RFINDCO1 LA    R2,1(,R2)           POINT TO NEXT POSITION         HW013
         BCT   R6,RFINDCA1         LOOP TILL END                  HW013
         NI    JCTUSER4+16,255-QUEWHL-QUEAFT SET OFF FLAGS        HW013
         $MID  936                 SHOW MESSAGE ID.               HW013
         MVC   JCTXWRK(80),RBADAFTM SET IN ERROR MESSAGE          HW013
         OI    RDWFLAGX,RDWXXSEM   SHOW MESSAGE SUPPLIED          HW013
         B     EX4RET12            GO TERMINATE JOB               HW013
RJPMCARD EQU   *                                                  HW012
         MVC   JCTXWRK+1(79),0(R2) MOVE COPY TO WORK AREA         HW012
         MVI   JCTXWRK,C'/'      MAKE IT A COMMENT                HW012
         LA    R2,9(R2)         POINT TO FIRST AVAILABLE POSITION HW012
         LA    R6,61            MAX POSITIONS FOR SEARCH          HW012
RFNDPARM CLI   0(R2),C'A'        FIND FIRST PARAMETER             HW012
         BL    RFINDCO2          NOT IN GOOD RANGE                HW012
         CLI   0(R2),C'Z'        TRY OTHER SIDE                   HW012
         BNH   RPARMFN1          GOT A HIT                        HW012
RFINDCO2 LA    R2,1(,R2)         INCREMENT POINTER                HW012
         BCT   R6,RFNDPARM       LOOP TILL END                    HW012
         B     EX4RET00          BAD JOBPARM? LET JES2 HANDLE IT  HW012
RPARMFN1 LR    R0,R2             SAVE START OF PARMS              HW012
RPARMFN2 LA    R14,MYTABLE1      POINT AT TABLE OF PARMS          HW012
RGETNPRM SR    R15,R15           CLEAR R15                        HW012
         IC    R15,0(R14)        GET LENGTH OF ENTRY              HW012
         EX    R15,CLC           COMPARE TO CURRENT               HW012
         BE    FNDMINE1          BE TO PROCESS                    HW012
         LA    R14,10(R14)       POINT TO NEXT ENTRY              HW012
         CLI   0(R14),X'FF'      END OF TABLE                     HW012
         BNE   RGETNPRM          NO - GO CHECK NEXT PARM          HW012
         LA    R14,MYTABLE1+7    POINT TO 1 POS TABLE             HW012
         LA    R15,0             SET EXECUTE REG TO 1             HW012
RGET1PRM EX    R15,CLC           COMPARE TO CURRENT               HW012
         BNE   RINC10            NOT THIS ONE TRY NEXT            HW012
         CLI   1(R2),C' '        BLANK AFTER CHARACTER            HW012
         BE    FNDMINE2          BE TO PROCESS                    HW012
         CLI   1(R2),C','        COMMA AFTER CHARACTER            HW012
         BE    FNDMINE2          BE TO PROCESS                    HW012
         BNE   RFNDNPRM          CANT BE MINE!                    HW012
RINC10   LA    R14,10(R14)       POINT TO NEXT ENTRY              HW012
         CLI   0(R14),X'FF'      END OF TABLE                     HW012
         BNE   RGET1PRM          NO - GO CHECK NEXT PARM          HW012
RFNDNPRM CLI   0(R2),C' '        END OF CARD?                     HW012
         BE    RCHEND1           YES                              HW012
         CLI   0(R2),C','        END OF PARAMETER?                HW012
         BNE   RPARMFN3          GOT A HIT                        HW012
         LA    R2,1(R2)          POINT TO NEXT POSITION           HW012
         BCT   R6,RPARMFN2       GO CHECK NEXT PARAMETER          HW012
         B     RCHEND1           CARD ENDS IN A COMMA?            HW012
RPARMFN3 LA    R2,1(,R2)         INCREMENT POINTER                HW012
         BCT   R6,RFNDNPRM       LOOP TILL END                    HW012
RCHEND1  CR    R2,R0             STILL AT START(ALL PARMS MINE)?  HW012
         BNE   RCHJES2           NO CHECK IF ANY                  HW012
         L     R2,0(R1)          POINT TO CARD                    HW012
         MVC   0(79,R2),JCTXWRK+1 PUT ORIGINAL COPY BACK          HW012
         B     EX4RET08          TELL JES2 TO MAKE IT A COMMENT   HW012
RCHJES2  TM    JRTFLAG,JRTALL    WERE ANY MINE?                   HW012
         BZ    EX4RET00          NO - TELL JES2 TO PROCESS        HW012
         TM    JRTFLAG,JRTBYP+JRTBMPS PRODUCTION?                 HW012
         BZ    RMIXED            NO                               HW012
         BO    RRESTBMP          YES                              HW012
         TM    JRTFLAG,JRTALL-JRTBYP                              HW012
         BZ    EX4RET00          TELL JES2 TO PROCESS             HW012
RRESTBMP NI    JRTFLAG,X'FF'-JRTBMPS TURN OFF FLAG                HW012
RMIXED   OI    RDWFLAGX,RDWXXSNC ELSE GIVE JES2 NEXT RECORD       HW012
         BCTR  R2,0              SUBTRACT 1
         CLI   0(R2),C','        COMMA AT END
         BNE   EX4RET00          NO - OK
         MVI   0(R2),C' '        YES - BLANK IT OUT
         B     RMIXED             AND CHECK NEXT POSITION BACK    HW012
CLC      CLC   0(0,R2),1(R14)    EXECUTED INSTRUCTION             HW012
FNDMINE1 LA    R5,1(R15,R2)      POINT AFTER PARAMETER            HW012
         LR    R7,R6             GET LENGTH LEFT                  HW012
         SR    R7,R15            SUBTRACT PARAMETER LENGTH        HW012
         EX    R7,MVC            SHIFT OUT MY PARAMETER           HW012
         CLC   =C'NIGHT',1(R14)  IS THIS NIGHT PARM
         BNE   RNNIGHT1           NO - OK
         OI    JCTUSER4+16,QUEUSERH  YES - SET HOLD BIT
RNNIGHT1 EQU   *
         LA    R5,9(R14)         POINT TO BIT MASK                HW012
         B     RORFLAG                                            HW012
FNDMINE2 LA    R5,1(R15,R2)      POINT AFTER PARAMETER            HW012
         LR    R7,R6             GET LENGTH LEFT                  HW012
         SR    R7,R15            SUBTRACT PARAMETER LENGTH        HW012
         EX    R7,MVC            SHIFT OUT MY PARAMETER           HW012
         CLI   1(R14),C'W'       IS THIS NIGHT PARM
         BNE   RNNIGHT2           NO - OK
         OI    JCTUSER4+16,QUEUSERH  YES - SET HOLD BIT
RNNIGHT2 EQU   *
         LA    R5,2(R14)         POINT TO BIT MASK                HW012
RORFLAG  OC    JRTFLAG,0(R5)     TURN ON BIT(S)                   HW012
         CLI   0(R2),C','        IS THERE A COMMA NEXT            HW012
         BNE   RCHEND1           NO  END OF CARD                  HW012
         LA    R5,1(,R2)         INCREMENT OVER ,                 HW012
         EX    R7,MVC            SHIFT OUT ,                      HW012
         B     RPARMFN2          GO LOOK FOR NEXT                 HW012
RHPMCARD EQU   *               HWDCPARM CARD PROCESSING           HW012
         LA    R2,10(R2)        POINT TO FIRST AVAILABLE POSITION HW012
         LA    R6,61            MAX POSITIONS FOR SEARCH          HW012
HFNDPARM CLI   0(R2),C'A'        FIND FIRST PARAMETER             HW012
         BL    HFINDCO2          NOT IN GOOD RANGE                HW012
         CLI   0(R2),C'Z'        TRY OTHER SIDE                   HW012
         BNH   HPARMFN1          GOT A HIT                        HW012
HFINDCO2 LA    R2,1(,R2)         INCREMENT POINTER                HW012
         BCT   R6,HFNDPARM       LOOP TILL END                    HW012
         B     EX4RET12          BAD HWDCPARM? KILL IT            HW012
HPARMFN1 LR    R0,R2             SAVE START OF PARMS              HW012
HPARMFN2 LA    R14,MYTABLE1      POINT AT TABLE OF PARMS          HW012
HGETNPRM SR    R15,R15           CLEAR R15                        HW012
         IC    R15,0(R14)        GET LENGTH OF ENTRY              HW012
         EX    R15,CLC           COMPARE TO CURRENT               HW012
         BE    FNDHWDC1          BE TO PROCESS                    HW012
         LA    R14,10(R14)       POINT TO NEXT ENTRY              HW012
         CLI   0(R14),X'FF'      END OF TABLE                     HW012
         BNE   HGETNPRM          NO - GO CHECK NEXT PARM          HW012
         LA    R14,MYTABLE1+7    POINT TO 1 POS TABLE             HW012
         LA    R15,0             SET EXECUTE REG TO 1             HW012
HGET1PRM EX    R15,CLC           COMPARE TO CURRENT               HW012
         BNE   HINC10            NOT THIS ONE TRY NEXT            HW012
         CLI   1(R2),C' '        BLANK AFTER CHARACTER            HW012
         BE    FNDHWDC2          BE TO PROCESS                    HW012
         CLI   1(R2),C','        COMMA AFTER CHARACTER            HW012
         BE    FNDHWDC2          BE TO PROCESS                    HW012
HTBL2    LA    R14,MYTABLE2      POINT AT TABLE OF PARMS          HW012
         LA    R7,JCTNJHDR
HGETHPRM SR    R15,R15           CLEAR R15                        HW012
         IC    R15,0(R14)        GET LENGTH OF ENTRY              HW012
         EX    R15,CLC           COMPARE TO CURRENT               HW012
         BE    FNDHWDC1          BE TO PROCESS                    HW012
         LA    R14,10(R14)       POINT TO NEXT ENTRY              HW012
         CLI   0(R14),X'FF'      END OF TABLE                     HW012
         BNE   HGETHPRM          NO - GO CHECK NEXT PARM          HW012
         B     HFNDNPRM          CANT BE MINE!                    HW012
HINC10   LA    R14,10(R14)       POINT TO NEXT ENTRY              HW012
         CLI   0(R14),X'FF'      END OF TABLE                     HW012
         BNE   HGET1PRM          NO - GO CHECK NEXT PARM          HW012
         B     HTBL2             YES - THEN CHECK TABLE 2
HFNDNPRM CLI   0(R2),C' '        END OF CARD?                     HW012
         BE    HCHEND1           YES                              HW012
         CLI   0(R2),C','        END OF PARAMETER?                HW012
         BNE   HPARMFN3          GOT A HIT                        HW012
         LA    R2,1(R2)          POINT TO NEXT POSITION           HW012
         BCT   R6,HPARMFN2       GO CHECK NEXT PARAMETER          HW012
         B     HCHEND1           CARD ENDS IN A COMMA?            HW012
HPARMFN3 LA    R2,1(,R2)         INCREMENT POINTER                HW012
         BCT   R6,HFNDNPRM       LOOP TILL END                    HW012
HCHEND1  EQU   *                                                  HW012
HRESTBMP NI    JRTFLAG,X'FF'-JRTBMPS TURN OFF FLAG                HW012
         B     EX4RET08          TELL JES2 TO MAKE IT A COMMENT   HW012
FNDHWDC1 EQU   *                 POINT AFTER PARAMETER            HW012
         LA    R5,9(R14)         POINT TO BIT MASK                HW012
         B     HORFLAG                                            HW012
FNDHWDC2 EQU   *                 POINT AFTER PARAMETER            HW012
         LA    R5,2(R14)         POINT TO BIT MASK                HW012
HORFLAG  NULL                                                     HW012
         CLC   =C'NIGHT',0(R2)
         BE    OURHOLD
         CLI   0(R2),C'W' IS THIS NIGHT ABREVIATION
         BNE   HORFLAG2
         OI    JCTUSER4+16,QUEUSERH
OURHOLD  EQU   *
         OI    JCTUSER4+16,QUEUSERH
HORFLAG2 OC    JRTFLAG,0(R5)     TURN ON BIT(S)                   HW012
HORFLAG3 LA    R15,1(R15)        GET TRUE LENGTH
         LA    R2,0(R15,R2)      POINT AFTER PARAMETER
         SR    R6,R15            SUBTRACT LENGTH OF PARM FROM
         BNP   EX4RET12          COUNTER AND BRANCH IF NOTHING LEFT
         CLI   0(R2),C'='        IS THIS A VALUE PARAMETER?
         BE    PROCVAL            YES - PROCESS THE VALUE
         CLI   0(R2),C','        IS THERE A COMMA NEXT            HW012
         BNE   HCHEND1           NO  END OF CARD                  HW012
         LA    R5,1(,R2)         INCREMENT OVER ,                 HW012
         B     HPARMFN2          GO LOOK FOR NEXT                 HW012
PROCVAL  EQU   *
         CLI   1(R14),C'B'       IS IT BUILDING?
         BE    MOVEB
         CLC   1(3,R14),MYTABLE2+1 IS IT DIVISION?
         BE    MOVEB              YES - USE BUILDING SLOT
         CLI   1(R14),C'R'       IS IT ROOM?
         BE    MOVER
         CLI   1(R14),C'D'       IS IT DEPARTMENT?
         BNE   EX4RET12           NO BAD PARM
         LA    R15,NJHGDEPT
         B     VALMOVE
MOVEB    LA    R15,NJHGBLDG
         B     VALMOVE
MOVER    LA    R15,NJHGROOM
VALMOVE  EQU   *
         CLI   1(R2),X'7D'             IS THERE A APOSTROPHY
         BE    VALMOVEA                 YES - PROCEED AS NORMAL
         LA    R14,2(R2)         POINT AFTER FIRST CHARACTER
         SLR   R5,R5             ZERO OUT COUNTER
VALMOVE1 EQU   *
         CLI   0(R14),C' '       END OF CARD
         BE    ENDVAL
         CLI   0(R14),C','       END OF VALUE?
         BE    ENDVAL
         LA    R14,1(R14)        POINT TO NEXT POSITION
         LA    R5,1(R5)          ADD ONE TO COUNTER
         CH    R5,=H'7'          MAX VALUE LENGTH
         BH    EX4RET12           BAD VALUE
         BCT   R6,VALMOVE1
ENDVAL   EQU   *
         EX    R5,MVC2
         LR    R2,R14
         B     HFNDNPRM          GO LOOK FOR NEXT                 HW012
VALMOVEA EQU   *
         LA    R2,1(R2)                BUMP POINTER
         BCT   R6,TAG1                 BRANCH IF NOT END
         B     HCHEND1
TAG1     EQU   *
         LA    R14,2(R2)         POINT AFTER FIRST CHARACTER
         SLR   R5,R5             ZERO OUT COUNTER
VALMOVA1 EQU   *
         CLI   0(R14),X'7D'      END OF PARAMETER
         BE    ENDVALA
         LA    R14,1(R14)        POINT TO NEXT POSITION
         LA    R5,1(R5)          ADD ONE TO COUNTER
         CH    R5,=H'7'          MAX VALUE LENGTH
         BH    EX4RET12           BAD VALUE
         BCT   R6,VALMOVA1
ENDVALA  EQU   *
         EX    R5,MVC2
         LR    R2,R14
         LA    R2,1(R2)          POINT AFTER '
         BCT   R6,HFNDNPRM       GO LOOK FOR NEXT                 HW012
         B     HCHEND1            END IF NOTHING LEFT
EX4STC00 EQU   *
         SLR   R15,R15             CLEAR REG
         MVC   JCTXWRK(80),0(R2)   COPY OUTPUT STMT
         IC    R15,2(R2)           GET 1ST CHAR
         LA    R15,1(,R15)         ADD 1
         STC   R15,JCTXWRK+2       SAVE FOR NEW OUTPUT
         LA    R15,20(,R4)         FOR SCAN
EX4DLP1  EQU   *
         CLC   =C'DEST=(',0(R15)   IS THIS DEST
         BE    EX4DEST             YES
         CLC   =C'¢¢¢¢¢¢¢¢',0(R15) END OF CARD?  SHOULDN'T BE
         BE    EX4RET00            YES
         LA    R15,1(,R15)         POINT TO NEXT POSITION
         B     EX4DLP1             AND PROCESS
EX4DEST  EQU   *
         LA    R15,5(,R15)         POINT AFTER DEST=
         LR    R14,R15             GET
         SR    R14,R4               OFFSET
         AR    R14,R2              POINT TO 1ST CARD
         LA    R1,70(,R2)          GET 'END' OF CARD
         SR    R1,R14              GET REMAINDER
         EX    R1,MVCDEST1         MOVE
         EX    R1,MVCDEST2         MOVE
EX4DLP2  EQU   *
         CLI   0(R14),C','         HAVE WE FOUND THE ,
         BE    EX4DCFND            YES
         CLI   0(R14),C' '         HAVE WE FOUND THE END
         BE    EX4RET00            YES
         LA    R14,1(,R14)         POINT TO NEXT POSITION
         B     EX4DLP2             AND CHECK
EX4DCFND EQU   *
         MVI   0(R14),C' '         BLANK OUT COMMA
         SR    R14,R2              GET OFFSET
         AR    R14,R4              POINT TO NEW CARD
         LA    R1,70(,R4)          GET 'END' OF WORK AREA
         SR    R1,R14              GET LENGTH
         EX    R1,MVCDEST3         SHIFT FIRST DEST OUT
         OI    RDWFLAGX,RDWXXSNC
         B     EX4RET00
MVCDEST1 MVC   0(0,R14),1(R14)
MVCDEST2 MVC   0(0,R15),1(R15)
MVCDEST3 MVC   0(0,R15),1(R14)
MVC2     MVC   0(0,R15),1(R2)    EXECUTED INSTRUCTION
MVC      MVC   0(0,R2),0(R5)     EXECUTED INSTRUCTION             HW012
         SPACE 3                                                  HWDC
EX4RET00 DS    0H                                                 HWDC
         LA    R15,0               SET ZERO RETURN CODE.          HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
         SPACE 2                                                  HWDC
EX4RET08 DS    0H                                                 HWDC
         LA    R15,8               SET 8 TO CALLER CODE.          HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
         SPACE 2                                                  HWDC
EX4RET12 DS    0H                                                 HWDC
         LA    R15,12              SET 12 TO CALLER CODE.         HWDC
         $RETURN RC=(R15)          RETURN TO CALLER               HWDC
***************************************************************** HWDC
         TITLE 'HWDC EXIT 004 CONSTANTS AND DSECTS      '         HWDC
***************************************************************** HWDC
*                                                               * HWDC
*     MISCELLANEOUS CONSTANTS AND DSECTS FOR JCL/JECL SCAN.     * HWDC
*                                                               * HWDC
***************************************************************** HWDC
         SPACE 3                                                  HWDC
RAFTRCRD DC    C'AFTER'            HWDC /*AFTER    CARD ID.       HW013
RWHILCRD DC    C'WHILE'            HWDC /*WHILE    CARD ID.       HW013
RJOBPCRD DC    C'JOBPARM'          JES2   /*JOBPARM CARD ID.      HW012
RHWDCCRD DC    C'HWDCPARM'         HWDC   /*HWDCPARM CARD ID.     HW012
FILLER   DC    C'/*PRODUCTION JOB SUBMITTED BY APOLLO'            HW013
         SPACE 1                                                  HW013
RBADWHLM DS    0CL80                                              HW013
         DC    CL8'$HASP935'                                      HW013
         DC    CL72'* JOBNAME SPECIFIED ON /*WHILE STATEMENT IS  INVALI+
               D. CORRECT - RESUBMIT'                             HW013
RBADAFTM DS    0CL80                                              HW013
         DC    CL8'$HASP936'                                      HW013
         DC    CL72'* JOBNAME SPECIFIED ON /*AFTER  STATEMENT IS INVALI+
               D. CORRECT - RESUBMIT'                             HW013
RNOJRTM  DS    0CL80                                              HW013
         DC    CL8'$HASP940'                                      HW013
         DC    CL72'* INTERNAL ERROR - NO JRT AVAILABLE TO PROCESS.    +
                CONTACT SOFTWARE.    '                            HW013
ROUTORDM DS    0CL80                                              HW013
         DC    CL8'$HASP939'                                      HW013
         DC    CL72'*/*WHILE AND /*AFTER STATEMENTS ARE OUT OF  ORDER, +
               CORRECT AND RESUBMIT.'                             HW013
MYTABLE1 EQU   *                                                  HW012
         DC    X'05',CL7'ADABAS',C'A',AL1(JRTADA)
         DC    X'04',CL7'NIGHT ',C'W',AL1(JRTPROD)
         DC    X'05',CL7'ONLINE',C'H',AL1(JRTTP)
         DC    X'05',CL7'REMOVE',C'D',AL1(JRTBYP+JRTBMPS)
         DC    X'01',CL7'TQ    ',C'Q',AL1(JRTTQ)
         DC    10X'FF'           END OF TABLE                     HW012
MYTABLE2 EQU   *                                                  HW012
         DC    X'02',CL7'DIV   ',C' ',X'00'
         DC    X'03',CL7'BLDG  ',C' ',X'00'
         DC    X'03',CL7'ROOM  ',C' ',X'00'
         DC    X'03',CL7'DEPT  ',C' ',X'00'
         DC    X'03',CL7'HOLD  ',C' ',X'00'
         DC    10X'FF'           END OF TABLE                     HW012
JRTALL   EQU   JRTBYP+JRTBMPS+JRTPROD+JRTADA+JRTTQ+JRTEX+JRTTP
         EJECT                                                    HW013
         LTORG                                                    HWDC
PATCH    DC    C'PATCH '
         DC    250C' '
***************************************************************** HWDC
         TITLE 'COMPLETE MODULE INFORMATION TABLE. '              HWDC
         $MODEND                                                  HWDC
         END   ,                                                  HWDC
