         MACRO
&NAME    RELEASE &L,&RC=0,&LI=LA,&WORKREG=R14,&FREE=YES,       05/08/77W
               &SP=0                                           01/06/78
.*
.*       RELEASE UNDOES WHAT CONNECT DID.  I.E., IT FREEMAINS
.*       ANY SAVEAREA AFTER DISCONNECTING IT, RESTORES REGISTERS,
.*       AND RETURNS.
.*
.*  CHANGES:                                                   05/08/77
.*                                                             05/08/77
.*       05/08/77 - ADD &WORKREG,&FREE,AND CHECK &RC           05/08/77
.*       05/25/77 - FIXUP CODE FROM 05/08/77 TO SET RC         05/25/77
.*       01/06/78 - ADD &SP (SUBPOOL SUPPORT)                  01/06/78
.*                                                             05/08/77
         LCLB  &CODE                                           05/08/77
&CODE    SETB  ('&RC' EQ '(15)' OR '&RC' EQ '(R15)')           05/08/77
&CODE    SETB  (&CODE OR '&RC' EQ '(0)' OR '&RC' EQ '(R0)')    05/08/77
&CODE    SETB  (&CODE OR '&RC' EQ '(1)' OR '&RC' EQ '(R1)')    05/08/77
         AIF   ('&FREE' EQ 'NO').XNAME                         05/08/77
         AIF   (T'&L EQ 'O').XNOFREE
.XNAME   ANOP                                                  05/08/77
&NAME    LR    R1,R13
         L     R13,4(R13)
         XC    8(4,R13),8(R13)
         AIF   ('&L' EQ 'NOFREE' OR '&FREE' EQ 'NO').XRC       05/08/77
         AIF   (NOT &CODE).XLI                                 05/08/77
         LR    &WORKREG,&RC(1)          SAVE RETURN CODE       05/08/77
.XLI     &LI   R0,&L                    WORKAREA LENGTH
         AIF   (&SP EQ 0).SUBPOL9                              01/06/78
         B     *+6                    BR. AROUND SUBPOOL       01/06/78
         DC    AL1(&SP),X'00'         SPECIFIED SUBPOOL & FILL 01/06/78
         ICM   R0,B'1000',*-2         INSERT SPECIFIED SUBPOOL 01/06/78
.SUBPOL9 ANOP                                                  01/06/78
         FREEMAIN R,LV=(0),A=(1)
         AIF   (NOT &CODE).XRC                                 05/25/77
         LR    R15,&WORKREG             SET THE RETURN CODE    05/08/77
         AGO   .XRC
.XNOFREE AIF   (T'&NAME EQ 'O').XRC
&NAME    DS    0H
.XRC     AIF   (T'&RC NE 'O').RCHK
         LM    R14,R12,12(R13)
         BR    R14
         MEXIT
.RCHK    AIF   ('&RC' EQ '0').RLA
         AIF   ('&RC'(1,1) NE '(').RLA
         AIF   ('&RC(1)' EQ '15' OR '&RC(1)' EQ 'R15').RETURN  05/08/77
         AIF   (&CODE).RETURN                                  06/01/77
         LR    R15,&RC(1)
         AGO   .RETURN
.RLA     LA    R15,&RC
.RETURN  L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
         MEND
