         MACRO
&NAME    CONNECT  &L,&D,&CPPL=NO,&BASE=,&PARM=,&CLEAR=,&LI=LA,&R0=,    W
               &MODESET=,&SAVE=YES,&ID=,&ENTRY=,&ENTREG=R15,           W
               &SP=0,                                          01/06/78W
               &REGSAVE='R14,R12,12(R13)'                      04/12/77
.*
.*       THIS MACRO GENERATES CSECT START-UP LINKAGE.
.*
.*                                                             01/06/78
.*  CHANGES:                                                   01/06/78
.*                                                             01/06/78
.*       01/06/78 - ADD &SP (SUBPOOL SUPPORT)                  01/06/78
.*       10/25/78 - RDN ALTER SUBPOOL FOR SAM BASS AND # SUBSYS10/25/78
.*                                                             01/06/78
         LCLA  &I,&J,&K,&N,&R                                  04/12/77
         LCLC  &CNAME,&XRNAME                                  04/12/77
         LCLC  &SPIDLBL                                        01/06/78
&CNAME   SETC  '&SYSECT'
         AIF   (T'&ENTRY EQ 'O').XANOP1
&CNAME   SETC  '&ENTRY(1)'
.XANOP1  ANOP
&N       SETA  N'&BASE
         AIF   (T'&ID EQ 'O').XIDLEN
&K       SETA  K'&ID-2
&K       SETA  ((&K+1)/2)*2
.XIDLEN  ANOP
&I       SETA  &K+28
&NAME    B     &I.(,&ENTREG)            BRANCH AROUND ID       05/24/77
         DC    FL1'23'                  ID LENGTH
         DC    CL9'&CNAME'              CSECT NAME
         DC    CL8'&SYSDATE'            ASSEMBLY DATE
         AIF   ('&SP(1)' NE '0' AND T'&SP(2) EQ 'O').SUBPOL1   10/25/78
         DC    CL6' &SYSTIME'           ASSEMBLY TIME
         AGO   .SUBPOL2                                        01/06/78
.SUBPOL1 ANOP                                                  01/06/78
         DC    CL5'&SYSTIME'            ASSEMBLY TIME          01/06/78
&SPIDLBL SETC  'SPID&SYSNDX'                                   01/06/78
&SPIDLBL DC    AL1(&SP)                 SPECIFIED SUBPOOL      01/06/78
.SUBPOL2 ANOP                                                  01/06/78
         AIF   (T'&ID EQ 'O').XSAVE
         DC    CL&K&ID
.XSAVE   AIF   ('&SAVE' EQ 'NO').XBASECK
&R       SETA  K'&REGSAVE-2                                    04/12/77
&XRNAME  SETC  '&REGSAVE'(2,&R)                                04/12/77
         STM   &XRNAME                  SAVE ALL REGISTERS     04/12/77
.XBASECK AIF   (&N EQ 0).XCPPL
         LR    &BASE(1),&ENTREG         INITIALIZE BASE REGISTER
         USING &CNAME,&BASE(1)
         AIF   (&N LE 1).XCPPL
&I       SETA  2
.XBASE   ANOP
&J       SETA  &I-1
         LA    &BASE(&I),2048(&BASE(&J))
         LA    &BASE(&I),2048(&BASE(&I))
&K       SETA  &J*4096
         USING &CNAME+&K,&BASE(&I)
&I       SETA  &I+1
         AIF   (&I LE &N).XBASE
.XCPPL   AIF   ('&CPPL' EQ 'NO').XPARM
         LR    &CPPL,R1                 CPPL ADDRESS
         USING CPPL,&CPPL
         AGO   .XMODE
.XPARM   AIF   (T'&PARM EQ 'O').XREG0
         LR    &PARM(1),R1              SAVE PARAMETER REGISTER
         AIF   (N'&PARM NE 2).XREG0
         USING &PARM(2),&PARM(1)        DSECT ADDRESSABILITY
.XREG0   AIF   (T'&R0 EQ 'O').XMODE
         LR    &R0,R0
.XMODE   AIF   (T'&MODESET EQ 'O').XWORK
         MODESET MF=(E,&MODESET)
.XWORK   AIF   ('&SAVE' EQ 'NO').MEND   QUIT IF SAVE=NO
         AIF   ('&SAVE' EQ 'YES').XGETM IF SAVE=YES, THEN GETMAIN
         LA    R15,&SAVE                LOAD UP ADDRESS OF SAVE AREA
         ST    R13,4(R15)               START CHAINING SAVE AREAS
         ST    R15,8(R13)               FINISH CHAINING
         LR    R13,R15                  NEW SAVE AREA ADDRESS
         AGO   .MEND                    WE IS ALL THRU......
.XGETM   AIF   (T'&L EQ 'O').MEND
         &LI   R0,&L
         AIF   (&SP EQ 0).SUBPOL3                              10/25/78
         AIF   (T'&SP(2) EQ 'O').SUBPOL4                       10/25/78
         ICM   R0,B'1000',&SP(2)        OBTAIN FROM CALLER     10/25/78
         AGO   .SUBPOL3                                        10/25/78
.SUBPOL4 ANOP                                                  10/25/78
         ICM   R0,B'1000',&SPIDLBL      INSERT SUBPOOL ID      01/06/78
.SUBPOL3 ANOP                                                  01/06/78
         GETMAIN R,LV=(0)               OBTAIN WORKAREA/SAVEAREA
         ST    R13,4(R1)                CONNECT SAVEAREAS
         ST    R1,8(R13)
         LR    R13,R1
         AIF   (T'&D EQ 'O').XCLEAR
         USING &D,R13
.XCLEAR  AIF   (T'&CLEAR EQ 'O').MEND
         XC    0(4,R13),0(R13)
         LA    R0,8(R13)
         AIF   ('&LI' EQ 'LA').XCLA
         &LI   R1,&L
         S     R1,=F'8'
         AGO   .XCLB
.XCLA    LA    R1,&L-8
.XCLB    SLR   R14,R14
         SLR   R15,R15
         MVCL  R0,R14                   ZERO WORKAREA
.MEND    MEND
