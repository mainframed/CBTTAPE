IGGPRE00 CSECT
**********************************************************************
* NAME       -   IGGPRE00                                            *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   LIMIT NEW DATASET ALLOCATION BY VOLUME SERIAL       *
*                NUMBER RANGE (SUCH AS STM-,VSM-,ETC) VIA ACF2       *
*                RESOURCE VALIDATION ON VOLUME NAME.                 *
* INPUT                                                              *
* PARAMETER                                                          *
* REGISTER   -   R1  -    DASDM EXIT PARAMETER LIST                  *
*                                                                    *
* SYSTEM     -   MVS/SP AND MVS/XA                                   *
*                                                                    *
* DATE       -   12/04/86                                            *
*                                                                    *
* MACROS     -   GETMAIN,FREEMAIN,ACFINCVT,ACFGUCB,WTO               *
*                                                                    *
* DSECTS     -   IECIEXPL,ACCVT,ACFINCVT,ACFGUCB                     *
*                ACFASVT,IHAPSA,IHAASCB,ACCVT,ACUCB,IEFUCBOB         *
*                                                                    *
*                                                                    *
* REGISTERS  -   R0   -   *** USED BY MVS ***                        *
*                R1   -   *** USED BY MVS ***                        *
*                R2   -   SAVE REGISTER FOR DASDM RETURN CODE        *
*                R3   -   EXIT PARAMETER LIST FROM REGISTER 1        *
*                R4   -   ACF2 RESOURCE VALIDATION DSECT ADDRESS     *
*                R5   -   ACF2 CVT ADDRESS                           *
*                R6   -   ACF2 ACUCB ADDRESS                         *
*                R7   -   WTO ERROR MESSAGE WORK AREA ADDRESS        *
*                R8   -     *** UNUSED ***                           *
*                R9   -     *** UNUSED ***                           *
*               R10   -     *** UNUSED ***                           *
*               R11   -   1ST BASE REGISTER                          *
*               R12   -   2ND BASE REGISTER                          *
*               R13   -   SYSTEM SAVE AREA ADDRESS                   *
*               R14   -   RETURN ADDRESS                             *
*               R15   -   ENTRY POINT AND RETURN CODES               *
*                                                                    *
* PROGRAM    -  ONLY NON-VIO ALLOCATE DASDM ACCESSES WILL BE         *
* LOGIC         PROCESSED.                                           *
*                                                                    *
*               GET THE VOLUME SERIAL NUMBER FROM THE UCB ADDRESS    *
*               AND PERFORM AN ACF2 RESOURCE CALL BASED ON THE       *
*               VOLUME SERIAL NUMBER TO A PRE-STORED ACF2 RESOURCE   *
*               RULE.                                                *
*                                                                    *
*               IF ACF2 RECOMMENDS TO LOG THIS REQUEST A CICA        *
*               DASDM WARNING MESSAGE IS WRITTEN TO THE USERS        *
*               JOBLOG AND AN ACF2 RESOURCE LOGGING RECORD IS        *
*               CREATED.  A RETURN CODE OF 0 IS PASSED TO DASDM      *
*               TO ALLOW THIS REQUEST.                               *
*                                                                    *
*  |----------| IF ACF2 RECOMMENDS TO DENY THIS REQUEST BUT A        *
*  |  ADDED   | $USERDATA(WARN) IS DETECTED IN THE ACGREDTA ACGRSRC  *
*  |          | RULE BUFFER FIELD,  ACF2 WILL LOG THIS ACCESS AS     *
*  | 10/30/86 | A VIOLATION.  A DASDM WARNING MESSAGE IS WRITTEN TO  *
*  |          | THE USERS JOBLOG AND AN ACF2 RESOURCE LOGGING RECORD *
*  |          | IS CREATED.  A RETURN CODE OF 0 IS PASSED TO DASDM   *
*  |----------| TO ALLOW THIS REQUEST.                               *
*                                                                    *
*               IF ACF2 RECOMMENDS TO DENY THIS REQUEST A CICA       *
*               DASDM ABORT MESSAGE IS WRITTEN TO THE USERS          *
*               JOBLOG AND AN ACF2 RESOURCE LOGGING RECORD IS        *
*               CREATED.  A RETURN CODE OF 8 IS PASSED TO DASDM      *
*               TO DENY THIS REQUEST.                                *
*                                                                    *
* PROGRAM    -  CORRECT OF INTERMITTENT ABEND0C4S IN IGGPRE00 WAS    *
* CORRECTIONS   DONE BY CORRECTING EVERY USE OF THE WKADSCT TO USE   *
*               R13 AND NOT R4 (I WAS DROPPING 72 BYTES THIS WAY     *
*               INDEXING INTO THE WTOANSW USER WTO AREA.) THE WTOANSW*
*               USER WTO AREA WAS ENLARGED TO 200 BYTES FROM 100 AS  *
*               A SAFETY VALVE.                                      *
*                                                                    *
*               LOADING OF A SECOND BASE REGISTER WAS SHORT BY 1     *
*               BYTE.                                                *
**********************************************************************
         USING IGGPRE00,R15                 TEMP ADDRESSABILITY
         B     DASDFC                       BRANCH AROUND EYECATCHER
         DC    C'IGGPRE00'                  IGGPRE00 PROGRAM
         DC    C' '
         DC    C'&SYSDATE'                  GREGORIAN DATE
         DC    C' '
         DC    C'&SYSTIME'                  GREGORIAN TIME
         DS    0H                           RE-ESTABLISH ALIGNMENT
DASDFC   EQU   *
         USING EXITPARM,R1                  SET ADDRESSABILITY TO IT
         CLI   IEXFUNC,IEXALL               ALLOCATE FUNCTION?
         BE    CHKVIO                           IF SO, SEE IF VIO
         B     CLEAR15
CHKVIO   TM    IEXFLAG,IEXVIO               VIO DATASET
         BZ    PROCESS                          IF NOT PROCESS NOW
CLEAR15  SLR   R15,R15                      GOOD RETURN CODE
         BR    R14                          BACK TO MVS NOW
         DROP  R1,R15                       DONE WITH ADDRESSABILITY
********************************************
*  PERFORM REENTRANT ENTRY                 *
*  HOUSEKEEPING.                           *
********************************************
PROCESS  STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R11,R15                      SET BASE REGISTER
         USING IGGPRE00,R11,R12             SET ADDRESSABILITY
         LA    R0,WKALEN                    SAVE AREA AND WORK AREA
         GETMAIN R,LV=(0)                   GETMAIN OUR SAVE AREA
         ST    R13,4(R1)                    SYSTEM S/A INTO OUR S/A
         ST    R1,8(R13)                    OUR S/A INTO SYSTEM S/A
         LR    R13,R1                       OUR S/A ADDRESS IN R13
         L     R1,4(R13)                    RELOAD SYSTEM S/A ADDRESS
         LM    R0,R1,20(R1)                 RELOAD REGS 0 AND 1
         LA    R12,4095(R11)                LOAD 2ND BASE REGISTER
         LA    R12,1(R12)
********************************************
*  CLEAR AND INITIALIZE ENTRY/RESOURCE     *
*  PARAMETER BLOCK.                        *
********************************************
         LR    R3,R1                        EXIT PARAMETER LIST ADDRESS
         USING EXITPARM,R3                  SET ADDRESSABILITY TO IT
         LR    R4,R13                       OUR SAVE/WORK AREA ADDRESS
         LA    R4,72(R4)                    OUR WORK AREA ADDRESS
         LR    R0,R4                        GET ADDRESS TO CLEAR
         LR    R14,R4                       GET ADDRESS TO CLEAR
         LA    R1,WKALEN-72                 SET LENGTH TO CLEAR
         SR    R15,R15                      SECOND LENGTH TO ZERO
         MVCL  R0,R14                       BINARY ZERO STORAGE
********************************************
*  GET ACF2 CVT VIA SUBSYSTEM INTERFACE    *
*  CONTROL BLOCKS                          *
********************************************
         ACFINCVT R5,NONE=EXIT0             GET ACF2 CVT
         USING ACCVT,R5                     SET ADDRESSABILITY TO IT
         USING ACGRSRC,R4                   SET ADDRESSABILITY TO IT
         TR    ACGRNAME,TRTAB40             BLANK OUT RESOURCE NAME
         ACFGUCB (6),NONE=EXIT0,INLINE=YES,SYS=AOS2  ACUCB ADDRESS
         USING ACUCB,R6
         L     R1,ACULRECP                  ADDRESS OF LOGONID RECORD
         USING LIDREC,R1                    DSECT IT
         SLR   R2,R2                        INSURE RC=0
         CLC   LIDLID(2),SS                 A TECH SUPPORT LOGONID
         BE    EXIT0                        DOES NOT GO THRU EXIT!
         DROP  R1                           DONE WITH DSECT NOW
********************************************
* SET UP RESOURCE ENTRY/RESOURCE PARAMETER *
* BLOCK                                    *
********************************************
         LR    R1,R6                        SAVE ACUCB ADDRESS
         ST    R1,ACGUCB                    SAVE FOR RESOURCE CALL
         MVI   ACGFCN,4                     SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP              INTERPRET FUNCTION
         MVI   ACGRFLGS,0                   INIT RETURN FLAGS
         LA    R1,MESSAGE                   MESSAGE AREA ADDRESS
         ST    R1,ACGMSG                    STORE IN CONTROL BLOCK
         LA    R1,RULEDATA                  RULE DATA AREA ADDRESS
         ST    R1,ACGREDTA                  STORE IN CONTROL BLOCK
         LA    R1,L'RULEDATA                RULE DATA AREA LENGTH
         ST    R1,ACGREDTL                  STORE IN CONTROL BLOCK
         MVC   ACGRTYPE,RESTYPE             RESOURCE TYPE = RVOL
         L     R1,IEXUCB                    DASDM UCB ADDRESS
         USING UCB,R1                       SET UCB ADDRESSABILITY
         MVC   ACGRNAME(6),UCBVOLI          SET RESOURCE VOLUME NAME
         DROP  R1                           DONE WITH ADDRESSABILITY
********************************************
* ISSUE ACF2 RESOURCE VALIDATION CALL      *
********************************************
         ACFSVC ACGRSRC,TYPE=A,CVT=HAVE,NONE=EXIT0
         LTR   R15,R15                      ACF2 CALL SUCCESSFULL?
         DROP  R5                           DONE WITH ADDRESSABILITY
         BZ    CHKFLG                       YES, CHECK FLAG BYTE
********************************************
* IF NO RULE WAS FOUND (RULE DATA LENGTH   *
* IS X'FFFF'), THEN ALLOW ACCESS. THIS IS  *
* TO ELIMINATE THE REQUIREMENT FOR A       *
* GLOBAL RULE OF THE FORM:                 *
*                                          *
*     $KEY(********) TYPE(VOL)             *
*            ALLOW                         *
*                                          *
*                                          *
* NOTE: THIS GLOBAL RULE IS NEEDED TO      *
*       DEACTIVATE ACF2 INTERNAL SECURITY  *
*       LOGGING.                           *
********************************************
         SLR   R15,R15                      MAKE SURE R15 IS 0
         L     R1,ACGREDTA                  ADDRESS OF RULE BUFFER
         CLC   FOXES,0(R1)                  ANY RULE FOUND
         BE    NORULE                       IF NONE, ALLOW ACCESS
********************************************
* ACF2 SVC CALL WAS SUCCESSFULL (R15=0)    *
*                                          *
* IF THE RULE SPECIFIES ALLOW -            *
* ALLOW THE ACCESS                         *
*                                          *
* (IF THE RULE SPECIFIED LOG               *
*  THE LOGGING OPERATION HAS               *
*  ALREADY COMPLETED)                      *
*                                          *
* IF THE RULE SPECIFIES PREVENT -          *
* ABORT AND LOG THE ACCESS                 *
********************************************
CHKFLG   CLI   ACGRFLGS,0              DID ACF2 ALLOW ACCESS?
         BE    EXIT0                   IF 0, ALLOW ACCESS
********************************************
* CHECK TO SEE IF THIS IS A REAL ABORT OR  *
* IS THERE A $USERDATA(WARN) TO INDICATE   *
* RUNNING THE RULE IN WARN-ONLY MODE.      *
********************************************
ABORT    L     R1,ACGREDTA             ADDRESS OF RULE BUFFER
         CLC   0(2,R1),ZERO2           ANY $USERDATA FIELD?
         BNE   WARN                        IF SO, CHECK FOR WARN MODE
********************************************
* ABORT THE REQUEST BY ADDING USER-BUILT   *
* ABORT MESSAGE TO USERS JOBLOG AND SETTING*
* RETURN CODE TO 8 TO FAIL DASDM REQUEST.  *
********************************************
         LR    R1,R13                  OUR SAVE/WORK AREA
         USING WKADSCT,R1              OUR WORK DSECT
         LA    R7,WTOANSW              OUR WTO WORK AREA
         USING VIOMSGD,R7              WTO OUTPUT DSECT
         DROP  R1                      DONE WITH DSECT NOW
         TR    0(VIOMSGL,R7),TRTAB40   FIRST BLANK IT OUT
         MVC   0(VIOMSGL,R7),VIOMSG    INITIALIZE WTO AREA
         MVC   VOLVIO,ACGRNAME         VOLUME NAME
         TR    DSNVIO(22),TRTAB40      CLEAR DSNAME FIELD
         L     R15,IEXDSN              ADDRESS OF DSNAME
         MVC   DSNVIO(22),0(R15)       DSNAME (FIRST 22 CHARACTERS)
         LA    R2,8                    TELL DASDM TO REJECT REQUEST
         WTO   MF=(E,(R7))             TELL USER ABOUT IT
         WTO   MF=(E,MEDMSG)           TO CONTACT MEDIA MANAGEMENT
         B     EXIT8                   AND FAIL IT
********************************************
* CHECK FOR A $USERDATA(WARN) IN THE RULE  *
* BUFFER.  IF IT IS FOUND, LET ACF2 LOG    *
* THE ACCESS VIOLATION.  ISSUE A WARNING   *
* WTO MESSAGE AND SET A RETURN CODE OF 0   *
* TO LET DASDM ALLOW THIS REQUEST.         *
********************************************
WARN     LR    R1,R13                  OUR SAVE/WORK AREA
         USING WKADSCT,R1              OUR WORK DSECT
         LA    R7,WTOANSW              OUR WTO WORK AREA
         USING WRNMSGD,R7              WTO OUTPUT DSECT
         DROP  R1                      DONE WITH DSECT NOW
         TR    0(WRNMSGL,R7),TRTAB40   FIRST BLANK IT OUT
         MVC   0(WRNMSGL,R7),WRNMSG    INITIALIZE WTO AREA
         MVC   VOLWRN,ACGRNAME         VOLUME NAME
         TR    DSNWRN(22),TRTAB40      CLEAR DSNAME FIELD
         L     R15,IEXDSN              ADDRESS OF DSNAME
         MVC   DSNWRN(22),0(R15)       DSNAME (FIRST 22 CHARACTERS)
         WTO   MF=(E,(R7))             TELL USER ABOUT IT
         WTO   MF=(E,MEDMSG)           TO CONTACT MEDIA MANAGEMENT
         B     EXIT0                   BUT ALLOW THE REQUEST
********************************************
* SET RETURN CODE TO DASDM AND FREE THE    *
* GETMAINED SAVE AREA AND ACF2 WORK AREA   *
********************************************
NORULE   EQU   *                       PROCESSING IF NO RULE FOUND
EXIT0    SLR   R2,R2                   INSURE RC=0
EXIT8    LR    R1,R13                  LOAD OUR S/A ADDRESS
         L     R13,4(,R13)             RESTORE SYSTEM S/A ADDRESS
         LA    R0,WKALEN               GET LENGTH
         FREEMAIN R,LV=(0),A=(1)       FREE S/A
         LR    R15,R2                  SET RETURN CODE FOR DASDM
         LM    R0,R12,20(R13)          RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)             RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
********************************************
*     C O N S T A N T S   E T C.           *
********************************************
SS       DC    C'SS'                   TECH SUPPORT LOGONID
RESTYPE  DC    C'RVOL'                 RESOURCE TYPE
FOXES    DC    X'FFFFFFFF'             NO RECORD INDICATOR
ZERO2    DC    X'0000'                 LNTH OF $USERDATA FROM ACGREDTA
TRTAB40  DC    256X'40'                BLANKS TRANSLATE TABLE
VIOMSG   DS    0F
         DC    AL2(VIOMSGL-4)
         DC    B'1000000000000000'
         DC    CL31'IGGPRE00: ALLOCATE FOR DATASET '
         DC    CL22'                      '    DSNAME
         DC    C' ON VOLUME '
         DC    CL6'      '                     VOLUME
         DC    C' DENIED BY INSTALLATION EXIT'
         DC    B'0000000000000000'
         DC    B'0000000000100000'   ROUTCDE=11
VIOMSGL  EQU   *-VIOMSG
WRNMSG   DS    0F
         DC    AL2(WRNMSGL-4)
         DC    B'1000000000000000'
         DC    CL31'IGGPRE00: ALLOCATE FOR DATASET '
         DC    CL22'                      '    DSNAME
         DC    C' ON VOLUME '
         DC    CL6'      '                     VOLUME
         DC    C' WILL SOON BE DENIED'
         DC    B'0000000000000000'
         DC    B'0000000000100000'   ROUTCDE=11
WRNMSGL  EQU   *-WRNMSG
MEDMSG   DS    0F
         DC    AL2(MEDMSGL-4)
         DC    B'1000000000000000'
         DC    C'IGGPRE00: CONTACT MEDIA MANAGEMENT FOR ASSISTANCE'
         DC    B'0000000000000000'
         DC    B'0000000000100000'   ROUTCDE=11
MEDMSGL  EQU   *-MEDMSG
         LTORG
********************************************
*        D S E C T S  E T C.               *
********************************************
WKADSCT  DSECT
SAVEAREA DS    18F                     SAVE AREA
         TITLE 'ACF2 RESOURCE PARAMETER LIST BLOCK'
         ACGRSRC DSECT=NO
         SPACE
*        ACF2 RULE DATA AREA
RULEDATA DS    CL132                   RULE DATA AREA
*        RULE DATA AREA: (HALFWORD LENGTH $USERDATA + $USERDATA) +
*                        (HALFWORD LENGTH RULE DATA + RULE DATA) =
*                        (2 + 64) + (2 + 64) = 132
**********************************************************************
*        ACF2 MESSAGE AREA
MESSAGE  DS    CL128                   MESSAGE AREA
WTOANSW  DS    CL200                   USER WTO AREA
WKALEN   EQU   *-WKADSCT            TOTAL LENGTH OF WORKAREA
********************************************
*     USER-DEVELOPED ABORT MESSAGE DSECT.  *
********************************************
VIOMSGD  DSECT
         DS    CL4     VIOMSGL AND BIT FLAGS
         DS    CL31    IGGPRE00: ALLOCATE FOR DATASET
DSNVIO   DS    CL22    FIRST 22 BYTES OF DATASET
         DS    CL11    ON VOLUME
VOLVIO   DS    CL6        VOLSER
*        DS    CL??    DENIED BY INSTALLATION EXIT
         SPACE
********************************************
*     USER-DEVELOPED WARN MESSAGE DSECT.   *
********************************************
WRNMSGD  DSECT
         DS    CL4     WRNMSGL AND BIT FLAGS
         DS    CL31    IGGPRE00: ALLOCATE FOR DATASET
DSNWRN   DS    CL22    FIRST 22 BYTES OF DATASET
         DS    CL11    ON VOLUME
VOLWRN   DS    CL6        VOLSER
*        DS    CL??    WILL SOON BE DENIED
         SPACE
*****************************************
*        D S E C T S                    *
*****************************************
EXITPARM IECIEXPL                DASDM EXIT PARAMETER LIST
*        PRINT  NOGEN
         TITLE 'ACF2 COMMUNICATIONS VECTOR TABLE'
         ACCVT  DSECT=YES
         TITLE 'ACF2 USER CONTROL BLOCK'
         ACUCB  DSECT=YES
         TITLE 'ACF2 ADDRESS SPACE VECTOR TABLE'
         ACFASVT DSECT=YES
         TITLE 'ACF2 LOGONID RECORD'
         LIDREC DSECT=YES
         TITLE 'MVS COMMUNICATIONS VECTOR TABLE'
         CVT    DSECT=YES
         TITLE 'MVS PREFIXED STORAGE AREA'
         IHAPSA
         TITLE 'MVS ADDRESS SPACE CONTROL BLOCK'
         IHAASCB
         TITLE 'MVS UNIT CONTROL BLOCK DSECT'
UCB      DSECT
         IEFUCBOB
         REGISTER
         END IGGPRE00
