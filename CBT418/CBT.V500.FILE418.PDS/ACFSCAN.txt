*          DATA SET ACFSCAN    AT LEVEL 001 AS OF 10/27/86
ACFSCAN  CSECT
 TITLE 'ACF2 USER-WRITTEN LOGONID EXPIRATION UTILITY'
**********************************************************************
* NAME       -   ACFSCAN                                             *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   SCAN THE PRIMARY ACF2 SEQUENTIAL BACKUP DATABASE    *
*                AND EXPIRE SELECTED LOGONIDS NOT USED IN 31 DAYS.   *
*                                                                    *
* INPUT      -   ACF2 PRIMARY SEQUENTIAL BACKUP DATABASE             *
*                                                                    *
* OUTPUT     -   VIA SYSPRINT LISTING                                *
*                                                                    *
* SYSTEM     -   MVS/XA 2.1.3.                                       *
*                                                                    *
* DATE       -   04/18/86                                            *
*                                                                    *
* LAST                                                               *
* MODIFIED   -   10/21/86                                            *
*                                                                    *
* MACROS/DSECTS  ACCVT,ACUCB,ACARE,LIDREC,ACFINCVT,MODESET           *
*                                                                    *
* ACF2                                                               *
* ROUTINES   -   $ACFGCVT                                            *
*                                                                    *
* REGISTERS  -   R0    -  **USED BY MVS**                            *
*                R1    -  **USED BY MVS**                            *
*                R2    -  **UNUSED**                                 *
*                R3    -  LIDREC ADDRESS                             *
*                R4    -  CURRENT MONTH VALUE                        *
*                R5    -  **UNUSED**                                 *
*                R6    -  ANY VALUE INDICATES LEAP YEAR              *
*                R7    -  WORK REGISTER                              *
*                R8    -  **UNUSED**                                 *
*                R9    -  **UNUSED**                                 *
*                R10   -  ADDRESS OF ACALT ERRMSG                    *
*                R11   -  **UNUSED**                                 *
*                R12   -  **UNUSED**                                 *
*                R13   -  SYSTEM SAVE AREA ADDRESS                   *
*                R14   -  RETURN TO MVS BRANCH ADDRESS               *
*                R15   -  ACFSCAN PROGRAM ENTRY POINT ADDRESS        *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    NONE                                               *
*                                                                    *
* ATTRIBUTES -    SUPERVISOR KEY 0 FOR ACTUAL LOGONID EXPIRATION     *
*                 ROUTINE. PROBLEM KEY 8 FOR REMAINDER OF PROGRAM.   *
*                                                                    *
**********************************************************************
        EJECT
**********************************************************************
* PROGRAM FUNCTIONAL DESCRIPTION:                                    *
*                                                                    *
*               1.) ACFSCAN READS THE ACF2 PRIMARY SEQUENTIAL BACKUP *
*                   LOGONID DATABASE AND COMPARES THE DATE OF LAST   *
*                   SYSTEM ACCESS WITH THE CURRENT DATE.             *
*                                                                    *
*  CHANGED BY                                                        *
*  CR #13293    2.) IF DATE OF LAST ACCESS (ACC-DATE) = 0 THEN       *
*                                                                    *
*                      DO                                            *
*                          IF SETUP DATE (DATE-SET) IS GREATER THAN  *
*                          31 THEN EXPIRE THE LOGONID                *
*                      END                                           *
*                                                                    *
*  CHANGED BY                                                        *
*  CR #13293    3.) IF DATE OF LAST ACCESS (ACC-DATE) Â¬= 0 THEN      *
*                                                                    *
*                      DO                                            *
*                          IF ACC-DATE IS GREATER THAN 31 THEN       *
*                          EXPIRE THE LOGONID                        *
*                      END                                           *
**********************************************************************
        EJECT
**********************************************************************
*                   IF ANY OF THE FOLLOWING IS TRUE THEN THE         *
*                   LOGONID WILL NEVER BE EXPIRED!                   *
*                                                                    *
*                   - ANY USER PRIVILEGES (NON-CNCL,SECURITY,ETC.)   *
*                                                                    *
*                   - LOGONIDS BEGINNING WITH SS                     *
**********************************************************************
         STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R12,R15                      ENTRY POINT ADDRESS LOAD
         USING ACFSCAN,R12                  SET ADDRESSABILITY
         LR    R3,R13                       SAVE SYSTEM ADDRESS
         ST    R13,SAVEAREA+4               SYSTEM ADDR IN MY SAVEAREA
         LA    R13,SAVEAREA                 ADDRESS OF MY SAVEAREA
         ST    R13,8(R3)                    MY SAVEAREA PUT IN SYSTEM
*********************************************
*   FIRST CHECK TO SEE IF ACF2 IS ALIVE AND *
*   AND WELL!                               *
*********************************************
        ACFINCVT R1,NONE=NOACF2   IF NO ACF2 BRANCH TO NOACF2 LABEL
*********************************************
*   PERFORM EDIT CHECKING TO SEE IF WE ARE  *
*   GOING TO EXPIRE ANY LOGONIDS.           *
*********************************************
         OPEN  (LIDS,(INPUT))     OPEN ACF2 PRIMARY SEQUENTIAL DSN
         TM    LIDS+48,GOODOPEN   WAS OPEN SUCCESSFULL???
         BZ    LIDSNO             NO, ISSUE MSG AND END
         OPEN  (SYSPRINT,(OUTPUT))   OPEN OUTPUT REPORT
         TM    SYSPRINT+48,GOODOPEN  WAS OPEN SUCCESSFULL??
         BZ    PRINTNO            NO, ISSUE MSG AND END
READID   GET   LIDS               R1 ADDRESS HAS INPUT AREA
         LR    R3,R1              SAVE IT IN R3
         LA    R3,4(R3)           BUMP PAST RDW
         USING LIDREC,R3          SET ADDRESSABILITY TO IT
         ICM   R1,15,LIDEXPDT     LOAD EXPIRE FIELD
         O     R1,MAKEPACK        MAKE SURE IT IS PACKED
         ST    R1,LIDEXPDT        PUT BACK IN FIELD
         CP    LIDEXPDT,PZERO4    LOGONID IN EXPIRE STATE?
         BNE   READID              NO, READ NEXT LOGONID
         CLC   LIDLID(2),SSPGMR   IS LOGONID A SYSTEMS SUPPORT PGMR
         BE    READID             YES, BYPASS PROCESSING IT
         CLI   LIDTFLAG,LIDTUSR   IS THIS A NORMAL USER?
         BE    SPECATTR           YES, ANY SPECIAL ATTRIBUTES?
         B     READID             SECURITY,AUDIT,CONSULT,PROJ LEADER
SPECATTR TM    LIDMFLG,LIDMRST+LIDMNCNL+LIDMRSTA+LIDMSTC ATTRIBUTES?
         BNZ   READID             IF SO BYPASS PROCESSING IT
*********************************************
*   DETERMINE IF THE LOGONID HAS NOT BEEN   *
*   USED FOR 31 DAYS.                       *
*********************************************
         TM    HEADSW,ON                HAVE WE ALREADY GENERATED RPT?
         BO    CHKLID                   YES, GO CHECK LIDREC
         TIME  DEC                      GET TODAY'S DATE AND TIME
         ST    R0,WORKF                 STORE TIME OF DAY
         UNPK  WORK7,WORKF              UNPACK TIME
         MVC   TIME(2),WORK7            MOVE HOURS
         MVC   TIME+3(2),WORK7+2        MOVE MINUTES
         ST    R1,DATENOW               SAVE DATE FOR AREPACK
         ST    R1,WORKF                 STORE DATE
         UNPK  WORK7,WORKF              UNPACK DATE
         MVC   DATE+6(2),WORK7+2        MOVE YEAR
         TM    WORKF+1,X'01'            IS IT AN ODD YEAR?
         BO    NOLEAP                     YES, CAN NOT BE LEAP YEAR
         TM    WORKF+1,X'12'            EVEN YEAR - IS IT LEAP YEAR?
         BM    NOLEAP                     NO, LEAVE FEBRUARY AT 28
         AP    FEB,=P'1'                  YES, MAKE FEB. 29 DAYS
         SPACE 1
NOLEAP   DS    0H
         LA    R15,12                   SET COUNTER TO 12 MONTHS
         LA    R4,MONTHS                POINT TO TABLE
         SPACE 1
MONTHLP  DS    0H                       FIND WHICH MONTH LOOP
         AP    MONTH,=P'1'              ADD ONE TO MONTH COUNTER
         SP    WORKF+2(2),0(2,R4)       SUBT DAYS IN MONTH
         BNP   MONTHFD                    ANY DAYS LEFT?
         LA    R4,2(,R4)                POINT TO NEXT MONTH IN TABLE
         BCT   R15,MONTHLP              GO CHECK THIS MONTH
         SPACE 1
MONTHFD  DS    0H                       FOUND WHICH MONTH IT IS
         AP    WORKF+2(2),0(2,R4)       ADD DAYS IN MONTH BACK IN
         OI    WORKF+3,X'0F'            INSURE PRINTABLE CHAR
         UNPK  DATE+3(2),WORKF+2(2)     UNPACK DAY OF MONTH
         OI    MONTH+1,X'0F'            INSURE PRINTABLE CHAR
         UNPK  DATE(2),MONTH            UNPACK MONTH
         ZAP   MONTH,PZERO2             INITIALIZE MONTH FIELD
         OI    HEADSW,ON                INDICATE TIME/DATE DONE
CHKLID   EQU   *
*********************************************
*   CHECK THE DATE OF LAST ACCESS           *
*   (ACC-DATE).                             *
*                                           *
*   IF IT IS 0 THEN USE THE DATE-SET IAP    *
*   FIELD TO CALCULATE DATE LAST USED.      *
*                                           *
*   IF IT IS NOT 0 THEN USE THE ACC-DATE    *
*   DATE OF LAST ACCESS FIELD TO CALCULATE  *
*   LAST DATE USED.                         *
*********************************************
         ICM   R1,15,LIDADATE           LOAD ACC-DATE FIELD
         O     R1,MAKEPACK              MAKE SURE IT IS PACKED
         ST    R1,LIDADATE              PUT BACK IN FIELD
         CP    LIDADATE,PZERO4          ACC-DATE IS 0?
         BNE   NOT0
         ICM   R1,15,LIDSETP            LOAD DATE-SET FIELD
         O     R1,MAKEPACK              MAKE SURE IT IS PACKED
         ST    R1,LIDSETP               PUT BACK IN FIELD
         CP    LIDSETP,PZERO4           IS DATE-SET 0 - SHOULDNT BE
         BNE   SETDATE                     IF NOT, SET WORK FIELD
         MVC   LIDSETP,MAY1986          SET MAY 1ST,1986 AS DEFAULT
SETDATE  ZAP   PACKDATE,LIDSETP         USE DATE-SET FOR CALCULATION
         B     FIGURE31                 SEE IF USED WITHIN 31 DAYS
*
NOT0     ZAP   PACKDATE,LIDADATE        USE ACC-DATE FOR CALCULATION
*
FIGURE31 LA    R1,DATENOW               ADDRESS OF TODAY'S DATE
         SLR   R5,R5                    ZERO OUT
         ICM   R5,12,0(R1)              GET TODAY'S YEAR VALUE
         SRL   R5,16                    SHIFT TO LOW-ORDER POSITIONS
         LA    R1,PACKDATE              ADDRESS OF LAST-USED DATE
         SLR   R6,R6                    ZERO OUT
         ICM   R6,12,0(R1)              GET LAST-USED YEAR VALUE
         SRL   R6,16                    SHIFT TO LOW-ORDER POSITIONS
         CR    R5,R6                    ARE THEY EQUAL?
         BE    SHORTSUB                 WE CAN DO SIMPLE SUBTRACT
         SLR   R5,R5                    ZERO OUT
         TM    PACKDATE+1,X'01'         IS IT AN ODD YEAR?
         BO    NOLEAPYR                   YES, CAN NOT BE LEAP YEAR
         ZAP   WORKP3,DAY366            366 DAYS TO A LEAP YEAR
         B     SUBPACK                  GO DO SUBTRACT
NOLEAPYR ZAP   WORKP3,DAY365            365 DAYS TO A LEAP YEAR
SUBPACK  SP    WORKP3,PACKDATE+2(2)     HOW MANY DAYS LEFT LAST YEAR
         AP    WORKP3,DATENOW+2(2)      HOW MANY DAYS USED THIS YEAR
         ZAP   DAYNO,WORKP3             PUT BACK IN DAYNO FIELD
         B     COMPPACK                 GO DO COMPARE
SHORTSUB ZAP   DAYNO,DATENOW+2(2)       NO. OF DAYS SINCE LAST ACCESS
         SP    DAYNO,PACKDATE+2(2)      NO. OF DAYS SINCE LAST ACCESS
COMPPACK CP    DAYNO,P31                LOGONID NOT USED 31 DAYS
         BL    READID                    NO, READ NEXT ID
*********************************************
*   WE HAVE FOUND A LOGONID NOT USED FOR 31 *
*   DAYS.  ISSUE INTERNAL ACF2 REQUEST FOR  *
*   LOGONID EXPIRATION AND WRITE TO SYSPRINT*
*   THE LOGONID AND NUMBER OF DAYS IT WAS   *
*   NOT USED.                               *
*********************************************
*XPIRE   B     REPORT             **** DIAGNOSIS ONLY ****
XPIRE    OI    USEREXP,EXPRPT     AT LEAST 1 TO-BE-EXPIRED USER
         MVI   ACAFCN,2           SET LID-UPDATE FUNCTION
         MVI   ACASFCN,ACASALT    CHANGE LOGONID TO EXPIRED
         MVC   ACAMSG,=A(ERRMSG)  SET MESSAGE ADDR
         MVC   ACALID,LIDLID      SET LOGONID TO BE EXPIRED
         MVC   ACAUCB,=X'FFFFFFFF'  SET TO NON-ZERO FOR APF AUTH
         MVC   ACAAREB,=A(ARE)      SET TO BUFFER OF ARE'S (ALTER REQ
         ZAP   AREPACK,DATENOW      SET DATE TO EXPIRE
         LA    R1,ARELNTH            ARE BUFFER LENGTH
         ST    R1,ACAAREL         SET IN BLOCK
         MODESET KEY=ZERO
         ACFSVC ACALT,TYPE=A,NONE=ENDDATA,CVT=FIND EXPIRE LOGONID
         LTR   R15,R15
         BZ    RC0
         LA    R10,WTOMSG+4
         WTO   MF=(E,(R10))        ISSUE ERRMSG
         B     FINIS
RC0      MODESET KEY=NZERO
REPORT   ZAP   PACKDATE,PZERO4          RE-ZERO WORK PACKED DATE
         CP    LINECNT,P55              NEED ANOTHER PAGE?
         BH    HEADING                    YES, GO TO NEXT PAGE
         CP    LINECNT,PZERO2           DO WE NEED HEADER LINES?
         BNZ   LINEPUT
HEADING  AP    PAGECNT,PONE             INCREMENT PAGE COUNTER
         MVC   PAGENO,MASK              EDIT MASK
         ED    PAGENO,PAGECNT
         PUT   SYSPRINT,HEADER
         MVC   CC,DBLSPC                SET CARRIAGE CONTROL TO 1
         ZAP   LINECNT,PTWO
LINEPUT  MVC   LOGONID(6),LIDLID        MOVE IN EXPIRED LOGONID
         MVC   NAME,LIDNAME             MOVE IN LOGONID NAME
         MVC   PHONE,LIDPHONE           MOVE IN LOGONID PHONE NUMBER
         MVC   DAYSNUM,MASK             EDIT MASK
         ED    DAYSNUM,DAYNO+2          PRINT NUMBER OF DAYS
PUTIT    PUT   SYSPRINT,RPTLINE         LOGONID UNUSED
         MVC   CC,BLANK                 SET CARRIAGE CONTROL TO 1
         AP    LINECNT,PONE             REPORT LINE AND SPACE
         B     READID
LIDSNO   WTL  'ACF2 PRIMARY SEQUENTIAL LOGONID DATABASE OPEN FAILED'
         B     FINIS
PRINTNO  WTL  'ACF2 SYSPRINT OPEN FAILED'
         CLOSE LIDS
         B     FINIS
NOACF2   WTO  'ACF2 IS NOT ACTIVE'
         B     FINIS
ENDDATA  SLR   R15,R15
         CLOSE (LIDS,,SYSPRINT)
FINIS    L     R13,SAVEAREA+4               SYSTEM SAVE AREA ADDRESS
         RETURN (14,12),RC=0                RESTORE REGISTERS R15=0
*********************************************
*  C O N S T A N T S  AND  E Q U A T E S    *
*********************************************
SAVEAREA DS    18F
MASK     DC    X'40202021'
GOODOPEN EQU   X'10'             GOOD OPEN INDICATOR
DBLSPC   DC    C'0'              DOUBLE SPACE INDICATOR
EXPRPT   EQU   X'FF'             REPORT INDICATOR
USEREXP  DS    X
DBLWORD  DS    D
DAY31    DS    H'31'
HEADER   DS    0CL133
         DC    C'1'
BLANK    DC    C' '
DATE     DC    C'MM/DD/YY'       CURRENT DATE - CHARACTER
         DC    5C' '
TIME     DC    C'HH.MM'          CURRENT TIME - CHARACTER
         DC    4C' '
         DC    C'LOGONID'
         DC    2C' '
         DC    C'NAME'
         DC    21C' '
         DC    C'PHONE'
         DC    6C' '
         DC    C'DAYS SINCE LAST USED'
         DC    6C' '
         DC    C'PAGE'
         DC    2C' '
PAGENO   DC    CL4'  '
         DC    4C' '
         SPACE 3
RPTLINE  DS    0CL133
CC       DC    C' '
         DC    23C' '
LOGONID  DS    CL6
         DC    3C' '
NAME     DC    CL20' '
         DC    CL5' '
PHONE    DC    CL5' '
         DC    4C' '
DAYSNUM  DC    CL4' '
         DC    63C' '
         SPACE 3
MONTH    DC    PL2'0'         COUNTER TO DETERMINE WHICH MONTH OF YEAR
         SPACE 1
MONTHS   DC    P'31'          NUMBER OF DAYS EACH A MONTH - JANUARY
FEB      DC    P'28',P'31',P'30',P'31',P'30'
         DC    P'31',P'31',P'30',P'31',P'30',P'31'
         SPACE 3
PACKDATE DS    PL4            DATE USED FOR CALCULATIONS (CR #13293)
PZERO2   DC    PL2'0'         PACKED VALUE OF 0 LENGTH OF 2
PZERO4   DC    PL4'0'         PACKED VALUE OF 0 LENGTH OF 4
P31      DC    PL4'31'        NO. OF DAYS LOGONID NOT USED
P55      DC    PL2'55'        PAGE COUNT LIMIT
ON       EQU   X'FF'          AT LEAST ONE LOGONID EXPIRED INDICATOR
HEADSW   DS    CL1            ALREADY GENERATED TIME AND DATE
PAGECNT  DC    PL2'0'         PAGE COUNT FIELD
LINECNT  DC    PL2'0'         LINE COUNT FIELD
DAYNO    DC    PL4'0'         FULL BYTE WORK AREA
WORKF    DC    F'0'           FULL BYTE WORK AREA
DATENOW  DC    F'0'           FULL BYTE WORK AREA
WORK2    DC    XL2'0'         TWO BYTE WORK AREA
WORK1    DC    XL1'0'         ONE BYTE WORK AREA
WORK7    DC    XL7'0'         SEVEN BYTE WORK AREA
WORKP3   DC    PL3'0'
DAY365   DC    PL3'365'
DAY366   DC    PL3'366'
         DS    0P
MAY1986  DC    XL4'0086121F'  MAY 1ST,1986
SSPGMR   DC    C'SS'          SYSTEMS SUPPORT PROGRAMMER
LIDS     DCB   DDNAME=LIDS,DSORG=PS,MACRF=GL,EODAD=ENDDATA
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,            *
               BLKSIZE=1330,RECFM=FBA
PONE     DC    PL1'1'         PACKED VALUE OF 1
PTWO     DC    PL1'2'         PACKED VALUE OF 2
MAKEPACK DC    F'15'          FORCE FIELD TO PACKED FORMAT
         ACALT DSECT=NO           PARM LIST FOR LID UPDATE FUNCTION
*********************************************
*  ALTER REQUEST ENTRY FOR THE LID UPDATE   *
*  FUNCTION.                                *
*********************************************
ARE      DC    AL2(ARELNTH)       LENGTH OF ARE
         DC    X'80'              ONE AND ONLY ENTRY
         DC    AL1(AREPREP)       REPLACE PACKED FIELD
         DC    CL8'EXPIRE'        NAME OF FIELD TO MODIFY
         DC    X'0000'            RESERVED
         DC    XL2'4'             MUST BE 4 FOR FUNCTION AREPREP
AREPACK  DC    PL4'0'             MOVE CURRENT PACKED DATE HERE
ARELNTH  EQU   *-ARE
WTOMSG   DS    0F
         DC    AL2(WTOMSGL-4)
MCSFLAG  DC    B'1000000000000000'
ERRMSG   DS    CL128             ACALT MESSAGE AREA
DESCFLG  DC    B'1000000000000000'
ROUTFLG  DC    B'0000000000100000'
WTOMSGL  EQU   *-WTOMSG
**********************
*   D S E C T S      *
**********************
AREPREP  EQU   5
         REGISTER
         ACCVT
         LIDREC DSECT=YES
         END  ACFSCAN
