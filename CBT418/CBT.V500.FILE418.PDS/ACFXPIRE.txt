ACFXPIRE CSECT
 TITLE 'ACF2 USER-WRITTEN LOGONID EXPIRATION UTILITY'
**********************************************************************
* NAME       -   ACFXPIRE                                            *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   SCAN THE PRIMARY ACF2 SEQUENTIAL BACKUP DATABASE    *
*                AND EXPIRE SELECTED LOGONIDS NOT USED IN 31 DAYS.   *
*                                                                    *
* INPUT      -   ACF2 PRIMARY SEQUENTIAL BACKUP DATABASE             *
*                                                                    *
* OUTPUT     -   NONE                                                *
*                                                                    *
* SYSTEM     -   MVS/XA 2.1.7.                                       *
*                                                                    *
* DATE       -   04/18/86                                            *
*                                                                    *
* LAST                                                               *
* MODIFIED   -   12/30/87                                            *
*                                                                    *
* MACROS/DSECTS  ACCVT,ACARE,LIDREC,ACFINCVT,MODESET                 *
*                                                                    *
* REGISTERS  -   R0    -  *** USED BY MVS ***                        *
*                R1    -  *** USED BY MVS ***                        *
*                R2    -  USED BY TRANSLATE INSTRUCTIONS             *
*                R3    -  ACF2 CVT ADDRESS                           *
*                R4    -  LIDREC ADDRESS                             *
*                R5    -  TODAY'S YEAR VALUE                         *
*                R6    -  LAST-USED YEAR VALUE                       *
*                R7    -  ADDRESS OF ACALT ERRMSG                    *
*                R8    -  *** UNUSED ***                             *
*                R9    -  *** UNUSED ***                             *
*                R10   -  *** UNUSED ***                             *
*                R11   -  *** UNUSED ***                             *
*                R12   -  *** UNUSED ***                             *
*                R13   -  SYSTEM SAVE AREA ADDRESS                   *
*                R14   -  RETURN TO MVS BRANCH ADDRESS               *
*                R15   -  ACFXPIRE PROGRAM ENTRY POINT ADDRESS       *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    NONE                                               *
*                                                                    *
* ATTRIBUTES -    SUPERVISOR KEY 0 FOR ACTUAL LOGONID EXPIRATION     *
*                 ROUTINE. PROBLEM KEY 8 FOR REMAINDER OF PROGRAM.   *
*                                                                    *
**********************************************************************
        EJECT
**********************************************************************
* PROGRAM FUNCTIONAL DESCRIPTION:                                    *
*                                                                    *
*               1.) ACFXPIRE READS THE ACF2 PRIMARY SEQUENTIAL BACKUP*
*                   LOGONID DATABASE AND COMPARES THE DATE OF LAST   *
*                   SYSTEM ACCESS WITH THE CURRENT DATE.             *
*                                                                    *
*  CHANGED BY                                                        *
*  CR #14758    2.) IF THE DATE-SET FIELD HAS THE CHARACTER '&' THEN *
*                   THIS IS A PROTOTYPE LOGONID WHICH WILL NEVER BE  *
*                   EXPIRED.                                         *
*  CHANGED BY                                                        *
*  CR #13293    3.) IF DATE OF LAST ACCESS (ACC-DATE) = 0 THEN       *
*                                                                    *
*                      DO                                            *
*                          IF SETUP DATE (DATE-SET) IS GREATER THAN  *
*                          31 THEN EXPIRE THE LOGONID                *
*                      END                                           *
*                                                                    *
*  CHANGED BY                                                        *
*  CR #13293    4.) IF DATE OF LAST ACCESS (ACC-DATE) Â¬= 0 THEN      *
*                                                                    *
*                      DO                                            *
*                          IF ACC-DATE IS GREATER THAN 31 THEN       *
*                          EXPIRE THE LOGONID                        *
*                      END                                           *
**********************************************************************
        EJECT
**********************************************************************
*                   IF ANY OF THE FOLLOWING IS TRUE THEN THE         *
*                   LOGONID WILL NEVER BE EXPIRED!                   *
*                                                                    *
*                   - ANY USER PRIVILEGES (NON-CNCL,SECURITY,ETC.)   *
*                                                                    *
*                   - LOGONIDS BEGINNING WITH SS                     *
**********************************************************************
        EJECT
**********************************************************************
*                                                                    *
*  ADDED BY                                                          *
*  CR #20587                                                         *
*                                                                    *
*       THE ACFSVC MACRO WAS CHANGED TO CVT=HAVE TO BUILD INLINE     *
*       CODE INSTEAD OF INVOKING THE $ACFGCVT MODULE EXTERNAL TO     *
*       THIS PROGRAM.                                                *
*                                                                    *
*       THE INFORMATION ASSET PROTECTION GROUP HAS REQUESTED THAT    *
*       THE REPORT BE DELETED FROM THIS PROGRAM SINCE IT IS NO       *
*       LONGER NEEDED.  REDID REGISTER SETTINGS FOR CLARITY.         *
*                                                                    *
*       THIS PROGRAM NOW USED EXTENDED ALTER REQUEST ENTRIES FOR     *
*       FUTURE RELEASE COMPATIBILITY.                                *
*                                                                    *
**********************************************************************
         STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R12,R15                      ENTRY POINT ADDRESS LOAD
         USING ACFXPIRE,R12                 SET ADDRESSABILITY
         LR    R3,R13                       SAVE SYSTEM ADDRESS
         ST    R13,SAVEAREA+4               SYSTEM ADDR IN MY SAVEAREA
         LA    R13,SAVEAREA                 ADDRESS OF MY SAVEAREA
         ST    R13,8(R3)                    MY SAVEAREA PUT IN SYSTEM
*********************************************
*   FIRST CHECK TO SEE IF ACF2 IS ALIVE AND *
*   AND WELL!                               *
*********************************************
         ACFINCVT R3,NONE=NOACF2   IF NO ACF2 BRANCH TO NOACF2 LABEL
         USING ACCVT,R3
*********************************************
*   PERFORM EDIT CHECKING TO SEE IF WE ARE  *
*   GOING TO EXPIRE ANY LOGONIDS.           *
*********************************************
         OPEN  (LIDS,(INPUT))     OPEN ACF2 PRIMARY SEQUENTIAL DSN
         TM    LIDS+48,GOODOPEN   WAS OPEN SUCCESSFULL???
         BZ    LIDSNO             NO, ISSUE MSG AND END
READID   GET   LIDS               R1 ADDRESS HAS INPUT AREA
         LR    R4,R1              SAVE IT IN R4
         LA    R4,4(R4)           BUMP PAST RDW
         USING LIDREC,R4          SET ADDRESSABILITY TO IT
         ICM   R1,15,LIDEXPDT     LOAD EXPIRE FIELD
         O     R1,MAKEPACK        MAKE SURE IT IS PACKED
         ST    R1,LIDEXPDT        PUT BACK IN FIELD
         CP    LIDEXPDT,PZERO4    LOGONID IN EXPIRE STATE?
         BNE   READID              NO, READ NEXT LOGONID
*********************************************
*   IF THE DATE-SET LOGONID FIELD HAS THE   *
*   CHARACTER '&' THEN IT A PROTOTYPE       *
*   LOGONID WHICH WILL NEVER EXPIRE.        *
*********************************************
         CLC   LIDIAPS,ANDSGN     IS IT AN AMPERSAND?
         BE    READID             YES, BYPASS PROCESSING IT
         CLC   LIDLID(2),SSPGMR   IS LOGONID A SYSTEMS SUPPORT PGMR
         BE    READID             YES, BYPASS PROCESSING IT
         CLI   LIDTFLAG,LIDTUSR   IS THIS A NORMAL USER?
         BE    SPECATTR           YES, ANY SPECIAL ATTRIBUTES?
         B     READID             SECURITY,AUDIT,CONSULT,PROJ LEADER
SPECATTR TM    LIDMFLG,LIDMRST+LIDMNCNL+LIDMRSTA+LIDMSTC ATTRIBUTES?
         BNZ   READID             IF SO BYPASS PROCESSING IT
*********************************************
*   DETERMINE IF THE LOGONID HAS NOT BEEN   *
*   USED FOR 31 DAYS.                       *
*                                           *
*   CHECK THE DATE OF LAST ACCESS           *
*   (ACC-DATE).                             *
*                                           *
*   IF IT IS 0 THEN USE THE DATE-SET IAP    *
*   FIELD TO CALCULATE DATE LAST USED.      *
*                                           *
*   IF IT IS NOT 0 THEN USE THE ACC-DATE    *
*   DATE OF LAST ACCESS FIELD TO CALCULATE  *
*   LAST DATE USED.                         *
*********************************************
         ICM   R1,15,LIDADATE           LOAD ACC-DATE FIELD
         O     R1,MAKEPACK              MAKE SURE IT IS PACKED
         ST    R1,LIDADATE              PUT BACK IN FIELD
         CP    LIDADATE,PZERO4          ACC-DATE IS 0?
         BNE   NOT0
         ICM   R1,15,LIDSETP            LOAD DATE-SET FIELD
         O     R1,MAKEPACK              MAKE SURE IT IS PACKED
         ST    R1,LIDSETP               PUT BACK IN FIELD
         CP    LIDSETP,PZERO4           IS DATE-SET 0 - SHOULDNT BE
         BNE   SETDATE                     IF NOT, SET WORK FIELD
         MVC   LIDSETP,MAY1986          SET MAY 1ST,1986 AS DEFAULT
SETDATE  ZAP   PACKDATE,LIDSETP         USE DATE-SET FOR CALCULATION
         B     FIGURE31                 SEE IF USED WITHIN 31 DAYS
*
NOT0     ZAP   PACKDATE,LIDADATE        USE ACC-DATE FOR CALCULATION
*
FIGURE31 TIME  DEC                      GET TIME (R0) AND DATE (R1)
         ST    R1,DATENOW               SAVE TODAY'S DATE FOR LATER
         LA    R1,DATENOW               ADDRESS OF TODAY'S DATE
         SLR   R5,R5                    ZERO OUT
         ICM   R5,12,0(R1)              GET TODAY'S YEAR VALUE
         SRL   R5,16                    SHIFT TO LOW-ORDER POSITIONS
         LA    R1,PACKDATE              ADDRESS OF LAST-USED DATE
         SLR   R6,R6                    ZERO OUT
         ICM   R6,12,0(R1)              GET LAST-USED YEAR VALUE
         SRL   R6,16                    SHIFT TO LOW-ORDER POSITIONS
         CR    R5,R6                    ARE THEY EQUAL?
         BE    SHORTSUB                 WE CAN DO SIMPLE SUBTRACT
         SLR   R5,R5                    ZERO OUT
         TM    PACKDATE+1,X'01'         IS IT AN ODD YEAR?
         BO    NOLEAPYR                   YES, CAN NOT BE LEAP YEAR
         ZAP   WORKP3,DAY366            366 DAYS TO A LEAP YEAR
         B     SUBPACK                  GO DO SUBTRACT
NOLEAPYR ZAP   WORKP3,DAY365            365 DAYS TO A LEAP YEAR
SUBPACK  SP    WORKP3,PACKDATE+2(2)     HOW MANY DAYS LEFT LAST YEAR
         AP    WORKP3,DATENOW+2(2)      HOW MANY DAYS USED THIS YEAR
         ZAP   DAYNO,WORKP3             PUT BACK IN DAYNO FIELD
         B     COMPPACK                 GO DO COMPARE
SHORTSUB ZAP   DAYNO,DATENOW+2(2)       NO. OF DAYS SINCE LAST ACCESS
         SP    DAYNO,PACKDATE+2(2)      NO. OF DAYS SINCE LAST ACCESS
COMPPACK CP    DAYNO,P31                LOGONID NOT USED 31 DAYS
         BL    READID                    NO, READ NEXT ID
*********************************************
*   WE HAVE FOUND A LOGONID NOT USED FOR 31 *
*   DAYS.  ISSUE INTERNAL ACF2 REQUEST FOR  *
*   LOGONID EXPIRATION.                     *
*********************************************
         MVI   ACAFCN,2                 SET LID UPDATE FUNCTION
         MVI   ACASFCN,ACASALT          LOGONID TO EXPIRED
         LA    R1,ERRMSG                LOAD MESSAGE ADDRESS
         ST    R1,ACAMSG                SET IN ACF2 CONTROL BLOCK
         MVC   ACALID,LIDLID            SET LOGONID TO BE EXPIRED
         MVC   ACAUCB,WORDFFFF          SET FOR SUPERCALL
         LA    R1,EXPIRE                ADDRESS OF OUR XARE
         ST    R1,ACAAREB               SET IN ACF2 CONTROL BLOCK
         ZAP   EXPIREV,DATENOW          SET DATE TO EXPIRE
         LA    R1,EXPIREL               SET XARE BUFFER LENGTH
         ST    R1,ACAAREL               SET IN ACF2 CONTROL BLOCK
         MODESET MODE=SUP,KEY=ZERO      SUPERVISOR STATE KEY 0
         ACFSVC ACALT,TYPE=A,NONE=ENDLIDS,CVT=HAVE    EXPIRE LOGONID
         LR    R2,R15                   SAVE RETURN CODE
         MODESET MODE=PROB,KEY=NZERO    PROBLEM STATE KEY 8
         LTR   R2,R2                    WAS RETURN CODE GOOD?
         BZ    READID                   READ NEXT LOGONID
         LA    R7,WTOMSG+4              SET ACF2 BUILD WTO
         WTO   MF=(E,(R7))              ISSUE ACF2 ERROR MESSAGE
*********************************************
*   AFTER ALL LOGONID PROCESSING, ISSUE     *
*   THE 'F ACF2,BACKUP' OPERATOR COMMAND    *
*   TO UPDATE THE BACKUP SEQUENTIAL         *
*   DATABASE.                               *
*********************************************
ENDLIDS  CLOSE (LIDS)                   CLOSE SEQUENTIAL DATABASE
         WTO   'ACFXPIRE:  ACF2 BACKUP INITIATED',ROUTCDE=1,DESC=2
         MODESET MODE=SUP,KEY=ZERO      SUPERVISOR STATE KEY 0
         LA    R1,OPERCMD               ADDRESS OF MESSAGE
         SR    R0,R0                    ZERO REGISTER 0
         SVC   34                       ISSUE OPERATOR COMMAND
         MODESET MODE=PROB,KEY=NZERO    PROBLEM STATE KEY 8
*********************************************
*   PERFORM EXIT LOGIC.                     *
*********************************************
FINIS    L     R13,SAVEAREA+4           SYSTEM SAVE AREA ADDRESS
         SR    R15,R15                  SET GOOD RETURN CODE
         LM    R0,R12,20(R13)           RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)              RESTORE RETURN ADDRESS
         BR    R14                      RETURN TO CALLER
*********************************************
*   ERROR ROUTINES.                         *
*********************************************
NOACF2   WTO  'ACFXPIRE: ACF2 IS NOT ACTIVE',                          +
               ROUTCDE=1,DESC=2
         B     FINIS
LIDSNO   WTO  'ACFXPIRE: LIDS INPUT FILE OPEN FAILED',                 +
               ROUTCDE=1,DESC=2
         B     FINIS
*********************************************
*  C O N S T A N T S  AND  E Q U A T E S    *
*********************************************
SAVEAREA DS    18F
ANDSGN   DC    X'50'          DATE-SET NEVER EXPIRE INDICATOR
PACKDATE DS    PL4            DATE USED FOR CALCULATIONS (CR #13293)
PZERO2   DC    PL2'0'         PACKED VALUE OF 0 LENGTH OF 2
PZERO4   DC    PL4'0'         PACKED VALUE OF 0 LENGTH OF 4
P31      DC    PL4'31'        NO. OF DAYS LOGONID NOT USED
ON       EQU   X'FF'          AT LEAST ONE LOGONID EXPIRED INDICATOR
DAYNO    DC    PL4'0'         FULL BYTE WORK AREA
WORKF    DC    F'0'           FULL BYTE WORK AREA
DATENOW  DC    F'0'           FULL BYTE WORK AREA
WORKP3   DC    PL3'0'
DAY365   DC    PL3'365'
DAY366   DC    PL3'366'
         DS    0P
MAY1986  DC    XL4'0086121F'  MAY 1ST,1986
SSPGMR   DC    C'SS'          SYSTEMS SUPPORT PROGRAMMER
LIDS     DCB   DDNAME=LIDS,DSORG=PS,MACRF=GL,EODAD=ENDLIDS
WORDFFFF DC    XL4'FFFFFFFF'  INDICATE SUPERCALL FUNCTION
MAKEPACK DC    F'15'          FORCE FIELD TO PACKED FORMAT
OPERCMD  DC    0D'0'
         DC    AL2(L'BKPCMD+4)           4 BYTES PLUS COMMAND LENGTH
         DC    XL2'00'                   FILLER
BKPCMD   DC    C'F ACF2,BACKUP'          OPERATOR COMMAND
         ACALT DSECT=NO       PARM LIST FOR LID UPDATE FUNCTION
*********************************************
*                                           *
*  EXTENDED ALTER REQUEST ENTRY FOR:        *
*                                           *
*  EXPIRE        ACF2 EXPIRED LOGONID       *
*                DATE.                      *
*                                           *
*********************************************
         DS    0H                 ROUND UP TO HALFWORD BOUNDARY
EXPIRE   DC    AL2(EXPIREL)       LENGTH OF ALTER REQUEST ENTRY
         DC    X'80'              INDICATES LAST ALTER REQUEST ENTRY
         DC    AL1(AREPREP)       REPLACE PACKED FIELD
         DC    CL8'EXPIRE'        NAME OF FIELD TO MODIFY
         DC    X'1000'            INDICATES AN EXTENDED ARE
         DC    AL2(EXPIREF-EXPIRE) LENGTH OF FIXED SECTION
         DC    AL2(EXPIREF-EXPIRE) OFFSET TO VALUE SECTION
EXPIREF  DC    XL2'4'             MUST BE 4 FOR AREPREP
EXPIREV  DS    XL4                MOVE PACKED VALUE HERE
EXPIREL  EQU   *-EXPIRE           TOTAL LENGTH OF EXPIRE
*********************************************
*  EXPANSION OF WRITE-TO-OPERATOR (WTO)     *
*  MESSAGE FOR ACALT MESSAGE AREA           *
*********************************************
WTOMSG   DS    0F
         DC    AL2(WTOMSGL-4)
MCSFLAG  DC    B'1000000000000000'
ERRMSG   DS    CL128              ACALT MESSAGE AREA
DESCFLG  DC    B'1000000000000000'
ROUTFLG  DC    B'0000000000100000'
WTOMSGL  EQU   *-WTOMSG
**********************
*   D S E C T S      *
**********************
AREPREP  EQU   5                  INDICATES REPLACE PACKED FIELD
GOODOPEN EQU   X'10'          GOOD OPEN INDICATOR
         REGISTER
         ACCVT
         LIDREC DSECT=YES
         END  ACFXPIRE
