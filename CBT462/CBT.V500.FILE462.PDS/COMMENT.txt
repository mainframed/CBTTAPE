         ISREDIT MACRO (TYPE) NOPROCESS

/*********************************************************************/
/*   BOB ZIMMERMAN                                                   */
/*   CNA INSURANCE                                                   */
/*   (312) 822-5719                                                  */
/*   LAST MODIFIED 8503                                              */
/*********************************************************************/
/*       SETUP COMMENT ENVIRONMENT -
/*           * THIS INCLUDES SETTING BOUNDS IN BY 5 COLS ON EACH SIDE
/*           * WRITING A LINES OF A BOX FOR THE APPROPRIATE TYPE
/*           * ENTERING TF LINE MODE FOR THE USER
/*********************************************************************/

         ISPEXEC CONTROL ERRORS CANCEL

        IF &STR(&TYPE) = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#COMMENT)
            EXIT
            END

SETOFF:  IF &STR(&TYPE) = OFF THEN +
             DO
             ISREDIT (LEFT,RIGHT) = BOUNDS
             SET &LEFT = &LEFT - 5
             SET &RIGHT = &RIGHT +5

             IF &LEFT < 0 OR &RIGHT < 0 THEN +
                 DO
                 SET ZEDSMSG = &STR(CURRENT BOUNDS INVALID)
                 SET ZEDLMSG = &STR(UNABLE TO RESET BOUNDS FROM +
                 CURRENT VALUE)
                 ISPEXEC SETMSG MSG(ISRZ001)
                 EXIT CODE(12)
                 END
             ISREDIT BOUNDS &LEFT &RIGHT
             ISREDIT MASKLINE = ' '
             EXIT CODE(0)
             END

         ELSE +
             DO
             ISREDIT (LEFT,RIGHT) = BOUNDS
             SET &LEFT = &LEFT + 5
             SET &RIGHT = &RIGHT -5
             END

         ISREDIT PROCESS DEST
         IF &LASTCC Â¬= 0 THEN +
             DO
             SET ZEDSMSG = &STR(COMMENT COMMAND PENDING)
             SET ZEDLMSG = &STR(ENTER AN 'A' OR 'B' LINE COMMAND WITH +
             THE 'COMMENT' PRIMARY COMMAND)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         IF &STR(&TYPE) = THEN +
             DO
             ISREDIT (TYPE) = PROFILE
             END

         IF &STR(&TYPE) = PANEL THEN SET TYPE = CLIST
         IF &STR(&TYPE) = PLIB THEN SET TYPE = CLIST
         IF &STR(&TYPE) = ISPPLIB THEN SET TYPE = CLIST
         IF &STR(&TYPE) = ASMB THEN SET TYPE = ASM
         IF &STR(&TYPE) = BAL THEN SET TYPE = ASM
         IF &STR(&TYPE) = CNTL THEN SET TYPE = JCL

         IF &STR(&TYPE) = CLIST THEN +
             DO
             IF &RIGHT > 69 THEN SET RIGHT = 69
             ISREDIT MASKLINE = <1 '/*' 70 '*/'>
             SET &LINE =        &STR(/*****************************-
****************************************/         )
             GOTO FLOWIT
             END

         IF &STR(&TYPE) = ASM THEN +
             DO
             IF &RIGHT > 70 THEN SET RIGHT = 70
             ISREDIT MASKLINE = <1 '*' 71 '*'>
             SET &LINE =        &STR(******************************-
*****************************************         )
             GOTO FLOWIT
             END

         IF &STR(&TYPE) = COBOL THEN +
             DO
             IF &RIGHT > 70 THEN SET RIGHT = 70
             ISREDIT MASKLINE = <7 '*' 71 '*'>
             SET &LINE =        &STR(      ************************-
*****************************************         )
             GOTO FLOWIT
             END

         IF &STR(&TYPE) = JCL THEN +
             DO
             IF &RIGHT > 70 THEN SET RIGHT = 70
             ISREDIT MASKLINE = <1 '//*' 71 '*'>
             SET &LINE =        &STR(//* * * * * * * * * * * * * * -
* * * * * * * * * * * * * * * * * * * * *         )
             GOTO FLOWIT
             END

         SET ZEDSMSG = &STR(INVALID COMMENT TYPE)
         SET ZEDLMSG = &STR(VALID TYPES ARE CLIST, PANEL, +
         COBOL, JCL AND ASM)
         ISPEXEC SETMSG MSG(ISRZ001)
         EXIT CODE(12)

FLOWIT:  +
         ISREDIT (DEST) = LINENUM .ZDEST
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         ISREDIT TENTER &EVAL(&DEST + 1)
         ISREDIT BOUNDS &LEFT &RIGHT
         ISREDIT LOCATE &DEST

         EXIT CODE(0)
