/* MACRO: COMEXEC                                                     */
/*                                                                    */
/* FUNCTION: Adds a comment block for a JCL data set in front of each */
/*  EXEC statement.                                                   */
/*                                                                    */
/*  WRITTEN BY MICHAEL BOTOS  --  07/24/85                            */
/*                                                                    */
/*                                                                    */
  ISREDIT MACRO (PARM)

/* ----------------->   ITEM DOCUMENTATION    <--------------------- */
/* |                                                               | */
/* | SYNTAX HELP                                                   | */
/* |                                                               | */
/* ----------------------------------------------------------------- */

  ISPEXEC CONTROL ERRORS RETURN

  IF &STR(&PARM.) = &STR(HELP) THEN +
    DO
       ISPEXEC DISPLAY PANEL(#COMEXEC)
     EXIT CODE(0)
    END

   ISREDIT (ENVIR) = USER_STATE
   ISREDIT UP MAX

   ISREDIT SEEK C' EXEC ' ALL NX
   ISREDIT (EXECS) = SEEK_COUNTS

   SET &EXECE = &EVAL(&EXECS)

   SET &LINEC = &STR(//*)
   SET &LINEB = &STR(//* -------------------------- COMMENT ------------
   SET &LINED = &STR(//* -----------------------------------------------
   SET &LINE  = &STR(//* |
   SET &LINEA = &STR(//* |) &STR( BY:&SYSUID.  ON:&SYSDATE.  AT:&SYSTIME
   SET &LINEZ = &SUBSTR(1:71,&LINEA.)&STR(|)

   DO WHILE &EXECS > 0
     ISREDIT SEEK C' EXEC ' NEXT NX
     ISREDIT (LIN COL) = CURSOR
     ISREDIT LINE_BEFORE &EVAL(&LIN) = DATALINE '&LINEC.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 1) = DATALINE '&LINEB.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 2) = DATALINE '&LINE.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 3) = DATALINE '&LINE.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 4) = DATALINE '&LINE.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 5) = DATALINE '&LINE.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 6) = DATALINE '&LINE.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 7) = DATALINE '&LINEZ.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 8) = DATALINE '&LINED.'
     ISREDIT LINE_BEFORE &EVAL(&LIN + 9) = DATALINE '&LINEC.'
     SET &EXECS = &EXECS - 1
     SET &LIN = &LIN + 11
     ISREDIT CURSOR = &LIN 1
   END

   ISREDIT USER_STATE = (ENVIR)

   SET ZEDSMSG = &STR(&EXECE COMMENTS ADDED)
   SET ZEDLMSG = &STR(&EXECE COMMENT BLOCKS HAVE BEEN ADDED)
   ISPEXEC SETMSG MSG(ISRZ001)

EXIT CODE(1)
