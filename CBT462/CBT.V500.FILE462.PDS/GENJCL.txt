         ISREDIT MACRO (DSN TSO PARMS) NOPROCESS

/*********************************************************************/
/*   BOB ZIMMERMAN                                                   */
/*   CNA INSURANCE                                                   */
/*   (312) 822-5719                                                  */
/*   LAST MODIFIED 8505                                              */
/*********************************************************************/

         ISPEXEC CONTROL ERRORS CANCEL

        IF &STR(&PARMS) = AND &TSO = AND &DSN = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#GENJCL)
            EXIT
            END


         IF &STR(&PARMS) ¬= THEN +
             DO
             SET ZEDSMSG = &STR(INVALID PARAMETERS)
             SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
             SYNTAX ONLY)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         IF &STR(&TSO) = OR &STR(&TSO) = TSO THEN
         ELSE +
             DO
             SET ZEDSMSG = &STR(INVALID PARAMETER)
             SET ZEDLMSG = &STR(THE ONLY VALID PARAMETER FOR THIS +
             COMMAND IS "TSO")
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         ISREDIT PROCESS DEST
         IF &LASTCC ¬= 0 THEN +
             DO
             SET ZEDSMSG = &STR(GENJCL COMMAND PENDING)
             SET ZEDLMSG = &STR(ENTER AN 'A' OR 'B' LINE COMMAND WITH +
             THE 'GENJCL' PRIMARY COMMAND)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         IF &STR(&DSN) = THEN +
             DO
             SET ZEDSMSG = &STR(SPECIY A DATASET)
             SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
             SYNTAX)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

        SET DSNINFO = &STR(&DSN)
        ISPEXEC CONTROL ERRORS RETURN
        ISPEXEC LMINIT DATAID(ID) DATASET(&DSNINFO)
        SET LCC = &LASTCC
        ISPEXEC CONTROL ERRORS CANCEL
        IF &LCC ¬= 0 THEN DO
            ISPEXEC SETMSG MSG(&ZERRMSG)
            EXIT CODE(12)
            END

        ISPEXEC VPUT (DSNINFO) SHARED
        ISPEXEC CONTROL ERRORS RETURN
        ISPEXEC SELECT PGM(DSN0INFO) PARM(DSNINFO)
        SET LCC = &LASTCC
        ISPEXEC CONTROL ERRORS CANCEL
        ISPEXEC LMFREE DATAID(&ID)
        IF &LCC ¬= 0 THEN DO
             SET ZEDSMSG = &STR(DATASET NOT FOUND)
             SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
             SYNTAX)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END
        ISPEXEC VGET (DEVTYPE VOLSER LRECL BLKSIZE RECFM) SHARED
        ISPEXEC VGET (DSORG DIRALLOC SCALO TRALC ATRECFM) SHARED

        IF &SUBSTR(1:1,&DSN) ¬= &STR(') THEN DO
            SET DSN = &STR(&SYSPREF..&DSN)
            END
        ELSE DO
             SET &DSN = &SUBSTR(2:&LENGTH(&DSN)-1,&DSN)
             END

         IF &STR(&TSO) = TSO THEN GOTO FLOWTSO

FLOWIT:  +
         IF &STR(&DIRALLOC) ¬= THEN SET DIR = &STR(,&DIRALLOC)
         ELSE SET DIR =
         ISREDIT (DEST) = LINENUM .ZDEST
         SET LINE = &STR(//GENJCL  DD  DSN=&DSN,)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(//            DISP=(NEW,CATLG),)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(//            UNIT=&DEVTYPE,)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(//            VOL=SER=&VOLSER,)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(//            DCB=(LRECL=&LRECL,+
         BLKSIZE=&BLKSIZE,RECFM=&RECFM,DSORG=&DSORG),)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(//            SPACE=(TRK,(&TRALC,+
         &SCALO.&DIR)))
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'

         EXIT CODE(0)

FLOWTSO: +
         IF &DIRALLOC = THEN SET DIR =
         ELSE SET DIR = &STR(DIR(&DIRALLOC))
         ISREDIT (DEST) = LINENUM .ZDEST
         SET LINE = &STR(ALLOC DA('&DSN') NEW CATALOG +)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(    UNIT(&DEVTYPE) VOL(&VOLSER) +)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(    LRECL(&LRECL) BLKSIZE(&BLKSIZE) +)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(    RECFM(&ATRECFM) &DIR +
         DSORG(&DSORG) +)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
         SET DEST = &DEST + 1
         SET LINE = &STR(    SPACE(&TRALC &SCALO) TRACK)
         ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'

         EXIT CODE(0)
