/* CENTER MACRO - COMMAND ===> CE   - LINE COMMANDS S<N> AND SS
   ISREDIT MACRO (TYPE) NOPROCESS
        IF &STR(&TYPE) = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#CENTER)
            EXIT
            END

   ISPEXEC CONTROL ERRORS RETURN
   CONTROL ASIS
   ISREDIT PROCESS RANGE S
   SET LCC = &LASTCC
   ISREDIT (RC) = RANGE_CMD
   IF &LASTCC = 4 | &STR(&RC) ¬= &STR(S) THEN DO
      SET &ZEDSMSG = NO RANGE SPECIFIED
      SET &ZEDLMSG = YOU MUST SPECIFY A RANGE OF LINES TO BE CENTERED +
         USING A "S<N>" OR "SS"
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
   END
   ISREDIT (FR) = LINENUM .ZFRANGE
   ISREDIT (LR) = LINENUM .ZLRANGE
   ISREDIT (LB,RB) = BOUNDS
   SET BL = &RB - &LB + 1
   SET I = &FR
   DO WHILE( &I <= &LR )
      ISREDIT (LN) = LINE &I
      SET L = &LENGTH(&SUBSTR(&LB:&RB,&NRSTR(&LN)))
      SET S = &LB
      DO WHILE(&S < &L)
         IF &SUBSTR(&S:&S,&NRSTR(&LN)) ¬= &STR( ) THEN GOTO FOUNDS
         SET S = &S + 1
      END
FOUNDS: +
      SET E = &L
      DO WHILE(&E > &S)
         IF &SUBSTR(&E:&E,&NRSTR(&LN)) ¬= &STR( ) THEN GOTO FOUNDE
         SET E = &E - 1
      END
FOUNDE: +
      SET LINE = &SUBSTR(&S:&E,&NRSTR(&LN))
      SET L = &LENGTH(&NRSTR(&LINE))
      SET D = &BL - &L
      IF &D > 1 THEN DO
         SET H = &D / 2
         SET LINE2 = &SUBSTR(1:&H,&STR(                                -
                                                                       )
         IF &LB = 1 THEN +
            SET LINE = &NRSTR(&LINE2.&LINE)
         ELSE +
            SET LINE = &SUBSTR(1:&LB-1,&NRSTR(&LN))&NRSTR(&LINE2.&LINE)
      END
      ISREDIT LINE &I = (LINE)
      SET I = &I + 1
   END
EXIT CODE(0)
