/**********************************************************************
/*       FIND ONLY THE SPECIFIED STRING -
/*       EXCLUDE ALL OTHER LINES
/**********************************************************************
         ISREDIT MACRO (PARMS)
/******************************************************************
/*     DETERMINE IF WE ARE RUNNING IN DEBUG MODE                  *
/*     AND SET CONTROL ACCORDINGLY                                *
/******************************************************************
         CONTROL MSG FLUSH NOPROMPT
         ISPEXEC  CONTROL  ERRORS CANCEL
         ISPEXEC VGET (SPFDBUG) PROFILE
         IF &LASTCC Â¬= 0 THEN SET &SPFDBUG = N
         IF &SPFDBUG = Y THEN CONTROL LIST MSG SYMLIST CONLIST
         ELSE CONTROL NOLIST NOMSG NOSYMLIST NOCONLIST
/******************************************************************
/*               MAIN CLIST LOGIC                                 *
/******************************************************************
         CONTROL NOCAPS
        IF &STR(&PARMS) = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#ONLY)
            EXIT
            END

         ISREDIT (STAT,VALUE) = NUMBER
         ISPEXEC CONTROL ERRORS RETURN
         ISREDIT UNNUM
         ISREDIT FIND &NRSTR(&PARMS) ALL
         SET SCC = &LASTCC
         IF &SCC > 11 THEN +
            DO
            ISREDIT NUMBER = (STAT,VALUE)
            ISPEXEC SETMSG MSG(&ZERRMSG)
            EXIT CODE(&SCC)
            END
         ISREDIT EXCLUDE ALL
         ISREDIT FIND &NRSTR(&PARMS) ALL
         SET SCC = &LASTCC
         IF &SCC > 11 THEN +
            DO
            ISPEXEC SETMSG MSG(&ZERRMSG)
            END
         ELSE DO
             ISREDIT (STRING LINES) = FIND_COUNTS
             SET STRING = &EVAL(&STRING)
             SET LINES = &EVAL(&LINES)
             SET ZEDSMSG = &STR(&STRING STRINGS ON &LINES LINES)
             SET ZEDLMSG = &STR(&PARMS FOUND &STRING TIMES ON &LINES +
             LINES)
             ISPEXEC SETMSG MSG(ISRZ000)
             END
         ISREDIT NUMBER = (STAT,VALUE)
         EXIT CODE(&SCC)
