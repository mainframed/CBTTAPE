         ISREDIT MACRO (TSODSN JCLDSN REST) NOPROCESS

         ISPEXEC CONTROL ERRORS CANCEL

        IF &STR(&REST) = AND &TSODSN = HELP AND &JCLDSN = THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#TSOTOJC)
            EXIT
            END

         IF &STR(&REST) ¬= OR &TSODSN = OR &JCLDSN = THEN DO
             SET ZEDSMSG = &STR(INVALID PARMS)
             SET ZEDLMSG = &STR(SPECIFY A TSO VARIABLE NAME AND A JCL +
             VARIABLE NAME)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         ISREDIT PROCESS DEST
         IF &LASTCC ¬= 0 THEN +
             DO
             SET ZEDSMSG = &STR(TSOJCL COMMAND PENDING)
             SET ZEDLMSG = &STR(ENTER AN 'A' OR 'B' LINE COMMAND WITH +
             THE 'TSOJCL' PRIMARY COMMAND)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(16)
             END


FLOWIT:  +
         ISREDIT (DEST) = LINENUM .ZDEST
         ISREDIT LINE_AFTER &DEST = DATALINE +
             ' IF (&&&&&TSODSN ¬= &&&&Z)'
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '     &&&&TEMP = TRUNC (&&&&&TSODSN,'''')'
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '     &&&&TRAIL = .TRAIL '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '     IF (&&&&TRAIL = &&&&Z) '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '         &&&&&JCLDSN = '&&&&ZPREFIX..&&&&&TSODSN''
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '     IF (&&&&TRAIL ¬= &&&&Z) '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '         &&&&&JCLDSN = TRUNC (&&&&TRAIL,'''') '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             ' IF (&&&&&TSODSN = &&&&Z) '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE +
             '     &&&&&JCLDSN = &&&&Z '
         SET DEST = &DEST + 1
         ISREDIT LINE_AFTER &DEST = DATALINE '     '
         EXIT CODE(0)
