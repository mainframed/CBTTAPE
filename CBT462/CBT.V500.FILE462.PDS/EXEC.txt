         ISREDIT MACRO (TYPE)
/******************************************************************
/*     DETERMINE IF WE ARE RUNNING IN DEBUG MODE                  *
/*     AND SET CONTROL ACCORDINGLY                                *
/******************************************************************
         CONTROL MSG FLUSH NOPROMPT
         ISPEXEC  CONTROL  ERRORS CANCEL
         ISPEXEC VGET (SPFDBUG) PROFILE
         IF &LASTCC ¬= 0 THEN SET &SPFDBUG = N
         IF &SPFDBUG = Y THEN CONTROL LIST MSG SYMLIST CONLIST
         ELSE CONTROL NOLIST NOMSG NOSYMLIST NOCONLIST
/******************************************************************
/*               MAIN CLIST LOGIC                                 *
/******************************************************************
        IF &STR(&TYPE) = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#EXEC)
            EXIT
            END

         IF &STR(&TYPE) = SAVE THEN ISREDIT SAVE
         IF &STR(&TYPE) = SAVE THEN SET TYPE =

         IF &STR(&TYPE) ¬= THEN DO
             SET ZEDSMSG = &STR(INVALID PARAMETER)
             SET ZEDLMSG = &STR(THE ONLY ALLOWABLE PARAMETER +
             FOR THIS COMMAND IS SAVE)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

         ISREDIT (FIRST,LAST) = DISPLAY_LINES

         ISREDIT (STATUS) = DATA_CHANGED
         IF &STATUS = NO THEN DO
             ISREDIT (EXDSN) = DATASET
             ISREDIT (EXMEM) = MEMBER
             IF &EXMEM = THEN SET EXDSN = &STR('&EXDSN.')
             ELSE SET EXDSN = &STR('&EXDSN.(&EXMEM.)')
             GOTO PROCESS
             END

         ISPEXEC SELECT PGM(GET0TABL) PARM(TABL)
         ISPEXEC VGET (TABL) SHARED

         SET EXDSN = &STR(&TABL..CLIST)
         ALLOC DA(&EXDSN) FILE(&TABL) NEW +
             SPACE(45 45) TRACK RELEASE   +
             LRECL(255) BLKSIZE(6233) RECFM(V B) DSORG(PS)

         ISPEXEC LMINIT DATAID(CLIST) DDNAME(&TABL) ENQ(EXCLU)
         IF &LASTCC = 8 THEN DO
             ISPEXEC SETMSG MSG(&ZERRMSG)
             EXIT CODE(12)
             END

         ISPEXEC LMOPEN DATAID(&CLIST) OPTION(OUTPUT)
         IF &LASTCC ¬= 0 THEN DO
             ISPEXEC SETMSG MSG(&ZERRMSG)
             EXIT CODE(12)
             END

         SET LINE = 1
         ISREDIT (RECFM) = RECFM
         ISREDIT (LASTL) = LINENUM .ZLAST

LOOP:    +
         IF &LASTL < &LINE THEN GOTO ENDLOOP
         ISREDIT (RECORD) = LINE &LINE
         IF &LASTCC ¬= 0 THEN DO
             ISPEXEC SETMSG MSG(&ZERRMSG)
             EXIT CODE(12)
             END

         SET LEN = &LENGTH(&NRSTR(&RECORD))
         ISPEXEC LMPUT DATAID(&CLIST) +
            MODE(INVAR) DATALOC(RECORD) DATALEN(&LEN)

         SET LINE = &LINE + 1
         GOTO LOOP

ENDLOOP: +
         ISPEXEC LMCLOSE DATAID(&CLIST)

PROCESS: +
         ISREDIT (FULLDSN) = DATASET
         ISREDIT (FULLMEM) = MEMBER
         IF &FULLMEM = THEN SET FULLDSN = &STR('&FULLDSN.')
         ELSE SET FULLDSN = &STR('&FULLDSN.(&FULLMEM.)')

         ISPEXEC DISPLAY PANEL(#EXEC2)
         IF &LASTCC ¬= 0 THEN DO
             IF &STATUS = YES THEN DO
                 ISPEXEC LMFREE DATAID(&CLIST)
                 FREE F(&TABL) DELETE
                 END
             EXIT CODE(0)
             END

         ISPEXEC CONTROL ERRORS RETURN
         ISPEXEC SELECT CMD(EX &EXDSN &EXECPARM)
         SET LCC = &LASTCC
         ISPEXEC CONTROL ERRORS CANCEL
         IF &STATUS = YES THEN DO
             ISPEXEC LMFREE DATAID(&CLIST)
             FREE F(&TABL) DELETE
             END
         IF &LCC ¬= 0 THEN DO
             ISPEXEC CONTROL ERRORS RETURN
             ISPEXEC SETMSG MSG(&ZERRMSG)
             EXIT CODE(12)
             END
         EXIT CODE(0)
