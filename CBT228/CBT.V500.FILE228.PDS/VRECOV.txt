         PROC 0 LEVEL(NONE) DSN(NONE) WAIT QUIET DEBUG

         /*-----------------------------------------------------------*/
         /* CLIST VRECOV                                              */
         /*                                                           */
         /* THIS CLIST IS DESIGNED TO BE INVOKED FROM ISPF OPTION 3.4 */
         /* TO RECOVER A DATASET VIA DFHSM.                           */
         /*                                                           */
         /*-----------------------------------------------------------*/

         IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST MSG

         /* GENERATE DSNAME FOR OUTPUT OF HLIST */

         ISPEXEC VGET (ZSCREEN,ZPREFIX)
         SET ODS = SCREEN&ZSCREEN..DFHSM.LIST
INIT:    CONTROL NOMSG NOFLUSH
         DELETE &ODS

         /* INVOKE DFDSS HLIST COMMAND TO GET */
         /* LIST OF BACKED UP DATASETS        */

         IF &LEVEL ¬= NONE +
         THEN DO
           SET OPTS = LEVEL(&LEVEL)
           SET MODE = LEVEL
           END

         ELSE IF &DSN ¬= NONE +
         THEN DO
           SET OPTS = DSN('&DSN')
           SET MODE = DSN
           END

         ELSE DO

           ISPEXEC DISPLAY PANEL(VRECOV0)
           IF &LASTCC > 0 THEN EXIT

           IF &SYSINDEX(&STR(*),&VHSMDD) > 0 THEN DO   /* USE "LEVEL" */

             SET L = &LENGTH(&STR(&VHSMDD))

             IF &L = 1 THEN SET OPTS = LEVEL(&ZPREFIX)

             ELSE IF &SUBSTR(1:1,&VHSMDD) = &STR(') THEN SET OPTS = +
             &STR(LEVEL(&SUBSTR(2:&L-2,&VHSMDD))

             ELSE SET OPTS = LEVEL(&ZPREFIX..&SUBSTR(1:&L-1,&VHSMDD))

             SET MODE = LEVEL

             END                                       /* USE "LEVEL" */

           ELSE DO                                       /* USE "DSN" */

             SET OPTS = DSN(&VHSMDD)
             SET MODE = DSN

             END                                         /* USE "DSN" */

           END

         IF &QUIET ¬= QUIET THEN DO
           SET TECSMSG =
           SET TECLMSG = DFHSM HLIST COMMAND IN PROGRESS
           ISPEXEC CONTROL DISPLAY LOCK
           ISPEXEC DISPLAY MSG(TECZ000)
           END

         HLIST &OPTS ODS(&ODS) BCDS

         IF &QUIET ¬= QUIET THEN DO
           SET TECSMSG =
           SET TECLMSG = OUTPUT FROM HLIST COMMAND NOW BEING SCANNED
           ISPEXEC CONTROL DISPLAY LOCK
           ISPEXEC DISPLAY MSG(TECZ000)
           END

         /* CREATE MAIN ISPF TABLE TO HOLD LIST OF DATASETS */

         ISPEXEC TBCREATE VHSMBACK NOWRITE KEYS(VHSMDSN) +
         NAMES(VHSMNOV VHSMVER VHSMLAST VHSM1ST VHSMTABL VHSMDONE)
         SET VHSMDSN  =
         SET VHSMDONE =
         SET VHSMVER  = 0
         SET VHSMNOV  = 0
         SET VHSMLAST = &STR(00/00/00)
         SET VHSM1ST  = &STR(99/12/31)

         /* ALLOCATE AND OPEN DFHSM OUTPUT DATASET */

         ISPEXEC LMINIT DATAID(LMHSMLST) DATASET(&ODS)
         ISPEXEC LMOPEN DATAID(&LMHSMLST)

         /* READ FIRST RECORD */

         ISPEXEC LMGET  DATAID(&LMHSMLST) DATALOC(X) MODE(INVAR) +
           DATALEN(LX) MAXLEN(133)
         SET GCC = &LASTCC
         SET VHSMNO = 0
         SET READ_MORE = LOOKING

         /* READ UNTIL END OF BCDS SECTION ("----- END OF ") IS FOUND */

         DO WHILE (&GCC = 0 AND &READ_MORE ¬= NO)

           /* LOOK FOR BEGINNING OF BCDS SECTION */

           IF &SUBSTR(31:36,&X) = BACKUP +
           THEN SET READ_MORE = YES

           IF &READ_MORE = YES THEN DO

             IF &SUBSTR(2:9,&X) = &STR(ARC0138I) THEN  DO
                ISPEXEC LMCLOSE DATAID(&LMHSMLST)
                ISPEXEC LMFREE  DATAID(&LMHSMLST)
                SET TECSMSG = NOT BACKED UP
                SET TECLMSG = NO DATASET MATCHING SEARCH CRITERIA IS +
                              BACKED UP IN DFHSM
                ISPEXEC SETMSG MSG(TECZ001)
                ISPEXEC TBEND VHSMBACK
                EXIT
                END

             /* LOOK FOR NEW DATASET */

             IF &SUBSTR(2:10,&X) = &STR(DSNAME = ) THEN DO

               IF &VHSMNO > 0 THEN DO
                 ISPEXEC TBADD VHSMBACK
                 SET VHSMDSN  =
                 SET VHSMVER  = 0
                 SET VHSMNOV  = 0
                 SET VHSMLAST = &STR(00/00/00)
                 SET VHSM1ST  = &STR(99/12/31)
                 END

               SET VHSMNO = &VHSMNO + 1
               SET VHSMDSN = &SUBSTR(11:54,&X)
               SET VHSMTABL = VHSM&VHSMNO
               ISPEXEC TBCREATE &VHSMTABL KEYS(VHSMVER) +
               NAMES(VHSMDSN VHSMVOL VHSMDATE VHSMCAT VHSMGEN VHSMACT)
               END

             /* LOOK FOR DATASET DETAIL LINE */

             ELSE IF &SUBSTR(2:5,&X) ¬= &Z +
             AND &SUBSTR(4:11,&X) ¬= &STR(DFSMSHSM) +
             AND &SUBSTR(2:9,&X) ¬= &STR(BACKUP V) +
             AND &SUBSTR(2:9,&X) ¬= &STR(TOTAL BA) +
             AND &SUBSTR(2:6,&X) ¬= &STR(-----) +
             AND &READ_MORE = YES THEN DO

               /* SET SECONDARY TABLE VARIABLES */

               SET VHSMNOV = &VHSMNOV + 1

               SET VHSMVER = &SUBSTR(88:90,&X)

               SET VHSMVOL = &SUBSTR(54:59,&X)

               SET VHSMDATE = &STR(&SUBSTR(61:68,&X))

               IF &STR(&VHSMDATE) > &STR(&VHSMLAST) +
               THEN SET VHSMLAST = &STR(&VHSMDATE)

               IF &STR(&VHSMDATE) < &STR(&VHSM1ST) +
               THEN SET VHSM1ST  = &STR(&VHSMDATE)

               IF &SUBSTR(79:81,&X) = YES +
               THEN SET VHSMCAT = YES
               ELSE SET VHSMCAT = NO

               SET VHSMGEN = &SUBSTR(83:85,&X)

               SET VHSMACT =

               /* CREATE SECONDARY TABLE ENTRY */

               ISPEXEC TBADD &VHSMTABL

               END

             /* LOOK FOR END OF BCDS SECTION */

             ELSE IF &SUBSTR(2:14,&X) = &STR(----- END OF ) +
             AND &READ_MORE = YES THEN DO
               ISPEXEC TBADD VHSMBACK
               SET READ_MORE = NO
               END

             END

           /* READ NEXT RECORD */

           ISPEXEC LMGET  DATAID(&LMHSMLST) DATALOC(X) MODE(INVAR) +
           DATALEN(LX) MAXLEN(133)
           SET GCC = &LASTCC
           END

         /* ALL RECORDS READ AND TABLES BUILT */
         /* DISCARD DFHSM REPORT DATASET */

         ISPEXEC LMCLOSE DATAID(&LMHSMLST)
         ISPEXEC LMFREE  DATAID(&LMHSMLST)

         IF &VHSMNO = 0 THEN DO
           SET TECSMSG = NOT BACKED UP
           SET TECLMSG = NO DATASET MATCHING SEARCH CRITERIA IS +
                         BACKED UP IN DFHSM
           ISPEXEC SETMSG MSG(TECZ001)
           ISPEXEC TBEND VHSMBACK
           EXIT
           END

         /* DISPLAY MAIN TABLE */

         ISPEXEC TBTOP   VHSMBACK
         SET D1CC = 0
         IF &MODE = LEVEL +
         THEN DO
           ISPEXEC TBDISPL VHSMBACK PANEL(VRECOV1)
           SET D1CC = &LASTCC
           END

         /* CONTINUE DISPLAYING MAIN TABLE UNTIL END OR RETURN KEY */

         DO WHILE (&D1CC < 8)

           IF &ZTDSELS > 0 OR &MODE ¬= LEVEL THEN DO

             IF &HSEL = S +
             OR &MODE ¬= LEVEL THEN DO /* S LINE COMMAND - MAIN TABLE */

               IF &MODE = LEVEL +
               THEN ISPEXEC CONTROL DISPLAY SAVE

               /* DISPLAY SELECTED SECONDARY TABLE */

               ISPEXEC TBTOP &VHSMTABL
               ISPEXEC TBDISPL &VHSMTABL PANEL(VRECOV2)
               SET D2CC = &LASTCC

               /* SPECIFIC DATASET SELECTED FOR ACTION */

               IF &ZTDSELS > 0 THEN DO

                 IF &DSEL = S THEN DO     /* S LINE COMMAND - DS LIST */

                   SET TOVOL =
                   SET NDSN =
                   SET REP = NO

                   ISPEXEC DISPLAY PANEL(VRECOV3)

                   IF &LASTCC = 0 THEN DO

                     SET OPTS =
                     IF &REP = YES THEN SET OPTS = REPLACE

                     IF &VHSMCAT ¬= YES +
                     THEN SET OPTS = &STR(&OPTS FROMVOLUME(&VHSMVOL))

                     IF &NDSN ¬= &Z +
                     THEN SET OPTS = &STR(&OPTS NEWNAME(&NDSN))

                     IF &TOVOL ¬= &VHSMVOL AND &TOVOL ¬= &Z +
                     THEN DO

                       SET &TUNT = &STR(3390)

                       ISPEXEC DISPLAY PANEL(VRECOV3A)

                       SET OPTS = +
                       &STR(&OPTS TOVOLUME(&TOVOL) UNIT(&TUNT))

                     END

                     CONTROL MSG
                     SET D = &VHSMDSN
                     SET G = &VHSMGEN
                     SET SYSOUTTRAP = 1
                     HRECOVER '&D' GENERATION(&G) &OPTS

                     IF &LENGTH(&SYSOUTLINE1) >= 8 THEN DO
                       SET TECSMSG =
                       SET TECLMSG = &SYSOUTLINE1
                       IF &SUBSTR(1:8,&TECLMSG) = ARC1007I +
                       THEN ISPEXEC SETMSG MSG(TECZ000)
                       ELSE ISPEXEC SETMSG MSG(TECZ001)
                       END

                     IF &DEBUG ¬= DEBUG THEN CONTROL NOMSG
                     SET VHSMACT  = &STR(RECALL REQUESTED)
                     ISPEXEC TBMOD &VHSMTABL
                     SET VHSMDONE = &STR(*SEL)
                     ISPEXEC TBMOD VHSMBACK

                     END

                   END                    /* S LINE COMMAND - DS LIST */

                 ELSE IF &DSEL = D THEN DO/* D LINE COMMAND - DS LIST */

                   ISPEXEC DISPLAY PANEL(VRECOV4)

                   IF &LASTCC = 0 THEN DO

                     SET OPTS = VERSIONS(&VHSMVER)

                     IF &VHSMCAT ¬= YES +
                     THEN SET OPTS = &STR(&OPTS FROMVOLUME(&VHSMVOL))

                     CONTROL MSG
                     SET D = &VHSMDSN
                     SET SYSOUTTRAP = 1
                     HBDELETE '&D' &OPTS

                     IF &LENGTH(&SYSOUTLINE1) >= 8 THEN DO
                       SET TECSMSG =
                       SET TECLMSG = &SYSOUTLINE1
                       IF &SUBSTR(1:8,&TECLMSG) = ARC1007I +
                       THEN ISPEXEC SETMSG MSG(TECZ000)
                       ELSE ISPEXEC SETMSG MSG(TECZ001)
                       END

                     IF &DEBUG ¬= DEBUG THEN CONTROL NOMSG
                     SET VHSMACT  = &STR(DELETE REQUESTED)
                     ISPEXEC TBMOD &VHSMTABL
                     SET VHSMDONE = 1DEL
                     ISPEXEC TBMOD VHSMBACK

                     END

                   END                    /* D LINE COMMAND - DS LIST */

                 SET DSEL =

                 END

               IF &MODE = LEVEL +
               THEN ISPEXEC CONTROL DISPLAY RESTORE
               ELSE SET D1CC = 8

               END                     /* S LINE COMMAND - MAIN TABLE */

             ELSE IF &HSEL = D THEN DO /* D LINE COMMAND - MAIN TABLE */

               IF &MODE = LEVEL +
               THEN ISPEXEC CONTROL DISPLAY SAVE

               ISPEXEC DISPLAY PANEL(VRECOV5)

               IF &LASTCC = 0 THEN DO

                 CONTROL MSG
                 SET D = &VHSMDSN
                 SET SYSOUTTRAP = 1
                 HBDELETE '&D'

                 SET TECSMSG =
                 SET TECLMSG = &SYSOUTLINE1
                 IF &SUBSTR(1:8,&TECLMSG) = ARC1007I +
                 THEN ISPEXEC SETMSG MSG(TECZ000)
                 ELSE ISPEXEC SETMSG MSG(TECZ001)

                 IF &DEBUG ¬= DEBUG THEN CONTROL NOMSG
                 SET VHSMDONE = &STR(&DEL)
                 ISPEXEC TBMOD VHSMBACK

                 END

               IF &MODE = LEVEL +
               THEN ISPEXEC CONTROL DISPLAY RESTORE

               END                     /* D LINE COMMAND - MAIN TABLE */

             SET HSEL =

             END

           IF &D1CC = 4 THEN DO
             ISPEXEC TBDISPL VHSMBACK
             SET D1CC = &LASTCC
             END
           ELSE IF &D1CC < 8 THEN DO
             ISPEXEC TBDISPL VHSMBACK PANEL(VRECOV1)
             SET D1CC = &LASTCC
             END

           END

         /* GET TO TOP OF MAIN TABLE, SKIP THROUGH EACH ROW, CLOSING  */
         /* SECONDARY TABLES.                                         */

         ISPEXEC TBTOP VHSMBACK
         ISPEXEC TBSKIP VHSMBACK
         SET SCC = &LASTCC
         DO WHILE (&SCC = 0)
           ISPEXEC TBEND &VHSMTABL
           ISPEXEC TBSKIP VHSMBACK
           SET SCC = &LASTCC
           END

         /* CLOSE MAIN TABLE */

         ISPEXEC TBEND VHSMBACK

         IF &LEVEL = NONE AND &DSN = NONE THEN GOTO INIT

         EXIT
