PROC 0
CONTROL NOLIST MSG PROMPT END(DEND)
ISPEXEC VGET (OROPT,TYP,DSNM,MBR,VOL)
SET &TCMD   = EDIT
SET &M     = /*NULL*/
SET &QUOTE = /*NULL*/
SET &VLM   = /*NULL*/
IF &VOL NE /*NULL*/ THEN -
   SET &VLM = &STR(VOLUME(&VOL))
 LISTDSI &DSNM &VLM
 IF &SYSREASON ¬= 0  THEN -
 DO
   SET &S = &SYSREASON
   SET &R = /* NULL */
   IF &S=1 THEN SET &R=&STR(ERROR PARSING STATEMENT)
   IF &S=2 THEN SET &R=&STR(DYNAMIC ALLOCATION PROCESSING ERROR)
   IF &S=3 THEN SET &R=&STR(FILE IS OF TYPE THAT CANNOT BE PROCESSED)
   IF &S=4 THEN SET &R=&STR(ERROR DETERMINING UNIT NAME)
   IF &S=5 THEN SET &R=&STR(DATA SET NOT CATALOGED)
   IF &S=6 THEN SET &R=&STR(ERROR OBTAINING THE DATA SET NAME)
   IF &S=7 THEN SET &R=&STR(ERROR FINDING DEVICE TYPE)
   IF &S=8 THEN SET &R=&STR(DATA SET DOES NOT RESIDE ON DA DEVICE)
   IF &S=9 THEN SET &R=&STR(HSM MIGRATED DATA SET NORECALL)
   IF &S=11 THEN SET &R=&STR(YOU LACK AUTHORITY TO ACCESS DATA SET)
   IF &S=12 THEN SET &R=&STR(VSAM DATA SETS ARE NOT SUPPORTED)
   IF &S=13 THEN SET &R=&STR(DATA SET COULD NOT BE OPENED)
   IF &S=14 THEN SET &R=&STR(DEVICE TYPE NOT FOUND IN UCB TABLES)
   IF &S=17 THEN SET &R=&STR(SYSTEM OR USER ABEND OCCURRED)
   IF &S=18 THEN SET &R=&STR(PARTIAL DATA SET INFORMATION WAS OBTAINED)
   IF &S=19 THEN SET &R=&STR(DATA SET RESIDES ON MULTIPLE VOLUMES)
   IF &S=20 THEN SET &R=&STR(DEVICE TYPE NOT FOUND IN EDT)
   IF &S=21 THEN SET &R=&STR(CATALOG ERROR IN TRYING TO LOCATE)
   IF &S=22 THEN SET &R=&STR(VOLUME &VOL NOT MOUNTED)
   IF &S=23 THEN SET &R=&STR(PERMANENT I/O ERROR ON VOLUME)
   IF &S=24 THEN SET &R=&STR(DATA SET NOT FOUND BY OBTAIN MACRO)
   IF &S=25 THEN SET &R=&STR(DATA SET MIGRATED TO NON-DASD DEVICE)
   IF &S=26 THEN SET &R=&STR(DATA SET RESIDES ON MASS STORAGE DEVICE)
   IF &S=27 THEN SET &R=&STR(NO VOLUME SERIAL IS ALLOCATED TO DATA SET)
   WRITE
   WRITE
   WRITE
   WRITE
   WRITE  &DSNM  CANNOT BE PROCESSED
   WRITE
   WRITE             &R
   EXIT
 DEND
   /*                                                */
IF &STR(&MBR) NE /*NULL*/ AND &STR(&MBR) NE &STR(*) THEN -
 DO
   SET &M=&STR((&MBR))
   IF &SUBSTR(1:1,&STR(&DSNM))= &STR(') THEN -
     DO
       SET &DSNM = &SUBSTR(1:&EVAL(&LENGTH(&STR(&DSNM))-1),&DSNM  )
       SET &QUOTE = &STR(')
     DEND
 DEND
   /*                                                */
IF &STR(&MBR) EQ &STR(*) THEN -
   SET &MBR = /* NULL */
   /*                                                */
IF &SUBSTR(1:2,&STR(&OROPT) ) = PR THEN GOTO PRTIT
   /*                                                */
IF &OROPT = PDS THEN -
 DO
   ISPEXEC  CONTROL DISPLAY LINE START(1)
   PDS &DSNM&QUOTE &VLM
   TERMIN EN END STOP
   DATA
    END
   ENDDATA
   CLEAR
   ISPEXEC CONTROL DISPLAY REFRESH
   END
 DEND
   /*                                                */
IF &OROPT = PDSB THEN -
 DO
   PDSB DS(&DSNM&QUOTE) &VLM
   ISPEXEC CONTROL DISPLAY REFRESH
   END
 DEND
   /*                                                */
IF &OROPT = B THEN SET &TCMD = BROWSE
SET &REC = 0
IF &OROPT = E THEN -
     DO
      SET &REC = 0
  EDRECLP: SET &CC = 0
      ISPEXEC  CONTROL DISPLAY LINE START(1)
      ISPEXEC EDREC QUERY
      SET &CC = &LASTCC
      IF &CC ¬= 0 THEN -
         DO
           SET &REC = 1
           SET &TDSN = &ZEDTDSN
           SET &TMEM = &ZEDTMEM
           IF  &ZEDTMEM ¬= /*NULL*/ THEN SET &TMEM = &STR((&TMEM))
           WRITE
           WRITE
           WRITE
           WRITE           EDIT RECOVERY IN PROGRESS FOR:
           WRITE
           WRITE              &TDSN&TMEM
           WAIT  02
           CLEAR
           ISPEXEC EDREC PROCESS
           GOTO EDRECLP
         DEND
      SET &DSN  = &STR(&DSNM&M&QUOTE)
      ISPEXEC VPUT (DSN) SHARED
     DEND
   IF  &REC = 1 THEN -
     DO
           WRITE
           WRITE
           WRITE
           WRITE           ORIGINAL EDIT SESSION IN PROGRESS FOR:
           WRITE
           WRITE                  &DSNM&M&QUOTE
           WAIT  02
           CLEAR
     DEND
ISPEXEC &TCMD DATASET(&DSNM&M&QUOTE) &VLM
ISPEXEC CONTROL DISPLAY REFRESH
END
EXIT
/* ****   PRINT IT   **** */
PRTIT: -
  SET &DSNM =  &STR(&DSNM&QUOTE)                                             005
  IF    &OROPT = PRT THEN -
     DO
        SET &PRDSN = &DSNM
        ISPEXEC DISPLAY PANEL(PRPANL)
        IF &RESPONSE EQ &STR(END)  THEN EXIT
        SET &PPARM = &PROPTS
        ISPEXEC VPUT (PROPTS) PROFILE
     DEND
  IF    &OROPT = PR1 THEN -
     DO
        ISPEXEC VGET (WPR1)
        SET &PPARM = &STR(&WPR1)
     DEND
  IF    &OROPT = PR2 THEN -
     DO
        ISPEXEC VGET (WPR2)
        SET &PPARM = &STR(&WPR2)
     DEND
 ISPEXEC VPUT (PPARM,VLM,DSNM,MBR) SHARED
 %WSRCPRTC
  ISPEXEC CONTROL DISPLAY REFRESH
END
