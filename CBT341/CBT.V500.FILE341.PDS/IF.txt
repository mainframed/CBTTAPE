               MACRO
.*
.* ------------------------------------------------------------------ *
.* 'IF' IS THE MAIN MACRO TO BUILD COMPARE AND BRANCH INSTRUCTIONS.   *
.*                                                                    *
.*                                                                    *
.* &TEST CONTAINS THE OPERANDS FOR THE COMPARE.                       *
.* &CON  CONTAINS A CONNECTOR FOR 'OR' & 'AND' TYPE OF CONDITIONS.    *
.*                                                                    *
.*                                                                    *
.* &IFNO CONTAINS A SUBSCRIPT FOR &IFEND                              *
.*                                                                    *
.* &KCON CONTAINS THE NUMBER OF CHARACTERS IN &CON                    *
.*                                                                    *
.* &IFFLAG CONTAINS THE INDICATOR FOR DENOTING WHETHER OR NOT THIS    *
.*         INVOCATION STARTS A NEW NESTED LEVEL.                      *
.*                                                                    *
.* &IFFEND IS A TABLE CONTAINING THE LABEL FOR THE END OF THIS        *
.*         INVOCATION.                                                *
.*                                                                    *
.* &TRUE CONTAINS THE LABEL FOR THE BRANCH STATEMENT FOR A TRUE       *
.*       CONDITION IN A COMPOUND 'OR' CONDITION.                      *
.*                                                                    *
.* &IF_OPCODE CONTAINS THE MNEMONIC FOR THE BRANCH STATEMENT.         *
.*                                                                    *
.* &IF_OPERAND CONTAINS THE OPERANDS FOR THE BRANCH STATEMENT.        *
.*                                                                    *
.* &ORFLAG CONTAINS THE INDICATOR FOR DENOTING WHETHER OR NOT THE     *
.*         PREVIOUS INVOCATIONS CONTAINED AN 'OR' CONNECTOR.          *
.*                                                                    *
.* &OR_OPCODE CONTAINS THE MNEMONIC FOR THE BRANCH STATEMENT FOR AN   *
.*            'OR' CONNECTOR                                          *
.*                                                                    *
.* &OR_OPERAND CONTAINS THE OPERANDS FOR THE BRANCH STATEMENT FOR AN  *
.*             'OR' CONNECTOR                                         *
.*                                                                    *
.* &IFOUT CONTAINS THE SUBSCRIPT VALUE OF THE PREVIOUS NESTED IF      *
.*        LEVEL.                                                      *
.* ------------------------------------------------------------------ *
.*
&NAME    IF    &TEST,&CON
         GBLA  &IFNO
         GBLA  &KCON
         GBLB  &IFFLAG
         GBLC  &IFEND(20)
         GBLC  &TRUE
         GBLC  &IF_OPCODE
         GBLC  &IF_OPERAND
         GBLB  &ORFLAG
         GBLC  &OR_OPCODE
         GBLC  &OR_OPERAND
         LCLA  &IFOUT
.*
.* ------------------------------------------------------------------ *
.* SET KCON TO THE NUMBER OF CHARACTERS IN &CON.                      *
.*                                                                    *
.* IF IFFLAG IS STILL ON, THEN THIS MUST BE A COMPOUND CONDITION      *
.* ------------------------------------------------------------------ *
.*
&KCON          SETA  K'&CON
               AIF   (&IFFLAG).FLAG_SET
&IFNO          SETA  &IFNO+1
&IFEND(&IFNO)  SETC  'IF'.'&SYSNDX'
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION CHECKS FOR A CONNECTOR AND BRANCHES ACCORDINGLY.  IF  *
.* A VALID CONNECTOR IS NOT FOUND, AN MNOTE IS ISSUED AND THE         *
.* CONNECTOR IS IGNORED.                                              *
.* ------------------------------------------------------------------ *
.*
.FLAG_SET      ANOP
               AIF   (&KCON EQ 0).NORMAL
               AIF   ('&CON' EQ 'OR').OR
               AIF   ('&CON' EQ 'AND').AND
               AIF   ('&CON' EQ 'DOOUT').DOOUT
               AIF   ('&CON' EQ 'IFOUT').IFOUT
.MNOTE1        MNOTE 2,'INVALID CONNECTOR - IGNORED'
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION BUILDS THE INSTRUCTIONS FOR THE PLAIN TYPE 'IF' USAGE *  *
.* - NO CONNECOTR.                                                    *
.*                                                                    *
 * THE COND MACRO BUILDS THE INSTRUCTIONS USING &TEST AS PARAMETERS.  *
.* COND SETS &IF_OPCODE AND &IF_OPERAND TO BUILD THE INSTRUCTION.     *
.*                                                                    *
.* IF ORFLAG IS SET, TURN OFF ORFLAG AND INSERT THE LABEL FOR TRUE    *
.* CONDITIONS DETECTED IN THE 'OR' CONNECTOR STATEMENTS.              *
.*                                                                    *
.* TURN OFF IFFLAG INDICATING THERE IS NO COMPOUND OR CONDITIONS TO   *
.* EVALUATE.                                                          *
.* ------------------------------------------------------------------ *
.*
.NORMAL        ANOP
&NAME    COND  &TEST
         &IF_OPCODE   &IF_OPERAND
               AIF   (NOT &ORFLAG).SET_FLAG1
&TRUE    DS    0H
&ORFLAG        SETB  0
.*
.SET_FLAG1     ANOP
&IFFLAG        SETB  0
               MEXIT
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION BUILDS THE INSTRUCTIONS FOR THE 'OR' CONNECTOR.       *  *
.*                                                                    *
.* IF ORFLAG IS NOT SET, ORFLAG AND IFFLAG ARE SET.  TRUE IS SET TO   *
.* THE LABEL FOR THE TRUE 'OR' CONDITIONS.                            *
.* ------------------------------------------------------------------ *
.*
.OR            ANOP
               AIF   (&ORFLAG).ORFLAG_SET
&ORFLAG        SETB  1
&IFFLAG        SETB  1
&TRUE          SETC  'TRUE_'.'&SYSNDX'
.*
.ORFLAG_SET    ANOP
&NAME    COND  &TEST
         &OR_OPCODE   &OR_OPERAND
               MEXIT
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION BUILDS THE INSTRUCTIONS FOR THE 'AND' CONNECTOR.      * . *
.*                                                                    *
.* IFFLAG IS TURNED ON.                                               *
.* ------------------------------------------------------------------ *
.*
.AND           ANOP
&NAME    COND  &TEST
         &IF_OPCODE   &IF_OPERAND
&IFFLAG        SETB  1
               MEXIT
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION BUILDS THE INSTRUCTIONS FOR THE 'DOOUT' CONNECTOR.    *
.*                                                                    *
.* IFFLAG IS TURNED ON.                                               *
.* ------------------------------------------------------------------ *
.*
.DOOUT         ANOP
&NAME    COND  &TEST
         &IF_OPCODE   &IF_OPERAND
         DOOUT
&IFFLAG        SETB  0
               MEXIT
.*
.* ------------------------------------------------------------------ *
.* THIS SECTION BUILDS THE INSTRUCTIONS FOR THE 'IFOUT' CONNECTOR.    *
.*                                                                    *
.* IFFLAG IS TURNED ON.  THIS IS TO PROVIDE AN ESCAPE FOR NESTED IF'S *
.* ------------------------------------------------------------------ *
.*
.IFOUT         ANOP
&NAME    COND  &TEST
         &IF_OPCODE   &IF_OPERAND
&IFOUT         SETA  &IFNO-1
         B     &IFEND(&IFOUT)
.*
         MEND
