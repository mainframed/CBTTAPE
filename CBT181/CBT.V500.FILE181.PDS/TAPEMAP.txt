TAPEMAP  TITLE '-----  TAPE ANALYSIS PROGRAM  -----            - LDW -'
*---------------------------------------------------------------------*
*                                                                     *
*  Copyright (C) 1988 by Leonard D. Woren.  All rights reserved,      *
*  except as explicitly noted below.  This updated copyright          *
*  supercedes all previous ones.  Permission to distribute this       *
*  program is now restricted:  only UNMODIFIED versions may be        *
*  distributed, i.e., only versions obtained from Leonard D. Woren    *
*  may be placed on "public domain" tapes, such as the SHARE mods     *
*  tape, the CBT (Connecticut Bank and Trust) tape, the LA MVSUG      *
*  (Los Angeles MVS User's Group) tape, etc. etc.  Modified versions  *
*  may be used at your site only; they may not be given to any other  *
*  sites.  I am tired of trying to refit other people's mods, and I   *
*  resent not getting credit for the main program.  I am tired of a   *
*  zillion different versions of the program floating around.         *
*                                                                     *
*  Since everyone will benefit from this, please send all updates to  *
*  me.  (Address below.)  I will merge them in, if they have been     *
*  made to a reasonably current version of the source, and if they    *
*  are in keeping with the general design of the rest of the program. *
*  Mods may be altered by me for this purpose.  Mods sent to me for   *
*  this purpose will then become governed by the restrictions         *
*  specified here for the whole program.                              *
*                                                                     *
*  Any copyright provisions below which are not superseded above are  *
*  still in effect.                                                   *
*                                                                     *
*  COPYRIGHT 1985, 1984, 1983, 1982 BY LEONARD D. WOREN.              *
*  PERMISSION IS HEREBY GRANTED TO COPY, USE, AND DISTRIBUTE THIS     *
*  PROGRAM SUBJECT TO THE FOLLOWING CONDITIONS:                       *
*  (BASICALLY THE STANDARD SHARE AND MVSUSER GROUP POLICY)            *
*   1)  THIS NOTICE MUST APPEAR IN ALL COPIES.                        *
*   2)  THE ONLY CHARGE WHICH MAY BE MADE FOR DISTRIBUTION IS TO      *
*       RECOVER REAL COSTS, SUCH AS POSTAGE.                          *
*   3)  THE ONLY CHARGE FOR RUNNING THE PROGRAM WHICH MAY BE MADE     *
*       IS YOUR NORMAL CHARGE FOR COMPUTER TIME.                      *
*   4)  ALL REFERENCES TO THE ORIGINAL AUTHORS MUST BE RETAINED.      *
*                                                                     *
*                                                                     *
*  COPYRIGHT 1981, 1980, 1979, 1978                                   *
*                       BY LEONARD D. WOREN    ALL RIGHTS RESERVED.   *
*  COPYRIGHT 1977 BY LEONARD D. WOREN AND MICHAEL S. MAITEN           *
*                                                                     *
*                        -- DISCLAIMER --                             *
*  ALTHOUGH THIS PROGRAM HAS BEEN EXTENSIVELY TESTED, AND IS IN USE   *
*  IN A PRODUCTION ENVIRONMENT (MVS/XA RELEASE 2.2, with DFP 2.3),    *
*  NO GUARANTEE IS MADE OF (OR RESPONSIBILITY ASSUMED FOR) CORRECT    *
*  OR RELIABLE OPERATION.  WE MAY TRY TO HELP WITH PROBLEMS.  WE      *
*  ALSO DO NOT ASSUME ANY RESPONSIBILITY TO DISTRIBUTE UPDATES,       *
*  ALTHOUGH I WILL TRY TO INTEGRATE ANY CHANGES AND PASS THEM ON TO   *
*  BE PUT ON THE various mod tapes.                                   *
*                                                                     *
*                                                                     *
*                                                                     *
*  I WOULD ALSO LIKE TO GET A COPY OF ANY ADDITIONS OR CORRECTIONS    *
*  MADE TO THIS PROGRAM, SO THEY CAN BE INCORPORATED.                 *
*                                                                     *
*                                                                     *
*  CONTACT:                                                           *
*          Leonard D. Woren                                           *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          sdcrdcf!usc-oberon!LDW (last resort)                       *
*          (213) 740-2875 (direct -- good luck!)                      *
*          (213) 743-2957 (msgs)                                      *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't call the     *
*          message number unless you're in 213 or 818.                *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  THIS PROGRAM IS BASED ON THE PROGRAM TAPEINDX WRITTEN BY           *
*  MICHAEL S. MAITEN OF THE UCLA COMPUTER CLUB.                       *
*  IT HAS BEEN ALMOST COMPLETELY REWRITTEN BY LEONARD D. WOREN OF     *
*  THE UCLA COMPUTER CLUB.                                            *
*                                                                     *
*                                                                     *
*  SAMPLE JCL:                                                        *
*                                                                     *
*        //JOBNAME  JOB (ACCOUNTING),'PRGMR ID',                      *
*        //            CLASS=S,MSGCLASS=R,MSGLEVEL=(1,1)              *
*        //STEP010  EXEC PGM=TAPEMAP                                  *
*        //SYSPRINT DD SYSOUT=*                                       *
*        //SYSPRNT2 DD SYSOUT=*                                       *
*        //SYSUT1   DD DSN=DSNAME,                                    *
*        //             DISP=OLD,                                     *
*        //             LABEL=(,BLP,EXPDT=98000),  <== FOR TMS        *
*        //             UNIT=(TAPE,,DEFER),                           *
*        //             VOL=SER=VOLID                                 *
*                                                                     *
*                                                                     *
*  AVAILABLE PARMS:                                                   *
*                                                                     *
*        CHECK      Print both standard output and SCAN analysis      *
*        DEN1       Print density character directly from tape label  *
*        FEET       Display cartridge length in feet                  *
*        GDATE      Print dates in "Gregorian" format                 *
*        INLINE     Print special info inline instead of on SYSPRNT2  *
*        JDATE      Print dates in "Julian" format                    *
*        LC=nn      Set lines per page                                *
*        LINECNT=nn Set lines per page                                *
*        NL         Force NL style analysis even if tape is SL        *
*        NOATTR     Skip second print file                            *
*        NOMEM      Do not list members of PDS's                      *
*        NOMEMBERS  Do not list members of PDS's                      *
*        NONOTE     Suppress note on tape length calculation          *
*        NULL=nn    Allow nn null files on NL tape before EOV         *
*        PERCENT    Display reel length in percent                    *
*        PCT        Display reel length in percent                    *
*        SCAN       Read all data blocks to determine accurate length *
*        TEST       Abend on logic errors                             *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  THINGS TO DO (MAY BE DONE SOMEDAY), PROBLEMS TO FIX (MAYBE SOONER) *
*    . OPTIONALLY LIST SPF STATISTICS FOR UNLOADED MEMBERS            *
*    . VOL1 ONLY (MAY WORK)                                           *
*    . IEHINITT (MAY WORK)                                            *
*    . FUGWA ARCHIVE TAPES???                                         *
*    . HANDLE MISSING EOF LABELS                                      *
*    . XCALL SOME ROUTINES, LIKE IEHMOVE2, CNVDSORG, CNVRECFM         *
*    . CLCL instead of TRT if looking for non-blank                   *
*    . GAP count - add bytes for labels when read                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
* Updates:  (Remember to update HEADER2!!)                            *
*                                                                     *
*  23Dec90 U048 LDW - Display 4 digit years for tape labels           *
*                        (PSWD field changed to 1 character wide,     *
*                        other slight field movement/resizing)        *
*                                                                     *
*  09Oct90 U047 LDW - Remove some U046 updates which broke NL support *
*                                                                     *
*  14Aug90 U046 LDW - Update DEVTABX from V50E FDRDEVTB               *
*                   - Correct (?) 3480 tapemark length                *
*                   - Show cartridge lengths in percent               *
*                   - Improve the "wrong volume mounted" message      *
*                   - Fix more U034 errors                            *
*                   - More corrections to "CHECK" tape length calc    *
*                   - Another fix like U045                           *
*                   - Fix bug that caused "CHECK" line to not be      *
*                        printed for a file with EOV2 label           *
*                   - Miscellenaneous minor cleanup                   *
*                                                                     *
*  25Jul90 U045 EMS - Fix S0C1 caused by bad U032/U033 refit          *
*                                                                     *
*  06Jun90 U044 LDW - Fix bug in DEVTABX for 3380K (caused S0C9s)     *
*                   - Add 3390 to DEVTABX (sort of... need to know    *
*                        what the FDR "device type character" is)     *
*                                                                     *
*  19Feb89 U043 LDW - Implement save area stack to allow program      *
*                        growth                                       *
*                   - Move LCTR to work area to free a register       *
*                                                                     *
*  19Feb89 U042 LDW - Fix bugs in rewritten CONVUFDR                  *
*                                                                     *
*  26Jan89 U041 LDW - Don't expand invalid density character          *
*                                                                     *
*  12Jan89 U040 LDW - Fix bugs in new method of tracking density      *
*                                                                     *
*  01Jan89 U039 LDW - New device type table (basically UCLA/OAC's     *
*                        CS02046 fix)                                 *
*                                                                     *
*  01Jan89 U038 LDW - Pick up UCLA/OAC's fix CS02199 for disks with   *
*                        > X'7FFF' tracks (3380K)                     *
*                                                                     *
*  30Dec88 U037 LDW - Pick up MAS's fix for FDR dump tapes with       *
*                        blocks with no DSCBs                         *
*                   - Update copyright to disallow distribution of    *
*                        versions modified by anyone other than LDW   *
*                   - Change some stuff to mixed case (not marked)    *
*                   - Change HEADER1 and HEADER2                      *
*                   - 3480 support                                    *
*                   - Fix bug in conditional assembly so that the     *
*                        TESTAUTH code for setting BLP gets           *
*                        assembled for MVS                            *
*                                                                     *
*  12Jan88 U036 EMS - Fix not resetting counters on null file         *
*                                                                     *
*  28FEB87 U035 SDM - REFIT U033/U034 BACK ONTO U032 BASE             *
*                                                                     *
*  28MAR86 U034 CSL - ADD SUPPORT FOR DF/DSS @ 1.2.1 LEVEL            *
*          U033 CSL - ADJUSTED FDR SUPPORT FOR V4.8 AND LATER         *
*                   - CORRECTED MISC ERRORS IN FDR DSNAME LISTING     *
*                                                                     *
*  06JUL85 U032 EMS - UPDATE FOR NEW VERSION OF FDR (4.8)             *
*                   - HANDLE BLOCKSIZES > 32K (NOW UP TO 64K)         *
*                   - CREATE MACRO FOR PARMTABLE ENTRIES              *
*                   - FIX FOR BLANK FIELDS IN LABELS                  *
*                   - FIX CHECK FOR NL FDR TAPES                      *
*                   - DETECT FDR/ABR TAPES AS DIFFERENT FROM FDR      *
*                   - CHECK FOR VOL RECORDS 80 BYTES LONG             *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  28MAY85 U031 LDW - CHANGE TAPE DSN TO KEEP ACF2 R4 QUIET           *
*  26APR85          - CHECK FOR INVALID DATA BEFORE TRYING TO         *
*                        CONVERT CDATE/EDATE                          *
*                                                                     *
*  11JUN85 U030 EMS - FIX FOR BLANK FIELDS IN DOS LABELS              *
*                   - UNDERSCORE FIELDS THAT DIFFER BETWEEN HDR/TRL   *
*                        LABELS                                       *
*                                                                     *
*  13FEB84 U029 LDW - FIX TURKEY BUG IN U025                          *
*                                                                     *
*  17JAN84 U028 LDW - PRINT DATES IN DDMMMYY FORM INSTEAD OF MMDDYY   *
*                        (PARM=GDATE WILL REQUEST MMDDYY FORM)        *
*                                                                     *
*  02MAR83 U027 LDW - PRINT DATES IN MMDDYY FORM INSTEAD OF YY.DDD    *
*                        (PARM=JDATE WILL REQUEST YY.DDD FORM)        *
*                                                                     *
*  13DEC82 U026 EMS - FIX BUG IN U025                                 *
*                                                                     *
*  02DEC82 U025 EMS - FIX DEVICETYPE TABLE IN U022                    *
*                   - EXTRACT DATA FROM HEADER INSTEAD OF TRAILER     *
*                        LABELS AND INDICATE INCONSISTANCIES          *
*                   - CREATE SOME DCB EQUATES                         *
*                                                                     *
*  03NOV82 U024 LDW - FIX LOOP CAUSED  BY U023                        *
*                   - ADD "LINECNT=" PARM                             *
*                   - FIX SPELLING OF "ASTERISKS"                     *
*                        (EMS:  IT'S NOT "ASTRISKS"!!)                *
*                   - FIX SPELLING OF "NOMEMBERS" IN PARM TABLE       *
*                                                                     *
*  15JUL82 U023 LDW - ADD SUPPORT FOR ASCII STANDARD LABELS           *
*                   - ADD THIRD BASE REG BY MOVING LCTR2 TO STORAGE   *
*                                                                     *
*  06JUL82 U022 EMS - LIST DSN'S DUMPED BY FDR & FRIENDS              *
*                   - ADD SUPPORT FOR 3375, 3380'S FOR UTILITIES      *
*                                                                     *
*  18APR81 U021 LDW - SUPPORT DOS LABELED TAPES                       *
*                   - RE-ORGANIZE PROGRAM SOMEWHAT                    *
*                   - CHANGE "ENTR" TO "OSENTER"                      *
*                   - CHANGE DEFAULT PRINT BLKSIZE FOR 3350'S         *
*                   - SET LABEL=BLP ONLY IF ACTUALLY RUNNING APFAUTH  *
*                                                                     *
*  25SEP80 U020 LDW - PARM=SCAN                                       *
*                   - COND ASM SWITCH FOR MVS                         *
*                   - IMPROVE HANDLING OF UNEXPECTED TAPE MARK        *
*                   - DON'T PRINT CDATE AND EDATE IF 00.000           *
*                   - FIX MINOR BUG IN FDR DUMP TAPE DETECTION        *
*                                                                     *
*  09SEP80 U019 LDW - SET LABEL=BLP FOR TAPE                          *
*                                                                     *
*  06MAR80 U018 SDM - FIX CONFUSION BETWEEN 6250 AND 800 BPI          *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  18JAN80 U017 SDM - FIX 6250 IBG ERROR IN LENGTH ESTIMATE           *
*                                                                     *
*  11JUL79 U016 LDW - FIX RECURSION BUG IN TAPE I/O ERROR HANDLING    *
*                                                                     *
*  10FEB79 U015 LDW - FIX VOLSER FOR NL TAPES                         *
*                   - PRINT SENSE BIT MEANINGS                        *
*                   - PRINT SENSE BIT MEANINGS                        *
*                   - CHANGE FORMAT OF "REQUESTED VOL" MSG            *
*                   - FIX ./DDNAME= BUG                               *
*                                                                     *
*  28JUN78 U014 LDW - RE-ARRANGE CODE, ADD 'NULL=' PARM               *
*                                                                     *
*  05MAY78 U013 LDW - PRINT SENSE INFO WHEN I/O ERROR OCCURS          *
*                                                                     *
*  06APR78      LDW - FIX A COUPLE OF TURKEY BUGS                     *
*                                                                     *
*  22FEB78      LDW - LIST MEMBERS IN AN IEBUPDTE INPUT STREAM        *
*                                                                     *
*  05JAN78      LDW - LIST MEMBERS UNLOADED BY IEHMOVE                *
*                                                                     *
*  26DEC78      LDW - NL TAPE ANALYSIS CODE ADDED                     *
*                                                                     *
*  14NOV77      LDW - DO A SENSE TO DETERMINE TRUE TAPE DENSITY       *
*                                                                     *
*  08NOV77      LDW - FIX SOME MISCELLANEOUS MINOR BUGS               *
*                                                                     *
*  16MAR77      LDW - LIST MEMBERS UNLOADED BY IEBCOPY                *
*                   - ATTRS FOR IEBISAM AND IEHDASDR UNLOADED DS'S    *
*                                                                     *
*  13FEB77      LDW - GENERAL RE-WRITING OF TAPE READ CODE TO LIST    *
*                     THE ORIGINAL ATTRIBUTESS FOR UNLOADED           *
*                     DATASETS CREATED BY IEHMOVE (SYSMOVE) AND       *
*                     IEBCOPY (VS2COPY)                               *
*                   - CHANGE TAPE I/O ERROR AND TAPE MARK DETECTION   *
*                     ALGORITHM.                                      *
*                                                                     *
*  07FEB77      LDW - PUT IN DSECTS FOR LABELS                        *
*                   - FIX A BUG INTRODUCED IN UPDATE OF 01/06/77      *
*                   - RE-WRITE PRINTLIN MACRO (CHANGE TO PRTLN)       *
*                                                                     *
*  06JAN77      LDW - COUNT LINES AND DO PAGE EJECT                   *
*                   - MUCH OTHER MISCELLANEOUS STUFF                  *
*                                                                     *
*  08NOV76      LDW - (UNCLUDGED A LITTLE:)                           *
*                   - FIX LENGTH CALCULATION                          *
*                   - MAKE SOME MACROS INTO SUBROUTINES (IT SEEMS     *
*                     THAT MSM NEVER HEARD OF SUBROUTINES             *
*                   - PUT IN SYMBOLIC OFFSETS FOR PRINT LINE INFO     *
*                                                                     *
*  30OCT74      MSM - *TAPEINDX* -- CLUDGED UP IN A HURRY FROM A      *
*                     SPASM PROGRAM TO MAKE A QUICK LOAD MODULE       *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         MACRO                                                     U043
&NFS     SUBINIT ,
&NFS     CSECT ,
.*&NFS   AMODE 31
.*&NFS   RMODE ANY
         DC    CL8'&NFS'                routine name
         STM   R0,R15,0(R10)            save callers regs
         LR    LBASE,R15                get local base
         USNGX &NFS,LBASE
         INUSE ,                                                   U043
         LA    R10,16*4(,R10)           -> next stack element
         CL    R10,STACKEND             overflow?
         BH    OVERFLOW                 yes - blow out of here
         SPACE 2
         MEND
         SPACE 3
         MACRO                                                     U043
&NFS     SUBEXIT ,
&NFS     LA    R12,16*4                 length of stack element
         SR    R10,R12                  -> previous stack element
         LM    R2,R14,4*R2(R10)         restore callers regs
         BR    R14                      return to caller
         SPACE 2
         MEND
         SPACE 3
         MACRO                                                     U043
&NFS     SCALL &ROUTINE
&NFS     L     R15,=A(&ROUTINE)         -> subroutine
         BAL   R14,8(,R15)
         SPACE 1
         MEND
         SPACE 3
         MACRO
&NFS     PRTLN &A,&I
         LCLC  &LQ,&NAME                                           U043
         LCLA  &L
&LQ      SETC  'L'''                    TO FOOL THE ASSEMBLER
&NAME    SETC  '&NFS'                                              U043
         AIF   ('&A'(1,1) NE '''').NOTQ NOT QUOTED STRING
&L       SETA  K'&A-2                   SET LENGTH OF IT
&NAME    MVC   OUTBUFF(&L),=C&A
&NAME    SETC  ''                                                  U043
         AGO   .BAL
.NOTQ    AIF   ('&A' EQ 'OUTBUFF').BAL                             U043
         AIF   ('&I' EQ 'I').I
&NAME    MVC   OUTBUFF(&LQ&A),&A
&NAME    SETC  ''                                                  U043
.BAL     ANOP
.*&NAME  BAL   R14,PRTLN
         MNOTE '         SCALL PRTLN'                              U043
&NAME    SCALL PRTLN                                               U043
         SPACE 1
         MEXIT ,                                                   U043
.I       ANOP
&NAME    L     R15,=A(&A)
&NAME    SETC  ''                                                  U043
         MVC   OUTBUFF(&LQ&A),0(R15)
         AGO   .BAL
         MEND
         SPACE 3
         MACRO
&NFS     PRTLN2  &DUMMY
.*&NFS   BAL   R14,PRTLN2
         MNOTE '         SCALL PRTLN2'                             U043
&NFS     SCALL PRTLN2                                              U043
         MEND
         SPACE 3
         MACRO
&NFS     TAPIO &CCW,&TM=UNEXTPMK
&NFS     LA    R0,&CCW
         BAL   LINK,TAPIO
         B     &TM                      TAPEMARK FOUND
         SPACE 1
         MEND
         SPACE 3
         MACRO
&NFS     NEWPAGE  &C,&F
         LCLC  &N                                                  U043
&N       SETC  '&F'                     use specified file         U043
         AIF   ('&F' NE '').HAVEN                                  U043
&N       SETC  '1'                      assume SYSPRINT            U043
.HAVEN   AIF   ('&C' EQ '').JUSTBAL
         AIF   ('&C' EQ '(R0)').R0
&NFS     LA    R0,&C
.R0      BAL   LINK,NEWPAG&N.C                                     U043
         MEXIT
.JUSTBAL ANOP
&NFS     BAL   LINK,NEWPAG&N                                       U043
         MEND
         SPACE 3
         MACRO
&NFS     IFP2  &L,&B
&NFS     TM    TFLAG2,T2@PRT2
         AIF   ('&L'(1,1) EQ 'N').NO
         BO    &B
         MEXIT
.NO      BNO   &B
         MEND
         SPACE 3
         MACRO
&NFS     ICALL &RTN,&REG,&R1=,&R15=
         LCLC  &NAME
&NAME    SETC  '&NFS'
         AIF   ('&R1' EQ '').A
&NAME    LA    R1,&R1
&NAME    SETC  ''
.A       AIF   ('&R15' EQ '').B
&NAME    LA    R15,&R15
&NAME    SETC  ''
.B       ANOP
&NAME    BAL   &REG,&RTN
         SPACE 1
         MEND
         SPACE 3
         MACRO ,                                                   U025
&NAME    LABCK &FLD,&TARG,&BLANK=NO                                U032
         LCLC  &ORG,&SAVE,&NFS,&N                                  U037
&NFS     SETC  '&NAME'                  SET NAME FIELD             U032
&N       SETC  'IHB&SYSNDX'                                        U037
&SAVE    SETC  'SAVEHDR1'               ASSUME HEADER 1 DATA       U025
&ORG     SETC  'FL1LABI'                DITTO                      U025
         AIF   ('&FLD'(1,3) EQ 'FL1').GEN01                        U025
&SAVE    SETC  'SAVEHDR2'               GET HEADER 2 DATA          U025
&ORG     SETC  'FL2LABI'                DITTO HERE TOO.            U025
.GEN01   ANOP  ,                                                   U025
         AIF   ('&BLANK' EQ 'YES').GEN02                           U032
&NFS     CLC   &FLD-&ORG+&SAVE,BLANKS           IS FIELD BLANK?    U030
&NFS     SETC  ''                       CLEAR NAME FIELD           U032
         BNE   *+4+4+4                          NO, KEEP CHECKING  U030
         MVI   #BLKFLD,C'#'                     INDICATE SUCH      U030
         B     &N                               AND EXIT           U030
.GEN02   ANOP  ,                        DON'T CHECK FOR BLANK FLDS U032
&NFS     CLC   &FLD,&FLD-&ORG+&SAVE             SAME CONTENTS?     U032
         BE    &N                               YES, CONTINUE      U030
         MVI   #MSMATCH,C'*'                    NO, FLAG DIF       U025
         MVC   &FLD,&FLD-&ORG+&SAVE             USE HDR1 INFO      U025
         MVC   &TARG,UNDER                      UNDERSCORE FLD     U030
&N       DS    0H                                                  U030
         SPACE 1
         MEND  ,                                                   U025
         SPACE 3
         MACRO ,                                                   U032
&NFS     PARMTAB  &NAME,&BITS                                      U032
         LCLA  &KN                                                 U032
         LCLC  &NBITS(2)                                           U032
&KN      SETA  K'&NAME-1                FOR STUPID ASMH            U032
&NBITS(1) SETC 'X''FF''-&BITS(1)'       SET INVERSE FOR FIRST ONE  U032
&NBITS(2) SETC 'X''FF''-&BITS(2)'       SET INVERSE FOR SECND ONE  U032
&NFS     DC    Y(&KN),AL1&BITS,CL10'&NAME'                         U032
         MEND  ,                                                   U032
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  NON-IBM LIBRARY MACROS USED IN THIS PROGRAM ARE:                   *
*                                                                     *
*   CHARTAB  - GENERATE TRANSLATE TABLE                               *
*   DAYS     - GENERATE TABLE OF DAY OF WEEK NAMES FOR "TODAY" MACRO  *
*   DCBEXIT  - GENERATE DCB EXIT TO FILL IN DEFAULT BLKSIZE, BUFNO    *
*   DROPX    - DELETE ENTRY FROM USING/DROP SYMBOL TABLE              *
*   FL1      - HDR1/EOF1/EOV1 DSECT                                   *
*   FL2      - HDR2/EOF2/EOV2 DSECT                                   *
*   HEX      - CONVERT INTERNAL TO PRINTABLE HEX                      *
*   HEXTAB   - GENERATE HEX TRANSLATE TABLE FOR "HEX" MACRO           *
*   ID       - INNER MACRO FOR OSENTER                                *
*   INUSE    - PRINT USING/DROP SYMBOL TABLE                          *
*   IOBD     - IOB DSECT                                              *
*   MONTHS   - GENERATE TABLE OF MONTH NAMES & NUMBER OF DAYS         *
*   OSENTER  - OS HOUSEKEEPING                                        *
*   OSENT01  - INNER MACRO FOR OSENTER                                *
*   REGEQU   - INNER MACRO FOR OSENTER TO GEN EQU'S FOR REGISTERS     *
*   TODAY    - FORMAT PRINTABLE CURRENT DATE & TIME                   *
*   USNGX    - ADD ENTRY TO USING/DROP SYMBOL TABLE                   *
*   VOL      - VOLUME LABEL DSECT                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         GBLB  &MVS                                                U020
&MVS     SETB  1                                                   U020
         EJECT
TAPEMAP  OSENTER  LPARM=R2,GETMAIN=(WORKLEN,WORKD,ZERO),           U021$
               EXIT=LEAVE,RC=0,BASE2=R11,BASE3=R8                  U021
         LA    R10,SA@STACK             -> first stack element     U043
         LR    R0,R10                   compute address of ...     U043
         AH    R0,=Y(STACKLEN)          ... last stack element     U043
         ST    R0,STACKEND              save for overflow check    U043
         SCALL INIT                     GO INITIALIZE              U043
         EJECT
READCARD L     R1,OPENMFLI              GET SYSIN DCB ADDR
         GET   (1),INBUFF
         OI    TFLAG1,T1@DATA           SET FLAG
         CLC   =C'./DDNAME',INBUFF      IS IT THIS?
         BE    INDDNAME                 YES - PROCESS
         CLC   =C'./VOLUME',INBUFF      IS IT THIS?
         BNE   *+10                     NO - MUST BE VOL NAME
         MVC   INBUFF(6),INBUFF+9       MOVE OVER
         MVI   JFCB+117,1               # OF VOL SER #'S
         MVC   JFCB+118(6),INBUFF       VOL SER #
         MVI   JFCB+52,X'40'            VOLUME SERIAL LIST CHANGED
         B     TAPEINDX                 SKIP THE RDJFCB, GO TO SUBROUTN
         SPACE 2
INDDNAME L     R1,TAPEMFL               GET ADDR OF TAPEDCB        U014
         MVC   DCBDDNAM-IHADCB(,R1),INBUFF+9  MOVE DDNAME INTO DCB U025
         SPACE 2
RDJFCB   RDJFCB  MF=(E,TAPEMFL)
         B     TAPEINDX                 GO TO SUBROUTINE
         SPACE 3
EOD2     CLOSE MF=(E,OPENMFL)
         IFP2  N,LEAVE                  SKIP IF NOT OPEN
         CLOSE MF=(E,OPENMFL2)
         B     LEAVE                    RETURN
         SPACE 2
EOD      TM    TFLAG1,T1@DATA           DATA READ IN?
         BO    EOD2                     YES.
         MVC   RET#ADDR,=A(EOD2)        SET NEW RETURN ADDR
         B     RDJFCB
         SPACE 3
OVERFLOW WTO   'Stack overflow in TAPEMAP -- Notify Support.',     U043$
               ROUTCDE=11                                          U043
         WTO   '   Routines called in sequence:',ROUTCDE=11        U043
         LA    R2,16*4                  length of a stack element  U043
         LA    R3,SA@STACK              -> first stack element     U043
         LA    R1,=CL8'USCLGN*'         fake the first name        U043
         SPACE 2
OF$LOOP  MVC   OF$WTO+8+6(8),0(R1)      routine name               U043
OF$WTO   WTO   '      xxxxxxxx',ROUTCDE=11                         U043
         LA    R3,16*4(,R3)             -> next stack element      U043
         L     R1,4*R11(,R3)            get routine's base         U043
         CLR   R3,R10                   at end of stack?           U043
         BNH   OF$LOOP                  no - keep going            U043
         ABEND 4095,DUMP                the ultimate diagnostic    U043
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
* T A P E I N D X                                                     *
*                                                                     *
*    A ROUTINE WHICH READS IN THE VOLUME NAME & ALL THE DATA SET NAMES*
*        (FROM THE TRAILER LABELS) OF A TAPE (WITH STANDARD LABELS)   *
*        AND FORMATS AND PRINTS THE INFORMATION FROM THE LABELS.      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
TAPEINDX XC    LCTR2,LCTR2              INIT SECOND LINE COUNTER   U021
         XC    NUMBNULL,NUMBNULL        INIT COUNT OF NULL FILES   U014
         OI    TFLAG1,T1@PAGE1+T1@ANLZ  1ST PAGE/THIS VOL + ANLZ LABEL
         SPACE 1
* OPEN TAPE VOLUME FOR LABEL=BLP
*U037    MVC   JFCB(40),HEADER1+34      BE CUTE WITH THE DSN
*U037    MVI   JFCB+4,C'.'              MAKE IT A ...              U031
*U037    MVI   JFCB+13,C'.'             ... SYNTACTICALLY ...      U031
*U037    MVI   JFCB+18,C'.'             ... VALID DSNAME TO ...    U031
*U037    MVI   JFCB+23,C'.'             ... KEEP ACF2 R4 ...       U031
*U037    MVI   JFCB+32,C'.'             ... FROM GETTING UPSET     U031
*U031    MVC   JFCB+40(4),=C'/LDW'      EVEN CUTER HERE...
*U037    MVC   JFCB+40(4),=C'.LDW'      EVEN CUTER HERE...         U031
*U037    MVC   JFCB+44(8),BLANKS        BLANK OUT ELEM NAME
         SPACE 1
         AIF   (NOT &MVS).MVS01                                    U037
         TESTAUTH  FCTN=1               SEE IF WE ARE RUNNING...   U021
         LTR   R15,R15                  ... APF AUTHORIZED         U021
         BNZ   *+8                      SKIP IF NOT                U021
.MVS01   MVI   JFCB+66,X'10'            LABEL=BLP                  U019
         MVC   JFCB+68(2),=H'1'         FILE SEQ #                 U021
         MVC   JFCB+70(2),=H'1'         VOL SEQ #                  U021
         MVI   JFCB+98,X'40'            DSORG=PS
         MVI   JFCB+100,X'90'           RECFM=FB
         MVC   HEADER1+5(6),JFCB+118    INIT volser IN CASE NOT SL
         XC    FILE#SEQ,FILE#SEQ
         SPACE 1
         OPEN  MF=(E,TAPEMFL),TYPE=J
         L     R1,TAPEMFL               GET ADDR OF TAPEDCB        U014
         TM    DCBOFLGS-IHADCB(R1),DCBOFOPN DATA SET OPEN?         U025
         BO    TPOPENOK                 YES.
         NEWPAGE ,                      PRINT THE HEADER
         PRTLN '-*** Tape volume could not be opened ***'
         B     RETURN
         SPACE 3
TPOPENOK NI    DCBMACRF-IHADCB(R1),X'FF'-DCBMRABC  BLOCK COUNT BAD U025
         XC    LEN#TAPE(4*2),LEN#TAPE   CLEAR SL & NL LENGTHS
         NI    TFLAG1,255-T1@BADEN-T1@SL RESET FLAGS
         NI    TFLAG2,255-T2@ASCII      RESET SL ASCII FLAG        U023
         L     R14,=A(COLHEAD2)         -> second column header    U046
         TM    PARMFLG2,P2@PCT          force percent?             U046
         BO    LEN_PCT                  yes                        U046
         TM    PARMFLG2,P2@FEET         force feet?                U046
         BO    LEN_FEET                 yes                        U046
         L     R15,TAPEMFL              -> TAPEDCB                 U046
         CLI   DCBDEVT-IHADCB(R15),DCBDVMT4  3480?                 U046
         BNE   LEN_FEET                 no - default to feet       U046
         SPACE 1
LEN_PCT  MVC   #LENGTH-OUTBUFF(6,R14),=C' (pct)'                   U046
         MVC   #CUMLEN-OUTBUFF(6,R14),=C'% full'                   U046
         B     LEN_SET                                             U046
         SPACE 1
LEN_FEET MVC   #LENGTH-OUTBUFF(6,R14),=C'(feet)'                   U046
         MVC   #CUMLEN-OUTBUFF(6,R14),=C'length'                   U046
         SPACE 2
LEN_SET  MVI   WHERE,0                  SET "WHERE ARE WE" FLAG
         TAPIO TCCW#DAT,TM=LEADTPMK     READ IN VOLUME LABEL
         MVC   VOLLABI(80),TAPEBUFF     SAVE VOLUME LABEL
         SPACE 1
DO$SENSE XR    R2,R2                    CLEAR                      U032
         ICM   R2,B'0011',LASTSIZE      PRESERVE LENGTH OVER SENSE U032
         LA    R0,C'0'                  assume 3480                U037
         LA    R1,DEN#3480              ...                        U037
         L     R15,TAPEMFL              -> TAPEDCB                 U037
         CLI   DCBDEVT-IHADCB(R15),DCBDVMT4  3480?                 U037
         BE    SAVE$DEN                 yes                        U037
*--- The code above will need to be changed when 3480s come in     U037
*--- multiple densities.                                           U037
         TAPIO TCCW#SNS                 READ SENSE DATA
         LA    R1,C'3'                  ASSUME 1600 BPI
         TM    SENSDATA+3,B'00000100'   PE MODE?
         BO    TRUE$GOT                 YES
         LA    R1,C'2'                  ASSUME 800 BPI             U018
         NI    SENSDATA+5,B'11000000'   CLEAN OFF SUBSYS DATA      U018
         CLI   SENSDATA+5,B'01000000'   'NEW' SUBSYSTEM?           U018
         BNE   TRUE$GOT                 NO, ASSUME 2400 SERIES     U018
         TM    SENSDATA+6,B'00001000'   6250 CAPABLE DRIVE (MODEL#)U018
         BNO   TRUE$GOT                 NO, DENSITY IS RIGHT       U018
         LA    R1,C'4'                  YES, SET 6250              U018
         SPACE 2
TRUE$GOT LR    R0,R1                    COPY DENSITY CHAR
         N     R1,=F'15'                STRIP IT                   U020
         MH    R1,=Y(DEN#TBLL)          multiple by tbl entry len  U037
         LA    R1,DEN#LIST(R1)          POINT TO table entry
         SPACE 2
SAVE$DEN MVC   DEN@BIN(DEN#TBLL),0(R1)  set den-related constants  U037
         STC   R0,DEN@TRUE              SAVE 1 CHAR FORM           U037
         SPACE 2
         CLI   NUMBNULL+1,0             HOW MANY NULL SKIPPED?     U014
         BNE   NULL1ST                  MORE THAN ZERO.            U014
         CH    R2,=H'80'                LABEL RECORD 80 BYTES?     U032
         BNE   NEITHER                  NO, CAN'T BE SL            U032
         CLC   VOLLABI(4),=C'VOL1VOL1'  VOLUME LABEL?
         BE    TPVOL1OK                 YES.
         CLC   VOLLABI(4),=X'564F4C31'  ASCII VOLUME LABEL?        U023
         BNE   NEITHER                  NO                         U023
         OI    TFLAG2,T2@ASCII          YES - SET FLAG             U023
         TR    VOLLABI(80),ASCIITAB     AND TRANSLATE TO ASCII     U023
         B     TPVOL1OK                 JOIN STANDARD LABEL CODE   U023
         SPACE 3
NEITHER  MVC   VOLSERNO(6),JFCB+118     SET HERE FOR OVERPRINTING  U015
         NEWPAGE ,                      PRINT THE HEADER LINES
         PRTLN '0++++ No VOL1 label found.  NL analysis follows ++++'
         LH    R0,LCTR                  get current line count     U043
         BCTR  R0,0                     acct for extra blank line  U043
         STH   R0,LCTR                  store updated line count   U043
         PRTLN OUTBUFF                  BLANK LINE
         SPACE 1
NULL1ST  MVC   FL1ID,BLANKS             SET THE DSN                U014
         SPACE 3
NL$NEXT  STH   R2,LASTSIZE              RESTORE SIZE OF FIRST BLOCK
         STH   R2,BYTECNT+2             INIT TOTAL BYTES READ
         XC    BYTECNT(2),BYTECNT       CLEAR HIGH ORDER HALFWORD
         STH   R2,MAXBLKSI              INIT BIGGEST BLOCK READ
         MVC   BLOCKCNT,=F'1'           INIT # OF BLOCKS READ      U020
         MVI   NLFLAGS,NL@F             INIT NL TYPE FLAGS
         CLC   MAXBLKSI,TAPEBUFF        IS FIRST BLOCK RECFM=V?    U032
         BNE   *+8                      NO - SKIP
         OI    NLFLAGS,NL@V             YES - SET FLAG
         MVI   WHERE,3                  SET "WHERE ARE WE" FLAG
         MVC   UNLOADER,UNLOADER-1      CLEAR INDICATOR
         LH    R1,FILE#SEQ              GET FILE NUMBER
         LA    R1,1(,R1)                INCR
         STH   R1,FILE#SEQ              PUT BACK
         CVD   R1,DWD                   -> PACKED
         UNPK  DWD(5),DWD+5(3)          -> EBCDIC
         OI    DWD+4,C'0'               FIX SIGN
         MVC   FL1FILSQ,DWD+1           PUT INTO "LABEL"
         SPACE 1
*  NOW SEE IF THIS FILE IS ANYTHING SPECIAL THAT WE KNOW ABOUT.
*                                       +-----------------------------+
*                                       |  IEHMOVE?                   |
*                                       +-----------------------------+
***      CLC   =H'800',LASTSIZE         RIGHT SIZE?
***      BNE   NL$NMOVE                 NOT IEHMOVE
         BAL   LINK,TEST$MV             IS IT IEHMOVE?
         B     NL$NMOVE                 NO
         B     IEHMOVE2                 YES - 2 PRINT FILE FORMAT
         B     IEHMOVE1                 YES - 1 PRINT FILE FORMAT
         SPACE 2
*                                       +-----------------------------+
*                                       |  IEBCOPY?                   |
*                                       +-----------------------------+
NL$NMOVE CLC   TAPEBUFF(8),=H'60,0,56,0' RECFM=V,BLK=60,REC=56?
         BNE   NL$NCOPY                 NO - NOT IEBCOPY
         BAL   LINK,TEST$CPY            IS IT IEBCOPY?
         B     NL$NCOPY                 NO
         B     IEBCOPY2                 YES - 2 PRINT FILE FORMAT
         B     IEBCOPY1                 YES - 1 PRINT FILE FORMAT
         SPACE 2
*                                       +-----------------------------+
*                                       |  IEHDASDR?                  |
*                                       +-----------------------------+
NL$NCOPY BAL   LINK,TEST$DMP            IS IT IEHDASDR?
         B     NL$NDSDR                 NO
*------* IFP2  Y,IHDASDR2
         B     IHDASDR1
         SPACE 2
*                                       +-----------------------------+
*                                       |  IEBUPDTE?                  |
*                                       +-----------------------------+
NL$NDSDR BAL   LINK,TEST$UPS            MAYBE IEBUPDTE INPUT STREAM?
         B     NL$NUPDT                 BOY WILL THIS BE UNRELIABLE
         B     IEBUPDT2                 YES - 2 PRINT FILE FORMAT
         MVC   OUTBUFF2+8(27),=C'is an IEBUPDTE input stream'
         B     NL$LOOP
         SPACE 2
*                                       +-----------------------------+
*                                       |  INNOVATION'S FDR?          |
*                                       +-----------------------------+
NL$NUPDT BAL   LINK,TEST$FDR            IS IT FDR?
         B     NL$NFDR                  NO
*------* IFP2  Y,
*U032    B     NL$LOOP                  YES, BUT I DON'T KNOW ANY MORE
         B     FDR2                     YES - 2 PRINT FILE         U032
         B     FDR1                     YES - 1 PRINT FILE         U032
         SPACE 2
*                                       +-----------------------------+
*                                       |  IEBISAM?                   |
*                                       +-----------------------------+
NL$NFDR  BAL   LINK,TEST$IS             IS IT IEBISAM?
         B     NL$NIS                   DIDN'T THINK SO...         U034
         B     IEBISAM2                 YES - 2 PRINT FILE FORMAT
         B     IEBISAM1                 YES - 1 PRINT FILE FORMAT
         SPACE 3
*                                       +-----------------------------+
*                                       |  IBM'S DF/DSS?              |
*                                       +-----------------------------+
NL$NIS   BAL   LINK,TEST$DSS            IS IT DSS?                 U034
         B     NL$LOOP                  NO                         U034
*------* IFP2  Y,                                                  U034
         B     NL$LOOP                  YES, BUT DON'T KNOW MORE   U034
         SPACE 2
*                                       +-----------------------------+
*                                       |  READ THE WHOLE FILE        |
*                                       +-----------------------------+
NL$LOOP  TAPIO TCCW#DAT,TM=NL$EOF       READ A BLOCK
         B     NL$LOOP                  DO THAT TILL EOF
         SPACE 2
*                                       +-----------------------------+
*                                       |  PRINT NL STATS             |
*                                       +-----------------------------+
NL$EOF   CLI   UNLOADER,C' '            A SECOND LINE?
         BE    *+12                     NO - SKIP
         NEWPAGE  2                     MAKE SURE WE HAVE ENOUGH
         SPACE 1
         MVC   #UNLOAD,UNLOADER
         LH    R1,FILE#SEQ              GET THE FILE SEQ #
         CVD   R1,DWD                   TO PACKED
         MVC   #FILE-1(5),=X'2020202120'
         ED    #FILE-2(6),DWD+5
         SPACE 3
NL$SL    CLC   =C'(null)',UNLOADER+3    SL NULL FILE?              U021
         BE    NLSLDONE                 YES - CAN'T DO ANYTHING    U021
         SCALL NL$ATTR                  DO NL DCB ATTRIBUTES       U043
         TM    TFLAG1,T1@SL             THIS TAPE SL?
         BO    NLSLDONE                 YES - DIFFERENT HANDLING
         SPACE 1
NL$DONEX TAPIO TCCW#DAT,TM=NULL$NL      READ NEXT FILE'S FIRST BLK U014
         XR    R2,R2                    CLEAR FOR ICM              U032
         ICM   R2,B'0011',LASTSIZE      GET SIZE OF LAST BLOCK     U032
         B     NL$NEXT                  PROCESS NEXT FILE
         SPACE 2
NLSLDONE PRTLN OUTBUFF                  BLANK LINE
         B     NEXTFILE                 NEXT SL FILE
         SPACE 2
NULL$NL  LH    R1,NUMBNULL              GET NUMBER OF NULL FILES   U014
         LA    R1,1(,R1)                INCR                       U014
         STH   R1,NUMBNULL              SAVE NEW COUNT             U014
         CH    R1,NULLNUMB              DONE ENOUGH?               U014
         BH    HAVE$EOV                 YES - STOP                 U014
         LH    R1,FILE#SEQ              MUST INCR...               U014
         LA    R1,1(,R1)                ...THIS ALSO...            U014
         STH   R1,FILE#SEQ              ...                        U014
         MVC   #FILE-1(5),=X'2020202120'  MOVE IN EDIT MASK        U014
         CVD   R1,DWD                                              U014
         ED    #FILE-2(6),DWD+5                                    U014
         MVC   #UNLOAD+3(6),=C'(null)'  PUT IN INDICATOR           U014
         PRTLN OUTBUFF                                             U014
         L     R0,LEN#TAPE+4            GET ACCUMULATED TAPE LENGTHU014
         AH    R0,=Y(375)               COUNT 1 TAPEMARK           U014
         ST    R0,LEN#TAPE+4            SAVE UPDATED               U014
         B     NL$DONEX                                            U014
         SPACE 3
TPVOL1OK OI    TFLAG1,T1@SL
         MVC   BYTECNT,=F'80'           init byte count            U046
         MVC   HEADER1+5(6),VOLSERNO
         MVC   HEADER1+19(10),VOLOWNER
         MVC   HEADER1+13(6),HEADER1+12 BLANK OUT KEYWORD
         CLC   VOLOWNER,BLANKS          OWNER ALL BLANK?
         BE    *+10
         MVC   HEADER1+13(6),=C'Owner='
         NI    TFLAG2,255-T2@RQVOL      TURN OFF FLAG
         CLC   VOLSERNO(6),JFCB+118     GET WHAT WE ASKED FOR?
         BE    *+8                      YES - CONTINUE
         OI    TFLAG2,T2@RQVOL          SET FLAG
         SPACE 1
         NEWPAGE ,                      PRINT HEADER LINES
         NI    TFLAG1,255-T1@ANLZ       RESET FLAG
         TM    PARMFLG1,P1@SCAN+P1@CHECK+P1@NL   ANALYZING?        U020
         BZ    *+8                      NO - SKIP
         OI    TFLAG1,T1@ANLZ           YES - SET ANALYZE FLAG
         SPACE 1
         TM    PARMFLG1,P1@NL           FORCE NL ANALYSIS?
         BNO   NO$FORCE                 NO - CONTINUE
         XI    TFLAG1,T1@SL             YES - TURN OFF FLAG
         B     NL$NEXT                  AND GO TO NL ROUTINE
         SPACE 2
NO$FORCE MVI   WHERE,1                  SET "WHERE ARE WE" FLAG
         NI    TFLAG2,255-T2@DOSTP      RESET DOS FLAG             U021
         TAPIO TCCW#L1,TM=MISSHDR1      READ THE first HDR1 LABEL  U021
         L     R0,BYTECNT               get current byte count     U046
         AH    R0,=H'80'                adjust by length just read U046
         ST    R0,BYTECNT               save updated value         U046
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL1LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         MVC   SAVEHDR1,FL1LABI         SAVE COPY OF HEADER 1      U025
         XC    FL2LABI(80),FL2LABI      IN CASE IT DOESN'T EXIST   U021
         XC    SAVEHDR2,SAVEHDR2        DITTO                      U025
         TAPIO TCCW#L2,TM=MIS1HDR2      READ THE first HDR2 LABEL  U021
         L     R0,BYTECNT               get current byte count     U046
         AH    R0,=H'80'                adjust by length just read U046
         ST    R0,BYTECNT               save updated value         U046
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL2LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         MVC   SAVEHDR2,FL2LABI         SAVE COPY FOR LATER        U025
         B     DATAREAD                 SKIP AROUND NORMAL CODE
         SPACE 2
*---  FIRST HDR2 LABEL IS MISSING.                                 U021
MIS1HDR2 CLI   FL1ID,C'0'               MAYBE IEHINITT?            U021
         BNE   MISSHDR2                 NO - SKIP                  U021
         CLC   FL1ID(80-4-1),FL1ID+1    ALL CHARACTER ZEROES?      U021
         BE    IEHINITT                 YES - IEHINITT TAPE        U021
         SPACE 1
*---  A HDR2 LABEL IS MISSING.                                     U021
MISSHDR2 OI    TFLAG2,T2@DOSTP          HDR2 LABEL MISSING         U021
         B     NO$FSF                   SKIP FIRST TIME CODE       U021
         SPACE 2
NEXTFILE TM    TFLAG2,T2@DOSTP          EOF2 MISSING?              U021
         BO    READHDR1                 YES - ALREADY AT TAPEMARK  U021
         TAPIO TCCW#FSF                 FWD SPACE
         SPACE 1
READHDR1 MVI   WHERE,1                  SET "WHERE ARE WE" FLAG
         NI    TFLAG2,255-T2@DOSTP      RESET DOS FLAG             U021
         TAPIO TCCW#L1,TM=HAVE$EOV      READ THE HDR1 LABEL        U021
         MVC   BYTECNT,=F'80'           init byte count            U046
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL1LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         MVC   SAVEHDR1,FL1LABI         SAVE COPY OF HEADER 1      U026
         XC    FL2LABI(80),FL2LABI      IN CASE IT DOESN'T EXIST   U021
         XC    SAVEHDR2,SAVEHDR2        DITTO                      U026
         TAPIO TCCW#L2,TM=MISSHDR2      READ THE HDR2 LABEL        U021
         L     R0,BYTECNT               get current byte count     U046
         AH    R0,=H'80'                adjust by length just read U046
         ST    R0,BYTECNT               save updated value         U046
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL2LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         MVC   SAVEHDR2,FL2LABI         SAVE COPY FOR LATER        U026
         SPACE 1
DATAREAD TAPIO TCCW#FSF                 SKIP TO END OF HDR LABELS  U021
         SPACE 1
NO$FSF   MVC   UNLOADER,UNLOADER-1      CLEAR UNLOADED FLAG
         MVI   NLFLAGS,NL@V+NL@F        ASSUME RECFM=V & RECFM=F
         MVI   WHERE,3                  SET "WHERE ARE WE" FLAG
         XC    MAXBLKSI,MAXBLKSI        Clear to 0                 U036
         MVC   BYTECNT,=A(4*80)         Set to 2 HDR + 2 EOF lbls  U036
         XC    BLOCKCNT,BLOCKCNT        Clear blocks read          U036
         TAPIO TCCW#DAT,TM=NULLFILE     READ FIRST DATA BLOCK
         XR    R1,R1                    CLEAR FOR ICM              U032
         ICM   R1,B'0011',LASTSIZE      GET SIZE OF (FIRST) BLOCK  U032
         STH   R1,MAXBLKSI              INIT BIGGEST BLOCK READ
*U036    AH    R1,=Y(4*80)              2 HDR + 2 EOF LABELS
         A     R1,BYTECNT               Update current byte count  U036
         ST    R1,BYTECNT               INIT TOTAL BYTES READ
         MVC   BLOCKCNT,=F'1'           INIT NUMBER OF BLOCKS READ U020
         NI    NLFLAGS,255-NL@CHANG     FIRST BLOCK NOT A CHANGE
         OI    NLFLAGS,NL@F             STILL COULD BE RECFM=F
         SPACE 2
         CLI   FL2RECFM,C'F'            RECFM=FB?
         BNE   NOT$RF$F                 NO - NOT IEHMOVE OR IEBISAM
         CLC   FL2LRECL,C00080          LRECL=80?
         BNE   FIND$EOF                 NO - NOT IEHMOVE OR IEBISAM
***      CLC   FL2BLKL,C00800           BLKSIZE=800?
***      BNE   TST$UPDT                 NO - NOT IEHMOVE'D
         CLI   FL2BLKA,C'B'             BLOCKED?
         BNE   TST$UPDT                 NO - NOT IEHMOVE'D
         BAL   LINK,TEST$MV             IS IT IEHMOVE?
         B     TST$UPDT                 NO - TRY NEXT
         B     IEHMOVE2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
NOT$RF$F CLI   FL2RECFM,C'V'            RECFM=V?
         BNE   NOT$RF$V                 NO - NOT IEBCOPY'D
         CLI   FL2BLKA,C'S'             RECFM=VS?
         BNE   FIND$EOF                 NO - NOT IEBCOPY'D
         BAL   LINK,TEST$CPY            IS IT VS2COPY?
         B     FIND$EOF                 NO
         B     IEBCOPY2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
NOT$RF$V CLI   FL2RECFM,C'U'            RECFM=U?
         BNE   FIND$EOF                 NO - NOT IEHDASDR
         BAL   LINK,TEST$DMP            IS IT IEHDASDR?
         B     NOT$DSDR                 NO
*------* IFP2  Y,IHDASDR2               USING SECOND PRINT FILE?
         B     TEST$LCT                 CONTINUE
         SPACE 3
NOT$DSDR BAL   LINK,TEST$FDR            IS IT FDR?
         B     TST$DSS                  NO                         U034
*------* IFP2  Y,                       USING SECOND PRINT FILE?
         B     FDR2                     USING SECOND PRINT FILE?   U022
         B     FDR1                     NO, JUST IDENTIFY          U022
         SPACE 3
TST$UPDT BAL   LINK,TEST$UPS            SEE IF IEBUPDTE INPUT STREAM
         B     TST$ISAM                 PROBABLY NOT
         B     IEBUPDT2                 YES - 2 PRINT FILE FORMAT
         MVC   OUTBUFF2+8(27),=C'is an IEBUPDTE input stream'
         B     TEST$LCT
         SPACE 3
TST$ISAM BAL   LINK,TEST$IS             IS IT IEBISAM?
         B     FIND$EOF                 NO
         B     IEBISAM2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT  U034
         SPACE 2
TST$DSS  BAL   LINK,TEST$DSS            IS IT DSS?                 U034
         B     FIND$EOF                 NO                         U034
         B     DSS2                     USING SECOND PRINT FILE?   U034
         B     DSS1                     NO, JUST IDENTIFY          U034
         SPACE 1
TEST$LCT TM    PARMFLG1,P1@INLIN        PARM=INLINE?
         BNO   FIND$EOF                 NO - SKIP THIS
         NEWPAGE  2                     MORE THAN 1 LINE LEFT?
         SPACE 1
         CLC   IEBCOPY,UNLOADER         UNLOADED BY IEBCOPY?
         BE    IEBCOPY1                 YES - PRINT EXTRA INFO
         CLC   IEHMOVE,UNLOADER         UNLOADED BY IEHMOVE?
         BE    IEHMOVE1                 YES - PRINT EXTRA INFO
         CLC   IEBISAM,UNLOADER         UNLOADED BY IEBISAM?
         BE    IEBISAM1                 YES - PRINT EXTRA INFO
         CLC   IEHDASDR,UNLOADER        DUMPED BY IEHDASDR?
         BE    IHDASDR1                 YES - PRINT EXTRA INFO
         SPACE 3
FIND$EOF TM    PARMFLG1,P1@SCAN+P1@CHECK  ANALYZING SL TAPE?       U020
         BZ    DATA$FSF                 no - FSF over the data     U037
*---  Keep reading until hit a tapemark (EOF)
         SPACE 1
SCAN$EOF TAPIO TCCW#DAT,TM=SL$SAVE      READ & TALLY A DATA BLOCK
         B     SCAN$EOF                 DO THAT TILL EOF
         SPACE 2
SL$SAVE  MVC   NL#SAVE(NLSTUFFL),NLSTUFF  SAVE ALL INFO            U046
         MVC   NL#FLAGX,NLFLAGS         MORE...
         B     READ$EOF                 GO READ THE EOF LABELS
         SPACE 2
NULLFILE MVC   UNLOADER+3(6),=C'(null)' PUT IN INDICATOR
         B     READ$EOF                 GO GET EOF1/EOF2           U021
         SPACE 2
DATA$FSF TAPIO TCCW#FSF                 FSF PAST REST OF DATA
         SPACE 3
READ$EOF MVI   WHERE,2                  SET "WHERE ARE WE" FLAG
         TAPIO TCCW#L1,TM=MISSEOF1      READ FIRST TRAILER LABEL   U021
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL1LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         CLC   =C'EOF1',FL1LABI         TRAILER LABEL?
         BE    HAVEEOF1                 YES - OK
         CLC   =C'EOV1',FL1LABI         OTHER KIND?
         BE    HAVEEOF1                 YES - OK
      PRTLN '0*** EOF1 or EOV1 label not found where expected ***' U021
         B     REWINDIT                 REWIND AND LEAVE    %%%%%%%%%
         SPACE 2
MISSEOF1 PRTLN '0*** Tapemark read when expecting EOF1 or EOV1 ***' U21
         B     REWINDIT                 REWIND AND LEAVE    %%%%%%%%%
         SPACE 2
MISSEOF2 OI    TFLAG2,T2@DOSTP          REMEMBER NO EOF2           U021
         B     NO$EOF2                  CONTINUE                   U021
         SPACE 2
HAVEEOF1 XC    FL2LABI(80),FL2LABI      IN CASE IT DOESN'T EXIST   U021
         TAPIO TCCW#L2,TM=MISSEOF2      READ TRAILER LABEL         U021
         TM    TFLAG2,T2@ASCII          ASCII STANDARD LABELS?     U023
         BNO   *+10                     NO - SKIP                  U023
         TR    FL2LABI(80),ASCIITAB     YES - TRANSLATE TO EBCDIC  U023
         SPACE 2
*                                       +-----------------------------+
*                                       |  PRINT SL STATS             |
*                                       +-----------------------------+
NO$EOF2  LA    R1,1                     ASSUME 1-LINE GROUPS
         TM    PARMFLG1,P1@CHECK        CHECKING?
         BNO   *+8                      NO - SKIP
         LA    R1,2(,R1)                YES - NEED 2 MORE EACH
         CLI   UNLOADER,C' '            IS THIS FILE UNLOADED?
         BE    *+8                      NO - SKIP
         LA    R1,1(,R1)                YES - NEED 1 MORE LINE
         LR    R0,R1                    COPY FOR STUPID MACRO
         NEWPAGE  (R0)                  CHECK FOR ENOUGH LINES
         SPACE 2
*                                       +-----------------------------+
*                                       |  UNLOADED BY                |
*                                       +-----------------------------+
         MVC   #UNLOAD,UNLOADER         SAY WHO UNLOADED IT
*                                       +-----------------------------+
*                                       |  FILE SEQUENCE NUMBER       |
*                                       +-----------------------------+
         LABCK FL1FILSQ,#FILEU          HDR/EOF MATCH?             U030
         MVC   #FILE,FL1FILSQ           FILE #
         ICALL DEZERO,R14,R1=#FILE      REMOVE LEADING ZEROES
*                                       +-----------------------------+
*                                       |  DATASET NAME               |
*                                       +-----------------------------+
         LABCK FL1ID,#DSNU              HDR/EOF MATCH?             U030
         MVC   #DSN(17),FL1ID           DSN
*                                       +-----------------------------+
*                                       |  PASSWORD PROTECTION INDIC. |
*                                       +-----------------------------+
         LABCK FL1FSEC,#PSWDU,BLANK=YES HDR/EOF MATCH?             U032
         TM    TFLAG2,T2@ASCII          ASCII LABELS?              U023
         BO    ASCII$PW                 YES - DIFFERENT CHECK      U023
         CLI   FL1FSEC,C'0'             PASSWORD PROTECTED?
         BE    NOTPSWD                  NO - SKIP
         CLI   FL1FSEC,C'1'             PASSWORD PROTECTED?
         BE    YESPSWD                  YES - MOVE IT IN
         CLI   FL1FSEC,C'3'             NOPWREAD?
         BNE   WHATPSWD                 NO - WHAT THEN????
*U048    MVC   #PSWD,=C'WRT'            YES
         MVI   #PSWD,C'W'               YES                        U048
         B     NOTPSWD                  CONTINUE
*HATPSWD MVC   #PSWD,=C'???'   *U048    SAY WHAT?
WHATPSWD MVI   #PSWD,C'?'               SAY WHAT?                  U048
         B     NOTPSWD
ASCII$PW CLI   FL1FSEC,C' '             ASCII TAPES HAVE BLANK     U023
         BE    NOTPSWD                  IF NOT, IT'S PROTECTED     U023
*YESPSWD MVC   #PSWD,=C'YES'   *U048    SAY SO.
YESPSWD  MVI   #PSWD,C'R'               SAY SO.                    U048
         SPACE 3
*                                       +-----------------------------+
*                                       |  CREATION DATE              |
*                                       +-----------------------------+
NOTPSWD  LABCK FL1CREDT,#CDATEU         HDR/EOF MATCH?             U030
         ICALL FMT$DATE,LINK,R1=FL1CREDT,R15=#CDATE                U048
*                                       +-----------------------------+
*                                       |  EXPIRATION DATE            |
*                                       +-----------------------------+
NO$CDATE LABCK FL1EXPDT,#EDATEU,BLANK=YES  HDR/EOF MATCH?          U032
         ICALL FMT$DATE,LINK,R1=FL1EXPDT,R15=#EDATE                U048
*                                       +-----------------------------+
*                                       |  BLOCK COUNT                |
*                                       +-----------------------------+
NO$EDATE MVC   #BLKCNT,FL1BLKCT         BLOCK COUNT
         ICALL DEZERO,R14,R1=#BLKCNT    REMOVE LEADING ZEROES
         TM    TFLAG2,T2@DOSTP          HDR2/EOF2 MISSING?         U021
         BNO   OK$RECFM                 NO - FORMAT RECFM,LRECL,BLKU021
         MVC   #CREATOR(18),=C'**** DOS tape ****'                 U021
         IC    R15,DEN@TRUE             get true density           U037
         STC   R15,FL2DEN               PUT HERE ALSO FOR LATER    U021
         B     DOS$DEN                  JUMP INTO THE MIDDLE       U021
         SPACE 2
*                                       +-----------------------------+
*                                       |  RECORD FORMAT              |
*                                       +-----------------------------+
OK$RECFM LABCK FL2RECFM,#RECFMU,BLANK=YES  HDR/EOF MATCH?          U032
         MVC   #RECFM(1),FL2RECFM       RECFM                      U025
         LABCK FL2BLKA,#RECFMU,BLANK=YES  HDR/EOF MATCH?           U032
         MVC   #RECFM+1(1),FL2BLKA      BLOCK ATTRIBUTE
         LABCK FL2CNTRL,#RECFMU,BLANK=YES  HDR/EOF MATCH?          U032
         MVC   #RECFM+2(1),FL2CNTRL     CONTROL CHARACTER
         CLI   #RECFM+1,C'R'            WEIRDO RECFM?  (FR OR VR)
         BNE   NOT$RF$R                 NOT RECFM=FR OR VR
         MVC   #RECFM+3(1),#RECFM+2     MOVE CTL CHAR OVER
         MVC   #RECFM+1(2),=C'BS'       IT'S REALLY FBS OR VBS
*                                       +-----------------------------+
*                                       |  BLOCK SIZE                 |
*                                       +-----------------------------+
NOT$RF$R LABCK FL2BLKL,#BLKSIU          HDR/EOF MATCH?             U030
         MVC   #BLKSIZE,FL2BLKL         BLOCK LENGTH               U030
         ICALL DEZERO,R14,R1=#BLKSIZE   REMOVE LEADING ZEROES      U030
*                                       +-----------------------------+
*                                       |  LOGICAL RECORD LENGTH      |
*                                       +-----------------------------+
         LABCK FL2LRECL,#LRECLU,BLANK=YES  HDR/EOF MATCH?          U032
         MVC   #LRECL,FL2LRECL          RECORD LENGTH
         ICALL DEZERO,R14,R1=#LRECL     REMOVE LEADING ZEROES
*                                       +-----------------------------+
*                                       |  DENSITY                    |
*                                       +-----------------------------+
         LABCK FL2DEN,#DENU             HDR/EOF MATCH?             U030
         IC    R15,FL2DEN               GET TAPE DENSITY
DOS$DEN  STC   R15,#DEN+1               SET IN PRINT LINE
         CLM   R15,B'0001',DEN@TRUE     LABEL CORRECT?             U037
         BE    *+8                      YES - OK
         OI    TFLAG1,T1@BADEN          SET BAD DENSITY FLAG
         TM    PARMFLG2,P2@DEN1         PARM=DEN1?                 U041
         BNZ   DEN$1                    YES - LEAVE 1 CHAR DEN     U041
         CLI   FL2DEN,C'0'              TOO LOW?                   U041
         BL    DEN$1                    YES - SKIP                 U041
         CLI   FL2DEN,C'4'              TOO HIGH for reel or cart? U041
         BH    DEN$1                    YES - SKIP                 U041
         N     R15,=F'15'               JUST DIGIT                 U020
         MH    R15,=Y(DEN#TBLL)         MULTIPLY BY table ent len  U037
         LA    R3,DEN#LIST(R15)         POINT TO DENSITY tbl ent
         L     R1,TAPEMFL               -> TAPEDCB                 U037
         CLI   DCBDEVT-IHADCB(R1),DCBDVMT4  3480?                  U037
         BNE   DEN$OK                   no - ok                    U037
         CLI   FL2DEN,C'0'              TOO HIGH for cartridge?    U041
         BH    DEN$1                    YES - SKIP                 U041
         LA    R3,DEN#3480(R15)         POINT TO DENSITY VALUE     U037
DEN$OK   MVC   #DEN,4(R3)               MOVE CHAR FORM OF DENSITY  U037
         CLI   #DEN,C' '                3 CHAR DENSITY?            U021
         BNE   *+10                     NO - SKIP                  U021
         MVC   #DEN(4),#DEN+1           YES - SHIFT IT OVER        U021
         SPACE 2
DEN$1    TM    TFLAG2,T2@DOSTP          DOS TAPE?                  U021
         BNO   DO$TRTCH                 NO - SKIP                  U021
         MVC   #LENGTH,=C'?????'                                   U021
         MVC   #CUMLEN,=C'?????'                                   U021
         TM    PARMFLG1,P1@SCAN         PARM=SCAN?                 U021
         BNO   DONE$FL2                 NO - SKIP                  U021
         SCALL NL$ATTR                  FORMAT RECFM/LRECL/BLKSIZE U043
         B     DONE$FL2                 SKIP THE REST              U021
         SPACE 1
*                                       +-----------------------------+
*                                       |  TRTCH                      |
*                                       +-----------------------------+
DO$TRTCH LABCK FL2TRTCH,#TRTCHU,BLANK=YES  HDR/EOF MATCH?          U032
         MVC   #TRTCH,FL2TRTCH          TAPE RECORDING TECHNIQUE   U025
*                                       +-----------------------------+
*                                       |  CREATING JOB & STEP NAMES  |
*                                       +-----------------------------+
         LABCK FL2JOBD,#CREATU          HDR/EOF MATCH?             U030
         MVC   #CREATOR,FL2JOBD         CREATOR'S JOB & STEP NAMES
*                                       +-----------------------------+
*                                       |  LENGTH (INCHES)            |
*                                       +-----------------------------+
         PACK  DWD,FL1BLKCT(6)          BLOCK COUNT
         CVB   R15,DWD                  SAVE BLOCK COUNT
         LA    R1,4(,R15)               ADD 4 GAPS TO BLOCK COUNT.
*  NOTE - LENGTH KEPT IN .01 INCHES
         M     R1-1,DEN@GAPL            multiply by gap length     U037
         SPACE 1
         LR    R2,R1                    SAVE LENGTH OF GAPS
         PACK  DWD,FL2BLKL(5)           BLKSIZE.
         CVB   R1,DWD
         MR    R1-1,R15                 BLKSIZE * BLOCK_COUNT = BYTES
         A     R1,=A(4*80)              2 HDR + 2 EOF labels       U036
         TM    PARMFLG1,P1@SCAN         PARM=SCAN?                 U020
         BNO   *+8                      NO - USE COMPUTED          U020
         L     R1,BYTECNT               YES - USE ACTUAL BYTE COUNTU020
*U036    AH    R1,=Y(4*80)              2 HDR & 2 EOF LABELS
         M     R1-1,=F'100'             CONVERT TO .01
         D     R1-1,DEN@BIN             DIVIDE BY DENSITY.         U037
*U037    AH    R1,=Y(375*3)             3 TAPEMARKS @ 3.75 INCH
         AH    R1,DEN@TM                add length of a tapemark   U037
         AH    R1,DEN@TM                add length of a tapemark   U037
         AH    R1,DEN@TM                add length of a tapemark   U037
         AR    R1,R2                    ADD TO LENGTH OF DATA
         LR    R2,R1                    save for later
         ICALL TAPE_LEN,R14,R15=#LENGTH-1 FORMAT FILE LENGTH (FEET)
         A     R2,LEN#TAPE              ADD TO TOTAL LENGTH
         ST    R2,LEN#TAPE              SAVE NEW TOTAL LENGTH
         LR    R1,R2                    COPY FOR SUBROUTINE
         ICALL TAPE_LEN,R14,R15=#CUMLEN-1 FORMAT CUMULATIVE LENGTH
         SPACE 3
DONE$FL2 XR    R2,R2                    CLEAR FLAG                 U030
         CLI   #MSMATCH,C'*'            HDR/EOF CONSISTANT?        U025
         BNE   *+12                     YES, CONTINUE              U030
         OI    TFLAG2,T2@HDRDF          NO, FLAG LINE              U025
         LA    R2,1                     FORCE POSITIVE             U030
         CLI   #BLKFLD,C'#'             HDR BLANK FIELDS?          U030
         BNE   *+8                      NO, CONTINUE               U030
         OI    TFLAG2,T2@HDRBK          YES, FLAG IT               U030
         SPACE 1
         PRTLN OUTBUFF                                             U025
         LTR   R2,R2                    ANYTHING?                  U030
         BZ    DONE$UND                 NO, SKIP UNDERSCORING      U030
         LH    R15,LCTR                 get current line count     U043
         LA    R15,1(,R15)              +1 FOR DECREMENT KLUDGE    U043
         STH   R15,LCTR                 store updated line count   U043
         PRTLN OUTBUFFU                 UNDER SCORE LINE           U030
         MVC   OUTBUFFU,OUTCLRU         CLEAR LINE                 U030
         MVI   OUTBUFFU,C'+'            OVER PRINT NEXT TIME       U030
         SPACE 3
DONE$UND CLC   =C'0001',FL1VOLSQ        FIRST VOLUME OF FILE?      U030
         BE    TEST$EOV                 YES.
         L     R14,=A(FVOL#MSG)         -> MESSAGE                 U022
         MVC   22(4,R14),FL1VOLSQ       VOL SEQ #                  U022
         MVC   80(6,R14),FL1FILSR       FIRST VOLSER               U022
         PRTLN FVOL#MSG,I                                          U022
         SPACE 2
TEST$EOV TM    TFLAG2,T2@DOSTP                                     U046
         BO    NOT$EOV                  EOV.                       U046
         CLC   =C'EOV2',FL2LABI
         BNE   NOT$EOV                  EOV.
         TM    PARMFLG1,P1@CHECK        SL WITH NL ANALYSIS?       U046
         BZ    EOV$MSG                  no - skip                  U046
         SCALL NL$ATTR                  DO NL DCB ATTRIBUTES       U046
         PRTLN OUTBUFF                  BLANK LINE                 U046
EOV$MSG  PRTLN ' *** The above file is continued on another volume ***'
         B     HAVE$EOV                 all done
         SPACE 2
NOT$EOV  TM    PARMFLG1,P1@CHECK        SL WITH NL ANALYSIS?
         BZ    NO$CHECK                 NO - SEE IF SECOND LINE    U047
*U046    MVC   MAXBLKSI(12),NL#SAVE     RESTORE INFO
         MVC   NLSTUFF(NLSTUFFL),NL#SAVE  restore info             U047
         MVC   NLFLAGS,NL#FLAGX         MORE...                    U047
         B     NL$SL                    YES - GO DO IT             U047
*U047    BNZ   NL$SL                    YES - GO DO IT             U046
         SPACE 2
NO$CHECK TM    PARMFLG1,P1@INLIN        INLINE MESSAGES?
         BNO   NEXTFILE                 NO - DO NEXT FILE
         CLI   UNLOADER,C' '            UNLOADED?
         BE    NEXTFILE                 NO - DO NEXT FILE
         PRTLN OUTBUFF2                 PRINT TRUE ATTRS OF UNLOADED DS
         MVC   OUTBUFF2,OUTCLR2         CLEAR OTHER PRINT LINE
         B     NEXTFILE                 DO NEXT FILE
         SPACE 3
HAVE$EOV PRTLN '-     *** EOV ***'
         OC    LCTR2,LCTR2              ANYTHING ON OTHER PAGE?    U023
         BZ    NO$EOV2                  NO - SKIP
         MVC   OUTBUFF2(17),=C'-     *** EOV ***'
         PRTLN2                         FLAG END OF OTHER PRINT FILE
         SPACE 1
NO$EOV2  TM    TFLAG1,T1@BADEN          ANY INCORRECT DENSITIES?
         BNO   NO$BADEN                 NO - SKIP
         LA    R0,9                     ASSUME NOT PARM=NONOTE
         TM    PARMFLG2,P2@NONOT        WANT THE NOTE?             U020
         BNO   *+8                      YES - SO USE 9
         LA    R0,6                     PARM=NONOTE, SO USE 6
         NEWPAGE  (R0)                  GO TO TOP OF PAGE IF NEEDED
         LH    R15,LCTR                 get current line count     U043
         LA    R15,1(,R15)              WILL CALL PRTLN EXTRA TIME U043
         STH   R15,LCTR                 store updated line count   U043
         L     R1,=A(INVDEN)            -> INVALID DENSITY MSG     U022
         MVC   OUTBUFF(113),0(R1)       MOVE IN MESSAGE            U022
         MVC   OUTBUFF+98(4),DEN@CHAR   4 char true den to prt lin U037
         PRTLN OUTBUFF
         MVI   OUTBUFF,C'+'             SET FOR OVERPRINT
         MVC   OUTBUFF+98(4),DEN@CHAR   light up the density       U040
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE
*U043    BAL   R14,PUTPRTLN             PRINT IT ONCE (NO CLEAR OUTBUFF
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  print it once              U043
         PRTLN OUTBUFF                  AND THE THIRD TIME
         SPACE 2
NO$BADEN TM    PARMFLG2,P2@NONOT        WANT THE NOTE?             U020
         BO    REWINDIT                 NO - SO DON'T
         NEWPAGE  4                     ENOUGH LINES LEFT ON PAGE?
         PRTLN DENWARN,I                MSG LIVES ELSEWHERE        U022
         TM    TFLAG1,T1@ANLZ           ANALYZING?
         BO    TST$HDR                  YES - GOOD LENGTHS PRINTED
         PRTLN LENWARN,I                MSG LIVES ELSEWHERE        U022
         SPACE 2
TST$HDR  TM    TFLAG2,T2@HDRDF          HDR/EOF MATCH?             U025
         BZ    TST$HDRB                 YES, CONTINUE              U030
         PRTLN HDRFOOT1,I               NO, TELL WHAT MESSAGE      U025
         PRTLN HDRFOOTU,I               LINE 2 AS WELL.            U030
         NI    TFLAG2,X'FF'-T2@HDRDF    RESET MISMATCH INDICATOR   U025
         SPACE 2
TST$HDRB TM    TFLAG2,T2@HDRBK          BLANK FIELDS IN HDR?       U030
         BZ    REWINDIT                 NO, CONTINUE               U030
         PRTLN HDRFOOT2,I               SEND MSG                   U030
         NI    TFLAG2,X'FF'-T2@HDRBK    RESET FLAG                 U030
         SPACE 2
REWINDIT TAPIO TCCW#RWD                 REWIND THE TAPE
         SPACE 2
CLOSE$TP CLOSE MF=(E,TAPEMFL)
         SPACE 2
RETURN   L     R14,RET#ADDR             GET RETURN ADDR
         BR    R14
         SPACE 2
UNEXTPMK CLI   WHERE,3                  READING DATA?              U020
         BE    UNEXTM02                 YES                        U020
         PRTLN '0*** Unexpected tape mark ***'
         B     REWINDIT                 CLOSE UP & LEAVE
         SPACE 1
UNEXTM02 IFP2  Y,UNEXTM03               PRINT MSG IF 2ND PRINT FILEU020
*  OTHERWISE, JUST IGNORE THE ERROR                                U020
         TM    TFLAG1,T1@SL             SL TAPE?                   U020
         BO    SL$SAVE                  YES                        U020
         B     NL$EOF                   NO                         U020
         SPACE 1
UNEXTM03 PRTLN2                         PRINT POSSIBLE PARTIAL BUF U020
         MVC   OUTBUFF2(29),=C'0*** Unexpected tape mark ***'      U020
         MVC   OUTBUFF2+26(50),=C'while reading data.  File is probably$
                unusable ***'                                      U020
         PRTLN2                                                    U020
         OI    TFLAG1,T1@MVEOF          INDICATE HIT EOF           U020
         B     STAR$TWO                                            U020
         SPACE 2
LEADTPMK NEWPAGE  ,                     PRINT THE HEADER LINES
         PRTLN '0++++ Tape has leading tape mark ++++'
         LH    R0,LCTR                  get current line count     U043
         BCTR  R0,0                     acct for extra blank line  U043
         STH   R0,LCTR                  store updated line count   U043
         PRTLN OUTBUFF                  BLANK LINE
         MVI   VOLLABI,C'X'             INSURE NL ROUTINE
         MVI   FILE#SEQ+1,1             INIT FILE SEQUENCE NUMBER  U014
         MVI   NUMBNULL+1,1             INIT NUMBER OF NULL FILES  U014
         TAPIO TCCW#DAT,TM=LEAD$EOV     READ FIRST BLOCK FROM FILE U014
         B     DO$SENSE                 DO NL ANALYSIS
         SPACE 2
LEAD$EOV MVI   NUMBNULL+1,2             SET NUMBER OF NULL FILES   U014
         CLI   NULLNUMB+1,2             TOO MANY?                  U014
         BL    HAVE$EOV                 YES - STOP                 U014
         MVC   #UNLOAD+3(6),=C'(null)'  FLAG IT                    U014
         MVI   #FILE+3,C'1'             SET FOR PRINT              U014
         MVI   FILE#SEQ+1,2             INIT COUNTER               U014
         PRTLN OUTBUFF                                             U014
         B     DO$SENSE                                            U014
         SPACE 2
IEHINITT PRTLN '-++++ Tape has been re-labelled with IEHINITT ++++'
         B     SHOWDEN                                             U021
         SPACE 2
*---  FIRST HDR1 LABEL IS MISSING.                                 U021
MISSHDR1 PRTLN '-++++ Tape contains volume label only ++++'        u021
         SPACE 1
SHOWDEN  MVC   OUTBUFF(34),=C'-***** This tape is XXXX BPI *****'  U021
         MVC   OUTBUFF+20(4),DEN@CHAR   4 char true den to prt lin U037
         PRTLN OUTBUFF                                             U021
         MVI   OUTBUFF,C'+'             SET FOR OVERPRINT          U021
         MVC   OUTBUFF+20(4),DEN@CHAR   light up the density       U040
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE       U021
*U043    BAL   R14,PUTPRTLN             PRINT IT ONCE (NO CLEAR)   U021
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  print it once (no clear)   U043
         PRTLN OUTBUFF                  AND THE THIRD TIME         U021
         B     REWINDIT                 ...
         EJECT
TEST$MV  CLC   =C'UNLOADED',TAPEBUFF+16 IEHMOVE'S TRADEMARK?
         BNER  LINK                     NO
         MVC   UNLOADER(7),IEHMOVE      SAY WHO
         SPACE 1
TEST$RET IFP2  Y,4(,LINK)               RETURN POINT FOR 2 PRINT FILES
         B     8(,LINK)                 RETURN POINT FOR 1 PRINT FILE
         SPACE 2
TEST$CPY CLC   =X'00CA6D0F',TAPEBUFF+8  VS2COPY'S TRADEMARK?
         BNER  LINK                     NO
         MVC   UNLOADER(7),IEBCOPY      SAY WHO
         B     TEST$RET                 RETURN
         SPACE 2
TEST$UPS ST    LINK,DWD                 SAVE MY RETURN ADDR
         LA    R2,80                    BXLE INCR
         LA    R15,TAPEBUFF             BXLE START
         XR    R3,R3                    CLEAR FOR ICM              U032
         ICM   R3,B'0011',LASTSIZE      LOAD LEN READ              U032
         AR    R3,R15                   POINT PAST END             U032
         SR    R3,R2                    POINT TO LAST CARD IN BLOCK
         SPACE 1
TEST$UPL LR    R1,R15                   COPY CARD START ADDR
         BAL   LINK,TEST$UP             IS THIS IEBUPDTE CTL CARD?
         B     TEST$UPB                 NO - GO BXLE
         MVC   UNLOADER(8),IEBUPDTE     SAY WHAT
         L     LINK,DWD                 GET BACK MY RETURN ADDR
         B     TEST$RET                 AND RETURN TO CALLER
         SPACE 1
TEST$UPB BXLE  R15,R2,TEST$UPL          TRY ALL CARDS IN THIS BLOCK
         L     LINK,DWD                 RESTORE RETURN ADDR
         BR    LINK                     NOT IEBUPDTE INPUT STREAM
         SPACE 2
TEST$UP  CLC   =C'./',0(R1)             START RIGHT?
         BNER  LINK                     NO - THAT WAS QUICK
         LA    R1,2(,R1)                POINT TO SCAN START
         LA    R0,69                    MAX SCAN LENGTH
         BAL   R14,F$BLANK              FIND END OF NAME FIELD
         BAL   R14,F$CHARS              FIND THE VERB
         CLC   =C'ADD ',0(R1)
         BE    TU$SAVE
         CLC   =C'REPL ',0(R1)
         BE    TU$SAVE
         CLC   =C'CHANGE ',0(R1)
         BE    TU$SAVE
         BR    LINK                     NOT IEBUPDTE VERB
         SPACE 2
TU$SAVE  MVC   UPDT#TYP(1),0(R1)        A,R,C
         SPACE 1
TU$LOOP  LA    R1,1(,R1)
         CLC   =C'MEMBER=',0(R1)
         BE    TU$MEM
         CLC   =C'NAME=',0(R1)
         BE    TU$NAM
         BCT   R0,TU$LOOP               SCAN THE REST OF THE CARD
         BR    LINK                     NOTHING INTERESTING ON CARD
         SPACE 2
TU$MEM   LA    R1,7(,R1)                POINT TO MEMBER NAME
         B     *+8                      SKIP OTHER LA
         SPACE 1
TU$NAM   LA    R1,5(,R1)                POINT TO MEMBER NAME
         SPACE 1
         MVI   8(R1),C','               BE SURE TO STOP
         B     4(,LINK)                 FOUND A NAME
         SPACE 2
F$BLANK  BCTR  R0,0                     -1 FROM LENGTH
         CLI   0(R1),C' '               GOT A BLANK YET?
         BER   R14                      YES - RETURN
         LA    R1,1(,R1)                MOVE SCAN PTR
         BCT   R0,F$BLANK+2             KEEP SCANNING
         BR    LINK                     NONE--RETURN TO CALLER'S CALLER
         SPACE 2
F$CHARS  BCTR  R0,0                     -1 FROM LENGTH
         CLI   0(R1),C' '               FIND A CHAR?
         BNER  R14                      YES - RETURN
         LA    R1,1(,R1)                INCR SCAN PTR
         BCT   R0,F$CHARS+2             KEEP SCANNING
         BR    LINK                     NONE--RETURN TO CALLER'S CALLER
         SPACE 2
TEST$DMP CLC   =X'F47006016663B24D',TAPEBUFF+12  IEHDASDR'S TRADEMARK?
         BNER  LINK                     NO
         MVC   UNLOADER(8),IEHDASDR     SAY WHO
         MVC   DASDRSAV(24),TAPEBUFF    SAVE HEADER RECORD
         LR    R2,LINK                  SAVE RETURN ADDR
         TAPIO TCCW#DAT                 IGNORE CCWS
         SPACE 1
COM$DUMP TAPIO TCCW#DAT                 READ FIRST TRK'S DATA
         B     4(,R2)                   RETURN
         SPACE 2
TEST$DSS DC    0H'0'                                               U034
         ST    LINK,DSSLINK             SAVE RETURN ADDRESS        U034
         CLC   =H'100',LASTSIZE         BLOCK SHORT ENOUGH?        U034
         BLR   LINK                     NO, CAN'T BE DF/DSS        U034
         CLC   =H'62',LASTSIZE          BLOCK LONG ENOUGH?         U034
         BHR   LINK                     NO, CAN'T BE DF/DSS        U034
         CLC   =BL4'1',TAPEBUFF         SEQ NUM FOR 1ST BLOCK?     U034
         BNER  LINK                     NO, CAN'T BE DF/DSS        U034
         CLC   LASTSIZE,TAPEBUFF+6      SIZE READ MATCHES CTL REC? U034
         BNER  LINK                     NO, CAN'T BE DF/DSS        U034
         LA    R1,0                                                U034
         IC    R1,TAPEBUFF+50           GET DEVICE UNIT TYPE       U034
         MH    R1,=Y(DEVTABXL)          TIMES LEN OF DEVTABX ENTRY U039
         A     R1,=A(DEVTABX)           POINT TO DEVICE TABLE      U039
         MVC   DSSDEVTY,4(R1)           SAVE DEVICE TYPE           U039
         MVC   DSSDEVSZ,TAPEBUFF+24     SAVE DEVICE SIZE           U034
         LH    R1,DSSDEVTT              GET TRACKS PER CYLINDER    U034
         ST    R1,TRKCYL                  AND PUT IN WORK FIELD    U034
         MVC   DSSVOLID,TAPEBUFF+32     SAVE SERIAL NUMBER         U034
         MVC   DSSVER,TAPEBUFF+59       SAVE VERSION#              U034
         MVC   DSSREL,TAPEBUFF+58       SAVE RELEASE#              U034
         TM    DSSVER,X'F0'                                        U034
         BNZR  LINK                     NO, CAN'T BE DF/DSS        U034
         TM    DSSREL,X'F0'                                        U034
         BNZR  LINK                     NO, CAN'T BE DF/DSS        U034
         OI    DSSVER,X'F0'                                        U034
         OI    DSSREL,X'F0'                                        U034
         CLI   DSSVER,C'1'              VERSION 1?                 U034
         BLR   LINK                     NO, CAN'T BE DF/DSS        U034
         CLI   DSSREL,C'2'              RELEASE 2?                 U034
         BLR   LINK                     NO, CAN'T BE DF/DSS        U034
         TAPIO TCCW#DAT,TM=BOOM         READ THE 2ND BLOCK         U034
         CLC   =C'BMBB',TAPEBUFF+16     DSS BIT MAP?               U034
         BE    TEST$DS1                 YES, IT'S DF/DSS           U034
         L     R1,BLOCKCNT              ADJUST THE                 U034
         BCTR  R1,0                       BLOCK COUNT FOR          U034
         ST    R1,BLOCKCNT                  A BACK SPACE BLOCK     U034
         L     R1,BYTECNT               ADJUST THE                 U034
         SH    R1,LASTSIZE                BYTE COUNT FOR           U034
         ST    R1,BYTECNT                   A BACK SPACE BLOCK     U034
         LA    R0,TCCW#BSB              RE-POSITION THE TAPE       U034
         ST    R0,TAPEIOB+16                                       U034
         EXCP  TAPEIOB                  BACK SPACE ONE BLOCK       U034
         WAIT  ECB=TAPEECB                                         U034
         EXCP  TAPEIOB                  BACK SPACE ONE BLOCK       U034
         WAIT  ECB=TAPEECB                                         U034
         TAPIO TCCW#DAT                 RE-READ THE 1ST BLOCK      U034
         L     R1,BLOCKCNT              ADJUST THE                 U034
         BCTR  R1,0                       BLOCK COUNT FOR          U034
         ST    R1,BLOCKCNT                  A BACK SPACE BLOCK     U034
         L     R1,BYTECNT               ADJUST THE                 U034
         SH    R1,LASTSIZE                BYTE COUNT FOR           U034
         ST    R1,BYTECNT                   A BACK SPACE BLOCK     U034
         L     LINK,DSSLINK             GET  RETURN ADDRESS        U034
         BR    LINK                     CAN'T BE DF/DSS            U034
TEST$DS1 DC    0H'0'                                               U034
         LH    R1,TAPEBUFF+24           # OF TRACKS IN THIS MAP    U034
         SRL   R1,3                     # OF BYTES IN BITMAP       U034
         LA    R1,1(R1)                                            U034
         STH   R1,BITMAPLN                                         U034
         LR    R0,R1                                               U034
         GETMAIN R,LV=(0)               GET STORAGE FOR BITMAP     U034
         ST    R1,BITMAP                  AND SAVE THE POINTER     U034
         LA    R1,0                     CLEAR REGISTER             U034
         IC    R1,TAPEBUFF+8              AND INSERT PREFIX LENGTH U034
         LA    R0,12(R1)                ADD BITMAP EXT LENGTH      U034
         LA    R2,TAPEBUFF                                         U034
         AR    R0,R2                    POINT TO THE BITMAP        U034
         L     R2,BITMAP                                           U034
         LH    R3,BITMAPLN                                         U034
         LR    R1,R3                                               U034
         MVCL  R2,R0                    SAVE THE BITMAP            U034
         CLC   =F'0',TAPEBUFF+20        ANOTHER BITMAP SEGMENT?    U034
         BZ    TEST$DS3                                            U034
         OI    TFLAG3,T3@DSSBO          INDICATE NEED OF MSG       U046
TEST$DS3 MVC   UNLOADER(3),DSS          SAY WHO                    U034
         MVI   UNLOADER+4,C'V'          SET VERSION PREFIX         U034
         MVC   UNLOADER+5(1),DSSVER     SET VERSION NUMBER         U034
         MVI   UNLOADER+6,C'.'                                     U034
         MVC   UNLOADER+7(1),DSSREL     SET RELEASE NUMBER         U034
         L     LINK,DSSLINK             GET  RETURN ADDRESS        U034
         B     TEST$RET                 RETURN W/DSS               U034
         SPACE 2
TEST$FDR CLC   =C'THATS ALL FOLK',TAPEBUFF+5    HIS TRADEMARK?     U034
         BNER  LINK                     NO
         CLI   TAPEBUFF+19,C'S'         OLD STYLE FDR?             U032
         BE    *+4+4+2+6                YES, CONTINUE              U032
         CLI   TAPEBUFF+19,C'F'         NEW STYLE FDR?             U032
         BNER  LINK                     NO, RETURN TO CALLER       U032
         MVC   UNLOADER+3(4),=C'/ABR'   YES, ABR DUMP              U032
         MVC   UNLOADER(3),FDR          SAY WHO
         B     TEST$RET                 RETURN W/FDR               U022
         SPACE 2
* FOR IEBISAM, THE BEGINNING OF THE DCB IS UNLOADED TO THE FIRST
* TWO CARDS.  SO CHECK (GUESS?) IF IT LOOKS LIKE AN ISAM DCB
TEST$IS  CLC   TAPEBUFF(4),=Y(0,154)    SEQ # AND LENGTH CORRECT?
         BNER  LINK                     NO
         CLC   TAPEBUFF+80(2),=H'1'     SECOND SEQ # CORRECT       U020
         BNER  LINK                     NO
         TM    TAPEBUFF+4+48,X'10'      DCBOFLGS,DCBOFOPN
         BNOR  LINK                     NOT "OPEN"
         CLI   TAPEBUFF+4+26,X'80'      DSORG=IS?
         BE    *+10                     YES - CONTINUE
         CLI   TAPEBUFF+4+26,X'81'      DSORG=ISU?
         BNER  LINK                     NO
         MVC   UNLOADER(7),IEBISAM      SAY WHO
         B     TEST$RET                 RETURN
         EJECT
IEHMOVE1 MVC   #BLKCNT+OUTBUFF2-OUTBUFF(4),=C'DSN='
         MVC   #BLKCNT+OUTBUFF2-OUTBUFF+4(44),TAPEBUFF+85 MOVE DISK DSN
         SPACE 1
         BAL   LINK,SET$ONE
         DC    Y(169)                   DSORG OFFSET
         DC    Y(171)                   RECFM OFFSET
         DC    Y(173)                   BLKSIZE OFFSET
         DC    Y(175)                   LRECL OFFSET
         SPACE 3
IEBCOPY1 BAL   LINK,SET$ONE
         DC    Y(12)                    DSORG OFFSET
         DC    Y(18)                    RECFM OFFSET
         DC    Y(14)                    BLKSIZE OFFSET
         DC    Y(16)                    LRECL OFFSET
         SPACE 3
IEBISAM1 BAL   LINK,SET$ONE
         DC    Y(4+26)                  DSORG OFFSET
         DC    Y(4+36)                  RECFM OFFSET
         DC    Y(4+62)                  BLKSIZE OFFSET
         DC    Y(4+2+82)                LRECL OFFSET
         SPACE 3
*  FIRST RECORD SAVED IN "DASDRSAV"
IHDASDR1 MVC   OUTBUFF2+8(15),=C'Dump from CCHH='
         UNPK  OUTBUFF2+23(9),DASDRSAV+0(5)
         TR    OUTBUFF2+23(8),HEXTAB    MAKE EBCDIC
         MVC   OUTBUFF2+31(5),=C' of a'
         ICALL CONVUNIT,LINK,R1=DASDRSAV+21  GET UNIT TYPE         U022
         MVC   OUTBUFF2+37(6),OUTBUFF2+125  MOVE TO CORRECT SPOT   U022
         MVC   OUTBUFF2+120(12),BLANKS  CLEAR OUT CONVERTED FLD    U022
         CLC   =F'0',DASDRSAV+8         THIS TAPE START AT 0?
         BNE   PRT$ONE                  NO - CAN'T FIND VOLSER
*  SECOND RECORD IS IN "TAPEBUFF"
         CLC   =C'VOL1VOL1',TAPEBUFF+216  LABEL?
         BNE   PRT$ONE                  NO - SKIP
         MVC   OUTBUFF2+46(7),=C'Volume='
         MVC   OUTBUFF2+53(6),TAPEBUFF+224
         MVC   OUTBUFF2+61(6),=C'Owner='
         MVC   OUTBUFF2+67(10),TAPEBUFF+261
         B     PRT$ONE
         SPACE 3
DSS1     DC    0H'0'                                               U034
         B     PRT$ONE                  YES, AND GET OUT           U034
         SPACE 3
FDR1     ICALL CONVUFDR,LINK,R1=TAPEBUFF+4  FORMAT UNIT TYPE       U022
         TAPIO  TCCW#DAT                GET NEXT BLOCK             U022
         CLC   =C'DUMMYDSF',TAPEBUFF+12 DUMMY DSF BLOCK?           U022
         BE    FDR1LOOP                 YES, CONTINUE CHECKING     U022
         CLC   UNLOADER+3(5),BLANKS     ANY VERSION YET?           U032
         BNE   *+10                     YES, SKIP UPDATE           U032
         MVC   UNLOADER+3(5),=C'-CCHH'  DSF DOING CCHH DUMP(FDR 4?)U022
         B     PRT$ONE                  YES, AND GET OUT           U022
         SPACE 1
FDR1LOOP TAPIO  TCCW#DAT                GET NEXT BLOCK             U022
         CLC   =C'DUMMYDSF',TAPEBUFF+12 STILL DUMMY VTOC?          U022
         BNE   PRT$ONE                  NO, EXIT FROM HERE         U022
         CLC   =C'VSAMDFEF',TAPEBUFF+32 ICF INFORMATION?           U033
         BE    FDR1LOOP                 YES, SKIP IT               U033
         CLC   =C'TRACKTAB',TAPEBUFF+32 FDR VERSION 4.5?           U022
         BNE   FDR1DFEF                 NO, CHECK VVDS             U032
         CLC   UNLOADER+4(4),BLANKS     ANY VERSION YET?           U032
         BNE   PRT$ONE                  YES, SKIP UPDATE           U045
         MVC   UNLOADER+4(4),=C'V4.5'   YES, LABEL AS SUCH         U045
         B     PRT$ONE                  AND EXIT                   U022
         SPACE 1
FDR1DFEF CLC   =C'VSAMDFEF',TAPEBUFF+32 VVDS INFO BLOCK?           U032
         BNE   FDR1LOOP                 NO, CONTINUE               U032
         MVC   UNLOADER+4(4),=C'V4.8'   SET VERSION NUMBER         U032
         B     PRT$ONE                  AND EXIT                   U032
         EJECT
SET$ONE  MVC   OUTBUFF2+8(24),=C'True attributes:  DSORG='
         LA    R1,TAPEBUFF              -> TAPE BUFFER             U022
         AH    R1,0(,LINK)              + OFFSET TO DSORG          U022
         ICALL CNVDSORG,R14,R15=OUTBUFF2+32 FORMAT THE DSORG
         SPACE 1
         LA    R1,TAPEBUFF              -> TAPE BUFFER             U022
         AH    R1,2(,LINK)              + OFFSET TO RECFM          U022
         ICALL CNVRECFM,R14,R15=#RECFM+OUTBUFF2-OUTBUFF
         SPACE 1
         LA    R1,TAPEBUFF
         AH    R1,4(,LINK)              POINT TO BLKSIZE
         ICALL CONVHALF,R14,R15=#BLKSIZE-1+OUTBUFF2-OUTBUFF        U030
         SPACE 1
         LA    R1,TAPEBUFF
         AH    R1,6(,LINK)              POINT TO LRECL
         ICALL CONVHALF,R14,R15=#LRECL-1+OUTBUFF2-OUTBUFF
         SPACE 2
PRT$ONE  TM    TFLAG1,T1@ANLZ           ANALYZING?
         BNO   FIND$EOF                 NO
         B     NL$LOOP                  YES - KEEP SCANNING NL TAPE
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  ROUTINES TO FORMAT INFORMATION TO SYSPRNT2 FOR UNLOADED FILES      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
IEHMOVE2 LA    R0,7                     ASSUME PARM=NOMEM
         TM    PARMFLG1,P1@NOMEM        IS IT?
         BO    *+8                      YES
         LA    R0,9                     NO
         TM    TAPEBUFF+169,X'02'       DSORG=PO?
         BO    *+8                      YES
         LA    R0,7                     NO
         NEWPAGE  (R0),2                ENOUGH LINES LEFT ON PAGE?
         SPACE 1
         BAL   LINK,SET$FULL            SET UP HEADER
         DC    Y(169)                   DSORG OFFSET
         DC    Y(171)                   RECFM OFFSET
         DC    Y(173)                   BLKSIZE OFFSET
         DC    Y(175)                   LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEHMOVE   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,LINK,R1=TAPEBUFF+282 CONVERT UNIT TYPE
         SPACE 1
         PRTLN2                         PRINT FIRST INFO LINE
         SPACE 1
         MVI   OUTBUFF2,C'0'            DOUBLE SPACE FOR MSM
         MVC   OUTBUFF2+25(7),=C'C-date='
         ICALL CONVDATE,LINK,R1=TAPEBUFF+138,R15=OUTBUFF2+32 C-DATE
         SPACE 1
         MVC   OUTBUFF2+42(7),=C'E-date='
         ICALL CONVDATE,LINK,R1=TAPEBUFF+141,R15=OUTBUFF2+49 E-DATE
         SPACE 1
         MVC   OUTBUFF2+59(4),=C'DSN='
         MVC   OUTBUFF2+63(44),TAPEBUFF+85       MOVE IN OLD DSN
         SPACE 1
****     CLC   =C'VS2',TAPEBUFF+152     CHECK PROGRAMMING SYSTEM
****     BE    MV$NOVOL                 SKIP IF OS/VS2
         TM    TAPEBUFF+132,X'0F'       SEE IF LOOKS LIKE PACKED DATE
         BO    MV$NOVOL                 SKIP IF NEW FORMAT IEHMOVE DS
         CLI   TAPEBUFF+135,0           INVALID FOR VOLSER ANYWAY?
         BE    MV$NOVOL                 SKIP IF NEW FORMAT IEHMOVE DS
         MVC   OUTBUFF2+120(4),=C'Vol='
         MVC   OUTBUFF2+124(6),TAPEBUFF+130
         SPACE 1
MV$NOVOL PRTLN2                         PRINT SECOND INFO LINE
         SPACE 2
         TM    TAPEBUFF+169,X'02'       DSORG=PO?
         BNO   STAR$TWO                 NO - SKIP
         BAL   LINK,FMU$                PRINT HEADER
         SPACE 2
         XR    BLEN,BLEN                CLEAR FOR ICM              U032
         ICM   BLEN,B'0011',LASTSIZE    GET SIZE OF BLOCK          U032
         SH    BLEN,=H'80'              MINUS FIRST CARD USED
         LA    BPTR,TAPEBUFF+80         -> NEXT CARD TO USE
         BAL   LINK,SEG$NEXT            NOW HAVE CARDS 1 & 2
         BAL   LINK,SEG$NEXT            NOW 2 & 3
         BAL   LINK,SEG$NEXT            NOW 3 & 4
         BAL   LINK,SEG$NEXT            NOW 4 & 5
         LA    DPTR,MV#BUFF1+46         -> FIRST MEMBER NAME -4
         CLI   0(DPTR),X'C8'            START OF MEMBER?
         BE    MV$GOT1                  YES
         LA    DPTR,3(,DPTR)            -> OTHER PLACE
         CLI   0(DPTR),X'C8'            START OF MEMBER?
         BE    MV$GOT1                  YES
         BAL   R14,BOOM                 PRINT LOGIC ERROR MSG
         SPACE 2
MV$GOT1  LA    DPTR,4(,DPTR)            -> FIRST MEMBER NAME
         SPACE 2
MV$LOOP  BAL   LINK,MOVE$MEM            MEMBER NAME TO PRINT LINE
         BAL   LINK,SEG$TEST            SEE IF WITHIN RANGE
         SPACE 2
*  RECORDS IN AN IEHMOVE UNLOADED DATASET HAVE THE FOLLOWING FORMAT:
*  FIRST 2 BYTES OF EACH CARD IS A BINARY SEQUENCE NUMBER.
*  AFTER THAT, THERE IS SOME HEADER INFO (THE FMT1 DSCB + OTHER JUNK),
*  THEN THE RECORDS IN THIS FORMAT:  LENGTH OF THIS "RECORD", 1 BYTE
*  INDICATOR WITH THE FOLLOWING BIT MEANINGS:
*    X'80' -> 3 BYTE TTR FOLLOWS INDICATOR BYTE
*    X'40' -> UNLOADED DS IS PDS
*    X'20' -> RECORD IS PART OF A MEMBER
*    X'10' -> RECORD IS A NOTE LIST
*    X'08' -> RECORD IS A DIRECTORY ENTRY
*    X'04' -> RECORD IS A DUMMY RECORD
*  THIS INFORMATION IS IN THE SOURCE TO MODULE IEHMVSRA
         SPACE 1
MV$NXTBL CLI   2(DPTR),X'E0'            DATA RECORD?
         BE    MV$REC                   YES
         CLI   2(DPTR),X'D0'            NOTE LIST RECORD?
         BE    MV$REC                   YES
         CLI   2(DPTR),X'C8'            DIRECTORY RECORD?
         BE    MV$MEND                  YES
         CLI   2(DPTR),X'C4'            DUMMY RECORD?
         BE    MV$DUMMY                 YES
         CLI   2(DPTR),X'0A'            END OF DS?
         BNH   DIR$END                  YES
*** LOGIC ERROR ***
         PRTLN2                         PRINT (POSSIBLY) UNFINISHED LIN
         BAL   R14,BOOM                 PRINT LOGIC ERROR MSG
         SPACE 2
MV$DUMMY MVC   DWD(2),0(DPTR)           COPY LENGTH TO ALIGNED PLACE
         LH    DLEN,DWD                 LOAD LENGTH OF DUMMY RECORD
         LA    DPTR,12(DPTR,DLEN)       POINT TO NEXT
         BAL   LINK,SEG$TEST            MAKE SURE STILL WITHIN RANGE
         B     MV$LOOP                  DO NEXT MEMBER NAME
         SPACE 2
MV$REC   MVC   DWD(2),0(DPTR)           COPY LEN TO ALIGNED PLACE
         LH    DLEN,DWD                 GET BLOCK LENGTH
         LA    DPTR,6(DPTR,DLEN)        POINT PAST LEN,E0,TTR,DATA
         SPACE 2
MV$SCAN  CL    DPTR,MV#ABUF2            WITHIN RANGE?
         BL    MV$NXTBL                 YES - PROCESS NEXT BLOCK
         BAL   LINK,SEG$NEXT            SKIP 78 BYTES
         B     MV$SCAN                  KEEP GOING
         SPACE 2
MV$MEND  LA    DPTR,6(,DPTR)            -> MEMBER NAME
         BAL   LINK,SEG$TEST            WITHIN RANGE?
         B     MV$LOOP                  GO PRINT NEXT NAME
         SPACE 2
SEG$TEST CL    DPTR,MV#ABUF2            WITHIN RANGE?
         BLR   LINK                     YES - JUST RETURN
         SPACE 1
SEG$NEXT MVC   MV#BUFF1,MV#BUFF2
         LTR   BLEN,BLEN                ANY LEFT?
         BP    SEG$MVC                  YES - USE IT
         BZ    *+8                      NO
* RAN OFF END OF DATASET
***      EX    0,*                      *** LOGIC ERROR ***
         B     DIR$END                  FORGET THAT - IT'S THE END
         SPACE 1
         ST    LINK,LINKSAVE            SAVE RETURN ADDR
         TAPIO TCCW#DAT,TM=SEG$EOF      READ ANOTHER BLOCK
         L     LINK,LINKSAVE            RESTORE RETURN ADDR
         LA    BPTR,TAPEBUFF            RESET PTR
         XR    BLEN,BLEN                CLEAR FOR ICM              U032
         ICM   BLEN,B'0011',LASTSIZE    AND LENGTH LEFT            U032
         SPACE 1
SEG$MVC  SH    BLEN,=H'80'              DECR LENGTH LEFT / THIS BLOCK
         MVC   MV#BUFF2,2(BPTR)         MOVE A SEGMENT
         LA    BPTR,80(,BPTR)           -> NEXT SEGMENT
         SH    DPTR,=H'78'              BACK UP DATA PTR
         BR    LINK                     RETURN TO CALLER
         SPACE 2
SEG$EOF  BCTR  BLEN,0                   SET FLAG
         L     LINK,LINKSAVE            RESTORE RETURN ADDR
         OI    TFLAG1,T1@MVEOF          REMEMBER HIT EOF
         BR    LINK                     RETURN TO CALLER
         SPACE 3
CONVDATE OC    0(3,R1),0(R1)            ANY DATA TO PRINT?         U022
         BZR   LINK                     NO, RETURN TO CALLER       U022
         SR    R0,R0                    CLEAR FOR IC               U022
         IC    R0,0(,R1)                GET THE YEAR
         CVD   R0,DWD                   CONVERT TO PACKED
         UNPK  0(3,R15),DWD+6(2)        AND THEN TO EBCDIC
         OI    2(R15),C'0'              FIX SIGN
         MVC   0(2,R15),1(R15)          MOVE YEAR OVER
         MVI   2(R15),C'.'              PUT IN THE DOT
         MVC   DWD(2),1(R1)             MOVE DAY TO ALIGNED PLACE
         LH    R0,DWD                   GET THE YEAR
         CVD   R0,DWD                   CONVERT TO PACKED
         UNPK  3(3,R15),DWD+6(2)        AND THEN TO EBCDIC
         OI    5(R15),C'0'              FIX THE SIGN
         BR    LINK                     RETURN TO CALLER
         EJECT
IEBCOPY2 LA    R0,7                     ASSUME MEMBERS
         TM    PARMFLG1,P1@NOMEM        PARM=NOMEMBERS?
         BNO   *+8                      NOPE - OK
         LA    R0,5                     NO MEMBERS
         NEWPAGE  (R0),2                TEST LINE COUNTER
         SPACE 1
         BAL   LINK,SET$FULL            SET UP HEADER
         DC    Y(12)                    DSORG OFFSET
         DC    Y(18)                    RECFM OFFSET
         DC    Y(14)                    BLKSIZE OFFSET
         DC    Y(16)                    LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEBCOPY   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,LINK,R1=TAPEBUFF+24 FORMAT THE UNIT TYPE
         SPACE 2
         BAL   LINK,FMU                 'FOLLOWING MEMBERS UNLOADED:'
         SPACE 2
         TAPIO TCCW#DAT                 IGNORE THE NEXT BLOCK
         SPACE 2
IBCPY$LP TAPIO TCCW#DAT                 READ A DIRECTORY BLOCK
         LH    BLEN,TAPEBUFF+4          GET RDW
         SH    BLEN,=H'8'               -8 FOR BDR & RDW
         LA    BPTR,TAPEBUFF+8          INIT BLOCK PTR
         SPACE 2
DIR$NEXT LA    DPTR,22(,BPTR)           INIT DIR BLK PTR
         LH    DLEN,20(,BPTR)           GET LENGTH USED
         SH    DLEN,=H'2'               LENGTH OF DATA
         SPACE 2
DIR$LP   CLC   0(8,DPTR),=8X'FF'        END OF DIRECTORY?
         BE    DIR$END                  YES - DONE
         BAL   LINK,MOVE$MEM            MOVE NAME TO PRINT LINE
         LTR   DLEN,DLEN                ANYTHING LEFT?
         BP    DIR$LP                   CONTINUE IF MORE LEFT
         LA    BPTR,256+12+8(,BPTR)     NEXT DIRECTORY BLOCK
         SH    BLEN,=Y(256+12+8)        DECR LENGTH LEFT
         BP    DIR$NEXT                 MORE IN THIS TAPE BLOCK
         B     IBCPY$LP                 GET NEXT BLOCK FROM TAPE
         SPACE 2
DIR$END  LA    R0,OUTBUFF2+20           GET START PTR FOR COMPARE
         CR    R0,PPTR                  ANY ON THIS LINE?
         BE    STAR$TWO                 NO - DONE
         PRTLN2                         PRINT PARTIAL LINE
         B     STAR$TWO                 CONTINUE PROCESSING
         SPACE 3
MOVE$MEM MVC   0(8,PPTR),0(DPTR)        MOVE NAME TO PRINT LINE
         IC    R1,11(,DPTR)             GET USER DATA LENGTH
         N     R1,=F'31'                STRIP OFF JUNK
         LA    R1,12(R1,R1)             GET LENGTH OF ENTRY
         AR    DPTR,R1                  ADVANCE DIR PTR
         SR    DLEN,R1                  DECR LENGTH LEFT
         SPACE 1
         LA    PPTR,10(,PPTR)           NEXT PRINT LINE POSITION
         CL    PPTR,OB2END              END OF PRINT LINE?
         BNHR  LINK                     NO - RETURN
         LR    PPTR,LINK                SAVE RETURN ADDR
         PRTLN2                         PRINT IT
         LR    LINK,PPTR                RESTORE RETURN ADDR
         LA    PPTR,OUTBUFF2+20         RESET PTR
         BR    LINK                     RETURN TO CALLER
         SPACE 3
*  FIRST RECORD SAVED IN "DASDRSAV"
*HDASDR2 BAL   LINK,SET$FULD            SET UP HEADER
***      DC    4Y(0)                    SO RETURN ADDR IS CORRECT
***      MVC OUTBUFF2+29(38),=C') IS AN IEHDASDR DUMP STARTING AT CCHH'
***      UNPK  OUTBUFF2+68(9),DASDRSAV+0(5)   CCHH OF THIS TAPE
***      TR    OUTBUFF2+68(8),HEXTAB    MAKE EBCDIC
***      MVC   OUTBUFF2+76(5),=C' OF A'
***      IC    R1,DASDRSAV+21           GET DEVTYPE
***      N     R1,=F'255'               CLEAN
***      MH    R1,=H'6'
***      LA    R14,=C'2321  2311  2314  2302  2301  2305-12305-23330  ?
***            ?0A  ??0B  ??0C  3330-1'
***      LA    R14,0(R14,R1)            GET ADDR OF THIS DEVTYPE
***      MVC   OUTBUFF2+82(6),0(R14)    MOVE TO PRINT LINE
***      CLC   =F'0',DASDRSAV+8         THIS TAPE START AT 0?
***      BNE   D$NOVOL                  NO - CAN'T FIND VOLSER
*  SECOND RECORD IS IN "TAPEBUFF"
***      CLC   =C'VOL1VOL1',TAPEBUFF+216  LABEL?
***      BNE   D$NOVOL                  NO - SKIP
***      MVC   OUTBUFF2+91(7),=C'VOLUME='
***      MVC   OUTBUFF2+98(6),TAPEBUFF+224
***      MVC   OUTBUFF2+106(6),=C'OWNER='
***      MVC   OUTBUFF2+112(10),TAPEBUFF+261
         SPACE 1
*D$NOVOL PRTLN2
***      B     STAR$TWO
         SPACE 3
IEBUPDT2 LA    R0,7
         TM    PARMFLG1,P1@NOMEM        PARM=NOMEM?
         BNO   *+8                      NO
         LA    R0,5                     YES
         NEWPAGE  (R0),2
         BAL   LINK,SET$FULD            SET UP HEADER INFO
         DC    4Y(0)                    FILLER
         MVC   OUTBUFF2+31(27),=C'is an IEBUPDTE input stream'
         BAL   LINK,FMU                 'FOLLOWING MEMBERS UNLOADED:'
         SPACE 2
UPDTE$L1 LA    BLEN,80                  BXLE INCR
         LA    DPTR,TAPEBUFF            POINT TO START
         XR    BPTR,BPTR                CLEAR FOR ICM              U032
         ICM   BPTR,B'0011',LASTSIZE    LOAD LEN READ              U032
         AR    BPTR,DPTR                POINT PAST END OF BLOCK    U032
         SR    BPTR,BLEN                BACK UP FOR BXLE
         SPACE 1
UPDTE$L2 LR    R1,DPTR                  COPY CARD ADDR
         BAL   LINK,TEST$UP             IS THIS ONE?
         B     UPDTE$BX                 NO - SKIP
         SPACE 1
         MVC   0(1,PPTR),UPDT#TYP       MOVE A,R,C
         MVI   1(PPTR),C'-'             SEPARATOR
         MVC   2(8,PPTR),0(R1)          MEMBER NAME
         LA    R1,1(,PPTR)
UPDTE$L3 CLI   0(R1),C','               END OF MEMBER NAME?
         BE    UPDTE$EM                 YES
         CLI   0(R1),C' '               END OF MEMBER NAME?
         BE    UPDTE$EM                 YES
         LA    R1,1(,R1)                INCR SCAN PTR
         B     UPDTE$L3                 CONTINUE
         SPACE 1
UPDTE$EM MVC   0(7,R1),OUTBUFF+1        BLANK OUT GARBAGE
         LA    PPTR,11(,PPTR)           BUMP PRINT LINE PTR
         CL    PPTR,OB2END2             PAST END?
         BNH   UPDTE$BX                 NO - CONTINUE
         PRTLN2                         PRINT THE MEMBERS
         LA    PPTR,OUTBUFF2+20         RESET PTR
         SPACE 1
UPDTE$BX BXLE  DPTR,BLEN,UPDTE$L2       KEEP SCANNING
         TAPIO TCCW#DAT,TM=UPDT$EOF     GET NEXT BLOCK
         B     UPDTE$L1                 GO CHECK IT
         SPACE 2
UPDT$EOF OI    TFLAG1,T1@MVEOF          SET "HIT EOF"
         B     DIR$END                  CLEAN UP
         SPACE 3
IEBISAM2 BAL   LINK,SET$FULL            SET UP HEADER
         DC    Y(4+26)                  DSORG OFFSET
         DC    Y(4+36)                  RECFM OFFSET
         DC    Y(4+62)                  BLKSIZE OFFSET
         DC    Y(4+2+82)                LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEBISAM   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,LINK,R1=TAPEBUFF+4+17-3 CONVERT UNIT TYPE
         SPACE 1
         PRTLN2                         PRINT SECOND INFO LINE
         SPACE 1
         B     STAR$TWO                                            U???
         SPACE 3
FDR2     BAL   LINK,SET$FULD            FORMAT UNLOADED MSG        U022
         DC    4Y(0)                    SO RETURN ADDR IS CORRECT  U024
         MVC   OUTBUFF2+58(25),=C'....... was unloaded from'       U022
         ICALL CONVUFDR,LINK,R1=TAPEBUFF+4  FORMAT UNIT TYPE       U022
         MVC   OUTBUFF2+89(7),OUTBUFF2+125  GRAB UNIT TYPE         U022
         MVC   OUTBUFF2+125(7),BLANKS   CLEAR IT OUT               U022
         TAPIO TCCW#DAT                 GET NEXT DATA BLOCK        U022
         CLC   =C'DUMMYDSF',TAPEBUFF+12 FAKE VTOC RECORD?          U022
         BE    FDR2HEAD                 YES, CONTINUE              U022
         CLC   UNLOADER+3(5),BLANKS     ANY VERSION YET?           U032
         BNE   *+10                     YES, SKIP UPDATE           U032
         MVC   UNLOADER+3(5),=C'-CCHH'  JUST DSF-CCHH DUMP (FDR4.0)U022
         MVC   OUTBUFF2+47(8),UNLOADER  MOVE IN UNLOADER           U022
         PRTLN2                         OUTPUT THE LINE            U022
         B     STAR$TWO                 DONE HERE                  U022
         SPACE 1
FDR2HEAD MVC   OUTBUFF2+47(8),UNLOADER  MOVE IN UNLOADER           U022
         PRTLN2                         OUTPUT TITLE LINE 1        U022
         TM    PARMFLG1,P1@NOMEM        DUMP DSN'S?                U022
         BO    FDR2LOOP                 NO, FINNISH GETTING UNLOAD U022
         MVI   OUTBUFF2,C'0'            SKIP A LINE                U022
         MVC   OUTBUFF2+16(28),=C'Following datasets unloaded:'    U022
         L     R1,=A(COLTITLE)                                     U035
         MVC   OUTBUFF2+55(78),0(R1)                               U035
         PRTLN2 ,                       SECOND LINE OF TITLE       U022
         MVI   OUTBUFF2,C'0'            SKIP A LINE HERE TOO.      U022
         CLC   =C'VSAMDFEF',TAPEBUFF+32 ICF INFORMATION?           U033
         BE    FDR2LOOP                 YES, SKIP IT               U033
         B     FDR2INIT                 CONTINUE W/BLOCK           U022
         SPACE 1
FDR2LOOP TAPIO TCCW#DAT                 GET NEXT DATA BLOCK        U022
         CLC   =C'DUMMYDSF',TAPEBUFF+12 FAKE VTOC RECORD?          U022
         BNE   FDR2$EOF                 NO, DONE HERE              U033
         CLC   =C'VSAMDFEF',TAPEBUFF+32 VSAM DATA FROM V4.8?       U033
         BE    FDR2LOOP                 YES, SKIP IT               U033
         CLC   =C'TRACKTAB',TAPEBUFF+32 TRACK BITMAP?              U033
         BE    FDR2LOOP                 YES, SKIP IT               U033
         SPACE 1
FDR2INIT TM    PARMFLG1,P1@NOMEM        DUMP DSN'S?
         BO    FDR2LOOP                 NO, FINNISH GETTING UNLOAD U022
         LA    DPTR,TAPEBUFF+32+8       -> FIRST DSCB IN BLOCK     U022
         ICM   DLEN,B'1111',TAPEBUFF+28 NUMBER OF DSCB'S IN BLOCK  U022
*U037    BNZ   *+8                      ALL OK                     U022
*U037    BAL   R14,BOOM                 SOMETHING IS FICHIE        U022
         BZ    FDR2LOOP                 skip null block            U037
FDR2$DSN CLI   44(DPTR),C'1'            FORMAT 1 DSCB?             U022
         BNE   FDR2$NXT                 NO, GET NEXT DSCB          U022
         LH    R1,LCTR2                                            U033
         BCT   R1,FDR2$NHD                                         U033
         MVI   OUTBUFF2,C'0'            SKIP A LINE                U033
         L     R1,=A(COLTITLE)                                     U035
         MVC   OUTBUFF2+55(78),0(R1)                               U035
         PRTLN2 ,                       SECOND LINE OF TITLE       U033
         MVI   OUTBUFF2,C'0'            SKIP A LINE HERE TOO.      U033
FDR2$NHD MVC   #FDSN,0(DPTR)            YES, MOVE IN DSN           U033
         ICALL CNVDSORG,R14,R1=82(,DPTR),R15=#FDSORG   DSORG       U022
         ICALL CNVRECFM,R14,R1=84(,DPTR),R15=#FRECFM   RECFM       U022
         ICALL CONVHALF,R14,R1=86(,DPTR),R15=#FBLKSI   BLKSIZE     U022
         ICALL CONVHALF,R14,R1=88(,DPTR),R15=#FLRECL   LRECL       U022
         ICALL CONVDATE,LINK,R1=53(,DPTR),R15=#FCDATE  CDATE       U022
         ICALL CONVDATE,LINK,R1=75(,DPTR),R15=#FLDATE  L/USED      U022
         XC    DWD,DWD                  CLEAR OUT WORKAREA         U022
         MVC   DWD+3,59(DPTR)           MOVE IN NUMBER OF EXTENTS  U022
         ICALL CONVHALF,R14,R1=DWD+2,R15=#FEXT         # EXT.      U022
         ICALL SLIDE,R14,R15=#FEXT+1    SHIFT TO LEFT SOME         U022
         XR    R1,R1                    CLEAR FOR ICM              U022
         ICM   R1,B'0111',95(DPTR)      GET QUANTITY FOR SEC       U022
         BZ    FDR2$ALC                 IF ZERO, DONE HERE         U022
         C     R1,=F'99999'             LARGER THAN EDIT MASK?     U022
         BNH   *+12                     YES, LEAVE FILL CHAR       U022
         OI    TFLAG2,T2@FDROV          INDICATE FIELD OVERFLOW    U022
         MVI   #FSEC,C'*'               INDICATE LARGER THAN FIELD U022
         CVD   R1,DWD                   CONVERT TO DEC             U022
         MVC   #FSEC+1(5),=X'2020202120'  EDIT MASK                U022
         ED    #FSEC,DWD+5              EDIT IN NUMBER             U022
         SPACE 1
FDR2$ALC MVC   DWD(1),94(DPTR)          COPY OVER ALLOC ALG        U022
         NI    DWD,X'C0'                JUST ALLOC ALG.            U022
         XR    R1,R1                    CLEAR FOR IC               U022
         IC    R1,DWD                   GET ALLOC ALG              U022
         SRL   R1,6                     JUST 2 BITS PLEASE         U022
         MH    R1,=H'3'                 INTO INDEX FOR TABLE       U022
         LA    R15,=C'ABSBLKTRKCYL'     ALLOCATION TYPE TABLE      U022
         AR    R1,R15                   -> ENTRY IN TABLE          U022
         MVC   #FALC(3),0(R1)           MOVE IN ALLOC TYPE         U022
         TM    94(DPTR),X'01'           WAS ROUND SPEC?            U022
         BZ    *+10                     NO, SKIP UPDATE            U022
         MVC   #FALC+1(3),=C'- R'       ROUND IT TO CYL            U022
         TM    94(DPTR),X'08'           CONTIG?                    U022
         BZ    FDR2$ALM                 NO, TRY MXIG               U022
         MVC   #FALC+1(2),=C'-C'        WHATEVER IN 1 HUNK         U022
         B     FDR2$USE                 GET AMOUNT USED            U022
FDR2$ALM TM    94(DPTR),X'04'           MXIG?                      U022
         BZ    FDR2$ALA                 NO, TRY ALX                U022
         MVC   #FALC,=C'MXIG'           YES, SAY SO                U022
         B     FDR2$USE                 AND CONTINUE               U022
FDR2$ALA TM    94(DPTR),X'02'           ALX ALLOC?                 U022
         BZ    FDR2$USE                 NO, DONE HERE              U022
         MVC   #FALC+1(2),=C'-A'        ORIGINAL IN ALX            U022
         SPACE 1
FDR2$USE SR    R1,R1                    clear for icm              U038
         ICM   R1,B'0011',98(DPTR)      get tt of ds1lstar         U038
         LA    R1,1(,R1)                add 1 for origin 1         U038
         MVI   TTREXMSK,C' '            FILL WITH BLANKS           U022
         ICALL CONVTTR,R14,R15=#FUSED   DS1LSTAR CCC.HH            U022
         XC    TRKTOT,TRKTOT            INIT TOTAL TRACKS          U033
         ICALL CONVEXT,R14,R1=105(,DPTR),R15=3   GET FIRST EXTS    U022
         CLI   59(DPTR),3               MORE THAN 3 EXTENTS?       U022
         BNH   FDR2$EXP                 NO, PRINT TOTAL NUMBER NOW U022
         OC    135(8,DPTR),135(DPTR)    MORE FORMAT 3?             U022
         BZ    FDR2$EXP                 NO, PRINT WHAT WE HAVE     U022
FDR2$EXL MVC   TRKCCHH,135(DPTR)        -> FMT3 DSCB               U022
FDR2$NX3 LA    DPTR,140(,DPTR)          -> COUNT OF NEXT DSCB      U033
         BCT   DLEN,FDR2$EXT            CHECK IF IN THIS BLOCK     U022
         SPACE 1
         TAPIO TCCW#DAT                 GET NEXT TAPE BLOCK        U022
         CLC   =C'DUMMYDSF',TAPEBUFF+12 STILL IN DSF BLOCKS        U022
         BNE   FDR2$OUT                 NO, GET OUT NOW            U022
         CLC   =C'VSAMDFEF',TAPEBUFF+32 ICF CAT (V4.8)?            U032
         BNE   *+4+6+4                  NO, TRY TRACKTAB           U032
         MVC   UNLOADER+4(4),=C'V4.8'   YES, INDICATE V4.8         U032
         B     FDR2LOOP                 AND SKIP THIS BLOCK        U032
         CLC   =C'TRACKTAB',TAPEBUFF+32 INTO TRACK MAPS?           U022
         BNE   FDR2$EXD                 NO, CONTINUE PROCESSING    U022
         CLC   UNLOADER+4(4),BLANKS     ANY VERSION YET?           U032
         BNE   *+10                     YES, SKIP UPDATE           U032
         MVC   UNLOADER+4(4),=C'V4.5'   YES, INDICATE VERSION 4.5  U046
FDR2$OUT MVI   TTREXMSK,C'*'            INDICATE QUANTITY UNKNOWN  U022
         OI    TFLAG2,T2@FDROV          INDICATE NEED OF MSG       U022
         L     R1,TRKTOT                GET TOTAL TRACKS COUNTED   U022
         ICALL CONVTTR,R14,R15=#FTOTAL  OUTPUT FIELD FOR PRINTING  U022
         PRTLN2  ,                      DRIVE OUT THE LINE         U022
         B     FDR2$EOF                 AND OUTPUT TITLE LINE      U022
         SPACE 1
FDR2$EXD ICM   DLEN,B'1111',TAPEBUFF+28 NUMBER OF DSCB'S IN BLOCK  U022
*U037    BNZ   *+8                      VALID, GO ON               U022
*U037    BAL   R14,BOOM                 LOGIC ERROR!!!!!           U022
         BZ    FDR2LOOP                 skip if none here          U037
         LA    DPTR,TAPEBUFF+32         -> COUNT OF FIRST DSCB     U022
         SPACE 1
FDR2$EXT CLC   TRKCCHH,0(DPTR)          IS THIS IT??               U022
         LA    DPTR,8(,DPTR)            -> DATA PORTION OF RECORD  U033
         BE    FDR2$F3                  YEP, FOUND IT              U033
         CLI   44(DPTR),C'1'            FORMAT 1 DSCB?             U033
         BNE   FDR2$NX3                 NO, GO SEARCH FOR NEXT     U033
         OI    TFLAG2,T2@FDROV          INDICATE NEED FOR MESSAGE  U022
         MVI   TTREXMSK,C'*'            FILL EDIT WITH ASTRISKS    U022
*                                       TO IND. UNKNOWN SIZE       U022
*                                       SORTA LIKE FORTRASH        U022
         LA    DLEN,1(,DLEN)            RESET COUNTER              U033
         SH    DPTR,=H'148'             RESET POINTER              U033
         B     FDR2$EXP                 GO PRINT TOTALS            U033
FDR2$F3  ICALL CONVEXT,R14,R1=4(,DPTR),R15=4  GET NEXT 4 EXTS      U033
         CLI   45(DPTR),0               ANY MORE EXTENTS HERE?     U022
         BE    FDR2$EXP                 NO, FORMAT ENTRY           U033
         ICALL CONVEXT,R14,R1=45(,DPTR),R15=9 GET NEXT 9 EXTS      U022
FDR2$EXN OC    135(5,DPTR),135(DPTR)    ANY MORE DSCB'S?           U022
         BNZ   FDR2$EXL                 YES, GO LOOK FOR THEM      U022
         SPACE 1
FDR2$EXP L     R1,TRKTOT                GET TOTAL TRACKS           U022
         ICALL CONVTTR,R14,R15=#FTOTAL  FORMAT TOTAL TRACKS        U022
         PRTLN2  ,                      AND PRINT THE LINE         U022
         SPACE 1
FDR2$NXT LA    DPTR,140+8(,DPTR)        -> NEXT DSCB IN BUFFER     U022
         BCT   DLEN,FDR2$DSN            LOOP BACK FOR NEXT         U022
         B     FDR2LOOP                 GET NEXT BUFFER            U022
         SPACE 3
FDR2$EOF TM    TFLAG2,T2@FDROV          MSG REQ?                   U022
         BZ    STAR$TWO                 NO, OUTPUT LINES           U022
         L     R15,=A(FDRFOOT1)         -> OUTPUT MESSAGE          U022
         MVC   OUTBUFF2(L'FDRFOOT1),0(R15)   MOVE IN MESSAGE       U022
         PRTLN2  ,                      DRIVE OUT MESSAGE          U022
         B     STAR$TWO                 AND TERMINATE THE FILE     U034
         EJECT
DSS2NEWV DC    0H'0'                                               U034
         XC    LCTR2,LCTR2                                         U034
         LA    R1,0                                                U034
         IC    R1,TAPEBUFF+50           GET DEVICE UNIT TYPE       U034
         MH    R1,=Y(DEVTABXL)          TIMES LEN OF DEVTABX ENTRY U039
         A     R1,=A(DEVTABX)           POINT TO DEVICE TABLE      U039
         MVC   DSSDEVTY,4(R1)           SAVE DEVICE TYPE           U039
         MVC   DSSDEVSZ,TAPEBUFF+24     SAVE DEVICE SIZE           U034
         LH    R1,DSSDEVTT              GET TRACKS PER CYLINDER    U034
         ST    R1,TRKCYL                  AND PUT IN WORK FIELD    U034
         MVC   DSSVOLID,TAPEBUFF+32     SAVE SERIAL NUMBER         U034
         TAPIO TCCW#DAT,TM=BOOM         READ THE 2ND BLOCK         U034
         CLC   =C'BMBB',TAPEBUFF+16     DSS BIT MAP?               U034
         BNE   BOOM                     NO, LOGIC ERROR            U034
         LH    R1,TAPEBUFF+24           # OF TRACKS IN THIS MAP    U034
         SRL   R1,3                     # OF BYTES IN BITMAP       U034
         LA    R1,1(R1)                                            U034
         STH   R1,BITMAPLN                                         U034
         LR    R0,R1                                               U034
         GETMAIN R,LV=(0)               GET STORAGE FOR BITMAP     U034
         ST    R1,BITMAP                  AND SAVE THE POINTER     U034
         LA    R1,0                     CLEAR REGISTER             U034
         IC    R1,TAPEBUFF+8              AND INSERT PREFIX LENGTH U034
         LA    R0,12(R1)                ADD BITMAP EXT LENGTH      U034
         LA    R2,TAPEBUFF                                         U034
         AR    R0,R2                    POINT TO THE BITMAP        U034
         L     R2,BITMAP                                           U034
         LH    R3,BITMAPLN                                         U034
         LR    R1,R3                                               U034
         MVCL  R2,R0                    SAVE THE BITMAP            U034
         CLC   =F'0',TAPEBUFF+20        ANOTHER BITMAP SEGMENT?    U034
         BZ    DSS2                                                U034
         OI    TFLAG3,T3@DSSBO          INDICATE NEED OF MSG       U046
DSS2     DC    0H'0'                                               U034
         BAL   LINK,SET$FULD            FORMAT UNLOADED MSG        U034
         DC    4Y(0)                    SO RETURN ADDR IS CORRECT  U034
         MVC   OUTBUFF2+58(25),=C'....... was unloaded from a'     U034
         MVC   OUTBUFF2+91(7),DSSDEVTY  GRAB UNIT TYPE             U034
         MVC   OUTBUFF2+100(16),=C'-  VOLID(      )'               U034
         MVC   OUTBUFF2+109(6),DSSVOLID GRAB VOL SERIDAL NUM       U034
         MVC   OUTBUFF2+47(8),UNLOADER  MOVE IN UNLOADER           U034
         PRTLN2                         OUTPUT TITLE LINE 1        U034
         TM    PARMFLG1,P1@NOMEM        DUMP DSN'S?                U034
         BO    STAR$TWO                 NO, FINNISH IT             U034
         MVI   OUTBUFF2,C'0'            SKIP A LINE                U034
         MVC   OUTBUFF2+16(28),=C'Following datasets unloaded:'    U034
         L     R1,=A(COLTITLE)                                     U035
         MVC   OUTBUFF2+55(78),0(R1)                               U035
         PRTLN2 ,                       SECOND LINE OF TITLE       U034
         MVI   OUTBUFF2,C'0'            SKIP A LINE HERE TOO.      U034
         B     DSS2TEST                                            U034
         SPACE 1
DSS2LOOP TAPIO TCCW#DAT,TM=DSS2$EOF     NEW BUFFER                 U034
DSS2TEST CLI   TAPEBUFF+10,X'10'        VTOC TRACK?                U034
         BE    DSS2INIT                                            U034
         CLI   TAPEBUFF+10,X'80'        VOL HEADER?                U034
         BE    DSS2$EOD                                            U034
         B     DSS2LOOP                                            U034
DSS2INIT LA    R1,0                                                U034
         IC    R1,TAPEBUFF+8            LENGTH OF PREFIX           U034
         LH    DLEN,TAPEBUFF+6          GET SEGMENT LENGTH         U034
         SR    DLEN,R1                  LENGTH OF PREFIX           U034
         SH    DLEN,=H'24'              LENGTH OF TRACK EXTENSION  U034
         LA    DPTR,TAPEBUFF+24         LENGTH OF TRACK EXTENSION  U034
         AR    DPTR,R1                  LENGTH OF PREFIX           U034
         LA    DPTR,8(DPTR)             POINT TO DSNAME            U034
DSS2$DSN CLI   44(DPTR),C'1'            FORMAT 1 DSCB?             U034
         BNE   DSS2$NXT                 NO, GET NEXT DSCB          U034
         LA    R0,0                                                U034
         LA    R1,0                                                U034
         ICM   R0,B'0011',107(DPTR)     GET LOW CYL ADDRESS        U034
         MH    R0,DSSDEVTT                AND MULTIPLY BY TRK/CYL  U034
         ICM   R1,B'0011',109(DPTR)         THEN GET LOW TRK ADR   U034
         AR    R0,R1                          AND MAKE IT REL TRK  U034
         SRDL  R0,3                     DEVIDE BY 8 FOR REL BYTE   U034
         L     R2,BITMAP                POINT TO BITMAP            U034
         AR    R2,R0                       AND ADD REL BYTE ADR    U034
         SRL   R1,29                    REL BIT WITHIN BYTE * 2    U034
         A     R1,=A(MASKTAB)           POINT TO MASK ENTRY        U034
         IC    R1,0(R1)                 GET THE MASK               U034
         EX    R1,DSS2$TM               DO TEST UNDER MASK         U034
         BNO   DSS2$NXT                                            U034
         LH    R1,LCTR2                                            U034
         BCT   R1,DSS2$NHD                                         U034
         MVI   OUTBUFF2,C'0'            SKIP A LINE                U034
         L     R1,=A(COLTITLE)                                     U035
         MVC   OUTBUFF2+55(78),0(R1)                               U035
         PRTLN2 ,                       SECOND LINE OF TITLE       U034
         MVI   OUTBUFF2,C'0'            SKIP A LINE HERE TOO.      U034
DSS2$NHD MVC   #FDSN,0(DPTR)            YES, MOVE IN DSN           U034
         ICALL CNVDSORG,R14,R1=82(,DPTR),R15=#FDSORG   DSORG       U034
         ICALL CNVRECFM,R14,R1=84(,DPTR),R15=#FRECFM   RECFM       U034
         ICALL CONVHALF,R14,R1=86(,DPTR),R15=#FBLKSI   BLKSIZE     U034
         ICALL CONVHALF,R14,R1=88(,DPTR),R15=#FLRECL   LRECL       U034
         ICALL CONVDATE,LINK,R1=53(,DPTR),R15=#FCDATE  CDATE       U034
         ICALL CONVDATE,LINK,R1=75(,DPTR),R15=#FLDATE  L/USED      U034
         XC    DWD,DWD                  CLEAR OUT WORKAREA         U034
         MVC   DWD+3,59(DPTR)           MOVE IN NUMBER OF EXTENTS  U034
         ICALL CONVHALF,R14,R1=DWD+2,R15=#FEXT         # EXT.      U034
         ICALL SLIDE,R14,R15=#FEXT+1    SHIFT TO LEFT SOME         U034
         XR    R1,R1                    CLEAR FOR ICM              U034
         ICM   R1,B'0111',95(DPTR)      GET QUANTITY FOR SEC       U034
         BZ    DSS2$ALC                 IF ZERO, DONE HERE         U034
         C     R1,=F'99999'             LARGER THAN EDIT MASK?     U034
         BNH   *+12                     YES, LEAVE FILL CHAR       U034
         OI    TFLAG2,T2@FDROV          INDICATE FIELD OVERFLOW    U034
         MVI   #FSEC,C'*'               INDICATE LARGER THAN FIELD U034
         CVD   R1,DWD                   CONVERT TO DEC             U034
         MVC   #FSEC+1(5),=X'2020202120'  EDIT MASK                U034
         ED    #FSEC,DWD+5              EDIT IN NUMBER             U034
         SPACE 1
DSS2$ALC MVC   DWD(1),94(DPTR)          COPY OVER ALLOC ALG        U034
         NI    DWD,X'C0'                JUST ALLOC ALG.            U034
         XR    R1,R1                    CLEAR FOR IC               U034
         IC    R1,DWD                   GET ALLOC ALG              U034
         SRL   R1,6                     JUST 2 BITS PLEASE         U034
         MH    R1,=H'3'                 INTO INDEX FOR TABLE       U034
         LA    R15,=C'ABSBLKTRKCYL'     ALLOCATION TYPE TABLE      U034
         AR    R1,R15                   -> ENTRY IN TABLE          U034
         MVC   #FALC(3),0(R1)           MOVE IN ALLOC TYPE         U034
         TM    94(DPTR),X'01'           WAS ROUND SPEC?            U034
         BZ    *+10                     NO, SKIP UPDATE            U034
         MVC   #FALC+1(3),=C'- R'       ROUND IT TO CYL            U034
         TM    94(DPTR),X'08'           CONTIG?                    U034
         BZ    DSS2$ALM                 NO, TRY MXIG               U034
         MVC   #FALC+1(2),=C'-C'        WHATEVER IN 1 HUNK         U034
         B     DSS2$USE                 GET AMOUNT USED            U034
DSS2$ALM TM    94(DPTR),X'04'           MXIG?                      U034
         BZ    DSS2$ALA                 NO, TRY ALX                U034
         MVC   #FALC,=C'MXIG'           YES, SAY SO                U034
         B     DSS2$USE                 AND CONTINUE               U034
DSS2$ALA TM    94(DPTR),X'02'           ALX ALLOC?                 U034
         BZ    DSS2$USE                 NO, DONE HERE              U034
         MVC   #FALC+1(2),=C'-A'        ORIGINAL IN ALX            U034
         SPACE 1
DSS2$USE LA    R1,1                     TTR'S ARE ORIGIN 0         U034
         AH    R1,98(,DPTR)             GET TT OF DS1LSTAR         U034
         MVI   TTREXMSK,C' '            FILL WITH BLANKS           U034
         ICALL CONVTTR,R14,R15=#FUSED   DS1LSTAR CCC.HH            U034
         XC    TRKTOT,TRKTOT            INIT TOTAL TRACKS          U034
         ICALL CONVEXT,R14,R1=105(,DPTR),R15=3   GET FIRST EXTS    U034
         CLI   59(DPTR),3               MORE THAN 3 EXTENTS?       U034
         BNH   DSS2$EXP                 NO, PRINT TOTAL NUMBER NOW U034
         OC    135(8,DPTR),135(DPTR)    MORE FORMAT 3?             U034
         BZ    DSS2$FLG                 NO, GO SET FLAG            U034
DSS2$EXL MVC   TRKCCHH,135(DPTR)        -> FMT3 DSCB               U034
DSS2$NX3 LA    DPTR,140(,DPTR)          -> COUNT OF NEXT DSCB      U034
         SH    DLEN,=H'148'             CHECK IF IN THIS BLOCK     U034
         BP    DSS2$EXT                 YES                        U034
         SPACE 1
         TAPIO TCCW#DAT,TM=DSS2$OUT     GET NEXT TAPE BLOCK        U034
         CLI   TAPEBUFF+10,X'10'        VTOC TRACK?                U034
         BNE   DSS2$FLG                 NO, GO SET FLAG            U034
         LA    R1,0                                                U034
         IC    R1,TAPEBUFF+8            LENGTH OF PREFIX           U034
         LH    DLEN,TAPEBUFF+6          GET SEGMENT LENGTH         U034
         SR    DLEN,R1                  LENGTH OF PREFIX           U034
         SH    DLEN,=H'24'              LENGTH OF TRACK EXTENSION  U034
         LA    DPTR,TAPEBUFF+24         LENGTH OF TRACK EXTENSION  U034
         AR    DPTR,R1                  LENGTH OF PREFIX           U034
         SPACE 1
DSS2$EXT CLC   TRKCCHH,0(DPTR)          IS THIS IT??               U034
         LA    DPTR,8(,DPTR)            -> DATA PORTION OF RECORD  U034
         BE    DSS2$F3                  YEP, FOUND IT              U034
         CLI   44(DPTR),C'1'            FORMAT 1 DSCB?             U034
         BNE   DSS2$NX3                 NO, GO SEARCH FOR NEXT     U034
         AH    DLEN,=H'148'             RESET COUNTER              U034
         SH    DPTR,=H'148'             RESET POINTER              U034
DSS2$FLG OI    TFLAG2,T2@FDROV          INDICATE NEED FOR MESSAGE  U034
         MVI   TTREXMSK,C'*'            FILL EDIT WITH ASTRISKS    U034
*                                       TO IND. UNKNOWN SIZE       U034
*                                       SORTA LIKE FORTRASH        U034
         B     DSS2$EXP                 GO PRINT TOTALS            U034
DSS2$F3  ICALL CONVEXT,R14,R1=4(,DPTR),R15=4  GET NEXT 4 EXTS      U034
         CLI   45(DPTR),0               ANY MORE EXTENTS HERE?     U034
         BE    DSS2$EXP                 NO, FORMAT ENTRY           U034
         ICALL CONVEXT,R14,R1=45(,DPTR),R15=9 GET NEXT 9 EXTS      U034
DSS2$EXN OC    135(5,DPTR),135(DPTR)    ANY MORE DSCB'S?           U034
         BNZ   DSS2$EXL                 YES, GO LOOK FOR THEM      U034
         SPACE 1
DSS2$EXP L     R1,TRKTOT                GET TOTAL TRACKS           U034
         ICALL CONVTTR,R14,R15=#FTOTAL  FORMAT TOTAL TRACKS        U034
         PRTLN2  ,                      AND PRINT THE LINE         U034
         SPACE 1
DSS2$NXT LA    DPTR,148(,DPTR)          -> NEXT DSCB IN BUFFER     U034
         SH    DLEN,=H'148'             ANOTHER ENTRY IN BLOCK?    U034
         BP    DSS2$DSN                                            U034
         B     DSS2LOOP                 GET NEXT BUFFER            U034
         SPACE 2
DSS2$OUT MVI   TTREXMSK,C'*'            INDICATE QUANTITY UNKNOWN  U034
         OI    TFLAG2,T2@FDROV          INDICATE NEED OF MSG       U034
         L     R1,TRKTOT                GET TOTAL TRACKS COUNTED   U034
         ICALL CONVTTR,R14,R15=#FTOTAL  OUTPUT FIELD FOR PRINTING  U034
         PRTLN2  ,                      DRIVE OUT THE LINE         U034
DSS2$EOF DC    0H'0'                                               U034
         OI    TFLAG1,T1@MVEOF                                     U034
DSS2$EOD DC    0H'0'                                               U034
         LH    R0,BITMAPLN                                         U034
         L     R2,BITMAP                                           U034
         FREEMAIN R,LV=(0),A=(2)                                   U034
         TM    TFLAG3,T3@DSSBO          MSG REQ?                   U046
         BZ    DSS2$END                 NO, OUTPUT LINES           U034
         L     R15,=A(DSSFOOT1)         -> OUTPUT MESSAGE          U034
         MVC   OUTBUFF2(L'DSSFOOT1),0(R15)   MOVE IN MESSAGE       U034
         PRTLN2  ,                      DRIVE OUT MESSAGE          U034
*U046    NI    TFLAG2,X'FF'-T2@FDROV                               U034
DSS2$END TM    TFLAG2,T2@FDROV          MSG REQ?                   U034
         BZ    DSS2$NEW                 NO, OUTPUT LINES           U034
         L     R15,=A(FDRFOOT1)         -> OUTPUT MESSAGE          U034
         MVC   OUTBUFF2(L'FDRFOOT1),0(R15)   MOVE IN MESSAGE       U034
         PRTLN2  ,                      DRIVE OUT MESSAGE          U034
         NI    TFLAG2,X'FF'-T2@FDROV                               U034
DSS2$NEW CLI   TAPEBUFF+10,X'80'        VOL HEADER?                U034
         BE    DSS2NEWV                                            U034
         B     STAR$TWO                     TERMINATE THE FILE     U034
DSS2$TM  TM    0(R2),0                                             U034
MASKTAB  DC    0H'0'                                               U034
         DC    B'10000000'                                         U034
         DC    B'01000000'                                         U034
         DC    B'00100000'                                         U034
         DC    B'00010000'                                         U034
         DC    B'00001000'                                         U034
         DC    B'00000100'                                         U034
         DC    B'00000010'                                         U034
         DC    B'00000001'                                         U034
         EJECT
STAR$TWO MVC   OUTBUFF2(2),=C'0*'
         MVC   OUTBUFF2+2(L'OUTBUFF2-2),OUTBUFF2+1
         PRTLN2
         TM    TFLAG1,T1@MVEOF          ALREADY HIT EOF?
         BO    CHKMVEOF                 YES - SEE WHERE TO GO
         TM    TFLAG1,T1@SL             SL TAPE?
         BO    FIND$EOF                 YES - CONTINUE PROCESSING
         B     NL$LOOP                  SCAN THRU REST OF FILE
         SPACE 1
CHKMVEOF NI    TFLAG1,255-T1@MVEOF      RESET FLAG
         TM    TFLAG1,T1@SL             SL TAPE?
         BO    SL$SAVE                  YES - GO READ THE EOF LABELS
         B     NL$EOF                   NO
         SPACE 3
FMU      PRTLN2                         PRINT INFO LINE
FMU$     TM    PARMFLG1,P1@NOMEM        WANT MEMBER LISTING?
         BO    STAR$TWO                 NO - DONE
         MVI   OUTBUFF2,C'0'            SET CC
         MVC   OUTBUFF2+16(27),=C'Following members unloaded:'
         PRTLN2                         PRINT HEADER
         MVI   OUTBUFF2,C'0'            DOUBLE SPACE FIRST MEMBER LINE
         LA    PPTR,OUTBUFF2+20         INIT PRINT LINE PTR
         BR    LINK                     RETURN TO CALLER
         SPACE 3
SET$FULL MVC   OUTBUFF2+54(17),=C'...  was:  DSORG='
         LA    R1,TAPEBUFF              -> TAPE BUFFER             U022
         AH    R1,0(,LINK)              + OFFSET TO DSORG          U022
         ICALL CNVDSORG,R14,R15=OUTBUFF2+71  CONVERT DSORG
         SPACE 1
         MVC   OUTBUFF2+76(6),=C'LRECL='
         LA    R1,TAPEBUFF
         AH    R1,6(,LINK)              GET LRECL ADDR
         ICALL CONVHALF,R14,R15=OUTBUFF2+82  CONVERT LRECL
         BAL   R14,SLIDE                MOVE IT LEFT
         SPACE 1
         MVC   OUTBUFF2+90(6),=C'RECFM='
         LA    R1,TAPEBUFF              -> TAPE BUFFER             U022
         AH    R1,2(,LINK)              + OFFSET TO RECFM          U022
         ICALL CNVRECFM,R14,R15=OUTBUFF2+96  CONVERT RECFM
         SPACE 1
         MVC   OUTBUFF2+104(8),=C'BLKSIZE='
         LA    R1,TAPEBUFF
         AH    R1,4(,LINK)              GET BLKSIZE ADDR
         ICALL CONVHALF,R14,R15=OUTBUFF2+112 CONVERT BLKSIZE
         BAL   R14,SLIDE                MOVE IT LEFT
         SPACE 2
SET$FULD MVI   OUTBUFF2,C'-'            SET CC
         MVC   OUTBUFF2+1(17),FL1ID     MOVE IN (TAPE) DSN
         MVC   OUTBUFF2+19(5),=C'(File'
         MVC   OUTBUFF2+25(4),FL1FILSQ  MOVE IN FILE SEQ#
         MVC   OUTBUFF2+29(17),=C') was unloaded by'
         B     8(,LINK)                 RETURN TO CALLER
         SPACE 3
BOOM     STM   R0,R15,BOOMREGS          SAVE ALL REGS
         MVC   OUTBUFF2(34),=C'0*** Logic error - contact LDW ***'
         PRTLN2                         PRINT ERROR MSG
         TM    PARMFLG2,P2@TEST         ABEND?                     U020
         BNO   STAR$TWO                 NO - IGNORE REST OF FILE
         SPACE 1
         CLOSE MF=(E,OPENMFL)           CLOSE SYSPRINT
         IFP2  N,BOOM2                  SKIP IF NOT OPEN
         CLOSE MF=(E,OPENMFL2)          CLOSE SYSPRNT2
         SPACE 1
BOOM2    LA    R14,BOOMREGS             POINT TO REG SAVE AREA
         EX    0,*                      AND AWAY WE GO
         EJECT
CONVHALF MVC   DWD(2),0(R1)             MOVE FIELD TO AN ALIGNED PLACE
         XR    R0,R0                    CLEAR FOR ICM              U032
         ICM   R0,B'0011',DWD           PICK IT UP                 U032
         CVD   R0,DWD                   CONVERT TO PACKED
         MVC   1(5,R15),=X'2020202120'  MOVE IN EDIT MASK
         ED    0(6,R15),DWD+5           DO THE EDIT
         BR    R14                      RETURN TO CALLER
         SPACE 3
SLIDE    CLI   0(R15),C' '              NON-BLANK YET?
         BNER  R14                      YES - RETURN TO CALLER
         MVC   0(6,R15),1(R15)          MOVE IT LEFT 1 POS
         B     SLIDE                    AND CHECK IT AGAIN
         SPACE 3
CONVUFDR MVC   OUTBUFF2+84(9),=C'Unit=????'  INSERT MASK           U022
         L     R15,=A(DEVTABX)          -> device type table       U039
CONVUF$L CLC   0(1,R1),1(R15)           this device?               U042
         BE    CONVUF$G                 yes - got it               U039
         LA    R15,DEVTABXL(,R15)       -> next table entry        U039
         CLC   =F'0',0(R15)             end of table?              U039
         BNE   CONVUF$L                 no - keep looking          U039
         MVC   TRKCYL,=F'255'           any old number for invalid U039
         MVC   OUTBUFF2+125(1),0(R1)    copy the bad FDR type code U039
         MVC   OUTBUFF2+126(6),=C'<-unkn'  set rest of devtype     U039
         BR    LINK                     return to caller           U039
         SPACE 1
CONVUF$G MVC   OUTBUFF2+125(7),4(R15)   copy the device type name  U042
         XC    TRKCYL,TRKCYL            CLEAR TRACKS/CYL           U022
         MVC   TRKCYL+2(2),2(R15)       save trks/cyl for this d/t U039
         BR    LINK                     return to caller           U039
         SPACE 1
CONVUNIT MVC   OUTBUFF2+120(10),=C'Unit=unkn-'                     U039
         HEX   OUTBUFF2+130,(3,R1),LEN=1  assume invalid           U039
         CLI   3(R1),DEVTABXM           too big?                   U039
         BHR   LINK                     yes - return
         SR    R14,R14                  clear for ICM              U039
         IC    R14,3(,R1)               get devtype byte           U022
         MH    R14,=Y(DEVTABXL)         length of a table entry    U039
         A     R14,=A(DEVTABX)          point to correct entry     U039
         MVC   OUTBUFF2+125(7),4(R14)   move devtype name to line  U039
         BR    LINK                     return to caller
         SPACE 3
CNVDSORG TM    0(R1),X'01'              DSORG=**U                  U022
         BNO   *+8                      NO - SKIP
         MVI   2(R15),C'U'
         SPACE 1
         MVC   0(2,R15),=C'PO'          ASSUME DSORG=PO
         TM    0(R1),X'02'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'DA'          ASSUME DSORG=DA
         TM    0(R1),X'20'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'PS'          ASSUME DSORG=PS
         TM    0(R1),X'40'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'IS'          ASSUME DSORG=IS
         TM    0(R1),X'80'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'VS'          LAST TRY = VSAM?           U033
         TM    1(R1),X'08'              IS IT?                     U022
         BOR   R14                      YES - RETURN TO CALLER     U022
         SPACE 1
         MVC   0(2,R15),=C'**'          NONE OF THE ABOVE???
         BR    R14                      RETURN TO CALLER
         SPACE 3
CNVRECFM TM    0(R1),X'C0'              RECFM=U?                   U022
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'U'
         B     RECFM$2                  CONTINUE
         SPACE 1
         TM    0(R1),X'80'              RECFM=F?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'F'
         B     RECFM$2                  CONTINUE
         SPACE 1
         TM    0(R1),X'40'              RECFM=V?
         BNO   *+8                      NO - TRY NEXT
         MVI   0(R15),C'V'
         SPACE 1
RECFM$2  LA    R15,1(,R15)
         TM    0(R1),X'10'              BLOCKED?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'B'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'08'              SPANNED?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'S'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'20'              TRK OVFL?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'T'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'02'              RECFM=A?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'A'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'01'              RECFM=M?
         BNOR  R14                      NO - RETURN TO CALLER
         MVI   0(R15),C'M'
         BR    R14                      RETURN TO CALLER
         SPACE 3
CONVTTR  LTR   R1,R1                    ANY VALUE?                 U022
         BZR   R14                      NO, RETURN TO CALLER       U022
         XR    R0,R0                    CLEAR FOR DIVISION (0C9)  U022
         D     R0,TRKCYL                GET TT;CCC                 U022
         CVD   R1,DWD                   CONVERT CYLINDERS          U022
         MVC   0(5,R15),TTREXMSK        INSERT EDIT MASK           U022
         ED    0(4,R15),DWD+6           CONVERT CYLINDERS          U022
         MH    R0,=H'10'                SHIFT 1 NIBBLE             U022
         CVD   R0,DWD                   CONVERT TRACKS             U022
         UNPK  5(3,R15),DWD+6(2)        CHARS TRACKS               U022
         MVI   7(R15),C' '              CLEAR GARBAGE              U022
         MVI   TTREXMSK,C' '            RESET FILL TO NULL         U022
         BR    R14                      RETURN TO CALLER           U022
         SPACE 3
CONVEXT  CLI   0(R1),0                  ANY EXTENTS LEFT?          U022
         BER   R14                      NO, RETURN TO CALLER       U022
         CLI   0(R1),X'40'              USER LABEL EXTENT?         U022
         BE    CONVEXTL                 YES, SKIP IT - NO DATA     U022
         LH    R0,6(,R1)                GET HIGH CC                U022
         SH    R0,2(,R1)                LESS LOW CC                U022
         MH    R0,TRKCYL+2              * TRKS/CYL                 U022
         AH    R0,8(,R1)                + HIGH TT                  U022
         SH    R0,4(,R1)                LESS LOW TT = TOTAL TRKS   U022
         AH    R0,=H'1'                 TTR'S ARE ORIGIN 0         U033
         A     R0,TRKTOT                NEW TOTAL FOR EXTENTS      U022
         ST    R0,TRKTOT                SAVE NEW TOTAL             U022
CONVEXTL LA    R1,10(,R1)               -> NEXT EXTENT             U022
         BCT   R15,CONVEXT              LOOP AGAIN TILL DONE       U022
         BR    R14                      AND RETURN TO CALLER       U022
         SPACE 3
*  Subroutine to compute tape length in feet or percent            U046
TAPE_LEN L     R3,TAPEMFL               -> TAPEDCB                 U046
         TM    PARMFLG2,P2@PCT          force percent?             U046
         BO    TAPE_PCT                 yes                        U046
         TM    PARMFLG2,P2@FEET         force feet?                U046
         BO    TAPEFEET                 yes                        U046
         CLI   DCBDEVT-IHADCB(R3),DCBDVMT4  3480?                  U046
         BE    TAPE_PCT                 yes - default to percent   U046
         SPACE 1
TAPEFEET XR    R0,R0
         D     R0,=F'1200'              GET LENGTH IN FEET
         CH    R0,=H'600'               ROUND UP?
         BL    *+8                      NO                         U021
         AH    R1,=H'1'                 ROUND UP.                  U021
         CVD   R1,DWD
         MVC   1(5,R15),=X'2020202120'
         ED    0(6,R15),DWD+5
         BR    R14                      RETURN TO CALLER
         SPACE 2
TAPE_PCT M     R0,=F'10000'             scale for percent(100)*100 U046
         CLI   DCBDEVT-IHADCB(R3),DCBDVMT4  3480?                  U046
         BE    PCT_CART                                            U046
PCT_REEL D     R0,=A(2400*12*100)       divide by inches*100/reel  U046
         B     PCT_CONV                                            U046
PCT_CART D     R0,=A(150*3937)          divide by inches*100/cart  U046
PCT_CONV CVD   R1,DWD                                              U046
         MVC   1(6,R15),=X'2020214B2020'                           U046
         ED    0(7,R15),DWD+5                                      U046
         BR    R14                      RETURN TO CALLER           U046
         SPACE 2
*  SUBROUTINE TO DO I/O TO TAPE
TAPIO    ST    R0,TAPEIOB+16            SET CCW ADDR
         EXCP  TAPEIOB
         WAIT  ECB=TAPEECB
         CLI   TAPEECB,X'7F'            DID IT WORK?
         BE    TAPIO$OK                 YES - CONTINUE
         CLI   TAPEECB,X'41'            CHANNEL PROGRAM ERROR?
         BNE   *+10     >=======+       NO - ERROR
         CLI   IOBCSW+4,X'0D'   |       CHEND, DEVEND, UNITEXCPT?
         BER   LINK             |       YES - IT'S REALLY A TAPEMARK
         ST    LINK,LINKTPIO <==+       SAVE BAL REGISTER          U016
         CLC   LCTR,=H'60'              ANYTHING ON PAGE?          U043
         BL    *+8                      YES - CONTINUE             U013
         NEWPAGE  ,                     PRINT THE TITLE            U013
         MVC   OUTBUFF(44),=C'-*** Tape I/O error --- ECB code=XX *** C$
               SW='                                                U013
         HEX   OUTBUFF+33,TAPEECB,LEN=1                            U013
         HEX   OUTBUFF+44,IOBCSW+1,LEN=7                           U013
         MVC   OUTBUFF+59(24),=C'*** IOBSENSE0,1=XXXX ***'         U013
         HEX   OUTBUFF+75,TAPEIOB+2,LEN=2                          U013
         PRTLN OUTBUFF
         SPACE 1
         AIF   (&MVS).MVS02                                        U020
**       L     R1,DD#PTR                -> DD SECTION OF TIOT      U013
**       SH    R1,=H'24'                -> TIOT                    U013
**       AH    R1,TAPEDCB+40            -> DD ENTRY FOR TAPE       U013
**       L     R1,16(,R1)               -> UCB                     U013
         L     R1,TAPEMFL               -> DCB                     U013
         L     R1,DCBDEBAD-IHADCB(,R1)  -> DEB                     U025
         L     R1,32(,R1)               -> UCB                     U013
         L     R2,24(,R1)               -> EXTENDED SENSE INFO     U013
         MVC   OUTBUFF(16),=C' *** Sense data='                    U013
         HEX   OUTBUFF+16,(00,R2),LEN=4                            U013
         HEX   OUTBUFF+25,(04,R2),LEN=4                            U013
         HEX   OUTBUFF+35,(08,R2),LEN=4                            U013
         HEX   OUTBUFF+44,(12,R2),LEN=4                            U013
         HEX   OUTBUFF+54,(16,R2),LEN=4                            U013
         HEX   OUTBUFF+63,(20,R2),LEN=4                            U013
         PRTLN OUTBUFF                  PRINT THE SENSE DATA       U013
         SPACE 3
         NEWPAGE  10                    NEXT PAGE IF NEED ROOM     U015
         MVC  OUTBUFF(35),=C'0    Bits set are flagged ''*'' below'U015
         PRTLN OUTBUFF                  PRINT HEADER               U015
         MVI   OUTBUFF,C'0'             DOUBLE SPACE FIRST ONE     U015
         SPACE 2
*  R2 -> SENSE DATA                                                U015
         LA    R3,10                    NUMBER OF BYTES            U015
         L     R4,=A(ERR#LIST)          -> ERROR MESSAGES          U015
         SPACE 1
NEXTBYTE LA    R6,OUTBUFF+5             -> LINE POS                U015
         LA    R1,X'80'                 MASK BIT                   U015
         SPACE 1
TEST$BIT MVC   2(10,R6),0(R4)           MOVE TEXT                  U015
         EX    R1,BIT$TM                IS THIS BIT SET?           U015
         BZ    *+8                      NO - SKIP                  U015
         MVI   0(R6),C'*'               YES - FLAG THIS ONE        U015
         SPACE 1
         LA    R4,10(,R4)               -> NEXT MSG                U015
         LA    R6,16(,R6)               -> NEXT PRINT LINE POS     U015
         SRA   R1,1                     MOVE BIT RIGHT             U015
         BNZ   TEST$BIT                 CONTINUE WITH THIS BYTE    U015
         PRTLN OUTBUFF                  PRINT DESC OF THIS BYTE    U015
         LA    R2,1(,R2)                -> NEXT BYTE OF SENSE DATA U015
         BCT   R3,NEXTBYTE              CONTINUE IF MORE           U015
.MVS02   ANOP                                                      U020
         SPACE 3
         CLI   WHERE,3                  READING DATA?
         BNE   CLOSE$TP                 NO - CLOSE UP AND LEAVE
         L     R1,TAPEIOB+16            GET CCW ADDR
         CLI   0(R1),RD                 WAS IT A READ?
         BNE   CLOSE$TP                 NO - CLOSE UP AND LEAVE
         LA    R0,TCCW#FSF              SKIP REST OF DATA...
         L     LINK,LINKTPIO            RESTORE BAL REGISTER       U016
         B     TAPIO                    START AT TOP OF THIS SUBRTN
         SPACE 2
         AIF   (&MVS).MVS03                                        U020
BIT$TM   TM    0(R2),*-*                << EXECUTED >>             U015
.MVS03   ANOP                                                      U020
         SPACE 2
TAPIO$OK L     R15,TAPEIOB+16           -> CCW                     U037
         XC    LASTSIZE,LASTSIZE        assume not a data read     U037
         CLI   0(R15),RD                was it a data read?        U037
         BNE   4(,LINK)                 no - skip this junk        U037
         SR    R0,R0                    clear for icm              U037
         ICM   R0,B'0011',6(R15)        get length requested       U037
         MVC   DWD+2(2),IOBCSW+6        COPY LENGTH LEFT
         XC    DWD(2),DWD               CLEAR HIGH 2 BYTES
         S     R0,DWD                   MINUS LENGTH UNUSED
*%%% make a fullword called LASTSIZF                               U037
         STH   R0,LASTSIZE              EQUALS SIZE READ
         TM    TFLAG1,T1@ANLZ           ANALYZING?
         BNO   4(,LINK)                 NO - SKIP THE REST
         A     R0,BYTECNT               ADD PREVIOUS BYTE COUNT
         ST    R0,BYTECNT               SAVE NEW TOTAL
         L     R1,BLOCKCNT              GET BLOCK COUNT
         LA    R1,1(,R1)                INCR
         ST    R1,BLOCKCNT              SAVE NEW COUNT
         CLC   LASTSIZE,TAPEBUFF        START WITH BLKSIZE?
         BE    *+8                      YES - COULD BE RECFM=V
         NI    NLFLAGS,255-NL@V         NO - NOT RECFM=V
         CLC   LASTSIZE,MAXBLKSI        SAME SIZE?
         BE    4(,LINK)                 YES - DONE HERE
         BH    TAPIO$H                  LAST BLOCK IS BIGGEST
         TM    NLFLAGS,NL@CHANG         ALREADY CHANGED?
         BO    TAPIO$NL                 YES - TURN OFF RECFM=F
         OI    NLFLAGS,NL@CHANG         SET "CHANGED"
         B     4(,LINK)                 RETURN
         SPACE 1
TAPIO$H  OI    NLFLAGS,NL@CHANG         SET "CHANGED"
         MVC   MAXBLKSI,LASTSIZE        SAVE NEW BIGGEST BLKSIZE
TAPIO$NL NI    NLFLAGS,255-NL@F         NOT RECFM=F
         B     4(,LINK)                 RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO FIND A TIOT ENTRY
TIOTSCAN L     R1,DD#PTR                -> DD SECTION
         XR    R0,R0                    CLEAR FOR IC
         BAL   R14,*+6                  SET LOOP ADDR & SKIP
         SPACE 1
         AR    R1,R0                    POINT TO NEXT ENTRY
         IC    R0,0(,R1)                GET LENGTH OF THIS ENTRY
         LTR   R0,R0                    END OF TIOT?
         BZR   LINK                     YES - "NOT FOUND" RETURN
         CLC   4(8,R1),0(R15)           THIS IT?
         BNER  R14                      NO - LOOP
         CLC   =F'0',16(R1)             DD DUMMY? (LIKE MSM?)
         BNE   4(,LINK)                 NO - RETURN (FOUND)
         BR    LINK                     SAY NOT FOUND
         SPACE 3
*  SUBROUTINE TO FORMAT DATE FROM TAPE LABEL                       U027
FMT$DATE CLC   =C' 00000',0(R1)         IS IT ZERO?                U048
         BER   LINK                     YES - RETURN (LEAVE BLANK) U027
         CLC   BLANKS(6),0(R1)          OR BLANK?                  U048
         BER   LINK                     YES - RETURN (IT'S STILL)  U030
         TM    PARMFLG2,P2@JDATE        JULIAN DATE REQUESTED?     U027
         BO    FD$JDATE                 YES - GO DO IT             U027
         CLC   3(3,R1),=C'00000'        DAY OF YEAR = ZERO?        U048
         BE    FD$JDATE                 YES - FORCE JULIAN FORMAT  U031
         MVC   DWD(5),1(R1)             COPY DATE                  U048
         NC    DWD(5),=C'00000'         REMOVE DIGITS, LEAVE ZONES U031
         CLC   DWD(5),=C'00000'         WAS IT ALL NUMERIC?        U031
         BNE   FD$JDATE                 NO - FORCE JULIAN FORMAT   U031
         PACK  DWD,1(2+1,R1)            PACK (SORT OF) YEAR        U048
*                                       (GET 000000000000YYXS)     U027
         L     R14,=A(MONTHS)           -> TARGET                  U030
         MVI   6+1(R14),28              ASSUME NOT LEAP YEAR       U030
         TM    DWD+6,X'01'              LEAP YEAR?                 U027
         BO    FD$01                    NO (ODD YEAR)              U027
         TM    DWD+6,X'12'              DIVISIBLE BY 4?            U027
         BM    FD$01                    NO - NOT LEAP YEAR         U027
         MVI   6+1(R14),29              YES - FEB HAS 29 DAYS      U030
FD$01    PACK  DWD,3(3,R1)              PACK DAY OF YEAR           U048
         CVB   R0,DWD                   GET IT IN BINARY           U027
         LA    R3,1                     INIT MONTH NUMBER          U027
FD$02    SH    R0,0(,R14)               SUB NUM OF DAYS THIS MONTH U027
         BNP   FD$03                    HAVE MONTH                 U027
         LA    R14,6(,R14)              NEXT TABLE ENTRY           U027
         LA    R3,1(,R3)                BUMP MONTH                 U027
         B     FD$02                    KEEP LOOKING               U027
FD$03    AH    R0,0(,R14)               GET BACK CORRECT RESIDUAL  U027
         CVD   R0,DWD                   CONVERT DAY OF MONTH       U028
         OI    DWD+7,X'0F'              REMOVE SIGN                U028
         TM    PARMFLG2,P2@GDATE        GREGORIAN DATE REQUESTED?  U028
         BO    FD$GDATE                 YES - GO DO IT             U028
         MVC   0(3,R15),2(R14)          MONTH NAME TO ANSWER       U048
         UNPK  3(2,R15),DWD             FORMAT DAY OF MONTH        U048
         MVI   5(R15),C','              put in separator           U048
         MVC   6(2,R15),=C'19'          assume 19xx                U048
         MVC   8(2,R15),1(R1)           PUT YEAR IN ANSWER         U048
         CLI   0(R1),C' '               is it 19xx?                U048
         BER   LINK                     yes - all done             U048
         MVI   6(R15),C'2'              no - set millenium         U048
         MVC   7(1,R15),0(R1)           ... and century            U048
         BR    LINK                     RETURN TO CALLER           U028
         SPACE 1
FD$GDATE UNPK  3(2,R15),DWD             FORMAT DAY OF MONTH        U048
         MVI   5(R15),C'/'              put in separator           U048
         CVD   R3,DWD                   CONVERT MONTH              U027
         OI    DWD+7,X'0F'              REMOVE SIGN                U027
         UNPK  0(2,R15),DWD             FORMAT MONTH               U027
         MVI   2(R15),C'/'              put in separator           U048
         MVC   6(2,R15),=C'19'          assume 19xx                U048
         MVC   8(2,R15),1(R1)           PUT YEAR IN ANSWER         U048
         CLI   0(R1),C' '               is it 19xx?                U048
         BER   LINK                     yes - all done             U048
         MVI   6(R15),C'2'              no - set millenium         U048
         MVC   7(1,R15),0(R1)           ... and century            U048
         BR    LINK                     RETURN TO CALLER           U027
         SPACE 1
FD$JDATE MVC   2(2,R15),1(R1)           YEAR                       U048
         MVI   4(R15),C'.'                                         U048
         MVC   5(3,R15),3(R1)           DAY OF YEAR                U048
         MVC   0(2,R15),=C'19'          assume 19xx                U048
         CLI   0(R1),C' '               is it?                     U048
         BER   LINK                     yes - return to caller     U048
         MVI   0(R15),C'2'              no - set millenium         U048
         MVC   1(1,R15),0(R1)           ... and century            U048
         BR    LINK                     RETURN TO CALLER           U027
         SPACE 3
*  SUBROUTINE TO REMOVE LEADING ZEROES
DEZERO   CLI   0(R1),C'0'               NON-ZERO YET?
         BNER  R14                      YES - RETURN
         MVI   0(R1),C' '               MAKE IT A BLANK
         AH    R1,=H'1'                 ADVANCE                    U021
         B     DEZERO                   LOOP
         SPACE 3
NEWPAG1C CH    R0,LCTR                  enough lines left?         U043
         BNHR  LINK                     YES - JUST RETURN          U043
         SPACE 1
NEWPAG1  MVC   LCTR,LINECNT             RESET LINE COUNTER         U043
         PRTLN HEADER1
         MVI   OUTBUFF,C'+'             SET FOR OVERPRINT
         MVC   OUTBUFF+5(6),VOLSERNO    OVERPRINT THE VOLSER
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE
*U043    BAL   R14,PUTPRTLN             OVERPRINT THE SECOND TIME
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  overprint the second time  U043
         PRTLN OUTBUFF                  AND THE THIRD
         TM    TFLAG1,T1@PAGE1          IS THIS FIRST PAGE OF VOL?
         BNO   NEWPAG$2                 NO - SKIP THIS STUFF.
         NI    TFLAG1,255-T1@PAGE1      TURN OFF FLAG
         MVC   HEADER2+1(28),HEADER2    BLANK IT OUT
         TM    TFLAG1,T1@PARM           HAVE PARM FIELD?
         BNO   NEWPAG$4                 NO - SKIP
         TM    TFLAG2,T2@LPARM          IS IT TOO LONG?
         BO    NEWPAG$4                 YES - SKIP
         MVC   HEADER2+1(28),PARM#MSG+1 MOVE IN THE PARM FIELD
         SPACE 1
NEWPAG$4 PRTLN HEADER2                  PRINT AUTHOR LINE
         TM    TFLAG1,T1@PARM           HAVE PARM FIELD?
         BNO   NEWPAG$2                 NO - DON'T PRINT IT
         TM    TFLAG2,T2@LPARM          LONG PARM FIELD?
         BNO   NEWPAG$5                 NO - ALREADY PRINTED       U015
         PRTLN PARM#MSG                 PRINT IT
NEWPAG$5 TM    TFLAG1,T1@PERR           ERROR IN PARM FIELD?
         BNO   NEWPAG$2                 NO - DON'T PRINT IT
         PRTLN PARMERR,I                PRINT IT                   U022
         SPACE 1
NEWPAG$2 LA    R0,DASHES
*U043    BAL   R14,PUTPRTLN             SEPARATOR LINE
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  separator line             U043
         PRTLN COLHEAD1,I
         PRTLN COLHEAD2,I
         LA    R0,DASHES
*U043    BAL   R14,PUTPRTLN             SEPARATOR LINE
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  separator line             U043
         MVI   OUTBUFF,C'0'             DOUBLE SPACE THE FIRST FILEU015
         LH    R0,LCTR                  get current line count     U043
         BCTR  R0,0                     ADJUST THE LINE COUNT      U043
         STH   R0,LCTR                  store updated line count   U043
         TM    TFLAG2,T2@RQVOL          WRONG VOLUME MOUNTED?      U015
         BNO   TRYASCII                 NO - SEE IF ASCII SL       U023
         NI    TFLAG2,255-T2@RQVOL      TURN OFF FLAG NOW          U015
         MVI   OUTBUFF,C'-'             TRIPLE SPACE               U015
*U046    MVI   OUTBUFF+41,C'*'          START OF BOX               U015
*U046    MVC   OUTBUFF+42(49),OUTBUFF+41  REST OF IT               U015
*U046    PRTLN OUTBUFF                  PRINT TOP OF BOX           U015
*U046    MVC   OUTBUFF+41(50),=C'* Id of mounted volume differs from id$
                requested *'            MOVE IN MSG                U015
         MVI   OUTBUFF+1,C'*'           START OF BOX               U046
         MVC   OUTBUFF+2(46),OUTBUFF+1  REST OF IT                 U046
         PRTLN OUTBUFF                  PRINT TOP OF BOX           U015
*U046    MVC   OUTBUFF+41(50),=C'* Id of mounted volume differs from id$
                requested *'            MOVE IN MSG                U015
         MVC   OUTBUFF+1(47),=C'* WARNING:  Your JCL requested volume  $
               xxxxxx *'                MOVE IN MSG                U046
         MVC   OUTBUFF+40(6),JFCB+118   fill in JCL volser         U046
         PRTLN OUTBUFF                  PRINT MSG                  U046
         MVC   OUTBUFF+1(47),=C'* Internal volser of mounted volume is $
               xxxxxx *'                MOVE IN MSG                U046
         MVC   OUTBUFF+40(6),VOLSERNO   fill in actual volser      U046
         PRTLN OUTBUFF                  PRINT MSG                  U046
         LH    R0,LCTR                  get current line count     U043
         SH    R0,=H'2'                 account for triple space   U043
         STH   R0,LCTR                  store updated line count   U043
*U046    MVI   OUTBUFF+41,C'*'          START OF BOX               U015
*U046    MVC   OUTBUFF+42(49),OUTBUFF+41  REST OF IT               U015
         MVI   OUTBUFF+1,C'*'           START OF BOX               U046
         MVC   OUTBUFF+2(46),OUTBUFF+1  REST OF IT                 U046
         PRTLN OUTBUFF                  PRINT BOTTOM OF BOX        U015
         MVI   OUTBUFF,C'0'             DOUBLE SPACE FIRST FILE    U015
         SPACE 2
TRYASCII TM    TFLAG2,T2@ASCII          IS TAPE ASCII SL?          U023
         BNOR  LINK                     NO - ALL DONE              U023
         MVI   OUTBUFF,C'-'             TRIPLE SPACE               U023
         MVI   OUTBUFF+16,C'*'          START OF BOX               U023
         MVC   OUTBUFF+17(24),OUTBUFF+16  REST OF IT               U023
         PRTLN OUTBUFF                  PRINT TOP OF BOX           U023
         MVC   OUTBUFF+16(25),=C'* Tape labels are ASCII *'        U023
         PRTLN OUTBUFF                  PRINT MSG                  U023
         LH    R0,LCTR                  get current line count     U043
         SH    R0,=H'2'                 account for triple space   U043
         STH   R0,LCTR                  store updated line count   U043
         MVI   OUTBUFF+16,C'*'          START OF BOX               U023
         MVC   OUTBUFF+17(24),OUTBUFF+16  REST OF IT               U023
         PRTLN OUTBUFF                  PRINT BOTTOM OF BOX        U023
         MVI   OUTBUFF,C'0'             DOUBLE SPACE FIRST FILE    U023
         BR    LINK                     RETURN
         SPACE 3
         LTORG ,                                                   U043
         SPACE 3
PRTLN    SUBINIT  ,                                                U043
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE
*U043    BAL   R14,PUTPRTLN             WRITE OUTPUT LINE
         L     R1,OPENMFL               -> SYSPRINT DCB            U043
         PUT   (1),(0)                  write output line          U043
         MVC   OUTBUFF,OUTCLEAR         CLEAR OUTPUT LINE
         LH    R15,LCTR                 get current line count     U043
         SH    R15,=H'1'                decrement                  U043
         STH   R15,LCTR                 store updated line count   U043
         BP    PRTLNRET                 return if more lines left  U043
         NEWPAGE ,                      DO A NEW PAGE
PRTLNRET SUBEXIT  ,                     RETURN TO CALLER           U043
         SPACE 2
PUTPRTLN L     R1,OPENMFL               -> SYSPRINT DCB
         PUT   (1),(0)                  WRITE OUTPUT LINE
         ORG   *-2                      BACK OVER BALR
         BR    R15                      MAKE HIM RETURN TO MY CALLER
         SPACE 2
         DROPX LBASE                    PRTLN                      U043
         LTORG ,                                                   U043
         SPACE 3
TAPEMAP  CSECT ,                        resume                     U043
NEWPAG2C CH    R0,LCTR2                 ENOUGH LINES LEFT?         U023
         BLR   LINK                     YES - JUST RETURN
         SPACE 1
NEWPAG2  LA    R0,HEADER1               GET RECORD ADDR
*U043    BAL   R14,PUTPRNT2+4           WRITE IT OUT
         L     R1,OPENMFL2              -> SYSPRNT2 DCB            U043
         PUT   (1),(0)                  write it out               U043
         LA    R0,DASHES
*U043    BAL   R14,PUTPRNT2+4           WRITE IT OUT
         L     R1,OPENMFL2              -> SYSPRNT2 DCB            U043
         PUT   (1),(0)                  write it out               U043
         MVC   LCTR2,LINECNT            RESET LINE COUNTER         U023
         BR    LINK                     RETURN TO CALLER
         SPACE 3
PRTLN2   SUBINIT  ,                                                U043
         LA    R1,3                     ASSUME TRIPLE SPACE
         CLI   OUTBUFF2,C'-'            IS IT?
         BE    P2$OK                    YES
         BCTR  R1,0                     ASSUME DOUBLE SPACE
         CLI   OUTBUFF2,C'0'            IS IT?
         BE    P2$OK                    YES
         BCTR  R1,0                     ASSUME SINGLE SPACE
         CLI   OUTBUFF2,C' '            IS IT?
         BE    P2$OK                    YES
         MVI   OUTBUFF2,C' '            MAKE A BLANK
         SPACE 1
P2$OK    LH    R14,LCTR2                GET SYSPRNT2 LINE COUNT    U023
         SR    R14,R1                   DECR LINE COUNT            U023
         STH   R14,LCTR2                SAVE UPDATED COUNT         U023
         BP    P2$GO                    CONTINUE IF OK
         NEWPAGE  ,2                    SET NEW PAGE
         MVI   OUTBUFF2,C'-'            TRIPLE SPACE FIRST LINE    U013
         LH    R14,LCTR2                GET SYSPRNT2 LINE COUNT    U023
         SH    R14,=H'3'                ACCOUNT FOR IT             U023
         STH   R14,LCTR2                SAVE UPDATED COUNT         U023
         SPACE 1
P2$GO    BAL   R14,PUTPRNT2             WRITE OUTPUT LINE
         MVC   OUTBUFF2,OUTCLR2         CLEAR OUTPUT LINE
         SUBEXIT  ,                     RETURN TO CALLER           U043
         SPACE 3
PUTPRNT2 LA    R0,OUTBUFF2              GET RECORD ADDR
         L     R1,OPENMFL2              -> SYSPRNT2 DCB
         PUT   (1),(0)                  WRITE IT OUT
         ORG   *-2                      BACK OVER BALR
         BR    R15                      MAKE HIM RETURN TO MY CALLER
         SPACE 2
         DROPX LBASE                    PRTLN2                     U043
         LTORG ,                                                   U043
         SPACE 3
TAPEMAP  CSECT ,                        resume                     U043
         SPACE 3
PRT$EXIT DCBEXIT  BLKSIZE=6118,BUFNO=1                             U021
         SPACE 6
         LTORG ,
         EJECT
*  REGISTERS USED IN ROUTINES TO LIST IEHMOVE & IEBCOPY UNLOADED MEMBRS
BLEN     EQU   R2      *PAIR*           BLOCK LENGTH LEFT
BPTR     EQU   R3      *PAIR*           -> CURRENT POS IN BLOCK
DLEN     EQU   R4                       DIRBLK OR DATA LENGTH LEFT
DPTR     EQU   R5                       -> CURRENT POS IN BLOCK
PPTR     EQU   R6                       -> POS IN PRINT LINE
         SPACE 1
*  OTHER MISC REGISTERS
LBASE    EQU   R7                       LOCAL SUBROUTINE BASE      U021
LINK     EQU   R9                       INTERNAL LINKAGE REGISTER  U023
         SPACE 1
CC       EQU   X'40'                    COMMAND CHAINING
SLI      EQU   X'20'                    SUPPRESS INCORR LENGTH INDIC.
RD       EQU   X'02'                    OPCODE FOR READ
         SPACE 1
BUFFSIZE EQU   65535                    SIZE OF TAPE BUFFER        U032
         SPACE 2
OPENMFL2 OPEN  (SYSPRNT2,OUTPUT),MF=L
OPENMFL  OPEN  (SYSPRINT,OUTPUT),MF=L
OPENMFLI OPEN  SYSIN,MF=L
TAPEMFL  OPEN  TAPEDCB,MF=L
         SPACE 1
EXLST    DC    A(0)
PRTEXLST DC    X'85',AL3(PRT$EXIT)
         SPACE 2
TAPEECB  DC    A(0)
TAPEIOB  DC    X'42000000'              IOBFLAG1 (SET FOR CMD CHAINING)
         DC    A(TAPEECB)               ECB ADDR
IOBCSW   DC    2F'0'                    CSW
         DC    A(0)                     CHANNEL PGM ADDR
         DC    A(TAPEDCB),A(0)          DCB ADDR
         DC    H'1',H'0'                TAPE BLOCK COUNT INCREMENT
         SPACE 1
TCCW#FSF CCW   X'3F',0,0,1              FORWARD SPACE FILE
         SPACE 1
TCCW#L1  CCW   RD,*-*,00,80             READ BLOCK INTO FL1LABI    U021
TCCW#L2  CCW   RD,*-*,00,80             READ BLOCK INTO FL2LABI    U021
TCCW#BSB CCW   X'27',0,0,1              BACK SPACE BLOCK           U033
         SPACE 1
TCCW#DAT CCW   RD,*-*,SLI,BUFFSIZE      READ DATA BLOCK
         SPACE 1
TCCW#RWD CCW   X'07',0,0,1              REWIND THE TAPE
         SPACE 1
TCCW#SNS CCW   X'04',SENSDATA,SLI,24    READ SENSE DATA
         SPACE 2
SENSDATA DC    XL24'00'                 SENSE DATA BUFFER
         SPACE 2
TFLAG1   DC    X'00'                    FLAGS
T1@PARM  EQU   X'80'                    HAVE PARM FIELD
T1@PERR  EQU   X'40'                    ERROR IN PARM FIELD
T1@PAGE1 EQU   X'20'                    THIS IS FIRST TITLE GROUP
T1@BADEN EQU   X'10'                    DENSITY ERROR IN TAPE LABEL(S)
T1@SL    EQU   X'08'                    THIS TAPE IS SL
T1@ANLZ  EQU   X'04'                    PARM=CHECK OR NL TAPE
T1@MVEOF EQU   X'02'                    IEHMOVE2 HAS READ TILL EOF
T1@DATA  EQU   X'01'                    HAVE INPUT DATA
         SPACE 1
TFLAG2   DC    X'00'                    MORE
T2@RQVOL EQU   X'80'                    REQUESTED VOL MISMATCH
T2@LPARM EQU   X'40'                    PARM FIELD LONGER THAN 21 CHARS
T2@PRT2  EQU   X'20'                    SECOND PRINT FILE IN USE
T2@DOSTP EQU   X'10'                    FILE HAS NO HDR2/EOF2      U021
T2@FDROV EQU   X'08'                    FDR FILE HAS OVERFLOW MSG  U022
T2@ASCII EQU   X'04'                    TAPE IS ASCII SL           U023
T2@HDRDF EQU   X'02'                    HDR/EOF LABELS DON'T MATCH U025
T2@HDRBK EQU   X'01'                    HDR FIELDS BLANK           U030
         SPACE 1
TFLAG3   DC    X'00'                    MORE                       U046
T3@DSSBO EQU   X'01'                    DSS BITMAP OVERFLOW        U046
         SPACE 1
NLFLAGS  DC    X'00'                    NON-LABELLED TAPE FLAGS
NL@V     EQU   X'80'                    RECFM=V
NL@F     EQU   X'40'                    RECFM=F
NL@CHANG EQU   X'20'                    BLOCKSIZE HAS CHANGED
         SPACE 1
PARMFLGS DC    0XL2'00'                 PARM FLAGS                 U020
PARMFLG1 DC    X'00'
*        EQU   X'80'                    (CURRENTLY UNUSED)         U020
P1@INLIN EQU   X'40'                    INLINE
P1@NOMEM EQU   X'20'                    NOMEMBERS (NOMEM)
*        EQU   X'10'                    (CURRENTLY UNUSED)         U020
P1@NATTR EQU   X'08'                    NOATTR
P1@SCAN  EQU   X'04'                    SCAN                       U020
P1@CHECK EQU   X'02'                    CHECK
P1@NL    EQU   X'01'                    NL
         SPACE 1
PARMFLG2 DC    X'00'                                               U020
P2@TEST  EQU   X'80'                    TEST                       U020
P2@DEN1  EQU   X'40'                    DEN1                       U020
P2@NONOT EQU   X'20'                    NONOTE                     U020
P2@JDATE EQU   X'10'                    JDATE                      U027
P2@GDATE EQU   X'08'                    GDATE                      U028
*        EQU   X'04'                    (CURRENTLY UNUSED)         U020
P2@PCT   EQU   X'02'                    PCT/PERCENT                U046
P2@FEET  EQU   X'01'                    FEET                       U046
         SPACE 2
IEBCOPY  DC    C'IEBCOPY'
IEHMOVE  DC    C'IEHMOVE'
IEBISAM  DC    C'IEBISAM'
IEHDASDR DC    C'IEHDASDR'
IEBUPDTE DC    C'IEBUPDTE'
FDR      DC    C'FDR'
DSS      DC    C'DSS'                                              U034
         SPACE 2
         DC    C' '                     FOR CLEARING "UNLOADER"
UNLOADER DC    CL9' '             /   /IEBCOPY/IEHMOVE/IEBISAM/IEHDASDR
C00080   DC    C'00080'
         ORG   C00080+1
C00800   DC    C'00800'
RET#ADDR DC    A(EOD2)                  RETURN ADDR
* Density in BPI, binary, char, IBG length in .01 inches, TM len   U037
DEN#LIST DC    F'0200',C' 200',F'60',F'375'                        U037
DEN#TBLL EQU   *-DEN#LIST               length of a table entry    U037
         DC    F'0556',C' 556',F'60',F'375'                        U037
         DC    F'0800',C' 800',F'60',F'375'                        U037
         DC    F'1600',C'1600',F'60',F'375'                        U037
         DC    F'6250',C'6250',F'30',F'375'                        U037
DEN#3480 DC    F'37871',C' 38K',F'08',F'13'                        U046
NULLNUMB DC    H'0'                     # OF NULL FILES TO SKIP    U014
NUMBNULL DC    H'0'                     # OF NULL FILES / THIS TAPEU014
LINECNT  DC    Y(60-2)                  # OF LINES/PAGE - 2 TITLES U024
WHERE    DC    X'00'                    WHERE-WE-ARE FLAG
         SPACE 1
HEXTAB   HEXTAB  DUAL=NO
         SPACE 1
ASCIITAB CHARTAB  TYPE=FROM-ASCII,FILL='?'                         U023
         SPACE 2
HEADER1  DC    C'1VOL=xxxxxx  Owner=xxxxxxxxxx       TAPEMAP -- Tape An$
               alysis Program         DAYDAYDAY  MON DD, 19yy  (yy.ddd)$
                 HH:MM:SS            '                             U037
TTL1TIME EQU   *-21                                                U037
TTL1DATE EQU   TTL1TIME-23
TTL1DAY  EQU   TTL1DATE-11
         SPACE 2
HEADER2  DC    C'                                    Copyright (c) Leon$
               ard D. Woren    Version=048<23Dec90>  assembled on      $
                                     '                             U044
         SPACE 2
PARMERR  DC    C' Unknown parm option at col=xxx.  Any remaining parm i$
               gnored.'
         SPACE 1
FVOL#MSG DC    C' *** This is volume # xxxx of the above multiple volum$
               e file.  First volume was xxxxxx.'
         SPACE 1
LENWARN  DC    C'        Lengths for recfms other than f,fb,etc may not$
                be very close to the true length.'                 U022
         SPACE 1
DENWARN  DC    C'0Note:  Length(s) are computed, (based on blksize, blk$
               count, and density), and are therefore only approximate.$
               '                                                   U022
         SPACE 1
INVDEN   DC    C'-*****  One or more files have density indicated incor$
               rectly in labels.  All files are written at xxxx BPI  **$
               ***'                                                U022
         SPACE 1
* --- New devtab table, includes all values for each type          U039
*  + 0 = IBM UCBTYP low byte value                                 U039
*  + 1 = FDR device type character                                 U039
*  + 2 = H-word # trk/cyl (always non-zero)                        U039
*  + 4 = CL8 of device name                                        U039
* Note that IBM uses the same device type for 3380 & 3380E         U039
*                                                                  U039
* Some routines that use this table index directly into it, so     U039
* there must be exactly one entry for each type in the first       U039
* section.  Duplicate entries can be placed in the second section. U039
DEVTABX  DC    0D'0'                                               U039
         DC    X'00',C' ',AL2(01),CL8'unknown '    invalid         U039
DEVTABXL EQU   *-DEVTABX                entry length               U039
         DC    X'01',C' ',AL2(01),CL8'2311    '    * OLD *         U039
         DC    X'02',C' ',AL2(01),CL8'2301    '    * OLD *         U039
         DC    X'03',C' ',AL2(01),CL8'2303    '    * OLD *         U039
         DC    X'04',C' ',AL2(01),CL8'2302    '    * OLD *         U039
         DC    X'05',C' ',AL2(01),CL8'2321    '    * OLD *         U039
         DC    X'06',C'A',AL2(08),CL8'2305-1  '                    U039
         DC    X'07',C'C',AL2(08),CL8'2305-2  '                    U039
         DC    X'08',C'T',AL2(20),CL8'2314    '                    U039
         DC    X'09',C'X',AL2(19),CL8'3330    '                    U039
         DC    X'0A',C'U',AL2(12),CL8'3340-35 '                    U046
         DC    X'0B',C'Z',AL2(30),CL8'3350    '                    U039
         DC    X'0C',C'N',AL2(12),CL8'3375    '                    U039
         DC    X'0D',C'Y',AL2(19),CL8'3330-11 '                    U046
         DC    X'0E',C'M',AL2(15),CL8'3380    '                    U039
         DC    X'0F',C'F',AL2(15),CL8'3390-1  '                    U046
DEVTABXM EQU   (*-DEVTABX)/DEVTABXL-1   maximum devtype in table   U039
*--- Duplicates follow                                             U039
         DC    X'0A',C'V',AL2(12),CL8'3340-70 '                    U046
         DC    X'0E',C'L',AL2(15),CL8'3380-E  '                    U046
         DC    X'0E',C'K',AL2(15),CL8'3380-K  '                    U046
         DC    X'0E',C'D',AL2(15),CL8'3380-1  '  3390-1 emulation? U046
         DC    X'0E',C'E',AL2(15),CL8'3380-2  '  3390-2 emulation? U046
         DC    X'0F',C'G',AL2(15),CL8'3390-2  '                    U046
         DC    X'85',C'B',AL2(20),CL8'F6421   '                    U046
         DC    XL4'00'            END OF TABLE                     U039
         SPACE 2
FDRFOOT1 DC    C'0Note:  Asterisks in SEC field indicates value over 99$
               999, and asterisks in TOTAL field indicates value is at $
               least that size.'                                   U024
         SPACE 1
DSSFOOT1 DC    C'0Note:  Bitmap on tape exceeds program''s capabilities$
                ----- dsname list may not be complete *****'       U034
         SPACE 1
HDRFOOT1 DC    C'0Note:  An asterisk after the file sequence number ind$
               icates header and trailer labels don''t match.  Data pri$
               nted is from header.'                               U025
         SPACE 1
COLTITLE DC    CL78'DSORG  RECFM  BLKSIZE  LRECL  CDATE  L/USED  EXT   $
               SEC   ALC     USED    TOTAL'                        U033
         SPACE 1
HDRFOOTU DC    C'        An underscored field indicates that field didn$
               ''t match in header and trailer labels'             U030
         SPACE 1
HDRFOOT2 DC    C'0Note:  A number(#) sign after the file sequence numbe$
               r indicates one or more fields from the header label is($
               are) blank.'                                        U030
         EJECT
WORKD    DSECT                          RESUME
STACKEND DS    A(SA@STACK+STACKLEN)     -> last stack element      U043
DD#PTR   DS    A                        -> DD SECTION OF TIOT
LEN#TAPE DS    F                        TAPE LENGTH
         DS    F                        NL LENGTH
FILE#SEQ DS    H                        NL FILE SEQ #
LCTR     DS    H                        LINES LEFT/PAGE, SYSPRINT  U043
LCTR2    DS    H                        LINES LEFT/PAGE, SYSPRNT2  U023
LINKSAVE DS    A                        %%%
LINKTPIO DS    A                        %%%
BOOMREGS DS    16F                      REG SAVE AREA FOR LOGIC ERROR
NLSTUFF  DS    0F                                                  U046
MAXBLKSI DS    H                        BIGGEST BLOCK READ SO FAR
LASTSIZE DS    H                        SIZE OF THE LAST BLOCK READ
BLOCKCNT DS    F                        # OF BLOCKS READ
BYTECNT  DS    F                        TOTAL # OF BYTES READ
*%GAPCNT DS    F                                                   U046
NLSTUFFL EQU   *-NLSTUFF                                           U046
DEN@BIN  DS    F       | must           bytes/inch                 U037
DEN@CHAR DS    CL4     | be             bytes/inch                 U037
DEN@GAPL DS    F       | together       gap length, in .01 inches  U037
DEN@TM   DS    F       |                tapemark len in .01 inches U037
DEN@TRUE DS    C                        density character          U037
         DS    CL3                      currently wasted           U037
BITMAP   DS    F                        DF/DSS POINTER TO BITMAP   U034
BITMAPLN DS    H                        DF/DSS LENGTH OF BITMAP    U034
DSSVER   DS    C                        DF/DSS VERSION NUMBER      U034
DSSREL   DS    C                        DF/DSS RELEASE NUMBER      U034
DSSDEVTY DS    CL8                      DF/DSS DUMP DASD TYPE      U034
DSSVOLID DS    CL6                      DF/DSS DUMP DASD ID        U034
DSSDEVSZ DS    0F                       DF/DSS DUMP DASD SIZE      U034
DSSDEVCC DS    H                        DF/DSS DUMP DASD CYLS      U034
DSSDEVTT DS    H                        DF/DSS DUMP DASD TRKS/CYL  U034
DSSLINK  DS    F                        DF/DSS LINK SAVE AREA      U034
NL#SAVE  DS    0F,XL(NLSTUFFL)          SAVE FOR NL STUFF IF SL    U046
NL#FLAGX DS    X                        SAME
UPDT#TYP DS    C                        A,R,C FOR UPDTE
UNDER    DC    CL17'_'                  UNDER SCORES               U030
BLANKS   DS    0CL17,CL16' '
OUTCLEAR DC    C' '                     FOR CLEARING OUTBUFF
OUTBUFF  DS    CL133
OUTCLRU  DC    C' '                     FOR CLEARING OUTBUFFU      U030
OUTBUFFU DS    CL133                                               U030
OUTCLR2  DC    C' '                     FOR CLEARING OUTBUFF2
OUTBUFF2 DS    CL133,7C                                            U014
DASHES   DS    CL133
INBUFF   DS    CL80
LABELS   DS    3CL80                    FOR VOL,FL1,FL2
SAVEHDR1 DS    CL80                     SAVE AREA FOR HEADER 1     U025
SAVEHDR2 DS    CL80                     SAVE AREA FOR HEADER 2     U025
MV#ABUF2 DS    A(MV#BUFF2)              ADDR OF SECOND DE-SPANNING BUFF
MV#BUFF1 DS    CL78                     FIRST DE-SPANNING BUFFER
MV#BUFF2 DS    CL78                     SECOND DE-SPANNING BUFFER
PARM#MSG DS    CL110                    FOR PARM
OB2END   DS    A(OUTBUFF2+110)          ADDR OF END OF OUTBUFF2
OB2END2  DS    A(OUTBUFF2+110+10)       SAME FOR UPDTE
DASDRSAV DS    XL24                     FOR HEADER RECORD
DWD      DS    D
JFCB     DS    XL176                    DWD ALIGNED
TRKCYL   DS    F                        TRACKS/CYL                 U022
TRKTOT   DS    F                        TOTAL TRACKS IN DATASET    U022
TRKCCHH  DS    XL5                      CCHHR OF NEXT EXTENT       U022
TTREXMSK DS    X'402021204B'            EDIT MASK FOR CC.TT DISP   U022
*
         DS    0D                       alignment?                 U043
SA@STACK DS    (10*18)F                 10 internal save areas     U043
STACKLEN EQU   *-SA@STACK                                          U043
         DS    18F                      well, ok, 11               U043
*
         ORG   WORKD+2048               ALIGN THE BUFFER REAL WELL
TAPEBUFF DS    CL(BUFFSIZE)             TAPE INPUT DATA BUFFER
WORKLEN  EQU   *-WORKD
         SPACE 2
         ORG   OUTBUFF+1
#UNLOAD  DS    CL9,1C
#FILE    DS    CL4
#MSMATCH DC    C'*'                     HDR/EOF MISMATCH           U025
#BLKFLD  DC    C'#',C' '                HDR BLANK FIELDS           U030
#DSN     DS    CL17,2C
#PSWD    DS    CL1,2C                                              U048
#CDATE   DS    CL10,C                                              U048
#EDATE   DS    CL10,2C                                             U048
#RECFM   DS    CL4,C                                               U048
#LRECL   DS    CL5,2C
#BLKSIZE DS    CL5,2C                                              U030
#BLKCNT  DS    CL6,2C
#DEN     DS    CL4,3C
#TRTCH   DS    CL2,C                                               U048
#LENGTH  DS    CL5,3C
#CUMLEN  DS    CL5,3C
#CREATOR DS    CL17
         SPACE 3
         ORG   OUTBUFFU+#FILE-OUTBUFF                              U030
#FILEU   DS    CL4,3C                                              U030
#DSNU    DS    CL17,2C                                             U030
#PSWDU   DS    CL1,2C                                              U048
#CDATEU  DS    CL10,C                                              U048
#EDATEU  DS    CL10,2C                                             U048
#RECFMU  DS    CL4,C                                               U048
#LRECLU  DS    CL5,2C                                              U030
#BLKSIU  DS    CL5,2C                                              U030
         ORG   OUTBUFFU+#DEN-OUTBUFF                               U030
#DENU    DS    CL4,3C                                              U030
#TRTCHU  DS    CL2,C                                               U048
         ORG   OUTBUFFU+#CREATOR-OUTBUFF                           U030
#CREATU  DS    CL17                                                U030
         SPACE 3
         ORG   OUTBUFF2+9                                          U022
#FDSN    DS    CL44,3C                                             U022
#FDSORG  DS    CL3,5C                                              U022
#FRECFM  DS    CL4,C                                               U022
#FBLKSI  DS    CL5,3C                                              U022
#FLRECL  DS    CL5,3C                                              U022
#FCDATE  DS    CL5,2C                                              U022
#FLDATE  DS    CL5,2C                                              U022
#FEXT    DS    CL2,2C                                              U022
#FSEC    DS    CL6,3C                                              U022
#FALC    DS    CL4,C                                               U022
#FUSED   DS    CL6,3C                                              U022
#FTOTAL  DS    CL6,3C                                              U022
         EJECT
         ORG   LABELS
         SPACE 2
         VOL   DSECT=NO
         FL1   DSECT=NO
         FL2   DSECT=NO
         SPACE 2
TAPEMAP  CSECT                          RESUME                     U021
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  FORMAT DCB ATTRIBUTES FOR NL TAPE                              U021*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
NL$ATTR  SUBINIT  ,                                                U043
*U047    MVC   NLSTUFF(NLSTUFFL),NL#SAVE  restore info             U046
*U047    MVC   NLFLAGS,NL#FLAGX         more...                    U046
         MVI   #RECFM,C'('              SHOW THAT IT'S A GUESS     U021
         MVI   #RECFM+2,C')'            ...                        U021
         SPACE 1
         MVI   #RECFM+1,C'V'            ASSUME RECFM=V             U021
         TM    NLFLAGS,NL@V             IS IT?
         BO    NL$ATTR1                 YES - CONTINUE
         MVI   #RECFM+1,C'F'            ASSUME RECFM=F             U021
         TM    NLFLAGS,NL@F             IS IT?
         BO    NL$ATTR1                 YES - CONTINUE
         MVI   #RECFM+1,C'U'            ANYTHING ELSE IS RECFM=U   U021
         SPACE 2
NL$ATTR1 XR    R0,R0                    CLEAR FOR ICM              U032
         ICM   R0,B'0011',MAXBLKSI      GET SIZE OF LARGEST BLOCK  U032
         CVD   R0,DWD
         MVC   #BLKSIZE,=X'2020202120'                             U030
         ED    #BLKSIZE-1(L'#BLKSIZE+1),DWD+5                      U030
         SPACE 1
         MVC   #LRECL(5),=C'?????'                                 U021
         SPACE 1
         L     R0,BLOCKCNT              GET BLOCK COUNT
         CVD   R0,DWD
         MVC   #BLKCNT-1(7),=X'20202020202120'
         ED    #BLKCNT-2(8),DWD+4
         SPACE 1
         MVC   #DEN+1(1),DEN@TRUE                                  U037
         TM    PARMFLG2,P2@DEN1                                    U020
         BO    *+10
         MVC   #DEN,DEN@CHAR                                       U037
         SPACE 1
         L     R15,BLOCKCNT             COUNT # OF GAPS
         L     R1,BYTECNT
         M     R1-1,=F'100'             SCALE
         D     R1-1,0(,R14)             DIVIDE BY BYTES/INCH
         AH    R1,=Y(375)               ADD 1 TAPEMARK
         TM    TFLAG1,T1@SL             REALLY SL?
         BNO   NL$ATTR2                 NO - SKIP                  U021
         AH    R1,=Y(2*375)             YES - 2 MORE TAPEMARKS
         LA    R15,2(,R15)              INCR # OF GAPS             U021
         TM    TFLAG2,T2@DOSTP          DOS LABELS?                U021
         BO    *+8                      YES - ONLY 2 LABELS        U021
         LA    R15,2(,R15)              INCR # OF GAPS             U021
NL$ATTR2 M     R15-1,DEN@GAPL           multiply by gap length     U037
         AR    R1,R15                   ADD IN LENGTH OF GAPS
         LR    R2,R1                    SAVE
         ICALL TAPE_LEN,R14,R15=#LENGTH-1
         A     R2,LEN#TAPE+4
         ST    R2,LEN#TAPE+4
         LR    R1,R2                    COPY FOR SUBROUTINE
         ICALL TAPE_LEN,R14,R15=#CUMLEN-1
         SPACE 2
         PRTLN OUTBUFF
         CLI   UNLOADER,C' '            ANY SECOND PRINT LINE?
         BE    NLA$RET                  NO - ALL DONE              U043
         TM    PARMFLG1,P1@INLIN        PARM=INLINE?
         BNO   NLA$RET                  NO - NO SECOND LINE        U043
         PRTLN OUTBUFF2
         MVC   OUTBUFF2,OUTCLR2
NLA$RET  SUBEXIT  ,                     RETURN TO CALLER           U043
         DROPX LBASE                    NL$ATTR                    U021
         SPACE 2
         LTORG ,                                                   U021
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  INITIALIZATION CODE                                            U014*
*                                                                     *
*---------------------------------------------------------------------*
INIT     SUBINIT  ,                                                U043
         MVI   BLANKS,C' '
         MVC   BLANKS+1(L'BLANKS+L'OUTBUFF),BLANKS  GET OUTBUFF ALSO
         MVI   OUTCLEAR,C' '            CLEAR IT                   U030
         MVC   OUTCLR2(L'OUTBUFF2),OUTCLEAR                        U030
         MVI   OUTCLRU,C' '             CLEAR IT                   U030
         MVC   OUTBUFFU,OUTCLRU                                    U030
         MVI   OUTBUFFU,C'+'            OVER PRINT                 U030
         MVI   DASHES,C' '              SET CC                     U021
         MVI   DASHES+1,C'-'
         MVC   DASHES+2(L'DASHES-2),DASHES+1
         MVI   UNDER,C'_'                                          U030
         MVC   UNDER+1(L'UNDER-1),UNDER FILL                       U030
         MVC   HEADER2+105(13),TAPEMAP+14 fill in asm date & time  U037
         MVC   PARM#MSG+1(L'PARM#MSG-1),PARM#MSG
         MVC   TTREXMSK,=X'402021204B'  INSERT DEFAULT EDIT MASK   U022
         XC    JFCB(176),JFCB
         LA    R0,JFCB
         ST    R0,EXLST
         MVI   EXLST,X'87'
         LA    R0,TAPEBUFF              GET ADDR OF DATA BUFFER
         STCM  R0,B'0111',TCCW#DAT+1    SET INTO CCW               U021
         LA    R1,FL1LABI               GET LABEL BUFFER ADDR
         STCM  R1,B'0111',TCCW#L1+1     SET IN CCW                 U021
         LA    R0,FL2LABI               GET LABEL BUFFER ADDR
         STCM  R0,B'0111',TCCW#L2+1     SET IN CCW                 U021
         LA    R1,MV#BUFF2              GET 2ND BUFFER ADDR
         ST    R1,MV#ABUF2              SAVE
         LA    R0,OUTBUFF2+110          GET ADDR OF END OF OUTBUFF2
         ST    R0,OB2END                SAVE FOR FUTURE REFERENCE
         LA    R0,OUTBUFF2+110+10       FOR UPDTE STUFF
         ST    R0,OB2END2
         SPACE 2
         OPEN  MF=(E,OPENMFL)           OPEN PRINT FILE
         L     R1,OPENMFL               -> SYSPRINT DCB
         TM    48(R1),X'10'             OPEN?
         BO    PRINTOK                  YES
         LA    R1,100                   GET ABEND CODE
         SVC   13                       AND LEAVE
         SPACE 2
PRINTOK  LH    R15,0(,R2)               GET PARM LEN
         LTR   R15,R15                  ANY?
         BNP   DONEPARM                 NO
         LA    R14,2(,R2)               SET ADDR OF FIRST ITEM
         LR    R0,R14                   SAVE IT
         MVC   PARM#MSG(8),=C' PARM='' '
         MVC   PARM#MSG+8(L'PARM#MSG-8),PARM#MSG+7   BLANK THE REST
         CH    R15,=H'21'               PARM TOO LONG FOR "HEADER2"?
         BNH   *+8                      NO - SKIP
         OI    TFLAG2,T2@LPARM          YES - SET FLAG
         EX    R15,MVCPARM              SAVE PARM IN PARM#MSG
         LA    R1,PARM#MSG+7(R15)       GET ADDR OF END+1
         MVI   0(R1),C''''              PUT IN ENDING QUOTE
         OI    TFLAG1,T1@PARM           SET "HAVE PARM" FLAG
         SPACE 2
NEXTPARM LM    R1,R3,=A(PARMTAB,PARMTABL,PARMLAST)  GET BXLE REGS  U020
TESTPARM LH    R4,0(,R1)                GET LENGTH-1 OF ITEM
         EX    R4,CLCPARM               THIS IT?
         BNE   INCRPARM                 NO - TRY NEXT
         OC    PARMFLGS,2(R1)           TURN ON BITS               U020
         LA    R14,2(R14,R4)            INCR SCAN PTR
         SR    R15,R4                   DECR LENGTH LEFT
         SH    R15,=H'2'
         BP    NEXTPARM                 LOOP IF ANY MORE
         B     DONEPARM                 DONE...
         SPACE 2
NULLPARM BAL   LINK,NUMBPARM            CONVERT NUMERIC OPERAND    U024
         STH   R1,NULLNUMB              SAVE                       U024
         LTR   R15,R15                  ANY MORE PARM?             U024
         BP    NEXTPARM                 YES - GO PROCESS           U024
         B     DONEPARM                 NO                         U024
         SPACE 2
LINEPARM BAL   LINK,NUMBPARM            CONVERT NUMERIC OPERAND    U024
         SH    R1,=H'2'                 ACCOUNT FOR 2 TITLE LINES  U024
         STH   R1,LINECNT               SAVE                       U024
         LTR   R15,R15                  ANY MORE PARM?             U024
         BP    NEXTPARM                 YES - GO PROCESS           U024
         B     DONEPARM                 NO                         U024
         SPACE 2
NUMBPARM SR    R15,R4                   DECR LENGTH LEFT           U024
         BNP   DONEPARM                 NOTHING LEFT               U014
         AR    R14,R4                   POINT TO OPERAND           U024
         SR    R1,R1                    CLEAR ACCUMULATOR          U014
         SPACE 1
NUMBLOOP CLI   0(R14),C'0'              DIGIT?                     U024
         BL    HAVENUMB                 NO - HAVE COMPLETE NUMBER  U024
         IC    R0,0(,R14)               GET A DIGIT                U014
         N     R0,=F'15'                STRIP IT                   U020
         MH    R1,=H'10'                SHIFT PREVIOUS             U014
         AR    R1,R0                    ADD NEW                    U014
         LA    R14,1(,R14)              BUMP SCAN PTR              U014
         BCT   R15,NUMBLOOP             CONT FOR LENGTH OF PARM    U024
         SPACE 2
HAVENUMB LA    R14,1(,R14)              BUMP SCAN PTR              U024
         BCTR  R15,0                    DECR LENGTH LEFT           U024
         BR    LINK                     RETURN TO CALLER           U024
         SPACE 2
INCRPARM BXLE  R1,R2,TESTPARM           KEEP LOOKING
*  DIDN'T FIND IT IN TABLE
         LA    R4,4+1                   SET KEYWORD LENGTH         U024
         CLC   =C'NULL=',0(R14)         SPECIAL KIND?              U014
         BE    NULLPARM                 YES - PROCESS IT           U014
         LA    R4,7+1                   SET KEYWORD LENGTH         U024
         CLC   =C'LINECNT=',0(R14)      SPECIAL KIND?              U024
         BE    LINEPARM                 YES - PROCESS IT           U024
         LA    R4,2+1                   SET KEYWORD LENGTH         U024
         CLC   =C'LC=',0(R14)           SPECIAL KIND?              U024
         BE    LINEPARM                 YES - PROCESS IT           U024
*  INVALID PARM ITEM
         SR    R14,R0                   COMPUTE OFFSET INTO PARM
         LA    R14,1(,R14)              FIX IT
         CVD   R14,DWD                  CONVERT TO PACKED
         L     R14,=A(PARMERR)          -> PARMERR MSG             U022
         UNPK  28(3,R14),DWD+6(2)       PUT INTO MSG               U022
         OI    30(R14),C'0'             FIX SIGN                   U022
         OI    TFLAG1,T1@PERR           SET "PARM ERROR" FLAG
         SPACE 3
DONEPARM MVC   LCTR,=H'60'              INIT LINECOUNTER           U043
         SPACE 2
         TODAY TTL1DATE,PUNCT=NO,WEEKDAY=TTL1DAY,TIME=TTL1TIME     U020
         SPACE 2
         L     R1,16                    -> CVT
         L     R1,0(,R1)                -> TCBWORDS
         L     R1,4(,R1)                -> TCB
         L     R1,12(,R1)               -> TIOT
         LA    R1,24(,R1)               -> DD SECTION
         ST    R1,DD#PTR                SAVE PTR
         SPACE 1
         TM    PARMFLG1,P1@INLIN+P1@NATTR PARM=INLINE OR NOATTR?
         BNZ   OK$PRT2                  YES - SKIP SECOND PRINT FILE
         L     R2,OPENMFL2              -> SYSPRNT2 DCB
         ICALL TIOTSCAN,LINK,R15=40(,R2) SEE IF WE HAVE IT
         B     OK$PRT2                  NOPE
         SPACE 1
         OPEN  MF=(E,OPENMFL2)          OPEN SECOND PRINT FILE
         OI    TFLAG2,T2@PRT2           ASSUME IT WORKS.
         TM    DCBOFLGS-IHADCB(R2),DCBOFOPN  DID IT OPEN?          U025
         BO    OK$PRT2                  YES
         SPACE 2
ABEND99  LA    R1,99                    GET ABEND CODE
         SVC   13                       BYE
         SPACE 2
OK$PRT2  L     R2,OPENMFLI              -> SYSIN DCB
         ICALL TIOTSCAN,LINK,R15=40(,R2) SEE IF IT'S THERE
         B     RDJFCB                   NOPE
         MVC   RET#ADDR,=A(READCARD)    SET RETURN ADDR
         SPACE 2
         OPEN  MF=(E,OPENMFLI)          OPEN INPUT FILE
         TM    48(R2),X'10'             OPEN?
         BZ    ABEND99                  NO.  (THIS SHOULD NEVER HAPPEN)
         MVI   OPENMFL,0                SET TO CLOSE BOTH FILES
         SUBEXIT  ,                     GO START MAIN PROCESSING   U043
         SPACE 2
MVCPARM  MVC   PARM#MSG+7(0),0(R14)     << EXECUTED >>
CLCPARM  CLC   4(*-*,R1),0(R14)         << EXECUTED >>             U020
         SPACE 2
         LTORG ,                                                   U014
         SPACE 2
         DROPX LBASE                                               U021
         EJECT
PARMTAB  PARMTAB  TEST,(0,P2@TEST)                                 U032
PARMTABL EQU   *-PARMTAB                                           U020
         PARMTAB  INLINE,(P1@INLIN,0)                              U032
         PARMTAB  NOMEMBERS,(P1@NOMEM,0)                           U032
         PARMTAB  NOMEM,(P1@NOMEM,0)                               U032
         PARMTAB  DEN1,(0,P2@DEN1)                                 U032
         PARMTAB  NOATTR,(P1@NATTR,0)                              U032
         PARMTAB  NONOTE,(0,P2@NONOT)                              U032
         PARMTAB  CHECK,(P1@CHECK,0)                               U032
         PARMTAB  SCAN,(P1@SCAN,0)                                 U032
         PARMTAB  JDATE,(0,P2@JDATE)                               U032
         PARMTAB  GDATE,(0,P2@GDATE)                               U032
         PARMTAB  PERCENT,(0,P2@PCT)                               U046
         PARMTAB  PCT,(0,P2@PCT)                                   U046
         PARMTAB  FEET,(0,P2@FEET)                                 U046
PARMLAST PARMTAB  NL,(P1@NL,0)                                     U032
         SPACE 2
DAYS     DAYS  LEFT
MONTHS   MONTHS
         SPACE 2
BIGSTUFF CSECT ,                                                   U043
         SPACE 2
TAPEDCB  DCB   DSORG=PS,MACRF=E,DDNAME=SYSUT1,DEVD=TA,EXLST=EXLST,     $
               IOBAD=TAPEIOB
         SPACE 2
SYSIN    DCB  DSORG=PS,MACRF=GM,DDNAME=SYSIN,LRECL=80,EODAD=EOD,BUFNO=1
         SPACE 2
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=133,            $
               RECFM=FBA,EXLST=PRTEXLST
         SPACE 2
SYSPRNT2 DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRNT2,LRECL=133,            $
               RECFM=FBA,EXLST=PRTEXLST
         SPACE 3
COLHEAD1 DC    C' Unloaded  File                         Creation   Exp$
               iration              Block  Blocks            Length  To$
               tal       Creator      '                            U048
         SPACE 1
COLHEAD2 DC    C' by / for  seq#   Dataset name      PW    date        $
               date    Recfm Lrecl   size   used   Den   Trt (feet)  le$
               ngth  Job name/Stepname'                            U048
         SPACE 2
         AIF   (&MVS).MVS04                                        U020
         EJECT
ERR#LIST DC    0F'0'                                               U015
*                                                                  U015
*  ERROR MSGS FOR EACH SENSE BIT FOR 3420 TAPE DRIVES              U015
*                                                                  U015
         SPACE 1
*** BYTE 0                       BIT                               U015
         DC    C'CMD REJ   '      0     80                         U015
         DC    C'INT REQ   '      1     40                         U015
         DC    C'BUS OUT CK'      2     20                         U015
         DC    C'EQC CHK   '      3     10                         U015
         DC    C'DATA CHK  '      4     08                         U015
         DC    C'OVERRUN   '      5     04                         U015
         DC    C'WORD CNT 0'      6     02                         U015
         DC    C'DATA CNVTR'      7     01                         U015
         SPACE 1
*** BYTE 1                       BIT                               U015
         DC    C'NOISE     '      0     80                         U015
         DC    C'SEL+RDY+NB'      1     40                         U015
         DC    C'NOT READY '      2     20                         U015
         DC    C'7 TRK FEAT'      3     10                         U015
         DC    C'@ LOAD PT '      4     08                         U015
         DC    C'WRITE STAT'      5     04                         U015
         DC    C'FILE PROT '      6     02                         U015
         DC    C'NOT CAPABL'      7     01                         U015
         SPACE 1
*** BYTE 2                       BIT                               U015
         DC    C'TIE BIT 0 '      0     80                         U015
         DC    C'TIE BIT 1 '      1     40                         U015
         DC    C'TIE BIT 2 '      2     20                         U015
         DC    C'TIE BIT 3 '      3     10                         U015
         DC    C'TIE BIT 4 '      4     08                         U015
         DC    C'TIE BIT 5 '      5     04                         U015
         DC    C'TIE BIT 6 '      6     02                         U015
         DC    C'TIE BIT 7 '      7     01                         U015
         SPACE 1
*** BYTE 3                       BIT                               U015
         DC    C'R/W VRC   '      0     80                         U015
         DC    C'MT/LRC    '      1     40                         U015
         DC    C'SKEW      '      2     20                         U015
         DC    C'END DC/CRC'      3     10                         U015
         DC    C'ENV/ECC   '      4     08                         U015
         DC    C'1600 BPI  '      5     04                         U015
         DC    C'BACKWARD  '      6     02                         U015
         DC    C'C/P COMPAR'      7     01                         U015
         SPACE 1
*** BYTE 4                       BIT                               U015
         DC    C'MP H E    '      0     80                         U015
         DC    C'DROP READY'      1     40                         U015
         DC    C'TAPE INDIC'      2     20                         U015
         DC    C'W T VRC   '      3     10                         U015
         DC    C'MICROPGM  '      4     08                         U015
         DC    C'LWR       '      5     04                         U015
         DC    C'UNIT CHECK'      6     02                         U015
         DC    C'RSRVD RPQ '      7     01                         U015
         SPACE 1
*** BYTE 5                       BIT                               U015
         DC    C'NEW SUBSYS'      0     80                         U015
         DC    C'NEW SUBSYS'      1     40                         U015
         DC    C'WTM CHECK '      2     20                         U015
         DC    C'ID BURST  '      3     10                         U015
         DC    C'START READ'      4     08                         U015
         DC    C'PART''L REC'     5     04                         U015
         DC    C'POSTAMBLE '      6     02                         U015
         DC    C'RSRVD RPQ '      7     01                         U015
         SPACE 1
*** BYTE 6                       BIT                               U015
         DC    C'7 TRK UNIT'      0     80                         U015
         DC    C'WRT CURR  '      1     40                         U015
         DC    C'DUAL DEN  '      2     20                         U015
         DC    C'NOT 1600  '      3     10                         U015
         DC    C'MOD 4,6,8 '      4     08                         U015
         DC    C'MD 5,6,7,8'      5     04                         U015
         DC    C'MOD 3,4   '      6     02                         U015
         DC    C'MD 3,4,7,8'      7     01                         U015
         SPACE 1
*** BYTE 7                       BIT                               U015
         DC    C'LAMP FAIL '      0     80                         U015
         DC    C'BOTM LEFT '      1     40                         U015
         DC    C'BOTM RIGHT'      2     20                         U015
         DC    C'RESET KEY '      3     10                         U015
         DC    C'DSE       '      4     08                         U015
         DC    C'ERASE HEAD'      5     04                         U015
         DC    C'AIR BEARNG'      6     02                         U015
         DC    C'LOAD FAIL '      7     01                         U015
         SPACE 1
*** BYTE 8                       BIT                               U015
         DC    C'IBG       '      0     80                         U015
         DC    C'SPARE ?   '      1     40                         U015
         DC    C'SPARE ?   '      2     20                         U015
         DC    C'EARLY READ'      3     10                         U015
         DC    C'CTL BURST '      4     08                         U015
         DC    C'SLOW RD B '      5     04                         U015
         DC    C'SLOW RD E '      6     02                         U015
         DC    C'VELOCITY  '      7     01                         U015
         SPACE 1
*** BYTE 9                       BIT                               U015
         DC    C'6250 CORR '      0     80                         U015
         DC    C'WRT VEL CH'      1     40                         U015
         DC    C'CHAN BUFF '      2     20                         U015
         DC    C'CRC III   '      3     10                         U015
         DC    C'6250 FEAT '      4     08                         U015
         DC    C'SPARE ?   '      5     04                         U015
         DC    C'SPARE ?   '      6     02                         U015
         DC    C'TCU RSRVD '      7     01                         U015
.MVS04   ANOP                                                      U020
         SPACE 2
*        DCBD  DEVD=TA,DSORG=PS                                    U025
IHADCB   DSECT ,                                                   U025
         ORG   IHADCB+17                                           U025
DCBDEVT  DS    B                                                   U037
DCBDVMT4 EQU   X'80'                    3480                       U037
         ORG   IHADCB+40                                           U025
DCBDDNAM DS    CL8                      DDNAME                     U025
         ORG   IHADCB+42                                           U025
DCBMACRF DS    2X                       MACRO FORMAT (AFTER OPEN)  U025
DCBMRABC EQU   X'04'                    BLOCK COUNT NOT MAINTAINED U025
DCBDEBAD DS    A                        -> DEB                     U025
DCBOFLGS DS    X                        OPEN FLAGS                 U025
DCBOFOPN EQU   X'10'                    OPEN BIT                   U025
         SPACE 2
         END   TAPEMAP
