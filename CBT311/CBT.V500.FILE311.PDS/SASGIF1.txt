This sample job:
- Creates an output GIF library large enough for a SAS-generated GIF
  file
- Invokes SAS to create a GIF file of the state of TEXAS using a
  background of SAS's choosing

To use:
- Change "ibmuser" to your userid

// job card here
//*----->   JCLLIB ORDER=(SAS609.TS450.PROCLIB)
//*
//** Create a new library for the SAS GIF file
//*
//NEWONE   EXEC PGM=IEFBR14
//SYSUT2   DD DSN=ibmuser.SPFTEMP1.GIF,
//            DISP=(,CATLG),UNIT=SYSDA,
//            DCB=(LRECL=32756,BLKSIZE=32760,RECFM=VB),
//            SPACE=(CYL,(1,1,8))
//*
//** Invoke SAS to write out a GIF file of the Map of Texas
//*
// EXEC SAS609
//MAPS  DD DISP=SHR,DSN=SAS.MAPS
//SYSIN DD *
     /*
      | The IMGSIZE macro lets you modify the size of the image area
      |    in pixel units.
      |
      |    Notes:
      |       1. The dots per inch value (dpi) should be the same as
      |          that used by the driver (dpi=xpixels/xmax).
      |
      |       2. The maximum width and height values specified as
      |          parameters should not exceed the xmax and ymax values
      |          of the device used. The default values used below
      |          are arbitrary.
      */
     %macro IMGSIZE(w=1280, h=1024, dpi=95, rows=43, cols=83);

        %if &dpi<=0 %then
           %put DPI must be greater than zero.;
        %else %do;
           goptions hsize=%sysevalf(&w/&dpi)in vsize=%sysevalf(&h/&dpi)in
                    hpos=&cols                 vpos=&rows;
        %end;
     %mend IMGSIZE;

     filename out 'ibmuser.spftemp1.gif(texas)';
     goptions dev=gif gsfname=out gsfmode=replace;
     %imgsize(w=300, h=200, dpi=95, rows=30, cols=50);
data loc;
     input st $ 1-2 loc;
     state=stfips(st);
     cards;
TX   40
     ;
data refiv;
     set maps.states;
     if state=stfips('TX');
proc gproject data=refiv out=refiv;
     id state;
proc gmap data=loc map =refiv;
     choro loc/levels=4;
     id state;
     pattern1 v=mempty c=green;
     title .h=2 .c=blue .f=complex T E X A S;
     footnote .h=2 .c=red .f=simplex The Lone Star State;
/*
//*
//*
//* At this point 'ibmuser.spftemp1.gif(texas)' has a GIF file
//* generated by SAS that is ready for download to a PC.
