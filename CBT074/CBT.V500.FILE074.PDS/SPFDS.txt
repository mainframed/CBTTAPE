         MACRO
&NAME    SPFDS  &PRJ,&LIB,&TYP,&MBR,&ODS,&TDSN,&TMBR,&NOVER
.*---------------------------------------------------------------------
.*       THIS CODE WILL BUILD A VALID DATA SET NAME FROM EITHER THE
.*       PROJECT,LIBRARY,TYPE FIELDS OR THE OTHER DATA SET NAME FIELD.
.*       IF BOTH SETS OF FIELDS ARE PROVIDED TO THIS MACRO, IT WILL
.*       SELECT THE APPROPRIATE DATA SET NAME TO USE IN THIS MANNER:
.*       1 - IF 'OTHER DATA SET NAME' FIELD IS NON-BLANK AND THE FIRST
.*           CHARACTER IS NOT A '¬', THIS FIELD WILL BE USED.
.*       2 - IF 'OTHER DATA SET NAME' FIELD IS BLANK OR THE FIRST
.*           CHARACTER IS A '¬', THEN PROJECT, LIBRARY, TYPE WILL BE
.*           USED.
.*       3 - THE PROJECT LIBRARY TYPE FIELDS WILL BE PLACED IN THE
.*           TARGET FIELD, WITH THE DOTS BETWEEN QUALIFIERS AND PADDED
.*           WITH BLANKS.
.*       4 - THE OTHER DATA SET NAME FIELD WILL BE PLACED IN THE TARGET
.*           FIELD, IF THE NAME WAS IN QUOTES THEY WILL BE REMOVED, IF
.*           THE NAME WAS NOT IN QUOTES, THE USERS PREFIX (ZPREFIX)
.*           IS ADDED TO THE DSN.
.*       5 - THE MEMBER NAME WILL BE MOVED TO THE TARGET MEMBER FIELD
.*           FROM EITHER THE INPUT MEMBER FIELD, OR STRIPPED FROM
.*           WITHIN PARENS OF OTHER DATA SET NAME.
.*       6 - THE CATALOG WILL BE CHECKED FOR THE DATA SET
.*       7 - THE FORMAT 1 DSCB WILL BE OBTAINED FOR THE DATA SET.
.*       8 - IF THE DATA SET IS A PDS AND A MEMBER NAME WAS SPECIFIED,
.*           A BLDL WILL BE DONE FOR THE MEMBER
.*
.*       REGISTERS AT COMPLETION
.*
.*       R0  = 0  -  PROJECT LIBRARY TYPE USED
.*       R0  = 4  -  OTHER DATA SET NAME USED
.*
.*       R15 = 0  -  VALID DATA SET
.*       R15 = 4  -  MBR DOES NOT EXIST
.*       R15 = 8  -  DATA SET HAS BEEN MIGRATED
.*       R15 = 12 -  DATA SET NOT CATALOGED
.*       R15 = 16 -  DATA SET NOT ON VOLUME
.*       R15 = 20 -  ALLOCATION FAILED FOR BLDL, DATA SET IN USE
.*       R15 = 24 -  VOLUME NOT MOUNTED
.*       R15 = 28 -  ALL OTHER ERRORS
.*---------------------------------------------------------------------
&NAME    DS    0H
      MNOTE *,'THIS MACRO USES TRT, R2 IS USED BY THE TRT INSTRUCTION'
         GBLA  &Y
         LCLC  &REGD,&REGM
         LCLA  &X
&X       SETA  &SYSNDX
&Y       SETA  &SYSNDX
         AIF   (T'&PRJ NE 'O' OR T'&ODS NE 'O').AAA
         MNOTE 8,'PROJECT/LIB/TYPE AND OTHER DSN NOT SPECIFIED'
         MEXIT
.AAA     ANOP
         AIF   (T'&TDSN NE 'O').BBB
         MNOTE 8,'TARGET DATA SET FIELD NOT SPECIFIED'
         MEXIT
.BBB     ANOP
         AIF   ('&TDSN'(1,1) EQ '(').REG1
         MVC   &TDSN,BLANKS            CLEAR THE TARGET FIELD
         AGO   .REG2
.REG1    ANOP
&REGD    SETC  '&TDSN'(2,K'&TDSN-2)
         MVC   0(44,&REGD.),BLANKS     CLEAR FIELD
.REG2    ANOP
         AIF   (T'&TMBR EQ 'O').CCC
         AIF   ('&TMBR'(1,1) EQ '(').REG3
         MVC   &TMBR,BLANKS            CLEAR THE TARGET FIELD
         AGO   .CCC
.REG3    ANOP
&REGM    SETC  '&TMBR'(2,K'&TMBR-2)
         MVC   0(8,&REGM.),BLANKS      CLEAR
.CCC     ANOP
         XC    SPFDSCBX,SPFDSCBX       CLEAR DSCB FIELD
         XC    SPFDSCBY,SPFDSCBY       CLEAR DSCB FIELD
         MVC   SPFBMBR,BLANKS          CLEAR BLDL MBR FIELD
         XC    SPFBAREA,SPFBAREA       CLEAR BLDL AREA
         AIF   (T'&ODS EQ 'O').PLT
*---------------------------------------------------------------------
*       OTHER DATA SET NAME FIELD
*---------------------------------------------------------------------
         SPFDS1 &ODS,&TDSN,&TMBR
         LTR   R15,R15
         BNZ   MZZZ&X
         AIF   (T'&PRJ EQ 'O').DSCB
.PLT     ANOP
*---------------------------------------------------------------------
*       PROJECT LIBRARY TYPE MEMBER FIELDS
*---------------------------------------------------------------------
         AIF   ('&TDSN'(1,1) EQ '(').REG4
         CLI   &TDSN,C' '              DO WE HAVE A DSN ?
         AGO   .REG5
.REG4    ANOP
         CLI   0(&REGD),C' '           DO WE HAVE A DSN ?
.REG5    ANOP
         BH    MAAA&X                  YES - SKIP PRJ LIB TYPE
         SPFDS2 &PRJ,&LIB,&TYP,&MBR,&TDSN,&TMBR
         LTR   R15,R15
         BNZ   MZZZ&X
MAAA&X   EQU   *
.DSCB    ANOP
         AIF   (T'&NOVER NE 'O').NOVER
*---------------------------------------------------------------------
*       OBTAIN THE FORMAT 1 DSCB FOR THE DATA SET
*---------------------------------------------------------------------
         SPFLOC &TDSN
         LTR   R15,R15
         BNZ   MZZZ&X
         SPFOBT &TDSN
         LTR   R15,R15
         BNZ   MZZZ&X
*---------------------------------------------------------------------
*       BLDL FOR THE MEMBER
*---------------------------------------------------------------------
         AIF   (T'&TMBR EQ 'O').NOVER
         TM    DS1DSORG,DCBDSGPO       PDS ?
         BNO   MBBB&X                  NO - SKIP BLDL
         SPFDS4 &TDSN,&TMBR
         LTR   R15,R15
         BNZ   MZZZ&X
MBBB&X   EQU   *
.NOVER   ANOP
         B     MYYY&X
.*---------------------------------------------------------------------
FLD&Y    DC    X'00'
*----------------------------------------------------------------------
MYYY&X   DS    0H
         LA    R15,0                   SET ZERO RETURN CODE
MZZZ&X   DS    0H
         XR    R0,R0                   CLEAR R0
         IC    R0,FLD&Y                DATA SET INDICATOR
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
