         MACRO
&NAME    SPFDS1 &ODS,&TDSN,&TMBR
&NAME    DS    0H
         GBLA  &Y
         LCLC  &REGO,&REGD,&REGM,&LA
         LCLA  &X
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
.*       OTHER DATA SET NAME FIELD
.*---------------------------------------------------------------------
         XR    R0,R0                   CLEAR
         XC    SPFTRT,SPFTRT           CLEAR TRANSLATE TABLE
         MVI   FLD&Y,X'04'             INDICATE OTHER DSN USED
&REGD    SETC  'R15'
         AIF   ('&TDSN'(1,1) EQ '(').AAA
         LA    &REGD,&TDSN             ADDRESS OF TARGET FIELD
         AGO   .BBB
.AAA     ANOP
&REGD    SETC  '&TDSN'(2,K'&TDSN-2)
.BBB     ANOP
         AIF   ('&ODS'(1,1) EQ '(').CCC
         LA    R14,&ODS                ADDRESS OF OTHER DATA SET
         AGO   .DDD
.CCC     ANOP
&REGO    SETC  '&ODS'(2,K'&ODS-2)
         LR    R14,&REGO               COPY ADDR
.DDD     ANOP
         CLI   0(R14),C'Â¬'             IGNORE OTHER DSN ?
         BE    MDDD&X                  YES - SKIP IT
         CLI   0(R14),C' '             IGNORE OTHER DSN ?
         BNH   MDDD&X                  NO - SKIP IT
         CLI   0(R14),X'7D'            NAME IN QUOTES ?
         BNE   MAAA&X                  NO - ADD PREFIX
         LA    R14,1(R14)              SKIP QUOTE
         B     MBBB&X
MAAA&X   DS    0H
         MVC   0(L'ZPREFIX,&REGD),ZPREFIX MOVE USER PREFIX
         MVI   SPFTRT+C' ',X'FF'       SET TRT KEY
         MVI   SPFTRT,X'FF'            SET TRT KEY
         TRT   0(L'ZPREFIX,&REGD),SPFTRT END OF PREFIX
         MVI   0(R1),C'.'              ADD DOT
         LR    R0,&REGD                SAVE &REGD
         LA    &REGD,1(R1)             BUMP TARGET
MBBB&X   DS    0H
         MVI   SPFTRT+C'(',X'FF'      SET TRT KEY
         MVI   SPFTRT+X'7D',X'FF'     SET TRT KEY
         AIF   ('&ODS'(1,1) EQ '(').EEE
&LA      SETC  'L''&ODS'
         TRT   0(&LA,R14),SPFTRT      FIND MEMBER/END OF DSN
         AGO   .FFF
.EEE     ANOP
         TRT   0(56,R14),SPFTRT       FIND MEMBER/END OF DSN
.FFF     ANOP
         SR    R1,R14                 LENGTH OF DSN
         BCTR  R1,R0                  DECR FOR 'EX'
         EX    R1,MVCD&X              MOVE DSN TO TARGET FIELD
         LTR   R0,R0                  DID WE SAVE &REGD ?
         BZ    MCCC&X                 NO - OK
         LR    &REGD,R0               YES - RESTORE
MCCC&X   EQU   *
&REGM    SETC  'R15'
         AIF   (T'&TMBR EQ 'O').ZZZ
         AIF   ('&TMBR'(1,1) EQ '(').GGG
         LA    &REGM,&TMBR            ADDRESS OF TARGET FIELD
         AGO   .HHH
.GGG     ANOP
&REGM    SETC  '&TMBR'(2,K'&TMBR-2)
.HHH     ANOP
         LA    R14,1(R1,R14)          BUMP TO DELIMITER
         CLI   0(R14),C'('            WAS A MEMBER FOUND ?
         BNE   MDDD&X                 NO - DONE
         LA    R14,1(R14)             FROM FIELD
         MVI   SPFTRT+C'(',X'00'      RESET TRT KEY
         MVI   SPFTRT+C')',X'FF'      SET TRT KEY
         TRT   0(10,R14),SPFTRT       FIND END OF MEMBER
         SR    R1,R14                 LENGTH OF MBRDSN
         BCTR  R1,R0                  DECR FOR 'EX'
         EX    R1,MVCM&X              MOVE MBR TO TARGET FIELD
.ZZZ     ANOP
         B     MDDD&X
MVCD&X   MVC   0(0,&REGD.),0(R14)
MVCM&X   MVC   0(0,&REGM.),0(R14)
MDDD&X   DS    0H
         LA    R15,0
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
