         TITLE '   T S S O   '
*$DOC@*****************************************************************
*                                                                     *
*        'TSSO' - TSO SUBSYSTEM FOR OPERATOR CONSOLES                 *
*                                                                     *
***********************************************************************
         SPACE
*  WRITTEN BY. BILL GODFREY, PLANNING RESEARCH CORPORATION, MCLEAN VA.
*  INSTALLATION. PRC, MCLEAN VA.
*  DATE WRITTEN. SEPTEMBER 1 1976.
*  DATE UPDATED. JUNE 11 1982.
*  ATTRIBUTES. NOT RE-ENTRANT.
*   MUST BE LINK-EDITED WITH AC=1 IN AUTHORIZED LIBRARY.
*   MUST BE EXECUTED AS A SUBSYSTEM. THAT IS, IT MUST RUN FROM AN
*   OPERATOR 'START' COMMAND AND THE NAME OF THE PROC MUST BE IN
*   THE SUBSYSTEM NAME TABLE (CSECT IEFJESNM IN MEMBER IEEVIPL).
*  DESCRIPTION.
*   THIS IS AN EXERCISE IN WRITING A SUBSYSTEM.
*
*   THIS SUBSYSTEM ALLOWS MOST TSO COMMANDS TO BE EXECUTED FROM
*   AN OPERATOR'S CONSOLE.
*
*  LOG OF CHANGES.
*   24JUL81 - INSTEAD OF PASSING COMMANDS TO THIS MODULE IN THE SSVT,
*             SSSM NOW PLACES THEM IN A CIRCULAR QUEUE, AND THIS
*             END OF THE SUBSYSTEM PICKS THEM OFF THE QUEUE.
*             INSTEAD OF WAITING FOR EACH COMMAND, THIS MODULE
*             ONLY WAITS IF THE QUEUE IS EMPTY.
*   24JUL81 - TSSOWTO IS LOADED DURING INITIALIZATION,
*             AND CALLED VIA 'BALR' INSTEAD OF VIA 'LINK'.
*   08DEC81 - FIX 0C1 ABEND IN ESTAE EXIT (CHANGE ESTAEW FROM
*             4 WORDS TO 5 WORDS.)
*   12FEB82 - DOCUMENTATION CHANGE ONLY.
*             COMMENTS ADDED TO DESCRIPTION OF 'EF' COMMAND
*             TO SHOW HOW TO EDIT NONUM.
*   03JUN82 - VERSION 1.4
*             ALLOW TSO COMMANDS TO BE CONTINUED IF LAST CHARACTER
*             ON A LINE IS A HYPHEN.  CONTINUATION MUST COME FROM
*             SAME CONSOLE AS PREVIOUS LINE.  IF A COMMAND IS
*             RECEIVED FROM A DIFFERENT CONSOLE WHILE THERE IS DATA
*             IN THE CONTINUATION HOLD AREA, THE HOLD AREA IS ERASED
*             AND A WARNING MESSAGE IS ISSUED TO THE CONSOLE USING
*             THE CONTINUATION HOLD AREA.
*             THERE ARE NOW 2 BASE REGISTERS (R11,R12).
*             IF TSSO ALREADY UP, ISSUE MESSAGE 'TSSO ALREADY ACTIVE'.
*   10JUN82 - VERSION 1.5
*             CHANGES TO SUPPORT THE POSSIBILITY THAT WHEN TSSO WAS
*             STARTED THE SSCTUPSS BIT IN THE TSSO SSCT WAS ON.
*             IF IT WAS, THEN TSSO CAME UP UNDER JES2 WITH ITS OWN
*             NAME INSTEAD OF SYSLOG, AND IT ALREADY HAS A JOB ID
*             SO IT DOESNT HAVE TO REQUEST ONE.  ITS SYSOUT BANNER
*             PAGES WILL SAY 'TSSO' INSTEAD OF 'SYSLOG'.
*             IF THE SSCTUPSS BIT WAS OFF, WE RUN LIKE WE USED TO.
*             NEW COMMAND &.S1 WILL SET SSCTUPSS ON.  USE IT, THEN
*             &LOGOFF, THEN START TSSO AGAIN AND SEE THE DIFFERENCE.
*             SEE NEW PROGRAM 'TSSOUPSS' FOR A NEW WAY TO START TSSO.
*             IT WILL SET THE BIT ON (OR OFF) AND ISSUE 'S TSSO'.
*             NEW FEATURE OF '.L' COMMAND ALLOWS SKIPPING LINES
*             (MUST USE NEW TSSOWTO TO SUPPORT SKIPPING).
*             DOCUMENTATION CHANGES:
*              DESCRIPTIONS OF 'STACK' AND 'EF' CHANGED (NEW SYNTAX).
*              DESCRIPTIONS OF BANNER PAGES.
*             NOTE FOR POSSIBLE FUTURE CHANGE: OUR FAKE SSIB IS IN
*             PAGEABLE CSA (SUBPOOL 241) AND IT SHOULD BE IN FIXED
*             LSQA (SUBPOOL 255).  IT'S ONLY USED IF SSCTUPSS WAS
*             OFF, AND NEVER CAUSED A PROBLEM, BUT SHOULD BE CHANGED.
*   11JUN82 - ALLOW .L TO SPECIFY NUMBER OF LINES (.L/NN OR .L+NN/NN)
*             .L COMMAND MAY BE ENTERED WITHOUT ITS LEADING PERIOD.
*             ALLOW SECONDARY JES TO BE SPECIFIED IN PARM.
*
***********************************************************************
         EJECT
***********************************************************************
*
*            OUTPUT FROM COMMANDS THAT USE 'PUTLINE'
*            WILL BE SENT TO THE CONSOLE FROM WHICH THE
*            COMMAND WAS ENTERED.  OUTPUT FROM COMMANDS
*            THAT USE 'TPUT' OR ACCESS METHODS WILL NOT
*            APPEAR ON THE CONSOLE.
*
*            A MAXIMUM OF 30 LINES OF OUTPUT WILL BE SENT TO THE
*            CONSOLE, TO PREVENT WTO BUFFERS FROM FILLING UP.
*            SEE THE .L COMMAND DESCRIBED LATER FOR A WAY TO
*            HANDLE MORE THAN 30 LINES.
*
*            TO ENTER A TSO COMMAND ON A CONSOLE, TYPE IN
*            AN AMPERSAND FOLLOWED BY THE COMMAND. FOR EXAMPLE:
*               &LISTDS 'SYS1.BRODCAST'
*
*            TO STOP THE SUBSYSTEM, TYPE IN
*               &LOGOFF
*            OR
*               &P
*
*            IF YOUR SYSTEM ALREADY USES THE AMPERSAND FOR
*            ANOTHER PURPOSE, THERE IS A PARM KEYWORD THAT
*            WILL MAKE TSSO LOOK FOR ANOTHER CHARACTER.
*
*            SINCE THIS IS A SUBSYSTEM, THE NAME OF THE
*            PROC YOU USE TO START IT MUST BE IN THE SYSTEM
*            SUBSYSTEM NAME TABLE.
*
***********************************************************************
         EJECT
***********************************************************************
*
*            UNFORTUNATELY, EACH LINE ENTERED HAS NO RELATION
*            TO THE LAST COMMAND ENTERED, SO IF YOU TYPE IN
*            THE 'EDIT' COMMAND, THE NEXT COMMAND YOU TYPE
*            WILL NOT BE IN 'EDIT' MODE, AND IF YOU TYPE IN
*            THE 'PROFILE' COMMAND, THE NEW PROFILE WILL NOT BE
*            PRESENT FOR THE NEXT COMMAND.
*
*            BUT IF YOU TYPE IN AN 'ALLOC' COMMAND, THE ALLOCATION
*            WILL STILL BE THERE FOR SUBSEQUENT COMMANDS.
*
*            WE USE A COMMAND CALLED 'EF' TO EDIT DATA SETS
*            FROM A CONSOLE.  ITS SYNTAX IS
*               EF SUBCOMMAND;SUBCOMMAND;SUBCOMMAND...
*            IT USES WHATEVER DATA SET IS CURRENTLY PRE-ALLOCATED
*            TO THE FILENAME 'EDITFILE' AND BUILDS AN EDIT COMMAND
*            AND STACKS THE EDIT COMMAND PLUS THE SPECIFIED
*            SUBCOMMANDS.  FOR EXAMPLE,
*               &ALLOC FI(EDITFILE) DA('SYS1.PARMLIB(LNKLST00)') SHR
*               &EF V;C /SYS3/SYS4/ ALL;C /SYS2/SYS3/ ALL;SAVE
*               &EF V;C /TESTLIB/USERLIB/ ALL;SAVE
*            TO EDIT A DATA SET WITH NO SEQUENCE NUMBERS,
*            USE THE 'EFN' COMMAND INSTEAD OF 'EF'.
*
*            WE USE A COMMAND CALLED 'STACK' TO ENTER MULTIPLE
*            COMMANDS ON ONE LINE. THE SYNTAX IS:
*               STACK COMMAND;COMMAND;COMMAND....
*
*            IN BOTH 'EF' AND 'STACK', IF YOU WANT THE SEMICOLON TO
*            BE LEFT IN THE COMMAND AS TEXT, SPECIFY A PAIR OF SEMI-
*            COLONS.  THEY WILL BE CONVERTED TO A SINGLE SEMICOLON
*            AND NOT TREATED AS A SEPARATOR.
*
*            ON SYSTEMS THAT HAVE THE 'PCF' PRODUCT, 'STACK' AND
*            'EF' MAY NOT WORK THE SAME UNDER A REAL TSO SESSION
*            AS THEY DO UNDER TSSO, BECAUSE PCF HANDLES SEMICOLONS
*            BEFORE 'STACK' AND 'EF' EVER GET CONTROL.
*
***********************************************************************
         EJECT
***********************************************************************
*
*            SINCE THIS IS A SUBSYSTEM AND NOT JUST A STARTED TASK,
*            IT WILL RUN FINE EVEN IF JES2 IS NOT UP, ALTHOUGH
*            COMMANDS THAT USE SYSOUT WILL NOT WORK.  TSSO IS A
*            HANDY TOOL FOR FIXING JES2 PROBLEMS WHEN JES2 IS DOWN,
*            OR FIXING TCAM/TSO PROBLEMS WHEN TCAM/TSO IS DOWN.
*            SOMETIMES JUST A 'RENAME' OR 'COPY' WILL FIX A PROBLEM,
*            AND THATS EASILY DONE FROM TSSO.
*
*            MODULE 'IKJEFT01' (TERMINAL MONITOR PROGRAM)
*            IS ATTACHED FOR EACH TSO COMMAND ENTERED.
*            DDNAMES REQUIRED: SYSTSIN (DUMMY) AND
*            SYSTSPRT (TEMPORARY SYSDA SPACE).
*
*            MODULE 'TSSOWTO' (COPY SYSTSPRT TO CONSOLE)
*            IS CALLED AFTER EACH TSO COMMAND IS PROCESSED.
*
*            MODULE 'TSSOSSSM' IS LOADED AND COPIED INTO CSA.
*            IT WAS ORIGINALLY REQUIRED TO BE IN LPA, BUT
*            THAT MADE MODIFICATIONS AND TEST RUNS FEW AND FAR
*            BETWEEN, SO THE CSA ROUTE WAS CHOSEN. TO RE-INSTATE
*            THE LPA REQUIREMENT (AND BYPASS THE CSA GETMAIN, ETC)
*            JUST CHANGE THE VALUE OF 'LPAOPT' FROM 'C' TO 'L'.
*
*            THE PARM FIELD (OR IF NO PARM, THE REPLY TO WTOR)
*            MAY CONTAIN THE FOLLOWING KEYWORDS:
*               U           -  NO ACTION, END OF PARMS
*               TMP(NAME)   -  THE NAME OF THE TSO TMP TO BE INVOKED.
*                              THE DEFAULT IS IKJEFT01.
*               CC(XX)      -  THE COMMAND CHARACTER NAME.
*                              THE DEFAULT IS CC(AM), AMPERSAND.
*                              SEE CCTAB FOR VALID NAMES.
*               JES         -  TSSO IS TO REQUEST A STARTED TASK
*                              ID FROM JES2 SO IT CAN USE SYSOUT.
*               NOJES       -  TSSO IS NOT TO REQUEST AN ID FROM JES2.
*                              USE THIS IF JES2 IS NOT UP.
*            IF MORE THAN 1 KEYWORD IS SPECIFIED, THEY MUST
*            BE SEPARATED BY A COMMA.
*            NULL STRINGS SUCH AS TMP() AND CC() ARE VALID AND
*            CAUSE THE KEYWORD TO BE IGNORED. THUS YOUR PROC
*            CAN HAVE PARM='TMP(&TMP.),CC(&CC.),&JES.JES&J.'
*
***********************************************************************
         EJECT
***********************************************************************
*
*            IN ADDITION TO TSO COMMANDS, THE TSSO SUBSYSTEM HAS SOME
*            COMMANDS OF ITS OWN.
*            THE 'L' COMMAND IS USED TO REPEAT THE OUTPUT FROM THE LAST
*            TSO COMMAND ENTERED.  THIS OUTPUT HAS BEEN STORED IN THE
*            SYSTSPRT FILE.
*            FOR EXAMPLE,
*               &L
*            WILL REPEAT THE OUTPUT (UP TO 30 LINES).
*               &L+20
*            WILL REPEAT THE OUTPUT BUT SKIP THE FIRST 20 LINES
*            AND DISPLAY UP TO 30 MORE LINES.
*               &L+20/10
*            WILL REPEAT THE OUTPUT, SKIPPING THE FIRST 20 LINES
*            AND DISPLAYING THE NEXT 10.
*               &L=4
*            WILL REPEAT THE OUTPUT, THIS TIME ON CONSOLE 4.
*               &L+20,L=4
*            WILL REPEAT THE OUTPUT, ON CONSOLE 4, SKIPPING
*            THE FIRST 20 LINES.
*               &L SYS1.PROCLIB(TSSO)
*            WILL INVOKE THE 'L' TSO COMMAND BECAUSE TSSO CAN
*            TELL BY THE PRESENCE OF A BLANK AND AN OPERAND
*            THAT THIS MUST BE THE TSO COMMAND.
*
***********************************************************************
*
*            NOTES ABOUT SYSOUT
*
*            THE SSCTUPSS BIT IS A BIT IN EACH SUBSYSTEM'S SSCT THAT
*            TELLS STARTED TASK CONTROL WHETHER THE SUBSYSTEM IS
*            DEPENDENT ON THE PRIMARY SUBSYSTEM (JES2 OR JES3) FOR
*            THINGS LIKE SYSOUT.  THE BIT IS NORMALLY OFF, AND TO USE
*            IT YOU MUST HAVE A PROGRAM THAT WILL SET IT ON BEFORE
*            THE START COMMAND IS ISSUED FOR THE SUBSYSTEM.
*
*            IF THE SSCTUPSS BIT WAS OFF WHEN TSSO STARTED
*            AND YOU DID NOT SPECIFY JES=NO IN THE START COMMAND,
*            SYSOUT DATA SETS WILL BE PRINTED UNDER THE BANNER
*            'SYSLOG'.  THAT IS CONTROLLED BY JES2 AND CANNOT BE
*            CHANGED BY TSSO.
*
*            IF THE SSCTUPSS BIT WAS OFF WHEN TSSO STARTED AND
*            YOU SPECIFIED JES=NO IN THE START COMMAND, SYSOUT
*            CANNOT BE USED (YOU WILL ABEND IF YOU TRY).
*
*            IF THE SSCTUPSS BIT WAS ON WHEN TSSO STARTED, SYSOUT
*            WILL BE PRINTED UNDER THE BANNER 'TSSO' (THE PROC NAME).
*
***********************************************************************
         EJECT
***********************************************************************
*
*            TO PREVENT 522 ABENDS (WAIT TIME EXCEEDED)
*            A SUBTASK IS ATTACHED WHICH JUST WAITS FOR
*            A TIMER INTERRUPT OVER AND OVER AGAIN.
*
*            SUGGESTED JCL PROC FOR TSSO
*
*               //TSSO PROC TMP=,CC=,JES=,J=
*               //TSSO EXEC PGM=TSSO,REGION=384K,TIME=10,DYNAMNBR=20,
*               //          PARM='TMP(&TMP),CC(&CC),&JES.JES&J.'
*               //SYSTSIN  DD  DUMMY,DCB=(RECFM=FB,LRECL=80,BLKSIZE=80)
*               //SYSTSPRT DD  UNIT=SYSDA,SPACE=(TRK,10),
*               //             DCB=(RECFM=VBA,LRECL=137,BLKSIZE=4000)
*               //SYSPROC  DD  DSN=SYS1.TSSOPROC,DISP=SHR  (OPTIONAL)
*
*            TO MAKE TSSO USE A SECONDARY JES (JESX FOR EXAMPLE),
*            SET THE SSCTUPSS BIT OFF, THEN
*               S TSSO,J=X
*            NOW YOU CAN &SUBMIT A JOB TO SECONDARY JES.
*
*$DOC$*****************************************************************
         EJECT
         GBLC  &MID,&CHAR,&CHAT
         SPACE
&CHAR    SETC  '&&'
&CHAT    SETC  '&&'
&MID     SETC  '&&'
         SPACE
TSSO     START
         USING *,R11,R12
         B     @PROLOG-*(,R15)
         DC    AL1(39),CL10'TSSO 1.5  '
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL13'PRC MCLEAN VA'
SUBPOOL  DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(13)
         LR    R11,R15
         LA    R15,1
         LA    R12,4095(R15,R11)
         LR    R2,R1
         SPACE
         L     R0,SUBPOOL
         GETMAIN R,LV=(0)
         SPACE
         LR    R9,R1
         XC    000(256,R9),000(R9)
         XC    256(256,R9),256(R9)
         XC    512(@DATAX-512,R9),512(R9)
         SPACE
         ST    R13,4(,R9)
         ST    R9,8(,R13)
         LR    R13,R9              NEW SAVEAREA POINTER
         USING @DATA,R9
         SPACE
         MODESET KEY=ZERO
         EJECT
************************************************************
*                                                          *
*        SET DEFAULTS                                      *
*                                                          *
************************************************************
         SPACE
         MVI   CHARW,C'&CHAR.'
         MVI   CHATW,C'&CHAT.'
         MVI   OPTION,1            JES
         SLR   R15,R15
         ST    R15,TIMECTR
         XC    HSSNM,HSSNM
         MVC   IKJEFT01,=C'IKJEFT01'
         MVC   MAXWTOS,MAXWTOV     RESET MAXWTOS
         EJECT
************************************************************
*                                                          *
*        COPY PARM                                         *
*                                                          *
************************************************************
         SPACE
         MVI   PARMAREA,X'FF'      FILL CHARACTER
         MVC   PARMAREA+1(99),PARMAREA  PROPOGATE FF
         L     R2,0(,R2)           SAVE PARM POINTER
         LH    R3,0(,R2)           GET PARM LENGTH
         LTR   R3,R3               TEST FOR PARM ABSENT
         BZ    PARMWTOR            NO PARM - BYPASS MOVE
         LR    R1,R3
         BCTR  R1,0
         B     *+10
         MVC   PARMAREA(0),2(R2)
         EX    R1,*-6
         B     PARMGOT
         SPACE
************************************************************
*                                                          *
*        GET OPTIONS FROM PARM FIELD OR OPERATOR           *
*                                                          *
************************************************************
         SPACE
PARMWTOR EQU   *
         LA    R6,PARMAREA
         LA    R7,PARMECB
         LA    R1,WTOW
         MVC   0(WTOR1L,R1),WTOR1
         WTOR  ,(R6),L'PARMAREA,(R7),MF=(E,(1))
         SPACE
PARMWAIT EQU   *
         WAIT  ECB=PARMECB
PARMGOT  EQU   *
         EJECT
************************************************************
*                                                          *
*              CHECK FOR VALID OPTIONS                     *
*                                                          *
************************************************************
         SPACE
         TR    PARMAREA,CAPS       UPPER CASE
         LA    R1,PARMAREA
PARMNEXT CLC   0(2,R1),=C'&CHAR.P'
         BE    PARMRET             RETURN IMMEDIATELY
         CLI   0(R1),C'U'          STOP SCAN
         BE    PARMDONE            END OF PARM SCAN
         CLC   0(4,R1),=C'TMP('    ALTERNATE TERMINAL MONITOR PGM
         BE    PARMTMP
         CLC   0(3,R1),=C'CC('     ALTERNATE COMMAND CHARACTER
         BE    PARMCC
         CLC   0(3,R1),=C'JES'     REQUEST STC ID FROM JES
         BE    PARMJES
         CLC   0(5,R1),=C'NOJES'   RUN WITHOUT JES
         BE    PARMJESN
         B     PARMERR
PARMTMP  LA    R1,4(,R1)           POINT PAST PAREN
         LA    R15,DOUBLE          POINT TO 8 BYTE WORKAREA
         MVC   DOUBLE,=CL8' '      BLANK THE WORKAREA
         LA    R0,8                MAX 8 CHARS TO COPY
PARMTMPL CLI   0(R1),C')'          END OF NAME
         BE    PARMTMPX            BRANCH IF END OF NAME
         CLI   0(R1),X'FF'         END OF INPUT
         BE    PARMERR             YES, MISSING CLOSE PARENS
         CLI   0(R1),C' '          BLANK PRESENT
         BE    PARMERR             YES, MISSING CLOSE PARENS
         CLI   0(R1),C','          COMMA PRESENT
         BE    PARMERR             YES, MISSING CLOSE PARENS
         MVC   0(1,R15),0(R1)      COPY 1 CHARACTER OF NAME
         LA    R1,1(,R1)           POINT TO NEXT CHARACTER
         LA    R15,1(,R15)         POINT TO NEXT OUTPUT
         BCT   R0,PARMTMPL         LOOP IF LESS THAN 8 CHARS
         CLI   0(R1),C')'          IS 8TH CHAR FOLLOWED BY CLOSE PAREN
         BNE   PARMERR             ERROR IF NOT
PARMTMPX CLI   DOUBLE,C' '         WAS THERE ANYTHING IN PARENS
         BE    *+10                IF NOT, LEAVE NAME UNCHANGED
         MVC   IKJEFT01,DOUBLE     COPY TMP NAME FROM WORKAREA
         LA    R1,1(,R1)           POINT PAST CLOSE PAREN
         B     PARMMORE            GO CHECK FOR MORE KEYWORDS
PARMCC   CLI   3(R1),C')'          IS IT CC(), NULL STRING
         BNE   PARMCCP             NO, BRANCH
         LA    R1,4(,R1)           YES, POINT PAST CLOSE PAREN
         B     PARMMORE
PARMCCP  CLI   5(R1),C')'          IS IT 2 CHARS IN PARENS
         BNE   PARMERR             NO, ERROR
         LA    R15,CCTAB           POINT TO TABLE OF CC NAMES
PARMCCL  CLI   0(R15),X'FF'        END OF TABLE
         BE    PARMERR             YES, NAME NOT FOUND
         CLC   3(2,R1),0(R15)      DOES CC(XX) MATCH NAME IN TABLE
         BE    PARMCCF             YES, FOUND IT
         LA    R15,3(,R15)         POINT TO NEXT ENTRY IN TABLE
         B     PARMCCL             LOOP TO COMPARE NEXT ENTRY
PARMCCF  MVC   CHARW,2(R15)        SET NEW CHARACTER
         MVC   CHATW,2(R15)        SET NEW CHARACTER
         LA    R1,6(,R1)           POINT PAST CLOSE PAREN
         B     PARMMORE            GO CHECK FOR MORE PARMS
PARMJES  OI    OPTION,1            JES
         LA    R1,3(,R1)           POINT PAST KEYWORD
         CLI   0(R1),C'A'          IS IT JESA THRU Z OR 0 THRU 9
         BL    PARMMORE            NO, BRANCH
         CLI   0(R1),X'FF'         IS IT JUST JES
         BE    PARMDONE            YES, BRANCH
         MVC   ALTJES,0(R1)        YES, SAVE SECONDARY JES NAME
         LA    R1,1(,R1)
         B     PARMMORE            GO CHECK FOR MORE PARMS
PARMJESN NI    OPTION,255-1        NOJES
         LA    R1,5(,R1)           POINT PAST KEYWORD
PARMMORE CLI   0(R1),C','          IS THERE MORE
         BE    PARMCOMA            YES, BRANCH
         CLI   0(R1),X'FF'         IS THIS END OF INPUT
         BE    PARMDONE            PARM SCAN COMPLETE IF 'FF'
         CLI   0(R1),X'40'         IS THIS END OF INPUT
         BE    PARMDONE            PARM SCAN COMPLETE IF BLANK
         B     PARMERR             INVALID CHARACTER AFTER CLOSE PAREN
PARMCOMA LA    R1,1(,R1)           POINT PAST COMMA
         B     PARMNEXT            GO DO NEXT KEYWORD
         SPACE
PARMERR  MVI   PARMAREA,X'FF'
         MVC   PARMAREA+1(99),PARMAREA
         MVI   PARMECB,0
         LA    R6,PARMAREA
         LA    R7,PARMECB
         LA    R1,WTOW
         MVC   0(WTOR2L,R1),WTOR2
         WTOR  ,(R6),L'PARMAREA,(R7),MF=(E,(1))
         B     PARMWAIT
         SPACE
PARMRET  LR    R2,R13
         L     R13,4(,R13)
         MODESET KEY=NZERO
         LR    R1,R2
         L     R0,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)
         LM    14,12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE
PARMDONE DC    0H'0'
         EJECT
************************************************************
*                                                          *
*        GET THE NAME OF THIS TASK                         *
*                                                          *
************************************************************
         SPACE
         L     R2,548              PSAAOLD-PSA  CURRENT ASCB
         L     R0,60(,R2)          ASCBTSB
         LTR   R0,R0               IS THIS A TSO SESSION
         BNZ   ITSATSO             YES, ERROR
         L     R3,172(,R2)         ASCBJBNI
         LTR   R3,R3               IS THIS A JOB
         BNZ   ITSAJOB             YES, USE THIS NAME
         L     R3,176(,R2)         ASCBJBNS
ITSAJOB  CLI   4(R3),C' '          IS NAME LONGER THAN 4 CHARACTERS
         BNE   LONGNAME            YES, ILLEGAL FOR SUBSYSTEM
         MVC   JBID,0(R3)          SAVE SUBSYSTEM NAME
         SPACE
************************************************************
*                                                          *
*        FIND OUR SUBSYSTEM CVT (SSCT)                     *
*                                                          *
************************************************************
         SPACE
         L     R2,540              PSATOLD-PSA  CURRENT TCB
         ST    R2,$HTCB            SAVE TCB ADDRESS
         L     R2,180(,R2)         TCBJSCB-TCB(,R2)
         L     R2,X'15C'(,R2)      JSCBSACT-JSCB(,R2)
         L     R2,X'13C'(,R2)      JSCBSSIB-JSCB(,R2)
         ST    R2,$SSIB
         USING SSIB,R2
         MVC   MSTR,SSIBSSNM       WILL BE 'MSTR' IF NOT UNDER JES
         DROP  R2                  SSIB
         L     R3,16               CVTPTR
         USING CVT,R3
         L     R4,CVTJESCT         JES CONTROL TABLE
         USING JESCT,R4
         SPACE
         MVC   PRIMSSIB(36),MASKSSIB
         MVC   PRIMSSIB+8(4),JESPJESN  MOVE IN JES2 OR JES3
         CLI   ALTJES,0            WAS PARM=JESX SPECIFIED
         BE    *+10                NO, JUST JES
         MVC   PRIMSSIB+11(1),ALTJES  YES, CHANGE JES2 TO JESX
         LA    R4,JESSSCT-(SSCTSCTA-SSCT) POINT TO SSCT HEAD
         USING SSCT,R4
ISSCTL   ICM   R4,15,SSCTSCTA      POINT TO NEXT SSCT
         BZ    INOSSCT             IF END, EXIT
         CLC   SSCTSNAM,JBID       IS THIS OURS?
         BNE   ISSCTL              NO - LOOP
         EJECT
************************************************************
*                                                          *
*        SET ESTAE EXIT TO CLEAN UP ABENDS                 *
*                                                          *
************************************************************
         SPACE
         STM   R11,R12,ESTAER12
         SPACE
         MVC   ESTAEW(ESTAEL),ESTAEM
         LA    R6,ESTAEXIT
         SPACE
         ESTAE (R6),TERM=YES,PARAM=(R9),MF=(E,ESTAEW)
         SPACE
         LTR   R15,R15
         BZ    OKESTAE
         WTO   MF=(E,IM09)         NO ESTAE
         B     EXITD
OKESTAE  EQU   *
         EJECT
************************************************************
*                                                          *
*        OBTAIN THE SSVT (NEW OR OLD)                      *
*                                                          *
************************************************************
         SPACE
         USING SSVT,R2
         ICM   R2,15,SSCTSSVT      IS OUR SSVT IN?
         BNZ   IALREADY            BRANCH IF ALREADY IN
         SPACE
         L     R3,=A($SVTL+8)      SIZE OF SSVT
         LR    R0,R3
         O     R0,SP241            SUBPOOL 241
         LR    R5,R0
         SPACE
         GETMAIN R,LV=(0)
         SPACE
         LR    R2,R1
         SLR   R15,R15
         MVCL  R2,R14              ZERO SSVT
         SPACE
         MVC   0(4,R1),=C'SSVT'
         ST    R5,4(,R1)           STORE LENGTH & SUBPOOL
         LA    R2,8(,R1)           GET ADDRESS OF ACTUAL SSVT
         XC    $SVSTUS,$SVSTUS     ZERO STATUS BYTE
         B     IHAVSSVT            SKIP SSVT CLAIM
         SPACE
IALREADY EQU   *
         TM    $SVSTUS,$SVSTUST    TERMINATED?
         BZ    INSTRTER            NO - TSSO IS ALREADY UP
         SPACE
         MVC   WTOW(IM03L),IM03
         MVC   WTOW+4(1),$SVCHAR
         LA    R1,WTOW
         WTO   MF=(E,(1))          OLD SSVT FOUND
         ST    R2,$SSVT            YES - REMOVE OLD TSSO FROM SYSTEM
         ST    R4,$SSCT
         DC    X'0000'             FORCE S0C1 - ESTAE TO CLEAN UP
         SPACE
IHAVSSVT ST    R2,$SSVT            SAVE SSVT ADDRESS
         ST    R4,$SSCT            SAVE SSCT ADDRESS
         EJECT
************************************************************
*                                                          *
*        LOAD THE SUBSYSTEM MONITOR                        *
*                                                          *
************************************************************
         SPACE
         LA    0,=CL8'TSSOSSSM'
         SPACE
         LOAD  EPLOC=(0)
         SPACE
         ST    0,$SSSM
         SPACE
*              GETMAIN AN AREA IN CSA (SUBPOOL 241)
*              THEN COPY TSSOSSSM TO CSA. STORE CSA ADDRESS
*              IN $SSSM. THEN IT WILL NOT HAVE TO BE IN LPA.
         SPACE
         CLI   LPAOPT,C'L'         IS SSSM-IN-LPA OPTION
         BE    NOCSA               YES, SKIP CSA
         LR    R1,R0               GET ADDRESS OF LOADED SSSM
         LH    R0,38(,R1)          LENGTH OF MODULE
         LTR   R0,R0               IS IT VALID
         BNP   NOCSA               NO, BRANCH
         O     R0,SP241            SPECIFY SUBPOOL
         ST    R0,CSAFREE          SAVE FOR FREEMAIN
         GETMAIN R,LV=(0)
         ST    R1,CSAFREE+4        SAVE FOR FREEMAIN
         OI    $SVSTUS,$SVSTUSC    INDICATE CSA GOTTEN
         LR    R14,R1              MVCL TO
         LH    R15,CSAFREE+2       MVCL TO LEN
         L     R0,$SSSM            MVCL FROM
         LR    R1,R15              MVCL FROM LEN
         ST    R14,$SSSM           NEW ADDRESS
         MVCL  R14,R0              COPY SSSM TO CSA
NOCSA    EQU   *
         EJECT
************************************************************
*                                                          *
*        BUILD THE SSVT (FUNCTION MATRIX FROM SSSM)        *
*                                                          *
************************************************************
         SPACE
         L     R1,$SSSM            POINT TO SSSM
*              CAUTION. FIXED OFFSETS 40 42 44 46 48 FOLLOW.
         LH    R15,48(,R1)         GET FNUM
         STH   R15,SSVTFNUM
         LH    R15,40(,R1)         GET FCOD COUNT
         LTR   R15,R15
         BZ    NOVECTOR
         LH    R14,42(,R1)         GET OFFSET TO CODES
         LA    R14,0(R1,R14)       ADD OFFSET AND R1 INTO R14
         BCTR  R15,0
         B     *+10
         MVC   SSVTFCOD,0(R14)     MOVE FUNCTION CODES
         EX    R15,*-6
NOVECTOR EQU   *
         SPACE
         LH    R15,44(,R1)         GET FRTN COUNT
         LTR   R15,R15
         BZ    NOVECRTN
         LH    R14,46(,R1)         GET OFFSET TO POINTERS
         LA    R14,0(R1,R14)       ADD OFFSET AND R1 INTO R14
*        SLL   R15,2               MULTIPLY BY 4
*        BCTR  R15,0
*        B     *+10
*        MVC   SSVTFRTN,0(R14)     MOVE ROUTINE POINTERS
*        EX    R15,*-6
         LA    R6,SSVTFRTN
RELOCATE L     R0,0(,R14)          GET OFFSET TO ROUTINE
         ALR   R0,R1               RELOCATE IT
         ST    R0,0(,R6)           STORE ADDRESS OF ROUTINE
         LA    R6,4(,R6)
         LA    R14,4(,R14)
         BCT   R15,RELOCATE
NOVECRTN EQU   *
         SPACE
         L     R15,X'0224'         PSAAOLD-PSA
         ST    R15,$SVPASCB        ASCB ADDRESS
         SPACE
         MVC   $SVCHAR,CHARW
         MVC   $SVCHAT,CHATW
         SPACE
         MVC   $SVOKWTO(10),OKWTO
         MVC   $SVOKWTO+4(1),$SVCHAR
         TM    OPTION,1            IS JES ID TO BE REQUESTED
         BZ    *+8                 NO, SKIP NEXT INSTRUCTION
         BAL   R14,REQJOBID        REQUEST JOB ID FROM JES
         LOAD  EP=TSSOWTO          LOAD RE-ENTRANT WTO PROGRAM
         ST    R0,ENTRYWTO         SAVE ENTRY POINT
         EJECT
************************************************************
*                                                          *
*         GETMAIN A COMMAND QUEUE AREA IN CSA              *
*                                                          *
************************************************************
         SPACE
         LA    R1,10               ROOM FOR 10 COMMANDS
         ST    R1,$SVQMAXQ         SAVE MAXIMUM QUEUE SIZE
         M     R0,=A($QESIZE)      MULTIPLY BY LENGTH OF AN ENTRY
         LR    R0,R1               SET R0 FOR FREEMAIN
         O     R0,SP241            SPECIFY SUBPOOL
         ST    R0,QUEFREE          SAVE FOR FREEMAIN
         GETMAIN R,LV=(0)
         ST    R1,QUEFREE+4        SAVE FOR FREEMAIN
         OI    $SVSTUS,$SVSTUSQ    INDICATE CSA GOTTEN
         ST    R1,$SVQADDR         SAVE ADDRESS OF QUEUE FOR THIS END
         SR    R0,R0               INDICATE QUEUE IS EMPTY
         STM   R0,R1,$SVQUEUE      SAVE ADDRESS OF QUEUE FOR SSSM
         OI    $SVSTUS,$SVSTUSQ    INDICATE CSA GOTTEN
         SPACE
************************************************************
*                                                          *
*         CREATE A CIRCULAR CHAIN OF QUEUE ENTRIES         *
*                                                          *
************************************************************
         SPACE
         L     R15,$SVQMAXQ        GET MAXIMUM QUEUE SIZE
         LR    R0,R1
CIRCLE   LR    R1,R0               NEXT ENTRY NOW CURRENT
         USING $QENTRY,R1
         A     R0,=A($QESIZE)      POINT TO NEXT ENTRY
         ST    R0,$QENEXT          STORE IN CURRENT ENTRY
         BCT   R15,CIRCLE          LOOP UNTIL EACH ENTRY HAS A POINTER
         L     R0,$SVQADDR         POINT TO FIRST ENTRY
         ST    R0,$QENEXT          PUT ITS ADDRESS IN LAST ENTRY
         DROP  R1                  $QENTRY
         SPACE
************************************************************
*                                                          *
*        CONNECT THE SSVT TO THE SSCT                      *
*                                                          *
************************************************************
         SPACE
*              NOTE - MUST BE IN KEY 0
         ST    R2,SSCTSSVT         STORE SSVT ADDRESS IN SSCT
         MVC   WTOW(IM04L),IM04
         MVC   WTOW+4(1),$SVCHAR
         LA    R1,WTOW
         WTO   MF=(E,(1))          CONNECTED
         SPACE
************************************************************
*                                                          *
*        START THE TIMER TASK                              *
*                                                          *
************************************************************
         SPACE
         LA    R0,=CL8'TSSOTIME'
         LA    R1,TIMER
         SPACE
         IDENTIFY EPLOC=(0),ENTRY=(1)
         SPACE
         LA    R1,TIMEPARM
         L     R15,=A(1*60*100)      1 MINUTE
         ST    R15,INTERVAL
         LA    R15,INTERVAL        ADDRESS OF INTERVAL
         ST    R15,0(,R1)
         LA    R15,ECBTIMER
         ST    R15,4(,R1)          ADDRESS OF ECB
         ST    R9,8(,R1)           ADDRESS OF @DATA
         OI    8(R1),X'80'
         XC    ATTCECB1,ATTCECB1
         XC    ECBTIMER,ECBTIMER
         LA    R6,ATTCECB1
         SPACE
         ATTACH EP=TSSOTIME,ECB=(R6),SHSPV=1
         SPACE
         ST    R1,ATTCTCB1
         SPACE
************************************************************
*                                                          *
*         SEE IF SSSM HAS QUEUED ANY COMMANDS              *
*                                                          *
************************************************************
         SPACE
         XC    TSOPARM(2),TSOPARM  ZERO TSO PARM LENGTH
         MVC   CTUCMID,=X'77777777'
NEXTCMD  EQU   *
         L     R0,$SVQUEUE         GET NUMBER OF COMMANDS QUEUED
NEXTCMD1 LR    R1,R0               COPY TO R0
         BCTR  R1,0                DECREMENT BY 1
         CS    R0,R1,$SVQUEUE      DECREMENT $SVQUEUE
         BNZ   NEXTCMD1            TRY AGAIN IF SSSM CHANGED IT
         LTR   R1,R1               TEST NEW VALUE
         BNM   PROCESS             IF 0 OR GREATER, NO NEED TO WAIT
         SPACE
************************************************************
*                                                          *
*        WAIT FOR POST FROM SSSM                           *
*                                                          *
************************************************************
         SPACE
WAIT     EQU   *
         TM    FLAG,X'01'          MONITORING?
         BZ    NOMON1
         WTO   MF=(E,IM10)         WAITING
NOMON1   EQU   *
         SPACE
         NI    $SVFLAG,255-X'80'   ALLOW COMMANDS THRU
         SPACE
         LA    R1,$SVPOSTE
         ST    R1,ECBLIST
         LA    R1,ECBTIMER
         ST    R1,ECBLIST+4
         OI    ECBLIST+4,X'80'
         SPACE
         WAIT  ECBLIST=ECBLIST
         SPACE
         TM    ECBTIMER,X'40'      TIMER POST?
         BO    TIMEPOST            YES - BRANCH
         SPACE
         OI    $SVFLAG,X'80'       REJECT ANY OTHER COMMANDS
         SPACE
         TM    FLAG,X'01'          MONITORING?
         BZ    NOMON2
         WTO   MF=(E,IM11)         POSTED
NOMON2   EQU   *
         SPACE
         MVI   $SVPOSTE,0
         SPACE
************************************************************
*                                                          *
*         GET THE COMMAND FROM THE QUEUE                   *
*                                                          *
************************************************************
         SPACE
PROCESS  EQU   *
         L     R8,$SVQADDR
         USING $QENTRY,R8
*
*               $SVQADDR WAS INITIALLY SET TO POINT TO THE
*               FIRST QUEUE ENTRY IN THE CIRCULAR CHAIN.
*
*               EACH TIME THIS END OF THE SUBSYSTEM PROCESSES
*               A COMMAND, IT POINTS $SVQADDR TO THE NEXT ENTRY
*               AFTER THE ONE POINTED TO BY $SVQADDR.
*
*               EACH TIME THE SSSM ADDS A COMMAND TO THE QUEUE,
*               IT PUTS IT IN THE ENTRY POINTED TO BY $SVQUEUE+4
*               AND THEN POINTS $SVQUEUE+4 TO THE NEXT ENTRY.
*
*               THE RESULT IS THAT EACH END OF THE SUBSYSTEM,
*               RUNNING ASYNCHRONOUSLY, IS WALKING AROUND THE
*               CIRCULAR CHAIN AT ITS OWN SPEED.
*
         L     R0,$QENEXT
         ST    R0,$SVQADDR
         SPACE
************************************************************
*                                                          *
*         PROCESS THE COMMAND                              *
*                                                          *
************************************************************
         SPACE
         NI    FLAG,255-X'80'      DO NOT LIST THE OUTPUT
         LH    R0,TSOPARM          IF PREVIOUS COMMAND
         LTR   R0,R0                     IS
         BZ    FRESH                     CONTINUED
         CLC   $QEUCMID,CTUCMID       AND THIS IS FROM SAME SOURCE
         BE    TSOCMD                 GO TO TSOCMD
         MVC   WTOW(IM15L),IM15    NOTIFY
         MVC   WTOW+4(1),$SVCHAR
         L     R0,CTUCMID          OTHER SOURCE
         WTO   MF=(E,WTOW)         THAT CONTINUATION BUFFER IS ERASED
         XC    TSOPARM(2),TSOPARM  ZERO TSO PARM LENGTH
         MVC   CTUCMID,=X'77777777'
FRESH    LA    R1,$QECMND
         CLC   0(1,R1),$SVCHAR
         BNE   NEXTCMD              (SHOULD NEVER HAPPEN)
         CLI   1(R1),C'.'           PERIOD COMMAND
         BE    NONTSO               YES, BRANCH
         CLC   1(6,R1),=C'LOGOFF'   EXCEPTION TO TSO SYNTAX
         BE    STOP
         CLC   1(6,R1),=C'P     '   EXCEPTION TO TSO SYNTAX
         BE    STOP
         CLC   1(6,R1),=C'L     '   TREAT SINGLE L LIKE .L
         BE    REPEAT
         CLC   1(2,R1),=C'L+'       L+ IS SAME AS .L+
         BE    REPEAT
         CLC   1(2,R1),=C'L/'       L/ IS SAME AS .L/
         BE    REPEAT
         CLC   1(2,R1),=C'L='       L= IS SAME AS .L=
         BE    REPEAT
         B     TSOCMD
REPEAT   LA    R1,1(,R1)            POINT TO L
         B     LISTTSO
NONTSO   LA    R1,2(,R1)            POINT PAST TRIPLE CHAR
         CLI   0(R1),C'P'           STOP
         BE    STOP
         CLI   0(R1),C'T'           TSO, NO REPLY
         BE    TSO
         CLI   0(R1),C'L'           LIST REPLY
         BE    LISTTSO
         CLI   0(R1),C'X'           STOP THE TIMER TASK
         BE    STOPTIME
         CLI   0(R1),C'M'           MONITOR, TURN ON DEBUG BIT
         BE    MONITOR
         CLI   0(R1),C'J'           REQUEST STC ID FROM JES
         BE    STC
         CLI   0(R1),C'S'           SET SSCTUPSS ON/OFF
         BE    UPSS
INVCMD   MVC   WTOW(IM05L),IM05
         MVC   WTOW+4(1),$SVCHAR
         L     R0,$QEUCMID         CONSOLE ID
         WTO   MF=(E,WTOW)         'INVALID COMMAND'
         B     NEXTCMD
         SPACE
STC      TM    $SVSTUS,$SVSTUSI    DO WE HAVE AN ID ALREADY
         BO    NEXTCMD             YES, DO NOTHING
         BAL   R14,REQJOBID
         B     NEXTCMD
         SPACE
STOPTIME OI    FLAG,X'20'          TELL TIMER TO STOP
         B     NEXTCMD
         SPACE
MONITOR  CLI   1(R1),C'0'
         BE    MONOFF
         CLI   1(R1),C'1'
         BE    MONON
         B     INVCMD
MONON    OI    FLAG,X'01'          MONITOR ON
         B     NEXTCMD
MONOFF   NI    FLAG,255-X'01'      MONITOR OFF
         B     NEXTCMD
         SPACE
************************************************************
*                                                          *
*         .S0 AND .S1 COMMAND                              *
*                                                          *
************************************************************
         SPACE
UPSS     CLI   1(R1),C'0'
         BE    UPSS0
         CLI   1(R1),C'1'
         BE    UPSS1
         B     INVCMD
UPSS1    OI    SSCTFLG1,SSCTUPSS
         B     NEXTCMD
UPSS0    NI    SSCTFLG1,255-SSCTUPSS
         B     NEXTCMD
         SPACE
************************************************************
*                                                          *
*         .L COMMAND                                       *
*                                                          *
************************************************************
         SPACE
LISTTSO  CLI   1(R1),C' '          JUST L
         BE    LIST                YES, BRANCH
         CLI   1(R1),C'+'          L+XX
         BE    LISTSK0             BRANCH IF SKIP SPECIFIED
         CLI   1(R1),C'/'          L/XX
         BE    LISTMX0             BRANCH IF MAX SPECIFIED
         CLI   1(R1),C'='          L=XX
         BE    LISTID
         B     LISTCOMA            CHECK FOR L,L=NN SAME AS JUST L=NN
LISTSK0  CLI   2(R1),C'0'          IS IT NUMERIC
         BL    LISTERR             NO, ERROR
         LA    R15,3(,R1)          POINT PAST 1ST DIGIT
         CLI   3(R1),C'0'          SECOND DIGIT
         BL    LISTSK1             NO, PACK 1
         LA    R15,1(,R15)         POINT PAST 2ND DIGIT
         CLI   4(R1),C'0'          3RD DIGIT
         BL    LISTSK2             NO, PACK 2
         LA    R15,1(,R15)         POINT PAST 3RD DIGIT
         CLI   5(R1),C'0'          4TH DIGIT
         BL    LISTSK3             NO, PACK 3
         LA    R15,1(,R15)         POINT PAST 4TH DIGIT
         CLI   6(R1),C'0'          5TH DIGIT
         BL    LISTSK4             NO, PACK 4
         B     LISTERR             YES, BUT WE DONT ALLOW 5
LISTSK4  PACK  DOUBLE,2(4,R1)
         B     LISTSKX
LISTSK3  PACK  DOUBLE,2(3,R1)
         B     LISTSKX
LISTSK2  PACK  DOUBLE,2(2,R1)
         B     LISTSKX
LISTSK1  PACK  DOUBLE,2(1,R1)
LISTSKX  CVB   R0,DOUBLE
         AH    R0,SKIPNUM
         STH   R0,SKIPNUM
         CLI   0(R15),C' '         FOLLOWED BY A BLANK
         BE    LIST                YES, VALID
         LR    R1,R15
         BCTR  R1,0                POINT R1 TO LAST DIGIT
         CLI   1(R1),C'+'          WANT TO CALCULATE +XX+YY
         BE    LISTSK0             OK, WE CAN DO THAT
         CLI   1(R1),C'/'          /MAX
         BNE   LISTCOMA
LISTMX0  CLI   2(R1),C'0'          IS IT NUMERIC
         BL    LISTERR             NO, ERROR
         LA    R15,3(,R1)          POINT PAST 1ST DIGIT
         CLI   3(R1),C'0'          SECOND DIGIT
         BL    LISTMX1             NO, PACK 1
         LA    R15,1(,R15)         POINT PAST 2ND DIGIT
         CLI   4(R1),C'0'          3RD DIGIT
         BL    LISTMX2             NO, PACK 2
         B     LISTERR             YES, ERROR
LISTMX2  PACK  DOUBLE,2(2,R1)
         B     LISTMXX
LISTMX1  PACK  DOUBLE,2(1,R1)
LISTMXX  CVB   R0,DOUBLE
         LTR   R0,R0
         BNZ   *+8
         LA    R0,1                CANNOT ALLOW LESS THAN 1
         CH    R0,=H'50'
         BNH   *+8
         LH    R0,=H'50'           CANNOT ALLOW MORE THAN 50
         STH   R0,MAXWTOS
         CLI   0(R15),C' '         FOLLOWED BY A BLANK
         BE    LIST                YES, VALID
         LR    R1,R15
         BCTR  R1,0                POINT R1 TO LAST DIGIT
LISTCOMA CLC   1(2,R1),=C',L'      FOLLOWED BY COMMA-L
         BNE   LISTERR             NO, SYNTAX ERROR
         LA    R1,2(,R1)           POINT R1 TO THE L
         CLI   1(R1),C' '          JUST COMMA-L IS OK BUT
         BE    LIST                 COULD HAVE BEEN LEFT OFF ALTOGETHER
LISTID   CLI   1(R1),C'='
         BNE   LISTERR
         CLI   2(R1),C'0'
         BL    LIST
         CLI   3(R1),C'0'
         BL    LISTID1
         PACK  DOUBLE,2(2,R1)
         B     *+10
LISTID1  PACK  DOUBLE,2(1,R1)
         CVB   R0,DOUBLE
         LTR   R0,R0
         BZ    LIST
         B     LISTR0
LISTERR  XC    SKIPNUM,SKIPNUM
         B     INVCMD
         SPACE
************************************************************
*                                                          *
*        TIMER INTERRUPT                                   *
*                                                          *
************************************************************
         SPACE
TIMEPOST EQU   *
         MVI   ECBTIMER,0
         L     R1,TIMECTR
         CH    R1,=H'10'
         BH    WAIT
*              ISSUE A MESSAGE AT FIRST 10 TIMER INTERRUPTS
         LA    R1,1(,R1)
         ST    R1,TIMECTR
         TM    FLAG,X'01'          MONITORING
         BZ    WAIT                NO, SKIP MESSAGE
         MVC   WTOW(IM06L),IM06
         MVC   WTOW+4(1),$SVCHAR
         WTO   MF=(E,WTOW)         'TIME'
         B     WAIT
         SPACE
************************************************************
*                                                          *
*        TSO COMMAND                                       *
*                                                          *
************************************************************
         SPACE
TSOCMD   OI    FLAG,X'80'          LIST THE OUTPUT
         LA    R1,$QECMNDL+5       POINT TO COMMAND
         LH    R15,$QECMNDL        GET LENGTH
         SL    R15,=F'5'           REDUCE LENGTH BY 1 LEADING CHAR
         B     TSOCOMM
TSO      LA    R1,$QECMNDL+7       POINT TO COMMAND
         LH    R15,$QECMNDL        GET LENGTH
         SL    R15,=F'7'           REDUCE LENGTH BY 3 LEADING CHARS
TSOCOMM  LH    R14,TSOPARM         GET CURRENT LENGTH OF TSO PARM
         LA    R0,TSOPARM+2(R14)   POINT AFTER ANY EXISTING DATA
         AR    R15,R14             COMBINE LENGTHS
         LR    R14,R0              PREPARE TO MOVE
         MVC   0(128,R14),0(R1)    COPY OR APPEND DATA TO PARM
         STH   R15,TSOPARM         SET LENGTH OF PARM
         LA    R15,TSOPARM+2(R15)  POINT PAST END OF COMMAND
CONTIN1  BCTR  R15,0               BACK UP 1
         CLI   0(R15),C' '         IS THIS A TRAILING BLANK
         BE    CONTIN1             YES, IGNORE
         CLI   0(R15),C'-'         IS LAST CHAR A HYPHEN
         BNE   CONTINX             NO, EXIT
         LA    R14,TSOPARM+2       YES, RESET LENGTH
         SR    R15,R14             COMPUTE LENGTH EXCLUDING HYPHEN
         BNP   CONTINX             MIGHT HAPPEN IF ALL BLANKS
         STH   R15,TSOPARM         SET LENGTH OF PARM
         MVC   CTUCMID,$QEUCMID    SAVE SOURCE UCMID
         B     NEXTCMD             GO GET CONTINUATION
CONTINX  LA    R14,TSOPARM         PUT ADDRESS OF PARM FIELD
         LA    R1,TSOPARMP          IN A FULLWORD
         ST    R14,0(,R1)            POINTED TO BY R1
         OI    0(R1),X'80'            AND SET HIGH BIT ON
         XC    ATTCECB2(4),ATTCECB2
         LA    R6,IKJEFT01         POINT TO TMP NAME
         LA    R7,ATTCECB2         POINT TO ECB
         SPACE
         ATTACH EPLOC=(R6),ECB=(R7),SZERO=NO
         SPACE
         ST    R1,ATTCTCB2
         SPACE
         WAIT  ECB=ATTCECB2
         SPACE
         LA    R1,ATTCTCB2
         DETACH (1)
         SPACE
         XC    TSOPARM(2),TSOPARM  ZERO TSO PARM LENGTH
         MVC   CTUCMID,=X'77777777'
         SPACE
         L     R1,540              PSATOLD
         L     R1,180(,R1)         TCBJSCB
         TM    236(R1),X'01'       JSCBAUTH BIT
         BO    AUTHOK              BRANCH IF ON
         OI    236(R1),X'01'       SET IT BACK ON
AUTHOK   EQU   *
         TM    FLAG,X'80'          OUTPUT TO BE LISTED
         BO    LIST                YES, BRANCH
         B     NEXTCMD
         SPACE
************************************************************
*                                                          *
*        LIST TSO OUTPUT                                   *
*                                                          *
************************************************************
         SPACE
LIST     LH    R0,$QEUCMID+2       CONSOLE ID OF SOURCE
LISTR0   LA    R1,WTOCOPYP
         LA    R15,6               PARM LENGTH
         STH   R15,4(,R1)
         LA    R15,4(,R1)          POINT TO NEW PARM FIELD
         MVC   2(2,R15),MAXWTOS    MAX WTO'S
         STH   R0,4(,R15)          CONSOLE ID
         LH    R0,SKIPNUM
         STH   R0,6(,R15)          SKIPNUM
         ST    R15,0(,R1)          STORE PARM FIELD ADDRESS
         OI    0(R1),X'80'
*        LINK  EP=TSSOWTO
         L     R15,ENTRYWTO
         BALR  R14,R15
         MVC   MAXWTOS,MAXWTOV     RESET MAXWTOS
         XC    SKIPNUM,SKIPNUM     RESET SKIPNUM TO ZERO
         B     NEXTCMD
         EJECT
************************************************************
*                                                          *
*        REQUEST JOB ID FROM PRIMARY SUBSYSTEM             *
*                                                          *
************************************************************
         SPACE
REQJOBID CLC   MSTR,=C'MSTR'       DID WE START UNDER JES
         BNER  R14                 YES, RETURN
         LR    R6,R14
         LA    R5,$SVSSOB
         USING SSOB,R5
         MVC   SSOBID(4),=C'SSOB'
         LA    R15,SSOBHSIZ
         STH   R15,SSOBLEN
         LA    R15,SSOBRQST
         STH   R15,SSOBFUNC
         LA    R15,$SVSSIB
         MVC   0(36,R15),PRIMSSIB
         ST    R15,SSOBSSIB
         LA    R15,SSRRBGN
         ST    R15,SSOBINDV
         LA    R1,$SVSSOBA
         ST    R5,0(,R1)
         OI    0(R1),X'80'
         LA    R15,8
         STH   R15,SSRRLEN
         LA    R15,$SVRRECB
         ST    R15,SSRRSECB
         DROP  R5                  SSOB
         SPACE
         LR    R5,R1
         MODESET MODE=SUP
         LR    R1,R5
         SPACE
         IEFSSREQ
         SPACE
         LR    R5,R15
         MODESET MODE=PROB
         LR    R15,R5
         SPACE
         LTR   R15,R15
         BZ    OKREQ
         MVC   WTOW(IM12L),IM12
         MVC   WTOW+4(1),$SVCHAR
         WTO   MF=(E,WTOW)         JOBID REQUEST FAILED
         BR    R6
OKREQ    OI    $SVSTUS,$SVSTUSI    WE HAVE A JOB ID
*
*              OPENING A SYSOUT DATA SET WILL CAUSE A 913-C0 ABEND
*              UNLESS THE JSCB POINTS TO A JES2 SSIB.  WE COULD
*              MODIFY THE SSIB, OR POINT THE JSCB TO A NEW SSIB.
*              THE LATTER IS USED HERE.  THE OTHER METHOD HAS
*              NOT BEEN TESTED.
*
         SPACE
*        L     R1,$SSIB            POINT TO ORIGINAL SSIB
*        USING SSIB,R1             ADDRESS ORIGINAL SSIB
*        MVC   SSIBJBID,$SVSSIB+12 MODIFY THE ORIGINAL SSIB
*        MVC   HSSNM,SSIBSSNM      SAVE ORIGINAL SSIBSSNM
*        MVC   SSIBSSNM,PRIMSSIB+8 MODIFY THE ORIGINAL SSIB
*        DROP  R1                  SSIB
         SPACE
         LA    R1,$SVSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         OI    $SVSTUS,$SVSTUSJ    INDICATE JSCB MODIFIED
         SPACE
*        MVC   JBIDM01A(8),$SVSSIB+12
*        LA    R1,JBIDM01
*        WTO   MF=(E,(1))
         BR    R6
         EJECT
************************************************************
*                                                          *
*        RETURN JOB ID TO PRIMARY SUBSYSTEM                *
*                                                          *
************************************************************
         SPACE
*              IF THIS IS NOT DONE, JES2 ISSUES MESSAGE
*              $HASP310 SYSLOG  TERMINATED AT END OF MEMORY
         SPACE
RETJOBID TM    $SVSTUS,$SVSTUSI    DO WE HAVE A JOB ID
         BZR   R14                 NO, RETURN
         LR    R6,R14              SAVE RETURN ADDRESS
         LA    R5,$SVSSOB          SSOB
         USING SSOB,R5
         LA    R15,SSOBRTRN        RETURN JOB ID
         STH   R15,SSOBFUNC
         DROP  R5                  SSOB
         PRINT NOGEN
         SPACE
         MODESET MODE=SUP
         SPACE
         LA    R1,$SVSSOBA
         SPACE
         IEFSSREQ
         SPACE
         MODESET MODE=PROB
         SPACE
         PRINT GEN
         LR    R14,R6              RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         EJECT
************************************************************
*                                                          *
*        STOP - DISCONNECT THE SUBSYSTEM                   *
*                                                          *
************************************************************
         SPACE
STOP     EQU   *
         OI    $SVSTUS,$SVSTUST    TERMINATING
         SLR   R15,R15
         ST    R15,SSCTSSVT        DISCONNECT SSVT
         MVC   WTOW(IM14L),IM14
         MVC   WTOW+4(1),$SVCHAR
         LA    R1,WTOW
         WTO   MF=(E,(1))          DISCONNECTED
         SPACE
         BAL   R14,RETJOBID        RETURN JOB ID
         SPACE
         L     R15,HSSNM           ORIGINAL SSIBSSNM
         L     R1,$SSIB            ORIGINAL SSIB
         LTR   R15,R15             DID WE CHANGE THE NAME
         BZ    *+8                 NO, SKIP NEXT INSTR
         ST    R15,8(,R1)          YES, RESTORE ORIGINAL
         SPACE
         TM    $SVSTUS,$SVSTUSJ    HAS JSCBSSIB BEEN MODIFIED
         BZ    OKJSCB              NO, BRANCH
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         ST    R1,X'13C'(,R15)     JSCBSSIB-JSCB(,R15)
OKJSCB   EQU   *
         SPACE
         TM    $SVSTUS,$SVSTUSQ    CSA TO BE FREED (QUEUE)
         BZ    EXITQ               NO, SKIP FREEMAIN
         LM    R0,R1,QUEFREE
         FREEMAIN R,LV=(0),A=(1)
EXITQ    EQU   *
         SPACE
         TM    $SVSTUS,$SVSTUSC    CSA TO BE FREED
         BZ    EXITF               NO, BRANCH
         LM    R0,R1,CSAFREE
         FREEMAIN R,LV=(0),A=(1)
EXITF    EQU   *
         SL    R2,=F'8'            POINT TO SSVT PREFIX
         LR    R1,R2               SET R1 FOR FREEMAIN
         L     R0,4(,R1)           GET LENGTH OF SSVT
         FREEMAIN R,LV=(0),A=(1)
         SPACE
         SLR   R15,R15
         ST    R15,$SSVT
         SPACE
*        WTO   '&MID. SSVT FREED'
         B     EXITD
         SPACE
INOSSCT  MVC   WTOW(IM01L),IM01
         L     R1,540              PSATOLD
         L     R1,12(,R1)          TCBTIOT
         MVC   WTOW+IM01NAME(4),0(R1)
         LA    R1,WTOW
         B     EXITWTO
         SPACE
LONGNAME MVC   WTOW(IM17L),IM17
         LA    R1,WTOW
         B     EXITWTO
         SPACE
ITSATSO  MVC   WTOW(IM16L),IM16
         LA    R1,WTOW
         B     EXITWTO
         SPACE
INSTRTER MVC   WTOW(IM02L),IM02
         MVC   WTOW+4(1),$SVCHAR
         MVC   WTOW+6(4),JBID      PUT SUBSYSTEM NAME IN MESSAGE
         LA    R1,WTOW
         WTO   MF=(E,(1))          TSSO IS ALREADY UP
         B     EXIT0               JUST GO AWAY
EXITWTO  EQU   *
         WTO   MF=(E,(1))
EXITFM   L     R2,$SSVT
         LTR   R2,R2
         BNZ   EXITF
         SPACE
EXITD    EQU   *
         LA    R1,ATTCTCB1         TIMER TASK
         CLC   0(4,R1),=F'0'       IS THERE A SUBTASK
         BE    EXITNOD             NO, BYPASS DETACH
         DETACH (1)
EXITNOD  EQU   *
         SPACE
EXIT0    SR    15,15
         LR    R2,R13              SAVE R13 FOR FREEMAIN
         L     13,4(,13)           POINT TO PREVIOUS SAVE AREA
         ST    15,16(,13)          SAVE R15 FOR LM
         SPACE
         MODESET KEY=NZERO
         SPACE
         LR    R1,R2               AREA TO BE FREEMAINED
         L     R0,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)
         LM    14,12,12(13)
         BR    14
         EJECT
************************************************************
*                                                          *
*        ESTAE EXIT                                        *
*                                                          *
************************************************************
         SPACE
ESTAEXIT DC    0H'0'
         USING *,R15
         CH    R0,ESTAE12
         BNE   ESTASDWA
         SPACE
*              REG 0 IS 12
*              STORAGE NOT AVAILABLE FOR SDWA
*              REG  1  -  CONTAINS ABEND COMPLETION CODE
*              REG  2  -  ADDRESS OF PARAM LIST FROM ESTAE MACRO
*              REG 14  -  RETURN ADDRESS
*              REG 15  -  ENTRY ADDRESS
*              REGS 3-13  UNPREDICTABLE
         SPACE
         STM   14,12,ESTAESV+12-@DATA(R2)
         LR    R9,R2               RESTORE R9
         ST    R1,ESTABEND
         MVI   ESTAESW,C'N'
         B     ESTAEX
ESTAE12  DC    H'12'
         SPACE
ESTASDWA EQU   *
         SPACE
*              REG  1  -  ADDRESS OF SDWA
*              REG 13  -  ADDRESS OF 72 BYTE REGISTER SAVE AREA
*              REG 14  -  RETURN ADDRESS
*              REG 15  -  ENTRY ADDRESS
*              REGS 2-12  UNPREDICTABLE
         SPACE
         STM   14,12,12(R13)
         L     R9,0(,R1)           RESTORE R9
         MVC   ESTABEND(4),4(R1)   SAVE ABEND CODE FROM SDWA
         MVC   ESTAESV+12(60),12(R13)
         MVI   ESTAESW,C'S'
         ST    R1,ESTAESDW         SAVE SDWA ADDRESS
         SPACE
*              COMMON TO EITHER TYPE OF ENTRY.
*              (AS LONG AS YOU DONT REFER TO SDWA)
         SPACE
ESTAEX   EQU   *
         LM    R11,R12,ESTAER12    RESTORE BASE REGISTER
         DROP  R15                 ESTAE EXIT ENTRY
         L     R2,$SSVT
         LTR   R2,R2
         BZ    *+8
         OI    $SVSTUS,$SVSTUST    SET TERMINATION FLAG
         L     R4,$SSCT
         LTR   R4,R4
         BNZ   ESTASSCT
         WTO   MF=(E,IM21)         ESTAE SSCT NOT AVAILABLE
         B     ESTASSVT
ESTASSCT L     R2,SSCTSSVT
         LTR   R2,R2               SSVT CONNECTED?
         BNZ   ESTADISC
         WTO   MF=(E,IM22)         ESTAE SSVT WAS NOT CONNECTED
         B     ESTASSVT
ESTADISC EQU   *
         L     R15,HSSNM           ORIGINAL SSIBSSNM
         L     R1,$SSIB            ORIGINAL SSIB
         LTR   R15,R15             DID WE CHANGE THE NAME
         BZ    *+8                 IF NOT, SKIP NEXT INSTR
         ST    R15,8(,R1)          YES, PUT OLD NAME BACK
         SPACE
         TM    $SVSTUS,$SVSTUSJ    HAS JSCBSSIB BEEN MODIFIED
         BZ    ESJSCB              NO, BRANCH
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         ST    R1,X'13C'(,R15)     JSCBSSIB-JSCB(,R15)
ESJSCB   EQU   *
         SPACE
         SLR   R15,R15
         ST    R15,SSCTSSVT        DISCONNECT SSVT
         WTO   MF=(E,IM23)         ESTAE SSVT DISCONNECTED
         SPACE
         TM    $SVSTUS,$SVSTUSQ    CSA TO BE FREED (QUEUE)
         BZ    ESTACSAQ            NO, SKIP FREEMAIN
         LM    R0,R1,QUEFREE
         FREEMAIN R,LV=(0),A=(1)
ESTACSAQ EQU   *
         SPACE
         TM    $SVSTUS,$SVSTUSC    CSA TO BE FREED (SSSM)
         BZ    ESTACSAF            NO, SKIP FREEMAIN
         LM    R0,R1,CSAFREE
         FREEMAIN R,LV=(0),A=(1)
ESTACSAF EQU   *
ESTASSVT L     R2,$SSVT
         LTR   R2,R2
         BNZ   ESTAFREE
         WTO   MF=(E,IM24)         ESTAE SSVT NOT AVAILABLE
         B     ESTAEX00
ESTAFREE SL    R2,=F'8'
         LA    R15,COPYSSVT        COPY OF SSVT FOR DUMP
         MVC   0(256,R15),0(R2)
         MVC   256(256,R15),256(R2)
         MVC   512(256,R15),512(R2)
         MVC   768(256,R15),768(R2)
         LR    R1,R2
         L     R0,4(,R1)
         SPACE
         FREEMAIN R,LV=(0),A=(1)
         SPACE
         WTO   MF=(E,IM25)         ESTAE SSVT FREED
         SPACE
ESTAEX00 EQU   *
         CLI   ESTAESW,C'S'        SDWA PRESENT?
         BNE   ESTANOWA            NO - BRANCH
         L     R1,ESTAESDW         YES, RESTORE SDWA ADDRESS
         MVI   X'FC'(R1),0         SET SDWARCDE = 0
         LM    14,12,12(13)
         BR    R14
         SPACE
ESTANOWA LM    14,12,ESTAESV+12
         SLR   R15,R15             CONTINUE TERMINATION
         BR    R14
         DROP  R11,R12          MAIN BASE
         EJECT
************************************************************
*                                                          *
*        TIMER TASK                                        *
*                                                          *
************************************************************
         SPACE
         DC    0D'0'
         USING *,R12            SUBTASK BASE
TIMER    B     16(,R15)
         DC    AL1(11),CL11'TSSOTIME'
         STM   14,12,12(R13)
         LR    R12,R15
         LM    R6,R7,0(R1)         INTERVAL, ECB
         L     R9,8(,R1)           WORK AREA
*        WTO   MF=(E,IM07)
TIMELOOP EQU   *
         SPACE
         STIMER WAIT,BINTVL=(R6)
         SPACE
         POST  (R7),X'111'
         SPACE
         TM    FLAG,X'20'          TIME TO STOP?
         BZ    TIMELOOP            NO - BRANCH
         SPACE
         WTO   MF=(E,IM08)
         LM    14,12,12(R13)
         BR    R14
         EJECT
************************************************************
*                                                          *
*        CONSTANTS AND WORKAREAS                           *
*                                                          *
************************************************************
         SPACE
SP241    DC    0F'0',AL1(241,0,0,0)  SUBPOOL 241
$SVTL    EQU   1024
$SVTLEN  DC    A($SVTL+8)
MAXWTOV  DC    H'30'               MAX WTO DEFAULT
LPAOPT   DC    C'C'                LPA OR CSA OPTION
         SPACE
*              THIS IS THE TABLE OF VALID NAMES OF COMMAND
*              CHARACTERS AND THE CHARACTER EACH REPRESENTS.
CCTAB    DC    CL3'AM&&'           AM = AMPERSAND
         DC    CL3'HY-'            HY = HYPHEN
         DC    CL3'AT@'            AT = AT SIGN
         DC    CL3'PO#'            PO = POUND SIGN
         DC    CL3'CA,'            CA = COMMA
         DC    CL3'PE.'            PE = PERIOD
         DC    CL3'SL/'            SL = SLASH
*              ON OUR KEYBOARDS THE FOLLOWING CHARACTERS ARE
*              LESS CONVENIENT BECAUSE THEY NEED THE SHIFT KEY.
         DC    CL3'EQ='            EQ = EQUAL
         DC    CL3'LT<'            LT = LESS THAN
         DC    CL3'SC;'            SC = SEMICOLON
         DC    CL3'CN:'            CN = COLON
         DC    CL3'PC%'            PC = PERCENT
         DC    CL3'AP'''           AP = APOSTROPHE (QUOTE)
         DC    CL3'GT>'            GT = GREATER THAN
         DC    CL3'AS*'            AS = ASTERISK (JES3 USES THIS)
         DC    CL3'LP('            LP = LEFT PAREN
         DC    CL3'RP)'            RP = RIGHT PAREN
         DC    CL3'UB_'            UB = UNDERLINE BAR
         DC    CL3'PL+'            PL = PLUS
         DC    CL3'NT'            NT = NOT SIGN
         DC    CL3'QM?'            QM = QUESTION MARK
         DC    CL3'VB|'            VB = VERTICAL BAR
         DC    CL3'EP!'            EP = EXCLAMATION POINT
         DC    CL3'DQ"'            DQ = DOUBLE QUOTES
         DC    AL3(0)              RESERVED FOR ZAP
         DC    XL3'FFFFFF'         END OF TABLE
         SPACE
IM01     WTO   '&MID. SUBSYSTEM NAME NOT DEFINED',ROUTCDE=(2),MF=L
IM01L    EQU   *-IM01
IM01NAME EQU   4+12                OFFSET
         SPACE
IM02     WTO   '&MID. TSSO IS ALREADY ACTIVE',ROUTCDE=(2),MF=L
IM02L    EQU   *-IM02
         SPACE
IM03     WTO   '&MID. OLD SSVT FOUND',ROUTCDE=(2),MF=L
IM03L    EQU   *-IM03
         SPACE
IM04     WTO   '&MID. CONNECTED',ROUTCDE=(2),MF=L
IM04L    EQU   *-IM04
         SPACE
IM05     WTO   '&MID. INVALID COMMAND',                                X
               DESC=(5),MCSFLAG=(REG0,NOCPY),MF=L
IM05L    EQU   *-IM05
         SPACE
IM06     WTO   '&MID. TIME',ROUTCDE=(2),MF=L
IM06L    EQU   *-IM06
         SPACE
IM07     WTO   '&MID. TIMER STARTING',ROUTCDE=(2),MF=L
IM07L    EQU   *-IM07
         SPACE
IM08     WTO   '&MID. TIMER STOPPING',ROUTCDE=(2),MF=L
IM08L    EQU   *-IM08
         SPACE
IM09     WTO   '&MID. NO ESTAE',ROUTCDE=(2),MF=L
         SPACE
IM10     WTO   '&MID. WAITING',ROUTCDE=(2),MF=L
         SPACE
IM11     WTO   '&MID. POSTED',ROUTCDE=(2),MF=L
         SPACE
IM12     WTO   '&MID. JOBID REQUEST FAILED',ROUTCDE=(2),MF=L
IM12L    EQU   *-IM12
         SPACE
IM14     WTO   '&MID. DISCONNECTED',ROUTCDE=(2),MF=L
IM14L    EQU   *-IM14
         SPACE
IM15     WTO   '&MID. CONTINUATION BUFFER ERASED, RE-ENTER',           X
               DESC=(5),MCSFLAG=(REG0,NOCPY),MF=L
IM15L    EQU   *-IM15
         SPACE
IM16     WTO   '&MID  TSSO FROM TSO',ROUTCDE=(2),MF=L
IM16L    EQU   *-IM16
         SPACE
IM17     WTO   '&MID. JOBNAME EXCEEDS 4 CHARACTERS',ROUTCDE=(2),MF=L
IM17L    EQU   *-IM17
         SPACE
IM21     WTO   '&MID. ESTAE SSCT NOT AVAILABLE',MF=L
IM22     WTO   '&MID. ESTAE SSVT WAS NOT CONNECTED',MF=L
IM23     WTO   '&MID. ESTAE SSVT DISCONNECTED',MF=L
IM24     WTO   '&MID. ESTAE SSVT NOT AVAILABLE',MF=L
IM25     WTO   '&MID. ESTAE SSVT FREED',MF=L
JBIDM01  WTO   '&MID. JOBID IS XXXXXXXX',ROUTCDE=(2),MF=L
JBIDM01A EQU   *-12,8
         SPACE
MASKSSIB DC    0F'0',C'SSIB',AL2(36,0),C'JES2',CL16' ',XL8'00'
         SPACE
WTOR1    WTOR  '&MID. SPECIFY TSSO OPTIONS',                           X
               R6,L'PARMAREA,R7,MF=L
WTOR1L   EQU   *-WTOR1
         SPACE
WTOR2    WTOR  '&MID. TSSO PARM OR OPTION ERROR - RESPECIFY',          X
               R6,L'PARMAREA,R7,MF=L
WTOR2L   EQU   *-WTOR2
         SPACE
ESTAEM   ESTAE 0,MF=L
ESTAEL   EQU   *-ESTAEM
         SPACE
OKWTO    WTO   '&MID. OK',DESC=(5),MCSFLAG=(REG0,NOCPY),MF=L
         SPACE
         LTORG
         DC    0D'0'
CAPS     DC    129AL1(*-CAPS)      00-80
         DC    9AL1(*-CAPS+X'40')  81-89 BECOME C1-C9
         DC    7AL1(*-CAPS)        8A-90
         DC    9AL1(*-CAPS+X'40')  91-99 BECOME D1-D9
         DC    8AL1(*-CAPS)        9A-A1
         DC    8AL1(*-CAPS+X'40')  A2-A9 BECOME E2-E9
         DC    86AL1(*-CAPS)       AA-FF
         SPACE
         DC    0D'0'
COPYSSVT DC    256F'0'
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
IKJEFT01 DS    CL8
PARMECB  DC    F'0'
PARMAREA DC    XL100'00',X'00'
ALTJES   DS    CL1
*
WTOW     DS    0F,XL128
$OPTSTAT DC    XL1'00'
$OPTSTD  EQU   X'30'
CHARW    DS    CL1
CHATW    DS    CL1
OPTION   DS    CL1
$HTCB    DC    F'0'
$SSVT    DC    F'0'
$SSCT    DC    F'0'
$SSSM    DC    F'0'
$SSIB    DC    F'0'
ATTCECB1 DC    F'0'
ATTCTCB1 DC    F'0'
ATTCECB2 DC    F'0'
ATTCTCB2 DC    F'0'
FLAG     DS    F
WTOCOPYP DS    3F
ECBLIST  DS    2F
ECBTIMER DS    F
TIMECTR  DS    F
INTERVAL DS    F
TIMEPARM DS    3F
HSSNM    DS    F
PRIMSSIB DS    9F  XL36
CSAFREE  DS    2F
QUEFREE  DS    2F
ENTRYWTO DS    F
CTUCMID  DS    F                   UCMID OF LINE TO BE CONTINUED
MSTR     DS    CL4                 MSTR IF NOT UNDER JES
JBID     DS    CL4                 SUBSYSTEM NAME
MAXWTOS  DS    H                   TSSOWTO MAX WTO'S
SKIPNUM  DS    H                   TSSOWTO SKIP COUNT
         DS    0F                  MVS/XA                  JDM
ESTAEW   DS    XL(ESTAEL)          MVS/XA                  JDM
ESTAER12 DS    3F
ESTAER2  DS    F
ESTAESW  DS    F
ESTABEND DS    F
ESTAESV  DS    18F
ESTAESDW DS    F
         DS    0D
@DATAX   EQU   *-@DATA
TSOPARMP DS    F
TSOPARM  DS    H,1024C
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
************************************************************
*                                                          *
*         MAP OF CVT                                       *
*                                                          *
************************************************************
         SPACE
CVTPTR   EQU   16
CVT      DSECT
CVTMAP   EQU   *
         ORG   CVT+X'128'
CVTJESCT DS    A
         SPACE
         IEFJESCT
         SPACE 3
         IEFJSCVT
         SPACE 3
         IEFJSSVT
         SPACE
************************************************************
*                                                          *
*         MAP OF SSVT AREA WE USE FOR OUR OWN PURPOSES     *
*                                                          *
************************************************************
         SPACE
         ORG   SSVTFRTN+(32*4)     ROOM FOR 32 RTN POINTERS
$SVSTUS  DS    F
$SVSTUSP EQU   X'80'
$SVSTUST EQU   X'40'               TERMINATING
$SVSTUSR EQU   X'20'
$SVSTUSQ EQU   X'08'               QUEUE AREA IS GETMAINED
$SVSTUSC EQU   X'04'               CSA SSSM AREA IS GETMAINED
$SVSTUSI EQU   X'02'               JOB ID HAS BEEN REQUESTED FROM JES
$SVSTUSJ EQU   X'01'               JSCB IS MODIFIED
$SVCHAR  DS    CL1
$SVCHAT  DS    CL1
$SVFLAG  DS    BL2
$SVPOSTP DS    F
$SVPOSTE DS    F
$SVPASCB DS    F
$SVPOSTL DS    3F
$SVACTIV DS    F
$SVOKWTO DS    3F
$SVQMAXQ DS    F                   MAXIMUM QUEUE SIZE
$SVQUEUE DS    2F                  NUMBER OF COMMANDS QUEUED
$SVQADDR DS    F                   ADDRESS OF NEXT COMMAND TO PROCESS
$SVSSIB  DS    9F
$SVSSOBA DS    F
$SVSSOB  DS    5F,7F
$SVRRECB DS    F
$SVTSIZE EQU   *-SSVTFRTN          MUST NOT EXCEED 1024
$SVTSIZR EQU   1024-$SVTSIZE       HOW CLOSE TO 1024 ARE WE
         SPACE
************************************************************
*                                                          *
*         MAP OF A QUEUE ENTRY                             *
*                                                          *
************************************************************
         SPACE
$QENTRY  DSECT
$QENEXT  DS    F                   POINTER TO NEXT QUEUE ENTRY
$QEUCMID DS    F
$QECMNDL DS    F
$QECMND  DS    CL140
         DS    0F                  INSURE LENGTH IS MULTIPLE OF 4
$QESIZE  EQU   *-$QENTRY           SIZE OF EACH QUEUE ENTRY
         SPACE
         IEFJSSIB
         SPACE 3
         IEFJSSOB (CM,RR),CONTIG=YES
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
