TESTVTM2 TITLE '-  C O M P L E X   V T A M   A P P L I C A T I O N'
         MACRO
&LABEL   CHKERR &VTAMMAC,&TYPE=CHECK,&SAVE=YES
.*
         GBLA  &DIECTR
         LCLC   &C,&C2
.*
&DIECTR  SETA  &DIECTR+1
&C       SETC   'ABN'.'&SYSNDX'
&C2      SETC   'ABNDU'.'&DIECTR'
.*
         AIF   (T'&LABEL EQ 'O').SKIPLAB
&LABEL   DS    0H
.SKIPLAB ANOP  ,
.*
         AIF   ('&TYPE' EQ 'ABORT').ABORT
.*
         LTR   R15,R15             DID OPERATION WORK
         BZ    &C                    YES, SKIP THE ABEND
         SPACE 1
.ABORT   ANOP  ,
         AIF   ('&SAVE' EQ 'NO').NOSAVE
         STM   R15,R1,REGSAVE
         SPACE 1
.NOSAVE  ANOP  ,
&C2      L     R1,=A(IABNDWTO)
         MVC   WORKAREA(LABNDWTO),0(R1)
         MVC   WORKAREA+42(2),=CL2'&DIECTR'   ADD TARGET LENGTH - GP@P6
         MVC   WORKAREA+47(8),=CL8'&VTAMMAC'                      GP@P6
         STC   R15,WORKAREA+63                                    GP@P6
         SRL   R15,4                                              GP@P6
         STC   R15,WORKAREA+62                                    GP@P6
         NI    WORKAREA+62,X'0F'                                  GP@P6
         NI    WORKAREA+63,X'0F'                                  GP@P6
         STC   R0,WORKAREA+69                                     GP@P6
         SRL   R0,4                                               GP@P6
         STC   R0,WORKAREA+68                                     GP@P6
         NI    WORKAREA+68,X'0F'                                  GP@P6
         NI    WORKAREA+69,X'0F'                                  GP@P6
         L     R1,=A(HEXTABLE)                                    GP@P6
         TR    WORKAREA+62(2),0(R1)                               GP@P6
         TR    WORKAREA+68(2),0(R1)                               GP@P6
         WTO   MF=(E,WORKAREA)
         SPACE 1
         ABEND &DIECTR,DUMP
         SPACE 1
&C       DS    0H
.*
         MEND
         EJECT ,
         MACRO
&LABEL   SETINSRT &TYPE=WTO,&ERROR=
.*
         LCLC  &C
.*
&C       SETC  '4'
         AIF   ('&TYPE' EQ 'WTO').START
&C       SETC  '0'                                                   JW
.*
.START   ANOP
&LABEL   L     R1,=A(QUESTTAB)        -> TRT TABLE
         TRT   WORKAREA+&C.(WORKLEN-&C),0(1)
.*
         AIF   (T'&ERROR EQ 'O').NOERROR
.*
         BZ    &ERROR
         AGO   .MEND
.*
.NOERROR ANOP  ,
         BNZ   *+4+4                  ? FOUND
         LA    R1,0                   ? NOT FOUND
.*
.MEND    MEND
         EJECT ,
         MACRO
&LABEL   GETRPL &REG
.*
&LABEL   LA    R1,#WORK               -> #WORK
         L     R15,=A(GETRPL)         LOAD GETRPL ROUTINE ADDRESS
         BALR  R14,R15                GET RPL (IN REG1)
.*
         AIF   (T'&REG EQ 'O').MEND
.*
         LR    &REG(1),R1             COPY RPL ADDRESS
.*
.MEND    MEND
         SPACE 3
         MACRO
&LABEL   FREERPL &RPL=
.*
&LABEL   LA    R0,4                   OFFSET TO PREFIX
         SR    &RPL(1),R0             -> PREFIX
         L     R15,RPLQUEUE           -> TOP OF QUEUE
         ST    R15,0(,&RPL(1))        MAKE IT NEXT
         CS    R15,&RPL(1),RPLQUEUE   ADD TO FREE QUEUE
         BNE   *-4-4                    TRY AGAIN
         SR    &RPL(1),&RPL(1)        RPL NO LONGER AVAILABLE
.*
         MEND
         EJECT ,
         MACRO
&LABEL   SCHEDCLS &ARG=
.*
         LCLC  &C
&C       SETC  'SCH'.'&SYSNDX'
.*
&LABEL   LA    R14,PENDCLOS
         LA    R15,NUMCLOSE
         SPACE 1
&C       LA    R0,0
         CS    R0,&ARG(1),CLOSECID-CLOSENTY(R14)
         BE    &C.B
         SPACE 1
         LA    R14,CLOSLNTH(,R14)
         BCT   R15,&C
         SPACE 1
         CHKERR *UNCOND*,TYPE=ABORT
         SPACE 1
&C.B     DS    0H
.*
         MEND
         MACRO                                                       JW
&LABEL   MAKEMSG ,                                                   JW
&LABEL   STM   R14,R0,MSGSAVE         remember linkage regs          JW
         LA    R0,#WORK               -> #WORK                       JW
         L     R15,=A(MAKEMSG)        load MAKEMSG routine address   JW
         BALR  R14,R15                complete the message           JW
         LM    R14,R0,MSGSAVE         restore linkage regs           JW
         MEND                                                        JW
         EJECT ,
*                                                                    JW
* This is a special version of TESTVTM2 adapted to the non ACF       JW
* VTAM Level 2 that's available with MVS 3.8j. It supports most      JW
* terminal types that can occur on MVS 3.8j when running under       JW
* Hercules:                                                          JW
*                                                                    JW
*  - local non SNA 3270                                              JW
*  - local SNA 3270 attached through 3791L                           JW
*  - remote SNA 3270 attached through 3705 NCP                       JW
*  - local SNA TTY emulated as 3767 attached through 3791L           JW
*  - remote SNA TTY emulated as 3767 attached through 3705 NCP       JW
*                                                                    JW
* This version is a rework of the original TESTVTM2 port to MVS 3.8J JW
* done by Greg Price in 2003. Device support relies on the specific  JW
* behavior of VTAM level 2 and is incompatible with later ACF/VTAM   JW
* versions. It should be noted that 3270 terminals get queried       JW
* unconditionally. So please use tn3270 emulations supporting        JW
* WSF QUERY only (don't know if others do exist at all) and also     JW
* use real 3270 tubes supporting WSF QUERY only.                     JW
*                                                                    JW
* Juergen Winkelmann (JW), ETH Zuerich, June 2012                    JW
* winkelmann@id.ethz.ch                                              JW
*                                                                    JW
* Credits: Binyamin Dissen - original version                        JW
*          Greg Price      - first non ACF (MVS 3.8j) port (GP@P6)   JW
*          Greg Price      - I've stolen all of the 3270 query       JW
*                            structured field analysis and           JW
*                            3270 buffer address conversion          JW
*                            logic from the brilliant 3270           JW
*                            datastream article and sample           JW
*                            program on his website                  JW
*
* JCL FOR THE TESTVTM2 STARTED TASK GOES SOMETHING LIKE THIS:
*
* //TESTVTM2 PROC
* //STEP1   EXEC PGM=TESTVTM2,REGION=4096K,TIME=1440,PARM='Y       ' JW
* //SNAPFILE DD  SYSOUT=X,HOLD=YES
* //SYSUDUMP DD  SYSOUT=X,HOLD=YES
*
* ADD A STEPLIB DD IF TESTVTM2 IS NOT IN THE LINK LIST.
*
*
* VTAMLST(X) MAJOR NODE DEFINITION GOES SOMETHING LIKE THIS:
*
* Y        APPL AUTH=NVPACE,BUFFACT=10                               JW
*
* ISSUE 'V NET,ACT,ID=X' BEFORE RUNNING TESTVTM2 FOR THE FIRST TIME.
*
*
* You can choose other names for X and Y.                            JW
*                                                                 GP@P6
         EJECT ,
TESTVTM2 CSECT
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
         SAVE  (14,12),,*
         SPACE 1
         LR    R12,R15
         USING TESTVTM2,R12
         SPACE 1
         L     R11,0(,R1)
         LA    R11,1(,R11)         -> APPLID
         SPACE 1
         GETMAIN RU,LV=#AMT,SP=8
         SPACE 1
         LA    R2,0(,R1)
         LA    R3,#AMT
         XR    R14,R14
         XR    R15,R15
         MVCL  R2,R14
         SPACE 1
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING #WORK,R13
         SPACE 1                       initialize 3270 msg prefix    JW
         MVC   PRFX3270(P3270),THECAT  have the cat sit on the tube  JW
         MVC   NLONLY(STTY+2),PROMPT-2 linemode command prompt       JW
         SPACE 1
         LA    R3,ACBLENTH
         SHOWCB AM=VTAM,FIELDS=(ACBLEN,RPLLEN,NIBLEN),AREA=(R3),       X
               LENGTH=12,MF=(G,WORKAREA)
         CHKERR SHOW-LEN
         SPACE 1
         GENCB BLK=EXLST,AM=VTAM,LOGON=LOGEXIT,RELREQ=REQEXIT,         X
               LOSTERM=LOSTEXIT,                                       X
               MF=(G,WORKAREA)     BUILD AN EXLST
         CHKERR GENEXLST
         LR    R3,R1               SAVE ADDRESS
         SPACE 1
         L     R9,ACBLENTH         FETCH ACB LENGTH
         LA    R9,4(,R9)           PLUS ANCHOR WORD
         GETMAIN RU,LV=(R9),SP=9   GET ACB IMAGE
         ST    R13,0(,R1)          SAVE SAVE AREA ADDRESS IN ANCHOR
         STM   R0,R1,ACBADDR       SAVE ACB REGS
         SPACE 1
         LA    R8,4(,R1)
         USING IFGACB,R8
         SPACE 1
         GENCB BLK=ACB,AM=VTAM,WAREA=(R8),LENGTH=(R9),APPLID=(R11),    X
               MACRF=LOGON,EXLST=(R3),MF=(G,WORKAREA)                  X
                                   COMPLETE THE ACB
         CHKERR GENACB
         SPACE 1
         GENCB BLK=NIB,AM=VTAM,MODE=RECORD,MF=(G,WORKAREA),            X
               COPIES=NUMCIDS+1    GENERATE NESCESSARY NIBS
         CHKERR GENNIB
         ST    R1,NIBADDR
         SPACE 1
         GENCB BLK=RPL,AM=VTAM,ACB=(R8),COPIES=3,MF=(G,WORKAREA)       X
                                        GENERATE MODEL + RECEIVE RPLS
         CHKERR GENRPL
         SPACE 1
         ST    R1,MODELRPL
         A     R1,RPLLENTH
         ST    R1,RECVRPL1
         A     R1,RPLLENTH
         ST    R1,RECVRPL2
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  ((R8)),MF=(E,OPENLIST)  ATTEMPT TO OPEN ACB
         CHKERR OPEN
         SPACE 1
         XR    R1,R1                   CLEAR
         IC    R1,0(,R11)              APPLID LENGTH
         BCTR  R1,0                    DECREMENT FOR EX
         MVC   APPLID,=8C' '           BLANK
         MVC   APPLID(*-*),1(R11)      MOVE MODEL
         EX    R1,*-6                  COPY APPLID
         SPACE 1
         MVC   WORKAREA(LSTRTWTO),ISTRTWTO
         SETINSRT ,
         MVC   0(8,R1),APPLID          INSERT APPLID
         WTO   MF=(E,WORKAREA)         APPLICATION ACTIVE
         SPACE 1
         GETRPL ,                      AQUIRE AN RPL
         L     R0,=A(FREPLXIT)
         SETLOGON RPL=(1),OPTCD=(ASY,START),EXIT=(R0)
         CHKERR SETLOGON
         SPACE 1
ALLQUAL  DS    0H
         L     R2,=A(RECVEXIT)
         L     R1,RECVRPL1
         RECEIVE RPL=(1),RTYPE=(DFSYN,NDFASY,NRESP),                   X
               OPTCD=(ASY,ANY,TRUNC,CA,Q),AREA=CARD1,AREALEN=256,      X
               BRACKET=(NBB,NEB),EXIT=(R2)
         CHKERR RECEIVE1
         SPACE 1
         L     R1,RECVRPL2
         RECEIVE RPL=(1),RTYPE=(DFSYN,NDFASY,NRESP),                   X
               OPTCD=(ASY,ANY,TRUNC,CA,Q),AREA=CARD2,AREALEN=256,      X
               BRACKET=(NBB,NEB),EXIT=(R2)
         CHKERR RECEIVE2
         SPACE 1
         MVC   OTIMRCOD(LTIMRCOD),ITIMRCOD
         STIMER REAL,OTIMRCOD,BINTVL==F'300'
         SPACE 1
         LA    R6,STOPECB             build ..                       JW
         ST    R6,MAINECBS                   ..                      JW
         LA    R6,CLOSECB                     .. ECB ..              JW
         ST    R6,MAINECBS+4                          ..             JW
         OI    MAINECBS+4,X'80'                        .. list       JW
         XC    STOPECB,STOPECB        clear ..                       JW
         XC    CLOSECB,CLOSECB               ..                      JW
MAINLOOP WAIT  1,ECBLIST=MAINECBS     wait for SHUTDOWN or LOGOFFs   JW
         SPACE 1
         L     R10,X40                \                              JW
         O     R10,CLOSECB             \                             JW
         L     R6,X3F                   \ if CLOSECB had been posted JW
         NR    R6,R10                   / reset it and go to DOCLOSE JW
         CS    R10,R6,CLOSECB          /                             JW
         BE    DOCLOSE                /                              JW
         SPACE 1                                                     JW
         B     DOSHUT                 no other ECB posted, shutdown  JW
         SPACE 1                                                     JW
DOCLOSE  LA    R10,PENDCLOS           -> PENDING CLSDST TABLE        JW
         USING CLOSENTY,R10                                          JW
         LA    R9,NUMCLOSE                                           JW
         SPACE 1                                                     JW
CLSDLOOP DS    0H                                                    JW
         ICM   R7,B'1111',CLOSECID    ENTRY ACTIVE?                  JW
         BZ    ECLSDLOP                 NO, IGNORE IT                JW
         SPACE 1                                                     JW
         TS    CLOSFLAG               ALREADY SEEN                   JW
         BZ    ECLSDLOP                 NOPE, ALLOW FULL TIME        JW
         SPACE 1                                                     JW
         XC    CLOSENTY(CLOSLNTH),CLOSENTY                           JW
         SPACE 1                                                     JW
         GETRPL ,                                                    JW
         L     R0,=A(FREPLXIT)                                       JW
         CLSDST RPL=(1),OPTCD=(ASY,RELEASE),ARG=(R7),EXIT=(R0)         X
                                      RELEASE THE TERMINAL           JW
         CHKERR CLSDST                                               JW
         SPACE 1                                                     JW
ECLSDLOP DS    0H                                                    JW
         LA    R10,CLOSENTY+CLOSLNTH  -> NEXT CLSDST ENTRY           JW
         BCT   R9,CLSDLOOP            AND CHECK IT                   JW
         SPACE 1                                                     JW
         B     MAINLOOP               start over                     JW
         SPACE 1                                                     JW
DOSHUT   WTO   'TESTVTM2 - APPLICATION IS SHUTTING DOWN',              X
               ROUTCDE=11,DESC=7
         SPACE 1
         GETRPL R6
         SETLOGON RPL=(R6),OPTCD=(SYN,QUIESCE)
         CHKERR SETLOGON
         SPACE 1
         STIMER WAIT,BINTVL==F'300'   WAIT 3 SECONDS
         SPACE 1
         LA    R4,CIDLIST
         USING CIDENTRY,R4
         LA    R5,NUMCIDS
CLSDST   DS    0H
         ICM   R2,B'1111',CIDCID      FETCH CID
         BZ    ECLSDST                  NOT ACTIVE
         SPACE 1
         CLSDST RPL=(R6),OPTCD=(SYN,RELEASE),ARG=(R2)                  X
                                      RELEASE THE TERMINAL
         CHKERR CLSDST
         SPACE 1
         MVC   WORKAREA(LCLOSWTO),ICLOSWTO
         SETINSRT ,
         MVC   0(8,R1),CIDNAME        INSERT TERMINAL ID
         WTO   MF=(E,WORKAREA)        REPORT ON LOGOFF
         SPACE 1
ECLSDST  DS    0H
         LA    R4,CIDENTRY+CIDLENTH   -> NEXT CID
         BCT   R5,CLSDST              AND CLOSE IT
         SPACE 1
         DROP  R4
         SPACE 1
         CLOSE ((R8)),MF=(E,OPENLIST)
         CHKERR CLOSE
         SPACE 1
         MVC   WORKAREA(LDONEWTO),IDONEWTO
         SPACE 1
         SETINSRT ,
         L     R15,RPLTOTAL
         CVD   R15,DWORD
         UNPK  0(3,R1),DWORD+6(2)     INSERT COUNT
         OI    2(R1),X'F0'
         SPACE 1
         SETINSRT ,
         L     R15,RPLREQST
         CVD   R15,DWORD
         UNPK  0(3,R1),DWORD+6(2)     INSERT COUNT
         OI    2(R1),X'F0'
         SPACE 1
         WTO   MF=(E,WORKAREA)        REPORT ON LOGOFF
         SPACE 1
         LM    R0,R1,ACBADDR
         FREEMAIN RU,LV=(0),A=(1),SP=9  FREE THE ACB
         SPACE 1
         STIMER WAIT,BINTVL==F'300'   wait 3 seconds                 JW
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN RU,LV=#AMT,A=(1),SP=8
         SPACE 1
         RETURN (14,12),RC=0
         SPACE 2
         DS    0F                                                    JW
X40      DC    X'40000000'            ECB posted                     JW
X3F      DC    X'3F000000'            ECB not posted                 JW
         DC    AL2(STTY-1)            length of cmd prompt w/o NL    JW
PROMPT   DC    AL1(NL),C'Enter command> ' linemode command prompt    JW
STTY     EQU   *-PROMPT               length of command prompt       JW
NL       EQU   37                     EBCDIC newline for TTYs        JW
THECAT   DC    X'7EC3'                special appearance of the cat  JW
         DC    X'11'                                                 JW
THECATR1 EQU   *-THECAT               first cat row                  JW
         DC    X'0000'                                               JW
         DC    C'       |l      _,,,---,,_'                          JW
         DC    X'11'                                                 JW
THECATR2 EQU   *-THECAT               second cat row                 JW
         DC    X'0000'                                               JW
         DC    C'ZZZzz /,''.-''`''    -.  ;-;;,'                     JW
         DC    X'11'                                                 JW
THECATR3 EQU   *-THECAT               third cat row                  JW
         DC    X'0000'                                               JW
         DC    C'     |,4-  ) )-,_. ,( (  ''''-'''                   JW
         DC    X'11'                                                 JW
THECATR4 EQU   *-THECAT               fourth cat row                 JW
         DC    X'0000'                                               JW
         DC    C'    ''---''''(_/--''  `-'')_)'                      JW
         DC    X'1140401D401311'                                     JW
SCREENR2 EQU   *-THECAT               second row on screen           JW
         DC    X'C150'                                               JW
         DC    X'1DF8'                                               JW
P3270    EQU   *-THECAT         length of 3270 prefix including cat  JW
         LTORG ,
         SPACE 2
ICLOSWTO WTO   'TESTVTM2 - LOGGED OFF ???????? FOR SHUTDOWN',          X
               ROUTCDE=11,DESC=7,MF=L
LCLOSWTO EQU   *-ICLOSWTO
         SPACE 2
IDONEWTO WTO   'TESTVTM2 - APPLICATION COMPLETE - RPL STATS: COUNT=???,*
                REQUESTS=???',ROUTCDE=11,DESC=7,MF=L
LDONEWTO EQU   *-IDONEWTO
         SPACE 2
ISTRTWTO WTO   'TESTVTM2 - APPLICATION ???????? IS UP',                X
               ROUTCDE=11,DESC=7,MF=L
LSTRTWTO EQU   *-ISTRTWTO
         SPACE 2
         DROP  ,
         EJECT ,
ITIMRCOD DS    0H
         STM   R14,R12,12(R13)
         SPACE 1
         LR    R1,R15                 CURRENT ADDRESS
         LA    R0,OTIMRCOD-#WORK      OFFSET TO #WORK
         SR    R1,R0                  R1 -> #WORK
         L     R15,ITIMRCD2-ITIMRCOD(,R15)  -> READ ONLY SECTION
         BR    R15                    GO DO THE TIMER
         SPACE 1
ITIMRCD2 DC    A(TIMRCODE)            READ ONLY SECTION ADDRESS
         SPACE 1
LTIMRCOD EQU   *-ITIMRCOD             LENGTH OF PREFIX
         SPACE 3
TIMRCODE DS    0H
         LR    R12,R15
         USING TIMRCODE,R12
         SPACE 1
         LR    R11,R1
         USING #WORK,R11
         SPACE 1
         LA    R1,SAVE3               GET A SAVE AREA
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         SPACE 1
         STIMER REAL,OTIMRCOD,BINTVL==F'300'  RESET INTERVAL
         POST  CLOSECB                release terminals logged off   JW
         SPACE 1
         L     R13,4(,R13)            -> PREV SAVE
         RETURN (14,12),RC=0          AND GO AWAY
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
LOGEXIT  DS    0H
         LR    R12,R15
         USING LOGEXIT,R12
         SPACE 1
         LR    R11,R1                 SAVE PARM ADDRESS
         SPACE 1
         L     R8,0(,R11)             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS
         BO    IGNORE                   YES, IGNORE THE LOGON
         SPACE 1
         L     R3,4(,R11)             -> TERMINAL NAME
         SPACE 1
         LA    R4,CIDLIST
         USING CIDENTRY,R4
         LA    R0,NUMCIDS
CHKDUP   DS    0H
         CLC   CIDNAME,0(R3)          DUPLICATE TERMINAL?
         BE    ABND$DUP                 YES, ERROR
         SPACE 1
         LA    R4,CIDENTRY+CIDLENTH   -> NEXT SLOT
         BCT   R0,CHKDUP              AND CHECK IT
         SPACE 1
         LA    R4,CIDLIST
         L     R7,NIBADDR
         LA    R0,NUMCIDS
FINDSLOT DS    0H
         OC    CIDCID,CIDCID          FREE CID?
         BZ    GOTSLOT                  YES, GOT THE SPOT
         SPACE 1
         LA    R4,CIDENTRY+CIDLENTH   -> SLOT
         A     R7,NIBLENTH            -> NEXT NIB
         BCT   R0,FINDSLOT            AND CHECK IT
         SPACE 1
         B     GOTSLOT          accept this logon only to ..         JW
         SPACE 1                 .. tell user it's not accepted      JW
ABND$DUP DS    0H
         CHKERR *UNCOND*,TYPE=ABORT,SAVE=NO
         SPACE 1
GOTSLOT  DS    0H
         MVC   CIDNAME,0(R3)                 NODE NAME
         SPACE 1
         MODCB NIB=(R7),NAME=(*,CIDNAME),USERFLD=(R4),AM=VTAM,         X
               MF=(G,WORKAREA)        COMPLETE THE NIB
         CHKERR MOD-NIB
         SPACE 1
         ICM   R0,B'0011',=X'C150'    3270 address ..                JW
         STH   R0,CIDROW2              .. of 2nd row on screen       JW
         LA    R3,RECVAREA
         L     R5,16(,R11)            -> READ-ONLY RPL
         SHOWCB AM=VTAM,RPL=(R5),FIELDS=(AREA),                        X
               AREA=(R3),LENGTH=L'RECVAREA,MF=(G,WORKAREA)
         L     R14,16                 -> CVT                      GP@P6
         CLI   116(R14),X'13'         MVS 3.8?                    GP@P6
         BNE   MUSTHAVE               NO, NEW PLIST SHOULD BE OK  GP@P6
         LTR   R15,R15                YES, LOGON EXIT HAS SHORTER GP@P6
         BNZ   OPENINQ                  PLIST - expect bad RPL GP@P6 JW
MUSTHAVE EQU   *                      SHOULD GET IT WITH ACF/VTAM GP@P6
         CHKERR SHOW-RPL
         SPACE 1
         L     R3,RECVAREA
         CLC   =X'810601',0(R3)       IS IT A CINIT RU?
         BNE   NOTCINIT                 NO, DO INQUIRE
         SPACE 1
         USING ISTDBIND-12,R3
         SPACE 1
         MVC   CIDBIND(24),ISTDBIND   COPY BIND DATA
         SPACE 1
         LA    R1,BINSPRIR            -> DEFAULTS
         SPACE 1
         CLI   BINPRESZ,BINPSZRC      ALTERNATES?
         BNE   *+4+4                    NOPE
         LA    R1,BINSALTR              YES, USE THEM
         SPACE 1
         XR    R0,R0                  CLEAR
         IC    R0,0(,R1)              FETCH ROWS
         STH   R0,CIDROWS             SAVE VALUE
         IC    R0,1(,R1)              FETCH COLUMNS
         STH   R0,CIDCOLS             SAVE VALUE
         SPACE 1
         DROP  R3
         SPACE 1
         B     OPENDEST
         SPACE 1
NOTCINIT DS    0H
         WTO   'TESTVTM2 - NOTCINIT CODE SECTION ENTERED',             X
               ROUTCDE=(2,11),DESC=(2,7)
         SPACE 1
OPENINQ  GETRPL ,                                                    JW
         L     R0,=A(BINDEXIT)                                       JW
         INQUIRE RPL=(1),NIB=(R7),AREA=CIDBIND,AREALEN=6*8,            X
               OPTCD=(ASY,SESSPARM),EXIT=(R0)                        JW
         CHKERR INQUIRE
         SPACE 1
OPENDEST DS    0H
         GETRPL ,
         OPNDST RPL=(1),ACB=(R8),NIB=(R7),EXIT=GOODMXIT,               X
               OPTCD=(ASY,ACCEPT,SPEC,NQ)   COMPLETE THE SESSION
         CHKERR OPNDST
         SPACE 1
         LA    R5,FAILOGON         \  no signon message              JW
         CR    R4,R5                > if reserve CIDENTRY            JW
         BE    LOGNEND             /  is in use                      JW
         SPACE 1                                                     JW
         MVC   WORKAREA(LSIGNWTO),ISIGNWTO
         SPACE 1
         SETINSRT ,
         MVC   0(8,R1),CIDNAME        INSERT TERMINAL NAME
         WTO   MF=(E,WORKAREA)
         SPACE 2
         B     LOGNEND
         SPACE 2
IGNORE   DS    0H
         GETRPL ,
         L     R0,=A(FREPLXIT)
         CLSDST RPL=(1),OPTCD=(ASY,RELEASE),EXIT=(R0)
         CHKERR CLSDST
         SPACE 2
LOGNEND  DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
ISIGNWTO WTO   'TESTVTM2 - TERMINAL ???????? SIGNON ACCEPTED',         X
               ROUTCDE=11,DESC=7,MF=L
LSIGNWTO EQU   *-ISIGNWTO
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
GOODMXIT DS    0H
         LR    R12,R15
         USING GOODMXIT,R12
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    GOODMEND                 YES, ALL DONE
         SPACE 1
         CHKERR CHECK                 FAIL ON OPNDST ERROR
         SPACE 1
         LA    R7,DWORD               find NIB                       JW
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(AREA),                        X
               AREA=(R7),LENGTH=4,MF=(G,WORKAREA)                    JW
         L     R7,DWORD               NIB address                    JW
         LA    R4,DWORD               NIB USERFLD has CIDENTRY addr  JW
         SHOWCB AM=VTAM,NIB=(R7),FIELDS=(USERFLD),                     X
               AREA=(R4),LENGTH=4,MF=(G,WORKAREA)                    JW
         L     R4,DWORD               CIDENTRY address               JW
         SHOWCB AM=VTAM,NIB=(R7),FIELDS=(CID),                         X
               AREA=(R4),LENGTH=4,MF=(G,WORKAREA) store CID in table JW
         SPACE 1
         USING CIDENTRY,R4                                           JW
         CLC   CIDROW2,=XL2'0000'     3270 terminal?                 JW
         BE    GOGOODM                 no,  send good morning msg    JW
         SPACE 1                       yes, issue WSF query          JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               AREA=QUERY,RECLEN=LQUERY,RESPOND=(EX,FME,NRRN),         X
               BRACKET=(BB,NEB),POST=SCHED,EXIT=(R0),CHNGDIR=(CMD)     X
                                      query terminal                 JW
         CHKERR SEND
         SPACE 1
GOODMEND DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
GOGOODM  L     R12,=A(RECVEXIT)      \                               JW
         USING RECVEXIT,R12           \                              JW
         LA    R11,X'FFF'(,R12)        \                             JW
         USING RECVEXIT+X'FFF',R11      > addressability of RECVEXIT JW
         LA    R9,X'FFF'(,R11)         /                             JW
         LA    R9,1(,R9)              /                              JW
         USING RECVEXIT+X'1FFF',R9   /                               JW
         B     DOGOODM                say good morning or good night JW
         SPACE 2                                                     JW
         LTORG ,
         SPACE 2
*                          3C5D7F00 CUT FROM ALL DATA STREAMS BY  GP@P6
QUERY    DC    X'F3000501FF02'      write structured field, query    JW
LQUERY   EQU   *-QUERY              length of WSF QUERY              JW
         SPACE 2
         DROP  ,
         EJECT ,
STATEXIT DS    0H
         LR    R12,R15
         USING STATEXIT,R12
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    STATEND                  YES, ALL DONE
         SPACE 1
         STM   R15,R1,REGSAVE         SAVE STATUS
         SPACE 1
         LA    R15,4                  RPL PREFIX LENGTH
         LCR   R15,R15
         L     R4,0(R15,R6)           FETCH PREFIX VALUE
         USING CIDENTRY,R4
         SPACE 1
         LA    R3,DWORD
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(FDBK),LENGTH=4,               X
               AREA=(R3),MF=(G,WORKAREA)   GET RPL FEEDBACK
         CHKERR SHOW-RPL
         L     R3,DWORD               FETCH FEEDBACK VALUE
         SPACE 1
         ICM   R15,B'1111',REGSAVE    RPL GO OK?
         BZ    GIVESTAT                 YES, GIVE STATUS
         SPACE 1
         LA    R3,DWORD
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(RTNCD,FDBK2),                 X
               AREA=(R3),LENGTH=12,MF=(G,WORKAREA)
         CHKERR SHOW-RPL
         SPACE 1
         CLI   DWORD+3,X'14'          RTCND=14
         BNE   FAILSTAT                 NO, NOT EXPECTED ERROR
         CLI   DWORD+7,X'4C'          FDBK2=4C?
         BE    GIVSTAT0                 UNKNOWN/INACTIVE LU
         CLI   DWORD+7,X'53'          FDBK2=53?
         BE    GIVSTAT0                 UNKNOWN/INACTIVE LU
         SPACE 1
FAILSTAT DS    0H
         CHKERR *UNCOND*,TYPE=ABORT,SAVE=NO    FAIL ON INQUIRE ERROR
         SPACE 1
GIVSTAT0 DS    0H
         LA    R3,36                  INVALID RETURN CODE
         SPACE 1
GIVESTAT DS    0H
         SLL   R3,1                   *2
         LA    R3,STATUSMS(R3)        -> TABLE ENTRY
         LM    R2,R3,0(R3)            FETCH SKELETON ADDR, LENGTH
         MVC   WORKAREA(*-*),0(R2)    MOVE MODEL
         EX    R3,*-6                 COPY SKELETON
         SPACE 1
         SETINSRT TYPE=SEND
         MVC   0(8,R1),CIDAPPLN       INSERT APPLID
         XC    CIDAPPLN,CIDAPPLN      CLEAR IT
         SPACE 1
         ST    R4,MSGTERM             remember terminal table addr   JW
         STH   R3,MSGTXTLN            store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         L     R2,CIDCID
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),ARG=(R2),   X
               RESPOND=(EX,FME,NRRN),                                  X
               BRACKET=(NBB,NEB),POST=SCHED,EXIT=(R0),CHNGDIR=(CMD)    X
                                      GIVE APPLICATION STATUS
         CHKERR SEND
         SPACE 1
STATEND  DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
         LTORG ,
         SPACE 2
STATUSMS DC    0F'0'
         DC    A(STATUSM1,LSTATSM1)   RC = 0
         DC    A(STATUSM2,LSTATSM2)        4
         DC    A(STATUSM3,LSTATSM3)        8
         DC    A(STATUSM4,LSTATSM4)        12
         DC    A(STATUSM5,LSTATSM5)        16
         DC    2X'00666666'                20
         DC    2X'00666666'                24
         DC    2X'00666666'                28
         DC    2X'00666666'                32
         DC    A(STATUSM6,LSTATSM6)        36
         SPACE 1
STATUSM1 DC    C'Application ???????? is active'                     JW
LSTATSM1 EQU   *-STATUSM1
         SPACE 2
STATUSM2 DC    C'Application ???????? is not active'                 JW
LSTATSM2 EQU   *-STATUSM2
         SPACE 2
STATUSM3 DC    C'Application ???????? active, MACRF=NLOGON'          JW
LSTATSM3 EQU   *-STATUSM3
         SPACE 2
STATUSM4 DC    C'Application ???????? active, logons are stopped'    JW
LSTATSM4 EQU   *-STATUSM4
         SPACE 2
STATUSM5 DC    C'Application ???????? ACTIVE, logons quiesced'       JW
LSTATSM5 EQU   *-STATUSM5
         SPACE 2
STATUSM6 DC    C'Application ???????? not defined to VTAM'           JW
LSTATSM6 EQU   *-STATUSM6
         SPACE 2
         DROP  ,
         EJECT ,
BINDEXIT DS    0H
         LR    R12,R15
         USING BINDEXIT,R12
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         CHKERR CHECK                 FAIL ON INQUIRE ERROR
         SPACE 1
         LA    R3,RECVAREA
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(AREA),                        X
               AREA=(R3),LENGTH=L'RECVAREA,MF=(G,WORKAREA)
         CHKERR SHOW-RPL
         SPACE 1
         L     R7,RECVAREA
         USING ISTDBIND,R7
         SPACE 1
         L     R4,=A(CIDENTRY-CIDBIND)  OFFSET TO BUFFER
         LA    R4,0(R4,R7)            -> CIDENTRY
         USING CIDENTRY,R4
         SPACE 1
         LA    R1,BINSPRIR            -> DEFAULTS
         SPACE 1
         CLI   BINPRESZ,BINPSZRC      ALTERNATES?
         BNE   *+4+4                    NOPE
         LA    R1,BINSALTR              YES, USE THEM
         SPACE 1
         XR    R0,R0                  CLEAR
         IC    R0,0(,R1)              FETCH ROWS
         STH   R0,CIDROWS             SAVE VALUE
         IC    R0,1(,R1)              FETCH COLUMNS
         STH   R0,CIDCOLS             SAVE VALUE
         SPACE 1
         FREERPL RPL=(R6)
         SPACE 1
         CLI   BINFM,3                SNA terminal?                  JW
         BNE   BINDDONE               no, must be local, we're done  JW
         CLC   CIDROWS(4),=F'0'       screen size found in bind?     JW
         BNE   BINDDONE               yes, it's SNA 3270, we're done JW
         XR    R0,R0                  \                              JW
         STH   R0,CIDROWS              \                             JW
         STH   R0,CIDROW2               > indicate linemode          JW
         LA    R0,80                   /                             JW
         STH   R0,CIDCOLS             /                              JW
         SPACE 1                                                     JW
BINDDONE L     R14,SAVE2                                             JW
         XR    R15,R15
         BR    R14
         SPACE 2
         LTORG ,
         DROP  ,
         EJECT ,
REQEXIT  DS    0H
         LR    R12,R15
         USING REQEXIT,R12
         SPACE 1
         LR    R11,R1                 SAVE PARM ADDRESS
         SPACE 1
         L     R8,0(,R11)             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         L     R3,4(,R11)             -> SYMBOLIC NAME
         LA    R4,CIDLIST
         USING CIDENTRY,R4
         LA    R0,NUMCIDS
FINDTERM DS    0H
         CLC   CIDNAME,0(R3)          FOR THIS TERMINAL
         BE    GOTTERM                  YES, GOT THE TERMINAL
         SPACE 1
         LA    R4,CIDENTRY+CIDLENTH   -> SLOT
         BCT   R0,FINDTERM            AND CHECK IT
         SPACE 1
         CHKERR *UNCOND*,TYPE=ABORT SOMETHING IS WRONG
         SPACE 1
GOTTERM  DS    0H
         MVC   WORKAREA(LDROPWTO),IDROPWTO
         SETINSRT ,
         MVC   0(8,R1),0(R3)          INSERT TERMINAL ID
         WTO   MF=(E,WORKAREA)
         SPACE 1
         L     R7,CIDCID              FETCH CID
         XC    CIDENTRY(CIDLENTH),CIDENTRY  ENTRY NO LONGER IN USE
         SPACE 1
         DROP  R4
         SPACE 1
         GETRPL ,
         L     R0,=A(FREPLXIT)
         CLSDST RPL=(1),OPTCD=(ASY,RELEASE),ARG=(R7),EXIT=(R0)         X
                                      RELEASE THE TERMINAL
         CHKERR CLSDST
         SPACE 1
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
IDROPWTO WTO   'TESTVTM2 - RELREQ ENTERED FOR ???????? - DROPPING',    X
               ROUTCDE=11,DESC=7,MF=L
LDROPWTO EQU   *-IDROPWTO
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
LOSTEXIT DS    0H
         LR    R12,R15
         USING LOSTEXIT,R12
         SPACE 1
         LR    R11,R1                 SAVE PARM ADDRESS
         SPACE 1
         L     R8,0(,R11)             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         L     R4,8(,R11)             -> TABLE ENTRY
         USING CIDENTRY,R4
         SPACE 1
         MVC   WORKAREA(LLOSTWTO),ILOSTWTO
         SETINSRT ,
         MVC   0(8,R1),CIDNAME        INSERT TERMINAL ID
         SPACE 1
         OC    CIDCID,CIDCID          ALREADY CLEARED?
         BNZ   *+4+6                    NO
         MVC   0(8,R1),=CL8'*UNKNOWN'   YES, WHATS UP?
         SPACE 1
         L     R0,12(,11)
         CVD   R0,DWORD
         SETINSRT ,
         UNPK  0(2,R1),DWORD+6(2)
         OI    1(R1),X'F0'
         WTO   MF=(E,WORKAREA)
         SPACE 1
         OC    CIDCID,CIDCID          ALREADY CLEARED?
         BZ    LOSTEND                  YES, SKIP THE CLSDST
         SPACE 1
         XC    CIDENTRY(CIDLENTH),CIDENTRY  CLEAR TABLE ENTRY
         SPACE 1
         DROP  R4
         SPACE 1
         L     R7,4(,R11)             FETCH CID
         GETRPL ,
         L     R0,=A(FREPLXIT)
         CLSDST RPL=(1),OPTCD=(ASY,RELEASE),ARG=(R7),EXIT=(R0)         X
                                      RELEASE THE TERMINAL
         CHKERR CLSDST
         SPACE 1
LOSTEND  DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
ILOSTWTO WTO   'TESTVTM2 - LOSTERM ENTERED ON ????????, CODE = ??',    X
               ROUTCDE=11,DESC=7,MF=L
LLOSTWTO EQU   *-ILOSTWTO
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
RECVEXIT DS    0H
         LR    R12,R15
         USING RECVEXIT,R12
         LA    R11,RECVEXIT+X'FFF'
         USING RECVEXIT+X'FFF',R11
         LA    R9,X'FFF'(,R11)        third base ..                  JW
         LA    R9,1(,R9)                        .. for ..            JW
         USING RECVEXIT+X'1FFF',R9                    .. RECVEXIT    JW
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    RECVEND                  YES, ALL DONE
         SPACE 1
         LR    R4,R15                 SAVE RETURN CODE
         LR    R5,R0                   AND REASON CODE
         SPACE 1
         LA    R3,RECVBUFF
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(AREA,USER,ARG),               X
               AREA=(R3),LENGTH=LRECVBUF,MF=(G,WORKAREA)
         CHKERR SHOW-RPL
         L     R7,RECVARG
         SPACE 1
         ICM   R1,B'1111',RECVUSER    -> TABLE ENTRY
         BZ    REDRIVE                  UNKNOWN TERMINAL
         CLI   0(R1),0                UNSOLICITED?
         BE    REDRIVE                  YES, IGNORE THE READ
         SPACE 1
         LR    R0,R5                  RESTORE REASON CODE
         LTR   R15,R4                 WAS RECEIVE OK?
         BZ    TESTFME                  YEP, CHECK OUT FME
         SPACE 1
         LA    R3,WORKAREA
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(RTNCD,FDBK2,USENSEI),         X
               AREA=(R3),LENGTH=12,MF=(G,WORKAREA+12)
         CHKERR SHOW-RPL
         SPACE 1
         CLI   WORKAREA+3,4           RTCND=4?
         BNE   BAD$RECV                 NO, NOT HARDWARE ERROR
         SPACE 1
         CLI   WORKAREA+7,3           FDBK2=3?
         BNE   BAD$RECV                 NO, NOT HARDWARE ERROR
         SPACE 1
         RECEIVE RPL=(R6)             REDRIVE THE RECEIVE
         CHKERR RECEIVE
         SPACE 1
         UNPK  DWORD(5),WORKAREA+10(3)  GET SENSE INFO
         TR    DWORD(4),HEXTABLE-C'0' MAKE PRINTABLE HEX
         SPACE 1
         MVC   WORKAREA(LFORCWTO),IFORCWTO
         SETINSRT ,
         L     R4,RECVUSER            -> TABLE ENTRY
         MVC   0(8,R1),CIDNAME-CIDENTRY(R4)  INSERT TERMINAL ID
         SPACE 1
         SETINSRT ,
         MVC   0(4,R1),DWORD          INSERT SENSE INFO
         SPACE 1
         WTO   MF=(E,WORKAREA)        REPORT THE STUFF
         SPACE 1
         XC    0(CIDLENTH,R4),0(R4)   CLEAR TERMINAL INFO
         SPACE 1
         GETRPL ,
         L     R0,=A(FREPLXIT)
         CLSDST RPL=(1),OPTCD=(ASY,RELEASE),ARG=(R7),EXIT=(R0)         X
                                      RELEASE THE REMINAL
         CHKERR CLSDST
         SPACE 2
         B     RECVEND
         SPACE 2
BAD$RECV DS    0H
         LR    R0,R5                  RESTORE REASON CODE
         LR    R15,R4                         ERROR CODE
         CHKERR *UNCOND*,TYPE=ABORT
         SPACE 1
TESTFME  DS    0H
         TESTCB AM=VTAM,RPL=(R6),RESPOND=NFME,ERET=ABEND$CB,           X
               MF=(G,WORKAREA)
         BE    CHKRESP
         SPACE 1
         GETRPL ,
         L     R0,=A(FREPLXIT)
         SEND  RPL=(1),STYPE=RESP,RESPOND=(EX,FME,NRRN),OPTCD=ASY,     X
               ARG=(R7),EXIT=(R0)
         CHKERR SEND
         SPACE 1
CHKRESP  DS    0H
         L     R5,RECVAREA
         MVC   CARD,=256C' '          all blanks                     JW
         L     R4,RECVUSER            get terminal table entry add   JW
         USING CIDENTRY,R4            tell assembler                 JW
         CLC   CIDROW2,=XL2'0000'     linemode terminal?             JW
         BE    CHKTTYRS                yes, edit linemode response   JW
         DROP  R4                     don't need terminal any more   JW
         XC    CARD(3),CARD           FIRST AID ALONE
         CLI   0(R5),X'88'            query reply?                   JW
         BNE   *+4+6+4                  no, translate input          JW
         MVC   CARD,0(R5)               yes, copy input              JW
         B     *+4+6                  skip translate                 JW
         OC    CARD,0(R5)
         CLI   3(R5),X'11'            LEADING SBA
         BNE   *+4+6+6                 no, skip compress             JW
         MVC   CARD+3(L'CARD-6),CARD+6 yes, compress the card ..     JW
         MVC   3(L'CARD-6,R5),6(R5)     .. and the original input    JW
         B     *+16                   skip linemode edit             JW
CHKTTYRS TR    0(256,R5),REMCRLF      linemode: blank CRLF and ..    JW
         OC    CARD+3(253),0(R5)       .. translate to uppercase     JW
         SPACE 1                                                     JW
         CLC   =C'SEND ',CARD+3       SEND command?                  JW
         BE    *+10                   yes, keep input for mixed case JW
         MVC   0(256,R5),=256C' '     CLEAR INPUT BUFFER             JW
         SPACE 1
         CLC   =C'SENDF2 ',CARD+3
         BE    DOSENDF2
         SPACE 1
         RECEIVE RPL=(R6)             REDRIVE IT
         CHKERR RECEIVE
         SPACE 1
         LA    R4,CANTCLER
         CLI   CARD,X'6D'             CLEAR KEY?
         BE    RESEND                   TOO BAD
         SPACE 1
         LA    R4,WHYINTRP
         CLI   CARD,X'6C'             PA1 KEY?
         BE    RESEND                   TOO BAD
         SPACE 1
         OC    CARD(3),CARD           ALL ZEROES INPUT?
         BZ    SENDELAY                 SIGNAL COMPLETED
         SPACE 1                                                     JW
         CLI   CARD,X'88'             query reply?                   JW
         BE    QRYREPLY                 analyze                      JW
         SPACE 1
         CLC   =C'LOGOFF',CARD+3
         BE    DOLOGOFF
         SPACE 1
         CLC   =C'SHUTDOWN',CARD+3
         BE    DOSHUTD
         SPACE 1
         CLC   =C'SHOWTERM',CARD+3
         BE    DOSHOWT
         SPACE 1
         CLC   =C'STATS',CARD+3
         BE    DOSHOWS
         SPACE 1
         CLC   =C'SHOWALL',CARD+3
         BE    DOSHOWA
         SPACE 1
         CLC   =C'SHOW TERM',CARD+3
         BE    DOSHOWT
         SPACE 1
         CLC   =C'SHOW ALL',CARD+3
         BE    DOSHOWA
         SPACE 1
         CLC   =C'STATUS ',CARD+3
         BE    DOSTATUS
         SPACE 1
         CLC   =C'SHOW APP',CARD+3
         BE    DOSHOWP
         SPACE 1
         CLC   =C'SEND ',CARD+3
         BE    DOSEND
         SPACE 1
         LA    R4,PROMPT2
         CLC   =C'PROMPT2',CARD+3
         BE    RESEND
         SPACE 1
         LA    R4,PROMPT1
         B     RESEND
         SPACE 1
DOLOGOFF DS    0H
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   WORKAREA(LLOGMSG),LOGMSG get message                  JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LLOGMSG     store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         L     R4,RECVUSER            -> TABLE ENTRY                 JW
         CLC   CIDROW2-CIDENTRY(,R4),=XL2'0000' linemode terminal?   JW
         BNE   *+16                    no, leave msg alone           JW
         L     R5,MSGLEN               yes, remove ..                JW
         SH    R5,NLONLY                .. command ..                JW
         ST    R5,MSGLEN                 .. prompt                   JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,EB),ARG=(R7),EXIT=(R0)
         CHKERR SEND
         SPACE 1
         MVC   WORKAREA(LLOFFWTO),ILOFFWTO
         SETINSRT ,
         MVC   0(8,R1),CIDNAME-CIDENTRY(R4)  INSERT TERMINAL ID
         SPACE 1
         WTO   MF=(E,WORKAREA)        REPORT THE STUFF
         SPACE 1
CLEARCID XC    0(CIDLENTH,R4),0(R4)   CLEAR THE ENTRY                JW
         SPACE 1
         SCHEDCLS ARG=(R7)            SCHEDULE CLSDST
         SPACE 2
         B     RECVEND
         SPACE 2
DOSHOWT  DS    0H
         L     R4,RECVUSER            -> TABLE ENTRY
         USING CIDENTRY,R4
         SPACE 1
         MVC   WORKAREA(LTERMMSG),TERMMSG  PRIME MESSAGE
         SPACE 1
         SETINSRT TYPE=SEND
         MVC   0(8,R1),CIDNAME        INSERT TERMINAL NAME
         SPACE 1
         LH    R0,CIDROWS
         CVD   R0,DWORD
         SETINSRT TYPE=SEND
         UNPK  0(3,R1),DWORD+5(3)     INSERT ROWS                    JW
         OI    2(R1),X'F0'                                           JW
         SPACE 1
         LH    R0,CIDCOLS
         CVD   R0,DWORD
         SETINSRT TYPE=SEND
         UNPK  0(3,R1),DWORD+5(3)     INSERT COLUMNS                 JW
         OI    2(R1),X'F0'                                           JW
         SPACE 1
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         ST    R4,MSGTERM             remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LTERMMSG    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         DROP  R4
         SPACE 1
         B     RECVEND
         SPACE 2
DOSHOWP  DS    0H
         MVC   WORKAREA(LAPPLMSG),IAPPLMSG  PRIME MESSAGE
         SPACE 1
         SETINSRT TYPE=SEND
         MVC   0(8,R1),APPLID         INSERT APPLID
         SPACE 1
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LAPPLMSG    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     RECVEND
         SPACE 2
DOSENDF2 DS    0H
         L     R4,RECVUSER            -> TABLE ENTRY                 JW
         USING CIDENTRY,R4                                           JW
         LA    R5,CIDBIND             bind data                      JW
         USING ISTDBIND,R5                                           JW
         CLI   BINFM,3                SNA terminal?                  JW
         BNE   DOTHEF2                no, must be local, go for it   JW
         SPACE 1                                                     JW
         DROP  R4                                                    JW
         DROP  R5                                                    JW
         GETRPL R5                                                   JW
         SPACE 1                                                     JW
         MVC   WORKAREA(LF2LOCAL),F2LOCAL get message                JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LF2LOCAL    store length of message text   JW
         MAKEMSG                      add terminal specific          JW
         MODCB RPL=(R5),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)                                       JW
         SEND  RPL=(R5),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,EB),ARG=(R7),EXIT=(R0)        JW
         CHKERR SEND                                                 JW
         SPACE 1                                                     JW
         B     REDRIVE                                               JW
         SPACE 1                                                     JW
DOTHEF2  GETRPL ,                                                    JW
         L     R0,=A(F2EXIT1)
         SEND  RPL=(1),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY,CS),          X
               AREA==X'F2',RECLEN=1,RESPOND=(EX,FME,NRRN),             X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     REDRIVE
         SPACE 2
DOSHOWS  DS    0H
         MVC   WORKAREA(LSTATMSG),STATMSG
         SETINSRT TYPE=SEND
         L     R15,RPLTOTAL
         CVD   R15,DWORD
         UNPK  0(3,R1),DWORD+6(2)     INSERT COUNT
         OI    2(R1),X'F0'
         SPACE 1
         SETINSRT TYPE=SEND
         L     R15,RPLREQST
         CVD   R15,DWORD
         UNPK  0(3,R1),DWORD+6(2)     INSERT COUNT
         OI    2(R1),X'F0'
         SPACE 1
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LSTATMSG    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     RECVEND
         SPACE 2
DOSHOWA  DS    0H
         MVC   WORKAREA(LSHALMSG),SHALMSG
         SPACE 1
         LA    R5,CIDLIST
         USING CIDENTRY,R5
         LA    R0,NUMCIDS
DOSHOWA1 DS    0H
         OC    CIDCID,CIDCID          ENTRY ACTIVE?
         BZ    DOSHOWA2                 NO, SKIP IT
         SPACE 1
         SETINSRT ,
         MVC   0(8,R1),CIDNAME        COPY ID
         SPACE 1
DOSHOWA2 DS    0H
         LA    R5,CIDENTRY+CIDLENTH   -> NEXT ENTRY
         BCT   R0,DOSHOWA1            AND CHECK IT
         SPACE 1
         DROP  R5
         SPACE 1
         LA    R0,WORKAREA-8
         SR    R1,R0                  LENGTH OF MESSAGE
         LR    R5,R1                  SAFE REGISTER
         SPACE 1
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         STH   R5,MSGTXTLN            store length of message text   JW
         MAKEMSG                      add terminal specific datas    JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     RECVEND
         SPACE 2
DOSHUTD  DS    0H
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   WORKAREA(LSHUTDMS),SHUTDMSG get message               JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table add    JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LSHUTDMS    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         L     R4,RECVUSER            -> TABLE ENTRY                 JW
         CLC   CIDROW2-CIDENTRY(,R4),=XL2'0000' linemode terminal?   JW
         BNE   *+16                    no, leave msg alone           JW
         L     R5,MSGLEN               yes, remove ..                JW
         SH    R5,NLONLY                .. command ..                JW
         ST    R5,MSGLEN                 .. prompt                   JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,EB),ARG=(R7),EXIT=(R0)
         CHKERR SEND
         SPACE 1
         MVC   WORKAREA(LSHUTWTO),ISHUTWTO
         SETINSRT ,
         MVC   0(8,R1),CIDNAME-CIDENTRY(R4)
         WTO   MF=(E,WORKAREA)
         SPACE 1
         POST  STOPECB
         SPACE 1
         B     RECVEND
         SPACE 2
DOSTATUS DS    0H
         L     R4,RECVUSER
         MVC   CIDAPPLN-CIDENTRY(,R4),CARD+10  SAVE APPLID
         SPACE 1
         TRT   CIDAPPLN-CIDENTRY(,R4),TABLE51
         BZ    STATUS2
         SPACE 1
         LA    R2,CIDAPPLN+L'CIDAPPLN-1-CIDENTRY(,R4)
         SR    R2,R1                  LENGTH TO BLANK
         MVC   0(*-*,R1),=8C' '       BLANK MODEL
         EX    R2,*-6                 BLANK THE END
         SPACE 1
STATUS2  DS    0H
         LA    R2,CIDAPPLN-CIDENTRY(,R4)  -> APPLICATION NAME
         SPACE 1
         L     R4,NIBLENTH
         MH    R4,=AL2(NUMCIDS)
         A     R4,NIBADDR
         MODCB NIB=(R4),NAME=(*,0(R2)),AM=VTAM,MF=(G,WORKAREA)         X
                                      INSERT SPECIFIED APPLID IN NIB
         CHKERR MOD-NIB
         SPACE 1
         GETRPL ,                     GET AN RPL
         SPACE 1
         LA    R15,4                  PREFIX LENGTH
         LCR   R15,R15
         AR    R15,R1                 -> PREFIX
         MVC   0(4,R15),RECVUSER      SAVE CID POINTER IN RPL PREFIX
         SPACE 1
         L     R0,=A(STATEXIT)
         L     R2,RECVUSER
         LA    R2,CIDBIND+24-CIDENTRY(R2)
         INQUIRE RPL=(1),EXIT=(R0),NIB=(R4),OPTCD=(ASY,APPSTAT),       X
               AREA=(R2),AREALEN=4    IS OTHER APPLICATION ACTIVE?
         SPACE 1
         B     RECVEND                ALL SCHEDULED
         SPACE 2
DOSEND   DS    0H
         LA    R1,CARD+8+8
         TRT   CARD+8(8),BLANKTAB     FIND TERMINAL ID
         ST    R1,TEXTSTRT
         LA    R0,CARD+8
         SR    R1,R0                  EX LENGTH OF ID
         MVC   DWORD,=8C' '
         MVC   DWORD(*-*),CARD+8
         EX    R1,*-6                 COPY TARGET TERMINAL
         SPACE 1
         L     R4,RECVUSER                                           JW
         CLC   CIDROW2-CIDENTRY(,R4),=XL2'0000' linemode terminal?   JW
         BNE   *+14                    no, go 3270 restore           JW
         MVC   CARD+3(253),0(R5)      restore input (linemode)       JW
         B     *+10                   skip 3270 restore              JW
         MVC   CARD,0(R5)             restore input (3270)           JW
         MVC   0(256,R5),=256C' '     clear input buffer             JW
         SPACE 1                                                     JW
         LA    R4,CIDLIST
         USING CIDENTRY,R4
         LA    R0,NUMCIDS
SENDFIND DS    0H
         CLC   CIDNAME,DWORD          TERMINAL LOGGED ON?
         BE    DOTHESND                 YES, DO THE SEND
         SPACE 1
         LA    R4,CIDENTRY+CIDLENTH   -> NEXT SLOT
         BCT   R0,SENDFIND            AND CHECK IT
         SPACE 1
         MVC   WORKAREA(LSENDMS1),ISENDMS1  PRIME FAILURE
         LA    R4,LSENDMS1
         B     SENDDONE
         SPACE 1
DOTHESND DS    0H
         LA    R14,WORKAREA                                          JW
         CLC   CIDROW2,=XL2'0000'     3270 terminal?                 JW
         BNE   *+12                    yes, go insert message        JW
         MVI   0(R14),NL               no, insert newline ..         JW
         LA    R14,1(,R14)                 .. and indent             JW
         MVC   0(LSENDMS9,R14),ISENDMS9 copy skeleton                JW
         SPACE 1
         SETINSRT TYPE=SEND
         L     R15,RECVUSER
         MVC   0(8,R1),CIDNAME-CIDENTRY(R15)
         SPACE 1
         LA    R1,CARD+255                                           JW
         TRT   CARD+3(L'CARD-3),TABLE51
         SPACE 1
         L     R14,TEXTSTRT
         SR    R1,R14                  LENGTH OF MESSAGE
         ST    R1,24(,R13)             SAVE IT
         SPACE 1
         SETINSRT TYPE=SEND
         L     R15,24(,R13)            LOAD LENGTH
         L     R14,TEXTSTRT                 ADDRESS
         MVC   0(*-*,R1),1(R14)        MOVE MODEL                    JW
         EX    R15,*-6                 COMPLETE THE MESSAGE
         SPACE 1
         TM    BINCMNP2+CIDBIND-ISTDBIND,BINHDXFF  TARGET 1/2 DUPLEX?
***                                                                  JW
*** For yet to be analyzed reasons the below SEND OPTCD=SIGNAL       JW
*** macro doesn't trigger a reply. The only LUs on MVS 3.8j that     JW
*** formally would require half duplex communication are 3767/3270   JW
*** attached through 3705/NCP or 3791L. But these in fact work       JW
*** full duplex flawlessly. For that reason we don't issue signal    JW
*** here but unconditionally send the message. This might need to    JW
*** be reviewed in more complex scenarios.                           JW
***                                                                  JW
***      BZ    SENDNOW                               NO, SEND NOW    JW
         B     SENDNOW                send unconditionally...        JW
         SPACE 1
         GETMAIN RU,LV=WORKLEN,SP=12   GET A MESSAGE BUFFER
         MVC   0(LSENDMS9,R1),WORKAREA  COPY MESSAGE BUFFER
         ST    R1,CIDPENDM             SAVE PENDING MESSAGE
         SPACE 1
         GETRPL ,
         L     R0,=A(FREPLXIT)
         L     R2,CIDCID
***                                                                  JW
*** SEND macro with CONTROL=SIGNAL give alignment error,             JW
*** thus we expand it manually here                                  JW
***                                                                  JW
*        SEND  RPL=(1),STYPE=REQ,CONTROL=SIGNAL,OPTCD=(ASY),          X
*              SIGDATA=X'00010000',RESPOND=(EX,FME,NRRN),             X
*              POST=SCHED,ARG=(R2),EXIT=(R0),BRACKET=(NBB,NEB)       JW
         CNOP  0,4                                                   JW
         TM    69(1),X'FF'       IS THE RPL ACTIVE?                  JW
         BO    MAN0850           YES BR AROUND MODIFY CODE           JW
         ST    R2,36(1)          PUT NEW ARGUMENT IN THE RPL         JW
         NI    68(1),X'FB'       SET NIB POINTER OFF                 JW
         ST    R0,8(1)           PUT EXIT ROUTINE ADDRESS IN THE RPL JW
         NI    40(1),X'FE'       SET ECB FLAG OFF                    JW
         NI    68(1),X'BF'       SET NOEXIT BIT OFF                  JW
         OI    68(1),X'20'       SET EXIT BIT ON                     JW
         OI    40(1),X'08'       TURN ON THE ASY BIT                 JW
         OI    20(1),X'04'       SET RPLEX ON (RESPOND=EX)           JW
         NI    20(1),X'FD'       SET RPLNFME OFF (RESPOND=FME)       JW
         NI    20(1),X'FE'       SET RPLRRN OFF (RESPOND=NRRN)       JW
         OI    20(1),X'80'       SET RPLSCHED FLAG ON (POST=SCHED)   JW
         XC    21(3,1),21(1)     SET CONTROL CODES OFF               JW
         OI    22(1),X'10'       SET RPLSIGNL ON (CONTROL=SIGNAL)    JW
         NI    16(1),X'7F'       SET RPLBB OFF (BRACKET=NBB)         JW
         NI    16(1),X'BF'       SET RPLEB OFF (BRACKET=NEB)         JW
         CNOP  0,4               align signal data                   JW
         B     *+8               branch around signal data           JW
         DC    XL4'00010000'     SIGNAL DATA FIELD                   JW
         L     15,*-4            GET SIGNAL DATA                     JW
         ST    15,108(1)         PUT SIGNAL DATA IN THE RPL          JW
         NI    17(1),X'7F'       SET RPLSRESP OFF (STYPE=REQ)        JW
MAN0850     EQU   *                                                  JW
         LA    0,X'22'           LOAD REQUEST CODE                   JW
         L     15,24(1)          GET ADDRESS OF ACB                  JW
         L     15,8(15)          GET ADDRESS OF INTERFACE RTN        JW
         BALR  14,15             GO TO THE INTERFACE ROUTINE         JW
         CHKERR SEND
         SPACE 1
         B     SENDREPT
         SPACE 1
SENDNOW  DS    0H
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         ST    R4,MSGTERM             remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LSENDMS9    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         L     R2,CIDCID
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),ARG=(R2),   X
               RESPOND=(EX,FME,NRRN),                                  X
               BRACKET=(NBB,NEB),POST=SCHED,EXIT=(R0)                  X
                                      SEND THE MESSAGE
         CHKERR SEND
         SPACE 1
SENDREPT DS    0H
         MVC   WORKAREA(LSENDMS2),ISENDMS2
         LA    R4,LSENDMS2
         SPACE 1
SENDDONE DS    0H
         SETINSRT TYPE=SEND
         MVC   0(8,R1),DWORD
         SPACE 1
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table add    JW
         STH   R4,MSGTXTLN            store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     RECVEND
         SPACE 2
SENDELAY DS    0H
         SPACE 1
         GETRPL R6                                                   JW
         L     R4,RECVUSER            terminal table address         JW
         L     R5,CIDPENDM
         MVC   WORKAREA(LSENDMS9),0(R5) restore message buffer       JW
         ST    R4,MSGTERM             remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LSENDMS9    store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),ARG=(R7),   X
               RESPOND=(EX,FME,NRRN),                                  X
               BRACKET=(NBB,NEB),POST=SCHED,EXIT=(R0),CHNGDIR=(CMD)    X
                                      SEND THE DELAYED MESSAGE
         CHKERR SEND
         SPACE 1
         XC    CIDPENDM,CIDPENDM
         FREEMAIN RU,LV=WORKLEN,SP=12,A=(R5)  FREE THE BUFFER
         SPACE 1
         B     RECVEND
         SPACE 2
QRYREPLY DS    0H                analyze query reply                 JW
         LA    R5,CARD+1         point past the AID                  JW
         LA    R3,DWORD          target for RECLEN                   JW
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(RECLEN),                      X
               AREA=(R3),LENGTH=4,MF=(G,WORKAREA) get data length    JW
         CHKERR SHOW-RPL         terminate in case of any error      JW
         L     R3,DWORD          copy data length                    JW
         SLR   R4,R4             clear for inserts                   JW
         BCT   R3,QUERYPRS       decrement for AID                   JW
         B     QERYDONE          just in case the AID was it         JW
QUERYPRS CLI   2(R5),X'81'       query reply ID?                     JW
         BNE   NXTSBFLD          no, ignore this structured field    JW
         CLI   3(R5),X'A6'       query reply implicit partition ID?  JW
         BE    QUERYIMP          yes, process it                     JW
NXTSBFLD ICM   R4,3,0(R5)        load structured field length        JW
         SR    R3,R4             subtract it from TGET data length   JW
         BNP   QERYDONE          end of Query structured fields      JW
         AR    R5,R4             point to next sub-field             JW
         B     QUERYPRS          examine it                          JW
QUERYIMP CLI   1(R5),17          length less than seventeen?         JW
         BL    QERYDONE          yes, ignore                         JW
         CLI   6(R5),11          parameter length less than eleven?  JW
         BL    QERYDONE          yes, ignore                         JW
         CLI   7(R5),1           implicit partition sizes?           JW
         BNE   QERYDONE          no, ignore                          JW
         L     R4,RECVUSER       get CIDENTRY address ..             JW
         USING CIDENTRY,R4        .. and tell assembler              JW
         CLI   16(R5),0          alternate lines defined?            JW
         BE    QUERYPRM          no, use primary screen              JW
         MVC   CIDROWS+1(1),16(R5) copy alternate lines              JW
         MVC   CIDCOLS+1(1),14(R5) copy alternate columns            JW
         B     CALCROW2          done                                JW
QUERYPRM MVC   CIDROWS+1(1),12(R5) copy primary lines                JW
         MVC   CIDCOLS+1(1),10(R5) copy primary columns              JW
CALCROW2 LH    R0,CIDCOLS        get line length                     JW
         BAL   R14,BUFFCALC      compute address of 2nd row on scrn  JW
         STH   R0,CIDROW2        remember in terminal table entry    JW
         LH    R1,CIDROWS        \                                   JW
         SRL   R1,1               \                                  JW
         SLL   R1,1                \                                 JW
         SH    R1,=H'1'             \ compute upper left             JW
         MH    R1,CIDCOLS           / corner of cat on screen        JW
         LA    R0,30               /                                 JW
         SR    R1,R0              /                                  JW
         SRL   R1,1              /                                   JW
         LR    R0,R1             \                                   JW
         BAL   R14,BUFFCALC       >  1st row of cat -> term table
         STH   R0,CIDCAT1        /                                   JW
         LH    R0,CIDCOLS        \                                   JW
         AR    R1,R0              \                                  JW
         LR    R0,R1               > 2nd row of cat -> term table
         BAL   R14,BUFFCALC       /                                  JW
         STH   R0,CIDCAT2        /                                   JW
         LH    R0,CIDCOLS        \                                   JW
         AR    R1,R0              \                                  JW
         LR    R0,R1               > 3rd row of cat -> term table
         BAL   R14,BUFFCALC       /                                  JW
         STH   R0,CIDCAT3        /                                   JW
         LH    R0,CIDCOLS        \                                   JW
         AR    R1,R0              \                                  JW
         LR    R0,R1               > 4th row of cat -> term table
         BAL   R14,BUFFCALC       /                                  JW
         STH   R0,CIDCAT4        /                                   JW
         SPACE 1                                                     JW
QERYDONE GETRPL R6               get new RPL                         JW
         MODCB RPL=(R6),ARG=(R7),MF=(G,WORKAREA) insert CID          JW
*                                                                    JW
*                                none 3270 terminals enter directly  JW
*                                from GOODMXIT here, no query        JW
*                                                                    JW
DOGOODM  ST    R4,MSGTERM             remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         LA    R5,FAILOGON         \  good night message             JW
         CR    R4,R5                > if reserve CIDENTRY            JW
         BE    DOGOODN             /  is in use                      JW
         MVC   WORKAREA(LFIRSTMS),FIRSTMSG get good morning message  JW
         SETINSRT TYPE=SEND                                          JW
         MVC   0(8,R1),APPLID         INSERT APPLID                  JW
         MVI   MSGTXTLN+1,LFIRSTMS    store length of message text   JW
         B     DOGOOD                 send good morning message      JW
DOGOODN  MVC   WORKAREA(LFAILMSG),FAILMSG get good night message     JW
         MVI   MSGTXTLN+1,LFAILMSG    store length of message text   JW
DOGOOD   MAKEMSG                      add terminal specific data     JW
         CR    R4,R5                  reserve CIDENTRY?              JW
         BNE   *+26                    no, leave msg alone           JW
         CLC   CIDROW2,=XL2'0000'     linemode terminal?             JW
         BNE   *+16                    no, leave msg alone           JW
         L     R5,MSGLEN               yes, remove ..                JW
         SH    R5,NLONLY                .. command ..                JW
         ST    R5,MSGLEN                 .. prompt                   JW
         L     R0,=A(FREPLXIT)
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),EXIT=(R0),                 X
               CHNGDIR=(CMD)          send good morning message      JW
         CHKERR SEND                                                 JW
         SPACE 1                                                     JW
         L     R7,CIDCID-CIDENTRY(,R4) get CID                       JW
         LA    R5,FAILOGON         \  sign off silently              JW
         CR    R4,R5                > if reserve CIDENTRY            JW
         BE    CLEARCID            /  is in use                      JW
         SPACE 1                                                     JW
         DROP  R4                                                    JW
         SPACE 1                                                     JW
         B     RECVEND                                               JW
         SPACE 2                                                     JW
RESEND   DS    0H                                                    JW
         GETRPL R6                                                   JW
         SPACE 1                                                     JW
         MVC   WORKAREA(70),0(R4)     get message                    JW
         MVC   MSGTERM(4),RECVUSER    remember terminal table addr   JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,70          store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)                                       JW
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY),            X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         B     RECVEND
         SPACE 1
REDRIVE  DS    0H
         RECEIVE RPL=(R6)
         CHKERR RECEIVE
         SPACE 1
         B     RECVEND
         SPACE 1
RECVEND  DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
ABEND$CB DS    0H
         CHKERR *UNCOND*,TYPE=ABORT
         SPACE 2
*                                                                    JW
*        ON ENTRY R0 HAS BINARY BUFFER ADDRESS.                      JW
*        ON EXIT R0 HAS DATA STREAM BUFFER ADDRESS.                  JW
*                                                                    JW
*        IF THE BUFFER ADDRESS IS 4K OR MORE THEN 14-BIT ADDRESSING  JW
*        IS OBVIOUSLY SUPPORTED.  12-BIT ADDRESSING IS ALWAYS SUP-   JW
*        PORTED SO IT IS USED IF THE BUFFER ADDRESS IS LESS THAN 4K. JW
*                                                                    JW
BUFFCALC DS    0H                 GET CODE FOR 3270 BUFFER ADDRESS.  JW
         CH    R0,=H'4095'        LOCATION GREATER THAN 4K (12 BITS) JW
         BHR   R14                YES, NO CONVERSION TO BE DONE.     JW
         STC   R0,DWORD+1         NO, DO ORIGINAL 3270 ADDRESSING.   JW
         NI    DWORD+1,B'00111111' GET LOW-ORDER SIX-BIT NUMBER.     JW
         SRL   R0,6                                                  JW
         STC   R0,DWORD           GET HIGH-ORDER SIX-BIT NUMBER.     JW
         TR    DWORD(2),BUFFTBL   CONVERT TO 3270 DATA STREAM CHARS. JW
         ICM   R0,X'3',DWORD      SAVE IN BOTTOM TWO BYTES OF R0.    JW
         BR    R14                RETURN TO MAINLINE.                JW
         SPACE ,                                                     JW
BUFFTBL  DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   ABCDEFGHI.<(+| JW
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  &JKLMNOPQR!$*); JW
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  -/STUVWXYZ,%_>? JW
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  0123456789:#@'=" JW
         SPACE 2                                                     JW
         LTORG ,
         SPACE 2
HEXTABLE DC    C'0123456789ABCDEF'
         SPACE 2
ILOFFWTO WTO   'TESTVTM2 - TERMINAL ???????? HAS LOGGED OFF',          X
               ROUTCDE=11,DESC=7,MF=L
LLOFFWTO EQU   *-ILOFFWTO
         SPACE 2
IFORCWTO WTO   'TESTVTM2 - HARDWARE ERROR ON ???????? - SENSE = ????', X
               ROUTCDE=11,DESC=7,MF=L
LFORCWTO EQU   *-IFORCWTO
         SPACE 2
ISHUTWTO WTO   'TESTVTM2 - SHUTDOWN COMMAND ACCEPTED FROM ????????',   X
               ROUTCDE=11,DESC=7,MF=L
LSHUTWTO EQU   *-ISHUTWTO
         SPACE 2
PROMPT1  DC    CL70'You get standard prompt # 1'                     JW
         SPACE 1
PROMPT2  DC    CL70'You have requested prompt # 2'                   JW
         SPACE 1
LOGMSG   DC    C'LOGOFF command accepted - returning to VTAM'        JW
LLOGMSG  EQU   *-LOGMSG
         SPACE 1
SHUTDMSG DC    C'Shutdown now in progress'                           JW
LSHUTDMS EQU   *-SHUTDMSG
         SPACE 1
CANTCLER DC    CL70'You can''t clear this screen'                    JW
         SPACE 1
WHYINTRP DC    CL70'Why do you want to interrupt me??'               JW
         SPACE 1
TERMMSG  DC    C'Your terminal id is ????????, size is ??? x ???'    JW
LTERMMSG EQU   *-TERMMSG
         SPACE 1
IAPPLMSG DC    C'TESTVTM2 application id is ????????'                JW
LAPPLMSG EQU   *-IAPPLMSG
         SPACE 1
STATMSG  DC    C'RPL statistics: count=???, used=???'                JW
LSTATMSG EQU   *-STATMSG
         SPACE 1
ISENDMS1 DC    C'Message not sent - terminal ???????? not logged on' JW
LSENDMS1 EQU   *-ISENDMS1
         SPACE 1
ISENDMS2 DC    C'Message sent to terminal ????????'                  JW
LSENDMS2 EQU   *-ISENDMS2
         SPACE 1
ISENDMS9 DC    C'Message from ????????: ?'                           JW
         DC    75C' '
LSENDMS9 EQU   *-ISENDMS9
         DC    C' '                                                  JW
         SPACE 1
SHALMSG  DC    C'Active terminals: '                                 JW
         DC    7C'???????? '
LSHALMSG EQU   *-SHALMSG
         SPACE 1                                                     JW
FIRSTMSG DC    C'Welcome to the TESTVTM2 application ????????'       JW
LFIRSTMS EQU   *-FIRSTMSG                                            JW
         SPACE 1                                                     JW
FAILMSG  DC    C'Logon not accepted - too many sessions'             JW
LFAILMSG EQU   *-FAILMSG                                             JW
         SPACE 1                                                     JW
F2LOCAL  DC    C'Invalid terminal type for x''F2'' '                 JW
         DC    C'(local 3270 support only)'                          JW
LF2LOCAL EQU   *-F2LOCAL                                             JW
         SPACE 2
IABNDWTO WTO   'TESTVTM2 - GENERATING USER ABEND CODE ?? - VTAMACRO - RX
               15=XX R0=XX',ROUTCDE=(2,11),DESC=7,MF=L
LABNDWTO EQU   *-IABNDWTO
         SPACE 2
QUESTTAB DC    256X'00'
         ORG   QUESTTAB+C'?'
         DC    X'04'
         ORG   ,
         SPACE 2
         DROP  ,
         EJECT ,                                                     JW
MAKEMSG  DS    0H          add terminal specific datastream to msg   JW
         SAVE  (14,12),,MAKEMSG-SUBROUTINE                           JW
         LR    R12,R15                                               JW
         USING MAKEMSG,R12                                           JW
         SPACE 1                                                     JW
         LR    R10,R0                                                JW
         USING #WORK,R10                                             JW
         L     R11,MSGTERM            get terminal table entry       JW
         USING CIDENTRY,R11                                          JW
         CLC   CIDROW2,=XL2'0000'     3270 terminal?                 JW
         BNE   MSG3270                 yes, send 3270 message        JW
         LA    R15,WORKAREA           linemode message address       JW
         LR    R1,R15                 copy for string scan           JW
         LH    R8,MSGTXTLN            message length                 JW
         BCTR  R8,0                   decrement for EXecute          JW
NEXTOKEN EX    R8,FINDNBLK            find nonblank char             JW
         BZ    ENDFOUND               no nonblank chars found        JW
         BM    STRTOKEN               nonblank char not at end       JW
NOBLANK  LH    R1,MSGTXTLN            last char nonblank, so use ..  JW
         B     HAVELNG                .. full message length         JW
STRTOKEN LR    R8,R15                 linemode message address       JW
         SR    R8,R1                  negative offset to nonblank    JW
         AH    R8,MSGTXTLN            residual count                 JW
         BCTR  R8,0                   decrement for EXecute          JW
         EX    R8,FINDBLNK            find blank                     JW
         BZ    NOBLANK                no blank found                 JW
         LR    R8,R15                 linemode message address       JW
         SR    R8,R1                  negative offset to blank       JW
         AH    R8,MSGTXTLN            residual count                 JW
         BCTR  R8,0                   decrement for EXecute          JW
         B     NEXTOKEN               start over                     JW
FINDBLNK TRT   0(1,R1),BLANKTAB       find blank (EXecuted)          JW
FINDNBLK TRT   0(1,R1),NBLNKTAB       find nonblank (EXecuted)       JW
ENDFOUND SR    R1,R15                 length without trailing blanks JW
HAVELNG  LA    R5,STTY                   linemode command prompt lng JW
         AR    R5,R1                     add text length -> total ln JW
         LR    R6,R15                    linemode message address    JW
         AR    R6,R1                     point behind text           JW
         MVC   0(STTY,R6),SUFFTTY        append command prompt EX'ed JW
         B     MSGSTORE                send message                  JW
MSG3270  LA    R15,PRFX3270            3270 message address          JW
         LA    R5,P3270                  3270 message prefix length  JW
         AH    R5,MSGTXTLN               add text length -> total ln JW
         MVC   ROW2(2),CIDROW2           prime address of screen r2  JW
         MVC   CAT1(2),CIDCAT1           prime address of cat row 1  JW
         MVC   CAT2(2),CIDCAT2           prime address of cat row 2  JW
         MVC   CAT3(2),CIDCAT3           prime address of cat row 3  JW
         MVC   CAT4(2),CIDCAT4           prime address of cat row 4  JW
MSGSTORE ST    R15,MSGADD             store message address          JW
         ST    R5,MSGLEN              store message length           JW
         RETURN (14,12)                                              JW
         LTORG ,                                                     JW
         DROP  ,                                                     JW
         SPACE 2                                                     JW
BLANKTAB DC    256X'00'
         ORG   BLANKTAB+C' '
         DC    X'04'
         ORG   ,
         SPACE 2                                                     JW
NBLNKTAB DC    256X'01'                                              JW
         ORG   NBLNKTAB+C' '                                         JW
         DC    X'00'                                                 JW
         ORG   ,                                                     JW
         SPACE 2
TABLE51  DC    256X'00'
         ORG   TABLE51+X'51'
         DC    X'04'
         ORG   ,
         SPACE 2                                                     JW
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                    JW
REMCRLF  DC    X'000102030405060708090A0B0C400E0F' 0                 JW
         DC    X'101112131415161718191A1B1C1D1E1F' 1                 JW
         DC    X'202122232440262728292A2B2C2D2E2F' 2                 JW
         DC    X'303132333435363738393A3B3C3D3E3F' 3                 JW
         DC    X'404142434445464748494A4B4C4D4E4F' 4                 JW
         DC    X'505152535455565758595A5B5C5D5E5F' 5                 JW
         DC    X'606162636465666768696A6B6C6D6E6F' 6                 JW
         DC    X'707172737475767778797A7B7C7D7E7F' 7                 JW
         DC    X'808182838485868788898A8B8C8D8E8F' 8                 JW
         DC    X'909192939495969798999A9B9C9D9E9F' 9                 JW
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF' A                 JW
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF' B                 JW
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF' C                 JW
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF' D                 JW
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF' E                 JW
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF' F                 JW
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                    JW
         EJECT ,
GETRPL   DS    0H
         SAVE  (14,12),,GETRPL-SUBROUTINE
         SPACE 1
         LR    R12,R15
         USING GETRPL,R12
         SPACE 1
         LR    R10,R1
         USING #WORK,R10
         SPACE 1
GETRPL1  DS    0H
         ICM   R5,B'1111',RPLQUEUE    ANY FREE RPL'S?
         BZ    GETRPL3                  NO, GET A NEW ONE
         SPACE 1
         L     R1,0(,R5)              FETCH CHAIN POINTER
         CS    R5,R1,RPLQUEUE         ACQUIRE THIS RPL
         BNE   GETRPL1                  IN USE
         SPACE 1
         B     GETRPL9                GOT THE RPL
         SPACE 1
GETRPL3  DS    0H
         L     R5,RPLLENTH
         LA    R0,4(,R5)
         GETMAIN RU,LV=(0),SP=10      GET A NEW RPL
         LR    R5,R1                  AND POINT R5 TO IT
         SPACE 1
         L     R15,RPLTOTAL
         LA    R0,1(,R15)
         CS    R15,R0,RPLTOTAL
         BNE   *-4-4
         SPACE 1
GETRPL9  DS    0H
         L     R1,RPLLENTH            RPL LENGTH
         BCTR  R1,0                   DECREMENT FOR EX
         L     R15,MODELRPL           -> MODEL RPL
         MVC   4(*-*,R5),0(R15)       MOVE MODEL
         EX    R1,*-6                 COMPLETE THE RPL
         XC    0(4,R5),0(R5)          CLEAR THE POINTER
         SPACE 1
         L     R15,RPLREQST
         LA    R0,1(,R15)
         CS    R15,R0,RPLREQST
         BNE   *-4-4
         SPACE 1
         LA    R1,4(,R5)              -> THE RPL
         L     R14,12(,R13)
         RETURN (2,12),RC=0           AND RETURN
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
FREPLXIT DS    0H
         LR    R12,R15
         USING FREPLXIT,R12
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    FREPLEND                 YES, ALL DONE
         SPACE 1
         CHKERR CHECK                 FAIL ON ANY ERROR
         SPACE 1
         FREERPL RPL=(R6)             GIVE UP RPL
         SPACE 1
FREPLEND DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
F2EXIT1  DS    0H
         LR    R12,R15
         USING F2EXIT1,R12
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    F2EX1END                 YES, ALL DONE
         SPACE 1
         CHKERR CHECK                 FAIL ON ANY ERROR
         SPACE 1
         LA    R3,RECVBUFF
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(AREA,USER,ARG,RECLEN),        X
               AREA=(R3),LENGTH=LRECVBUF,MF=(G,WORKAREA)
         CHKERR SHOW-RPL
         SPACE 1
         L     R4,RECVUSER
         USING CIDENTRY,R4
         LH    R3,CIDROWS
         MH    R3,CIDCOLS
         DROP  R4
         SPACE 1
         LA    R4,4(R3,R3)                                        GP@P6
         LA    R4,4095(,R4)           ROUND UP TO 4K MULTIPLE     GP@P6
         SRL   R4,12                                              GP@P6
         SLL   R4,12                                              GP@P6
         LR    R0,R4                  WHO SAYS R0 IS PRESERVED?   GP@P6
         GETMAIN RU,LV=(0),SP=34,BNDRY=PAGE
         ST    R4,0(,R1)              R4 IS PRESERVED             GP@P6
         LA    R4,4(,R1)
         SLL   R3,1                   DOUBLE SCREEN SIZE
         SPACE 1
         L     R2,=A(F2EXIT2)
         RECEIVE RPL=(R6),RTYPE=(DFSYN,NDFASY,NRESP),                  X
               OPTCD=(ASY,SPEC,TRUNC,CS,Q),AREA=(R4),AREALEN=(R3),     X
               BRACKET=(NBB,NEB),EXIT=(R2)
         CHKERR RECEIVE
         SPACE 1
F2EX1END DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
         LTORG ,
         SPACE 2
         DROP  ,
         EJECT ,
F2EXIT2  DS    0H
         LR    R12,R15
         USING F2EXIT2,R12
         SPACE 1
         L     R11,=A(HEXTABLE)
         USING HEXTABLE,R11
         SPACE 1
         LR    R6,R1                  SAVE RPL ADDRESS
         USING IFGRPL,R6
         SPACE 1
         L     R8,RPLDACB             -> ACB
         USING IFGACB,R8
         SPACE 1
         LA    R10,4
         LCR   R10,R10
         L     R10,0(R10,R8)          FETCH ACB PREFIX
         USING #WORK,R10
         SPACE 1
         LA    R13,SAVE2              SET UP NEW SAVE AREA
         ST    R14,SAVE2              AND SAVE RETURN ADDRESS
         SPACE 1
         CHECK RPL=(R6)
         SPACE 1
         TM    STOPECB,X'40'          SHUTDOWN IN PROGRESS?
         BO    F2EX2END                 YES, ALL DONE
         SPACE 1
         LR    R4,R15                 SAVE RETURN CODE
         LR    R5,R0                   AND REASON CODE
         SPACE 1
         LA    R3,RECVBUFF
         SHOWCB AM=VTAM,RPL=(R6),FIELDS=(AREA,USER,ARG,RECLEN),        X
               AREA=(R3),LENGTH=LRECVBUF,MF=(G,WORKAREA)
         CHKERR SHOW-RPL
         L     R7,RECVARG
         SPACE 1
         ICM   R1,B'1111',RECVUSER    -> TABLE ENTRY
         DC    X'4780',S(*+1)           UNKNOWN TERMINAL
         ST    R1,MSGTERM             remember terminal table addr   JW
         SPACE 1
         LR    R0,R5                  RESTORE REASON CODE
         LTR   R15,R4                 WAS RECEIVE OK?
         DC    X'4770',S(*+1)           NO, FAIL S0C6
         SPACE 1
         TESTCB AM=VTAM,RPL=(R6),RESPOND=NFME,ERET=F2EXABND,           X
               MF=(G,WORKAREA)
         BE    F2EXCHKR
         SPACE 1
         GETRPL ,
         L     R0,=A(FREPLXIT)
         SEND  RPL=(1),STYPE=RESP,RESPOND=(EX,FME,NRRN),OPTCD=ASY,     X
               ARG=(R7),EXIT=(R0)
         CHKERR SEND
         SPACE 1
F2EXCHKR DS    0H
         SPACE 1                                                     JW
         MVC   WORKAREA(LF2DONE),F2DONE get message                  JW
         XC    MSGTXTLN,MSGTXTLN      clear length of message text   JW
         MVI   MSGTXTLN+1,LF2DONE     store length of message text   JW
         MAKEMSG                      add terminal specific data     JW
         MODCB RPL=(R6),RECLEN=(*,MSGLEN),AREA=(*,MSGADD),             X
               MF=(G,MODCBWRK)        complete the RPL               JW
         SPACE 1                                                     JW
         L     R0,=A(FREPLXIT)
         SEND  RPL=(R6),STYPE=REQ,CONTROL=DATA,OPTCD=(ASY,CA),         X
               RESPOND=(EX,FME,NRRN),                                  X
               POST=SCHED,BRACKET=(NBB,NEB),ARG=(R7),EXIT=(R0),        X
               CHNGDIR=(CMD)
         CHKERR SEND
         SPACE 1
         MVC   OSNAPDCB(LSNAPDCB),ISNAPDCB
         MVI   OPENSNAP,X'80'
         OPEN  (OSNAPDCB,OUTPUT),MF=(E,OPENLIST)
         SPACE 1
         TM    OSNAPDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BZ    F2EXFBUF
         SPACE 1
         L     R2,SNAPCNT
         LA    R2,1(,R2)
         ST    R2,SNAPCNT
         SPACE 1
         L     R3,RECVAREA
         L     R4,RECVARLN
         AR    R4,R3
         BCTR  R4,0
         SPACE 1
         MVC   OSNAP(LSNAP),ISNAP
         SNAP  DCB=OSNAPDCB,ID=(R2),STORAGE=((R3),(R4)),MF=(E,OSNAP)
         SPACE 1
         CLOSE (OSNAPDCB),MF=(E,OPENSNAP)
         SPACE 1
F2EXFBUF DS    0H
         LA    R1,4
         LCR   R1,R1
         AR    R1,R3
         L     R0,0(,R1)
         FREEMAIN RU,LV=(0),A=(1),SP=34
         SPACE 1
F2EX2END DS    0H
         L     R14,SAVE2
         XR    R15,R15
         BR    R14
         SPACE 2
F2EXABND DS    0H
         DC    X'47F0',S(*+1)
         SPACE 2
ISNAPDCB DCB   DSORG=PS,MACRF=(W),RECFM=VBA,BLKSIZE=882,LRECL=125,     X
               DDNAME=SNAPFILE
LSNAPDCB EQU   *-ISNAPDCB
         SPACE 2
ISNAP    SNAP  DCB=1-1,STORAGE=(2-2,3-3),MF=L
LSNAP    EQU   *-ISNAP
         SPACE 2
         LTORG ,
         SPACE 2
F2DONE   DC    C'x''F2'' processing complete'                        JW
LF2DONE  EQU   *-F2DONE
         EJECT ,
CIDENTRY DSECT ,
         SPACE 1
CIDCID   DS    F                   CID VALUE
         SPACE 1
CIDNAME  DS    CL8                 NAME OF LTERM
         SPACE 1
CIDROWS  DS    H                   NUMBER OF ROWS
CIDCOLS  DS    H                   NUMBER OF COLUMNS
         SPACE 1                                                     JW
CIDROW2  DS    H                   3270 address of 2nd row on screen JW
CIDCAT1  DS    H                   3270 address of 1st cat row       JW
CIDCAT2  DS    H                   3270 address of 2nd cat row       JW
CIDCAT3  DS    H                   3270 address of 3rd cat row       JW
CIDCAT4  DS    H                   3270 address of 4th cat row       JW
         SPACE 1
CIDAPPLN DS    CL8                 APPLID INQUIRED ABOUT
         SPACE 1
CIDPENDM DS    A                   -> PENDING MESSAGE
         SPACE 1
CIDBIND  DS    6D                  BIND INFO AREA
         SPACE 1
         DS    0D                  ALIGNMENT
         SPACE 1
CIDLENTH EQU   *-CIDENTRY          ENTRY LENGTH
         SPACE 3
CLOSENTY DSECT ,
         SPACE 1
CLOSFLAG DS    X                   SEEN FLAG
         DS    CL3                 ..RESERVED
         SPACE 1
CLOSECID DS    F                   CID OF TERMINAL TO CLSDST
         SPACE 1
CLOSLNTH EQU   *-CLOSENTY          LENGTH OF ENTRY
         EJECT ,
#WORK    DSECT ,
         SPACE 1
         DS    18F
         SPACE 1
SAVE2    DS    18F
         SPACE 1
SAVE3    DS    18F
         SPACE 1
APPLID   DS    CL8
         SPACE 1
REGSAVE  DS    3F
         SPACE 1
NIBADDR  DS    A
         SPACE 1
ACBADDR  DS    2F
         SPACE 1
ACBLENTH DS    F
RPLLENTH DS    F
NIBLENTH DS    F
         SPACE 1
MAINECBS DS    2F                                                    JW
STOPECB  DS    F
CLOSECB  DS    F                                                     JW
         SPACE 1
OPENLIST DS    F
         SPACE 2
OTIMRCOD DS    ((LTIMRCOD+3)/4)F
         SPACE 1
RECVBUFF DS    0F
RECVAREA DS    A
RECVUSER DS    A
RECVARG  DS    A
RECVARLN DS    F
LRECVBUF EQU   *-RECVBUFF
         SPACE 2
CARD     DS    CL256                                                 JW
CARD1    DS    CL256                                                 JW
CARD2    DS    CL256                                                 JW
         SPACE 2
RECVRPL1 DS    A
RECVRPL2 DS    A
MODELRPL DS    A
         SPACE 1
TEXTSTRT DS    F
         SPACE 1
RPLQUEUE DS    A
RPLREQST DS    F
RPLTOTAL DS    F
         SPACE 2
NUMCIDS  EQU   3
CIDLIST  DS    ((CIDLENTH*NUMCIDS)/8)D   CID TABLE AREA
FAILOGON DS    0D                  CIDENTRY for failed logon         JW
         ORG   *+CIDLENTH                                            JW
         SPACE 1
DWORD    DS    D
         SPACE 1
NUMCLOSE EQU   2*NUMCIDS
PENDCLOS DS    ((CLOSLNTH*NUMCLOSE)/4)F  PENDING CLSDST TABLE
         SPACE 1
MSGSAVE  DS    3F                  mini save area to call MAKEMSG    JW
MSGADD   DS    F                   address and length of message ..  JW
MSGLEN   DS    F                    .. to be sent to terminal        JW
MSGTERM  DS    F                   address of terminal table entry   JW
MSGTXTLN DS    H                   length of message text            JW
NLONLY   DS    H                   lng of TTY suffix w/o leading NL  JW
SUFFTTY  DS    0C                  TTY suffix for messages (prompt)  JW
         ORG   *+STTY              length of TTY suffix datastream   JW
         DS    0D                  align                             JW
         DS    XL6                 filler                            JW
PRFX3270 DS    0X                  3270 prefix datastream            JW
         ORG   PRFX3270+THECATR1   skip                              JW
CAT1     DS    XL2                 buffer address of 1st cat row     JW
         ORG   PRFX3270+THECATR2   skip                              JW
CAT2     DS    XL2                 buffer address of 2nd cat row     JW
         ORG   PRFX3270+THECATR3   skip                              JW
CAT3     DS    XL2                 buffer address of 3rd cat row     JW
         ORG   PRFX3270+THECATR4   skip                              JW
CAT4     DS    XL2                 buffer address of 4th cat row     JW
         ORG   PRFX3270+SCREENR2   skip                              JW
ROW2     DS    XL2                 buffer address of 2nd row on scrn JW
         ORG   PRFX3270+P3270      skip to end of prefix datastream  JW
         DS    0D                                                    JW
         ORG   ,                                                     JW
WORKAREA DS    32D                                                   JW
WORKLEN  EQU   *-WORKAREA
MODCBWRK DS    20D                                                   JW
         SPACE 2
OSNAPDCB DCB   DSORG=PS,MACRF=(W),RECFM=VBA,BLKSIZE=882,LRECL=125,     X
               DDNAME=SNAPFILE
         SPACE 2
OSNAP    SNAP  DCB=OSNAPDCB,STORAGE=(2-2,3-3),MF=L
         SPACE 1
OPENSNAP OPEN  (OSNAPDCB,OUTPUT),MF=L
         SPACE 1
SNAPCNT  DS    F
         SPACE 3
#AMT     EQU   *-#WORK
         EJECT ,
         PRINT NOGEN
         SPACE 2
         IFGACB AM=VTAM
         SPACE 2
         IFGRPL AM=VTAM
         SPACE 2
         ISTDNIB ,
         SPACE 2
         ISTDBIND ,
*  EXTRA DEFINITIONS FOR THE FREE VTAM OF MVS 3.8 FOLLOW TO GIVE A
*  CORRECT ASSEMBLE.  DELETE THESE IF USING A "MODERN" ISTDBIND MACRO.
         ORG   BINPSCHR
BINDFLAG DS    XL1                 DEVICE FLAG               @D14AKMC
BINSEDS  EQU   X'80'               EXTENDED 3270 DATA STREAM @D14AKMC
         DS    XL4                 RESERVED                  @D14AKMC
BINSCRSZ DS    0XL5                PRESENTATION SPACE SIZE   @ZA45868
BINSPRIR DS    FL1                 PRIMARY (DEFAULT) NUMBER
*                                  OF ROWS
BINSPRIC DS    FL1                 PRIMARY (DEFAULT) NUMBER
*                                  OF COLUMNS
BINSALTR DS    FL1                 ALTERNATE NUMBER OF ROWS
BINSALTC DS    FL1                 ALTERNATE NUMBER OF COLUMNS
*  VALUES FOR BINPRESZ (PRESENTATION SPACE SIZE)
BINPSZRC EQU   X'7F'               PRESENTATION SPACE HAS
*                                  DEFAULT AND ALTERNATE
*                                  SIZES AS DEFINED IN
*                                  DEFAULT, ALTERNATE
*                                  ROW/COL FIELDS
BINPSFX  EQU   X'7E'               PRESENTATION SPACE IS
*                                  FIXED SIZE AS DEFINED BY
*                                  ROW/COL VALUES IN
*                                  DEFAULT ROW/COL FIELDS
BINPSZ3  EQU   X'03'               24X80 DEFAULT UNDEFINED ALTERNATE
*                                  DO WRITE STRUCTURED FIELD QUERY
*                                  TO IDENTIFY ALTERNATE     @R505701
*  END OF MVS 3.8 ISTDBIND ADDITIONS.
         SPACE 2
         DCBD  DSORG=BS
         SPACE 3
         END
