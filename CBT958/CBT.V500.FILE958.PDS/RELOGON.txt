//SBGOLOBU JOB ,'SAM GOLOB',CLASS=B,
//    MSGCLASS=X,NOTIFY=&SYSUID
//*
//*     ASM LINKEDIT FROM SOURCE ASM
//*
//ASMH EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD)
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,20)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=SYSDA,SPACE=(TRK,30),DISP=(,PASS),DCB=BLKSIZE=3200,
//            DSN=&&TEMP
//SYSIN    DD *
    TITLE 'RELOGON - RE-LOGON TO YOUR RUNNING TSO ID '
* ------------------------------------------------------------------- *
*      RELOGON  -  LOGOFF AND LOGON AGAIN TO YOUR TSO USERID          *
*                                                                     *
*   Author:   Jeffrey R. Broido                                       *
*                                                                     *
*   Reconstituted from a disassembly and fixed by:  Sam Golob         *
*                                                                     *
*   Not re-entrant.                                                   *
*                                                                     *
*   This program has been fixed for 8-character userids.              *
* ------------------------------------------------------------------- *
*   The BYE command is more general than this command.                *
* ------------------------------------------------------------------- *
*   See note about the TSB password below.... instructive.            *
* ------------------------------------------------------------------- *
*   The original intent of this program was to put the statement:     *
*                                                                     *
*   LOGON userid/password                                             *
*                                                                     *
*   into the RELOGON buffer, pointed to by the PSCBRLGB field of      *
*   the PSCB.  Also, the RELOGON switch X'10' in the ECTSWS field     *
*   of the ECT is set.                                                *
*                                                                     *
*   This has the effect that when you get out of ISPF, you            *
*   immediately LOGOFF, and execute the instructions in the RELOGON   *
*   buffer, which is LOGON userid/password .                          *
*                                                                     *
*   This is an old program, and the following is true:                *
*   The "password" part of this, only works if the userid was         *
*   defined by UADS.  When the userid was defined by RACF, this       *
*   password field is blank.  In that case, the LOGON command, set    *
*   up by RELOGON, is:                                                *
*                                                                     *
*   LOGON userid                                                      *
*                                                                     *
*   So you have to enter the password again, when you re-LOGON.       *
*   Sorry about that!  (The BYE command is more general, and it       *
*   has more power than this command.)                                *
*                                                                     *
*   If you executed this program when in ISPF, and you haven't        *
*   been logged off yet, you can undo the forced LOGOFF by using      *
*   the non-APF-authorized TSO command called NOLOGOF, which          *
*   (I hope) is included in this pds.      (S.Golob - July 2017)      *
* ------------------------------------------------------------------- *
RELOGON  CSECT
RELOGON  RMODE 24
RELOGON  AMODE 24
         USING *,R15
         B     STARTPG                   Eyecatcher
         DC    CL10'RELOGON - '
         DC    C'&SYSDATE'
         DC    C', '
         DC    C'&SYSTIME'
STARTPG  DS    0H
         DROP  R15
         USING RELOGON,R12
         STM   R14,R12,12(R13)           Save regs
         LR    R12,R15                   Load base
         LR    R14,R13                   Caller save area
         LA    R13,SAVEAREA              Save area address
         ST    R14,SAVEAREA+4            Store old save
         ST    R13,8(,R13)               Store new save
         L     R7,12(,R1)                ECT from CPPL
         L     R3,16                     CVT address
         L     R3,0(,R3)                 TCB words
         L     R2,12(,R3)                ASCB <- R2
         L     R3,0(,R3)                 TCB
         L     R3,180(,R3)               JSCB <- R3
         TM    236(R3),X'01'             JSCBAUTH on?
         BO    RELCONT                   Yes. Continue.
         TPUT  MESSAGAP,L'MESSAGAP       No. Msg and get out.
RELEXIT8 LA    R15,8                     Condition code
RELEXIT  DS    0H
         L     R13,SAVEAREA+4            Old save area
         LM    R0,R12,20(R13)            Restore regs
         L     R14,12(,R13)
         BR    R14                       Exit
*
RELCONT  DS    0H                        Authorized. Continue.
         L     R4,264(,R3)               PSCB from JSCB
         L     R6,60(,R2)                TSB from ASCB
         LTR   R6,R6                     Is there a TSB?
         BZ    RELEXIT8                  No. Get out.
         MODESET KEY=ZERO
*                                        Form the LOGON command
         L     R5,48(,R4)                RELOGON buffer from PSCB
         MVI   0(R5),X'00'               Maximum length for
         MVI   1(R5),X'1B'                LOGON userid/password
         MVI   4(R5),X'40'               Clear data part of the
         MVC   5(251,R5),4(R5)             Relogon Buffer
         MVC   4(6,R5),LOGONX            MOVE "LOGON " CMD IN
         CLC   =C'>7BYTES',0(R4)         Is userid 8 bytes?
         BE    USER8BY                   Yes, get it from +X'54'
         MVC   10(7,R5),0(R4)            Move in our Userid
         B     REGUSID
*
USER8BY  DS    0H                        DO IT WITH 8-CHAR USERID
         MVC   10(8,R5),X'54'(R4)        8-char id to buffer
         CLC   X'58'(8,R6),BLANKS        Is there a TSB password?
         BE    NOSLASH                   No. Don't put slash either.
         MVI   17(R5),X'61'              Move in SLASH
         MVC   18(8,R5),X'58'(R6)        Move TSB PASSWORD (see below)
         B     NOSLASH
REGUSID  DS    0H
         CLC   X'58'(8,R6),BLANKS        Is there a TSB password?
         BE    NOSLASH                   No. Don't put slash either.
         MVI   17(R5),X'61'              MOVE IN SLASH
         MVC   18(8,R5),X'58'(R6)        TSB PASSWORD (see below)
NOSLASH  DS    0H
* ------------------------------------------------------------------- *
*     About the TSB password:  It is:  Blanks if the userid           *
*     is RACF-defined, and you'll have to re-enter it to LOGON.       *
*     But if the id is UADS-defined, then the password is inserted    *
*      automatically and is meaningful, which was the original        *
*      purpose of this command--that you shouldn't have to enter      *
*      the password again, in order to LOGON.                         *
* ------------------------------------------------------------------- *
         MODESET KEY=NZERO
         OI    28(R7),X'10'              ECTSWS/INDICATE LOGOFF
         SR    R15,R15                   RC = 0
         B     RELEXIT                   Normal program end.
         SPACE
* ------------------------------------------------------------------- *
*                     C  O  N  S  T  A  N  T  S                       *
* ------------------------------------------------------------------- *
SAVEAREA DS    9D                        Save Area
LOGONX   DC    CL6'LOGON '
MESSAGAP DC    C'THIS COMMAND NEEDS APF AUTHORIZATION ...'
BLANKS   DC    CL16' '
         LTORG
         YREGS
         END
/*
//LKED   EXEC  PGM=IEWL,
//       PARM='NCAL,MAP,LIST,LET'
//SYSUT1   DD  UNIT=SYSDA,SPACE=(2048,(200,20))
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DISP=SHR,DSN=SYS1.W$$.LINKLIB
//SYSLMOD  DD  DISP=SHR,DSN=SYS1.W$$.LINKLIB
//SYSLIN   DD  DSN=&&TEMP,DISP=(OLD,PASS)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
 ENTRY   RELOGON
 SETCODE AC(1)
 SETSSI  CB495731
 NAME    RELOGON(R)
