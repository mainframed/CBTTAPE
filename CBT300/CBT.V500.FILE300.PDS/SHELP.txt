 TITLE 'S H E L P   ---   S U B C O M M A N D   H E L P'
*        |     |                        |                             |
SHELP    CSECT
         SPACE
         USING SHELP,R12
         USING CPPL,R3
         USING CBUF,R4
         USING ECT,R7
         USING XCTLWRK,R13
         SPACE
         SAVE  (14,12),,SHELP-&SYSTIME-&SYSDATE
         SPACE
         LR    R12,R15
         LR    R3,R1
         LM    R4,R7,CPPL
         SPACE
         LH    R15,CBLEN
         LH    R14,CBOFF
         LA    R8,CBTXT(R14)
         LA    R1,0(R4,R15)
         LR    R11,R1
         SLR   R11,R8
         BCTR  R11,0
         EX    R11,TRT
         BALR  R0,0
         SPACE
         LR    R10,R1
         SR    R10,R8
         BNP   RETURN
         BCTR  R10,0
         MVC   ECTPCMD,BLANKS
         MVC   ECTPCMD(*-*),0(R8)
         EX    R10,*-6
         TR    ECTPCMD,UPPER
         EJECT
         LA    R14,2(R10,R14)
         STH   R14,CBOFF
         MVC   0(*-*,R8),BLANKS
         EX    R10,*-6
         MVI   0(R1),C' '
         MVI   ECTSCMD,C'H'
         SPACE
         LM    R14,R11,XRSA
         DROP  R3,R4,R7
         MVC   XCTLNAME,IKJHELP
         SPACE
         XCTL  (12),EPLOC=XCTLNAME,SF=(E,XCTL),MF=(E,(1)),DCB=0
         SPACE 2
RETURN   RETURN (14,12),RC=4
         EJECT
BLANKS   DC    CL8' '
IKJHELP  DC    CL8'HELP'
TRT      TRT   0(*-*,R8),TRTABLE
TRTABLE  DC    256AL1(0)
         ORG   TRTABLE+64
         DC    C' '
         ORG   TRTABLE+107
         DC    C','
         ORG   TRTABLE+94
         DC    C';'
         ORG   TRTABLE+256
UPPER    DC    256AL1(*-UPPER)
         ORG   UPPER+129
         DC    C'ABCDEFGHI'
         ORG   UPPER+145        |
         DC    C'JKLMNOPQR'       T
         ORG   UPPER+162
         DC    C'STUVWXYZ'
         ORG   UPPER+256
         EJECT
XCTLWRK  DSECT
         DS    F,A,A
XRSA     DS    A
XCTLNAME DS    CL8
XCTL     XCTL  SF=L
         SPACE 2
CBUF     DSECT
CBLEN    DS    H
CBOFF    DS    H
CBTXT    DS    0CL256
         SPACE 2
         JVDEQU
         EJECT
         IKJCPPL
         EJECT
         IKJECT
         SPACE 2
         END   SHELP
