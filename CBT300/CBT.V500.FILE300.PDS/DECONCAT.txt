DCAT TITLE 'NASA/GSFC -- DECONCAT (TSO) COMMAND PROCESSOR'
*---------------------------------------------------------------------*
** GSFTSODC,  VERSION 1.0,  GENE CZARCINSKI,  09JUN73                **
** DECONCAT, VERSION 1.1, GENE CZARCINSKI, 10APR77                   **
**       . RENAME CSECT FROM 'GSFTSODC' TO 'DECONCAT'                **
**         (MORE CONSISTANCY AND CLEANER CMDLIB)                     **
**                                                                   **
** ATTR - RENT                                                       **
**                                                                   **
** ALIAS -- DECAT                                                    **
**                                                                   **
** THIS ROUTINE IS A TSO COMMAND PROCESSOR DESIGNED TO PERFORMED     **
** DECONCATENATION OF DYNAMICALLY CONCATENTATED FILES.  'IKJDAIR' IS **
** USED TO PERFORM ACTUAL DECONCATENATION.  TO EXECUTE THIS CP, ONLY **
** THE PRIME FILENAME/DDNAME CAN AND SHOULD BE SPECIFIED.  ONCE      **
** DECONCATENATED, THE ORIGINAL FILENAMES ARE RESTORED AND AGAIN     **
** AVAILABLE.                                                        **
**                                                                   **
** CHNGE LOG: 13JUN91-DECIDED TO CLEAN UP CODE AND GET UNIQUE MACROS **
**            OUT OF THE CODE IN PREP FOR ESA. JIM MARSHALL      JDM1**
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0                                                 JDM1
R1       EQU   1                                                 JDM1
R2       EQU   2                                                 JDM1
R3       EQU   3                                                 JDM1
R4       EQU   4                                                 JDM1
R5       EQU   5                                                 JDM1
R6       EQU   6                                                 JDM1
R7       EQU   7                                                 JDM1
R8       EQU   8                                                 JDM1
R9       EQU   9                                                 JDM1
R10      EQU   10                                                JDM1
R11      EQU   11                                                JDM1
R12      EQU   12                                                JDM1
R13      EQU   13                                                JDM1
R14      EQU   14                                                JDM1
R15      EQU   15                                                JDM1
SP000    EQU   0                                                 JDM1
         SPACE 1                                                 JDM1
DECONCAT AMODE 31                                                JDM1
DECONCAT RMODE ANY                                               JDM1
DECONCAT CSECT
         SAVE  (14,12),,DECONCAT_&SYSDATE                        JDM1
         LR    R12,R15             GET BASE ADDR                 JDM1
         USING DECONCAT,R12                                      JDM1
         LR    R9,R1               SAVE R1                       JDM1
         USING CPPL,R9             GET ADDRESSABILITY            JDM1
         GETMAIN RU,LV=@SIZE,SP=SP000,LOC=ANY                    JDM1
         ST    R13,4(,R1)                                        JDM1
         ST    R1,8(,R13)                                        JDM1
         LR    R13,R1                                            JDM1
         LR    R11,R1                                            JDM1
         USING @STORAGE,R11                                      JDM1
         SPACE 1                                                 JDM1
         XC        ECB,ECB
         XC        RCODE,RCODE
**
         LA        R1,PL1              INIT FOR TERM I/O
         USING     IOPL,R1
         MVC       IOPLUPT,CPPLUPT
         MVC       IOPLECT,CPPLECT
         LA        R0,ECB
         ST        R0,IOPLECB
**
         LA        R1,PL2              INIT FOR COMMAND PARSE
         USING     PPL,R1
         MVC       PPLUPT,CPPLUPT
         MVC       PPLECT,CPPLECT
         LA        R0,ECB
         ST        R0,PPLECB
         MVC       PPLPCL,=A($PARSEDC)
         LA        R0,APDL
         ST        R0,PPLANS
         MVC       PPLCBUF,CPPLCBUF
         XC        PPLUWA,PPLUWA
**
         LA        R1,PL3              INIT PL FOR DAIR
         USING     DAPL,R1
         MVC       DAPLUPT,CPPLUPT
         MVC       DAPLECT,CPPLECT
         MVC       DAPLPSCB,CPPLPSCB
         LA        R0,ECB
         ST        R0,ECB
         LA        R5,PL4              A(DAPB)
         ST        R5,DAPLDAPB
         DROP      R1
**
         USING     DAPB10,R5
         XC        PL4(16),PL4
         MVI       DA10CD+1,X'10'      FOR DECONCATENATE
**
         SPACE
** PARSE
         LA        R1,PL2
         LINK      EP=IKJPARS
         LTR       R15,R15             PARSE OK?
         BZ        PARSE1              YES.
PARSE0   LA        R2,MSG0             BAD PARSE..ISSUE MSG & ABORT
         B         TPUT
**
PARSEMVC MVC       0(0,R14),0(R15)
**
PARSE1   L         R2,APDL             GOOD PARSE
         USING     @PDL,R2
         MVI       DA10DDN,C' '        CLEAR DDNAME AREA
         MVC       DA10DDN+1(7),DA10DDN
         L         R15,#NAME           PNTR TO DDNAME
         LTR       R15,R15             SPECIFIED?
         BZ        PARSE0              NO, BAD
         LH        R1,#NAME+4          LENGTH OF DDNAME
         LTR       R1,R1
         BZ        PARSE0
         LA        R14,DA10DDN
         BCTR      R1,0
         EX        R1,PARSEMVC
                   SPACE
** INVOKE DAIR TO DECONCATENATE
         LA        R1,PL3
         LINK      EP=IKJDAIR
         LTR       R15,R15             OK?
         BZ        DONE                RC=0, YES.
         CVD       R15,WORK            RCÂ¬=0, BAD
         OI        WORK+7,X'0F'
         MVC       MSGA(MSG1L),MSG1
         UNPK      MSGA+(MSG1A-MSG1)(2),WORK
         UNPK      WORK(5),DA10DARC(3)
         MVZ       WORK(4),=XL4'0'
         TR        WORK(4),=C'0123456789ABCDEF'
         MVC       MSGA+(MSG1B-MSG1)(4),WORK
         LA        R2,MSGA
         B         TPUT
         SPACE
** OUTPUT ERROR MSG
TPUT     PUTLINE   PARM=PTPB,OUTPUT=(0(R2),TERM,SINGLE,DATA),          +
               TERMPUT=(EDIT,WAIT,NOHOLD),MF=(E,PL1)
         MVI       RCODE+1,12
         SPACE
**
DONE     CLI       PL2+(PPLANS-PPL),X'FF'
         BE        DONE2
         L         R1,PL2+(PPLANS-PPL)
         IKJRLSA   (1)
**
**
DONE2    CLI       RCODE+1,12
         BNE       DONE3
         STACK     PARM=STPB,DELETE=ALL,MF=(E,PL1)
         TCLEARQ   INPUT
         SPACE
DONE3    LH        R2,RCODE
         LR        R1,R13                                        JDM1
         L         R13,4(R13)                                    JDM1
         ST        R2,16(,R13)     STORE RC                      JDM1
         FREEMAIN  RU,LV=@SIZE,A=(R1),SP=SP000                   JDM1
         RETURN    (14,12)                                       JDM1
         SPACE
** CONSTANTS
         DC        0D'0'
         LTORG
MSG0     DC        0F'0',Y(MSG0L,0),C'**ERROR PARSING COMMAND '
         DC        C'OPERAND,  EXECUTION SUPPRESSED.'
MSG0L    EQU       *-MSG0
MSG1     DC        Y(MSG1L,0),C'**DECONCATENATION ERROR, DAIR RC='
MSG1A    DC        C'00',C', DARC='
MSG1B    DC        CL4' '
MSG1L    EQU       *-MSG1
**
**
         PRINT     NOGEN
**
$PARSEDC IKJPARM   DSECT=@PDL
#NAME    IKJIDENT  'FILENAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,    +
               PROMPT='FILENAME/DDNAME FOR DECONCATENATION'
         IKJENDP
**
         PRINT     GEN
         SPACE
@STORAGE DSECT
SAVE     DC        18F'0'                                        JDM1
WORK     DC        D'0'
ECB      DC        F'0'
APDL     DC        A(0)
PTPB     PUTLINE   MF=L
STPB     STACK     MF=L
PL1      DC        7F'0'               IOPL
PL2      DC        7F'0'               PPL
PL3      DC        5F'0'               DAPL
PL4      DC        4F'0'               DAPB10
**
MSGA     DC        0F'0',Y(0,0),CL60' '
RCODE    DC        Y(0)
@SIZE    EQU       *-@STORAGE
         SPACE
         IKJCPPL
         IKJIOPL
         IKJPPL
         IKJDAPL
         IKJDAP10
         IKJPSCB
         SPACE 3
         END   DECONCAT
