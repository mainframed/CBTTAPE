         TITLE 'NEWSPACE - EASY ALLOCATION OF DATASETS'
**********************************************************************
***                                                                ***
***                NEWSPACE COMMAND                                ***
***                                                                ***
***  DESCRIPTION:                                                  ***
***    THE FUNCTION OF THIS COMMAND IS TO PROVIDE AN EASY WAY FOR  ***
***    NEW TSO USERS TO CREATE NEW SEQUENTIAL AND PARTITIONED      ***
***    DATASETS.  THIS COMMAND IS MUCH EASIER TO USE THAN          ***
***    ISPF/PDF OPTION 3.2, OR THE ALLOCATE AND ATTRIB COMMANDS.   ***
***    HOWEVER, IT IS NOT AS VERSITILE AS THOSE COMMANDS.          ***
***                                                                ***
***  FORMAT:                                                       ***
***          NEWSPACE  DSNAME ORG TYPE FORMAT DDNAME(NAME)  CAH TS04204
***                                             FILE(NAME)  CAH TS04204
***                                                                ***
***      REQUIRED: DSNAME                                          ***
***                                                                ***
***  FUNCTION:                                                     ***
***                                                                ***
***  1. NEWSPACE FIRST PARSES THE PARM STRING, THEN DETERMINES     ***
***     THE LENGTH AND TEXT OF THE LOW-LEVEL QUALIFIER OF THE      ***
***     DSNAME.                                                    ***
***                                                                ***
***  2. A TABLE IS THEN USED.  THE ENTRY IN THE TABLE IS LOCATED   ***
***     ON THE BASIS OF THE 'TYPE' OPERAND.  IF 'TYPE' IS NOT      ***
***     USED, THE ENTRY IS LOCATED BY USE OF THE LOW-LEVEL         ***
***     QUALIFIER.                                                 ***
***                                                                ***
***  3. EACH DATA TYPE ENTRY IS FOLLOWED BY ONE OR MORE SETS OF    ***
***     DCB INFORMATION (SUBENTRY).  IF THE 'FORMAT' OPTION HAS    ***
***     BEEN USED, THAT SUBENTRY IS SEARCHED FOR.  IF NOT, THE     ***
***     FIRST SUBENTRY IN THE LIST IS USED AS A DEFAULT.  THE      ***
***     TABLE IS AT THE 'DATATBLE' LABEL IN THE SOURCE CODE.       ***
***                                                                ***
***  4. NEWSPACE THEN ALTERS THE SVC-99 DYNAMIC ALLOCATION TEXT    ***
***     UNITS CREATED BY THE GTEDADAT MACRO.  THE ALTERATION FILLS ***
***     IN THE DATASET NAME AND DCB INFORMATION.  IF A FILE CAH TS04204
***     NAME HAS BEEN SELECTED, IT IS FILLED INTO THE TEXT  CAH TS04204
***     UNIT, ELSE THE KEY IS SET TO X'0000'.  THE TYPE IS  CAH TS04204
***     APPENDED ONTO THE END OF THE DATASET NAME, UNLESS QUOTES   ***
***     HAVE BEEN USED AROUND THE NAME, OR THE TYPE OPERAND HAS    ***
***     NOT BEEN USED.  THE DATASET IS THEN DYNAMICALLY ALLOCATED. ***
***     THE GTEDAALC MACRO USES DAIRFAIL TO PRODUCE DYNAMIC        ***
***     ALLOCATION ERROR MESSAGES.                                 ***
***                                                                ***
***  5. NEWSPACE THEN CLEANS UP AND TERMINATES.                    ***
***                                                                ***
***  6. ERROR/INFO MESSAGES ARE HANDLED BY THE ERRMSG ROUTINE.     ***
***     MOST MESSAGES ARE A SINGLE SEGMENT.  MESSAGE #3, THE       ***
***     'DATASET CREATED' MESSAGE HAS THREE SEGMENTS WHICH ARE     ***
***     GLUED TOGETHER BY PUTLINE BEFORE BEING WRITTEN TO THE      ***
***     TERMINAL.                                                  ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  RETURN CODES:                                                 ***
***     00 - OKAY                                                  ***
***     12 - SOMETHING WAS WRONG.  SHOULD ALSO HAVE AN ERROR MSG.  ***
***                                                                ***
***  REGISTER USAGE:                                               ***
***     R0-R3   - USED BY MACROS                                   ***
***     R4-R7   - WORK REGISTERS, NOT USED TO PASS INFORMATION     ***
***               BETWEEN ROUTINES.                                ***
***     R8      - UNUSED                                           ***
***     R9      - BASE OF PARM DESCRIPTOR LIST (PDL) RETURNED BY   ***
***               IKJPARS PARSE SERVICE ROUTINE                    ***
***     R10     - LINKAGE REGISTER USED WITH BAL                   ***
***     R11     - BASE OF WORK AREA OBTAINED WITH GETMAIN          ***
***     R12     - BASE REGISTER OF PROGRAM CODE.                   ***
***     R13-R15 - LINKAGE REGISTERS                                ***
***                                                                ***
***                                                                ***
***  NON-IBM MACROS USED:                                          ***
***     LINKSAVE - LINKAGE CONVENTIONS IN                          ***
***     EQ$R     - REGISTER EQUATES                                ***
***     LINKBACK - LINKAGE CONVENTIONS BACK                        ***
***     GTEDADAT - CREATE SVC-99 AND IKJEFF18 CONTROL BLOCKS       ***
***     GTEDASET - INITIALIZE AND LINK SVC-99 AND IKJEFF18 CONTROL ***
***                BLOCKS                                          ***
***     GTEDAALC - PERFORM DYNAMIC ALLOCATION AND DAIRFAIL         ***
***                ANALYSIS OF FAILURES                            ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  RENT, REUS, ALIAS(NEW)               ***
***                                                                ***
***  CULPRIT:                                                      ***
***     CHUCK HOFFMAN, SYSTEMS PROGRAMMING                         ***
***     GTE LABORATORIES TECHNICAL COMPUTING CENTER                ***
***     WALTHAM, MASSACHUSETTS  617/466-2131    V/N 466-2131       ***
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***     1.  A. CHANGE CLIST AND COMMAND, FIXED, BLKSIZE     CAH TS04202
***            FROM 6160 TO 1680.                           CAH TS04202
***         B. CREATE DATA TYPE 'DIALOG'                    CAH TS04202
***         C. ADD 'EJECT' AFTER THIS BOX.                  CAH TS04202
***     2.  A. CHANGE LRECL TO 133 FOR DEFAULT FOR LIST.    CAH TS04203
***         B. ADD OPTIONAL RECFM FIXED FOR LIST.           CAH TS04203
***         C. ADD OPTIONAL RECFM UNFORMATTED FOR DIALOG.   CAH TS04203
***     3.  A. ADD DATA TYPE 'C'.                           CAH TS04204
***         B. ADD DSECT=YES TO CVT EXPANSION.              CAH TS04204
***         C. ADD FILE/DDNAME OPERAND.                     CAH TS04204
***         D. SET CC=12 IF FAILURE TO ALLOCATE             CAH TS04204
***                                                                ***
**********************************************************************
         EJECT                                              CAH TS04202
         PRINT NOGEN
         MACRO
&LABEL   HIREP &WORD=,&CODE=,&REG=15    SET HIGHER CODE
&LABEL   LA    &REG,&CODE.(0,0)         NEW CODE TO REG
         C     &REG,&WORD               IF EXISTING CODE HIGHER
         BNH   *+8                        BRANCH AROUND
         ST    &REG,&WORD               ELSE SAVE NEW CODE
         CNOP  0,4                      ALIGN
         MEND
         EJECT
**********************************************************************
***                                                                ***
***   PROLOGUE                                                     ***
***                                                                ***
**********************************************************************
*
NEWSPACE LINKSAVE BASE=12,GETMAIN=YES
         EQ$R
         USING WORK0,R13                ADDRESSABILITY OF WORK0
         USING PARMPDL,R9               ADDRESSABILITY OF PRM DESC LST
         ST    R1,CPPLPTR               SAVE CPPL POINTER
         GETMAIN  R,LV=WORK1LEN         GET MAIN STORAGE
         LR    R11,R1                   ADDRESS OF MAIN STORAGE
         USING WORK1,R11                ADDRESSABILITY OF WORK1
         XC    COMPCODE,COMPCODE        CLEAR PROGRAM RETURN CODE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
*
         BAL   R10,PPLSETUP             SET UP PARSE PARM LIST
         BAL   R10,PARSE                PARSE THE INPUT PARAMETERS
         BAL   R10,LOLEVEL              FIND LOW LEVEL OF NAME
         L     R4,RETCDE                IF RETURN CODE NOT GOOD
         LTR   R4,R4                      THEN
         BNZ   ENDING                     BRANCH TO ENDING
         BAL   R10,DATATYPE             FIND DATA TYPE
         BAL   R10,DCBSELCT             SELECT DCB INFO FROM TABLE
         L     R4,RETCDE                IF RETURN CODE NOT GOOD
         LTR   R4,R4                      THEN
         BNZ   ENDING                     BRANCH TO ENDING
         BAL   R10,ALLOC                ALLOCATE THE DATASET
         L     R4,RETCDE                IF RETURN CODE
         LTR   R4,R4                      IS ZERO
         BZ    GOODMSG                  BRANCH AROUND
         LA    R4,2(0,0)                ELSE
         ST    R4,RETCDE                  SELECT MESSAGE #2
         BAL   R10,ERRMSG                 AND WRITE IT
         HIREP WORD=COMPCODE,CODE=12      SET CC=12         CAH TS04204
         B     ENDING                     THEN BRANCH TO ENDING
*
GOODMSG  LA    R4,3(0,0)
         ST    R4,RETCDE                SELECT MESSAGE #3
         BAL   R10,ERRMSG                 AND WRITE IT
         B     ENDING                     THEN BRANCH TO ENDING
*
         EJECT
**********************************************************************
***                                                                ***
***   EPILOGUE                                                     ***
***                                                                ***
**********************************************************************
ENDING   LA       R4,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R4,0(0,R4)              R4 POINTS TO PDL
         IKJRLSA  (R4)                    FREE STORAGE OF PDL
         FREEMAIN R,LV=WORK1LEN,A=(R11) , FREE MAIN STORAGE
         LINKBACK RCADDR=COMPCODE
*
*
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST                             ***
***                                                                ***
**********************************************************************
PPLSETUP ST    R10,PPR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         MVC   MYPPL+(PPLUPT-PPL)(4),CPPLUPT              UPT  (CPPL)
         MVC   MYPPL+(PPLECT-PPL)(4),CPPLECT              ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,MYPPL+(PPLECB-PPL)                      ECB  (MINE)
         MVC   MYPPL+(PPLPCL-PPL)(4),VPARMPCL             PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,MYPPL+(PPLANS-PPL)                      ANS  (MINE)
         MVC   MYPPL+(PPLCBUF-PPL)(4),CPPLCBUF            CBUF (CPPL)
         XC    MYPPL+(PPLUWA-PPL)(4),MYPPL+(PPLUWA-PPL)   UWA  (MINE)
         DROP  R4
*
PPEND    L     R10,PPR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
**********************************************************************
PARSE    ST    R10,PAR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     R10,PAR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   FIND LENGTH AND VALUE OF LOW LEVEL QUALIFIER                 ***
***                                                                ***
**********************************************************************
LOLEVEL  ST    R10,LOR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,NAME+0                R4 POINTS TO DSN
         LH    R5,NAME+4                R5 HAS LENGTH OF DSN
         LA    R6,0(R5,R4)              R6 POINTS JUST BEYOND DSN
*
LOLOOP   BCTR  R6,0                     DECR POINTER BY 1
         CLI   0(R6),C'.'               IF PRECEEDING DECIMAL POINT
         BE    *+12                       BRANCH OUT
         BCT   R5,LOLOOP                ELSE KEEP LOOKING
*                                    ** REACH HERE ONLY IF 1 LEVEL DSN
         B     *+8                      POINTING AT BEGINNING, SO BR
*                                    ** REACH HERE IF FOUND '.'
         LA    R6,1(0,R6)               POINTING TO '.', SO INCR BY 1
*                                    ** R6 POINTS TO 1ST CHAR OF QULFR
*
         AH    R4,NAME+4                R4 POINTS JUST BEYOND NAME
         SR    R4,R6                    R4 IS LENGTH OF NAME
*
         ST    R6,LOADDR                SAVE ADDR OF LOW LEVEL OF DSN
         STH   R4,LOLEN                 SAVE LEN OF LOW LEVEL OF DSN
*
         LTR   R4,R4                    IF LOW LEVEL IS LENGTH 0
         BP    LOSAVE                     BRANCH TO END
         LA    R4,4(0,0)                ELSE
         ST    R4,RETCDE                  SET INTERNAL RETCDE TO 4
         BAL   R10,ERRMSG                 PUT OUT ERROR MESSAGE #4
         HIREP WORD=COMPCODE,CODE=12      SET COMPLETION CODE TO 12
         B     LOEND                      BRANCH TO END OF ROUTINE
*
LOSAVE   BCTR  R4,0                     MINUS 1 FOR EXECUTE
         B     *+10                     BRANCH AROUND EXECUTED INSTR
         MVC   LOTEXT(0),0(R6)            SAVE TEXT OF LOW LEVEL
         EX    R4,*-6                   EXECUTE THE MOVE
*
LOEND    L     R10,LOR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    LOCATE DATA TYPE IN TABLE                                   ***
***                                                                ***
***    IF THE TYPE OPERAND HAS BEEN USED, USE THE INTEGER RETURNED ***
***    FROM IKJPARS AS THE KEY TO ENTERING THE TABLE.  OTHERWISE,  ***
***    USE THE TEXT AND LENGTH OF THE LOW LEVEL QUALIFIER AS THE   ***
***    KEY FOR ENTERING THE TABLE.                                 ***
***                                                                ***
**********************************************************************
DATATYPE ST    R10,DAR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LH    R4,TYPE                  LOAD TYPE NUMBER FROM PARM
         LTR   R4,R4                    IF NO TYPE DESIGNATED
         BNP   DATAFIND                   USE TABLE FOR TYPE
         B     DATAPARM                 ELSE LOOK UP TYPE DIRECTLY
*
DATAFIND LH    R4,LOLEN                 LENGTH OF LO LEVEL QUALIFIER
         XR    R5,R5                    CLEAR R5 FOR IC
         LA    R6,DATATBLE              BASE ADDRESS OF TABLE
*
DALOOP   CLI   0(R6),X'FF'              IF AT END OF TABLE,
         BE    DAFOUND                    BRANCH OUT OF LOOP
         IC    R5,1(0,R6)               R5 HAS LEN OF NAME IN TABLE
         CR    R4,R5                    IF NO MATCH
         BNE   DANEXT                     BRANCH TO BOTTOM OF LOOP
         BCTR  R5,0                     MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND COMPARE
         CLC   2(0,R6),LOTEXT             COMPARE LO TEXT WITH TBL TXT
         EX    R5,*-6                   EXECUTE COMPARE
         BE    DAFOUND                  IF EQUAL, BRANCH OUT OF LOOP
*
DANEXT   LH    R5,10(0,R6)              RECFM COUNT TO R5
         LA    R6,12(0,R6)              POINT TO FIRST RECFM
         LA    R6,6(0,R6)               POINT TO NEXT RECFM
         BCT   R5,*-4                   PERFORM TILL NO MORE RECFM'S
         B     DALOOP                   LOOP BACK UP FOR NEXT ENTRY
*
DATAPARM LA    R6,DATATBLE              BASE ADDRESS OF TABLE
DAPLOOP  CLI   0(R6),X'FF'              IF AT END OF TABLE
         BE    DAFOUND                    BRANCH OUT OF LOOP
         XR    R5,R5                    CLEAR R5 FOR IC
         IC    R5,0(0,R6)               DATA TYPE FROM TBL TO R5
         CR    R4,R5                    IF A MATCH
         BE    DAFOUND                    BRANCH OUT OF LOOP
         LH    R5,10(0,R6)              RECFM COUNT TO R5
         LA    R6,12(0,R6)              POINT TO FIRST RECFM
         LA    R6,6(0,R6)               POINT TO NEXT RECFM
         BCT   R5,*-4                   PERFORM TILL NO MORE RECFM'S
         B     DAPLOOP
*
DAFOUND  ST    R6,DATYPADR              SAVE ADDR OF TABLE ENTRY
*
DAEND    L     R10,DAR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   SELECT DCB INFO FROM TABLE                                   ***
***                                                                ***
***   LOOK FOR DCB-INFO SUBENTRIES IN THE TABLE TO MATCH THE       ***
***   INTEGER RETURNED BY IKJPARS FOR THE FORMAT OPERAND.  IF      ***
***   THE FORMAT OPERAND HAS NOT BEEN USED, SELECT THE FIRST       ***
***   DCB-INFO SUBENTRY.                                           ***
***                                                                ***
**********************************************************************
DCBSELCT ST    R10,DCR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,DATYPADR              R4 IS ADDR OF TABLE ENTRY
         LH    R5,10(0,R4)              R5 IS COUNT OF DCB ENTRIES
         LA    R6,12(0,R4)              R6 POINTS TO FIRST DCB ENTRY
*
         LH    R7,RFORM                 RFORM OPERAND TO R7
         LTR   R7,R7                    IF RFORM OPERAND NOT USED
         BZ    DCBMATCH                   BRANCH AROUND
*
         LA    R6,12-6(0,R4)            POINT TO -1 DCB ENTRY
DCBLOOP  LA    R6,6(0,R6)               R6 POINTS TO NEXT DCB ENTRY
         B     *+8                      BRANCH AROUND COMPARE
         CLI   0(R6),0                    REG FOR EXEC : DCB ENTRY
         EX    R7,*-4                   EXECUTE COMPARE
         BE    DCBMATCH                 IF HIT, BRANCH OUT
         BCT   R5,DCBLOOP               LOOP UP IF MORE DCB ENTRIES
*          ***  GET HERE ONLY IF NO HIT ***
         LA    R15,1(0,0)               SET INTERNAL R/C
         ST    R15,RETCDE                 TO ONE
         BAL   R10,ERRMSG               PERFORM ERROR MSG
         HIREP WORD=COMPCODE,CODE=12    SET COMP CODE TO 12
         B     DCEND                    BRANCH TO ENDING
*
DCBMATCH ST    R6,DCBADDR               SAVE DCB ENTRY ADDR
*
DCEND    L     R10,DCR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   ERROR MESSAGES                                               ***
***                                                                ***
***     ===> NOTE THAT RETCDE IS USED AS AN INPUT ARGUMENT <===    ***
***                                                                ***
**********************************************************************
ERRMSG   ST    R10,ERR10SAV             SAVE RETURN ADDRESS
*
*    +-------------------------------------+
*    |  SPECIAL PROCESSING FOR MESSAGE #3  |
*    +-------------------------------------+
         LA    R4,3(0,0)                CONSTANT FOR COMPARE
         C     R4,RETCDE                IF RETCODE WAS NOT 3
         BNE   ERRMSGS                    BRANCH AROUND SPECL PROCESS
         LA    R6,OLD0003V              ADDRESS OF WORK O.L.D.
         MVC   OLD0003V(16),OLD0003     COPY CONSTANTS TO WORK O.L.D.
         CLI   ORG+1,X'01'              IF ORG PARM NOT 'SEQUENTIAL'
         BNE   *+12                       BRANCH AROUND
         LA    R4,MSG0003S              ELSE ADDR OF 'SEQ' LITERAL
         ST    R4,OLD0003V+8              INTO WORK O.L.D.
         MVC   MSG0003C(48),MSG0003B    INITIALIZE WORK MSG SEGMENT
         LA    R4,MSG0003C              ADDRESS OF WORK MSG SEGMENT
         ST    R4,OLD0003V+12             INTO WORK O.L.D
         LH    R4,DSNLEN                R4 IS TOTAL DSN LENGTH
         LA    R4,4(0,R4)                 PLUS HEADER IN MSG SEGMENT
         STH   R4,MSG0003C+0              INTO WORK MSG SEGMENT
         MVC   MSG0003C+4(44),DSDSNAM   MOVE DSNAME INTO WORK MSG SEG
         B     ERIOPL                   BRANCH TO INITIALIZE IOPL
*
ERRMSGS  L     R6,RETCDE                RET CODE TO R4
         SLA   R6,2                     R6 IS OFFSET FROM ERADDRS-4
         LA    R6,ERADDRS-4(R6)         R6 POINTS TO ERADNNNN
         L     R6,0(0,R6)               R6 POINTS TO OLDNNNN
*
ERIOPL   XC    MYECB,MYECB              CLEAR ECB
         L     R4,CPPLPTR               R4 POINTS TO CPPL
         L     R4,(CPPLUPT-CPPL)(0,R4)  R4 CONTAINS UPT ADDRESS
         L     R5,CPPLPTR               R5 POINTS TO CPPL
         L     R5,(CPPLECT-CPPL)(0,R5)  R5 CONTAINS ECT ADDR
ERRPUTL  PUTLINE PARM=MYPTPB,UPT=(R4),ECT=(R5),                        X
               ECB=MYECB,OUTPUT=((R6)),MF=(E,MYIOPL)
*
EREND    L     R10,ERR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
*
*
ERADDRS  DS    0F                       TABLE OF OUTPUT LINE DESCRIPTRS
ERAD0001 DC    A(OLD0001)               #1 BAD TYPE/FORMAT COMBO
ERAD0002 DC    A(OLD0002)               #2 DATASET NOT ALLOCATED
ERAD0003 DC    A(0)                     DUMMY
ERAD0004 DC    A(OLD0004)               #4 IMPROPER DSN
         EJECT
**********************************************************************
***                                                                ***
***   ALLOCATE THE DATASET                                         ***
***                                                                ***
**********************************************************************
ALLOC    ST    R10,ALR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   DS(256),@DS              COPY CNTROL BLOCK DTA TO WORK1
         MVC   DS+256(DSLEN-256),@DS+256  COPY THE REMAINDER TO WORK1
*
ALDSN    L     R4,NAME                  R4 POINTS TO PARM DSNAME
         LH    R5,NAME+4                R5 IS LENGTH OF NAME
         STH   R5,DSNLEN                SAVE THE LENGTH
         BCTR  R5,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MVC
         MVC   DSDSNAM(0),0(R4)           MOVE PARM NAME TO TEXT UNIT
         EX    R5,*-6                   PERFORM MOVE
ALDDN    CLC   KDDNAME(2),H0            IF DDNAME NOT USED  CAH TS04204
         BE    ALNODDN                    GO TO ZERO THE TU CAH TS04204
         L     R4,SDDNAME               R4 POINTS TO DDNAME CAH TS04204
         LH    R5,SDDNAME+4             R5 IS LENGTH OF DDN CAH TS04204
         STH   R5,DDNLEN                SAVE THE LENGTH     CAH TS04204
         BCTR  R5,0                       MINUS 1 FOR EXEC  CAH TS04204
         B     *+10                     BRANCH AROUND MVC   CAH TS04204
         MVC   DSDDNAM(0),0(R4)           MOVE DDN TO T.U.  CAH TS04204
         EX    R5,*-6                   PERFORM THE MOVE    CAH TS04204
         B     *+10                     GO TO NEXT PROCESS  CAH TS04204
ALNODDN  MVC   DSDDNAM-6(2),H0          NO DDNAME: KEY=0000 CAH TS04204
         L     R4,DATYPADR              R4 POINTS TO TABLE TYPE-ENTRY
         TM    NAME+6,X'40'             IF PARM HAD QUOTES
         BO    ALDSORG                    BRANCH TO NEXT ROUTINE
         LH    R6,TYPE                  ELSE
         LTR   R6,R6                    IF NOT USING 'TYPE' PARM
         BZ    ALDSORG                    BRANCH TO NEXT ROUTINE
         CLI   0(R4),X'FF'              ELSE IF USING DEFALT TYPE-ENTRY
         BE    ALDSORG                    BRANCH TO NEXT ROUTINE
         LA    R5,DSDSNAM+1(R5)         R5 POINTS JUST BEYOND DSNAME
         MVI   0(R5),C'.'               PUT DOT AT END OF NAME
         IC    R6,1(0,R4)               R6 HAS LENGTH OF TYPE-NAME
         BCTR  R6,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MVC
         MVC   1(0,R5),2(R4)              TYPE-NAME TO END OF DSNAM
         EX    R6,*-6                   EXECUTE MVC
         LA    R6,2(0,R6)               LENGTH OF SUFFIX + DOT
         AH    R6,DSNLEN                ADD THE PREVIOUS LENGTH OF DSN
         STH   R6,DSNLEN                  AND SAVE IT
ALDSORG  LH    R4,ORG                   PARM ORG TO R4
         LA    R5,1(0,0)                CONSTANT FOR COMPARE
         CR    R4,R5                    IF 1 (SEQUENTIAL)
         BNE   ALRECFM                  THEN
         XC    DSDIR-6(2),DSDIR-6         SET DIRECTORY SPACE KEY=0000
         MVC   DSDSORG+6(2),PS            SET DSORG TO PS (X'4000')
ALRECFM  L     R4,DCBADDR               R4 POINTS TO DCB INFO IN TABLE
         MVC   DSRECFM+6(1),1(R4)       MOVE RECORD FORMAT TO T.U.
ALLRECL  MVC   DSLRECL(2),2(R4)         MOVE LRECL TO T.U.
ALBLKSZ  MVC   DSBLKSZ(2),4(R4)         MOVE BLKSIZE TO T.U.
*
*
ALSET    GTEDASET DS,CPPLPTR=CPPLPTR    LINK CONTROL BLOCKS TOGETHER
*
ALALC    GTEDAALC DS,VERB=AL,ERRMSG=YES ALLOCATE THE DATASET
         MVC   RETCDE,DSS99RC           COPY ALLOCATION R/C
*
ALEND    L     R10,ALR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
CONSTDTA DS    0D                       AREA FOR DATA CONSTANTS
PS       DC    XL2'4000'                TEXT UNIT DATA FOR DSORG=PS
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
H0       DC    H'0'                     X'0000' CONSTANT    CAH TS04204
CONSTEND DS    0D
*
*----------------------------------------------------------------------
DATATBLE DC    X'01,03',CL8'ASM     ',H'1'  +------------------------+
         DC    X'01,90',H'080',H'6160'      | TABLE ENTRIES:         |
*                                           |                        |
         DC    X'02,05',CL8'CLIST   ',H'2'  | TYPE ENTRY:            |
         DC    X'02,50',H'255',H'6160'      |   BYTE 1  SEQUENCE NO, |
         DC    X'01,90',H'080',H'1680'      |     DEFAULT=X'FF' (EOL)|
*                                           |   BYTE 2  NAME LENGTH  |
         DC    X'03,04',CL8'CNTL    ',H'1'  |   BYTE 3-10  NAME      |
         DC    X'01,90',H'080',H'6160'      |   BYTE 11-12 NUMBER-OF |
*                                           |     DCB-INFO SUBENTRIES|
         DC    X'04,05',CL8'COBOL   ',H'1'  |                        |
         DC    X'01,90',H'080',H'6160'      | DCB INFO SUBENTRY:     |
*                                           |   BYTE 1  FORMAT PARM  |
         DC    X'05,07',CL8'COMMAND ',H'2'  |     01 FIXED           |
         DC    X'01,90',H'080',H'1680'      |     02 VARIABLE        |
         DC    X'02,50',H'255',H'6160'      |     03 UNFORMATTED     |
*                                           |   BYTE 2  RECFM        |
         DC    X'06,04',CL8'DATA    ',H'2'  |     50 VB              |
         DC    X'01,90',H'080',H'6160'      |     54 VBA             |
         DC    X'02,50',H'255',H'6160'      |     90 FB              |
*                                           |     94 FBA             |
         DC    X'07,04',CL8'FORT    ',H'1'  |     C0 U               |
         DC    X'01,90',H'080',H'6160'      |   BYTE 3-4  LRECL      |
*                                           |   BYTE 5-6  BLKSIZE    |
         DC    X'08,04',CL8'LIST    ',H'2'  +-------------- CAH TS04203
         DC    X'01,94',H'133',H'6118'                      CAH TS04203
         DC    X'02,54',H'255',H'6160'                      CAH TS04203
*
         DC    X'09,04',CL8'LOAD    ',H'1'
         DC    X'03,C0',H'000',H'6233'
*
         DC    X'0A,03',CL8'OBJ     ',H'1'
         DC    X'01,90',H'080',H'0800'
*
         DC    X'0B,03',CL8'PLI     ',H'1'
         DC    X'02,50',H'104',H'6160'
*
         DC    X'0C,04',CL8'PLIF    ',H'1'
         DC    X'01,90',H'080',H'0400'
*
         DC    X'0D,04',CL8'TEXT    ',H'2'
         DC    X'02,50',H'255',H'6160'
         DC    X'01,90',H'080',H'6160'
*
         DC    X'0E,07',CL8'VSBASIC ',H'1'
         DC    X'02,50',H'255',H'6160'
*                                                           CAH TS04202
         DC    X'0F,06',CL8'DIALOG  ',H'2'                  CAH TS04203
         DC    X'01,90',H'080',H'3120'                      CAH TS04202
         DC    X'03,C0',H'000',H'6233'                      CAH TS04203
*                                                           CAH TS04204
         DC    X'10,01',CL8'C       ',H'1'                  CAH TS04204
         DC    X'01,90',H'080',H'6160'                      CAH TS04204
*
         DC    X'FF,07',CL8'DEFAULT ',H'2'
         DC    X'01,90',H'080',H'6160'
         DC    X'02,50',H'255',H'6160'
*----------------------------------------------------------------------
         EJECT
*
         PRINT GEN
***  CHANGED MACRO, BELOW                                   CAH TS04204
@DS      GTEDADAT  DDNAM=X,                                            X
               DSNAM=X,STATS=NEW,NDISP=CATLG,CDISP=DELETE,TRK=YES,     X
               PRIME=2,SECND=2,DIR=10,UNIT=SYSTSO,BLKSZ=0,DSORG=PO,    X
               LRECL=0,RECFM=FB
         PRINT NOGEN
*
OLD0001  DC    F'1',A(MSG0001)
MSG0001  DC    H'55',H'00',CL51'NSP0001 RECORD FORMAT IS INVALID FOR THX
               IS DATA TYPE'
*
OLD0002  DC    F'1',A(MSG0002)
MSG0002  DC    H'35',H'00',CL31'NSP0002 DATASET WAS NOT CREATED'
*
OLD0003  DC    F'3',A(MSG0003),A(MSG0003P),A(0)
*                               ....+....1....+.. . .2....+....3....+
MSG0003  DC    H'30',H'00',CL26'NSP0003 DATASET '''' CREATED'
MSG0003P DC    H'16',H'08',CL12'PARTITIONED '
MSG0003S DC    H'15',H'08',CL12'SEQUENTIAL '
MSG0003B DC    H'0',H'17',CL44' '
*
OLD0004  DC    F'1',A(MSG0004)
MSG0004  DC    H'33',H'00',CL29'NSP0004 IMPROPER DATASET NAME'
*
CONSTLEN EQU   *-CONSTDTA               TOTAL LENGTH OF CONSTANTS
*
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
NAME     IKJPOSIT DSNAME,PROMPT='DATASET NAME +',USID,                 X
               HELP=('NAME OF SEQUENTIAL DATASET OR LIBRARY (PARTITIONEX
               D DATASET) TO BE CREATED')
*
TYPE     IKJKEYWD
         IKJNAME 'ASM'
         IKJNAME 'CLIST'
         IKJNAME 'CNTL',ALIAS=('JCL')
         IKJNAME 'COBOL'
         IKJNAME 'COMMAND'
         IKJNAME 'DATA'
         IKJNAME 'FORT',ALIAS=('FORTGI','FORTVS','FORTRAN')
         IKJNAME 'LIST'
         IKJNAME 'LOAD'
         IKJNAME 'OBJ',ALIAS=('OBJECT')
         IKJNAME 'PLI',ALIAS=('PL1')
         IKJNAME 'PLIF',ALIAS=('PL1F')
         IKJNAME 'TEXT'
         IKJNAME 'VSBASIC'
         IKJNAME 'DIALOG' ,                                 CAH TS04202
         IKJNAME 'C' ,                                      CAH TS04204
*
ORG      IKJKEYWD DEFAULT='PARTITIONED'
         IKJNAME  'SEQUENTIAL'
         IKJNAME  'PARTITIONED',ALIAS=('PDS','LIBRARY')
*
RFORM    IKJKEYWD
         IKJNAME  'FIXED'
         IKJNAME  'VARIABLE'
         IKJNAME  'UNFORMATTED'
*                                                           CAH TS04204
KDDNAME  IKJKEYWD ,                                         CAH TS04204
         IKJNAME 'DDNAME',SUBFLD=KSDDNAME                   CAH TS04204
         IKJNAME 'FILE',SUBFLD=KSDDNAME                     CAH TS04204
*                                                           CAH TS04204
KSDDNAME IKJSUBF ,                                          CAH TS04204
*                                           FOLLOWING MACRO CAH TS04204
SDDNAME  IKJPOSIT DSNAME,DDNAM,PROMPT='1-8 CHARACTER FILE/DD NAME'
*
         IKJENDP
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA OBTAINED WITH LINKSAVE                             ***
***                                                                ***
**********************************************************************
WORK0    DSECT                          AREA-0 FOR VARIABLES
MYSAVE   DS    18F                      REGISTER SAVE AREA
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
*
         EJECT
**********************************************************************
***                                                                ***
***  DATA VARIABLES                                                ***
***                                                                ***
**********************************************************************
WORK1    DSECT                          AREA-1 FOR VARIABLES
PPR10SAV DS    F                        RETURN ADDRESS SAVE AREA
PAR10SAV DS    F                        RETURN ADDRESS SAVE AREA
LOR10SAV DS    F                        RETURN ADDRESS SAVE AREA
DAR10SAV DS    F                        RETURN ADDRESS SAVE AREA
DCR10SAV DS    F                        RETURN ADDRESS SAVE AREA
ERR10SAV DS    F                        RETURN ADDRESS SAVE AREA
ALR10SAV DS    F                        RETURN ADDRESS SAVE AREA
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
MYIOPL   DS    4F                       IOP LIST FOR PUTLINE
MYPTPB   DS    3F                       PUTLINE PARM BLOCK
LOADDR   DS    F                        ADDR OF LOW LEVEL QUALIFIER
LOLEN    DS    H                        LENGTH OF LOW LEVEL QUALIFIER
LOTEXT   DS    CL8                      TEXT OF LOW LEVEL QUALIFIER
DATYPADR DS    F                        ADDR OF TABLE ENTRY
DCBADDR  DS    F                        DCB ENTRY ADDRESS
DSNLEN   DS    H                        TOTAL LENGTH OF DSN
DDNLEN   DS    H                        LENGTH OF DDNAME    CAH TS04204
OLD0003V DS    F,3A                     WORK O.L.D. FOR MSG #3
MSG0003C DS    2H,CL44                  WORK MSG SEG #2 FOR MSG #3
         PRINT GEN
*     CHANGED MACRO, BELOW                                  CAH TS04204
DS       GTEDADAT MAP=ONLY,DDNAM=X,                                    X
               DSNAM=X,STATS=NEW,NDISP=CATLG,CDISP=DELETE,TRK=YES,     X
               PRIME=2,SECND=2,DIR=10,UNIT=SYSTSO,BLKSZ=0,DSORG=PO,    X
               LRECL=0,RECFM=FB
         PRINT NOGEN
WORK1END DS    0D
WORK1LEN EQU   *-WORK1                  TOTAL LENGTH OF WORK-1 AREA
*
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING MACROS                                               ***
***                                                                ***
**********************************************************************
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         PRINT GEN
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         IKJPTPB                        PUTLINE PARM BLOCK
         PRINT NOGEN
         IEFZB4D0                       MAPS SVC-99 CONTROL BLOCKS
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES  MAPS IKJEFF18 CONTROL BLOCKS
         END
