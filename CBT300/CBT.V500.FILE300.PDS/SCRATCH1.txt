*          DATA SET TSOSCRATCH AT LEVEL 001 AS OF 12/03/79
 TITLE 'S C R A T C H   ---   S C R A T C H   D A T A S E T'
*---------------------------------------------------------------------*
*                                                                     *
*   SCRATCH IS A TSO COMMAND PROCESSOR WHICH SCRATCHES A DATASET      *
*      OR LIST OF DATASETS, WITHOUT REFERENCE TO THE CATALOG, ON      *
*      A DESIGNATED VOLUME (SYSTEM DADSM SCRATCH FUNCTION).  THE      *
*      OPTION TO OVERRIDE AN EXPIRATION DATE IS PROVIDED.             *
*                                                                     *
*   SCRATCH IS INVOKED AS FOLLOWS :                                   *
*                                                                     *
*      SCRATCH  |  SCR                                                *
*                    "DSNAME | ("DSNAME" ... )                        *
*                    VOLUME ("VOLID")                                 *
*                    UNIT ("UNITNAME")  |  DEVTYPE ("HEX DIGITS")     *
*                                                                     *
*   SCRATCH USES THE FOLLOWING SERVICE ROUTINES AND SYSTEM FUNCTIONS  *
*                                                                     *
*      IKJPARS  -- TSO COMMAND PARAMETER PARSE ROUTINE                *
*      IKJEFF18 -- TSO DYNAMIC ALLOCATION ERROR DIAGNOSIS             *
*      IKJEFF19 -- TSO GENERAL COMMAND SYSTEM ERROR DIAGNOSIS         *
*      SOFDA08  -- MMSC DATASET ACCESS AUTHORIZATION EXIT             *
*      GETMAIN, FREEMAIN, LINK, SCRATCH, AND CAMLST SYSTEM MACROS     *
*      CPPL, PSCB, ECT, UPT, DAPB08 SYSTEM MAPPING MACROS             *
*      PPL, DAPL, CAMLST, AND DSNAME AND IDENT PDE IMPLICIT MAPPING   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
SCRATCH  CSECT
         SPACE 1
         USING DSAWRK,R10
         USING SCRATCH,R12
         USING UPT,R3
         USING PSCB,R4
         USING ECT,R5
         USING PDL,R2
         USING DAPB08,R9
         USING DSNPDE,R6
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   PERFORM STANDARD REENTRANT PROLOGUE - GET WORK AREA AND SET UP    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         JVDENTER BASE=(R12),DSA=(DSAEND-DSAWRK),VERSION=&SYSPARM
         SPACE 2
         LM    R2,R5,0(R1)
         LR    R10,R13
         L     R6,PCLA
         ST    R12,UWA
         XC    ECB,ECB
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   PARSE COMMAND PARAMETERS AND INITIALIZE FOR SCRATCHING LOOP       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LINK  EP=IKJPARS,DCB=0,SF=(E,LINK),                           +
               VL=1,MF=(E,PPL),                                        +
               PARAM=(UPT,ECT,ECB,(6),ANS,(2),SSA)
         SPACE 1
         ST    R15,PARC
         LTR   R15,R15
         BNZ   PARSFAIL
         L     R2,ANS
         CL    R2,NULL
         BE    PARSFAIL
         SPACE 1
         LA    R6,DSNAME
         LA    R9,DAPB
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   CONSTRUCT VOLUME LIST FROM COMMAND PARAMETERS AND DEVICE NAMES    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LH    R14,VOLID+4
         L     R15,VOLID+0
         BCTR  R14,0
         MVC   VOLWVSN,DSBLANK
         MVC   VOLWVSN(*-*),0(R15)
         EX    R14,*-6
         LA    R15,1
         STH   R15,VOLWCNT
         SPACE 1
         CLI   DEVICE+1,1
         BE    DSLOOP
         L     R15,DEVTYPE+0
         LH    R14,DEVTYPE+4
         TR    0(*-*,R15),TRHEX
         EX    R14,*-6
         PACK  DWD,0(*-*,R15)
         EX    R14,*-6
         XR    R0,R0
         ICM   R0,B'1111',DWD+3
         STCM  R0,B'1111',VOLWDEVT
         B     DSLOOP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   IF A UNIT NAME IS GIVEN, LOOK UP DEVICE TYPE IN NAME TABLE.       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
UNITCHK  EQU   *
         SPACE 1
         SAVE  (14,12),,UNITCHK
         LR    R2,R1
         LR    R11,R15
         LM    R9,R10,0(R1)
         L     R12,UWA
         SPACE 1
         L     R15,0(R9)
         LH    R14,4(R9)
         BCTR  R14,0
         MVC   UNITWRK,DSBLANK
         MVC   UNITWRK(*-*),0(R15)
         EX    R14,*-6
         SPACE 1
         LOAD  EP=DEVNAMET
         SPACE 1
         LR    R15,R0
         L     R14,0(R15)
         LA    R1,4(R15)
         SPACE 1
UNITLOOP EQU   *
         CLC   UNITWRK,0(R1)
         BE    UNITHIT
         LA    R1,12(R1)
         BCT   R14,UNITLOOP
         EJECT
UNITMISS EQU   *
         LA    R11,4
         B     UNITRTRN
         SPACE 1
UNITHIT  EQU   *
         MVC   VOLWDEVT,8(R1)
         XR    R11,R11
         TM    VOLWDEVT+2,X'A0'
         BZ    UNITMISS
         SPACE 1
UNITRTRN EQU   *
         SPACE 1
         DELETE EP=DEVNAMET
         SPACE 1
         LR     R15,R11
         RETURN (14,12),,RC=(15)
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   FOR EACH DATASET SPECIFIED, BUILD DAPB AND VALIDATE REQUEST       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
DSLOOP   EQU   *
         SPACE 1
         MVC   DAPB,DAP8LST
         MVC   DSNVALUE,DSBLANK
         LA    R15,DSNVLEN
         ST    R15,DA08PDSN
         SPACE 1
         DA08BLD DSNPDE,VOLID,DISP=(NEW,KEEP,KEEP)
         SPACE 1
ALLOC    EQU   *
         LINK  EP=SECDAIRC,DCB=0,SF=(E,LINK),                          X
               VL=1,MF=(E,DAPL),                                       X
               PARAM=(UPT,ECT,ECB,PSCB,DAPB)
         SPACE 1
         ST    R15,DARC
         LTR   R15,R15
         BNZ   AUTHFAIL
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   IF ACCESS TO DATASET IS AUTHORIZED, ISSUE SCRATCH FOR DSNAME      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   DSNWKA(44),DSNVALUE
         TM    DA08CTL,DA08UID
         BZ    CAMBLD
         XR    R15,R15
         IC    R15,PSCBUSRL
         MVC   DSNVALUE(*-*),PSCBUSER
         EX    R15,*-6
         LR    R14,R15
         AH    R14,DSNVLEN
         LA    R14,1(R14)
         STH   R14,DSNVLEN
         LA    R15,DSNVALUE(R15)
         MVI   0(R15),C'.'
         LA    R14,DSNVALUE+L'DSNVALUE-2
         SLR   R14,R15
         MVC   1(*-*,R15),DSNWKA
         EX    R14,*-6
         SPACE 2
CAMBLD   MVC   CAMLST,CAMSCRNV
         LA    R15,DSNVALUE
         ST    R15,CAMDSN
         LA    R15,VOLWKA
         ST    R15,CAMVOL
         XC    CAMSER,CAMSER
         CLI   PURGE+1,1
         BNH   SCRTCH
         MVC   CAMLST,CAMSCRCV
         LA    R15,VOLSER
         ST    R15,CAMSER
         EJECT
SCRTCH   SCRATCH CAMLST
         SPACE 1
         LTR   R15,R15
         BNZ   CATFAIL
         SPACE 1
DSNEXT   EQU   *
         CLC   DSNLINK,NULL
         BE    DONE
         L     R6,DSNLINK
         B     DSLOOP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   ABNORMAL SERVICE ROUTINE (DAIR AND PARS) TERMINATION PROCESSING   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
CATFAIL  EQU   *
         ST    R15,RCFAIL
         XC    SCFAIL,SCFAIL
         CH    R15,H08
         BNE   CFAIL
         MVC   SCFAIL+3(1),VOLWSEQ+1
         SPACE 1
CFAIL    LINK  EP=CVSFMSG,DCB=0,SF=(E,LINK),                           +
               MF=(E,FAIL),                                            +
               PARAM=(UPT,PSCB,ECT,ECB,F03,RCFAIL,SCFAIL)
         B     DSNEXT
         EJECT
AUTHFAIL EQU   *
         LINK  EP=IKJEFF18,DCB=0,SF=(E,LINK),                          +
               MF=(E,FAIL),                                            +
               PARAM=(DAPL,DARC,ZERO,H01)
         B     DSNEXT
         EJECT
PARSFAIL EQU   *
         LINK  EP=IKJEFF19,DCB=0,SF=(E,LINK),                          +
               MF=(E,FAIL),PARAM=(0,PARC,ZERO,H21)
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   WHEN ALL DATASETS HAVE BEEN PROCESSED, FREE PDL AND WORK AREAS    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
DONE     EQU   *
         IKJRLSA ANS
         SPACE 1
         JVDLEAVE
         SPACE 2
         PRINT NOGEN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   PARSE CONTROL CSECT AND PARSE DESCRIPTOR LIST DSECT DEFINITIONS   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
SCRPCL   IKJPARM DSECT=PDL
         SPACE 2
DSNAME   IKJPOSIT DSNAME,LIST,                                         +
               PROMPT='DSNAME(S)+',                                    +
               HELP='NAME(S) OF DATA SET(S) TO BE SCRATCHED'
         SPACE 2
VOL      IKJKEYWD DEFAULT='VOLUME'
         SPACE 1
         IKJNAME 'VOLUME',SUBFLD=VOLIDS
         SPACE 2
DEVICE   IKJKEYWD DEFAULT='UNITNAME'
         SPACE 1
         IKJNAME 'UNITNAME',SUBFLD=UNITS
         IKJNAME 'DEVTYPE',SUBFLD=DEVTS
         SPACE 2
PURGE    IKJKEYWD DEFAULT='NOPURGE'
         SPACE 1
         IKJNAME 'NOPURGE'
         IKJNAME 'PURGE'
         IKJNAME 'OVRD'
         EJECT
VOLIDS   IKJSUBF
         SPACE 1
VOLID    IKJIDENT 'VOLID',MAXLNTH=6,FIRST=ANY,OTHER=ANY,               +
               PROMPT='VOLID+',                                        +
               HELP='ID OF VOLUME(S) ON WHICH DATASET(S) RESIDE'
         SPACE 1
UNITS    IKJSUBF
         SPACE 1
UNIT     IKJIDENT 'UNIT NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,           +
               PROMPT='UNIT NAME+',                                    +
               VALIDCK=UNITCHK,                                        +
               HELP='NAME OF UNIT ON WHICH DATASET(S) RESIDE'
         SPACE 1
DEVTS    IKJSUBF
         SPACE 1
DEVTYPE  IKJIDENT 'DEVTYPE',MAXLNTH=8,FIRST=ALPHANUM,OTHER=ALPHANUM,   +
               PROMPT='DEVTYPE+',                                      +
               HELP='HEXADECIMAL DIGITS FOR DEVICE TYPE DATA'
         SPACE 1
         SPACE 1
         IKJENDP
         SPACE 2
         PRINT GEN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   DECLARE ALL CONSTANTS, LITERALS, WORK AREAS, AND CONTROL BLOCKS   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
PCLA     DC    A(SCRPCL)
NULL     DC    0A(0),X'FF',AL3(0)
ZERO     DC    A(0)
F03      DC    F'3'
H01      DC    H'1'
H08      DC    H'8'
H21      DC    H'21'
         SPACE 1
TRHEX    DC    256AL1(*-TRHEX)
         ORG   TRHEX+X'C1'
         DC    X'FAFBFCFDFEFF'
         ORG   TRHEX+X'FA'
         DC    C'ABCDEF'
         PRINT DATA
         SPACE 1
SCRERR   DC    X'0018800000380000000000000000',2CL8' ',CL2' '
         DC    X'0400',CL8' '
DAP8LST  DC    X'0008',BL2'0',XL2'0',XL2'0',AL4(0)
         DC    3CL8' ',4F'0',2CL8' '
         DC    AL1(DA08OLD),AL1(DA08KEEP),AL1(DA08KEEP)
         DC    BL1'0',XL3'0',BL1'0',CL8' '
DSBLANK  DC    CL44' '
         EJECT
CAMSCRCV CAMLST    SCRATCH,*-*,,*-*,,OVRD      NAME,,VOLLIST,,OV
         SPACE 1
CAMSCRNV CAMLST    SCRATCH,*-*,,*-*            NAME,,VOLLIST
         SPACE 1
PATCH    DC    32S(*)
         SPACE 1
         LTORG
         EJECT
DSNPDE   DSECT
         SPACE 1
DSNADDR  DS    A
DSNLEN   DS    H
DSNFLG1  DS    B
DSNMADDR DS    A
DSNMLEN  DS    H
DSNFLG2  DS    B
DSNPADDR DS    A
DSNPLEN  DS    H
DSNFLG3  DS    B
DSNLINK  DS    A
DSNPRES  EQU   B'10000000'
DSNQUOTE EQU   B'01000000'
         SPACE 2
EXTENT   DSECT
         SPACE 1
EXTTYPE  DS    BL1
EXTSEQ   DS    XL1
EXTLOCC  DS    HL2
EXTLOHH  DS    HL2
EXTHICC  DS    HL2
EXTHIHH  DS    HL2
         SPACE 1
EXTNEXT  EQU   *
         EJECT
         IKJDAP08
DA08END  EQU   *
         EJECT
         IKJDAP18
         EJECT
         IKJUPT
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         EJECT
         IKJCPPL
         EJECT
         SPACE 3
         JVDEQU
         SPACE 2
         PRINT GEN
         EJECT
DSAWRK   DSECT
         SPACE 1
SSA      DS    18F
TIOTA    DS    A
ANS      DS    A
ECB      DS    A
UWA      DS    A
DWD      DS    D
RCFAIL   DS    F
SCFAIL   DS    F
         SPACE 1
VOLWKA   DS    0D
VOLWCNT  DS    H
VOLWNTRY DS    0XL12
VOLWDEVT DS    XL4
VOLWVSN  DS    CL6
VOLWSEQ  DS    H
         SPACE 1
UNITWRK  DS    CL8
DSNWKA   DS    CL44
DSAPATCH DC    8S(*)
DARC     DS    F
PARC     DS    F
DVTWKA   DS    5F
FAIL     DS    8F
BTRALC   DS    F
CAMLST   DS    F
CAMDSN   DS    A
CAMSER   DS    A
CAMVOL   DS    A
CAMTTR   DS    A
         EJECT
PPL      CALL  ,(UPT,ECT,ECB,PCLA,ANS,CPPL,UWA),MF=L
DAPL     CALL  ,(UPT,ECT,ECB,PSCB,DAPB),MF=L
EXTRACT  EXTRACT  TIOTA,FIELDS=TIOT,MF=L
LINK     LINK     SF=L
         SPACE 1
DAPB     DS    XL(DA08END-DAPB08)
DSNVLEN  DS    H
DSNVALUE DS    CL44
VOLSER   DS    CL6
CUU      DS    CL3
         SPACE 1
         SPACE 1
DSAEND   DS    0D
         END   SCRATCH
