*          DATA SET TSOQ       AT LEVEL 001 AS OF 12/03/79
 TITLE 'MACROS                     M A C R O S'
* * * *  TADDR                     TEST ADDR FIELD FOR ZERO OR NOT ZERO
         SPACE
         MACRO
&LBL     TADDR &FLD,&BZ=,&BNZ=
&LBL     L     R1,&FLD             LOAD FIELD
         LA    R1,0(,R1)           CLEAR HI ORDER BYTE
         LTR   R1,R1               IS FIELD ZERO
         AIF   (T'&BZ EQ 'O').BNZ  IF BZ NOT WANTED, DO BNZ
         BZ    &BZ                 IF SO, GO TO &BZ
         MEXIT
.BNZ     ANOP
         BNZ   &BNZ                IF NOT, GO TO &BNZ
         MEND
         SPACE 3
* * * *  LOOPC                     CHECK FOR ININATE LOOP
         SPACE
         MACRO
&LBL     LOOPC &FLD,&NAM=
&LBL     L     R1,&FLD             CHECK FOR INFINATE LOOP
         LA    R1,1(R1)            UP IT 1
         ST    R1,&FLD
         C     R1,=F'1001'         IS IT OVER 1000
         BL    OK&SYSNDX           IF NOT, ITS OK
         OI    QWDSTATS,QINFNT     SET INFINATE LOOP BIT
         B     &NAM.XIT
OK&SYSNDX DS   0H
         MEND
 TITLE 'QUEUE   I N I T A L I Z A T I O N'
*  'Q'  SEARCHES AND OUTPUTS QCB QEL TO USER
         SPACE
*  'Q' WAS WRITTEN FOR TSO AND WAS LATER ADAPTED TO RUN UNTER JES,
*      MSTR SCHDLR, AND BATCH.  FOR SIMPLICITY, THESE OTHER ADAPTATIONS
*      ARE FRONT ENDS THAT SIMULATE THE TSO PARSE ROUTINE .
         SPACE
*
*  COMMAND FORMAT
*
*  TSO:
*    Q MAJOR(X),MINOR(X),JOB(X),TYPE(XXX),CONFLICT(XXX)
*
*  JES:
*    $Q MAJOR=X,MINOR=X,JOB=X,TYPE=XXX,CONFLICT=XXX
*
*      Q        REQUIRED            COMMAND NAME
*      MAJOR(X) OPTIONAL            ONLY MAJOR QNAME QEL TO BE LISTED
*      MINOR(X) OPTIONAL            ONLY MINOR QNAME QEL TO BE LISTED
*      JOB(X)   OPTIONAL            ALL MAJOR AND MINOR OF JOB X LISTED
*      TYPE(XCL)                    ONLY EXCLUSIVE TO BE LISTED
*      TYPE(RSV)                    ONLY RESERVED TO BE LISTED
*      TYPE(SHR)                    ONLY SHARED TO BE LISTED
*      TYPE(ALL) DEFAULT            ALL TYPES OF QEL LISTED
*      CONFLICT(YES) DEFAULT        ONLY QUEUE CONFLICTS LISTED
*                                   IF 'YES', TYPE IS SUPERFLUOUS
*      CONFLICT(NO)  DEFAULT        IF ANY OTHER KEYWORD IS SPECIFIED,
*                                      THEN C(NO) IS THE DEFAULT
         SPACE
*      ALIAS
*        MAJOR  MAJ,MJR,MJ
*        MINOR  MIN,MNR,MN
*        JOB    JB,J
*        TYPE   T
*        CONFLICT CONFLCT,CONFL,CONF,CNFLCT,CNFL,CNF,CF,C
         SPACE 2
*  ATTRIBUTES = RENT,REUS
         SPACE 2
R0       EQU   0                   STD IBM REGS
R1       EQU   1
W0       EQU   2                   WORK REGS
W1       EQU   3
W2       EQU   4
W3       EQU   5
D0       EQU   6                   MULTI-PURPOSE DSECT BASE
D1       EQU   7                   PDL DSECT BASE
D3       EQU   8                   CPPL DSECT BASE
D4       EQU   9                   AST BASE
FREEREG1 EQU   10                  UNALLOCATED REG
BASE0    EQU   11                  PGM BASE REGS
BASE1    EQU   12
R13      EQU   13                  STD IBM REG/Q WORK DSECT BASE
R14      EQU   14
R15      EQU   15
INVR     EQU   14                  BAL RETURN REG
         SPACE 2
*  QSTATS BIT EQUATES
QIO      EQU   X'80'               OUTPUT HAS BEEN PROCESSED
QINFNT   EQU   X'40'               BAD LOOP DETECTED
QCONF    EQU   X'20'               QUEUE CONFLICT DETECTED
QCONFOFF EQU   X'DF'               TURN OFF CONFLICT BIT
QVIRG    EQU   X'10'               VIRGIN CMD  (DFLT TAKEN)
QMSTASCB EQU   X'08'               MSTR SCHEDULAR ASCB
QJESASCB EQU   X'04'               JES ASCB
QTSOASCB EQU   X'02'               TSO ASCB
QBJASCB  EQU   X'01'               BATCH JOB ASCB
         SPACE 2
*  GENERAL EQUATES
MO       EQU   5                   BRANCH IF MIXED OR ONES  (TM INST)
SUBFPRES EQU   X'80'               PDE SUBFIELD PRESEND INDICATOR
LSTQADDR EQU   X'80'               FIELD CONTAINS LIST QUEUE EL ADDR
RSV      EQU   X'10'               RSV QEL
SHR      EQU   X'80'               SHR QEL
CBUFL    EQU   132                 CMD BUF LEN
*  RMCT AND RMQH DSECTS NOT AVAILABLE
RMCTWTQE EQU   104                 WAIT SWAP OUT RMQH
RMCTOTQE EQU   108                 READY SWAP OUT RMQH
RMQHFWD  EQU   4                   1ST OUCB POINTER
         EJECT
Q        CSECT
         B     68(,R15)
         DC    CL64'Q   &SYSDATE   &SYSTIME   AUTH=RON KURTZ'
         STM   R14,12,12(R13)
         LR    BASE0,R15
         LA    BASE1,2048(BASE0)
         LA    BASE1,2048(BASE1)
         USING Q,BASE0,BASE1
         LR    D1,R1               TEMP SAVE CPPL PTR
         L     R0,QWDLEN           GET LEN AND SUBPOOL NUM
         GETMAIN R,LV=(0)          GET SOME COR
         MVI   0(R1),X'00'         CLEAR GETMAINED AREA
         LR    W0,R1               TO ADDR
         L     W1,=A(QWDLENX)      TO LEN
         LR    W2,R1               FROM ADDR
         SR    W3,W3               FROM LEN
         MVCL  W0,W2               DO IT
         ST    R1,8(,R13)          CHAIN LOW SAV TO HI SAV
         ST    R13,4(,R1)          CHAIN HI SAV TO LOW SAV
         LR    R13,R1              GET ADDR OF SAVE AREA AND WORD DSECT
         USING QWD,R13
         ST    D1,QWDCPPLA         PERM SAVE CPPL
         LR    D3,D1               SET CPPL BASE
         USING CPPL,D3
         LA    D4,QWDAST           AST BASE
         USING AST,D4
         SPACE 1
*  DETERMINE TYPE OF ENTRY  (BATCH/TSO/JES/MSTR)
         SR    D0,D0               SET PSA BASE
         USING PSA,D0
         L     D0,PSAAOLD          THIS ASCB
         USING ASCB,D0
         L     D0,ASCBOUCB         THIS OUCB
         USING OUCB,D0
         TM    OUCBYFL,OUCBLOG     IS IT A LOGON
         BNO   QWHAT10             NO, CHECK NEXT
         OI    QWDSTATS,QTSOASCB   SET AS TSO ASCB
         B     TSOCTL              DO TSO CTL
QWHAT10  DS    0H
         TM    OUCBYFL,OUCBSTT+OUCBMNT IS IT START OR MOUNT ASCB
         BM    QWHAT20             IF SO, CHECK WHICH (JES/MSTR)
         OI    QWDSTATS,QBJASCB    IF NOT, ITS A BATCH JOB ASCB
         B     BJCTL
QWHAT20  DS    0H
         L     D0,OUCBASCB         RESTORE ASC PTR
         USING ASCB,D0
         L     W0,ASCBJBNS         JOB NAME BASE
         CLC   0(8,W0),=C'*MASTER*' IS IT MSTR SCHEDULAR
         BNE   QWHAT30             IF NOT, DO JES
         OI    QWDSTATS,QMSTASCB   IF SO, SET MSTR ASCB
         B     MSTRCTL
QWHAT30  DS    0H
         OI    QWDSTATS,QJESASCB   SET JES ASCB
         DROP  D0
         SPACE 3
*  JES CTL AND SET-UP
*
*  UPON ENTRY OF Q, R1 = JES PARM PTR (NOW LOCATED IN QWDCPPLA)
*  PARM IS MAPPED BY JPRM
         SPACE
JESCTL   DS    0H
         L     D0,QWDCPPLA         SET JES PARM DSECT
         USING JPRM,D0
         ST    D0,QWDJESP          SAVE IT IN PROPER FIELD
         SPACE
*  PROVIDE VIRGIN CMD SET-UP
*  - BUFFER
         MVC   QWDCBUF(2),=H'6'    VIRG CMD LEN + HEADER
         MVC   QWDCBUF+2(2),=H'2'  VIRG CMD LEN
         MVC   QWDCBUF+4(128),=CL128'$Q'
*  - CPPL
         LA    D3,QWDCPPL          CPPL DSECT BASE
         ST    D3,QWDCPPLA         CPPL PTR
         LA    W0,QWDCBUF
         ST    W0,CPPLCBUF         BUF PTR
*  - PDL
         LA    D1,QWDPDL           PDL DSECT BASE
         ST    D1,QWDPDLA          PDL PTR
         MVC   QWDPDL(8),=X'FF00000001000100'
         SPACE
*  PROCESS OPERANDS
         L     W1,JPRMOP           1ST OP PTR PTR      * SET FOR BXLE*
         LM    W2,W3,0(W1)         1ST AND 2ND OP PTRS
         SR    W3,W2               1ST OP PTR - 2ND OP PTR = 1ST OP LEN
         C     W3,=F'3'            1ST OP LEN LESS THAN 3
         BL    DFLTSET             IF SO, IT WAS A VIRG CMD
         L     W2,JPRMOPL          OP PTR TBL EL LEN   * SET FOR BXLE *
         L     W3,JPRMOPN          LAST O PTR PTR      * SET FOR BXLE *
JESCPROS DS    0H
         LR    R1,W1               GET CUR OP PTR PTR
         BAL   INVR,JESOP          DO THIS OPERAND
         BXLE  W1,W2,JESCPROS      DO NEXT OPERAND
         B     DFLTSET             DO REMAINING GEFAULTS
         DROP  D0
         SPACE 3
*  MASTER SCHEDULAR CTL AND SETUP
MSTRCTL  DS    0H
         WTO   'MASTER SCHEDULAR  -Q- COMMAND NOT COMPLETE'
         B     QXIT                NONE YET
         SPACE 3
*  BATCH CTL AND SET-UP
BJCTL    DS    0H
         WTO   'BATCH -Q- COMMAND NOT COMPLETE'
         B     QXIT                NONE YES
         SPACE 2
* TSO SERVICES
*  PARS THE COMMAND
         SPACE
TSOCTL   DS    0H
         LA    D0,QWDPPL           SET UP PARS DSECT ADDRESSABILITY
         USING PPL,D0
         MVC   PPLUPT,CPPLUPT      USER PROFILE TBL ADDR
         MVC   PPLECT,CPPLECT      ENVIRONMENT CTL TBL ADDR
         LA    W0,QWDPPLEC         GET ADDR OF ECB
         ST    W0,PPLECB
         L     W0,=A(QPCL)         GET ADDR OF PARSE INPUT PARM
         ST    W0,PPLPCL           PARSE CNTL LIST PARM
         LA    W0,QWDPDLA
         ST    W0,PPLANS           PARS DESCRIPTER LIST ADDR
         MVC   PPLCBUF,CPPLCBUF    CMD BUFF ADDR
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
         LTR   R15,R15             PARSE GOOD
         BNZ   QXIT                IF NOT, XIT
         L     D1,PPLANS           GET PARSE OUTPUT PTR PTR
         L     D1,0(,D1)           GET PARSE OUTPUT DSECT BASE (PDL)
         DROP  D0
         SPACE 2
*  ELIM IKJ56601I CMD SYS RESTRT/CRITICAL ERROR MESSAGE
*  CANT UPDATE DATA IN TERMINAL MONITOR DATA AREAS
*  SOLUTION
*    COPY CMD BUF AND PDL INTO MY REGION AND ALTER NECESSARY POINTERS
         MVC   QWDPDL,0(D1)        COPY PDL TO THIS REGION
         LA    D1,QWDPDL           UPDATE PDL DSECT BASE
         ST    D1,QWDPDLA          UPDATE PDL PTR
         MVC   QWDCPPL(16),0(D3)   COPY CPPL TO THIS REGION
         L     W0,CPPLCBUF         CMD BUF PTR
         MVC   QWDCBUF,0(W0)       COPY CMD BUF
         LA    D3,QWDCPPL          UPDATE CPPL DXECT BASE
         ST    D3,QWDCPPLA         UPDATE CPPL PTR
         LA    W0,QWDCBUF          NEW BUF PTR
         ST    W0,CPPLCBUF         UPDATE CPPL CMD BUF PTR
         SPACE 3
*  DEFAULT PROCESSOR
         SPACE
DFLTSET  DS    0H
         USING QPDLD,D1            PDL BASE
         SR    W1,W1
         L     W0,CPPLCBUF          BUF PTR
         LH    W1,0(,W0)           CMD LEN
         C     W1,=F'6'            DEFAULT CMD LEN
         BH    DFLTTYPE            IF OVER 6, USER REQUESTED SOMETHING
         OI    QWDSTATS,QVIRG      IF UNDER 6, SET AS VIRGIN CMD
         SPACE
*  TYPE(ALL)
DFLTTYPE DS    0H
         TM    QTYPESUB+6,SUBFPRES DID USER SPECIFY TYPE
         BO    DFLTCNFL            IF SO, DO NEXT DEFAULT
         TM    QWDSTATS,QTSOASCB   IS IT TSO
         BO    DFLTTTSO            IF SO, DO TSO TYPE
         TM    QWDSTATS,QVIRG
         BO    DFLTOTHR            IF VIRG DONT INSERT ','
         MVC   QWDDFLT,=CL16',T=ALL'
         B     DFLTTCM1            DO COMMON TYPE NO. 1
DFLTOTHR DS    0H
         MVC   QWDDFLT,=CL16'T=ALL'
         MVC   QWDDFLTL,=F'5'
         B     DFLTTCM2            DO COMMON TYPE NO. 2
DFLTTTSO DS    0H
         MVC   QWDDFLT,=CL16'T(ALL)' DEFAULT DATA
DFLTTCM1 DS    0H
         MVC   QWDDFLTL,=F'6'      DEFAULT LEN
DFLTTCM2 DS    0H
         MVC   QWDDFLTS,=H'3'      SUBFIELD LEN
         LA    R1,QTYPESUB         PDE PTR
         BAL   INVR,DFLT           PROCESS DEFAULT
         SPACE
*  CONFLICT(YES)
DFLTCNFL DS    0H
         TM    QCNFLSUB+6,SUBFPRES DID USER SPECIFY CONFLICT
         BO    DFLTENFR            IF SO, ENFORCE TYPE(ALL)
         TM    QWDSTATS,QVIRG      IS IT A VIRG CMD
         BO    DFLTCY              IF SO, SET COFL TO YES
         TM    QWDSTATS,QTSOASCB   IS IT TSO
         BO    DFLTCTSO            IF SO, DO TSO CONFLICT
         MVC   QWDDFLT,=CL16',C=NO'
         B     DFLTCCM1
DFLTCTSO DS    0H
         MVC   QWDDFLT,=CL16'C(NO)'
DFLTCCM1 DS    0H
         MVC   QWDDFLTL,=F'5'      DFLT LEN
         MVC   QWDDFLTS,=H'2'      SUB FLD LEN
         LA    R1,QCNFLSUB         PDE PTR
         BAL   INVR,DFLT           SET CONFLICT AS NO
         B     DFLTNXT             DO NEXT DEFAULT
DFLTCY   DS    0H
         TM    QWDSTATS,QTSOASCB   IS IT TSO
         BO    DFLTCTS2            IF SO, DO TSO CONFLICT
         MVC   QWDDFLT,=CL16',C=YES'
         B     DFLTCM2             DO CONFLICT COMMON
DFLTCTS2 DS    0H
         MVC   QWDDFLT,=CL16'C(YES)' DEFAULT DATA
DFLTCM2  DS    0H
         MVC   QWDDFLTL,=F'6'      DEFAULT LEN
         MVC   QWDDFLTS,=H'3'      SUB FIELD LEN
         LA    R1,QCNFLSUB         PDE PTR
         BAL   INVR,DFLT           PROCESS DEFAULT
DFLTENFR DS    0H
         L     W0,QCNFLSUB         GET CONF DATA BASE
         CLC   0(3,W0),=C'NO'
         BE    DFLTNXT             IF USER SPECIFIED NO, DO NXT DEFAULT
         L     W0,QTYPESUB         GET SUB CMD BASE
         MVC   0(3,W0),=C'ALL'     ENFORCE TYPE(ALL)
DFLTNXT  DS    0H
         DROP  D1
         SPACE 3
         SPACE
*  BUILD ADDR SPACE TBL  (AST)
         SPACE
         LA    D4,QWDAST           PRE-INIT AST - - - - -
         LA    W0,257              LOOP BOUNTRIES
BASTINIT DS    0H
         MVC   ASTJOB,=CL8'UNKNOWN'
         MVI   ASTSWAP,C' '
         LA    D4,ASTLEN(D4)       NEXT EL
         BCT   W0,BASTINIT         DO IT AGAIN TILL DONE
         SPACE 2
*  OBTAIN JOB NAMES FROM ACTIVE ASCB CHAIN
         SPACE
BAST     DS    0H
         SR    W0,W0               RUN AWAY LOOP CHECK
         L     D0,CVTPTR           CVT BASE
         USING CVT,D0
         L     R1,CVTASCBH         1ST ASCB BASE
         DROP  D0
         LA    R1,0(,R1)           SET FOR WHOLE ASCB CHAIN
         BAL   INVR,ASCP           DO ASCB PROCESSING
         LA    W0,1(,W0)           UP SUPER LOOP COUNT
         C     W0,=F'100000'       COUNT TOO HI
         BH    BASTOUCB            IF SO, KNOCK IT OFF
         C     R15,=F'0'           WAS ASCB XIT GOOD
         BNE   BAST                IF NOT, DO IT ALL OVER AGAIN
         SPACE 2
*  OBTAIN JOB NAMES FROM SWAP QUEUE  (CVT-RSMCT-RSMQH-OUCB-ASCB)
         SPACE
BASTOUCB DS    0H
         L     R15,CVTPTR          CVT BASE
         USING CVT,R15
         L     W0,CVTOPCTP         (RMCT) RESOURCE MNGR CTL TBL
         DROP  R15
         CLC   0(4,W0),=C'RMCT'
         BNE   QGO                 IF NOT RMCT, FORGET SWAP JOBS
         SPACE
*  SWAPPED WAIT JOBS
         L     R15,RMCTWTQE(,W0)   (RMHQ/WTQE) WAIT SWAP HEADER
         CLC   0(4,R15),=C'WTQE'
         BNE   BASTRMQH            IF NO WAIT-SWAP DO NEXT SWAP
         L     R1,RMQHFWD(,R15)    OUCB PTR
         BAL   INVR,OUCP           PROCESS OUCB
         SPACE
*  SWAPPED READY JOBS
BASTRMQH DS    0H
         L     R15,RMCTOTQE(,W0)   (RMQH/OTQE) READY SWAP HEADER
         CLC   0(4,R15),=C'OTQE'
         BNE   QGO                 IF NO READY-SWAP FORGET SWAPS
         L     R1,RMQHFWD(,R15)    OUCB PTR
         BAL   INVR,OUCP           PROCESS OUCB
 TITLE 'QUEUE                      D I S P A T C H E R'
*  PROCESS
QGO      DS    0H
         L     D0,CVTPTR          CVT BASE
         USING CVT,D0
         L     R1,CVTFQCB          INIT NXT MJR QCB PTR
         DROP  D0
         BAL   INVR,MJR            START THE BALL ROLLING
         TM    QWDSTATS,QINFNT     BAD CHAIN FOUND
         BZ    QEND                IF NOT, XIT
         LA    W0,34               MSG LEN
         STH   W0,QWDDLEN
         MVC   QWDDATA,=CL126' '
         MVC   QWDDATA,=CL126'Q0001   INVALID CHAIN DETECTED'
         BAL   INVR,QPUT
         SPACE
*  END PROCESSING
QEND     DS    0H
         TM    QWDSTATS,QIO        WAS ANY IO GIVEN TO USER
         BO    QXIT                IF SO, XIT
         OI    QWDSTATS,QIO        FORCE NO TITLE
         BAL   INVR,BLDXPUT        IF NOT, GIVE MSG TO USER
         SPACE 3
*  Q RETURN
QXIT     DS    0H
         LR    R1,R13              GETMAIN ADDR
         L     R0,QWDLEN           GETMAIN LEN
         L     R13,QWDSAV+4        RESTORE HI SAVE ADDR
         FREEMAIN R,LV=(0),A=(1)
         LM    R14,12,12(R13)
         SR    R15,R15             RC = 0
         BR    R14
 TITLE 'OUCB PROC                  O U C B   P R O C E S S O R'
*  OBTAIN ASCB FROM OUCB THEN PASS CTL TO ASCP
*
*  UPON ENTRY
*
*        R1 CONTAINS THE ADDR OF THE 1ST OUCB IN A SWAPPED OUCB CHAIN
         SPACE
OUCP     DS    0H
         ST    D0,QWDOUCPD         SAVE REGS
         ST    INVR,QWDOUCPR
         USING OUCB,D0
         LR    D0,R1
OUCPLOOP DS    0H
         LA    D0,0(,D0)
         LTR   D0,D0               OUCB XIST
         BZ    OUCPXIT             NO - XIT
         CLC   OUCBNAME,=C'OUCB'   IS IT OUCB
         BNE   OUCPXIT             NO - XIT
         L     R1,OUCBASCB         GET ASCB PTR
         ICM   R1,8,=X'80'         SET AS ONLY 1 ASCB  (DONT CHAIN)
         BAL   INVR,ASCP           DO ASCB
         L     D0,OUCBFWD          NXT OUCB
         B     OUCPLOOP
OUCPXIT  DS    0H
         L     D0,QWDOUCPD         RESTORE REGS
         L     INVR,QWDOUCPR
         BR    INVR
         DROP  D0
 TITLE 'ASCP                       A D D R   S P A C E   C T L'
*  BUILD AST FROM ASCB
*
*  UPON ENTRY
*
*        R1 = ADDR OF ASCB  (HI ORDER BIT ON = ONLY DO 1 ASCB (NO CHN))
*
*  UPON EXIT
*
*        R15 = 4 IF ASCB CHAIN ALTERED BY SYS BEFORE ASCB CHAIN COMPLET
         SPACE
ASCP     DS    0H
         ST    INVR,QWDASCPR       SAVE REGS
         ST    D0,QWDASCPD
         STM   W0,W3,QWDASCPW
         ST    R1,QWDASCPP
         USING ASCB,D0
         SR    W0,W0
         SR    W1,W1
         LA    W2,ASTLEN           AST EL LEN
         SR    W3,W3
         SPACE
*  EL CHECK
ASCPLOOP DS    0H
         LR    D0,R1               ASCB PTR
         CLC   ASCBASCB,=C'ASCB'   IS IT ASCB
         BNE   ASCPGXIT            IF NOT, XIT
         TM    QWDASCPP,X'80'      IS SINGLE ASCB REQUESTED
         BO    ASCPONE1            IF SO, DONT SEQUENCE CHECK
         LA    W3,1(W3)            UP SEQ COUNT
         CH    W3,ASCBSEQN         DID SYS ALTER ASCB CHAIN BEFORE END
         BNE   ASCPBXIT            IN SO, BAD XIT
         SPACE
*  GET AST EL BASE
ASCPONE1 DS    0H
         LH    W1,ASCBASID         ASID
         CH    W1,=H'256'          ASID OVER 256
         BH    ASCPRED1            IF SO, GET NEXT ASCB
         MR    W0,W2               ASID * ASTLEN = AST DSPL (W1)
         LA    D4,QWDAST           AST BASE
         AR    D4,W1               AST PTR + ASTEL DSPL = AST EL PTR
         SPACE
*  'INIATED' JOB NAME
         L     W0,ASCBJBNI         INITATED JOB PTR
         LTR   W0,W0               IS IT THERE
         BZ    ASCPSML             IF NOT, ITS START/MOUNT/LOGON
         MVC   ASTJOB,0(W0)        INITIATED JOB NAME
         B     ASCPREDO            DO NEXT ASCB
         SPACE
*  'START/MOUNT/LOGON' JOB NAME
ASCPSML  DS    0H
         L     W0,ASCBJBNS         START/MOUNT/LOGON JOB NAME PTR
         MVC   ASTJOB,0(W0)        JOB
         SPACE
*SET UP FOR NEXT EL
ASCPREDO DS    0H
         TM    QWDASCPP,X'80'      IS IT A SINGLE ASCB REQUEST
         BNO   ASCPRED2            IN NOT, DO NEXT ASCB
         MVI   ASTSWAP,C'*'        INDICATE JOB SWAPPED
         B     ASCPGXIT            GOOD XIT
ASCPRED1 DS    0H
         TM    QWDASCPP,X'80'      IS SINGLE ASCB REQUESTED
         BO    ASCPGXIT            IF SO, GOOD XIT
ASCPRED2 DS    0H
         L     R1,ASCBFWDP         NEXT ASCB
         LA    R1,0(,R1)
         LTR   R1,R1
         BP    ASCPLOOP            DO NEXT ASCB
         CH    W3,ASCBSEQN         STILL THE SAME ASCB (DID SYS CHG IT)
         BE    ASCPGXIT            IF SO, GOOD XIT
         SPACE
*  ASCP XIT
ASCPBXIT DS    0H                  BAD XIT
         LA    R15,4               SET RC
         B     ASCPXIT             XIT
ASCPGXIT DS    0H                  GOOD XIT
         SR    R15,R15             SET RC
ASCPXIT  DS    0H
         L     INVR,QWDASCPR       RESTORE REGS
         L     D0,QWDASCPD
         LM    W0,W3,QWDASCPW
         BR    INVR
         DROP D0
 TITLE 'MJR                        M A J O R   Q C B'
*  IS THIS A DESIRED MAJOR?  IF SO, PASS CTL TO ASSOC MINOR
*
*  UPON ENTRY
*
*        R1 MUST CONTAIN THE ADDR OF THE 1ST MAJOR QCB
*        D1 CONTAINS THE PDL PTR   (PARSE OUTPUT)
         SPACE
*  INIT
MJR      DS    0H
         ST    INVR,QWDMJRR        SAVE REGS
         ST    D0,QWDMJRD
         STM   W0,W2,QWDMJRW
         USING QCB,D0              MAJOR QCB BASE
         USING QPDLD,D1            PARSE OUTPUT (PDL)
         SPACE
*  IS THIS MJR SELECTED
MJRLOOP  DS    0H
         LR    D0,R1               SET QCB BASE
         TM    QMAJSUB+6,SUBFPRES  IS USER PARTICULAR
         BZ    MJRHIT              IF NOT, THIS MAJOR IS SELECTED
         SR    W0,W0               IF SO, SET UP FOR MJR COMPARE
         SR    W1,W1
         LH    W0,QMAJSUB+4        USR MAJ LEN
         BCTR  W0,R0               -1
         L     W1,QMAJSUB          USR MAJ PTR
         LA    W2,MAJNAME          QCB MAJ PTR
         EX    W0,QCLC             IS IT A HIT
         BNE   MJRNMJR             IF NOT, CHECK NEXT MJR
         SPACE
*  MJR HIT
MJRHIT   DS    0H
         MVC   QWDMJRNM,MAJNAME    SAVE MJR NAME
         TADDR MAJFMIN,BZ=MJRBAD   GET 1ST MNR PTR, IF 0, ITS BAD
         BAL   INVR,MNR            DO MINOR QCB
         SPACE
*  NEXT MAJOR SETUP
MJRNMJR  DS    0H
         TM    QWDSTATS,QINFNT     ANY BAD CHAINS
         BO    MJRXIT              IF SO, XIT
         LOOPC QWDMJRLP,NAM=MJR    OVER 1000 LOOP CHECK
         TADDR MAJNMAJ,BNZ=MJRLOOP GET NXT QCB PTR
         B     MJRXIT
         SPACE
*  MAJOR HAS NO CONNECTION MINOR QCB
MJRBAD   DS    0H
         OI    QWDSTATS,QINFNT     SET BAD BIT
         SPACE
*  MJR XIT
MJRXIT   DS    0H
         L     INVR,QWDMJRR        RESTORE GEGS
         L     D0,QWDMJRD
         LM    W0,W2,QWDMJRW
         BR    INVR
         DROP  D0
         DROP  D1
 TITLE 'MNR                        M I N O R   Q C B'
*  IS THIS A DESIRED MINOR?  IF SO, PASS CTL TO ASSOC QEL
*
*  UPON ENTRY
*
*    R1 MUST CONTAIN THE ADDR OF THE 1ST MINOR
*    D1 CONTAINS THE PDL PTR   (PARSE OUTPUT)
         SPACE
*  INIT
MNR      DS    0H
         ST    INVR,QWDMNRR        SAVE REGS
         ST    D0,QWDMNRD
         STM   W0,W2,QWDMNRW
         XC    QWDMNRLP,QWDMNRLP   RESET LOOP CNTR
         USING MIN,D0              MINOR QCB DSECT
         USING QPDLD,D1            PARSE DESCRIPTOR LIST DSECT
         SPACE
*  IS THIS MNR SELECTED
MNRLOOP  DS    0H
         LR    D0,R1               SET MNR QCB BASE
         TM    QMINSUB+6,SUBFPRES  IS USER PARTICULAR
         BZ    MNRHIT              IF NOT, THIS MNR QCB IS SELECTED
         SR    W0,W0               IF SO, SET UP FOR MNR COMPARE
         SR    W1,W1
         LH    W0,QMINSUB+4        USR MNR LEN
         BCTR  W0,R0               -1
         L     W1,QMINSUB          USR MNR PTR
         LA    W2,MINNAME          QCB MNR PTR
         EX    W0,QCLC             IS IT A HIT
         BNE   MNRNMNR             IF NOT, CHECK NEXT MNR
         SPACE
*  MNR HIT
MNRHIT   DS    0H
         CLC   QWDMJRNM,=CL8'SYSZOPEN' IS THIS THE FUNNY GUY
         BNE   MNRHNORM            IF NOT, CONTINUE
         MVC   QWDMNRNM(14),=C'NOT APPLICABLE' DONT USE ITS MINOR
         MVC   QWDMNRLN,=F'14'     SYSZOPEN DOES NOT USE A MINOR
         B     MNRQEL              SO DONT USE RESIDUE FROM LAST USER
MNRHNORM DS    0H
         SR    W0,W0
         IC    W0,MINNAMEL         LEN
         C     W0,=F'108'          IS IT OVER MY MAX MNR NAME LEN
         BL    MNRHOK              IF NOT, CONTINUE AS NORMAL
         LA    W0,107              IF SO, ENFORCE AT MY MAX
MNRHOK   DS    0H
         ST    W0,QWDMNRLN         SAVE LEN
         BCTR  W0,R0               -1
         LA    W1,MINNAME          QCBNAME PTR
         EX    W0,QMNRMVC          SAVE NAME
MNRQEL   DS    0H
         TADDR MINFQEL,BZ=MNRBAD   GET 1ST QEL PTR, IF 0, ITS BAD
         BAL   INVR,QL             DO QEL'S
         SPACE
*  NEXT MINOR SETUP
MNRNMNR  DS    0H
         MVC   QWDMNRNM,=CL8' '    BLANK MNR NAME
         TM    QWDSTATS,QINFNT     ANY BAD CHAINS
         BO    MNRXIT
         LOOPC QWDMNRLP,NAM=MNR    OVER 1000 LOOP CHECK
         TADDR MINNMIN,BNZ=MNRLOOP GET NEXT QCB PTR
         B     MNRXIT              IF NO MORE, XIT
         SPACE
*  MINOR HAS NO CONNECTION QEL
MNRBAD   DS    0H
         OI    QWDSTATS,QINFNT     SET BAD BIT
         SPACE
*  MNR XIT
MNRXIT   DS    0H
         L     INVR,QWDMNRR        RESTORE REGS
         L     D0,QWDMNRD
         LM    W0,W2,QWDMNRW
         BR    INVR
         DROP  D0
         DROP  D1
 TITLE 'QL                         Q U E U E   E L'
*  IS THIS A DESIRED QEL?  IF SO-
*  IS THIS A DESIRED JOB?  IF SO-
*  DO CONFLICT OPTIONS MATCH?  IF SO-
*  PASS CTL TO OUTPUT ROUTINE
*
*  UPON ENTRY
*
*        R1 MUST COTAIN THE ADDR OF THE 1ST QEL
*        D1 CONTAINS THE PDL PTR   (PARSE OUTPUT)
         SPACE
*  INIT
QL       DS    0H
         ST    INVR,QWDQLR         SAV REGS
         ST    D0,QWDQLD
         STM   W0,W2,QWDQLW
         XC    QWDQLLP,QWDQLLP     CLEAR LOOP CNTR
         MVC   QWDQLC,=CL3' '      PRIME ACTIVE/WAIT STATUS
         MVC   QWDQLC2,=CL3' '     PRIME NEXT ACTIVE WAIT STATUS
         USING QEL,D0              QEL DSECT
         USING QPDLD,D1            PARSE DESCRIPTER LIST DSECT
         SPACE
*  IS THIS TYPE SELECTED
QLLOOP   DS    0H
         LR    D0,R1               SET QL BASE
         L     W0,QTYPESUB         GET USER TYPE BASE
         CLC   0(3,W0),=C'ALL'     DOES USR WANT ALL TYPES
         BE    QLTHIT              IF SO, THIS QEL TYPE IS GOOD
         CLC   0(3,W0),=C'RSV'     DOES USR WANT RSV ONLY
         BE    QLRSV               IF SO, CHECK FOR RESV
         CLC   0(3,W0),=C'SHR'     DOES USR WANT SHR ONLY
         BE    QLSHR               IF SO, DO SHR
         CLC   0(3,W0),=C'XCL'     DOES USER WANT XCL ONLY
         BE    QLXCL               IF SO, DO XCL
         B     QLTHIT              SUBFIELD NOT VALID, GIVE USER ALL
QLSHR    DS    0H                  - - - -   SHR   - - - -
         TM    QELQFLGS,SHR        IS IT SHARED
         BO    QLTHIT              IF SO, THIS QEL IS GOOD
         B     QLNQL               IF NOT, DO NEXT QEL
QLRSV    DS    0H                  - - - -   RSV   - - -
         TM    QELQFLGS,RSV        IS IT RESERVED
         BZ    QLNQL               IF NOT, DO NEXT QEL
         B     QLTHIT              IF SO, THIS QEL TYPE IS GOOD
QLXCL    DS    0H                  - - - -   XCL   - - -
         TM    QELQFLGS,SHR        IS IT SHARED
         BO    QLNQL               IF SO, ITS NOT XCL, SO DO NEXT QEL
         SPACE
*  QEL HIT  (TYPE)
QLTHIT   DS    0H
         TM    QELQFLGS,RSV        IS IT RSV
         BO    QLTHRSV             IF SO, DO SAV RSV TYPE
         TM    QELQFLGS,SHR        IS IT SHR
         BO    QLTHSHR             IF SO, SAVE SHR TYPE
         MVC   QWDDSPNM(7),=CL7'  XCL'
         B     QLJOBCHK            GO CHECK JOB
QLTHSHR  DS    0H
         MVC   QWDDSPNM(7),=CL7'  SHR'
         B     QLJOBCHK            GO CHECK JOB
QLTHRSV  DS    0H
         TM    QELQFLGS,SHR        IS IT SHR/RSV
         BZ    QLTHXCL             IF NOT, GO DO XCL/RSV
         MVC   QWDDSPNM,=C'SHR/RSV'
         B     QLJOBCHK            GO CHECK JOB
QLTHXCL  DS    0H
         MVC   QWDDSPNM,=C'XCL/RSV'
         SPACE
*  IS THIS JOB SELECTED
QLJOBCHK DS    0H
         SR    W1,W1               SET UP AST BASE - - - -
         LH    W1,QELASID          ASID
         M     W0,=A(ASTLEN)       ASID * AST EL LEN = AST DSPL  (W1)
         LA    D4,QWDAST           AST BASE
         AR    D4,W1               AST BASE + DSPL = AST EL PTR
         TM    QJOBSUB+6,SUBFPRES  IS USER PARTICULAR
         BZ    QLJHIT              IF NOT, THE EL IS GOOD
         SR    W0,W0               SET UP FOR JOB CMP- - -
         LH    W0,QJOBSUB+4        USER JOB LEN
         BCTR  W0,R0               -1
         L     W1,QJOBSUB          USER JOB PTR
         EX    W0,QCLCJOB          IS IT A HIT
         BNE   QLNQL               IF NOT, DO NEXT QEL
         SPACE
*  QEL HIT  (JOB)
QLJHIT   DS    0H
         MVC   QWDJOBSW,ASTSWAP    SWAP INDICATOR
         MVC   QWDJOBNM,ASTJOB     JOB
         SPACE
*  IS THIS A QUEUE CONFLICT
         L     W0,QCNFLSUB         GET SUBFIELD BASE
         CLC   0(2,W0),=C'NO'      IS USER PARTICULAR
         BE    QLCHIT              IF NOT, ITS GOOD
         TM    QWDSTATS,QCONF      IS A CONFLICT ALREADY KNOWN
         BO    QLCHIT              IF SO, ITS GOOD
         L     W0,QELNQEL          NXT QEL IN CHAIN
         LTR   W0,W0               IS THERE AONTHER QEL
         BZ    QLNQL               IF NOT, CONFLICT IS IMPODDIBLE
         MVC   QWDQLC,=C'(O)'      SET CONFLICT STATUS
         MVC   QWDQLC2,=C'(O)'     SET NEXT CONFLICT STATUS
         TM    QELQFLGS,SHR        IS THIS QEL SHARED
         BO    QLCXCL              IF SO, CHECK OPPOSIT OF SHR
         MVC   QWDQLC2,=C'(W)'     SET FOR NEXT STATUS (NOT THIS QEL)
         OI    QWDSTATS,QCONF      SET CONFLICT FOUND BIT
         B     QLCHIT
QLCXCL   DS    0H
         TM    QELFDSPL(W0),SHR    IS NXT QEL SHR
         BO    QLNQL               IF SO, NO CONFLICT
         MVC   QWDQLC2,=C'(W)'     SET FOR NEXT STATUS (NOT THIS QEL)
         OI    QWDSTATS,QCONF      SET CONFLICT BIT ON
         SPACE
*  QEL HIT  (CONFLICT)
QLCHIT   DS    0H
         BAL   INVR,BLDQPUT        PUT QEL EL
         SPACE
*  NEXT QEL SETUP
QLNQL    DS    0H
         MVC   QWDQLC,QWDQLC2      SET NEXT STATUS FOR NEXT QEL
         LOOPC QWDQLLP,NAM=QL      OVER 1000 LOOP CHECK
         TADDR QELNQEL,BNZ=QLLOOP  GET NEXT QEL PTR
         SPACE
*  QL XIT
QLXIT    DS    0H
         NI    QWDSTATS,QCONFOFF   TURN OF CONFLICT BIT
         L     INVR,QWDQLR         RESTORE REGS
         L     D0,QWDQLD
         LM    W0,W2,QWDQLW
         BR    INVR
         DROP  D0
         DROP  D1
 TITLE 'BLDQPUT                    F O R M A T   O U T P U T'
*  UPON ENTRY
*
*  THE FOLLOWING FIELDS MUST BE PRIMED
*
*        QWDMJRNM                  MAJOR QNAME
*        QWDMNRLN                  MINOR QNAME LEN
*        QWDMNRNM                  MINOR QNAME
*        QWDJOBNM                  JOB NAME
*        QWDDSPNM                  TYPE  (SHR/RSV/XCL)
         SPACE
BLDQPUT  DS    0H
         ST    INVR,QWDBQR         SAVE REGS
         STM   W0,W1,QWDBQW
         MVC   QWDDATA,=CL126' '   BLANK OUT OUTPUT AREA
         SPACE
*  1ST TIME FOR EACH MAJOR
         CLC   QWDBQMJ,QWDMJRNM    MJR CTL NAME THE SAME
         BE    BLDQDUPM            IF SO, DO DUPLICATE
         MVC   QWDBQMJ,QWDMJRNM    SET CTL TO NEW MAJOR
         MVC   QWDDATA(8),QWDBQMJ  MAJOR
         MVC   QWDBQMN(4),=X'12345678' ENSURE MINOR WILL BE 1ST TIME
         B     BLDQCOMN            DO COMMON CODE
         SPACE
*  '2ND ON' TIME FOR EACH MINOR
BLDQDUPM DS    0H
         MVC   QWDDATA(8),=CL8'- DUP'
         SPACE
*  COMMON PROCESSING
BLDQCOMN DS    0H
         MVC   QWDDATA+10(7),QWDDSPNM DISPOSITION
         MVC   QWDDATA+18(1),QWDJOBSW SWAP INDICATOR
         MVC   QWDDATA+19(8),QWDJOBNM JOB
         MVC   QWDDATA+27(3),QWDQLC STATS
         SPACE
*  1ST TIME FOR EACH MINOR
         CLC   QWDBQMN,QWDMNRNM    MNR CTL SAME
         BE    BLDQDUPJ            IF SO, DO DUPLICATE
         MVC   QWDBQMN,QWDMNRNM    SET CTL TO NEW MINOR
         L     W0,QWDMNRLN         MINLEN
         BCTR  W0,R0               -1
         EX    W0,QMVCMNR           MINOR
         B     BLDQBAL
         SPACE
*  '2ND ON' TIME FOR EACH JOB
BLDQDUPJ DS    0H
         MVC   QWDDATA+31(8),=CL8'- DUP'
*  PUT QEL
BLDQBAL  DS    0H
         MVC   QWDDLEN,=H'130'     SET UP LINE SIZE
         BAL   INVR,QPUT
         SPACE
BLDWXIT  DS    0H
         L     INVR,QWDBQR         RESTORE REGS
         LM    W0,W1,QWDBQW
         BR    INVR
 TITLE 'BLDXPUT                    N O T   F O U N D   M S G'
*  PUT Q INFO NOT FOUND MSG
*
*  UPON ENTRY
*
*        D1 CONTAINS ADDR OF PARSE OUTPUT
         SPACE
BLDXPUT  DS    0H
         ST    INVR,QWDBXPR        SAVE REGS
         STM   W0,W1,QWDBXPW
         MVC   QWDDATA,=CL126' '
         SPACE
*  CMD TO MSG OUPUT
BLDXUSR  DS    0H
         SR    W0,W0
         L     W1,CPPLCBUF         CMD BUFF ADDR
         LH    W0,0(,W1)           GET CMD LEN
         S     W0,=F'7'            -1 FOR EX INST, -6 FOR HDR/CMD NAME
         C     W0,=F'97'           IS LEN-1 OVER MAX
         BL    BLDXUOK             IF NOT, ITS OK
         L     W0,=F'96'           IF SO, ENFORCE MAX LEN-1
BLDXUOK  DS    0H
         EX    W0,QTR              TRANSLATE CMD TO UPPER CASE
         EX    W0,QMVC             MOVE CMD TO OUTPUT AREA
         SPACE
*  ATTACH 'NOT FOUND' TO MSG OUTPUT
         LA    W1,QWDDATA          DATA LINE
         AR    W1,W0                 + DATA LEN-1
         LA    W1,4(W1)            UP IT 4 BYTES TO TAG WITH MSG
         MVC   0(17,W1),=C'- - NOT FOUND - -'
         LA    W0,21(W0)           GET TOTAL LEN OF MSG
         STH   W0,QWDDLEN          GIVE IT TO PUTLINE ROUTINE
         BAL   INVR,QPUT           PUT THE MSG
         SPACE
*  BLDXPUT XIT
BLDXXIT  DS    0H
         L     INVR,QWDBXPR        RESTORE REGS
         LM    W0,W1,QWDBXPW
         BR    INVR
 TITLE 'JESOP                      J E S   O P E R A N D S'
*  BUILD AND CONNECT OPERANDS FROM JES 'COMMAND'
*
*  UPON ENTRY
*
*        R1    CUR OPERAND PTR PTR
*        D1    CONTAINS PDL PTR
*        VIRG CMD AND PTRS MUST BE PRE SET
         SPACE
JESOP    DS    0H
         USING QPDLD,D1
         ST    INVR,QWDJESOR       SAVE REGS
         STM   W0,W1,QWDJESOW
         SPACE
*  LEN CHECK
         LM    W0,W1,0(R1)         CUR OP AND NXT OP PTRS
         SR    W1,W0               CUR OP LEN
         C     W1,=F'3'            IS LEN LESS THAN MININUM
         BL    JESOPXIT            IF SO, FORGET THIS OPERAND
         SPACE
*  MOVE OPERAND TO CMD BUF AND CONNECT ALL PTRS
         LR    R15,W0              CUR OP PTR
         AR    R15,W1              OP PTR + OP LEN = 1 BYTE PAST OP
         BCTR  R15,R0              -1 = LAST BYTE OF OP
         CLI   0(R15),C' '         IS LAST BYTE A BLANK
         BNE   JESSOPNF            IF NOT, DONT FIX IT
         BCTR  W1,R0               ELIM BLANK BYTE
JESSOPNF DS    0H
         BAL   INVR,JESSOP         GET SUB-OPERAND LEN
         LTR   R15,R15             SUB OPERAND GOOD
         BNZ   JESOPXIT            IF NOT, XIT
         STH   R1,QWDDFLTS         SUBFIELD LEN
         ST    W1,QWDDFLTL         OPERAND LEN
         BCTR  W1,R0               OP LEN -1
         C     W1,=F'113'          MAX LEN
         BL    JESSOPOK            IF NOT, LEAVE IT AS IS
         L     W1,=F'112'          SET AT MAX
JESSOPOK DS    0H
         EX    W1,QJESCMVC         OPERAND DATA
         CLI   0(W0),C'J'          JOB
         BE    JESOJOB
         CLI   0(W0),C'T'          TYPE
         BE    JESOTYPE
         CLI   0(W0),C'C'          CONFLICT
         BE    JESOCONF
         CLC   0(2,W0),=C'MA'      MAJOR
         BE    JESOMJR
         CLC   0(2,W0),=C'MJ'      MAJOR
         BE    JESOMJR
         SPACE
JESOMNR  DS    0H
         LA    R1,QMINSUB          MNR PDE
         B     JESOBLD
JESOMJR  DS    0H
         LA    R1,QMAJSUB          MJR PDE
         B     JESOBLD
JESOCONF DS    0H
         LA    R1,QCNFLSUB         CONFLICT PDE
         B     JESOBLD
JESOTYPE DS    0H
         LA    R1,QTYPESUB         TYPE PDE
         B     JESOBLD
JESOJOB  DS    0H
         LA    R1,QJOBSUB          JOB PDE
JESOBLD  DS    0H
         BAL   INVR,DFLT           LET DFLT PROCESSOR DO ALL THE WORK
         SPACE
*  XIT
JESOPXIT DS    0H
         L     INVR,QWDJESOR       RESTORE REG
         LM    W0,W1,QWDJESOW
         BR    INVR
         DROP  D1
 TITLE 'JESSOP                     J E S   S U B - O P E R A N D S'
*  DETERMINE LENTH OF SUB-OPERAND  (1 BYTE BEFORE SUB-OP IS A '=')
*
*  UPON ENTRY
*
*        W0    OPERAND PTR
*        W1    OPERAND LEN
*
*  UPON EXIT
*
*        R1    SUB FIELD LEN
*        R15   0 IF GOOD, NOT 0 IF BAD
         SPACE
JESSOP   DS    0H
         ST    INVR,QWDJESPR       SAVE REGS
         STM   W0,W1,QWDJESPW
         LR    R14,W0              OP PTR
         LR    R15,W1              OP LEN
         SPACE
*  LOCATE '='
JESSOP10 DS    0H
         CLI   0(R14),C'='         IS IT A HIT
         BE    JESSOP20            HIT, GO
         LA    R14,1(R14)          UP BASE
         BCT   R15,JESSOP10        CHECK NEXT BYTE
         SR    R1,R1               NOT FOUND, 0 LEN RETURN
         LA    R15,4               SET RC TO NON ZERO
         B     JESSOPXT            XIT
         SPACE
*  HIT
JESSOP20 DS    0H
         AR    W0,W1               OP PTR + OP LEN = 1 PAST OPERAND
         BCTR  W0,R0               -1 = PTR TO LAST BYTE OF OPERAND
         CLI   0(W0),C','          IS LAST CHAR A ','
         BNE   JESSOP30            IF NOT, END PTR IS GOOD
         BCTR  W0,R0               BACK UP END PTR 1 BYTE BEFORE ','
JESSOP30 DS    0H
         SR    W0,R14              END SUB PTR - START SUB PTR = SUBLEN
         SPACE
*  END
         C     W0,=F'98'           MAX SUBFIELD LEN
         BL    JESSOPX1            LEAVE LEN AS IS IF NOT OVER MAX
         L     W0,=F'98'           SET MAX LEN
JESSOPX1 DS    0H
         LR    R1,W0               SET RETURN LEN
         SLR   R15,R15             0 RETURN CODE
JESSOPXT DS    0H
         L     INVR,QWDJESPR       RESTORE REGS
         LM    W0,W1,QWDJESPW
         BR    INVR
  TITLE 'DFLT                      D E F A U L T'
*  PROCESS DEFAULT INITIALIZATION
*
*  UPON ENTRY
*
*    R1        PTR TO RELATED PDE
*    QWDDFLT   PRIMED WITH DEFAULT DATA
*    QWDDFLTL  PRIMED WITH DEFAULT LEN
*    QWDDFLTS  PRIMED WITHE SUBFIELD LEN
         SPACE
DFLT     DS    0H
         ST    INVR,QWDDFLTR       SAVE REGS
         STM   W0,W2,QWDDFLTW
         SPACE
*  VALIDITY CHECK
DFLTVALD DS    0H
         SR    W0,W0
         L     W2,CPPLCBUF         CMD BUFF ADDR
         LH    W0,0(,W2)           CMD LEN
         A     W0,QWDDFLTL         +DFLT LEN = TOT LEN
         TM    QWDSTATS,QTSOASCB   IS THIS TSO
         BNO   DFLTNTS1            IF NOT, DONT COUNT FOR A SPACE
         LA    W0,1(W0)            TOT LEN + 1 SPACE AFTER LAST SUB CMD
DFLTNTS1 DS    0H
         C     W0,=A(CBUFL)        IS NEW LEN TO BIG
         BH    DFLTXIT             IF SO, FORGET THIS DEFAULT
         SPACE
*  MOVE DFLT TO CMD BUF
         LR    W1,W2               GET BUF ADDR
         AH    W1,0(,W2)           UP BUF BASE PAST LAST SUB CMD
         TM    QWDSTATS,QTSOASCB   IS THIS TSO
         BNO   DFLTNTSO            IF NOT, DONT UP CMD BUF BASE PTR
         LA    W1,1(W1)            UP BUF BASE 1 BYTE PAST LAST SUB CMD
DFLTNTSO DS    0H
         STH   W0,0(,W2)           UPDATE CMD LEN
         SH    W0,=H'4'            - DESCRIPTOR WORD FORM CMD LEN
         STH   W0,2(,W2)           UPDATE USER CMD LEN
         L     W0,QWDDFLTL         GET DFLT LEN
         BCTR  W0,R0               -1
         EX    W0,QDFLTMVC         MOVE IN DFLT SUB CMD TO CMD BUFFER
         SPACE
*  CONNECT PDE TO SUB CMD
         LR    W1,W2               GET BUFFER ADDR
         AH    W1,0(,W2)           BUF PTR + CMD LEN = CMD END PTR
         BCTR  W1,R0               -1 FROM END PTR (PTR TO RIGHT PAREN)
         TM    QWDSTATS,QTSOASCB   IS IT TSO
         BO    DFLTYTSO            IF SO, CMD END PTR IS GOOD
         CLI   0(W1),C','          IF NOT, IS IT AN ENDING DELIMETER
         BE    DFLTYTSO            IF SO, CMD END PTR IS GOOD
         LA    W1,1(W1)            IF NOT, THIS CHAR IS SIGNIFICANT
DFLTYTSO DS    0H
         SH    W1,QWDDFLTS         END PTR - SUBCMD LEN = SUB CMD PTR
         ST    W1,0(,R1)           PUT SUBFIELD PTR IN PDE
         MVC   4(2,R1),QWDDFLTS    PUT SUBFIELD LEN
         OI    6(R1),SUBFPRES      SET SUBFIELD AS PRESENT
         SPACE
*  DFLT XIT
DFLTXIT  DS    0H
         L     INVR,QWDDFLTR       RESTORE GEGS
         LM    W0,W2,QWDDFLTW
         BR    INVR
 TITLE 'QPUT                       Q P U T'
*  UPON ENTRY
*
*        QWDIOLIN ASSUMED TO BE PRIMED
         SPACE
*  TITLE CHECK
QPUT     DS    0H
         ST    INVR,QWDQPUTR       SAVE RETURN ADDR
         STM   W0,W2,QWDQPUTS      SAVE WORK REGS
         ST    D0,QWDQPUTD         SAVE DSECT BASE
         TM    QWDSTATS,QIO+QINFNT IS TITL PRESENT OR IS THIS AN ERRMSG
         BC    MO,QPUTNTTL         IF SO, DONT BUILD TITLE
         MVC   QWDQPUTA,QWDQPUTR   ALLOW RECURSIVE ENTRY INTO QPUT
         MVC   QWDQPUTB,QWDIOLIN
         OI    QWDSTATS,QIO        SET AS IO COMPLETE
         LA    W0,60               GET TITLE LEN
         STH   W0,QWDDLEN
         MVC   QWDDATA+49(L'QWDDATA-50),=CL126' '
         MVC   QWDDATA(49),=C'MAJOR- -   DISP    JOB- - -    MINOR- - -X
                - - - -'
         BAL   INVR,QPUT           PUT MSG
         MVC   QWDQPUTR(20),QWDQPUTA RESTORE SAVE AREAS
         MVC   QWDIOLIN,QWDQPUTB
         SPACE
*  PUT THE OUTPUT LINE
QPUTNTTL DS    0H
         TM    QWDSTATS,QTSOASCB   IS IT TSO
         BO    QPUTTSO
         TM    QWDSTATS,QJESASCB   IS IT JES
         BNO   QPUTXIT             IF NOT, FORGET IT
         SPACE
*  'JES' OUTPUT
         L     D0,QWDJESP          SET JES PARM DSECT BASE
         USING JPRM,D0
         L     R1,JPRMID           COMMID PTR MUST BE IN R1 FOR CON
         OI    1(R1),X'0F'         SET $HASP000 ID INSTEAD OF JOB ID
         L     W0,JPRMML           MSG LEN AREA PTR
         MVC   0(1,W0),=FL1'55'    SET MSG LEN
         L     W0,JPRMMSG          MSG AREA PTR
         MVC   0(126,W0),QWDDATA   MOVE IN MESSAGE
         LR    W1,BASE0
         LR    W2,R13
         L     BASE0,JPRMHCT       R11 MUST CONTAIN THE HCT PTR
         L     R13,JPRMPCE         R13 MUST CONTAIN THE PCE PTR
         L     R0,JPRMCFLG         R0  MUST CONTAIN THE COMMFLAG PTR
         L     R15,JPRM$WTO        WTO ROUTINE  (CON)
         BAL   INVR,0(,R15)        CONFORMING TO JES STANDARDS
         LR    BASE0,W1
         LR    R13,W2
         LR    BASE0,W1            JES MIGHT DO A RETURN +4, SO:
         LR    R13,W2              DUP 1ST 4 BYTES AFTER BAL TO HASPCON
         B     QPUTXIT
         DROP  D0
         SPACE
*  'TSO' OUTPUT
QPUTTSO  DS    0H
         L     W0,CPPLUPT          UPT
         L     W1,CPPLECT          ECT
         XC    QWDPPLEC,QWDPPLEC
         LA    W2,QWDPPLEC         ECB ADDR
         PUTLINE PARM=QWDPTPB,UPT=(W0),ECT=(W1),ECB=(W2),              X
               OUTPUT=(QWDIOLIN,TERM,SINGLE,DATA),                     X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               MF=(E,QWDIOPL)
         SPACE
*  QPUT XIT
QPUTXIT  DS    0H
         L     R14,QWDQPUTR        RESTORE REGS
         LM    W0,W3,QWDQPUTS
         L     D0,QWDQPUTD         RESTORE DSECT BASE
         BR    R14
 TITLE 'DATA                        D A T A   C O N S T A N T S'
QWDLEN   DS    0F
         DC    X'00'               SUBPOOL NUM
         DC    AL3(QWDLENX)
QCLC     CLC   0(*-*,W1),0(W2)
QMNRMVC  MVC   QWDMNRNM(*-*),0(W1) SAVE MINOR NAME
QTR      TR    6(*-*,W1),QUPRCASE
QMVC     MVC   QWDDATA(*-*),6(W1)
QMVCMNR  MVC   QWDDATA+31(*-*),QWDMNRNM
QCLCJOB  CLC   ASTJOB(*-*),0(W1)
QDFLTMVC MVC   0(*-*,W1),QWDDFLT
QJESCMVC MVC   QWDDFLT(*-*),0(W0)
QUPRCASE DS    0D
         DC    XL64'00'
         DC    C' '
         DC    XL9'00'
         DC    X'00'
         DC    C'.'
         DC    X'00'
         DC    C'(+'
         DC    X'00'
         DC    C'&&'
         DC    XL9'00'
         DC    C'!$*);'
         DC    X'00'
         DC    C'-/'
         DC    XL9'00'
         DC    C',%_'
         DC    X'00'
         DC    C'?'
         DC    XL10'00'
         DC    C':#@''="'
         DC    X'00'
         DC    C'ABCDEFGHI'
         DC    XL3'00'
         DC    C'(+'
         DC    XL2'00'
         DC    C'JKLMNOPQR'
         DC    XL3'00'
         DC    C')|'
         DC    XL3'00'
         DC    C'STUVWXYZ'
         DC    XL6'00'
         DC    C'0123456789'
         DC    XL5'00'
         DC    C'_'
         DC    X'00'
         DC    C'ABCDEFGHI'
         DC    XL7'00'
         DC    C'JKLMNOPQR'
         DC    XL8'00'
         DC    C'STUVWXYZ'
         DC    XL6'00'
         DC    C'0123456789'
         DC    XL6'00'
         SPACE 3
         SPACE 3
* * * *  PARSE TBLS AND DSECTS
         SPACE 3
*  CREATE PCL CSECT   ((PARM CTL LIST),(CSECT OF PARSE INPUT))
*  CREATE PDL ((PARM DESCRIPTER LIST),(DSECT OF PARSE OUTPUT))
         SPACE 2
QPCL     IKJPARM DSECT=QPDLD
         SPACE 3
         SPACE 3
* * * *  'PRIME' KEYWORDS AND THIER NAMES
         SPACE 3
*  PROVIDE LBL FOR PCE KEYWORD 'MAJOR'  (PARM CTL EL)     MAJOR * * * *
         SPACE 2
QPCEMAJ  IKJKEYWD
         SPACE 3
*  DEFINE 'MAJOR' KEYWORD AND ITS ALIAS
         SPACE 2
         IKJNAME 'MAJOR',SUBFLD=QMAJSUBC,ALIAS=('MAJ','MJR','MJ')
         SPACE 3
         SPACE 3
*  PROVIDE LBL FOR PCE KEYWORD 'MINOR'     MINOR * * * * * * * * * * *
         SPACE 2
QPCEMIN  IKJKEYWD
         SPACE 3
*  DEFINE 'MINOR' KEYWRD AND ITS ALIAS
         SPACE 2
         IKJNAME 'MINOR',SUBFLD=QMINSUBC,ALIAS=('MIN','MNR','MN')
         SPACE 3
         SPACE 3
*  PROVIDE LBL FOR PCE KEYWORD 'JOB'      JOB * * * * * * * * * * * * *
         SPACE 2
QPCEJOB  IKJKEYWD
         SPACE
*  DEFINE 'JOB' KEYWORD AND ITS ALIAS
         SPACE 2
         IKJNAME 'JOB',SUBFLD=QJOBSUBC,ALIAS=('JB','J')
         SPACE 3
         SPACE 3
*  PROVIDE LBL FOR PCE KEYWORD 'TYPE'     TYPE   * * * * * * * * * * *
         SPACE 2
QPCETYPE IKJKEYWD
         SPACE
*  DEFINE 'TYPE' KEYWORD AND IST ALIAS
         SPACE 2
         IKJNAME 'TYPE',SUBFLD=QTYPSUBC,ALIAS=('T')
         SPACE 3
*  PROVIDE LBL FOR PCE KEYWORD 'CONFLICT'   CONFLICT * * * * * * * * *
         SPACE 2
QPCECNFL IKJKEYWD
         SPACE 2
*  DEFINE 'CONFLICT' KEYWORD AND ITS ALIAS
         SPACE 2
         IKJNAME 'CONFLICT',SUBFLD=QCNFSUBC,                           X
               ALIAS=('CONFLCT','CONFL','CONF','CNFLCT','CNFL',        X
               'CNF','CF','C')
         SPACE 3
         SPACE 3
* * * *  'SUBFIELDS' AND THIER KEYWORDS AND NAMES
         SPACE 3
*  CONNECT SUBFIELD TO 'MAJOR'   MAJOR  * * * * * * * * * * * * * * * *
         SPACE 2
QMAJSUBC IKJSUBF
         SPACE
*  DEFINE SUBFIELD OF 'MAJOR' KEYWORD AND PROVIDE LBL FOR ITS PCE
         SPACE 2
QMAJSUB  IKJIDENT 'MAJOR',UPPERCASE,FIRST=ANY,OTHER=ANY,               X
               PROMPT='MAJOR SUBFIELD',                                X
               HELP=('A MAJOR QNAME AS THE SUBFIELD OF MAJOR, MAJ, MJR,X
                OR MJ')
         SPACE 3
         SPACE 3
*  CONNECT SUBFIELD TO 'MINOR'   MINOR  * * * * * * * * * * * * * * * *
         SPACE 2
QMINSUBC IKJSUBF
         SPACE
*  DEVINE SUBFIELD OF 'MINOR' DEYWORD AND PROVIDE LBL FOR ITS PCE
         SPACE 2
QMINSUB  IKJIDENT 'MINOR',UPPERCASE,FIRST=ALPHA,OTHER=ANY,             X
               PROMPT='MINOR SUBFIELD',                                X
               HELP=('A MINOR QNAME AS THE SUBFIELD OF MINOR, MIN, MNR,X
                OR MN')
         SPACE 3
         SPACE 3
*  CONNECT SUBFIELD TO 'JOB'   JOB  * * * * * * * * * * * * * * * * * *
         SPACE 2
QJOBSUBC IKJSUBF
         SPACE
*  DEFINE SUBFIELD OF 'JOB' KEYWORK AND PROVIDE LBL FOR ITS PCE
QJOBSUB  IKJIDENT 'JOB',UPPERCASE,MAXLNTH=8,FIRST=ANY,OTHER=ANY,       X
               PROMPT='JOB SUBFIELD',                                  X
               HELP=('A JOB NAME AS THE SUBFIELD OF JOB, JB, OR J')
         SPACE 3
         SPACE 3
*  CONNECT SUBFIELD TO 'TYPE'   TYPE  * * * * * * * * * * * * * * * * *
         SPACE 2
QTYPSUBC IKJSUBF
         SPACE
*  DEFINE SUBFIELD 'TYPE' KEYWORD AND PROVIDE LBL FOR ITS PCE
QTYPESUB IKJIDENT 'TYPE',UPPERCASE,MAXLNTH=3,FIRST=ALPHA,OTHER=ALPHA,  X
               PROMPT='TYPE SUBFIELD',                                 X
               HELP=('DISPOSITION  - RSV/XCL/SHR/ALL -')
         SPACE 3
         SPACE 3
*  CONNECT SUBFIELD TO 'CONFLICT'   CONFLICT * * * * * * * * * * * * *
         SPACE 2
QCNFSUBC IKJSUBF
         SPACE 2
*  DEFINE SUBFIELD OF 'CONFLICT' KEYWORD AND PROVIDE LBL FOR ITS PCE
         SPACE 2
QCNFLSUB IKJIDENT 'CONFLICT',UPPERCASE,MAXLNTH=3,FIRST=ALPHA,          X
               OTHER=ALPHA,PROMPT='YES OR NO FOR KEYWORD CONFLICT',    X
               HELP=('DO YOU WANT QUEUE CONFLICTS ONLY, ENTER ''YES OR X
               NO''')
         SPACE 3
         SPACE 3
* * * *  END THE 'PCL'
         SPACE 3
         IKJENDP
 TITLE 'LTORG                      L T O R G'
         LTORG
 TITLE 'LOCAL                      D S E C T S'
* * * *  AST   ADDR SPACE TBL  (ASCENDING SEQUENCE BY ASTID)
         SPACE
AST      DSECT
ASTJOB   DS    CL8                 ADDR SPACE JOB NAME
ASTSWAP  DS    C                   * =SWAPPED OUT
ASTLEN   EQU   *-AST               AST EL LEN
         SPACE 3
* * * *  JPRM  INPUT FROM JES
JPRM     DSECT
JPRMOP   DS    F                   PTR TO OPERAND PTR TBL
JPRMOPN  DS    F                   PTR TO LAST OP PTR IN PTR TBL
JPRMOPL  DS    F                   LEN OF OPERAND TBL EL
JPRMID   DS    F                   COMMID PTR   (2 BYTE FLAGS)
JPRMML   DS    F                   MSG LEN PTR  (1 BYTE LEN OF MSG)
JPRMMSG  DS    F                   MSG AREA PTR
JPRMCFLG DS    F                   COMFLAG PTR  (MUST BE IN R0 FOR CON)
JPRMPCE  DS    F                   PCE PTR     (MUST BE IN R13 FOR CON)
JPRMHCT  DS    F                   HCT PTR     (MUST BE IN R11 FOR CON)
JPRM$WTO DS    F                   $WTO PTR     (BALR ADDR)
         SPACE 3
* * * *  QWD                       Q WORK DSECT
         SPACE
QWD      DSECT
         SPACE
*  PGM REG SAVE ARES
QWDSAV   DS    18F                 REG SAV AREA
         SPACE
*  'ASCP' SAVE AREA
QWDASCPD DS    F                   DSECT
QWDASCPR DS    F                   RETURN
QWDASCPW DS    4F                  WORK
QWDASCPP DS    F                   PARM (R1)
         SPACE
*  'DFLT'  SAVE AREA
QWDDFLTR DS    F                   RETURN ADDR
QWDDFLTW DS    3F                  WORK GEGS
QWDDFLTL DS    F                   FFLT DATA LEN
QWDDFLTS DS    H                   SUB CMD LEN
QWDDFLT  DS    CL16                DFLT DATA
QWDDFLT1 DS    CL97                DFLT DATA EXTENSION
         SPACE
*'JESOP' SAVE AREA
QWDJESOR DS    F                   RETURN
QWDJESOW DS    2F                  WORK
         SPACE
*  'JESSOP' SAVE AREA
QWDJESPR DS    F                   RETURN
QWDJESPW DS    2F                  WORK
         SPACE
*  'MJR' SAVE AREA
QWDMJRR  DS    F                   RETURN ADDR
QWDMJRD  DS    F                   DSECT BASE
QWDMJRW  DS    3F                  WRK REGS
QWDMJRLP DS    F                   LOOP CNTR
         SPACE 2
*  'MNR' SAVE AREA
QWDMNRR  DS    F                   RETURN ADDR
QWDMNRD  DS    F                   DSECT BASE
QWDMNRW  DS    3F                  WORK REGS
QWDMNRLP DS    F                   LOOP CNTR
         SPACE
*  'OUCP' SAVE AREA
QWDOUCPR DS    F                   RETURN
QWDOUCPD DS    F                   DSECT
         SPACE 2
*  'QPUT' SAVE AREA
QWDQPUTR DS    F                   RETURN ADDR  (SEE RECURSIVE CODE  -
QWDQPUTS DS    3F                  REG SAVE  AREA (BEFORE CHG THIS DATA
QWDQPUTD DS    F                   DSECT SAVE AREA
QWDQPUTA DS    XL20                RECURSIVE REG SAVE AREA
QWDQPUTB DS    CL130               RECURSIVE OUTPUT SAVE AREA
         SPACE 2
*  'QL' SAVE AREA
QWDQLR   DS    F                   RETURN ADDR
QWDQLD   DS    F                   DSECT SAVE
QWDQLW   DS    3F                  WORK REGS
QWDQLLP  DS    F                   LOOP CNTR
QWDQLC   DS    CL3                 QEL STATUS  (ACTIVE OR WAIT)
QWDQLC2  DS    CL3                 NEXT QEL STATUS  (ACTIVE OR WAIT)
         SPACE 2
*  'BLDQPUT' SAVE AREA
QWDBQR   DS    F                   RETURN ADDR
QWDBQW   DS    2F                  WORK REGS
QWDBQMJ  DS    CL8                 MAJ CONTROL NAME
QWDBQMN  DS    CL107               MINOR CTL NAME
         SPACE 2
*  'BLDXPUT' SAVE AREA
QWDBXPR  DS    F                   RETURN ADDR
QWDBXPW  DS    2F                  WORK REGS
         SPACE 2
*  COMMON SAVE AREA
QWDJESP  DS    F                   JES PARM PTR
QWDCPPLA DS    F                   PTR TO CPPL
QWDPDLA  DS    F                   PARS DSCRPTR LIST ADDR (PARSE OUTPT)
QWDPPLEC DS    F                   PARS ECB ADDR
QWDCPPL  DS    4F                  THIS REGIONS CPPL
QWDCBUF  DS    CL132               THIS REGIONS CMD BUFFER
QWDPDL   DS    CL64                THIS REGIONS PDL
QWDCBUFL EQU   CBUFL               THIS REGIONS CMD BUFFER LEN
QWDSTATS DS    X
*              1... ....           SOME OUTPUT HAS BEEN PROCESSED
*              .1.. ....           INFINATE LOOP ENCOUNTERED
*              ..1. ....           QUEUE CONFLICT FOUND
*              ...1 ....           VIRGIN CMD  (0 PARAMETERS SPECIFIED)
*              .... 1...           MSTR SCHEDULAR ASCB
*              .... .1..           JES ASCB
*              .... ..1.           TSO ASCB
*              .... ...1           BATCH JOB ASCB
         DS    0F                  OUTPUT LINE
QWDIOLIN DS    CL130               OUTPUT DATA LINE HDR + DATA LINE
         ORG   QWDIOLIN
QWDDLEN  DS    H                   LEN OF OUTPUT LINE DATA
         DS    H
QWDDATA  DS    CL126               OUTPUT DATA
         ORG
QWDIOPL  DS    4F                  I/O PARM LIST (INPUT TO PUTLINE)
QWDMJRNM DS    CL8                 MJR QCB NAME
QWDMNRLN DS    F                   MINOR NAME LENGTH
QWDMNRNM DS    CL107               1ST 107 BYTES OF MINOR QCB NAME
QWDJOBNM DS    CL8                 JOB NAME
QWDJOBSW DS    C                   SWAP INDICATOR
QWDDSPNM DS    CL7                 DISPOSITION  (SHR/RSV - XCL/RSV)
QWDPSW1  DS    X                   BITS 0-7 OF PROB STATE PSW
QWDPSW2  DS    X                   BITS 0-7 OF SUPV STATE PSW
*
*  BUILD THE PUTLINE PARAMETER BLOCK  (PTPB)
*        INITIATED BY PUTLINE MF=E
QWDPTPB  PUTLINE MF=L
QWDPPL   DS    7F                  PARS PARM LIST         (PARSE INPUT)
QWDAST   DS    0F                  ADDR SPACE TBL   (AST)
         ORG   (QWDAST+(ASTLEN*257)) 256 INITS + 1 DUMMY   (EL 0)
QWDLENX  EQU   *-QWD
 TITLE 'OS/VS/TSO                  D S E C T S'
* * * *  CPPL  COMMAND PROCESSOR PARM LIST
         SPACE
         IKJCPPL
         SPACE 3
* * * *  PPL   PARS PARM LIST
         SPACE
         IKJPPL
PPLDLENX EQU   *-PPL
 TITLE 'OS/VS                      D S E C T S'
* * * *  ASCB
         SPACE
         IHAASCB DSECT=YES
         EJECT
* * * *  CVT
         SPACE
         CVT   LIST=NO,TSO=YES,DSECT=YES
         EJECT
* * * *  OUCB
         SPACE
         IRAOUCB DSECT=YES
         EJECT
* * * *  QCB
         SPACE
         IHAQCB
         EJECT
* * * *  QEL
         SPACE
         IHAQEL
         SPACE 2
QELFDSPL EQU   (QELQFLGS-QEL)      DISPLACMENT TO QELQFLGS
         EJECT
* * * *  PSA
         IHAPSA DSECT=YES
         END
