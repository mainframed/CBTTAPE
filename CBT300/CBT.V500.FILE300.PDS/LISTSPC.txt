 TITLE 'L I S T S P   ---   L I S T   D A T A S E T   S P A C E'
*---------------------------------------------------------------------*
*                                                                     *
*   LISTSP IS A TSO COMMAND PROCESSOR WHICH LISTS THE ATTRIBUTES OF   *
*      A DATASET OR LIST OF DATASETS.  THE VOLUME ON WHICH THE DATA   *
*      SET(S) RESIDE MAY BE SPECIFIED.  A COUNT OF DIRECTORY BLOCKS   *
*      ALLOCATED AND USED MAY BE REQUESTED.                           *
*                                                                     *
*   LISTSP IS INVOKED AS FOLLOWS :                                    *
*                                                                     *
*      LISTSP | LSP  "DSNAME | ("DSNAME")                             *
*                    VOLUME (* | "VOLID")                             *
*                    DIRECTORY                                        *
*                                                                     *
*   LISTSP PRODUCES THE FOLLOWING OUTPUT :                            *
*                                                                     *
*      LINE 1 :                                                       *
*        DATASET NAME, VOLUME, AND UNIT ADDRESS                       *
*      LINE 2 :                                                       *
*        NUMBER OF TRACKS ALLOCATED AND NUMBER USED                   *
*        NUMBER OF EXTENTS                                            *
*        UNITS OF ALLOCATION (C/T/B/A)                                *
*        SECONDARY ALLOCATION QUANTITY                                *
*        RECORD FORMAT, BLOCK SIZE, AND RECORD LENGTH                 *
*        KEYLENGTH, DATASET ORGANIZATION, RELATIVE KEY POSITION       *
*        CREATION DATE AND EXPIRATION DATE                            *
*        DIRECTORY BLOCKS ALLOCATED AND USED, IF REQUESTED            *
*                                                                     *
*   LOG OF CHANGES:                                                   *
*      30MAR82 (82.089) - TRACKS USED WAS SHOWN AS ZERO IF            *
*         DS1LSTAR WAS X'000001'.  CHANGED CLI FROM 1 TO 0            *
*         WHEN LOOKING AT DS1LSTAR+2.                                 *
*      01NOV91 - JDM1 - WAS NOT SETTING RETCDE AT TERMINATION         *
*---------------------------------------------------------------------*
         SPACE 2
LISTSP   CSECT
         SPACE
         USING DSAWRK,R13
         USING UPT,R3
         USING PSCB,R4
         USING ECT,R5
         USING PDL,R2
         USING DAPB08,R9
         USING DSNPDE,R6
         USING UCBWRK,R7
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   PERFORM STANDARD REENTRANT PROLOGUE - GET WORK AREA AND SET UP    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         JVDENTER BASE=(R11),VERSION=&SYSPARM,DSA=(DSAEND-DSAWRK)
         EJECT
*   PARSE PARAMETERS, GET TIOT ADDRESS, AND INITIALIZE FOR DSLOOP   *
         SPACE
         LM    R2,R5,0(R1)
         L     R6,PCLA
         XC    ECB,ECB
         LINK  EP=IKJPARS,DCB=0,SF=(E,LINK),                           +
               VL=1,MF=(E,PPL),                                        +
               PARAM=(UPT,ECT,ECB,(6),ANS,(2),UWA)
         SPACE
         ST    R15,PARC
         LTR   R15,R15
         BNZ   PARSFAIL
         L     R2,ANS
         CL    R2,NULL
         BE    PARSFAIL
         SPACE
         EXTRACT TIOTA,'S',FIELDS=TIOT,MF=(E,EXTRACT)
         SPACE
         LA    R6,DSNAME
         LA    R9,DAPB
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   FOR EACH DATASET SPECIFIED, BUILD DAPB AND CALL DAIR TO ALLOCATE  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
DSLOOP   EQU   *
         SPACE
         MVC   DAPB,DAP8LST
         MVC   DSNVALUE,DSBLANK
         XC    DFPBLIST,DFPBLIST
         LA    R15,DSNVLEN
         ST    R15,DA08PDSN
         SPACE
         DA08BLD DSNPDE,VOLID,DISP=(SHR,KEEP,KEEP)
         EJECT
         LA    R14,DFPBLIST
         USING DFPB,R14
         LA    R15,DSNVLEN
         ST    R15,DFPBDSN
         MVI   DFPBCODE,DFPB04
         LA    R15,DWD
         ST    R15,DFPBQUAL
         ST    R4,DFPBPSCB
         TM    DA08CTL,DA08UID
         BZ    ALLOC
         MVI   DFPBCNTL,DFPBUID
         DROP  R14
         NI    DA08CTL,X'FF'-DA08UID
         EJECT
DEFAULT  LINK  EP=IKJEHDEF,DCB=0,SF=(E,LINK),MF=(E,DFPL),              +
               PARAM=(UPT,ECT,ECB,DFPBLIST)
         SPACE
ALLOC    LINK  EP=IKJDAIR,DCB=0,SF=(E,LINK),                           +
               VL=1,MF=(E,DAPL),                                       +
               PARAM=(UPT,ECT,ECB,PSCB,DAPB)
         SPACE
         ST    R15,DARC
         LTR   R15,R15
         BNZ   DAIRFAIL
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   LOOK UP DDNAME IN TIOT, GET UCB INFORMATION, AND OBTAIN DSCB'S    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         L     R15,TIOTA
         LA    R15,TIOENTRY-TIOT(R15)
         USING TIOENTRY,R15
         XR    R14,R14
         SPACE
TIOLOOK  EQU   *
         CLC   TIOEDDNM,DA08DDN
         BE    DDNHIT
         IC    R14,TIOELNGH
         LA    R15,0(R14,R15)
         LTR   R14,R14
         BP    TIOLOOK
         SPACE
         ABEND 100
         SPACE
DDNHIT   EQU   *
         ICM   R7,B'0111',TIOEFSRT
         MVC   VOLSER,UCBVOLI
         MVC   CUU,UCBNAME
         MVC   DWD,DA08DDN
         SPACE
         DEVTYPE DWD,DVTWKA,DEVTAB
         SPACE
         MVC   CAMLST,CAMSRCH
         EJECT
OBTAINF1 EQU   *
         LA    R15,DSNVALUE
         ST    R15,CAMDSN
         LA    R15,VOLSER
         ST    R15,CAMSER
         LA    R15,OBTWKA
         ST    R15,CAMWKA
         XR    R0,R0
         SPACE
         OBTAIN CAMLST
         SPACE
         LTR   R15,R15
         BNZ   OBTNFAIL
         SPACE
         CLI   DS1NOEPV,3
         BNH   FORMAT
         SPACE
         LA    R15,DS1PTRDS
OBTAINF3 ST    R15,CAMDSN
         LA    R15,F3WKA
         ST    R15,CAMWKA
         MVC   CAMLST,CAMSEEK
         SPACE
         OBTAIN CAMLST
         SPACE
         LTR   R15,R15
         BNZ   OBTNFAIL
         SPACE
         CLI   DS3FMTID,C'3'
         LA    R15,DS2PTRDS
         BNE   OBTAINF3
         SPACE
         DROP  R15
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   OUTPUT HEADINGS AND FORMAT DATA SET LABEL INFORMATION IN BUFFER   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
FORMAT   EQU   *
         MVC   OUTDEAD,DSHEAD
         MVC   OUTDSN,DSNVALUE
         MVC   OUTSER,VOLSER
         MVC   OUTCUU,CUU
         LA    R0,4+(DSHEND-DSHEAD)
         STCM  R0,B'1100',OUTDLEN+2
         STH   R0,OUTDLEN
         LA    R0,OUTHNXT
         ST    R0,OUTMSG
         MVC   OUTHEAD,HEADING
         LA    R0,4+(HEADEND-HEADING)
         STCM  R0,B'1100',OUTHLEN+2
         STH   R0,OUTHLEN
         LA    R0,OUTLAST
         ST    R0,OUTHNXT
         MVC   OUTPUT,MASK
         XC    OUTLAST,OUTLAST
         LA    R0,4+(MASKEND-MASK)
         STCM  R0,B'1100',OUTLLEN+2
         STH   R0,OUTLLEN
         SPACE
*DS1SCAL EQU   DS1SCALO+1,3
         CVRT  SCAL
         CVRT  BLKL
         EJECT
         CVRT  LRECL
         CVRT  KEYL
         CVRT  RKP
         CVRT  NOEPV
         EJECT
RFAORM   LA    R15,FMTRECFM+L'FMTRECFM-1
         TM    DS1RECFM,B'00000110'
         BZ    RFT
         BCTR  R15,0
         TM    DS1RECFM,B'00000010'
         BZ    RFA
RFM      MVI   1(R15),C'M'
         B     RFT
RFA      MVI   1(R15),C'A'
RFT      TM    DS1RECFM,B'00100000'
         BZ    RFS
         MVI   0(R15),C'T'
         BCTR  R15,0
RFS      TM    DS1RECFM,B'00001000'
         BZ    RFB
         MVI   0(R15),C'S'
         BCTR  R15,0
RFB      TM    DS1RECFM,B'00010000'
         BZ    RFU
         MVI   0(R15),C'B'
         BCTR  R15,0
RFU      TM    DS1RECFM,B'11000000'
         BM    RFFORV
         BZ    UNITS
         MVI   0(R15),C'U'
         B     UNITS
RFFORV   TM    DS1RECFM,B'10000000'
         BO    RFF
         MVI   0(R15),C'V'
         B     UNITS
RFF      MVI   0(R15),C'F'
         EJECT
UNITS    XR    R15,R15
         IC    R15,DS1SCALO
         SRL   R15,6
         LA    R14,ALLCUNIT(R15)
         MVC   FMTUN+L'FMTUN-1(1),0(R14)
         SPACE
         LA    R15,FMTDSORG+L'FMTDSORG-2
         TM    DS1DSORG,1
         BZ    DSORG
         MVI   1(R15),C'U'
         BCTR  R15,0
         SPACE
DSORG    MVC   DWD(1),DS1DSORG
         NI    DWD,X'E2'
         TR    DWD(1),TRDSORG
         XR    R1,R1
         IC    R1,DWD
         LA    R14,DSORGMN(R1)
         MVC   0(2,R15),0(R14)
         SPACE
DATES    CVDT  CREDT
         CVDT  EXPDT
         XR    R15,R15
         ST    R15,BTRALC
         ICM   R15,B'0001',DS1NOEPV
         BZ    EXTCVRT
         MVC   DS3FMTID(9*10),DS3ADEXT
         MVC   DS3EXTNT-3*10(3*10),DS1EXT1
         LA    R14,DS3EXTNT-3*10
         EJECT
EXTENTS  EQU   *
         USING EXTENT,R14
         CLI   EXTTYPE,0
         BNH   EXTLOOP
* DEVTPC EQU   DVTWKA+10
         LH    R1,EXTLOCC
         MH    R1,DEVTPC
         AH    R1,EXTLOHH
         LH    R0,EXTHICC
         MH    R0,DEVTPC
         AH    R0,EXTHIHH
         BCTR  R1,0
         SR    R0,R1
         A     R0,BTRALC
         ST    R0,BTRALC
         SPACE
EXTLOOP  LA    R14,EXTNEXT
         BCT   R15,EXTENTS
         SPACE
EXTCVRT  EQU   *
*DS1TRALC EQU  BTRALC,4
         CVRT  TRALC
*DS1TRUSE EQU  DS1LSTAR,2
         ICM   R15,B'11',DS1LSTAR
         CLI   DS1LSTAR+2,0                                       82089
         BNH   *+L'*+4
         LA    R15,1(R15)
         STCM  R15,B'11',DS1LSTAR
         CVRT  TRUSE
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   TRANSLATE OUT EDIT CHARACTERS AND WRITE FORMATTED LABEL DATA      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         TR    OUTPUT(MASKEND-MASK),TREDIT
         SPACE
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPL),ECB=ECB,ECT=ECT,UPT=UPT,       +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(OUTMSG,TERM,MULTLIN,DATA)
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   WHEN OUTPUT HAS BEEN GENERATED, FREE DATASET AND SEE IF MORE      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
FREE     EQU   *
         LA    R15,DAPB
         USING DAPB18,R15
         MVC   OUTPUT(8),DA08DDN
         MVC   DAPB,DA18LST
         MVC   DA18CTL,DA08CTL
         MVC   DA18DDN,OUTPUT
         SPACE
         LINK  EP=IKJDAIR,DCB=0,SF=(E,LINK),MF=(E,DAPL)
         SPACE
         DROP  R15
         ST    R15,DARC
         LTR   R15,R15
         BNZ   DAIRFAIL
         SPACE
DSNEXT   EQU   *
         CLC   DSNLINK,NULL
         BE    DONE
         L     R6,DSNLINK
         B     DSLOOP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   ABNORMAL SERVICE ROUTINE (DAIR AND PARS) TERMINATION PROCESSING   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
OBTNFAIL EQU   *
         STH   R15,DA08DARC
         MVI   DA08DARC,X'67'
         MVI   DARC+3,12
         SPACE
DAIRFAIL EQU   *
         LINK  EP=IKJEFF18,DCB=0,SF=(E,LINK),                          +
               MF=(E,FAIL),                                            +
               PARAM=(DAPL,DARC,ZERO,H01)
         B     DSNEXT
         SPACE
PARSFAIL EQU   *
         LINK  EP=IKJEFF19,DCB=0,SF=(E,LINK),                          +
               MF=(E,FAIL),PARAM=(0,PARC,ZERO,H21)
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   WHEN ALL DATASETS HAVE BEEN PROCESSED, FREE PDL AND WORK AREAS    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
DONE     EQU   *
         IKJRLSA ANS
         SPACE
         XR    R15,R15                                           JDM1
         JVDLEAVE RC=(15)                                        JDM1
         SPACE 2
         PRINT NOGEN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   PARSE CONTROL CSECT AND PARSE DESCRIPTOR LIST DSECT DEFINITIONS   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
LSPPCL   IKJPARM DSECT=PDL
         SPACE
DSNAME   IKJPOSIT DSNAME,LIST,                                         +
               PROMPT='DSNAME(S)+',                                    +
               HELP='NAMES OF DATA SET(S) WHOSE SPACE IS TO BE LISTED'
         SPACE
VOLUME   IKJKEYWD DEFAULT='VOLUME(*)'
         SPACE
         IKJNAME 'VOLUME',SUBFLD=VOLIDS
         SPACE
DIR      IKJKEYWD
         SPACE
         IKJNAME 'DIRECTORY'
         SPACE
VOLIDS   IKJSUBF
         SPACE
VOLID    IKJIDENT 'VOLID',ASTERISK,MAXLNTH=6,FIRST=ANY,OTHER=ANY,      +
               PROMPT='VOLID+',                                        +
               HELP='ID OF VOLUME ON WHICH DATA SET(S) RESIDE'
         SPACE
         IKJENDP
         SPACE 2
         PRINT GEN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*   DECLARE ALL CONSTANTS, LITERALS, WORK AREAS, AND CONTROL BLOCKS   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
PCLA     DC    A(LSPPCL)
NULL     DC    0A(0),X'FF',AL3(0)
ZERO     DC    A(0)
H01      DC    H'1'
H21      DC    H'21'
DAP8LST  DC    X'0008',BL2'0',XL2'0',XL2'0',AL4(0)
         DC    3CL8' ',4F'0',2CL8' '
         DC    AL1(DA08SHR),AL1(DA08KEEP),AL1(DA08KEEP)
         DC    BL1'0',XL3'0',BL1'0',CL8' '
DA18LST  DC    X'0018',BL2'0',2XL2'0',AL4(0)
         DC    2CL8' ',CL2' ',AL1(DA18KEEP),BL1'0',CL8' '
DSBLANK  DC    CL44' '
HEADING  DC    C' TRALC TRUSE EX UN SCALO RECFM BLKSZ LRECL KEYLN '
         DC    C'DSO RKP CREDT EXPDT'
HEADEND  EQU   *
MASK     DC    X'402020202120402020202120202120404040'
         DC    X'402020202120404040404040402020202120'
         DC    X'4020202021204020202021204020212040202120'
         DC    X'402020202120402020202120'
MASKEND  EQU   *
DSHEAD   DC    CL50'DSN: ',CL12'VOL: ',CL9'UNIT: '
DSHEND   EQU   *
TREDIT   DC    256AL1(*-TREDIT)
         ORG   TREDIT+X'20'
         DC    X'4040'
         ORG   TREDIT+256
TRDSORG  DC    XL256'00'
         ORG   TRDSORG+X'80'
         DC    AL1(2)
         ORG   TRDSORG+X'40'
         DC    AL1(4)
         ORG   TRDSORG+X'20'
         DC    AL1(6)
         ORG   TRDSORG+X'02'
         DC    AL1(8)
         ORG   TRDSORG+256
DSORGMN  DC    C'  ISPSDAPO'       DATA SET ORGANIZATIONS
ALLCUNIT DC    C'ABTC'             ALLOCATION UNIT MNEMONICS
         EJECT
CAMSRCH  CAMLST      SEARCH,*-*,*-*,*-*
CAMSEEK  CAMLST      SEEK,*-*,*-*,*-*
         PRINT       DATA
         SPACE
PATCH    DC          64S(*)
         SPACE
         LTORG
         EJECT
DSNPDE   DSECT
         SPACE
DSNADDR  DS    A
DSNLEN   DS    H
DSNFLG1  DS    B
DSNMADDR DS    A
DSNMLEN  DS    H
DSNFLG2  DS    B
DSNPADDR DS    A
DSNPLEN  DS    H
DSNFLG3  DS    B
DSNLINK  DS    A
DSNPRES  EQU   B'10000000'
DSNQUOTE EQU   B'01000000'
         SPACE 2
EXTENT   DSECT
         SPACE
EXTTYPE  DS    BL1
EXTSEQ   DS    XL1
EXTLOCC  DS    HL2
EXTLOHH  DS    HL2
EXTHICC  DS    HL2
EXTHIHH  DS    HL2
         SPACE
EXTNEXT  EQU   *
         EJECT
         IKJDAP08
DA08END  EQU   *
         EJECT
         IKJDAP18
         EJECT
         IKJUPT
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         EJECT
         IKJCPPL
         EJECT
         IKJDFPB
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
UCBWRK   DSECT
         IEFUCBOB
         EJECT
         JVDEQU
         SPACE 2
         PRINT GEN
         EJECT
DSAWRK   DSECT
         SPACE
SSA      DS    18F
TIOTA    DS    A
ANS      DS    A
ECB      DS    A
UWA      DS    A
DWD      DS    D
OBTWKA   DS    0D,XL148
F3WKA    DS    0D,XL148
DARC     DS    F
PARC     DS    F
DVTWKA   DS    5F
FAIL     DS    4F
BTRALC   DS    F
CAMLST   DS    F
CAMDSN   DS    A
CAMSER   DS    A
CAMWKA   DS    A
DSNQDE   DS    0XL24,A,H,B,X,A,H,B,X,A,H,B,X
DFPBLIST DS    0XL20,HL1,AL3,BL1,AL3,XL1,AL3,A,A
DAPB     DS    XL(DA08END-DAPB08)
DSNVLEN  DS    H
DSNVALUE DS    CL44
VOLSER   DS    CL6
CUU      DS    CL3
         EJECT
PPL      CALL  ,(UPT,ECT,ECB,PCLA,ANS,CPPL,UWA),MF=L
DAPL     CALL  ,(UPT,ECT,ECB,PSCB,DAPB),MF=L
IOPL     CALL  ,(UPT,ECT,ECB,PUTLINE),MF=L
DFPL     CALL  ,(UPT,ECT,ECB,DFPB),MF=L
EXTRACT  EXTRACT  TIOTA,FIELDS=TIOT,MF=L
PUTLINE  PUTLINE  MF=L
LINK     LINK     SF=L
         EJECT
         SPACE
OUTLAST  DS    A
OUTLLEN  DS    2H
OUTPUT   DS    0CL(MASKEND-MASK)
FMTTRALC DS    ZL6
FMTTRUSE DS    ZL6
FMTNOEPV DS    ZL3
FMTUN    DS    CL3
FMTSCAL  DS    ZL6
FMTRECFM DS    CL6
FMTBLKL  DS    ZL6
FMTLRECL DS    ZL6
FMTKEYL  DS    ZL6
FMTDSORG DS    CL4
FMTRKP   DS    ZL4
FMTCREDT DS    ZL6
FMTEXPDT DS    ZL6
OUTHNXT  DS    A(OUTLAST)
OUTHLEN  DS    2H
OUTHEAD  DS    CL(HEADEND-HEADING)
OUTMSG   DS    A(OUTHNXT)
OUTDLEN  DS    2H
OUTDEAD  DS    0CL(DSHEND-DSHEAD)
         DS    C'DSN: '
OUTDSN   DS    CL44
         DS    C' VOL: '
OUTSER   DS    CL6
         DS    C' UNIT: '
OUTCUU   DS    CL3
         EJECT
         ORG   OBTWKA-44
         IECSDSL1 (1)
         EJECT
         ORG   F3WKA
         SPACE
         IECSDSL1 (3)
         SPACE
         ORG   F3WKA
         SPACE
         IECSDSL1 (2)
         SPACE
         ORG
         SPACE
DSAEND   DS    0D
         SPACE
DS1SCAL  EQU   DS1SCALO+1,3
DEVTPC   EQU   DVTWKA+10
DS1TRALC EQU   BTRALC,4
DS1TRUSE EQU   DS1LSTAR,2
         END   LISTSP
