         TITLE 'SYSDSN - FIND WHO IS USING/WAITING FOR A DSNAME'
**********************************************************************
***                                                                ***
***                       SYSDSN COMMAND                           ***
***                                                                ***
***  GENERAL FUNCTION:                                             ***
***                                                                ***
***      THIS COMMAND CHECKS THE SYSDSN GENERALIZED RESOURCE       ***
***      QUEUE FOR QUEUING ON DATASET NAMES.  IT USES GQSCAN       ***
***      TO CREATE RESOURCE INFORMATION BLOCKS (RIB), THEN         ***
***      EXTRACTS INFORMATION FOR A REPORT TO THE TERMINAL.        ***
***                                                                ***
***  FORMAT:                                                       ***
***                                                                ***
***    SYSDSN  'DSNAME'                                            ***
***      SYSD                                                      ***
***                                                                ***
***    ALIAS:                                                      ***
***      1. SYSD                                                   ***
***                                                                ***
***    REQUIRED:                                                   ***
***      1. NONE.  DSNAME WILL BE PROMPTED FOR.                    ***
***                                                                ***
***    DEFAULTS:                                                   ***
***      1. NONE                                                   ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  RETURN CODES -                                                ***
***       00 - NORMAL COMPLETION                                   ***
***       04 - NORMAL, LIST TRUNCATED                              ***
***       12 - ERROR                                               ***
***                                                                ***
***  REGISTER USAGE                                                ***
***                                                                ***
***      R0-R1 - MACROS, LINKAGE CONVENTIONS                       ***
***      R2-R3 - MACROS, WORK                                      ***
***      R4-R6 - WORK                                              ***
***      R7-R8 - (NOT USED)                                        ***
***         R9 - BASE FOR PARM DESCRIPTOR LIST CREATED BY PARSE    ***
***        R10 - BAL REGISTER                                      ***
***        R11 - BASE FOR RIB WORK AREA                            ***
***        R12 - BASE FOR PROGRAM                                  ***
***        R13 - BASE OF WORK0 AREA, REG SAVE AREA AND CODES       ***
***        R14 - MACROS, LINKAGE CONVENTIONS                       ***
***        R15 - MACROS, WORK, LINKAGE CONVENTIONS                 ***
***          +---------------------------------------------+       ***
***          |  EXCEPT FOR BASE REGISTERS, DATA IS NEVER   |       ***
***          |  PASSED IN REGISTERS BETWEEN SUBSECTIONS OF |       ***
***          |  CODE.                                      |       ***
***          +---------------------------------------------+       ***
***                                                                ***
***  NON-IBM MACROS USED:  NONE                                    ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  RENT, REUS, REFR, ALIAS(SYSD)        ***
***                                                                ***
***  CULPRIT:                                                      ***
***     CHUCK HOFFMAN, CONSULTANT                                  ***
***     USER SERVICES CENTER                                       ***
***                                                                ***
***     BUREAU OF SYSTEMS OPERATIONS                               ***
***     OFFICE OF MANAGEMENT INFORMATION SYSTEMS                   ***
***     EXECUTIVE OFFICE FOR ADMINISTRATION AND FINANCE            ***
***                                                                ***
***     COMMONWEALTH OF MASSACHUSETTS                              ***
***     ONE ASHBURTON PLACE, ROOM 1619                             ***
***     BOSTON, MA  02108                                          ***
***                           (617) 727-5725                       ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***  MAINTENANCE HISTORY:                                          ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  PROGRAM LOGIC:                                                ***
***                                                                ***
***  1.  GET WORKSPACE AND ASSIGN BASE REGISTERS.  FOLLOW          ***
***      STANDARD LINKAGE CONVENTIONS.  GET INPUT STORAGE FOR      ***
***      RESOURCE INFORMATION BLOCK (RIB) AND EXTENSIONS (RIBE).   ***
***                                                                ***
***  2.  CREATE PARSE PARAMETER LIST (PPL), THEN PARSE THE PARM.   ***
***                                                                ***
***  3.  SCAN THE GENERALIZED RESOURCE QUEUES FOR MAJOR NAME       ***
***      'SYSDSN' AND MINOR NAME MATCHING THE PARM.  ASSIGN        ***
***      RETURN CODES.  IF MORE THAN ONE RIB IS RETURNED, OR IF    ***
***      RETURNED INFORMATION OVERFLOWS THE INPUT AREA, SET A      ***
***      SWITCH TO NOTE THAT THE LIST WILL BE TRUNCATED.           ***
***                                                                ***
***  4.  CREATE A LIST BY PROCESSING THE FIRST RETURNED RIB, AND   ***
***      ALL ITS RIBES WHICH FIT INTO THE RETURN AREA.             ***
***                                                                ***
***  5.  GENERATE A MESSAGE FOR ANY BUT 00 RETURN CODES.           ***
***                                                                ***
***  6.  FREE STORAGE AND TERMINATE USING STANDARD LINKAGE         ***
***      CONVENTIONS.                                              ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***            EQUATES                                             ***
**********************************************************************
RBASE    EQU   12                       BASE REGISTER NUMBER
RBAL     EQU   10                       BAL REGISTER NUMBER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         PRINT NOGEN
**********************************************************************
***                                                                ***
***   PROLOGUE                                                     ***
***                                                                ***
**********************************************************************
*
SYSDSN   CSECT                        , CSECTID
         B     28(0,R15)                BRANCH AROUND ID
         DC    AL1(8)                     LENGTH
         DC    CL8'SYSDSN'                ID
         DC    CL8'&SYSDATE'              SYSTEM DATE MM/DD/YY
         DC    CL6' &SYSTIME'             SYSTEM TIME HH.MM
         STM   R14,R12,12(R13)          SAVE R0-R12, R14-R15
         LR    RBASE,R15                LOAD BASE ADDRESS BASER
         USING SYSDSN,RBASE             ADDRESSABILTIY OF CSECT
         GETMAIN R,LV=WORK0LEN          GET STORAGE FOR S.A. + WORK
         ST    R13,4(0,R1)              ADDR OF PREV S.A. TO MY S.A.
         ST    R1,8(0,R13)              ADDR OF MY S.A. TO PREV S.A.
         LM    R0,R1,20(R13)            RESTORE R0-R1 TO PRE-GETMAIN
         L     R13,8(0,R13)             R13 POINTS TO MY S.A.
         USING WORK0,R13                ADDRESSABILITY OF WORK0
         ST    R1,R1SAVE                INITIAL VAL OF R1 TO MY STORAGE
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         USING PARMPDL,R9               ADDRESSABILITY OF PRM DESC LST
         ST    R1,CPPLPTR               SAVE CPPL POINTER
         GETMAIN  R,LV=WORK1LEN         GET MAIN STORAGE FOR RIB
         LR    R11,R1                   ADDRESS OF RIB STORAGE
         USING WORK1,R11                ADDRESSABILITY OF RIB
         XC    COMPCODE,COMPCODE        CLEAR PROGRAM RETURN CODE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
         B     MAINLINE
*
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
*
MAINLINE BAL   R10,PPLSETUP             SET UP PARSE PARM LIST
         BAL   R10,PARSE                PARSE THE INPUT PARAMETERS
         BAL   R10,GQPROC               SCAN THE GLOBAL RESOURCE QUEUES
         CLC   RETCDE,F4                IF RETURN GT 4 (NO LISTING)
         BH    MAINEND                    GO TO END
         BAL   R10,REPORT               ELSE WRITE REPORT
*
MAINEND  B     ENDING                   BRANCH TO ENDING
         EJECT
**********************************************************************
***                                                                ***
***   EPILOGUE                                                     ***
***                                                                ***
**********************************************************************
ENDING   BAL      R10,KISSOFF             PRINT KISSOFF MESSAGE, IF ANY
         LA       R4,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R4,0(0,R4)              R4 POINTS TO PDL
         IKJRLSA  (R4)                    FREE STORAGE OF PDL
         FREEMAIN R,LV=WORK1LEN,A=(R11) , FREE MAIN STORAGE
*
         LR    R1,R13                   LOAD MY S.A. ADDR FOR FREEMAIN
         L     R14,COMPCODE             SAVE PROGRAM COMPLETION CODE
         L     R13,4(0,R13)             RESTORE R13 FROM MY S.A.
         FREEMAIN R,LV=WORK0LEN,A=(R1)  FREE STORAGE FOR WORK0
         LR    R15,R14                  SET PROGRAM COMPLETION CODE
         LM    R0,R12,20(R13)           RESTORE R0-R12 FM CALLERS S.A.
         L     R14,12(0,R13)            RESTORE R14 FROM CALLERS S.A.
         MVI   12(R13),X'FF'            RETURN INDCATR TO CALLERS S.A.
         BR    R14                      RETURN TO CALLER
*
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST                             ***
***                                                                ***
**********************************************************************
PPLSETUP ST    R10,PPR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         MVC   MYPPL+(PPLUPT-PPL)(4),CPPLUPT              UPT  (CPPL)
         MVC   MYPPL+(PPLECT-PPL)(4),CPPLECT              ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,MYPPL+(PPLECB-PPL)                      ECB  (MINE)
         MVC   MYPPL+(PPLPCL-PPL)(4),VPARMPCL             PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,MYPPL+(PPLANS-PPL)                      ANS  (MINE)
         MVC   MYPPL+(PPLCBUF-PPL)(4),CPPLCBUF            CBUF (CPPL)
         XC    MYPPL+(PPLUWA-PPL)(4),MYPPL+(PPLUWA-PPL)   UWA  (MINE)
         DROP  R4
*
PPEND    L     R10,PPR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
**********************************************************************
PARSE    ST    R10,PAR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     R10,PAR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    SCAN THE GENERALIZED RESOURCE QUEUES                        ***
***                                                                ***
**********************************************************************
GQPROC   ST    R10,GQR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R5,PDSN                  R2 POINTS TO DSN PDE
         LH    R6,4(0,R5)               R3 IS LENGTH OF DSN
         L     R5,0(0,R5)               R2 POINTS TO DSN
         XC    GQTOKEN,GQTOKEN          ZERO THE TOKEN
         LA    R4,GQTOKEN               R4 POINTS TO TOKEN (FOR CONT)
         MVC   GQSCANL(GQSLLEN),GQSCANP INITIALIZE PARM LIST
*
GQSCAN   GQSCAN AREA=((R11),L'RETAREA),RESNAME=(,(R5),(R6)),           X
               TOKEN=(R4),MF=(E,GQSCANL),SCOPE=ALL
*
         STM   R0,R1,GQRETREG
         ST    R15,GQRETREG+8
         NI    SWITCH1,B'00001111'      INITIALIZE SW0-SW3 TO ZERO
*
         LTR   R15,R15                  IF RC NOT ZERO
         BNZ   GQCK4                      GO TO CHECKS
         C     R1,F1                    ELSE IF NUMBER OF RIBS NOT 1
         BNE   GQRIB                      GO TO ERROR ROUTINE
         B     GQEND                    ELSE GO TO END
*
GQCK4    C     R15,F4                   IF RC=04  (NONE FOUND)
         BNE   *+24                       NO: BRANCH AROUND
         OI    SWITCH1,X'80'             YES: SET SW0 ON
         MVC   RETCDE,F4                      SET PROC RETCDE=04
         MVC   COMPCODE,F4                    SET PGM RETCDE=04
         B     GQEND                          AND GO TO ENDING
*
         C     R15,F8                   IF RC=08  (OVERFLOW)
         BNE   *+24                       NO: BRANCH AROUND
         OI    SWITCH1,X'40'             YES: SET SW1 ON
         MVC   RETCDE,F4                      SET PROC RETCDE=04
         MVC   COMPCODE,F8                    SET PGM RETCDE=08
         B     GQEND                          AND GO TO ENDING
*
*                                       RC MUST BE GREATER THAN 08
         OI    SWITCH1,X'10'              SET SW3 ON
         MVC   RETCDE,F12                 SET PROC RETCDE=12
         MVC   COMPCODE,F12               SET PGM RETCDE=12
         B     GQEND                          AND GO TO ENDING
*
GQRIB    OI    SWITCH1,X'20'            SET SW2 ON
         MVC   RETCDE,F4                SET PROC RETCDE=04
         MVC   COMPCODE,F8              SET PGM RETCDE=08
         B     GQEND                    GO TO ENDING
*
GQEND    L     R10,GQR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***    WRITE THE REPORT                                            ***
**********************************************************************
REPORT   ST    R10,RER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         TM    SWITCH1,X'80'            IF NO INFO WAS FOUND
         BO    REEND                      BRANCH TO ENDING
*
RERNAME  MVC   OUTLINE(80),BLANKS       INITIALIZE OUTPUT LINE
         LA    R4,RETAREA               R4 POINTS TO RETURNED DATA
         USING RIB,R4                   ADDRESSABILITY OF RIB
         LH    R5,RIBVLEN               R5 LENGTH OF VARIABLE SEG
         AH    R5,GQRETREG+00           R5 LEN OF BASE + VARIABLE SEG
         LA    R5,0(R5,R4)              R5 POINTS TO FIRST RIBE
         USING RIBE,R5                  ADDRESSABILITY OF RIBE
*
         XR    R1,R1                    CLEAR R1
         IC    R1,RIBRNMLN              LENGTH OF RNAME
         BCTR  R1,0                       MINUS 1 FOR EXEC
         LH    R2,GQRETREG+00           R2 IS LEN OF BASE
         LA    R2,0(R2,R4)              R2 POINTS TO RNAME
         B     *+10                     BRANCH AROUND EXEC
         MVC   OUTLINE(0),0(R2)           MOVE RNAME TO OUTPUT
         EX    R1,*-6                   EXECUTE MOVE
*
         LA    R3,OUTLINE               ADDR OF OUTPUT LINE
         TPUT  (R3),70                  WRITE THE LINE
         LA    R3,BLANKS                ADDR OF OUTPUT LINE
         TPUT  (R3),70                  WRITE THE LINE
*
RETITLE  LA    R3,TITLE1                POINT TO FIRST TITLE
         TPUT  (R3),70                    PRINT IT
         LA    R3,TITLE2                POINT TO SECOND TITLE
         TPUT  (R3),70                    PRINT IT
*
         MVC   OUTLINE(80),BLANKS       INITIALIZE OUTPUT LINE
RESCOPE  TM    RIBSCOPE,RIBSYS          IF SCOPE NE SYSTEM
         BNO   *+14                       BRANCH AROUND
         MVC   OF1SCPE(7),SYSTEM        ELSE MOVE 'SYSTEM' TO SCOPE
         B     RERIBENO                   AND BRANCH TO NEXT PIECE
         TM    RIBSCOPE,RIBSYSS         IF SCOPE NE SYSTEMS
         BNO   *+14                       BRANCH AROUND
         MVC   OF1SCPE(7),SYSTEMS       ELSE MOVE 'SYSTEMS' TO SCOPE
         B     RERIBENO                   AND BRANCH TO NEXT PIECE
         TM    RIBSCOPE,RIBSTEP         IF SCOPE NE STEP
         BNO   *+14                       BRANCH AROUND
         MVC   OF1SCPE(7),STEP          ELSE MOVE 'STEP' TO SCOPE
         B     RERIBENO                   AND BRANCH TO NEXT PIECE
         TM    RIBSCOPE,RIBGLBL         IF SCOPE NE GLOBAL
         BNO   *+10                       BRANCH AROUND
         MVC   OF1SCPE(7),GLOBAL        ELSE MOVE 'GLOBAL' TO SCOPE
         B     RERIBENO                   AND BRANCH TO NEXT PIECE
*
RERIBENO L     R3,RIBNRIBE              NUMBER OF RIBE IN USER AREA
*
REJOBID  MVC   OF1JNAM(8),RIBEJBNM      MOVE JOB NAME TO LINE
*
RETYPE   TM    RIBERFLG,RIBETYPE        IF TYPE IS SHR
         BNO   *+14                       THEN
         MVC   OF1TYPE(4),SHR               MOVE 'SHR' TO LINE
         B     *+10                     ELSE
         MVC   OF1TYPE(4),EXCL            MOVE 'EXCL' TO LINE
*
RESTAT   TM    RIBESFLG,RIBESTAT        IF STATUS IS OWNS
         BNO   *+14                       THEN
         MVC   OF1STAT(7),USING             MOVE 'USING' TO LINE
         B     *+10                     ELSE
         MVC   OF1STAT(7),WAITING         MOVE 'WAITING' TO LINE
*
         AH    R5,GQRETREG+02           R5 POINTS TO NEXT RIBE
         LA    R2,OUTLINE               R3 POINTS TO OUTPUT LINE
         TPUT  (R2),70                  WRITE THE LINE
         BCT   R3,REJOBID               LOOP BACK UP FOR NEXT RIBE
*
         DROP  R4
         DROP  R5
*
REEND    L     R10,RER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***    WRITE THE KISSOFF MESSAGE, IF ANY                           ***
**********************************************************************
KISSOFF  ST    R10,KIR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         TM    SWITCH1,X'F0'            IF NO PROBLEMS
         BZ    KIEND                      GO TO ENDING (NO MESSAGE)
KINOMAT  TM    SWITCH1,X'80'            ELSE IF NO MATCH
         BZ    KITRUNC                    THEN
         LA    R3,NOTINUSE                  WRITE
         TPUT  (R3),70                        'NOT IN USE' MSG
         B     KIEND                        AND GO TO END
KITRUNC  TM    SWITCH1,X'60'            ELSE IF TRUNCATED LIST
         BZ    KIBADRC                    THEN
         LA    R3,TRUNC                     WRITE
         TPUT  (R3),70                        'LIST TRUNCATED' MSG
         B     KIEND                        AND GO TO END
KIBADRC  TM    SWITCH1,X'10'            ELSE IF GQSCAN R/C GT 8
         BZ    KIEND                      THEN
         LA    R3,BADSCAN                   WRITE
         TPUT  (R3),70                        'BAD GQSCAN' MSG
         B     KIEND                        AND GO TO END
*
KIEND    L     R10,KIR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
CONSTDTA DS    0D                       AREA FOR DATA CONSTANTS
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
DZIPS    DS    0D                       DOUBLE WORD OF ZEROES
         DC    2F'0'
BLANKS   DC    CL80' '                  10 DOUBLE WORDS OF BLANKS
F1       DC    F'1'                     CONSTANT
F4       DC    F'4'                     CONSTANT
F8       DC    F'8'                     CONSTANT
F12      DC    F'12'                    CONSTANT
*
GQSCANP  GQSCAN SCOPE=ALL,RESNAME=(MAJNAME,,),MF=L
GQSPLEN  EQU   *-GQSCANP
MAJNAME  DC    CL8'SYSDSN'
*
TITLE1   DC    CL80'    Job Name  Scope    Type  Status'
TITLE2   DC    CL80'    --------  -------  ----  --------'
*
SYSTEM   DC    CL7'System'
SYSTEMS  DC    CL7'Systems'
STEP     DC    CL7'Step'
GLOBAL   DC    CL7'Global'
SHR      DC    CL4'SHR'
EXCL     DC    CL4'EXCL'
USING    DC    CL7'Using'
WAITING  DC    CL7'Waiting'
NOTINUSE DC    CL80'*** Not in use ***'
TRUNC    DC    CL80'*** List truncated due to length ***'
BADSCAN  DC    CL80'*** Bad GQSCAN, disregard all data ***'
*
CONSTEND DS    0D
CONSTLEN EQU   *-CONSTDTA               TOTAL LENGTH OF CONSTANTS
*
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
PDSN     IKJPOSIT DSNAME,USID,PROMPT='DATASET NAME TO BE CHECKED'
*
         IKJENDP
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA OBTAINED WITH LINKSAVE                             ***
***                                                                ***
**********************************************************************
WORK0    DSECT                          AREA-0 FOR VARIABLES
MYSAVE   DS    18F                      REGISTER SAVE AREA
R1SAVE   DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
PPR10SAV DS    F                        RETURN ADDRESS SAVE AREA
PAR10SAV DS    F                        RETURN ADDRESS SAVE AREA
MSR10SAV DS    F                        RETURN ADDRESS SAVE AREA
GQR10SAV DS    F                        RETURN ADDRESS SAVE AREA
RER10SAV DS    F                        RETURN ADDRESS SAVE AREA
KIR10SAV DS    F                        RETURN ADDRESS SAVE AREA
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
SWITCH1  DC    B'00000000'              SWITCHES AREA #1
*                1.......   X'80'         NO RESOURCES MATCHED
*                .1......   X'40'         RETURN AREA FULL
*                ..1.....   X'20'         MORE THAN ONE RIB
*                ...1....   X'10'         RC GT 08 FROM GQSCAN
*                ....1111                 (NOT USED)
         PRINT GEN
GQSCANL  GQSCAN SCOPE=ALL,RESNAME=(MAJNAME,,),MF=L
GQSLLEN  EQU   *-GQSCANL
         PRINT NOGEN
GQTOKEN  DS    F                        TOKEN FOR CONTINUING SCAN
GQRETREG DS    3F                       RETURN OF R0,R1,R15 FROM SCAN
*
OUTLINE  DS    CL80                     OUTPUT LINE FOR TPUT
OF1      ORG   OUTLINE                  FORMAT 1 OUTPUT
         DS    CL4                        FILLER
OF1JNAM  DS    CL8                        JOBNAME
         DS    CL2                        FILLER
OF1SCPE  DS    CL7                        SCOPE
         DS    CL2                        FILLER
OF1TYPE  DS    CL4                        TYPE
         DS    CL2                        FILLER
OF1STAT  DS    CL7                        STATUS
         DS    CL44                       FILLER
*
         DS    0D
WORK0LEN EQU   *-WORK0
*
         EJECT
**********************************************************************
***  WORK AREA FOR RESOURCE INFORMATION BLOCK                      ***
**********************************************************************
WORK1    DSECT                          AREA-1 FOR RESOURCE INFO BLOCKS
RETAREA  DS    CL4096
*
WORK1END DS    0D
WORK1LEN EQU   *-WORK1                  TOTAL LENGTH OF WORK-1 AREA
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING MACROS                                               ***
***                                                                ***
**********************************************************************
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         PRINT GEN
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         ISGRIB                         RESOURCE INFO BLOCK (3 DSECTS)
         END
