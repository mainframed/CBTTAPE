PROC 0 ADD TRACE
/*                                                                    *
/*                                                                    *
/*            * * *  DAYTIMER APPOINTMENT CALENDAR * * *              *
/*                                                                    *
/*                                                                    *
/*                                                                    *
/*REVISED:  FEBRUARY 20, 1991                                         *
/*                                                                    *
/*          GEORGE RAMAS                                              *
/*          SYSTEMS ANALYST                                           *
/*          CATELLUS DEVELOPMENT CORPORATION                          *
/*          MIS DEPARTMENT 3RD FLOOR                                  *
/*          SAN FRANCISCO, CALIFORNIA 94105                           *
/*          (415) 974-4597                                            *
/*                                                                    *
/*          DISPLAYS & DOCU. BY:  GEORGE RAMAS                        *
/*          TECHNICAL REWRITE BY: JAY RHODES (415) 351-5411           *
/*                                                                    *
/*                                                                    *
/* THE PURPOSE OF THIS CLIST IS TO NOTIFY YOU OF CALENDER             *
/* APPOINTMENTS. YOU CAN ADD TO YOUR CALENDAR OR UPDATE AN APPOINTMENT*
/* ALREADY LOADED.  IT WILL NOTIFY YOU WHEN YOU LOG ONTO TSO AND      *
/* DISPLAY ALL APPOINTMENTS FOR THAT DAY ON YOUR FILE.  YOU CAN       *
/* ALL SEE ALL YOUR LOGGED APPOINTMENTS BY CALLING PFK6 'DAYTIMER'    *
/*                                                                    *
/*                                                                    *
/*  REQUIREMENTS:  YOUR MUST HAVE A AN 80 BYTE DATA SET CALLED:       *
/*                 LOGONID.DATE.DATA (PS FORMAT) AND A DSN CALLED     *
/*                 LOGONID.DAYTIMER.ISPTLIB (PO FORMAT) 80 BYTE       *
/*                 RECFM.                                             *
/*                                                                    *
/*                 THESE PANELS ARE SETUP FOR COLOR MONITORS, BUT IT'S*
/*                 NOT NECESSARY TO HAVE ONE.                         *
/*                                                                    *
/*                 YOU NEED TO COPY 'THIS' CLIST MEMBER INTO YOUR     *
/*                 CLIST LIBRARY.                                     *
/*                                                                    *
/*                 DAYTIMEA    IN YOUR PLIB                           *
/*                 DAYTIMER    IN YOUR PLIB (CAREFUL NOT TO MIX UP    *
/*                                           YOU PLIB & CLIST MEMBER) *
/*                 DTMM00      IN YOUR MLIB                           *
/*                 DTMM01      IN YOUR MLIB                           *
/*                                                                    *
/*                 IN ORDER TO HAVE YOUR APPOINTMENTS DISPLAY         *
/*                 WHEN YOU LOGON YOU MUST HAVE A 'SIGNON' MEMBER     *
/*                 IN YOUR CLIST WHICH CONTAIN 'DAYTIMER'             *
/*                                                                    *
/*            COLUMN 1                                                *
/*                   WRITE  **  EXECUTE SIGNON                        *
/*                   DAYTIMER                                         *
/*                   SDATE                                            *
/*                                                                    *
/*                   OR HAVE 'DAYTIMER' CANCATENATED IN YOUR TSO      *
/*                   LOGON PROC.                                      *
/*                                                                    *
/*                 CHECK YOU TSO LOGON PROC, YOUR LIBRARIES MAY AL-   *
/*                 READY BE CANCATENATED.  VERIFY YOUR CANCATENATION  *
/*                 WITH YOUR TECHNICAL SERVICES DEPARTMENT.           *
/*                                                                    *
/*                 ALLOCATE A PDS LIBRARY 80/9440 CALLED LOGID.PLIB   *
/*                 AND COPY THE SUPPLIED PLIB MEMBERS INTO THIS.      *
/*                                                                    *
/*                 ALLOCATE A PDS LIBRARY 80/9440 CALLED LOGID.MLIB   *
/*                 AND COPY THE SUPPLIED MLIB MEMBERS INTO THIS.      *
/*                                                                    *
/*                 YOU ALSO NEED THE SUPPLIED LOAD PROGRAM 'CLEAR' IN *
/*                 YOUR LOAD LIBRARY.  CHANGE &UTIL BELOW FOR YOUR    *
/*                 LOAD LIBRARY.                                      *
/*                                                                    *
/*          >>>>>> BE CAREFUL WHEN SETTING THIS UP, IF YOU MESS UP    *
/*                 YOUR SIGNON CLIST YOU WILL HAVE TO HAVE IT RESTORED*
/*                 TO BE ABLE TO LOGON AND FIX IT OR RENAME IT        *
/*                 THROUGH YOUR TSO READY COMMAND.                    *
/*                                                                    *
/*                                                                    *
/*            TO PERFORM A 'DISPLAY' KEY IN 'DAYTIMER' ON YOUR        *
/*            TSO (PF6) SCREEN AND THIS WILL DISPLAY ALL APPOINTMENTS *
/*            YOU HAVE ENTERED.  YOU CAN PERFORM EDITS AT THIS DISPLAY*
/*            BY USING THE AN 'A' FOR ADD AT THE COMMAND LINE, 'D'    *
/*            FOR DELETION AND 'U' FOR UPDATING NEXT TO THE DESIRED   *
/*            COMMENT.                                                *
/*                                                                    *
/*            ITEMS WILL AUTOMATICALLY DELETE WHEN APPOINTMENT DATE   *
/*            IS LESS THAN YOUR LOGON DATE.  IF NO ITEMS RESIDE IN    *
/*            YOUR DATA SET, IT WILL DISPLAY 'APPOINTMENT FILE        *
/*            CLEARED' WHEN YOU LOGON TO TSO.                         *
/*                                                                    *
/*            WHEN YOU DISPLAY YOUR APPOINTMENTS AT A COMMAND CLIST   *
/*            LEVEL, SOME APPOINTMENTS MAY NOT BE IN SORTED ORDER     *
/*            BECAUSE SOME APPOINTMENTS WERE INSERTED BETWEEN DATES.  *
/*            SIMPLY PERFORM A COMMAND LEVEL SORT ON THE 'AUTOMATED   *
/*            APPOINTMENT CALENDAR' PANEL DISPLAY.                    *
/*                                                                    *
/*            **NOTE** YOU CANNOT USE '&' IN YOUR TEXT.  IF YOU DO,   *
/*                     IT WILL DISPLAY AN ERROR MESSAGE WHEN YOU LOGON*
/*                     AND DELETE THAT PARTICULAR APPOINTMENT MESSAGE.*
/*                                                                    *
/*            IF YOU HAVE SEVERAL APPOINTMENTS TO ENTER YOU CAN       *
/*            EDIT YOUR FILE 'LOGONID.DATE.DATA' AND MANUALLY ADD     *
/*            ALL YOUR APPOINTMENTS AS LONG AS IT IS IN THE CORRECT   *
/*            FORMAT.  WHEN ADDING AN APPOINTMENT WITH THE 'ADD/UPDATE'
/*            FEATURE OR EDIT TO 'LOGONID.DATE.DATA, YOUR MAXIMUM     *
/*            TEXT LENGTH IS 53 BYTES.                                *
/*                                                                    *
/*                                                                    *
/*            ***IMPORTANT ***                                        *
/*                                                                    *
/*            BE SURE TO RENAME YOUR PANEL MEMBER 'P#PAYTMR' TO       *
/*            'DAYTIMER' AND YOUR CLIST MEMBER 'C#DAYTMR' ALSO        *
/*            TO 'DAYTIMER' IN THEIR RESPECTIVE LIBRARIES IF YOU      *
/*            RECEIVE THEM IN THIS RENAME FORMAT.                     *
/*                                                                    *
/*                                                                    *
/*                                                                    *
/*--------------------------------------------------------------------*
/*
IF &TRACE = THEN -
   CONTROL NOMSG ASIS
ELSE -
   CONTROL MSG CONLIST LIST ASIS
/*
/*--------------------------------------------------------------------*
/* INITIALIZE VARIABLES FOR COMMAND PROCESSOR                         *
/*--------------------------------------------------------------------*
/*
SET  &DISP     = 0                /* LOOP CONTROL 4 DISPLAY
SET  &GET      = 0                /* LOOP CONTROL 4 RECORD RETRIEVE
SET  &N        = 1                /* LOOP CONTROL 4 RECORD UPDATE
SET  &M        = 1                /* LOOP CONTROL 4 RECORD DELETE/UPDATE
SET  &A        = 1                /* CONTROL FOR FILE OPEN DURING UPDATE
SET  &F        = 0                /* LOOP CONTROL FOR 5 DAY FORECAST
SET  &YN       = Y                /* LOOP CONTROL FOR CONTIUEOUS UPDATE
SET  &ACTIVITY = 0                /* RECORD ACTIVITY CONTROLLER
SET  &KEEP     = 0                /* DISPLAY CONTROLLER
SET  &DAYTIMER = &SYSUID.W        /* DAYTIMER TABLE FILE
SET  &FUTURE   = 0                /* SET FUTURE BIT
SET  &UTIL     = DEV.GLR.LOADLIB  /* LOAD LIBRARY FOR 'CLEAR' PROGRAM
SET  &PGM      = CLEAR
SET  &PAN1     = DAYTIMER
SET  &PAN2     = DAYTIMEA
SET  &LIB      = DAYTLIB
SET  &JULN     = &SUBSTR(1:2,&SYSJDATE)+
                 &SUBSTR(4:6,&SYSJDATE)
SET  &SDATE    = &SUBSTR(7:8,&SYSDATE)+
                 &SUBSTR(1:2,&SYSDATE)+
                 &SUBSTR(4:5,&SYSDATE)
/*
/*--------------------------------------------------------------------*
/*  ALLOCATE USER APPOINTMENT FILE: DSORG(PS)                         *
/*--------------------------------------------------------------------*
/*
IF &SYSISPF = &STR(NOT ACTIVE) THEN -
   DO
     FREE F(DATE)
     ALLOC F(DATE) DA('&SYSUID..DATE.DATA')   SHR
     OPENFILE DATE
/*
/*--------------------------------------------------------------------*
/*  EXECUTE THE FOLLOWING WHEN A GETFILE ERROR OCCURRS                *
/*--------------------------------------------------------------------*
/*
     CALL  '&UTIL(&PGM)'
     ERROR -
     DO
       ERROR OFF
       CLOSFILE DATE
       IF &N = 1 THEN -
          DO
            WRITE
            WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
            WRITE YOUR APPOINTMENT CALENDAR IS CLEAR
            WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
            OPENFILE DATE OUTPUT
            SET &A = 2
          END
       ELSE -
          DO
            WRITE
            OPENFILE DATE OUTPUT
          END
/*
/*--------------------------------------------------------------------*
/*  IF DATA EXIST IN APPT FILE, KEEP ONLY UNEXPIRED RECORDS USING     *
/*  DOUBLE SUBSTITUTION.                                              *
/*--------------------------------------------------------------------*
/*
       IF &N > 1 THEN -
       DO WHILE &M ¬= &N
          SET XXXX = &&D&M
          SET &DATE = &STR(&XXXX)
          PUTFILE DATE
          SET &M = &M + 1
          SET &A = 2
       END
/*
/*--------------------------------------------------------------------*
/*  THE FOLLOWING ROUTINE IS EXECUTED IF THE USER REQUESTED TO ADD    *
/*  TEXT TO APPOINTMENT FILE.                                         *
/*--------------------------------------------------------------------*
/*
       IF &ADD ¬= THEN -
          DO WHILE &YN = Y
             WRITENR PRESS (ENTER) TO EXIT OR ENTER DATE IN FORM MM/DD/Y
             READ &D
             IF &NRSTR(&D) ¬= THEN -
                DO
                  WRITENR ENTER TEXT:
                  READ
                  SET &DATE = &NRSTR(&D &SYSDVAL)
                  PUTFILE DATE
                  SET &A = 2
                END
             ELSE -
               DO
                 SET &YN = N
                 CLOSFILE DATE
                 FREE F(DATE)
               END
          END
     END
/*
/*--------------------------------------------------------------------*
/*  RETRIEVE RECORD FROM USER APPOINTMENT FILE                        *
/*--------------------------------------------------------------------*
/*
     IF &A = 1 THEN -
        DO WHILE &GET = 0
           CONTROL NOMSG
           GETFILE DATE
           SET &GET = &LASTCC
           SET &F   = 0
           IF &GET = 0 THEN -
              DO
                SET &DDATE = &SUBSTR(7:8,&NRSTR(&DATE))+
                             &SUBSTR(1:2,&NRSTR(&DATE))+
                             &SUBSTR(4:5,&NRSTR(&DATE))
/*
/*--------------------------------------------------------------------*
/*  IF APPOINTMENT DATE IS LESS THAN CURRENT DATE, MARK ITEM FOR      *
/*  DELETION.                                                         *
/*--------------------------------------------------------------------*
/*
                IF &NRSTR(&DDATE) < &SDATE THEN -
                   DO
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
WRITE OLD APPOINTMENT DELETED:
WRITE &DATE
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
                     SET &PROCESS = 0
                     SET &FUTURE  = 1
                   END
/*
/*--------------------------------------------------------------------*
/*  OTHERWISE, DISPLAY                                                *
/*--------------------------------------------------------------------*
/*
                IF &SDATE EQ &DDATE THEN  -
                   DO
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
WRITE APPOINTMENTS FOR TODAY:
WRITE &DATE
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
                     SET &PROCESS = 1
                   END
/*
                DO WHILE &F < 1
                   SET &F = &F + 1
                   IF &SDATE+&F = &DDATE THEN -
                   DO
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
WRITE APPOINTMENTS COMING UP TOMORROW
WRITE &DATE
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
                     SET &PROCESS = 1
                   END
                END
/*
/*--------------------------------------------------------------------*
/*  PERFORM 5 DAY FORECAST.                                           *
/*--------------------------------------------------------------------*
/*
                DO WHILE &F < 7
                   SET &F = &F + 1
                   IF &SDATE+&F = &DDATE THEN -
                   DO
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
WRITE APPOINTMENTS COMING UP WITHIN THE NEXT 7 DAYS
WRITE &DATE
WRITE *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
                     SET &PROCESS = 1
                   END
                END
/*
/*--------------------------------------------------------------------*
/*  OTHERWISE, THIS IS FOR THE FAR FUTURE (GEORGE)                    *
/*--------------------------------------------------------------------*
/*
                IF &FUTURE = 0 THEN -
                   SET &PROCESS = 1
/*
/*--------------------------------------------------------------------*
/*  IF ITEM WAS DISPLAYED TO USER IS >= TO CURRENT DATE, KEEP         *
/*--------------------------------------------------------------------*
/*
                IF  &PROCESS  > 0 THEN -
                    DO
                      SET D&N = &DATE
                      SET &N  = &N + 1
                    END
                SET &FUTURE = 0
              END
        END
     EXIT
   END
/*
/*--------------------------------------------------------------------*
/*  OTHERWISE, AN ISPF ENVIRONMENT EXIST SO INVOKE PANEL FOR PROCESS  *
/*--------------------------------------------------------------------*
/*
ELSE -
   DO
     FREE F(DATE)
     ALLOC F(DATE) DA('&SYSUID..DATE.DATA')   SHR
     OPENFILE DATE
     SET &ALLOC = &LASTCC
     ISPEXEC CONTROL ERRORS RETURN
     ISPEXEC LIBDEF ISPPLIB DATASET ID('&SYSUID..PLIB')
     ISPEXEC LIBDEF ISPMLIB DATASET ID('&SYSUID..MLIB')
     ISPEXEC LIBDEF &LIB DATASET ID('&SYSUID..DAYTIMER.ISPTLIB')
     ISPEXEC TBCLOSE &DAYTIMER LIBRARY(&LIB)
     ISPEXEC TBERASE &DAYTIMER LIBRARY(&LIB)
     ISPEXEC TBCREATE &DAYTIMER KEYS(RCNT) NAMES(DIMM DIDD DIYY DTXT) +
             LIBRARY(&LIB)
     SET &RC = &LASTCC
     IF &RC = 16 THEN -
        DO
          ISPEXEC SETMSG MSG(DTMM013)
          EXIT
        END
     ISPEXEC TBSORT   &DAYTIMER FIELDS(RCNT,N,A)
   END
/*
/*--------------------------------------------------------------------*
/*  PERFORM THE FOLLOWING IF ERROR OCCURS WHILE RETREIVING RECORDS    *
/*--------------------------------------------------------------------*
/*
     ERROR DO
             ERROR OFF
             CLOSFILE DATE
             FREE F(DATE)
             ISPEXEC TBTOP &DAYTIMER
/*
/*--------------------------------------------------------------------*
/*  DISPLAY WORK TABLE FOR USER VIEW, ADD OR DELETE                   *
/*--------------------------------------------------------------------*
/*
             DO WHILE &DISP <= 4
                ISPEXEC TBDISPL &DAYTIMER PANEL(&PAN1)
                SET &DISP  = &LASTCC
                SET &DISP1 = &DISP
                SET &KEEP  = 0
                SET &CTR   = 0
/*
/*--------------------------------------------------------------------*
/*  USER REQUESTED TO SORT TABLE. SORT ONLY OCCURS IN DATE ASCENDING  *
/*  ORDER.                                                            *
/*--------------------------------------------------------------------*
/*
                IF &DCMD = SORT THEN -
                   DO
                     ISPEXEC TBSORT &DAYTIMER  +
                             FIELDS(DIMM,N,A,DIDD,N,A,DIYY,N,A)
                     IF &LASTCC = 0 THEN -
                        SET &CHNG = 1
                   END
/*
/*--------------------------------------------------------------------*
/*  USER REQUESTED TO UPDATE ENTRY                                    *
/*--------------------------------------------------------------------*
/*
                IF &DSEL = U THEN -
                   DO WHILE &ZTDSELS > 0
                      ISPEXEC CONTROL DISPLAY SAVE
                      ISPEXEC TBPUT    &DAYTIMER
                      SET &KEEP = 1
                      ISPEXEC DISPLAY PANEL(&PAN2)
                      IF &LASTCC = 0 THEN -
                         DO
                           SET &CTR  = &CTR + 1
                           SET &CHNG = 1
                           ISPEXEC TBMOD &DAYTIMER ORDER
                         END
                      ISPEXEC CONTROL DISPLAY RESTORE
                      IF &ZTDSELS > 1 THEN -
                         ISPEXEC TBDISPL &DAYTIMER
                      ELSE -
                         SET &ZTDSELS = 0
                      ISPEXEC SETMSG MSG(DTMM012)
                   END
/*
/*--------------------------------------------------------------------*
/*  USER REQUESTED TO DELETE ENTRY                                    *
/*--------------------------------------------------------------------*
/*
                IF &DSEL = D THEN -
                   DO WHILE &ZTDSELS > 0
                      ISPEXEC CONTROL DISPLAY SAVE
                      ISPEXEC TBPUT    &DAYTIMER
                      ISPEXEC TBDELETE &DAYTIMER
                      IF &LASTCC = 0 THEN -
                         DO
                           SET &CTR  = &CTR + 1
                           SET &CHNG = 1
                         END
                      ISPEXEC CONTROL DISPLAY RESTORE
                      IF &ZTDSELS > 1 THEN -
                         DO
                           ISPEXEC TBDISPL &DAYTIMER
                         END
                      ELSE -
                         DO
                           SET &ZTDSELS = 0
                           ISPEXEC TBTOP &DAYTIMER
                         END
                      ISPEXEC SETMSG MSG(DTMM010)
                   END
/*
/*--------------------------------------------------------------------*
/*  USER REQUESTED TO ADD NEW ENTRY                                   *
/*--------------------------------------------------------------------*
/*
                IF &DSEL = A   OR   +
                   &DCMD = A   THEN -
                   DO WHILE &DISP1 = 0
                      ISPEXEC DISPLAY PANEL(&PAN2)
                      SET &DISP1 = &LASTCC
                      IF &DISP1  = 0 THEN -
                         DO
                           ISPEXEC TBSORT &DAYTIMER FIELDS(RCNT,N,A)
                           ISPEXEC TBBOTTOM &DAYTIMER
                           SET &RCNT = &RCNT + 1
                           ISPEXEC VGET (DIMM DIDD DIYY DTXT)
                           ISPEXEC TBADD &DAYTIMER ORDER
                           SET &ADD = &LASTCC
/*
/*--------------------------------------------------------------------*
/*  IF ADD WAS SUCCESSFUL, DISPLAY MESSAGE AND SET CHNG FLAG          *
/*--------------------------------------------------------------------*
/*
                           IF  &ADD = 0 THEN -
                               DO
                                 ISPEXEC TBTOP &DAYTIMER
                                 ISPEXEC SETMSG MSG(DTMM000)
                                 SET &CHNG = 1
                               END
                           ELSE -
                               DO
                                 ISPEXEC TBTOP &DAYTIMER
                                 ISPEXEC SETMSG MSG(DTMM001)
                                 SET &CHNG = 0
                               END
                         END
                   END
             END
/*
/*--------------------------------------------------------------------*
/*  IF USER ADDED/DELETED APPOINTMENTS, UPDATE DATE FILE              *
/*--------------------------------------------------------------------*
/*
             IF &CHNG = 1 THEN -
                DO
                  SET &SKIP = 0
                  ISPEXEC TBTOP &DAYTIMER
                  ALLOC F(DATE) DA('&SYSUID..DATE.DATA') SHR REUSE
                  OPENFILE DATE OUTPUT
                  DO WHILE &SKIP = 0
                     ISPEXEC TBSKIP &DAYTIMER
                     SET &SKIP = &LASTCC
                     IF &SKIP = 0 THEN -
                        DO
                          SET &DATE = &NRSTR(&DIMM/&DIDD/&DIYY +
                                             &DTXT)
                          PUTFILE DATE
                        END
                  END
                  CLOSFILE DATE
                  FREE F(DATE)
                  ISPEXEC SETMSG MSG(DTMM009)
                END
/*
/*--------------------------------------------------------------------*
/*  DELETE USER WORK TABLE AND EXIT                                   *
/*--------------------------------------------------------------------*
/*
             ISPEXEC TBEND &DAYTIMER
             EXIT
           END
/*
/*--------------------------------------------------------------------*
/*  RETRIEVE RECORDS FROM USER LOGONID.DATE.DATA FILE                 *
/*--------------------------------------------------------------------*
/*
     DO WHILE &GET = 0
        GETFILE DATE
        SET &GET = &LASTCC
        IF  &GET = 0 THEN -
            DO
              SET &RCNT = &RCNT + 1
              SET &L    = &LENGTH(&NRSTR(&DATE))
              SET &DIMM = &SUBSTR(1:2,&NRSTR(&DATE))
              SET &DIDD = &SUBSTR(4:5,&NRSTR(&DATE))
              SET &DIYY = &SUBSTR(7:8,&NRSTR(&DATE))
              SET &DTXT = &SUBSTR(10:&L,&NRSTR(&DATE))
              ISPEXEC TBADD &DAYTIMER ORDER
              SET &ADD = &LASTCC
              IF  &ADD = 0 THEN -
                  SET &UPD = 1
            END
     END
   END
