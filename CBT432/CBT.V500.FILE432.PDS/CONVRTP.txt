  /* REXX                                                          */
  /*                                                               */
  /*  *--------------------------------------------------------*   */
  /*  * CONVERSION DES ENTREES CATALOGUE 3480 3480X EN 3490    *   */
  /*  * (NECESSAIRE POUR PASSAGE EN 7490E OU 3490E)            *   */
  /*  *--------------------------------------------------------*   */
  /*                                                               */
  /*     CARTES DD NECESSAIRES POUR EXEC BATCH DE CET EXEC REXX :  */
  /*                                                               */
  /* //LISTCAT  RESULTAT D'UN LISTCAT BATCH NONVSAM    : INPUT     */
  /* //OLDDEF   PRODUIT LES DEFINE NONVSAM 3480 (IDEM) : OUTPUT    */
  /* //NEWDEF   PRODUIT LES DEFINE NONVSAM 3490        : OUTPUT    */
  /*                                                               */
  /*     VOIR L'EXEMPLE DE JCL QUI SE TROUVE A LA FIN              */
  /*                                                               */
  /* LIMITE MAXIMALE DU NOMBRE DE VOLSERS PAR FICHIERS : CF LIMV   */
  /*                                                               */

  /*                                                               */
  /*    P A R A M E T R E S    (M O D I F I A B L E S)             */
  /*                                                               */
  LIMV = 17    /* PAS PLUS DE 17 VOLUMES POUR 1 FICHIER CASSETTE   */
  OWNER = '7490E'      /* POUR REPERER LES ENTREES CONVERTIES      */

  'EXECIO * DISKR LISTCAT (STEM REC. FINIS'

  /* ETABLIR LA BORNE DROITE DES ORDRES IDCAMS */
  RIGHT = LIMV*7 + 9
  SAY '** LES FICHIERS OLDDEF ET NEWDEF DEVRAIENT AVOIR UN LRECL >' ,
      1+RIGHT

  /*                        */
  /* SUPPRIMER LE CODE SAUT */
  /*                        */
  ARG OPT
  I = 1
  DO WHILE I \> REC.0
     L = LENGTH(REC.I)
     IF L > 1 THEN REC.I = SUBSTR(REC.I,2,L-1)   /* DEL CODE SAUT */
     I = I+1
     END
  BLANC24 =     '                        '
  TIRET24 =     '------------------------'
  TOT = 0
  TOTLU = 0
  TOTMI = 0
  NBERR = 0
  I = 0
  DS = ''
  NN = 1
  OLD.1 = '  PARM MARGINS(2 '||RIGHT||') '
  NEW.1 = OLD.1

  DO WHILE I  <  REC.0
  I  = I  + 1
  IF WORDS(REC.I) = 0 THEN ITERATE
  IF WORD(REC.I,1) = 'NONVSAM'         THEN DO   /* NOUVEAU FICHIER */

        /* TRAITER FICH PRECEDENT */
          IF N > 0 & DS \= '' THEN DO
          NN = NN+1
          TOT = TOT+1
          OLD.NN = '   DELETE  ' DS '  NSCR'
          NEW.NN = OLD.NN
          NN = NN+1
          OLD.NN = '   DEF NVSAM ( NAME (' DS ') -'
          NEW.NN = OLD.NN
          K = 1
          VOLUMES = ''
          DEVT    = ''
          DEVTO   = ''
          FSEQN   = ''
          IF N > LIMV THEN DO
             SAY 'CONVRTP - ** ERREUR : PLUS DE' LIMV 'VOLUMES POUR' DS
                  Z = LIMV
                  DO WHILE Z < N
                  Z=Z+1
                  SAY 'CONVRTP -**           VOLUME IGNORE : ' V.Z
                  END
             NBERR = NBERR + 1
             N = LIMV   /* FORCER A LA LIMITE */
             END
          DO WHILE K \> N
             VOLUMES = VOLUMES V.K
             DEVT    = DEVT    '3490'
             DEVTO   = DEVTO   D.K
          /* CONTROLE DU FSEQN : VALEUR 0 NON SUPPORTEE PAR IDCAMS */
             IF F.K = 0 THEN DO
                SAY '** FSEQN = 0 NON SUPPORTE POUR' DS ', CHANGE EN 1'
                F.K = 1
                END
             FSEQN   = FSEQN   F.K
             K = K+1
             END
          NN = NN+1
          OLD.NN = ' VOL('||VOLUMES||') -'
          NEW.NN = OLD.NN
          NN = NN+1
          OLD.NN = ' DEVT    ('  DEVTO   ') -'
          NEW.NN = ' DEVT    ('  DEVT    ') -'
                  IF OWNER \= '' THEN DO
                  NN = NN+1
                  OLD.NN = ' OWNER   ('  OWNER   ') -'
                  NEW.NN = OLD.NN
                  END
          NN = NN+1
          OLD.NN = ' FSEQN   ('  FSEQN   '))'
          NEW.NN = OLD.NN
          END
        /* FIN TRAITEMENT FICHIER PRECEDENT */

        IF  WORD(REC.I,3) = '' THEN ITERATE /* STATS FINAL LISTC*/
        TOTLU = TOTLU+1

        DS = WORD(REC.I,3)
        IF OPT = 'DEBUG' THEN SAY DS
        N = 0                       /* NBRE DE VOLUMES */
        F = 1
        END

  IF LENGTH(REC.I) < 6 THEN ITERATE

  IF SUBSTR(WORD(REC.I,1),1,6) = 'VOLSER'  & DS \= ''  THEN DO
        N = N+1
        V  = WORD(REC.I,1)   /*  VOLSER    */
        D  = WORD(REC.I,2)   /*  DEVTYPE   */
        F  = WORD(REC.I,3)   /*  FSEQN     */
        V  = TRANSLATE(V,BLANC24,TIRET24)
        D  = TRANSLATE(D,BLANC24,TIRET24)
        F  = TRANSLATE(F,BLANC24,TIRET24)
        V = WORD(V,2)
        D = WORD(D,2)
        IF  D = 'X''78008080''' THEN D = '3480'
        IF  D = 'X''78048080''' THEN D = '3480X'
        F = WORD(F,2)
        IF OPT = 'DEBUG' THEN SAY V D F

        /* NE PAS GARDER LES MIGRATES */
        IF  V = 'MIGRAT' THEN DO
             DS = ''
             TOTMI=TOTMI+1
             ITERATE
             END

        /* NE GARDER QUE 3480 ET 3480X */
        IF  D \= '3480' & D \= '3480X' THEN DS = ''
        ELSE DO
             V.N = V
             D.N = D
             F.N = F
             END
     END
  END
  'EXECIO * DISKW OLDDEF  (STEM OLD. FINIS'
  'EXECIO * DISKW NEWDEF  (STEM NEW. FINIS'
  SAY 'CONVRTP ** ' TOTLU 'FICHIERS AU CATALOGUE'
  SAY 'CONVRTP ** ' TOTMI 'FICHIERS MIGRES (NON TRAITES)'
  SAY 'CONVRTP ** ' TOT 'ENTREES CATALOGUES TRAITEES POUR CONVERSION'
  IF NBERR > 0 THEN ,
  SAY 'CONVRTP **   DONT' NBERR 'EN ERREUR (PLUS DE' LIMV 'VOLUMES)'
  EXIT(NBERR)
  /*                                                               */
  /*     EXEMPLE DE JCL POUR EXPLOITER CET EXEC REXX :             */
  /*                                                               */
  /* //# LISTER LES ENTREES A CONVERTIR                            */
  /* //LISTCAT EXEC  PGM=IDCAMS                                    */
  /* //SYSPRINT  DD  DISP=(NEW,CATLG),UNIT=3390,SPACE=(CYL,(4,4)), */
  /* //  LRECL=125,RECFM=VBA,DSN=SY02.CONVRTP.PRINT                */
  /* //SYSIN     DD  *                                             */
  /*      LISTC NVSAM ALL    CAT(CATALOG.MVSICF.VMVS132)           */
  /*      LISTC NVSAM ALL    CAT(CATALOG.MVSICF.VEXPP01)           */
  /*      LISTC NVSAM ALL  LVL(KES6)                               */
  /* //#                                                           */
  /* //PROCESS EXEC  PGM=IKJEFT01,PARM='%CONVRTP'                  */
  /* //SYSPROC   DD  DISP=SHR,DSN=SY02.MVS.CLIST                   */
  /* //LISTCAT   DD  DISP=SHR,DSN=SY02.CONVRTP.PRINT               */
  /* //OLDDEF    DD  DISP=(NEW,CATLG),UNIT=3390,SPACE=(CYL,(4,4)), */
  /* //  LRECL=133,RECFM=FB,DSN=SY02.CONVRTP.OLDDEF                */
  /* //NEWDEF    DD  DISP=(NEW,CATLG),UNIT=3390,SPACE=(CYL,(4,4)), */
  /* //  LRECL=133,RECFM=FB,DSN=SY02.CONVRTP.NEWDEF                */
  /* //SYSTSPRT  DD  SYSOUT=*                                      */
  /* //SYSTSIN   DD  DUMMY                                         */
  /* //#                                                           */
  /* //# PASSER LES ORDRES IDCAMS DE CONVERSION                    */
  /* //CONVERT EXEC  PGM=IDCAMS                                    */
  /* //SYSPRINT  DD  SYSOUT=*                                      */
  /* //SYSIN     DD  DISP=SHR,DSN=*.PROCESS.NEWDEF                 */
