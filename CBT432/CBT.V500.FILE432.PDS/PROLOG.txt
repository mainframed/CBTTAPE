         MACRO
.* PARTICULARITES   :
.* MACROS INVOQUEES : SAVE GETMAIN
&NLA  PROLOG &AUTHOR=,&BASEREG=,&RENT=NO,&REQUATE=NO,                  *
               &COMMENT=' '
.* CETTE MACRO ETABLIT LE CHAINAGE DES SAVES, INITIALISE LES REGISTRES
.* DE BASE, CREE LA SAVE AREA DU PROGRAMME.       PARAMETRES :
.*           AUTHOR : CARACTERES QUELCONQUES, GENERE 'AUTHOR=...'
.*           BASEREG=(...,...,) : LISTE DES REGISTRES DE BASE
.*           RENT : YES OU NO SELON QUE PGM RE-ENTRANT OU PAS
.*           REQUATE : YES POUR AVOIR EQUIVALENCE R1<->1 ETC.
.*
.* EXEMPLE : PROLOG AUTHOR=A.B.,BASEREG=(12,11),REQUATE=YES,
.*             COMMENT='PROGRAMME DE TEST'
.*
        LCLA    &I,&D
        LCLC    &CH,&MONTH(12),&MOIS,&JOUR,&AN,&M
&MONTH(1) SETC 'JANVIER','FEVRIER','MARS'
&MONTH(4) SETC  'AVRIL','MAI','JUIN','JUILLET'
&MONTH(8) SETC 'AOUT','SEPTEMBRE','OCTOBRE','NOVEMBRE','DECEMBRE'
&JOUR       SETC  '&SYSDATE'(4,2)
&AN         SETC  '&SYSDATE'(7,2)
&M          SETC  '&SYSDATE'(1,2)
&MOIS       SETC  '&MONTH(&M)'
.*
        AIF     ('&REQUATE' NE 'YES').DEB
&I      SETA    0
.INCR   ANOP
R&I     EQU     &I
&I      SETA    &I+1
        AIF     (&I LT 16).INCR
.DEB    ANOP
 MNOTE '*--------------------------------------------------------*'     '*
 MNOTE '* CSECT               : &SYSECT'
 MNOTE '* RE-ENTRANT (YES/NO) : &RENT'
 MNOTE '* DATE  ASSEMBLAGE    : &JOUR &MOIS 19&AN'
 MNOTE '* HEURE ASSEMBLAGE    : &SYSTIME'
 MNOTE '* AUTEUR              : &AUTHOR'
 MNOTE '* PARAMETRES (SYSPARM): &SYSPARM'
 MNOTE '* REGISTRE(S) DE BASE : &BASEREG'
 MNOTE '*--------------------------------------------------------*'
 TITLE &COMMENT
        USING *,15
&NLA    SAVE    (14,12),,&SYSECT-&SYSDATE-&SYSTIME-&AUTHOR
        AIF     ('&RENT' EQ 'NO').LOAD
        AIF     ('&RENT' NE 'YES').ERR1
        GETMAIN R,LV=72  GET 72 BYTES OF VIRTUAL STORAGE
        ST      13,4(1)  STORE ADDRESS OF OLD SAVE AREA
        ST      1,8(13)  STORE ADDRESS OF NEW SAVE AREA
        LR      13,1     R13 CONTAINS ADDRESS OF NEW SAVE (FOR SUBPGMS)
        L       1,4(1)   LOAD ADDRESS OF OLD SAVE AREA
        L       15,16(1) RESTAURER REG15 TO ENTRY POINT ADDRESS
        L       1,24(1)  RESTAURE REG1 FROM REG1 IN OLD SAVE
        AGO     .LOAD
.*
.LOAD   ANOP
        DROP    15
.* CONSTRUCTION "USING" DES REGISTRES DE BASE
&CH     SETC    '&BASEREG(1)'
&I      SETA     2
.T1     AIF     (&I GT N'&BASEREG).SUIT1
&CH     SETC    '&CH'.','.'&BASEREG(&I)'
&I      SETA    &I+1
        AGO     .T1
.SUIT1  ANOP
        LR      &BASEREG(1),15
.*      LA      &BASEREG(1),0(&BASEREG(1))
        USING   &SYSECT,&CH
.* CONSTRUCTION CHARGEMENT DES REGISTRES DE BASE
&I      SETA    2
.T2     AIF     (&I GT N'&BASEREG).SUIT2
        CNOP    0,4
        B       *+8
        DC      F'4096'    REPRESENTS 4 K (SIZE COVERED BY 1 BASE REG)
        L       0,*-4          LOAD 4096
.T3     AIF     (&I GT N'&BASEREG).SUIT2
        AR      15,0
        LR      &BASEREG(&I),15    1ST BASE REG CONTAINS ENTRY ADDRESS
.**     LA      &BASEREG(&I),0(&BASEREG(&I))
&I      SETA    &I+1
        AGO     .T3
.SUIT2  ANOP
        AIF     ('&RENT' EQ 'NO').NORENT
        MEXIT
.*
.* CHAINAGE DES SAVE AREAS EN NON RE-ENTRANT
.NORENT ANOP
        B       PRLG&SYSNDX
        DC      C'SAVEAREA'
        CNOP    0,4
SAVE&SYSNDX     DS   18F    MY SAVE AREA (NO RE-ENTERABLE PGM)
        DC      C'END'
PRLG&SYSNDX     DS   0H
        ST      13,SAVE&SYSNDX+4   ADDRESS OF CALLER SAVE AREA IN MINE
        LA      15,SAVE&SYSNDX     ADDRESS OF MY SAVE AREA
        ST      15,8(0,13)            IS SAVED IN CALLER SAVE AREA
        LR      13,15              ADDRESSING NOW MY SAVE AREA
        MEXIT
.ERR1   ANOP
        MNOTE 8,'RENT PARAMETER MUST BE YES OR NO'
        MEXIT
        MEND
