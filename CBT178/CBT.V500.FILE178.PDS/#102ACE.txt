PROC 0
/* #102ACE *********************************************************/
/***                                                             ***/
/***   # #    1    000  2222   AAA   CCCC EEEEE                  ***/
/***  #####  11   0   0     2 A   A C     E                      ***/
/***   # #    1   0   0    2  AAAAA C     EEEE                   ***/
/***  #####   1   0   0   2   A   A C     E                      ***/
/***   # #   111   000  22222 A   A  CCCC EEEEE                  ***/
/***                                                             ***/
/***                                                             ***/
/***  THE #102ACE CLIST DISPLAYS THE CSECTACE OF THE ABEND       ***/
/***  CONTROL ELEMENT AND PLACE THE ACE IN THE STACK FOR         ***/
/***  BROWSE.                                                    ***/
/***                                                             ***/
/***  USED BY: #102IDMS                                          ***/
/***                                                             ***/
/***  USES:    #102ACEF                                          ***/
/***           BLSUFLAG                                          ***/
/***                                                             ***/
/*******************************************************************/
GLOBAL BIT1 BIT2 BIT3 BIT4 BIT5 BIT6 BIT7 BIT8
NOTE '+-------------------------------------------------+' PAGE
NOTE '| ACE ABEND CONTROL ELEMENT ANALYSIS      #102ACE |'
NOTE '+-------------------------------------------------+'
F C'CSECTACE' ADDR(0.:00FFFFFF.) NOBREAK FIRST PRINT NOTERMINAL
IF &LASTCC NE 0 THEN DO
   NOTE '+-------------------------------------------------+'
   NOTE '|  ERROR!   CSECTACE   LITERAL NOT FOUND!         |'
   NOTE '|           #102IDMS CLIST TERMINATED!            |'
   NOTE '|           CHECK SETDEF STATEMENT FOR ACCURACY.  |'
   NOTE '+-------------------------------------------------+'
   RETURN 12
END
ELSE SET &TCEADDR = 0     /* TCEADDR IS R9 FROM THE CSECTACE */
EQUATE CSECTACE X           AREA LENGTH(X'AF') +
       REMARK('ABEND CONTROL ELEMENT')
EQUATE ACEPSW  CSECTACE+8   AREA LENGTH(8) REMARK('PSW')
EQUATE ACEGPRS CSECTACE+10  AREA LENGTH(X'80') +
       REMARK('REGISTERS 0 - 15')
EQUATE ACEFPRS CSECTACE+50  AREA LENGTH(X'20') +
       REMARK('FLOATING POINT REGISTERS 0 - 4')
EQUATE ACEMSGN CSECTACE+A3  AREA LENGTH(X'4') +
       REMARK('MESSAGE ID NUMBER')
EQUATE ACEILC  CSECTACE+A8  AREA LENGTH(X'1') +
       REMARK('ILC:PGM INTERRUPT LENGTH COUNTER')
EQUATE ACEINTC CSECTACE+A9  AREA LENGTH(X'2') +
       REMARK('INTC:INTERRUPTION CODE')
%#102ACEF
LIST ACEPSW   NODISPLAY(MACHINE REQUEST)
LIST ACEGPRS  NODISPLAY(MACHINE REQUEST)
LIST ACEFPRS  NODISPLAY(MACHINE REQUEST)
LIST ACEMSGN  NODISPLAY(MACHINE REQUEST)
LIST ACEILC   NODISPLAY(MACHINE REQUEST)
LIST ACEINTC  NODISPLAY(MACHINE REQUEST)
LIST CSECTACE NODISPLAY(MACHINE REQUEST)
STACK CSECTACE REMARK('ABEND CONTROL ELEMENT')
NOTE '----------------------------------------------------------------'
