PROC 0 ALL BC53 CCE CLT CSA DB DC DCE DCETCE DM58 ESE ERE FC59 FM61 ICE-
      JD62 MAP LTT NLT PR60 RLEC SCA TCE TRACE VB50 VECTORS
/* #102IDMS ********************************************************/
/***                                                             ***/
/***   # #    1    000  2222   III  DDDD  M   M  SSSS            ***/
/***  #####  11   0   0     2   I   D   D MM MM S                ***/
/***   # #    1   0   0    2    I   D   D M M M  SSS             ***/
/***  #####   1   0   0   2     I   D   D M   M     S            ***/
/***   # #   111   000  22222  III  DDDD  M   M SSSS             ***/
/***                                                             ***/
/***                                                             ***/
/***  #102IDMS IS THE MAIN IPCS CLIST FOR RELEASE 10.2 OF IDMS.  ***/
/***                                                             ***/
/***  AT EXECUTION TIME #102IDMS CAN BE PASSED PARAMETERS TO DUMP***/
/***  ONLY THE IDMS CONTROL BLOCKS SPECIFIED BY THE USER ON THE  ***/
/***  EXEC STATEMENT.  THE USER CAN ALSO REQUEST THAT ONLY THE   ***/
/***  IDMS-DC OR DATABASE CONTROL BLOCKS BE DUMPED BY SPECIFYING ***/
/***  EITHER DC OR DB AS A PARAMETER ON THE EXEC STATEMENT.      ***/
/***                                                             ***/
/***  THE IDMS CONTROL BLOCKS ARE DIVIDED INTO TWO CATEGORIES:   ***/
/***  IDMS-DC AND IDMS DATABASE CONTROL BLOCKS AND ARE LISTED    ***/
/***  BELOW:                                                     ***/
/***                                                             ***/
/*** DC - DATA COMMUNICATION PORTION OF DUMP - DEFAULT IF NO     ***/
/***      PARAMETER IS GIVEN ON THE EXEC STATEMENT               ***/
/***  CCE      - CENTRAL CONTROL BLOCK ELEMENT                   ***/
/***  CSA      - COMMON SYSTEM AREA                              ***/
/***  DCE      - DISPATCH CONTROL ELEMENT                        ***/
/***  DCETCE   - DISPATCH CONTROL ELEMENT WITH ASSOCIATED TCE,   ***/
/***             RLE, AND RCE CHAINS DECODED.                    ***/
/***  ESE      - EXTERNAL SERVICE ELEMENT                        ***/
/***  ERE      - EXTERNAL REQUEST ELEMENT                        ***/
/***  ICE      - INTERVAL CONTROL ELEMENT                        ***/
/***  MAP      - REGION MAP ENTRIES                              ***/
/***  LTT      - LOGICAL TERMINAL TABLE - FORMAT ALL LTE'S.      ***/
/***             PTE'S, RCE/RLE CHAIN WITH SON TOBE ADD LATER    ***/
/***  NLT      - NUCLEUS LOAD TABLE                              ***/
/***  RLEC     - RUN THE RLE-RCE CHAIN OFF OF THE CSA            ***/
/***  SCA      - SUBTASK CONTROL AREA AND ASSOCIATED TCE         ***/
/***  TCE      - CSECTACE TCE POINTED TO BY R9 IN THE CSECTACE   ***/
/***             WITH LTE, PTE, SON, RLE, & RCE                  ***/
/***  TRACE    - IDMS SYSTEM TRACE TABLE ENTRIES                 ***/
/***  VECTORS  - VECTOR TABLE OF ENTRY POINTS FOR IDMS MODULES   ***/
/*** DB -  DATABASE CONTROL BLOCKS                               ***/
/***  BC53     - BUFFER CONTROL ELEMENT                          ***/
/***  CLT      - CENTRAL LOCKING TABKE INCLUDING:                ***/
/***             CLTE - CENTRAL LOCKING TABLE ELEMENTS           ***/
/***             SLT  - SYNONYM LOCKING TABLE HEADER             ***/
/***             SLTF - SYNONYM LOCKING TABLE FRAGMENTS          ***/
/***             LIDH - LOCK-UNIT TABLE HEADER                   ***/
/***             LIDF - LOCK-UNIT TABLE FRAGMENTS                ***/
/***             LIDE - LOCK-UNIT TABLE ELEMENTS                 ***/
/***             RLTH - RUN-UNIT LOCK TABLE HEADER               ***/
/***             RLTE - RUN-UNIT LOCK TABLE ELEMETS              ***/
/***             RLTF - RUN-UNIT LOCK TABLE FRAGMENTS            ***/
/***  DM58     - DMCL TABLES HEADER                              ***/
/***  FC59     - DMCL FILE CONTROL ELEMENTS                      ***/
/***  FM61     - DMCL FILE-AREA MEMBERSHIP                       ***/
/***  JD62     - JOURNAL CONTROL BLOCK                           ***/
/***  PR60     - DMCL AREA MAPPING ELEMENTS                      ***/
/***  VB50     - VARIABLE IB50 SUBSCHEMA CONTROL BLOCKS          ***/
/***                                                             ***/
/***                                                             ***/
/***  WE NEED TO GIVE CREDIT WHERE CREDIT IS DUE. THE GUTS OF    ***/
/***  THIS SYSTEM OF CLIST WAS DESIGNED BY THE TECH SUPPORT      ***/
/***  STAFF OF CA. I ENHANCED IT IN ORDER TO PROVIDE ENGLISH     ***/
/***  TRANSLATION OF MANY CONTROL BLOCK INDICATORS AND FORCE ALL ***/
/***  OUTPUT TO THE IPCS PRINT FILE, AND SEPARATED FUNCTIONS     ***/
/***  INTO THEIR OWN MODULES.                                    ***/
/***                                                             ***/
/***  HISTORY: EXPAND ACE DIAGNOSTICS                  09-SEP-92 ***/
/***  HISTORY: ADD LTE PROCESSING                      01-SEP-92 ***/
/***  HISTORY: CORRECTED DCETCE COMMAND TO FORMAT EACH           ***/
/***           TCE IN SYSTEM.                          01-JUN-92 ***/
/***  HISTORY: ADD RLE/RCE CHAIN FORMAT OFF OF CSA     01-JUN-92 ***/
/***  HISTORY: ADD RLE/RCE CHAIN FORMAT OFF OF TCE     01-JUN-92 ***/
/***           WHEN FORMATTING ALL TCE'S WITH DCETCE             ***/
/***  HISTORY: ADD LTT COMMAND                         08-JUN-92 ***/
/***                                                             ***/
/*******************************************************************/
CONTROL MAIN
GLOBAL BC53A CCEA CSAA DM58A FC59A FM61A JD62A PR60A TCEADDR VB50A X
IF &ALL     NE ALL THEN     SET &ALL     = @NOTINIT
IF &BC53    NE BC53 THEN    SET &BC53    = @NOTINIT
IF &CCE     NE CCE THEN     SET &CCE     = @NOTINIT
IF &CLT     NE CLT THEN     SET &CLT     = @NOTINIT
IF &CSA     NE CSA THEN     SET &CSA     = @NOTINIT
IF &DB      NE DB THEN      SET &DB      = @NOTINIT
IF &DC      NE DC THEN      SET &DC      = @NOTINIT
IF &DCE     NE DCE THEN     SET &DCE     = @NOTINIT
IF &DCETCE  NE DCETCE THEN  SET &DCETCE  = @NOTINIT
IF &DM58    NE DM58 THEN    SET &DM58    = @NOTINIT
IF &ESE     NE ESE THEN     SET &ESE     = @NOTINIT
IF &ERE     NE ERE THEN     SET &ERE     = @NOTINIT
IF &FC59    NE FC59 THEN    SET &FC59    = @NOTINIT
IF &FM61    NE FM61 THEN    SET &FM61    = @NOTINIT
IF &ICE     NE ICE THEN     SET &ICE     = @NOTINIT
IF &JD62    NE JD62 THEN    SET &JD62    = @NOTINIT
IF &MAP     NE MAP THEN     SET &MAP     = @NOTINIT
IF &LTT     NE LTT THEN     SET &LTT     = @NOTINIT
IF &NLT     NE NLT THEN     SET &NLT     = @NOTINIT
IF &PR60    NE PR60 THEN    SET &PR60    = @NOTINIT
IF &RLEC    NE RLEC THEN    SET &RLEC    = @NOTINIT
IF &SCA     NE SCA THEN     SET &SCA     = @NOTINIT
IF &TCE     NE TCE THEN     SET &TCE     = @NOTINIT
IF &TRACE   NE TRACE THEN   SET &TRACE   = @NOTINIT
IF &VB50    NE VB50 THEN    SET &VB50    = @NOTINIT
IF &VECTORS NE VECTORS THEN SET &VECTORS = @NOTINIT
/**********************************************************************/
/* LOCATE THE IDMS CSA IN THE DUMP. IT MUST BE FOUND. OTHERWISE, QUIT!*/
/**********************************************************************/
IF &LASTCC NE 0 THEN GOTO RUNAWAY
/**********************************************************************/
/* LOCATE MODULE RHDCCOS00 IN THE DUMP. IF NOT FOUND-RUNAWAY.         */
/**********************************************************************/
FIND C'RHDCOS00' ADDR(0.:00FFFFFF.)  NOBREAK FIRST PRINT NOTERMINAL
IF &LASTCC NE 0 THEN DO
   NOTE '+-------------------------------------------------+'
   NOTE '|  ERROR!   RHDCOS00   LITERAL NOT FOUND!         |'
   NOTE '|           #102IDMS CLIST TERMINATED!            |'
   NOTE '|           CHECK SETDEF STATEMENT FOR ACCURACY.  |'
   NOTE '+-------------------------------------------------+'
   GOTO  RUNAWAY
END
/**********************************************************************/
/* USE THE CSA ADDRESS FOUND IN RHDCOS00 TO BASE THE ADDRESS OF MOST  */
/* OTHER IDMS CONTROL BLOCKS.                                         */
/**********************************************************************/
ELSE EQ CSA X+24% AREA REMARK('CSA BEGINS HERE')    NODROP
EQ CSAA X+24%     AREA REMARK('CSA BEGINS HERE')    NODROP
/**********************************************************************/
/* THE FOLLOWING ROUTINES LIST (I.E. DISPLAY) THE REQUESTED IDMS      */
/* CONTROL BLOCKS.                                                    */
/**********************************************************************/
%BLSCBSYB                         /* INITIAL ANALYSIS FOR SYSMDUMP    */
%#102ACE                          /* PROCESS THE ABEND CONTROL ELEMENT*/
IF &DB = DB OR &ALL = ALL THEN DO
   SET &DB   = DB
   SET &BC53 = BC53
   SET &CLT  = CLT
   SET &DM58 = DM58
   SET &FC59 = FC59
   SET &FM61 = FM61
   SET &JD62 = JD62
   SET &PR60 = PR60
   SET &VB50 = VB50
END
SETUPDC: +
IF &DC EQ DC OR &ALL EQ ALL THEN DO
   SET &DC  = DC
   SET &CCE = CCE
   SET &CSA = CSA
   SET &DCE = DCE
   SET &ESE = ESE
   SET &ERE = ERE
   SET &ICE = ICE
   SET &MAP = MAP
/* SET &LTT = LTT    */
   SET &NLT = NLT
   SET &RLEC = RLEC
   SET &SCA = SCA
   SET &TCE = TCE
   SET &TRACE = TRACE
   SET &VECTORS = VECTORS
END
STARTDC: +                        /* PROCESS DC CONTROL BLOCKS        */
IF &CCE = CCE  THEN DO            /* PROCESS CENTRAL CONTROL ELEMENT  */
   SET &CBPROC = YES
   %#102CCE
END
IF &CSA = CSA  THEN DO            /* PROCESS COMMON SYSTEM AREA       */
   SET &CBPROC = YES
   %#102CSA TRACE RLEC
END
IF &DCE = DCE THEN DO             /* PROCESS DISPATCH CONTROL         */
   SET &CBPROC = YES              /* ELEMENTS.                        */
   %#102DCE
END
IF &DCETCE = DCETCE THEN DO       /* PROCESS DCE/TCE CHAIN            */
   SET &CBPROC = YES
   %#102DCEC
END
IF &ESE = ESE OR +
   &ERE = ERE THEN DO             /* PROCESS EXTERNAL SERVICE AND     */
   SET &CBPROC = YES              /* EXTERNAL REQUEST ELEMENTS.       */
   %#102ESE
END
IF &ICE = ICE THEN DO             /* PROCESS INTERVAL CONTROL         */
   SET &CBPROC = YES              /* ELEMENTS.                        */
   %#102ICE
END
IF &LTT = LTT THEN DO             /* PROCESS LOGICAL TERMINAL TABLE   */
   SET &CBPROC = YES
   %#102LTT
END
IF &NLT = NLT THEN DO             /* PROCESS NUCLEUS LOAD TABLE       */
   SET &CBPROC = YES
   %#102NUC
END
IF &MAP = MAP THEN DO             /* PROCESS REGION MAP               */
   SET &CBPROC = YES
   %#102REG
END
IF &SCA = SCA THEN DO             /* PROCESS SUB TASK CONTROL AREA    */
   SET &CBPROC = YES
   %#102SCA
END
IF &TCE = TCE THEN DO             /* PROCESS THE TASK CONTROL ELEMENTS*/
   SET &CBPROC = YES
   %#102TCEG
END
IF &VECTORS = VECTORS THEN DO
   SET &CBPROC = YES
   %#102VECT
END
STARTDB: +                        /* PROCESS DATABASE CONTROL BLOCK   */
IF &DB   = DB   OR +
   &BC53 = BC53 OR +
   &DM58 = DM58 OR +
   &FC59 = FC59 OR +
   &FM61 = FM61 OR +
   &JD62 = JD62 OR +
   &PR60 = PR60 OR +
   &VB50 = VB50 THEN DO
   SET &CBPROC = YES
   %#102DM58
END
IF &BC53 = BC53 THEN DO           /* PROCESS BUFFER CONTROL           */
   SET &CBPROC = YES
   %#102BC53
END
IF &FC59 = FC59 THEN DO
   SET &CBPROC = YES
   %#102FC59
END
IF &FM61 = FM61 THEN DO
   SET &CBPROC = YES
   %#102FM61
END
IF &PR60 = PR60 THEN DO
   SET &CBPROC = YES
   %#102PR60
END
IF &VB50 = VB50 THEN DO
   SET &CBPROC = YES
   %#102VB50
END
IF &CLT = CLT THEN DO             /* PROCESS CENTRAL LOCK TABLE       */
   SET &CBPROC = YES
   %#102CLT
END
IF &JD62 = JD62 THEN DO           /* PROCESS JOURNAL DEVICE DESC      */
   SET &CBPROC = YES
   %#102JD62
END
IF &CBPROC = YES THEN GOTO OUT_OF_HERE
NOTE '+--------------------------------------------------------+'
NOTE '| NO #102IDMS PARAMETERS WERE SPECIFIED. THE DEFAULT     |'
NOTE '| IS TO DUMP DC CONTROL BLOCKS.                          |'
NOTE '+--------------------------------------------------------+'
SET &DC = DC
GOTO SETUPDC
OUT_OF_HERE:END
