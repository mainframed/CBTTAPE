PROC 0
/* #102CLT *********************************************************/
/***                                                             ***/
/***   # #    1    000  2222   CCCC L     TTTTT                  ***/
/***  #####  11   0   0     2 C     L       T                    ***/
/***   # #    1   0   0    2  C     L       T                    ***/
/***  #####   1   0   0   2   C     L       T                    ***/
/***   # #   111   000  22222  CCCC LLLLL   T                    ***/
/***                                                             ***/
/***                                                             ***/
/***  THE #102CLT CLIST DISPLAYS THE CENTRAL LOCKING TABLES      ***/
/***  AND ITS ASSOCIATED CONTROL BLOCKS.                         ***/
/***                                                             ***/
/***  USED BY: #102IDMS                                          ***/
/***                                                             ***/
/*******************************************************************/
GLOBAL BC53A CCEA CSAA DM58A FC59A FM69A JD62A PR60A TCEADDR VB50A X
NOTE '+-------------------------------------------------+' PAGE
NOTE '| CENTRAL LOCKING TABLE ANALYSIS          #102CLT |'
NOTE '+-------------------------------------------------+'
/****  GET AND LIST THE CLT AND CLTES    ****/
/****                                    ****/
EQ X CSAA+30C%
EQ RCE X-8%
/* GET LENGTH OF CLT+CLTE */
EVAL RCE+8 CLIST(STORAGE(STGLEN)) LENGTH(4)
/* MINUS THE BOOKEND */
INTEGER X'&STGLEN' CLIST(STORAGE(STGLEN)) UNSIGNED(8)
SET STGLEN = (&STGLEN - 8)
EQ CLT CSAA+30C% AREA REMARK('CENTRAL LOCK TABLE AND ELEMENTS') +
   NODROP LENGTH(&STGLEN)
LIST  CLT
STACK CLT
/****  GET AND LIST THE SLTH  AND SLTFS  ****/
/****                                    ****/
/* SLTH IS FIXED LENGTH */
EQ SLTH CLT+08% AREA REMARK('SYNONYM LOCK TABLE HEADER') +
   LENGTH(64) NODROP
LIST  SLTH
STACK SLTH
EQ XLTF SLTH+4%    /* ADDRESS OF SLTF */
EQ RCE  XLTF-8%
/* GET LENGTH OF SLTF */
EVAL  RCE+8 CLIST(STORAGE(STGLEN)) LENGTH(4)
/* MINUS THE BOOKEND */
INTEGER X'&STGLEN' CLIST(STORAGE(STGLEN)) +
      UNSIGNED(8)
SET STGLEN = (&STGLEN - 8)
EQ SLTF SLTH+4% AREA REMARK('SYNONYM LOCK FRAGMENT') +
   LENGTH(&STGLEN) NODROP
RUNCHAIN ADDR(SLTF) LINK(X'04') NAME(SLTF)  +
         NULL(X'80000000') MASK(X'F0000000')    +
         NODISPLAY(MACHINE REQUEST)         +
         DISPLAY(NOMACHINE NOREQUEST STORAGE)
/****  GET AND LIST THE LIDH  AND LIDFS  ****/
/****                                    ****/
EQ LIDH CLT+0C% AREA REMARK('LOCK ID TABLE HEADER') +
   LENGTH(64) NODROP
LIST  LIDH
STACK LIDH
/* LIDFS ARE FIXED LENGTH */
EQ LIDF LIDH+4% AREA REMARK('LOCK ID TABLE FRAGMENT') +
   LENGTH(1064) NODROP
RUNCHAIN ADDR(LIDF) LINK(X'04') NAME(LIDF)  +
         NULL(X'80000000') MASK(X'F0000000') +
         NODISPLAY(MACHINE REQUEST)         +
         DISPLAY(NOMACHINE NOREQUEST STORAGE)
/****  GET AND LIST THE LONGTERM RLTHS AND RLTFS ****/
/****                                            ****/
EQ XLTF CLT+44%     /* ADDRESS OF RLTH */
EQ RCE  XLTF-8%
/* GET LENGTH OF RLTH */
EVAL  RCE+8 CLIST(STORAGE(STGLEN)) LENGTH(4)
/* MINUS THE BOOKEND */
INTEGER X'&STGLEN' CLIST(STORAGE(STGLEN)) UNSIGNED(8)
SET STGLEN = (&STGLEN - 8)
EQ RLTH CLT+44% AREA REMARK('LONGTERM RUN UNIT LOCK TABLE HEADER') +
   LENGTH(&STGLEN) NODROP
EVALSYM CLT CLIST(ADDR(ENDRLTH))      /* SET END OF RLTH CHAIN */
EVALSYM RLTH CLIST(ADDR(NXTRLTH))     /* EXIT IF NOT RLTHS     */
IF &NXTRLTH = &ENDRLTH THEN GOTO LEAVE
RLTHLOOP:LIST RLTH
IF &STGLEN = 128 THEN DO      /* FIRST RLTF MAY BE IN RLTH STG */
   EQ XLTF RLTH+4%     /* ADDRESS OF RLTF */
   EQ RCE  XLTF-8%
   /* GET LENGTH OF RLTF */
   EVAL  RCE+8 CLIST(STORAGE(STGLEN)) LENGTH(4)
   /* MINUS BKEND */
   INTEGER X'&STGLEN' CLIST(STORAGE(STGLEN)) UNSIGNED(8)
   SET STGLEN = (&STGLEN - 8)
   EQ RLTF RLTH+4% AREA +
      REMARK('LONGTERM RUN UNIT LOCK TABLE FRAGMENT') +
      LENGTH(&STGLEN) NODROP
   RUNCHAIN ADDR(RLTF) LINK(X'04') NAME(RLTF)  +
            NULL(X'80000000') MASK(X'F0000000') +
            NODISPLAY(MACHINE REQUEST)         +
            DISPLAY(NOMACHINE NOREQUEST STORAGE)
END
EQ XLTH RLTH+44% NODROP
EQ RCE  XLTH-8%
/* GET LENGTH OF RLTH */
EVAL  RCE+8 CLIST(STORAGE(STGLEN)) LENGTH(4)
/* MINUS THE BOOKEND */
INTEGER X'&STGLEN' CLIST(STORAGE(STGLEN)) UNSIGNED(8)
SET STGLEN = (&STGLEN - 8)
EQ RLTH RLTH+44% AREA REMARK('LONGTERM RUN UNIT LOCK TABLE') +
   LENGTH(&STGLEN) NODROP
EVALSYM RLTH CLIST(ADDR(NXTRLTH))
IF &NXTRLTH = &ENDRLTH THEN GOTO LEAVE
   GOTO RLTHLOOP
LEAVE:EXIT
