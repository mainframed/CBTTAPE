READSPC  QSTART 'QUEUE COMMAND - READ A BLOCK FROM HASPACE'
***********************************************************************
* RNB CHANGES:                                                        *
*      (1) RNB03 - WITH RACF SUPPORT, WHEN A JCT IS READ WIPE OUT THE *
*                  PASSWORD FIELDS (JCTPASS, JCTNUPAS)                *
***********************************************************************
         GBLB  &QRACF                                             RNB03
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CONVERT MTTR TO MBBCCHHR                                          *
*                                                                     *
***********************************************************************
*
* NOTE - THE INPUT TO THIS ROUTINE IS AS FOLLOWS:
*            QCTRAK CONTAINS THE MTTR
*            R1 CONTAINS THE IOAREA ADDRESS
*
         LR    R2,R1          MOVE IOAREA ADDR TO R2
         SR    R3,R3          ZERO R3
         IC    R3,QCTRAKM     RELATIVE DCB NUMBER
         SLL   R3,2           MULTIPLY BY 4
         MVC   QCDADR,QCTRAKR MOVE RECORD NUMBER
         MVC   QCDADHH,QCTRAKTT MOVE TRACK TO A HALFWORD BOUNDARY
         LH    R5,QCDADHH     LOAD RELATIVE TRACK NUMBER
         SR    R4,R4          ZERO R4
         D     R4,QCTRKCYL(R3) DIVIDE TRACKS BY TRACKS PER CYLINDER
         STH   R4,QCDADHH     STORE HEAD NUMBER
         STH   R5,QCDADCC     STORE CYLINDER NUMBER
         L     R3,QCSPOOLS(R3) DCB ADDRESS
         USING IHADCB,R3      ADDRESSING FOR DCB DSECT            UF009
         MVC   DCBSYNAD+1(3),=AL3(SYNAD)  SET SYNAD ADDR IN DCB   UF009
         DROP  R3             DROP ADDRESSING FOR DCB             UF009
         READ  HDECB2,DI,(R3),(R2),MF=E
         CHECK HDECB2
         TM    QFLAG1,QFLG1IOE  I/O ERROR?                        UF009
         BZ    QSTOP          NO, RETURN TO CALLER                UF009
         NI    QFLAG1,255-QFLG1IOE  CLEAR ERROR FLAG              UF009
*        SIMULATE QTILT ACTION                                    UF009
         L     R15,QVDSPL     LOAD BASE REG                       GP@P6
         USING QDISPLAY,R15   BASE REG FOR DISPLAY WORK AREA      GP@P6
         L     R14,QDSHADO@   POINT TO SHADOW BUFFER              GP@P6
         AH    R14,QDLNELEN   POINT TO HEADING SHADOW             GP@P6
         MVI   0(R14),X'22'   RED REVERSE VIDEO FOR ERROR         GP@P6
         MVC   1(79,R14),0(R14)                                   GP@P6
         DROP  R15                                                GP@P6
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE             UF009
         LR    R14,R15        SET FOR LOOP                        UF009
         BR    R15            GO TO IT                            UF009
QSTOP    EQU   *                                                  RNB03
         AIF   (NOT &QRACF).RNB03A                                RNB03
         USING JCTDSECT,R2    SEE IF WE READ A JCT                RNB03
         CLC   JCTID,=CL4'JCT'  POSSIBLE JCT?                     RNB03
         BNE   RNB03C           /NO  - GO QSTOP                   RNB03
         CLC   JCTJNAME,JCTJMRJN  JOB NAME IN BOTH PLACES?        RNB03
         BNE   RNB03C             /NO  - GO QSTOP                 RNB03
         CLC   =C'JOB ',JCTJOBID  IS IT AN STC, A JOB, OR A TSU?  RNB03
         BE    RNB03B             IF SO, ASSUME THIS IS A JCT     RNB03
         CLC   =C'TSU ',JCTJOBID                                  RNB03
         BE    RNB03B                                             RNB03
         CLC   =C'STC ',JCTJOBID                                  RNB03
         BNE   RNB03C                                             RNB03
RNB03B   XC    JCTPASS,JCTPASS    WIPE OUT THE PASSWORDS          RNB03
         XC    JCTNUPAS,JCTNUPAS                                  RNB03
         DROP  R2                                                 RNB03
RNB03C   EQU   *                                                  RNB03
.RNB03A  ANOP                                                     RNB03
         QSTOP                                                    RNB03
SYNAD    SYNADAF ACSMETH=BDAM DECODE ERROR CAUSE                  UF009
         OI    QFLAG1,QFLG1IOE  SET I/O ERROR FLAG                UF009
         L     R15,QVDSPL     LOAD BASE REG                       UF009
         USING QDISPLAY,R15   BASE REG FOR DISPLAY WORK AREA      UF009
         MVC   QDMLNG,=H'0'   ZERO MESSAGE LENGTH                 UF009
         L     R15,QDHLINE@   POINT TO HEADING LINE               GP@P6
         DROP  R15                                                UF009
         MVC   0(80,R15),QBLANK BLANK MESSAGE AREA          GP@P6 UF009
         MVC   1(78,R15),50(R1) COPY SYNAD MESSAGE          GP@P6 UF009
         SYNADRLS ,           RELEASE WORK AREA                   UF009
         BR    R14            RETURN TO OP SYS                    UF009
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         AIF   (NOT &QRACF).RNB03X                                RNB03
READSPC  CSECT                                                    RNB03
JCT      EQU   0                                                  RNB03
BUFSTART EQU   0                                                  RNB03
BUFDSECT EQU   0                                                  RNB03
         $JCT                                                     RNB03
.RNB03X  ANOP                                                     RNB03
         QCOMMON
         DCBD  DSORG=DA,DEVD=DA                                   UF009
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
