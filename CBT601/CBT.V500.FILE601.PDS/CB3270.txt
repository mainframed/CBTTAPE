         TITLE 'CB3270 - COMPRESS A 3270 OUTPUT BUFFER - HMD 04/79'
***********************************************************************
*                                                                     *
*        CSECT CB3270 PERFORMS THE FOLLOWING:                         *
*                                                                     *
*        1 - COMPRESSES A 3270 TPUT BUFFER                            *
*                                                                     *
*            FIVE OR MORE CONSECUTIVE IDENTICAL CHARACTERS ARE        *
*            REPLACED BY A 4 BYTE REPEAT TO ADDRESS (RA) ORDER.       *
*                                                                     *
*            A SET BUFFER ADDRESS (SBA) ORDER TO THE CURRENT          *
*            SCREEN ADDRESS IS DELETED.                               *
*                                                                     *
*            PARAMETERS REQUIRED:                                     *
*                                                                     *
*            A THREE WORD PARAMETER LIST IS PROVIDED.                 *
*                                                                     *
*            WORD 1 - POINTS TO A FULL WORD CONTAINING THE LENGTH     *
*                     OF THE UNCOMPRESSED INPUT STRING.  ON EXIT      *
*                     THE WORD WILL CONTAIN THE LENGTH OF THE         *
*                     COMPRESSED OUTPUT STRING.                       *
*                                                                     *
*            WORD 2 - POINTS TO A FULL WORD CONTAINING THE NUMBER     *
*                     OF SCREEN LINES.  *** NOW CHANGED ***           *
*                     NOW IT POINTS TO A FULLWORD CONTAINING THE      *
*                     NUMBER OF SCREEN LOCATIONS.  THIS CHANGE        *
*                     MAKES THE ROUTINE SCREEN WIDTH INDEPENDENT.     *
*                     GREG PRICE, PRYCROFT SIX P/L.  JUNE 2002        *
*                                                                     *
*            WORD 3 - POINTS TO THE BEGINNING OF THE INPUT STRING.    *
*                                                                     *
*            WORD 4 - POINTS TO THE BEGINNING OF THE OUTPUT AREA.     *
*                     FOR AN "IN PLACE" COMPRESSION, SPECIFY THE      *
*                     ADDRESS OF THE INPUT STRING FOR BOTH THE        *
*                     INPUT AND OUTPUT AREAS.                         *
*                                                                     *
*            CSECT ATTRIBUTES - RENT, REFR                            *
*                                                                     *
***********************************************************************
         EJECT
CB3270   CSECT
         SPACE
         SAVE  (14,12),,CB3270_HMD_ASSEMBLED_&SYSDATE
         LR    R12,R15                 COPY ENTRY ADDRESS
         USING CB3270,R12              DEFINE BASE REGISTER
         LR    R4,R1                   SAVE PARM LIST ADDRESS
         L     R3,0(R4)                LOAD ADDRESS OF LENGTH
         L     R11,0(R3)               LOAD LENGTH
         L     R9,8(R4)                LOAD INPUT ADDRESS
         LA    R11,0(R11,R9)           GET END OF INPUT ADDRESS
         BCTR  R11,0
         L     R8,12(R4)               LOAD OUTPUT AREA ADDRESS
         LR    R5,R8                   SAVE ADDRESS
         L     R2,4(R4)
         L     R2,0(R2)
*        MH    R2,=H'80'   DO NOT DO - SCREEN SIZE - NOT LINE COUNT
         LA    R6,0                    INITIALIZE SCREEN ADDRESS
*
TESTORDR CLI   0(R9),IC                TEST FOR INSERT CURSOR
         BE    COPY1
         CLI   0(R9),PT                TEST FOR PROGRAM TAB
         BE    COPY1
         CLI   0(R9),SF                TEST FOR START FIELD
         BE    COPY2
         CLI   0(R9),SA                TEST FOR SET ATTRIBUTE
         BE    COPY3X
         CLI   0(R9),SFE               TEST FOR START FIELD EXTENDED
         BE    COPYX
         CLI   0(R9),SBA               TEST FOR SET BUFFER ADDRESS
         BE    COPY3
         CLI   0(R9),EUA               TEST FOR ERASE UNPROTECTED
         BE    COPY3
         CLI   0(R9),RA                TEST FOR REPEAT TO ADDRESS
         BE    COPY4
*
         LA    R10,1                   SET INCREMENT TO 1
         LR    R7,R9                   SAVE ADDRESS OF FIRST CHAR
TESTNEXT LA    R6,1(R6)                INCREMENT SCREEN ADDRESS
         BXH   R9,R10,ENDBUF           INCREMENT TO NEXT CHAR
         CLC   0(1,R7),0(R9)           SEE IF SAME CHARACTER
         BE    TESTNEXT                IF SAME, LOOP
*
ENDBUF   LR    R1,R9                   ADDRESS OF CURRENT CHAR
         SR    R1,R7                   NUMBER OF SAME CHARACTERS
         C     R1,=F'4'                AT LEAST 4 FOR RA
         BH    BUILDRA
         BCTR  R1,0                    SUBTRACT 1 FOR EXECUTE
         EX    R1,COPY                 COPY TO OUTPUT AREA
         LA    R8,1(R1,R8)             INCREMENT OUTPUT ADDRESS
         CR    R9,R11                  SEE IF ANY MORE
         BH    OUT
         B     TESTORDR
*
BUILDRA  MVC   3(1,R8),0(R7)           COPY CHARACTER
         MVI   0(R8),RA                MOVE IN RA ORDER CODE
         CR    R6,R2
         BL    GETADDR
         SR    R6,R2
GETADDR  STCM  R6,3,1(R8)
         TM    1(R8),X'F0'             LOCATION OVER 4095?
         BNZ   GOTADDR                 YES, USE 14-BIT ADDRESS
         SR    R14,R14                 CONVERT SCREEN ADDRESS TO
         LR    R15,R6                       3270 ADDRESS CODE
         SLDL  R14,26
         SRL   R15,26
         IC    R14,TABLE(R14)
         IC    R15,TABLE(R15)
         STC   R14,1(R8)
         STC   R15,2(R8)
GOTADDR  LA    R8,4(R8)                INCREMENT OUTPUT AREA ADDRESS
         CR    R9,R11                  SEE IF ANY MORE
         BH    OUT
         B     TESTORDR
*
COPY1    LA    R10,1                   SET COUNT
         B     COPYDATA
COPYX    SLR   R10,R10                 CLEAR FOR INSERT
         IC    R10,1(R9)               GET ATTRIBUTE PAIR COUNT
         LA    R10,1(R10)              COUNT SFE,COUNT AS A PAIR
         SLA   R10,1                   GET THE BYTE COUNT
         B     COPYATTR
COPY2    LA    R10,2                   SET COUNT
COPYATTR LA    R6,1(R6)                INCREMENT SCREEN ADDR FOR ATTR
         B     COPYDATA
COPY3X   LA    R10,3                   SET COUNT
         B     COPYDATA
COPY3    LA    R10,3                   SET COUNT
         B     UPDATADR
COPY4    LA    R10,4                   SET COUNT
UPDATADR IC    R15,1(R9)               CONVERT ADDRESS TO SCREEN POS.
         SLL   R15,8
         IC    R15,2(R9)
         SLDL  R14,24
         SLL   R15,2
         SRDL  R14,6
         SRL   R15,20
         CR    R6,R15                  SEE IF ALREADY AT THIS ADDRESS
         BNE   RESETADR
         CLI   0(R9),SBA               IF ORDER IS SBA, SKIP IT
         BE    INCRPTR
RESETADR LR    R6,R15                  UPDATE SCREEN ADDRESS
*
COPYDATA BCTR  R10,0                   DECREMENT COUNT FOR EXECUTE
         LR    R7,R9                   COPY SOURCE ADDRESS
         EX    R10,COPY                COPY TO OUTPUT AREA
         LA    R10,1(R10)              RESTORE COUNT
         LA    R8,0(R10,R8)            UPDATE OUTPUT AREA ADDRESS
INCRPTR  BXLE  R9,R10,TESTORDR         INCREMENT ADDRESS AND LOOP
*
OUT      SR    R8,R5                   CALCULATE OUTPUT LENGTH
         LA    R8,0(0,R8)              TURN OFF HIGH BIT
         ST    R8,0(R3)                STORE NEW LENGTH
         SR    R15,R15                 CLEAR RETURN CODE
         RETURN (14,12),RC=(15)
*
         LTORG
*
COPY     MVC   0(0,R8),0(R7)           << EXECUTED INSTRUCTION >>
*
TABLE    DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
IC       EQU   X'13'
PT       EQU   X'05'
SF       EQU   X'1D'
SA       EQU   X'28'
SFE      EQU   X'29'
SBA      EQU   X'11'
EUA      EQU   X'12'
RA       EQU   X'3C'
         END
