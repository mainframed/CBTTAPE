JLOG     QSTART 'QUEUE COMMAND - LIST THE JOBLOG MESSAGES FOR A JOB'
         GBLB  &QRNB                                              RNB04
         LCLB  &SAVB                                              GP@P6
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   DETERMINE IF JOB LOG IS AVAILABLE                                 *
*                                                                     *
***********************************************************************
         USING PDBDSECT,R2    BASE REG FOR PDDB
         USING IOTSTART,R3    BASE REG FOR IOT
         L     R3,QCIOTA      LOAD BASE REG
NEXTIOT  LR    R4,R3          BASE OF IOT
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB
         LR    R2,R3          BASE OF IOT
         A     R2,QCPDDB1     OFFSET TO FIRST PDDB IN JOT
FINDDS   CLC   PDBDSKEY,=H'2' IS THIS THE JOB LOG
         BE    FOUNDDS        YES. CONTINUE.
         LA    R2,PDBLENG(R2) NO. LOOK AT NEXT PDDB
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB
         BL    FINDDS         NO. TRY AGAIN
         B     TILT
&SAVB    SETB  (&QRNB EQ 1)                                       GP@P6
&QRNB    SETB  1                                                  GP@P6
         AIF   (&QRNB).RNB04A                                     RNB04
FOUNDDS  CLC   PDBRECCT,=F'1' IS JOB LOG EMPTY
         BE    TILT           YES, SAY SO
         AGO   .RNB04B                                            RNB04
.RNB04A  ANOP                                                     RNB04
***********************************************************************
* RNB CHANGES:                                                        *
*          (1) RNB04 - ALLOW JOBLOG FOR JOBS THAT HAVE BEGUN EXECUTING*
*                      BUT HAVEN'T FINISHED FIRST STEP YET. WILL ONLY *
*                      SHOW JOB-STARTED MESSAGE.                      *
***********************************************************************
FOUNDDS  L     R3,QCJCTA      GET THE JCT                         RNB04
         USING JCTSTART,R3    #####                               RNB04
         OC    JCTXEQON,JCTXEQON  JOB EXECUTING OR EXECUTED?      RNB04
         BZ    TILT2              /NO  - REALLY EMPTY             RNB04
*                                 /YES - OK TO LIST IT            RNB04
         DROP  R3                                                 RNB04
.RNB04B  ANOP                                                     RNB04
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' PRINT OFFSET FOR EACH RECORD
         MVC   QPDSID,=H'2'   DSID OF DATASET TO BE PRINTED
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
         AIF   (&QRNB).RNB04C                                     RNB04
TILT     QTILT '*** JOBLOG IS NOT AVAILABLE ***'
         AGO   .RNB04D                                            RNB04
.RNB04C  ANOP                                                     RNB04
TILT     QTILT '*** JOBLOG NOT FOUND ***'                         RNB04
TILT2    QTILT '*** JOBLOG NOT AVAILABLE - JOB HAS NOT EXECUTED ***' 04
.RNB04D  ANOP                                                     RNB04
&QRNB    SETB  (&SAVB EQ 1)                                       GP@P6
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
JLOG     CSECT ,                                                  UF023
JCT      EQU   0
         $BUFFER                                                  UF020
         $JQE
         $TAB
         $JCT
         $PDDB
         $IOT
         QCOMMON
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
