* ------------------------------------------------------------------- *
*                                                                     *
*              TSO MAINLINE                                           *
*                                                                     *
* ------------------------------------------------------------------- *
DITTTSOM DITTPRFX TSOMSAVE,'TSO MAINLINE PROGRAM'
         USING DITTCOMM,R11        SPECIFY BASE
         USING DYNBLOK,R3          DEFINE BASE
         LOAD  EP=ISPLINK          LOAD SPF INTERFACE
         ST    R0,AISP             SAVE SPF INTERFACE ADDRESS
         LA    R2,CMDPARM          'CMD' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,KLPARM           'KEYLEN' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DLPARM           'DATALEN' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,ADDRPARM         'DISKADDR' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DATAPARM         'DATA' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,SAMPPARM         'SAMPLE' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DCMDPARM         'CMD01-CMD05' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,TAPEPARM         'TAPE01' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DASDPARM         'DASD01' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,MSGPARM          'MSG01-MSG05' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         L     R2,AATTN            ATTENTION EXIT ADDRESS
         STAX  (R2),               SET ATTENTION EXIT                  +
               USADDR=(R11)        .. COMM AREA ADDRESS WILL BE HELPFUL
MENU0000 DS    0H
         DITTRACE ID=MENU
         LA    R1,CMD01            FIRST COMMAND DISPLAY AREA
         LA    R2,50               NUMBER OF DISPLAYABLE COMMANDS
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
MENU0010 DS    0H
         CLI   FUNCELEN,X'FF'      END OF TABLE??
         BE    MENU0030            YES.. EXIT
         TM    FUNCENV,$FUNCTSO    DOES THIS COMMAND APPLY?
         BNO   MENU0020            NO
         MVC   0(L'FUNCCMD,R1),FUNCCMD
         LA    R1,6(R1)            NEXT COMMAND AREA
         BCT   R2,MENU0020         1 LESS LEFT
         B     MENU0030            ALL USED UP
MENU0020 DS    0H
         AH    R5,FUNCELEN         NEXT COMMAND
         B     MENU0010            LOOP
MENU0030 DS    0H
         MVC   CMD,MAINCMD         RESTORE SAVED COMMAND
         MVC   TAPE01,COMMBLKS     INITIALIZE ALLOCATED UNITS AREA
         MVC   TAPE01(MAINM01L),MAINM01 MOVE IN 'NONE' MESSAGE
         ICM   R3,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    MENU0090            NO DEVICES ALLOCATED
         LA    R4,TAPE01           FIRST UNIT IN DISPLAY AREA
         LA    R5,6                NUMBER OF UNITS DISPLAYABLE
         USING TAPEDSCT,R4         DEFINE BASE
MENU0040 DS    0H
         CLC   TSOTAPE,DYNDDNAM    IS THIS A TAPE UNIT?
         BNE   MENU0080            NOPE
         MVC   TAPECUU(TAPEL),COMMBLKS   INTIALIZE THIS ENTRY
         MVC   TAPECUU,DYNDDNAM+5  MOVE CUU FROM DDNAME
         L     R1,DYNDEBA          DEB ADDRESS
         USING DEB,R1              DEFINE DEB BASE
         CLI   DEBSDVM,$DEN1600    1600 BPI DENSITY?
         BE    MENU0050            YES
         CLI   DEBSDVM,$DEN6250    6250 BPI DENSITY?
         BE    MENU0060            YES
         CLI   DEBSDVM,$DEN3480    3480 DRIVE?
         BNE   MENU0070            NO
         MVC   TAPEDEN(MAINM02L),MAINM02
         B     MENU0070
MENU0050 DS    0H
         MVC   TAPEDEN(MAINM03L),MAINM03
         B     MENU0070
MENU0060 DS    0H
         MVC   TAPEDEN(MAINM04L),MAINM04
MENU0070 DS    0H
         LA    R4,TAPEL(R4)        NEXT DISPLAY AREA
         BCT   R5,MENU0080         MINUS 1 DISPLAY AREA
         B     MENU0090            ALL DISPLAY AREA USED
MENU0080 DS    0H
         ICM   R3,15,DYNNEXT       NEXT DYNAMIC BLOCK
         BNZ   MENU0040            CONTINUE
MENU0090 DS    0H
         MVC   DASD01,COMMBLKS     INITIALIZE ALLOCATED UNITS AREA
         MVC   DASD01(MAINM01L),MAINM01 MOVE IN 'NONE' MESSAGE
         ICM   R3,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    MENU0120            NO DEVICES ALLOCATED
         LA    R4,DASD01           FIRST UNIT IN DISPLAY AREA
         LA    R5,4                NUMBER OF UNITS DISPLAYABLE
         USING DASDDSCT,R4         DEFINE BASE
MENU0100 DS    0H
         CLC   TSOTAPE,DYNDDNAM    IS THIS A TAPE UNIT?
         BE    MENU0110            YES
         MVC   DASDCUU(DASDL),COMMBLKS   INTIALIZE THIS ENTRY
         MVC   DASDCUU,DYNDDNAM+5  COPY DASD CUU
         MVC   DASDVOL,DYNVOL      MOVE VOLSER
         LA    R4,DASDL(R4)        NEXT DISPLAY AREA
         BCT   R5,MENU0110         MINUS 1 DISPLAY AREA
         B     MENU0120            ALL DISPLAY AREA USED
MENU0110 DS    0H
         ICM   R3,15,DYNNEXT       NEXT DYNAMIC BLOCK
         BNZ   MENU0100            CONTINUE
MENU0120 DS    0H
         LA    R1,DISPPARM         DISPLAY PARAMETERS
         L     R15,AISP            SPF ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         TM    COMMATTN,$ATTN      ATTENTION DRIVEN DURING MENU?
         BO    ATTN0000            YES
         CH    R15,H8              END/RETURN REQUESTED?
         BE    MAIN9900            YES.. GO TO THE HOUSE
         CLC   ABENDCMD,CMD        ABEND?
         BE    ABEND000            YES
         MVC   SAMPLE,COMMBLKS     CLEAR MESSAGE
         MVC   MSG01,COMMBLKS      CLEAR MESSAGE
         MVC   MSG02,COMMBLKS      CLEAR MESSAGE
         MVC   MSG03,COMMBLKS      CLEAR MESSAGE
         MVC   MSG04,COMMBLKS      CLEAR MESSAGE
         MVC   MSG05,COMMBLKS      CLEAR MESSAGE
MAIN0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE
         MVC   MAINCMD,CMD         SAVE USER'S COMMAND
         CLC   TRACECMD,CMD        EXTERNAL TRACE?
         BE    XTRACE00            YES
         MVI   COMMPFLG,X'00'      CLEAR ANY OPTIONAL INDICATORS
         NI    COMMFLAG,255-$ABORT CLEAR ABORT FLAG IF SET
         NI    COMMFLAG,255-$COMMCER AND COMMAND ERROR FLAG IF SET
         LA    R3,CMD              COMMAND INPUT AREA
         LA    R4,L'CMD            MAX LENGTH OF REPLY
         LA    R1,CMD+(L'CMD-1)    LAST BYTE OF COMMAND DATA
TSO0000  DS    0H
         CLI   0(R1),C' '          BLANK?
         BH    TSO0010             NO
         BCTR  R1,0                MINUS 1
         BCT   R4,TSO0000          LOOP
         B     TSO0020             NO DATA AT ALL
TSO0010  DS    0H
         BAL   R14,PARSE000        FIND COMMAND ON INPUT RECORD
         OC    TSOWLEN,TSOWLEN     COMMAND PRESENT?
         BNZ   TSO0030             YES
TSO0020  DS    0H
         MVC   MSG01(TSOM001L),TSOM001
         B     MENU0030            REBUILD MENU
TSO0030  DS    0H
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=TSOWORK       .. CAPTURE THE COMMAND
TSO0040  DS    0H
         DITTRACE ID=SCANCMD       TRACE COMMAND TABLE SCAN
         CLI   FUNCELEN,X'FF'      END OF TABLE??
         BE    TSO0060             YES, INVALID COMMAND
         TM    FUNCENV,$FUNCTSO    DOES THIS COMMAND APPLY?
         BNO   TSO0050             NO
         LH    R1,TSOWLEN          GET LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH??
         BNE   TSO0050             NO
         BCTR  R1,0                AJUST FOR EXECUTE
         EX    R1,TSOFCLC          CHECK FUNCTION CODE
         BE    TSO0070             FOUND IT
TSO0050  DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     TSO0040             LOOP
TSO0060  DS    0H
         MVC   MSG01(TSOM002L),TSOM002
         B     MENU0030            REBUILD MENU
TSO0070  DS    0H
         DITTRACE ID=CMMDFND       TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED??
         BNE   TSO0080             NO
         DITTRACE ID=EXAMPLE       TRACE POINT
         MVC   SAMPLE,FUNCEX2      MOVE COMMAND EXAMPLE
         B     MENU0030            RE-BUILD MENU
TSO0080  DS    0H
         SR    R6,R6               CLEAR FOR NUMBER OF REQ PARMS
         ICM   R6,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    TSO0120             NONE... TRY FOR OPTIONAL PARMS
         LA    R7,FUNCREQ          FIRST REQUIRED PARAMETER
TSO0090  DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN REPLY??
         BZ    TSO0300             NO... REQUIRED PARMS ARE MISSING
         MVC   TSOWORK,COMMBLKS    INITIALIZE WORK AREA
         BAL   R14,PARSE000        GET PARAMETER
         OC    TSOWLEN,TSOWLEN     PARAMETER FOUND?
         BZ    TSO0300             NO
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
TSO0100  DS    0H
         MVC   PARMPARM,0(R7)      PASS FIELD ID
         MVC   PARMWORK,TSOWORK    PASS DATA
         MVC   PARMWLEN,TSOWLEN    PASS DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY PONT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE??
         BO    TSO0110             YES
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R2,0(R1)            MESSAGE LENGTH
         BCTR  R2,0                FOR EXECUTE
         EX    R2,PARMMSGE         MOVE MESSAGE
         B     MENU0030            RE-BUILD MENU
TSO0110  DS    0H
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,TSO0090          PROCESS NEXT PARAMETER
* ------------------------------------------------------------------- *
*        ALL REQUIRED PARAMETERS HAVE BEEN PROCESSED.                 *
*        NOW PROCESS ANY OPTIONAL PARAMETERS.                         *
* ------------------------------------------------------------------- *
TSO0120  DS     0H
         SR    R6,R6               CLEAR FOR NUMBER OF OPTIONAL PARMS
         ICM   R6,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    TSO0160             NONE..
         LA    R7,FUNCOPT          FIRST OPTIONAL PARAMETER
TSO0130  DS    0H
         LTR   R4,R4               ANY DATA LEFT IN REPLY??
         BZ    PROC0000            NO
         MVC   TSOWORK,COMMBLKS    INITIALIZE WORK AREA
         BAL   R14,PARSE000        GET PARAMETER
         OC    TSOWLEN,TSOWLEN     PARAMETER FOUND?
         BZ    TSO0150             NO, PARAMETER OMITTED
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
TSO0140  DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,TSOWORK    DATA
         MVC   PARMWLEN,TSOWLEN    DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE??
         BO    TSO0150             YES
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R2,0(R1)            MESSAGE LENGTH
         BCTR  R2,0                FOR EXECUTE
         EX    R2,PARMMSGE         MOVE MESSAGE
         B     MENU0030            REBUILD MENU
TSO0150  DS    0H
         LA    R7,1(R7)            NEXT OPTIONAL PARAMETER
         BCT   R6,TSO0130          PROCESS NEXT PARAMETER
TSO0160  DS    0H
         LTR   R4,R4               ANY EXTRANEOUS DATA?
         BZ    PROC0000            NO
         MVC   MSG01(TSOM003L),TSOM003
         B     MENU0030            REBUILD MENU
         SPACE 2
* ------------------------------------------------------------------- *
*        OPERATOR COMMAND EXHAUSTED.  WE STILL NEED SOME REQUIRED     *
*        PARAMETERS.  BUILD MESSAGES FOR UP TO 5 MISSING PARMS.       *
* ------------------------------------------------------------------- *
TSO0300  DS    0H
         DITTRACE ID=MISSING       ENTRY INTO MISSING PARM PROMPTS
         LA    R2,5                NUMBER OF MESSAGE AREAS
         LA    R3,MSG01            FIRST MESSAGE AREA
         MVI   PARMCMMD,$PARMMIS   RETURN MESSAGE FOR MISSING PARMS
TSO0310  DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R14,0(R1)           MESSAGE LENGTH
         BCTR  R14,0               FOR EXECUTE
         EX    R14,PARMMSGM        MOVE MESSAGE
         LA    R3,L'MSG01(R3)      NEXT MESSAGE AREA
         BCT   R2,TSO0320          ACCOUNT FOR MESSAGE AREA USED
         B     MENU0030            ALL AREAS USED
TSO0320  DS    0H
         LTR   R4,R4               WAS ANY COMMAND DATA LEFT?
         BNZ   MENU0030            YES.. ONLY ISSUE MESSAGE FOR 1 PARM
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,TSO0310          PROCESS NEXT PARAMETER
         B     MENU0030            ALL AREAS USED
* ------------------------------------------------------------------- *
*        ALL PARAMETERS PROCESSED.  PASS INPUT AND OUTPUT MODULE      *
*        ADDRESSES AND FLAGS.                                         *
* ------------------------------------------------------------------- *
PROC0000 DS    0H
         MVC   COMMPANL,FUNCPANL   PANEL NAME FOR DISPLAY
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN2        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    PROC0010            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
PROC0010 DS    0H
         ICM   R1,3,FUNCOUT2       TSO OUTPUT MODULE ADDRESS DISP
         BZ    PROC0030            NO OUTPUT MODULE
PROC0020 DS    0H
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
PROC0030 DS    0H
         MVC   CMD,COMMBLKS        CLEAR COMMAND AREA
         CLC   COMMINM,AALOC       IS INPUT MODULE 'ALOC'?
         BE    PROC0090            YES
         OC    COMMINM,COMMINM     IS THERE AN INPUT MODULE?
         BZ    PROC0060            NO
         OC    COMMINU,COMMINU     IS THERE AN INPUT UNIT?
         BZ    PROC0060            NO
         DITTRACE ID=ALOCIN        ALLOCATE INPUT
         MVI   DAIRCMMD,$DAIRLOC   DAIR COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNAMIC BLOCK BY CUU
         MVC   DAIRCUU,COMMINU     SET CUU
         LA    R1,DAIRBLOK         DAIR PARAMETERS
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR INTERFACE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    PROC0050            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    PROC0040            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
PROC0040 DS    0H
         MVC   MSG01(MAINM06L),MAINM06
         B     MENU0030            REBUILD MENU
PROC0050 DS    0H
         L     R1,COMMINM          INPUT MODULE ADDRESS
         USING MODPRFX,R1          DEFINE BASE
         ICM   R3,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM   UNIT CONSISTENT?
         BNE   PROC0200            NO
         ST    R3,COMMIND          SAVE DYNAMIC BLOCK ADDRESS
PROC0060 DS    0H
         OC    COMMOUTM,COMMOUTM   IS THERE AN OUTPUT MODULE?
         BZ    PROC0090            NO
         OC    COMMOUTU,COMMOUTU   IS THERE AN OUTPUT DEVICE?
         BZ    PROC0090            NO
         DITTRACE ID=ALOCOUT       ALLOCATE INPUT
         MVI   DAIRCMMD,$DAIRLOC   DAIR COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNAMIC BLOCK BY CUU
         MVC   DAIRCUU,COMMOUTU    SET CUU
         LA    R1,DAIRBLOK         DAIR PARAMETERS
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR INTERFACE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    PROC0080            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    PROC0070            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
PROC0070 DS    0H
         DITTRACE ID=OUTNTACQ
         MVC   MSG01(MAINM07L),MAINM07
         B     MENU0030            REBUILD MENU
PROC0080 DS    0H
         L     R1,COMMOUTM         OUTPUT MODULE ADDRESS
         ICM   R3,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM   UNIT CONSISTENT?
         BNE   PROC0210            NO
         ST    R3,COMMOUTD         SAVE DYNAMIC BLOCK ADDRESS
PROC0090 DS    0H
         DITTRACE ID=FIRST,        TRACE ENTRY INTO I/O LOOP           +
               DATA1=COMMIND,                                          +
               DATA2=COMMOUTD
         OI    COMMFLAG,$COMM1ST   SIGNAL FIRST TIME
PROC0100 DS    0H
         TM    COMMATTN,$ATTN      ATTENTION EXIT DRIVEN?
         BO    ATTN0010            YES
         DITTRACE ID=CALLIN        TRACE
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    PROC0110            ZERO .. DON'T ATTEMPT TO INVOKE IT
         SR    R1,R1               NO INPUT PARAMETER FROM MAIN
         BALR  R14,R15             CALL INPUT
         TM    COMMFLAG,$ABEND     ABEND SET BY INPUT??
         BO    PROC0180            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY INPUT??
         BO    PROC0130            YES
         CLC   RTNCMD,CMD          'RETURN' COMMAND?
         BE    PROC0130            YES
         CLC   EXITCMD,CMD         'EXIT' COMMAND?
         BE    PROC0130            YES
* ------------------------------------------------------------------- *
*                                                                     *
*        NOTE THAT THE EOF FLAG MAY BE SET BY AN OUTPUT ROUTINE.      *
*        THIS IS THE CASE WHEN THE FUNCTION REQUIRES AN OUTPUT        *
*        ROUTINE ONLY SUCH AS 'REW' OR 'RUN'.                         *
*                                                                     *
* ------------------------------------------------------------------- *
PROC0110 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ON INPUT??
         BO    PROC0130            YES...
         TM    COMMATTN,$ATTN      ATTENTION EXIT DRIVEN?
         BO    ATTN0010            YES
         DITTRACE ID=CALLOUT       TRACE
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    PROC0120            ZERO .. DON'T ATTEMPT TO INVOKE IT
         SR    R1,R1               NO INPUT PARAMETER FROM MAIN
         BALR  R14,R15             CALL OUTPUT
         TM    COMMFLAG,$ABEND     ABEND SET BY OUTPUT??
         BO    PROC0190            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY OUTPUT??
         BO    PROC0130            YES
         TM    COMMFLAG,$COMMEOF   END OF FILE ON OUTPUT??
         BO    PROC0130            YES...
         CLC   RTNCMD,CMD          'RETURN' COMMAND?
         BE    PROC0130            YES
         CLC   EXITCMD,CMD         'EXIT' COMMAND?
         BE    PROC0130            YES
PROC0120 DS    0H
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         B     PROC0100            LOOP 'TILL END OF FILE
PROC0130 DS    0H
         DITTRACE ID=CLOSE         'CLOSE' IN PROGRESS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         OI    COMMFLAG,$COMMCLS   INDICATE CLOSE FILES
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    PROC0140            NO INPUT MODULE
         BALR  R14,R15             ALLOW INPUT TO DO ANY CLEAN-UP
PROC0140 DS    0H
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    PROC0150            NO OUTPUT MODULE
         BALR  R14,R15             ALLOW OUTPUT TO DO ANY CLEAN-UP
PROC0150 DS    0H
         TM    COMMFLAG,$ABORT     COMMAND ABORTED?
         BNO   PROC0160            NO
         MVC   MSG05(MAINM05L),MAINM05
         B     PROC0170            RE-INITIALIZE
PROC0160 DS    0H
         MVC   MSG05(MAINM09L),MAINM09
         ICM   R3,15,COMMIND       INPUT DYNAMIC BLOCK ADDRESS
         BZ    PROC0170            NO DYNAMIC BLOCK
         CLC   TSOTAPE,DYNDDNAM    INPUT FROM TAPE?
         BE    PROCX160            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY DASD EOF RECORD?
         BNO   PROC0170            NO
         MVC   MSG01(MAINM14L),MAINM14
         B     PROC0170            RE-INITIALIZE
PROCX160 DS    0H
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    PROCX165            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY TAPE MARK?
         BNO   PROC0170            NO, RE-INITIALIZE
         MVC   MSG01(MAINM15L),MAINM15
         B     PROC0170            RE-INITIALIZE
PROCX165 DS    0H
         MVC   MSG01(MAINM16L),MAINM16
         B     PROC0170            RE-INITIALIZE
PROC0170 DS    0H
         DITTRACE ID=REINIT
         NI    COMMFLAG,255-$COMMCLS TURN OFF CLOSE FLAG
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         NI    COMMFLAG,255-$COMMFE  TURN OFF FORCED EOF FLAG
         XC    COMMINM,COMMINM     CLEAR MODULE ADDRESS
         XC    COMMINU,COMMINU     CLEAR INPUT UNIT ADDRESS
         XC    COMMOUTM,COMMOUTM   CLEAR MODULE ADDRESS
         XC    COMMOUTU,COMMOUTU   CLEAR OUTPUT UNIT ADDRESS
         B     MENU0030            RE-BUILD MENU
PROC0180 DS    0H
         DITTRACE ID=ABENDIN       INPUT REQUESTED ABEND
         ABEND ABEND002,DUMP,,USER ABEND WITH REASON
PROC0190 DS    0H
         DITTRACE ID=ABENDOUT      OUTPUT REQUESTED ABEND
         ABEND ABEND003,DUMP,,USER ABEND WITH REASON
PROC0200 DS    0H
         MVC   MSG01(MAINM12L),MAINM12
         B     PROC0170            EXIT THIS COMMAND
PROC0210 DS    0H
         MVC   MSG01(MAINM13L),MAINM13
         B     PROC0170            EXIT THIS COMMAND
* ------------------------------------------------------------------- *
*                                                                     *
*           EXTERNAL TRACE CONTROL                                    *
*                                                                     *
* ------------------------------------------------------------------- *
XTRACE00 DS    0H
         DITTRACE ID=XTRACE
         CLC   ONCMD,CMD+6         TURN TRACE ON?
         BE    XTRACE10            YES
         CLC   OFFCMD,CMD+6        TURN TRACE OFF?
         BE    XTRACE30            YES
         MVC   MSG01(MAINM17L),MAINM17
         B     MENU0030            BUILD MENU
XTRACE10 DS    0H
         DITTRACE ID=TRACEON
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    XTRACE20            YES
         OI    XTRFLAG,$XTRACE     SET EXTERNAL TRACE FLAG
         MVC   MSG01(MAINM08L),MAINM08
         B     MENU0030            AND BUILD MENU
XTRACE20 DS    0H
         DITTRACE ID=DUPON
         MVC   MSG01(MAINM18L),MAINM18
         B     MENU0030            BUILD MENU
XTRACE30 DS    0H
         DITTRACE ID=TRACEOFF
         TM    XTRFLAG,$XTRACE     TRACE ON?
         BNO   XTRACE40            NO
         MVC   MSG01(MAINM19L),MAINM19
         NI    XTRFLAG,255-$XTRACE RESET FLAG
         B     MENU0030            AND BUILD MENU
XTRACE40 DS    0H
         DITTRACE ID=DUPOFF
         MVC   MSG01(MAINM20L),MAINM20
         B     MENU0030            BUILD MENU
         SPACE 2
ABEND000 DS    0H
         DITTRACE ID=ABEND
         ABEND ABEND999,DUMP,,USER ABEND AT OPERATOR'S REQUEST
VDEF0000 DS    0H
         USING VDEFDSCT,R2         DEFINE BASE
         DITTRACE ID=VDEF,         TRACE CALLS FOR VARIABLE DEFINITIONS+
               DATA1=VDEFNAME      .. CAPTURE VARIABLE BEING DEFINED
         ST    R2,DEFLEN           SET VARIABLE LENGTH ADDRESS
         OI    DEFLEN,X'80'        SET END OF LIST
         LA    R3,VDEFNAME         1ST NAME/DISPLACEMENT
         USING VDEFNAME,R3         DEFINE BASE
VDEF0010 DS    0H
         CLI   VDEFNAME,X'FF'      END OF LIST?
         BER   R10                 YES.. ALL DONE
         ST    R3,DEFNAME          SET VARIABLE NAME ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,VDEFDISP       DISPLACEMENT TO VARIABLE IN 'COMM'
         AR    R1,R11              VARIABLE'S ADDRESS
         ST    R1,DEFADDR          SET ADDRESS
         LA    R1,DEFPARM          VDEF PARM LIST
         L     R15,AISP            SPF ENTRY POINT
         BALR  R14,R15             LINK TO SPF TO DEFINE VARIABLE
         LTR   R4,R15              SPF RETURN CODE ZERO?
         BNZ   VDEF0020            NO
         LA    R3,VDEFNDL(R3)      NEXT NAME/DISPLACEMENT
         B     VDEF0010            LOOP
VDEF0020 DS    0H
         DITTRACE ID=VDEFFAIL,     TRACE DEFINITION FAILURES           +
               RDATA1=R4
         ABEND ABEND007,DUMP,,USER
* ------------------------------------------------------------------- *
*                                                                     *
*              INPUT PARSER                                           *
*         R1, R2, R15 ARE WORK REGISTERS                              *
*         R3 HAS CURRENT INPUT ADDRESS                                *
*         R4 LENGTH REMAINING ON INPUT                                *
*         R14 IS RETURN ADDRESS                                       *
*                                                                     *
*         ON EXIT 'TSOWORK' CONTAINS DATA FOUND TO NEXT DELIMITER,    *
*         UP TO 44 BYTES.  'TSOWLEN' CONTAINS ITS LENGTH.             *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL           *
*         REFLECT LENGTH LEFT FOLLOWING THE DELIMITER.                *
*                                                                     *
* ------------------------------------------------------------------- *
PARSE000 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD??
         BZR   R14                 NO.. EXIT
         SR    R2,R2               CLEAR LENGTH COUNTER
         XC    TSOWORK,TSOWORK     CLEAR WORK AREA
PARSE010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BH    PARSE020            NO
         LA    R3,1(R3)            NEXT
         BCT   R4,PARSE010         LOOP
         B     PARSE050            NO DATA
PARSE020 DS    0H
         LA    R1,TSOWORK          POINT TO WORK AREA
         LA    R15,44              MAXIMUM LENGTH
PARSE030 DS    0H
         CLI   0(R3),C' '          BLANK??
         BE    PARSE050            YES
         CLI   0(R3),C','          COMMA??
         BE    PARSE050            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,PARSE040         DECREMENT COUNT
         STH   R2,TSOWLEN          SAVE CURRENT LENGTH
         BR    R14                 LENGTH EXHAUSTED, EXIT HERE
PARSE040 DS    0H
         BCT   R15,PARSE030        LOOP
PARSE050 DS    0H
         STH   R2,TSOWLEN          SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
         BR    R14                 EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*              ATTENTION EXIT                                         *
*                                                                     *
* ------------------------------------------------------------------- *
ATTN0000 DS    0H
         DITTRACE ID=MENUSTAX      ATTENTION ENTERED WHILE IN MENU
         LA    R1,MAINM10          MAINLINE INTERRUPTED
         LA    R0,MAINM10L         TEXT LENGTH
         TPUT  (R1),(R0)           ISSUE MESSAGE
         B     MAIN9910            AND EXIT
ATTN0010 DS    0H
         DITTRACE ID=FUNCSTAX
         LA    R1,MAINM11          FUNCTION INTERRUPTED
         LA    R0,MAINM11L         TEXT LENGTH
         TPUT  (R1),(R0)           ISSUE MESSAGE
         NI    COMMATTN,255-$ATTN  RESET ATTENTION FLAG
         L     R2,AATTN            ATTENTION EXIT ADDRESS
         STAX  (R2),               RE-ESTABLISH ATTENTION EXIT         +
               USADDR=(R11)        .. COMM AREA ADDRESS WILL BE HELPFUL
         B     PROC0130            INTERRUPT CURRENT I/O LOOP
MAIN9900 DS    0H
         STAX  ,                   CANCEL ANY STAX EXIT
MAIN9910 DS    0H
         DITTRACE ID=EXIT          TRACE
         L     R2,COMMDYHD         FIRST BLOCK ON CHAIN
MAIN9920 DS    0H
         LTR   R3,R2               ADDRESS PRESENT??
         USING DYNBLOK,R3          DEFINE BASE
         BZ    MAIN9930            ALL HAVE BEEN RELEASED
         DITTRACE ID=RELDYN,       TRACE RELEASE                       +
               RDATA1=R2           .. SAVE DYNAMIC BLOCK ADDRESS
         L     R2,DYNNEXT          NEXT DYNAMIC BLOCK ADDRESS
         MVI   DAIRCMMD,$DAIRREL   SET COMMAND (RELEASE)
         MVI   DAIRTYPE,$DAIRCUU   SET TYPE OF LOCATE (BY CUU)
         MVC   DAIRCUU,DYNCUU      SET CUU
         LA    R1,DAIRBLOK         PARAMETER BLOCK ADDRESS
         L     R15,ADAIR           'DITTDAIR' ENTRY POINT
         BALR  R14,R15             RELEASE THIS RESOURCE
         CLI   DAIRSTAT,$DAIRSOK   SUCCESSFULLY RELEASED??
         BE    MAIN9920            YES
         ABEND ABEND008,DUMP,,USER ABEND
MAIN9930 DS    0H
         DITTRACE ID=RELDONE       DYNAMIC RELEASES ARE COMPLETE
         TM    XTRFLAG,$XTROPEN    EXTERNAL TRACE FILE OPEN??
         BNO   MAIN9940            NO
         LA    R2,XTRDCB           EXTERNAL TRACE DCB
         CLOSE ((R2))              CLOSE EXTERNAL TRACE
MAIN9940 DS    0H
         DELETE EP=ISPLINK         RELEASE SPF INTERFACE PROGRAM
         L     R13,4(R13)          RESTORE REGISTER 13                  ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS          ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE                ASE01690
         BR    R14                 GET OUT OF DODGE                     ASE01700
* ------------------------------------------------------------------- *
*                                                                     *
*               EXECUTED INSTRUCTIONS                                 *
*                                                                     *
* ------------------------------------------------------------------- *
TSOFCLC  CLC   FUNCCMD(0),TSOWORK   CORRECT COMMAND??
PARMMSGE MVC   MSG01(0),2(R1)       MOVE ERROR MESSAGE
PARMMSGM MVC   0(0,R3),2(R1)        MOVE 'MISSING' MESSAGE
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              PARAMETER CONVERTER INTERFACE                          *
*                                                                     *
* ------------------------------------------------------------------- *
PARMBLOK PARMBLOK
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC ALLOCATION INTERFACE                           *
*                                                                     *
* ------------------------------------------------------------------- *
DAIRBLOK DAIRBLOK TYPE=CSECT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              CONSTANTS/WORK AREAS                                   *
*                                                                     *
* ------------------------------------------------------------------- *
TSOWORK  DC    CL44' '             PARSER OUTPUT WORK AREA
TSOWLEN  DC    H'0'                CURRENT PARSER OUTPUT LENGTH
DEFPARM  DC    A(VARDEF)         'VDEFINE' COMMAND
DEFNAME  DC    A(0)              VARIABLE NAME
DEFADDR  DC    A(0)              VARIABLE'S DATA ADDRESS
DEFFORM  DC    A(CHARFORM)       VARIABLE FORMAT
DEFLEN   DC    A(0)              VARIABLE LENGTH
DISPPARM DC    A(DISPLAY)        'DISPLAY' COMMAND
         DC    A(PANLNAME+X'80000000')
VARDEF   DC    CL8'VDEFINE'      SPF 'VDEFINE' COMMAND
DISPLAY  DC    CL8'DISPLAY'      SPF 'DISPLAY' COMMAND
CHARFORM DC    CL4'CHAR'
PANLNAME DC    CL8'DITTMENU'     PANEL NAME
TSOTAPE  DC    CL4'TAPE'
TRACECMD DC    C'TRACE '
ONCMD    DC    C'ON '
OFFCMD   DC    C'OFF '
ABENDCMD DC    C'ABEND '
RTNCMD   DC    C'RETURN '
EXITCMD  DC    C'EXIT '
MAINCMD  DC    CL67' '
         SPACE 1
CMDPARM  VDEF  LENGTH=67,                                              +
               FIELDS=(CMD,CMD)
KLPARM   VDEF  LENGTH=06,                                              +
               FIELDS=(KEYLEN,KEYLEN)
DLPARM   VDEF  LENGTH=06,                                              +
               FIELDS=(DATALEN,DATALEN)
ADDRPARM VDEF  LENGTH=11,                                              +
               FIELDS=(DISKADDR,DISKADDR)
DATAPARM VDEF  LENGTH=79,                                              +
               FIELDS=(DATA,DATA)
SAMPPARM VDEF  LENGTH=67,                                              +
               FIELDS=(SAMPLE,SAMPLE)
DCMDPARM VDEF  LENGTH=60,                                              +
               FIELDS=(CMD01,CMD01,                                    +
               CMD02,CMD02,                                            +
               CMD03,CMD03,                                            +
               CMD04,CMD04,                                            +
               CMD05,CMD05)
TAPEPARM VDEF  LENGTH=60,                                              +
               FIELDS=(TAPE01,TAPE01)
DASDPARM VDEF  LENGTH=60,                                              +
               FIELDS=(DASD01,DASD01)
MSGPARM  VDEF  LENGTH=79,                                              +
               FIELDS=(MSG01,MSG01,                                    +
               MSG02,MSG02,                                            +
               MSG03,MSG03,                                            +
               MSG04,MSG04,                                            +
               MSG05,MSG05)
MAINHX0F DC    8X'0F'
MAINHXCH DC    C'0123456789ABCDEF'
TSOM001  DC    C'ENTER A DITTO COMMAND'
TSOM001L EQU   *-TSOM001
TSOM002  DC    C'INVALID DITTO COMMAND'
TSOM002L EQU   *-TSOM002
TSOM003  DC    C'EXTRANEOUS DATA IN COMMAND, RE-ENTER'
TSOM003L EQU   *-TSOM003
MAINM01  DC    C'NONE'
MAINM01L EQU   *-MAINM01
MAINM02  DC    C'3480'
MAINM02L EQU   *-MAINM02
MAINM03  DC    C'1600'
MAINM03L EQU   *-MAINM03
MAINM04  DC    C'6250'
MAINM04L EQU   *-MAINM04
MAINM05  DC    C'COMMAND ABORTED'
MAINM05L EQU   *-MAINM05
MAINM06  DC    C'INPUT UNIT NOT ALLOCATED, COMMAND ABORTED'
MAINM06L EQU   *-MAINM06
MAINM07  DC    C'OUTPUT UNIT NOT ALLOCATED, COMMAND ABORTED'
MAINM07L EQU   *-MAINM07
MAINM08  DC    C'EXTERNAL TRACE ACTIVATED'
MAINM08L EQU   *-MAINM08
MAINM09  DC    C'LAST FUNCTION TERMINATED NORMALLY'
MAINM09L EQU   *-MAINM09
MAINM10  DC    C'DITTO TERMINATED DUE TO ATTENTION INTERRUPT'
MAINM10L EQU   *-MAINM10
MAINM11  DC    C'CURRENT FUNCTION TERMINATED DUE TO ATTENTION INTERRUPT+
               '
MAINM11L EQU   *-MAINM11
MAINM12  DC    C'INPUT DEVICE INCONSISTENT WITH COMMAND, COMMAND ABORTE+
               D'
MAINM12L EQU   *-MAINM12
MAINM13  DC    C'OUTPUT DEVICE INCONSISTENT WITH COMMAND, COMMAND ABORT+
               ED'
MAINM13L EQU   *-MAINM13
MAINM14  DC    C'EOF REACHED ON INPUT, COMMAND TERMINATED'
MAINM14L EQU   *-MAINM14
MAINM15  DC    C'TAPE MARK READ ON INPUT, COMMAND TERMINATED'
MAINM15L EQU   *-MAINM15
MAINM16  DC    C'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINA+
               TED'
MAINM16L EQU   *-MAINM16
MAINM17  DC    C'INVALID TRACE COMMAND (''TRACE ON'' OR ''TRACE OFF'')'
MAINM17L EQU   *-MAINM17
MAINM18  DC    C'TRACE ALREADY ACTIVE'
MAINM18L EQU   *-MAINM18
MAINM19  DC    C'TRACE DE-ACTIVATED'
MAINM19L EQU   *-MAINM19
MAINM20  DC    C'TRACE NOT ACTIVE'
MAINM20L EQU   *-MAINM20
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
* ------------------------------------------------------------------- *
TSOMSAVE DC    18F'0'              REGISTER SAVE AREA
H8       DC    H'8'                CONSTANT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY REGEQU
         ABCODES
* ------------------------------------------------------------------- *
*                                                                     *
*              COMMON MODULE DSECT                                    *
*                                                                     *
* ------------------------------------------------------------------- *
DITTCOMM DITTCOMM TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
*              DYNAMIC RESOURCE CONTROL BLOCK                         *
*                                                                     *
* ------------------------------------------------------------------- *
DYNBLOK  DYNBLOK TYPE=SHORT
* ------------------------------------------------------------------- *
*                                                                     *
*              FUNCTION TABLE BLOCK                                   *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY FUNCBLOK
* ------------------------------------------------------------------- *
*                                                                     *
*              VARIABLE PARAMETERS                                    *
*                                                                     *
* ------------------------------------------------------------------- *
VDEFDSCT DSECT
VDEFLEN  DS    AL4      VARIABLE LENGTH
VDEFNAME DS    CL8      VARIABLE NAME
VDEFDISP DS    AL2      DISPLACEMENT INTO 'DITTCOMM'
VDEFNDL  EQU   *-VDEFNAME   NAME+DISPLACEMENT DATA LENGTH
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              ALLOCATED TAPE DEVICE DISPLAY                          *
*                                                                     *
* ------------------------------------------------------------------- *
TAPEDSCT DSECT
TAPECUU  DS   CL3
         DS   C
TAPEDEN  DS   CL4
         DS   CL2
TAPEL    EQU  *-TAPEDSCT
* ------------------------------------------------------------------- *
*                                                                     *
*              ALLOCATED DASD DEVICE DISPLAY                          *
*                                                                     *
* ------------------------------------------------------------------- *
DASDDSCT DSECT
DASDCUU  DS   CL3
         DS   C
DASDVOL  DS   CL6
         DS   CL5
DASDL    EQU  *-DASDDSCT
         SPACE 2
* ------------------------------------------------------------------- *
*                                                                     *
*              DATA EXTENT BLOCK                                      *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZDEB LIST=NO
         END
